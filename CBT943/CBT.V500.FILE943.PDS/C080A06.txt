A command-level CA-TOP SECRET interface


THE PROBLEM

Our security manager gets a lot of telephone calls from users
who have been suspended because they have made too many
attempts to access the system using the wrong password (this
often happens when users return from their vacations). We
wanted to be able to distribute some simple functions of CA-
TOP SECRET administration, such as removing suspended
users, changing passwords, and a list function, to some people
in our organization. We did not want to use the standard CA-
TOP SECRET administration menus.


THE SOLUTION

I wrote a command-level interface to use instead. Maps are
documented with the screen layout and variable names, and are
created with SDF. The program could easily be extended to
carry out other functions as well.


TSSPADM SOURCE CODE

*** Command-level interface to TOP SECRET for the following commands:
*                     1. Remove suspend
*                     2. Change password
*                     3. List acid
*                     4. List password (last changed)
*   Transaction: TSS
*   Program    : TSSPADM
*   Mapset     : TSSMADM
*** Top Secret module: TSSCICS
         DFHREGS
TIOABAR  EQU   R5
TCTTEAR  EQU   R10
CSACBAR  EQU   R13
         COPY  DFHCSADS
         DFHTCA CICSYST=YES
         DFHTCTZE CICSYST=YES
COMMAR   DSECT
COMMFUNC DS    CL1
DFHEISTG DSECT
         COPY  TSSMADM
NFUNC    DS    CL1
PASSW    DS    CL16
USERW    DS    CL8
         COPY  DFHTIOA
TSSCMD   DS    0CL5
TSSTRAN  DS    CL3
         DS    CL1
TSSFUNC  DS    CL1
TSSPADM  DFHEIENT CODEREG=(9),EIBREG=(11),DATAREG=(8)
         EXEC  CICS HANDLE CONDITION MAPFAIL(ANYKEY)
         EXEC CICS ADDRESS CSA(CSACBAR)
         USING DFHCSABA,CSACBAR           Address CSA
         L     TCACBAR,CSACDTA            Current dispatched task
         L     TCTTEAR,TCAFCAAA           Load TCT address
         USING DFHTCTTE,TCTTEAR
         L     TIOABAR,TCTTEDA            Terminal data area address
         CLC   EIBCALEN,=H'00'            Have we been here before?
         BNE   CHKCOM                     Yes, check COMMAREA
         CLI   TSSFUNC,X'F0'              Is there a function in TIOA?
         BL    SENDMENU                   No, go send menu
         MVC   NFUNC,TSSFUNC
         B     ANYKEY
CHKCOM   L     R7,DFHEICAP                Address COMMAREA
         USING COMMAR,R7
         MVC   NFUNC,COMMFUNC
         CLI   NFUNC,C'0'
         BE    RECMENU
         CLI   NFUNC,C'1'
         BE    RECREM
         CLI   NFUNC,C'2'
         BE    RECREP
         CLI   NFUNC,C'3'
         BE    RECLIST
         CLI   NFUNC,C'4'
         BE    RECLISP
         B     SENDMENU                        <=== Should not be here
ANYKEY   CLI   NFUNC,C'0'
         BE    SENDMENU
         CLI   NFUNC,C'1'
         BE    SENDREM
         CLI   NFUNC,C'2'
         BE    SENDREP
         CLI   NFUNC,C'3'
         BE    SENDLIST
         CLI   NFUNC,C'4'
         BE    SENDLISP
         B     SENDMENU                        <=== Should not be here
RECMENU  EXEC  CICS RECEIVE MAP('TSSMENU') MAPSET('TSSMADM')
         CLI   EIBAID,X'F3'                         PF3
         BE    EXIT                                 Terminate
         CLI   EIBAID,X'7D'                         ENTER
         BNE   SENDMENU                             If not, resend menu
         CLI   TSSOPTI,C'1'                         Test function
         BE    SENDREM
         CLI   TSSOPTI,C'2'
         BE    SENDREP
         CLI   TSSOPTI,C'3'
         BE    SENDLIST
         CLI   TSSOPTI,C'4'
         BE    SENDLISP
         B     RETURNT
RECREM   EXEC  CICS RECEIVE MAP('TSSREM') MAPSET('TSSMADM') SET(R4)
         BAL   R6,AIDTEST
         CLI   EIBAID,X'7D'                         ENTER
         BNE   SENDREM                              Execute function
         CLI   17(R4),X'C1'                         If there is any
         BL    RETURNT                              User-id
         ST    R4,TCTTEDA                           Store cmd address
         MVC   USERW,17(R4)                         Move out user-id
         MVC   8(2,R4),=X'0016'                     Cmd length
         MVC   12(22,R4),=C'REM (        ) SUSPEND'
         MVC   17(8,R4),USERW                       Move in user-id
         B     LINKTSS
RECREP   EXEC  CICS RECEIVE MAP('TSSREP') MAPSET('TSSMADM') SET(R4)
         BAL   R6,AIDTEST
         CLI   EIBAID,X'7D'                         ENTER?
         BNE   SENDREP                              Execute function
         CLI   17(R4),X'C1'                         If there is any
         BL    RETURNT                              User-id or
         CLI   30(R4),X'C1'                         Password
         BL    RETURNT
         ST    R4,TCTTEDA                           Store cmd address
         MVC   PASSW,30(R4)                         Move out password
         MVC   USERW,17(R4)                         Move out user-id
         MVC   8(2,R4),=X'0025'                     Cmd length
         MVC   12(37,R4),=C'REP  (       ) PASS(                )'
         MVC   17(8,R4),USERW                       Move in user-id
         MVC   32(16,R4),PASSW                      Move in password
         B     LINKTSS
RECLIST  EXEC  CICS RECEIVE MAP('TSSLIST') MAPSET('TSSMADM') SET(R4)
         BAL   R6,AIDTEST
         CLI   EIBAID,X'7D'                         ENTER?
         BNE   SENDLIST                             Execute function
         CLI   17(R4),X'C1'                         If there is any
         BL    RETURNT                              User-id
         ST    R4,TCTTEDA                           Store cmd address
         MVC   USERW,17(R4)                         Move out user-id
         MVC   8(2,R4),=X'0018'                     Cmd length
         MVC   12(24,R4),=C'LIST (        ) DAT(ALL)'
         MVC   18(8,R4),USERW                       Move in user-id
         B     LINKTSS
RECLISP  EXEC  CICS RECEIVE MAP('TSSLISP') MAPSET('TSSMADM') SET(R4)
         BAL   R6,AIDTEST
         CLI   EIBAID,X'7D'                         ENTER?
         BNE   SENDLISP                             Execute function
         CLI   17(R4),X'C1'                         If there is any
         BL    RETURNT                              User-id
         ST    R4,TCTTEDA                           Store cmd address
         MVC   USERW,17(R4)                         Move out user-id
         MVC   8(2,R4),=X'0019'                     Cmd length
         MVC   12(24,R4),=C'LIST (         ) DAT(PASS)'
         MVC   18(8,R4),USERW                       Move in user-id
LINKTSS  EXEC  CICS LINK PROGRAM('TSSCICS')
         B     RETURNT
AIDTEST  CLI   EIBAID,X'F3'                         PF3
         BE    SENDMENU                             Return to menu
         CLI   EIBAID,X'7C'                         PF12
         BE    EXIT                                 Terminate
         BR    R6
SENDMENU MVI   NFUNC,C'0'
         EXEC  CICS SEND MAP('TSSMENU') MAPSET('TSSMADM') FREEKB ERASE
         B     RETURNT
SENDREM  MVI   NFUNC,C'1'
         EXEC  CICS SEND MAP('TSSREM') MAPSET('TSSMADM') FREEKB ERASE
         B     RETURNT
SENDREP  MVI   NFUNC,C'2'
         EXEC  CICS SEND MAP('TSSREP') MAPSET('TSSMADM') FREEKB ERASE
         B     RETURNT
SENDLIST MVI   NFUNC,C'3'
         EXEC  CICS SEND MAP('TSSLIST') MAPSET('TSSMADM') FREEKB ERASE
         B     RETURNT
SENDLISP MVI   NFUNC,C'4'
         EXEC  CICS SEND MAP('TSSLISP') MAPSET('TSSMADM') FREEKB ERASE
RETURNT  EQU   *
         EXEC  CICS RETURN TRANSID('TSS') COMMAREA(NFUNC) LENGTH(1)
EXIT     EXEC  CICS SEND TEXT FROM(DUMMY) LENGTH (1) ERASE WAIT
         EXEC  CICS RETURN
DUMMY    DC    C' '
         END


SCREEN LAYOUT FOR TSSMENU

SDF/CICS OS/VS R5.0  DVD6BTCH - BATCH UTILITY CONTROLLER
================================================================================
FIELD NAMES FOR MAP TSSMADM.TSSMENU
=====================================
             1         2         3         4         5         6         7
    12345678901234567890123456789012345678901234567890123456789012345678
   +----------------------------------------------------------------------------
01 !                           TOP SECRET Menu
02 !                          -----------------
03 !
04 !
05 !                         1 -  Remove Suspend
06 !                         2 -  Change Password
07 !                         3 -  List Userid
08 !                         4 -  List Password (Last changed)
09 !
10 !
11 !
12 !                        Select ===> TSSOPT
13 !
14 !
15 !
16 !                           PF3 => Exit
17 !
18 !
19 !
20 !
21 !
22 !
23 !
24 !
   +----------------------------------------------------------------------------
    12345678901234567890123456789012345678901234567890123456789012345678
             1         2         3         4         5         6         7


SCREEN LAYOUT FOR TSSREM

SDF/CICS OS/VS R5.0  DVD6BTCH - BATCH UTILITY CONTROLLER
================================================================================
FIELD NAMES FOR MAP TSSMADM.TSSREM
=====================================
             1         2         3         4         5         6         7
    12345678901234567890123456789012345678901234567890123456789012345678
   +----------------------------------------------------------------------------
01 !                           TOP SECRET Remove Suspend
02 !                          ---------------------------
03 !
04 !
05 !
06 !                          Userid ===> TSSRUSR
07 !
08 !
09 !
10 !
11 !
12 !
13 !                       PF3 => Return      PF12 => Exit
14 !
15 !
16 !
17 !
18 !
19 !
20 !
21 !
22 !
23 !
24 !
   +----------------------------------------------------------------------------
    12345678901234567890123456789012345678901234567890123456789012345678
             1         2         3         4         5         6         7


SCREEN LAYOUT FOR TSSREP

SDF/CICS OS/VS R5.0  DVD6BTCH - BATCH UTILITY CONTROLLER
================================================================================
FIELD NAMES FOR MAP TSSMADM.TSSREP
=====================================
             1         2         3         4         5         6         7
    12345678901234567890123456789012345678901234567890123456789012345678
   +----------------------------------------------------------------------------
01 !                           TOP SECRET Change Password
02 !                          ----------------------------
03 !
04 !
05 !
06 !                          Userid ===> TSSRPUS
07 !                        Password ===> TSSRPPW
08 !
09 !
10 !
11 !
12 !
13 !
14 !                       PF3 => Return      PF12 => Exit
15 !
16 !
17 !
18 !
19 !
20 !
21 !
22 !
23 !
24 !
   +----------------------------------------------------------------------------
    12345678901234567890123456789012345678901234567890123456789012345678
             1         2         3         4         5         6         7


SCREEN LAYOUT FOR TSSLIST

SDF/CICS OS/VS R5.0  DVD6BTCH - BATCH UTILITY CONTROLLER
================================================================================
FIELD NAMES FOR MAP TSSMADM.TSSLIST
=====================================
             1         2         3         4         5         6         7
    12345678901234567890123456789012345678901234567890123456789012345678
   +----------------------------------------------------------------------------
01 !                           TOP SECRET List User
02 !                          ----------------------
03 !
04 !
05 !
06 !                        Userid ===> TSSLUSR
07 !
08 !
09 !
10 !
11 !
12 !
13 !                       PF3 => Return      PF12 => Exit
14 !
15 !
16 !
17 !
18 !
19 !
20 !
21 !
22 !
23 !
24 !
   +----------------------------------------------------------------------------
    12345678901234567890123456789012345678901234567890123456789012345678
             1         2         3         4         5         6         7


SCREEN LAYOUT FOR TSSLISP

SDF/CICS OS/VS R5.0  DVD6BTCH - BATCH UTILITY CONTROLLER
================================================================================
FIELD NAMES FOR MAP TSSMADM.TSSLISP
=====================================
             1         2         3         4         5         6         7
    12345678901234567890123456789012345678901234567890123456789012345678
   +----------------------------------------------------------------------------
01 !                      TOP SECRET List Password
02 !                     --------------------------
03 !
04 !
05 !
06 !                        Userid ===> TSSPUSR
07 !
08 !
09 !
10 !
11 !
12 !
13 !                       PF3 => Return      PF12 => Exit
14 !
15 !
16 !
17 !
18 !
19 !
20 !
21 !
22 !
23 !
24 !
   +----------------------------------------------------------------------------
    12345678901234567890123456789012345678901234567890123456789012345678
             1         2         3         4         5         6         7


Truls Pedersen
Systems Programmer
Vital Insurance Co (Norway)

