Dataspace access via Task Related User Exit

Using CICS/MVS 2.1 in an MVS/ESA environment you are
able to access MVS/ESA dataspaces via CICS Task Related
User Exit.  This very efficient method of improving access
from CICS to the most frequently read datasets reduces the
response time.  The dataspace should be created outside CICS,
and before CICS comes up.  It may be loaded outside of CICS
too.  An Access List Entry Token (ALET) identifies the
dataspace.  It exists as long as the owning task does not
terminate.  The loading of the dataspace as well as the reading
is done by Task Related User Exit.  Load applications,
however, must run in supervisor state.  CICS user-driven tasks,
for instance PL/I or COBOL  applications, use a CALL
interface to access the dataspace.


DSPCREAT

DSPCREAT is a non-swappable program outside CICS.  It
creates an MVS/ESA dataspace and goes into a wait.

DSPCREAT CSECT
DSPCREAT AMODE 31
DSPCREAT RMODE 24
         BAKR  R14,0                linkage conventions for
         SAC   512                  programs written
         LAE   R12,0(R15,0)         in Access Register mode
         USING DSPCREAT,R12         (AR mode)
         SAC   0                    switch to primary mode
         MODESET MODE=SUP,KEY=ZERO  set supervisor state
         SAC   512                  switch to AR mode
* create MVS/ESA dataspace
         DSPSERV CREATE,NAME=DSPCNAME,STOKEN=DSPCSTKN,FPROT=NO,KEY=8,  X
               BLOCKS=DSPBLCKS,ORIGIN=DSPCORG,SCOPE=COMMON,DREF=YES
* establish addressability to dataspace
         ALESERV ADD,STOKEN=DSPCSTKN,ALET=DSPCALET,                    X
               ACCESS=PUBLIC,AL=PASN,CHKEAX=NO
         LAM   R2,R2,DSPCALET
         L     R2,DSPCORG
         MVC   ALET,DSPCALET        save the returned ALET
         SAC   0                    switch to primary mode
         MODESET MODE=PROB,KEY=NZERO   leave supervisor state
         LINKX EP=ALETSAVE,PARAM=ALET      make ALET accessible for
*                                          other programs and regions
*                                          via link
         STIMER WAIT,DINTVL=DI      task goes into wait to keep
         B     ENDE                 dataspace alive.
         DS    0D
DI       DC    C'12000000'          CICS productivity time
ENDE     DS    0H
         SAC   512                  switch to AR mode
         SLR   R15,R15              set return code zero
         PR                         Program Return
DSPCNAME DC    CL8'A1      '        dataspace name
DSPCSTKN DS    CL8                  dataspace STOKEN
DSPCALET DS    F                    dataspace ALET returned
DSPCORG  DS    F                    dataspace origin returned
DSPCSIZE EQU   500000               500000 bytes of space(can be more!)
DSPBLCKS DC    A((DSPCSIZE+4095)/4096)  number of blocks needed for a
         DS    0F                       500000 byte dataspace
ALET     DS    CL4                  save field
         END


ALETSAVE

ALETSAVE is a non-AR program which makes ALET
accessible for several regions.

ALETSAVE CSECT
         MSAVE R11                    user-defined macro of
*                                     linkage conventions
         LR    R6,R1                  establish addressability to
         L     R7,0(R6)               parameter list for obtaining ALET
         OPEN  (ALET,OUTPUT)          save ALET into a file
         MVC   ALETREC(4),0(R7)       that will be read by
         PUT   ALET,ALETREC           CICS PLTPI program as an
         CLOSE (ALET)                 extrapartition TD queue
         L     R13,$SAVE+4            instruction related to MSAVE
         RETURN (R14,R12),RC=0          "          "    "    "
ALETREC  DS    CL80
ALET     DCB   DDNAME=ALET,DSORG=PS,MACRF=PM
         END   ALETSAVE


PLTPIPGM

PLTPIPGM is a CICS PLTPI program which enables the Task
Related User Exit and provides an ALET within CICS.

DFHEISTG DSECT
PLTPIPGM CSECT
* install Task Related User Exit and specify length of the
* Global Work Area: ALET takes four bytes
         EXEC CICS ENABLE PROGRAM('TRUEPGM') GALENGTH(4) START
* get address of Global Work Area
         EXEC CICS EXTRACT EXIT PROGRAM('TRUEPGM') GASET(R5)           X
               GALENGTH(LEN1)
* read extrapartition TD queue to obtain the ALET
         EXEC CICS READQ TD QUEUE('AL01') INTO(ALETREC) LENGTH(LEN2)
* move ALET into Global Work Area
         MVC   0(4,R5),ALETREC
ENDE     EXEC CICS RETURN
ALETREC  DS    CL80
LEN1     DC    H'4'
LEN2     DC    H'80'
         END   PLTPIPGM


TRUEAPPL

TRUEAPPL is a user-driven CICS application with a CALL
interface to an MVS/ESA dataspace.  Only a skeleton is provided.

     ID DIVISION.
     PROGRAM-ID. TRUEAPPL.
     .
     .
     PROCEDURE DIVISION.
* address the ALET that will be returned at pointer "GAPTR"
         EXEC CICS EXTRACT EXIT PROGRAM('TRUEPGM') GASET(GAPTR)        X
               GALENGTH(4) END-EXEC.
* provide a local data area receiving requested data from dataspace
         EXEC CICS GETMAIN SET(GETMPTR) LENGTH(133)                    X
               INITIMG(BLANK) END-EXEC.
* invoke Task Related User Exit and pass following parameters:
* an ALET addressing the dataspace, a search-argument for direct read
* access within the dataspace and a pointer to the getmained area
         CALL  TRUECALL USING ALET KEYWORD GETMPTR
         .
         .
         EXEC  CICS RETURN END-EXEC.
         GOBACK.


TRUEPGM

TRUEPGM is a Task Related User Exit which performs read
access to an MVS/ESA dataspace.

         DFHUEXIT TYPE=RM           create DSECT DFHUEPAR/DFHUERTR
RESMDPAR DSECT                     Resource Manager-dependent PARMLIST
ALETADR  DS    A
KEYADR   DS    A
GETMADR  DS    A
TRUEPGM  CSECT
TRUEPGM  AMODE 31
TRUEPGM  RMODE 24                   save caller's status
         BAKR  R14,0                on linkage stack
         SAC   512                  switch to AR mode
         LAE   R12,0(R15,0)         set up program base register
         USING TRUEPGM,R12          program start
         SAC   0                    switch to primary mode
         USING DFHUEPAR,R1          CICS provided PARMLIST
         L     R3,UEPHMSA           address of caller's save area
         L     R3,24(R3)            addressing the
         USING RESMDPAR,R3            dependent parameter list
         L     R4,ALETADR           load address of ALET
         L     R9,KEYADR            load address of search argument
         L     R6,GETMADR           load getmain pointer
         SAC   512                  switch to AR mode
         MVC   DSPCALET(4),0(R4)    provide the ALET
         MVC   KEYWORD(4),0(R9)     provide the search argument
         LAM   R2,R2,DSPCALET       using Access Register for
*                                   dataspace reference
         L     R2,DSPCORG           using General Purpose register for
*                                   referencing data in dataspace
LOOP     EQU   *
         CLC   0(133,R2),ENDOFDSP   end of dataspace reached ?
         BE    GETOUT
         CLC   0(4,R2),KEYWORD      search argument found ?
         BE    GETOUT
         LA    R2,133(,R2)          increment address pointer within
         B     LOOP                 the dataspace
GETOUT   EQU   *
         MVC   0(133,R6),0(R2)      copy data from dataspace into
EXIT     DS    0H                   Primary Space
         SR    R15,R15              set return code zero
         PR                         Program Return
DSPCALET DS    F                    dataspace ALET
DSPCORG  DC    F'0'                 dataspace origin
KEYWORD  DS    CL4                  search argument
ENDOFDSP DC    133X'00'             user-defined end of dataspace
         END   TRUEPGM


Gerrit Herlyn and Markus Zehnder
CICS Systems Programmers
TWS Stuttgart (Germany)                                              c TWS 1991

