 Circumventing DHTK abends
In our company we sometimes close our on-line DL/I databases for CICS processing
while we are running large batch jobs.  We do not want to use the MPS facility
because of the size of our databases.
While the database is closed, all CICS transactions trying to access this
database will abend with DHTK.  Such a cryptic message can be very irritating
for the user.  Moreover, the same message is displayed on the system console and
the dump dataset is filled with unusable dumps.
Obviously we had to do something about this problem.  We looked at the standard
CICS solutions.  Firstly, we looked at EXEC CICS HANDLE CONDITION(label), but
this has no effect in a DL/I environment.  Secondly, we looked at EXEC CICS
HANDLE ABEND, and found it usable but difficult and impractical.  Thirdly, after
reading CICS Update, issue 8, we wrote an Assembler program using the macro
DFHTM (with thanks to Graham Payne for the idea), which was to be called from
all programs using databases.  The program tests whether a given file is open or
closed, and returns this information to the calling program.
An example of how to call the program would be:
     DCL 1 COMMAREA,
           2 FILENAME   CHAR(8) INIT('yourname'),
           2 CODE1      CHAR(1),
           2 CODE2      CHAR(1);
     EXEC CICS LINK PROGRAM('ZZAFCT');
     IF CODE2 ='C' THEN GOTO DB_IS_CLOSED;
     else continue
The program ZZAFCT is written and tested under CICS 1.6.0 in VSE/SP 2.1.5.
PROGRAM ZZAFCT
***********************************************************************
*  THIS PROGRAM TESTS STATUS OF A FILE:    OPEN/CLOSED                *
*                 AND TYPE OF FILE    :    VSAM/DLI                   *
*  FILENAME AND RESPONSE VIA COMMAREA                                 *
***********************************************************************
         GBLB  &DFHEIMX
&DFHEIMX SETB  1
DFHEISTG DSECT
FCTDSBAR EQU   5
TCTTEAR  EQU   2
TIOABAR  EQU   8
COMPTR   EQU   7
COMMAREA DSECT
FILENAME DS    CL8
CODE1    DS    CL1        CODE-1 = TYPE V=VSAM,D=DLI
CODE2    DS    CL1        CODE-2 = O=OPEN, C=CLOSED
         COPY  DFHCSADS
WORK     DS    CL2048     CWA SIZE
         COPY  DFHTCADS
BUFADDR  DS    F
         DFHTCTZE CICSYST=YES
         COPY  DFHTIOA
         COPY  DFHFCTDS
         DFHTM MF=(D,DIRELEM)
         DFHTM MF=(D,SKTTBLE)
         DFHTM MF=(D,PARMLIST)
         DFHTM MF=(D,TMSTATIC)
         DFHTM MF=(D,DIRSEG)
         DFHTM MF=(D,LOCKBLOCK)
CICSFCT  CSECT
         BALR  6,0
         USING *,6
* ESTABLISH ADDRESSABILITY TO COMMAREA
         L     COMPTR,DFHEICAP
         USING COMMAREA,COMPTR
* ESTABLISH ADDRESSABILITY FOR DFHTMRQ
         LA    4,PARMLIST
         USING DFHTMRQ,4
* LOCATE THE FILE IN FCT
         DFHTM TYPE=LOCATE,TABLE=FCT,NAME=FILENAME,                    X
                MF=(N,(4)),                                             X
               SET=POINTER,                                            X
               NORMAL=NORMAL,                                          X
               NOTFND=ERROR,                                           X
               RLHELD=ERROR,                                           X
               DUPFND=ERROR,                                           X
               INVREQ=ERROR,                                           X
               PROTECT=ERROR,                                          X
               ERROR=ERROR,                                            X
               BUSY=ERROR
NORMAL   EQU   *     LOCATE OK
         L     FCTDSBAR,POINTER
         TM    FCTDSVR1,X'01'      IS FILE OPEN?
         BC    3,OPEN              YES
         MVC   CODE2,=C'C'         FILE IS CLOSED
         B     TESTTYPE
OPEN     EQU   *
         MVC   CODE2,=CUOU         FILE IS OPEN
TESTTYPE EQU   *
         TM    FCTDSVR3,X'10'      IS FILE A DL/I FILE?
         BC    3,DLI               YES
         TM    FCTDSVR3,X'80'      IS FILE A VSAM FILE?
         BC    3,VSAM              YES
         B     ERROR               NEITHER VSAM NOR DL/I
DLI      EQU   *
         MVC   CODE1,=C'D'         DL/I FILE
         B     ENDPROG
VSAM     EQU   *
         MVC   CODE1,=C'V'         VSAM FILE
         B     ENDPROG
ERROR    EQU   *     GENERAL ERROR
         MVC   CODE1,=C'E'
         MVC   CODE2,=C'E'
         B     ENDPROG
ENDPROG  EQU   *
         EXEC CICS RETURN
PARMLIST DS    CL20
POINTER  DS    CL4
         LTORG
         END


Per Vium
Systems Manager
FalckUs Redningskorps A/S (Denmark)     ) Xephon 1988





















