 CICS statistics file history
We do not have a CICS monitor, but we needed a way to keep track of CICS
resource utilisation over an extended period of time.  I wrote the accompanying
program to search the PCT, PPT, TCT, and FCT and create transaction records that
could be saved for later reporting purposes.  It was added to the PLT shut-down
program to execute just before CICS comes down.  It uses an extra-partition
transient dataset, the DCT table entries are coded to use the resource only at
program execution time.  This program is currently being executed on VSE/SP 2.1
with CICS 1.6.
THE PROGRAM SOURCE CODE
NIBBAR   EQU   2
SAACBAR  EQU   8
TDOABAR  EQU   9
FCTDSBAR EQU   10
TCTTEAR  EQU   10
PPTCBAR  EQU   10
PCTCBAR  EQU   10
         COPY  DFHCSADS
         COPY  DFHTCADS
         COPY  DFHSAADS
INPTID   DS    CL4
DWORD    DS    D
         DS    0F
TWAADR   DS    F
TWAEVT   DS    0CL21
TWAKEY   DS    CL8
TWACAT   DS    CL8
TWARET   DS    CL4
TWAERR   DS    CL1
         COPY  DFHFCTDS
         COPY  DFHPCTDS
         COPY  DFHPPTDS
         COPY  DFHTCTTE
         COPY  TCTUA
         COPY  DFHTDOA
FCOMREC  DS    0CL80               RECORD
FCOMCON  DS    CL4                 RECORD TYPE CONSTANT
FCOMRSC  DS    CL8                 RESOURCE NAME
FCOMJYD  DS    PL4                 DATE
FCOMDAT  DS    CL64                TYPE DEPENDENT DATA
         ORG   FCOMDAT
FTCTUSR  DS    0CL12               TCT USER AREA
FTCTETI  DS    CL4                 TERMINAL ID
         DS    CL4                 FUTURE EXPANSION
FTCTETT  DS    XL1                 TERMINAL TYPE
         DS    CL3                 FUTURE EXPANSION
FTCTENI  DS    F                   INPUT MESSAGES
FTCTENO  DS    F                   OUTPUT MESSAGES
FTCTETE  DS    F                   TRANSMISSION ERRORS
FTCTEOT  DS    F                   TRANSACTIONS
FTCTEOE  DS    F                   TRANSACTION ERRORS
         ORG   FCOMDAT
FPPTUSR  DS    0CL12               PPT USER AREA
FPPTLNG  DS    CL3                 PROGRAM LANGUAGE
FPPTRES  DS    CL1                 RESIDENT PROGRAM
FPPTSAR  DS    F                   PROGRAM SIZE IN BYTES
         DS    CL4
FPPTUCC  DS    F                   PROGRAM USE COUNT
FPPTFCH  DS    F                   PROGRAM FETCH COUNT
         ORG   FCOMDAT
FPCTUSR  DS    0CL12               CLEAR USER INFO
FPCTIPI  DS    CL8                 PROGRAM ID
         DS    CL4
FPCTTA   DS    F                   TRANSACTION ACCUMULATOR
FPCTSPA  DS    F                   STALL/PURGE ACCUMULATOR
FPCTSVC  DS    F                   NUMBER OF STORAGE VIOLATIONS
 FPCTASA  DS    F                   NUMBER OF ADDT'N STORAGE ACQUIRED
FPCTATR  DS    F                   RESTART COUNTER
         ORG   FCOMDAT
FFCTUSR  DS    0CL12               CLEAR USER INFO
         DS    CL12
FFCTDSR  DS    F                   READ REQUEST
FFCTDSG  DS    F                   GET FOR UPDATES
FFCTDSB  DS    F                   BROWSE REQUESTS
FFCTDSA  DS    F                   ADD REQUESTS
FFCTDSU  DS    F                   UPDATE REQUESTS
FFCTDEL  DS    F                   VSAM DELETES
FFCTHSW  DS    F                   HIGHEST WAIT-ON-STRING
FFCTTSW  DS    F                   TOTAL WAIT-ON-STRING
FFCTOFL  DS    F                   OVERFLOW RECORDS
FFCTRMD  DS    F                   REMOTE DELETES
         ISTDNIB
DPT312   CSECT
         USING *,R11
         LR    R11,R14             LOAD REGISTER
         DFHOC TYPE=OPEN,DATASET=TRANSDATA,DSETID=(STAT,(AA))
         TM    TCAOCTR,X'FF'       OPEN ERROR
         BNZ   MESSAGE             YES, GO DISPLAY MESSAGE
         DFHWTO 'DPT312 - TERMINAL STATISTICS PROGRAM STARTED'
         DFHSC TYPE=GETMAIN,NUMBYTE=80,CLASS=USER
         L     SAACBAR,TCASCSA
         DFHSC TYPE=GETMAIN,NUMBYTE=80,CLASS=TRANSDATA
         L     TDOABAR,TCASCSA
TCT000   MVC   INPTID,ZEROS        MOVE BINARY ZEROS
TCT100   MVC   TCATPTA,INPTID      LOCATE NEXT TCT AFTER THIS ONE
         MVC   TCATPLDM,BLANKS     SUPPLY LDC MNEMONIC (BLANKS)
         DFHTC CTYPE=LOCATE,       LOCATE TCTTE                       X
               DOMAIN=ALL,         ON ANY SYSTEM                      X
               TERM=NEXT,          NEXT TCTTE (OR FIRST)              X
               LDC=YES,            LDC MNEMONIC                       X
               NORESP=TCT200,      LOCATE O.K.                        X
               LASTTRM=TCT999,     NO MORE ENTRIES                    X
               ERROR=TCTERR        LOCATE ERROR
TCT200   L     TCTTEAR,TCATPTA     EST. ADDRESSABILITY TO TCTTE
         MVC   INPTID,TCATPTA      SAVE ADDRESS OF THIS TERMINAL
TCT300   XC    FCOMREC,FCOMREC     CLEAR RECORD AREA
         MVC   FCOMCON,TCT         MOVE TCT CONSTANT
         MVC   FCOMJYD,CSAJYDP      ""  DATE
         MVC   FCOMRSC,BLANKS      CLEAR NODE ID
         MVC   FCOMRSC(4),TCTTETI  MOVE TERMINAL ID TO RESOURCE
         MVC   FTCTUSR,BLANKS      CLEAR USER ID
         MVC   FTCTETI,TCTTETI     MOVE TERMINAL ID
         MVC   FTCTETT,TCTTETT      ""  TERMINAL TYPE
         MVC   FTCTENI,TCTTENI      ""  NUMBER OF INPUTS
         MVC   FTCTENO,TCTTENO      ""  NUMBER OF TERMINAL OUTPUTS
         MVC   FTCTETE,TCTTETE      ""  NUMBER OF TRANSMISSION ERRORS
         MVC   FTCTEOT,TCTTEOT      ""  NUMBER OF TRANSACTION
         MVC   FTCTEOE+2(2),TCTTEOE MOVE NUMBER OF TRANSACTION ERRORS
         TM    TCTIVTAM,TCTEVTAM   VTAM TERMINAL
         BZ    TCT400              NO, GO WRITE RECORD
         L     NIBBAR,TCTENIBA     LOAD ADDRESS OF NIB
         USING ISTDNIB,NIBBAR      ESTABLISH ADDRESSABILITY
         MVC   FCOMRSC,NIBSYM      MOVE VTAM NAME
         DROP  NIBBAR
TCT400   LA    R1,80               LOAD LENGTH OF RECORD
         STH   R1,TDOAVRL          SAVE LENGTH OF RECORD
         DFHTD TYPE=PUT,DESTID=STAT,TDADDR=TDOADBA
         B     TCT100              GO PROCESS NEXT
TCT999   B     PCT000
TCTERR   DFHWTO 'DPT312 - TCT LOCATE ERROR'
         B     PCT000
PCT000   MVC   INPTID,ZEROS        MOVE BINARY ZEROS
 PCT100   MVC   TCAKCFA,INPTID      LOCATE NEXT PCT AFTER THIS ONE
         DFHKC CTYPE=BROWSE,       LOCATE PCT ENTRY                 X
               DOMAIN=ALL,         ON ANY SYSTEM                    X
               UNLOCK=YES
         LTR   PCTCBAR,PCTCBAR     END OF TABLE
         BZ    PCT999              YES, GO PROCESS FILE
         MVC   INPTID,TCAKCFA      SAVE ADDRESS OF THIS PROGRAM
PCT200   XC    FCOMREC,FCOMREC     CLEAR RECORD AREA
         MVC   FCOMCON,PCT         MOVE PCT CONSTANT
         MVC   FCOMJYD,CSAJYDP      ""  DATE
         MVC   FCOMRSC,BLANKS      CLEAR TRAN ID
         MVC   FCOMRSC(4),PCTTI    MOVE TRAN ID
         MVC   FPCTUSR,BLANKS      CLEAR USER INFO
         MVC   FPCTIPI,PCTIPIA     MOVE INITIAL PROGRAM ID
         MVC   FPCTTA,PCTTA        MOVE TRANSACTION ACCUMULATOR
         MVC   FPCTSVC,PCTSCSVC    MOVE NUMBER OF STORAGE VIOLATIONS
         XC    FPCTSPA,FPCTSPA     CLEAR STALL/PURGE ACCUMULATOR
         MVC   FPCTSPA+2(2),PCTSPA MOVE STALL/PURGE ACCUMULATOR
         MVC   FPCTASA,PCTASAA     MOVE NUMBER OF ADDT'N STOR ACQ
         XC    FPCTATR,FPCTATR     CLEAR RESTART COUNTER
         MVC   FPCTATR+3(1),PCTATRCT MOVE RESTART COUNTER
PCT300   LA    R1,80               LOAD LENGTH OF RECORD
         STH   R1,TDOAVRL          SAVE LENGTH OF RECORD
         DFHTD TYPE=PUT,DESTID=STAT,TDADDR=TDOADBA
         B     PCT100              GO PROCESS NEXT
PCT999   B     PPT000
PPT000   MVC   INPTID,ZEROS        MOVE BINARY ZEROS
PPT100   MVC   TCAPCLA,INPTID      LOCATE NEXT PPT AFTER THIS ONE
         DFHPC CTYPE=BROWSE,UNLOCK=YES       LOCATE PPT ENTRY
         LTR   PPTCBAR,PPTCBAR     END OF TABLE
         BZ    PPT999              YES, GO PROCESS FILE
         MVC   INPTID,TCAPCLA      SAVE ADDRESS OF THIS PROGRAM
PPT200   XC    FCOMREC,FCOMREC     CLEAR RECORD AREA
         MVC   FCOMCON,PPT         MOVE PPT CONSTANT
         MVC   FCOMJYD,CSAJYDP      ""  DATE
         MVC   FCOMRSC,PPTPI       MOVE PROGRAM ID
         MVC   FPPTUSR,BLANKS      CLEAR USER INFO
         MVC   FPPTUCC,PPTUCC      MOVE USE COUNTER
         XC    FPPTFCH,FPPTFCH     CLEAR FETCH COUNT
         MVC   FPPTFCH+2(2),PPTFCH MOVE FETCH COUNT
         MVC   FPPTSAR,PPTSAR      MOVE PROGRAM SIZE
         MVI   FPPTRES,C'N'        SET RESIDENT PROGRAM OFF
         TM    PPTTLR,PPTPCRES     RESIDENT PROGRAM
         BZ    PPT300              NO, GO MOVE LANGUAGE
         MVI   FPPTRES,C'Y'        SET RESIDENT PROGRAM ON
PPT300   MVC   FPPTLNG,COB         MOVE COBOL PROGRAM
         TM    PPTTLR,PPTCBIND     COBOL PROGRAM
         BO    PPT400              YES, GO WRITE RECORD
         MVC   FPPTLNG,PLI         MOVE PLI PROGRAM
         TM    PPTTLR,PPTPLIND     PLI PROGRAM
         BO    PPT400              YES, GO WRITE RECORD
         MVC   FPPTLNG,RPG         MOVE RPG PROGRAM
         TM    PPTTLR,PPTRPIND     RPG PROGRAM
         BO    PPT400              YES, GO WRITE RECORD
         MVC   FPPTLNG,ASM         MOVE ASM PROGRAM
PPT400   LA    R1,80               LOAD LENGTH OF RECORD
         STH   R1,TDOAVRL          SAVE LENGTH OF RECORD
         DFHTD TYPE=PUT,DESTID=STAT,TDADDR=TDOADBA
         B     PPT100              GO PROCESS NEXT
PPT999   B     FCT000
FCT000   MVC   INPTID,ZEROS        MOVE BINARY ZEROS
         L     FCTDSBAR,CSAFCTBA   LOAD ADDRESS OF FCT
FCT100   USING DFHFCTDS,FCTDSBAR   ESTABLISH ADDRESSABILITY
         CLC   FCTDSID,HIGHVAL     END OF TABLE
         BE    FCT999              YES, GET OUT
FCT200   XC    FCOMREC,FCOMREC     CLEAR RECORD AREA
          MVC   FCOMCON,FCT         MOVE FCT CONSTANT
         MVC   FCOMJYD,CSAJYDP      ""  DATE
         MVC   FCOMRSC,BLANKS      CLEAR FILE ID
         MVC   FCOMRSC(7),FCTDSID  MOVE FILE ID
         MVC   FFCTUSR,BLANKS      CLEAR USER INFO
         MVC   FFCTDSR,FCTDSRD     MOVE READ REQUESTS
         MVC   FFCTDSG,FCTDSGU     MOVE GET UPDATE REQUESTS
         MVC   FFCTDSB,FCTDSBR     MOVE BROWSE REQUESTS
         MVC   FFCTDSA,FCTDSWRA    MOVE ADD REQUESTS
         MVC   FFCTDSU,FCTDSWRU    MOVE UPDATE REQUESTS
         MVC   FFCTOFL,FCTDSOFL    MOVE OVERFLOW RECORDS
         MVC   FFCTRMD,FCTRMDEL    MOVE REMOTE DELETES
         TM    FCTDSVSM,FCTVSAMI   VSAM DATA SET
         BZ    FCT400              NO, GO WRITE RECORD
FCT300   ZAP   DWORD,FCTDSDEL      NUMBER OF DELETES
         CVB   R2,DWORD            CONVERT TO BINARY
         ST    R2,FFCTDEL          SAVE NUMBER OF VSAM DELETES
         ZAP   DWORD,FCTDSHSW      HIGHEST WAIT-ON-STRING
         CVB   R2,DWORD            CONVERT TO BINARY
         ST    R2,FFCTHSW          SAVE HIGHEST WAIT-ON-STRING
         ZAP   DWORD,FCTDSTSW      TOTAL WAIT-ON-STRING
         CVB   R2,DWORD            CONVERT TO BINARY
         ST    R2,FFCTTSW          SAVE TOTAL WAIT-ON-STRING
FCT400   LA    R1,80               LOAD LENGTH OF RECORD
         STH   R1,TDOAVRL          SAVE LENGTH OF RECORD
         DFHTD TYPE=PUT,DESTID=STAT,TDADDR=TDOADBA
         AH    FCTDSBAR,FCTDSTEL   POINT TO NEXT ENTRY
         B     FCT100              GO PROCESS NEXT
FCT999   B     RETURN
MESSAGE  DFHWTO 'DPT312 - OPEN ERROR - FILE DPTSTAT'
         B     RETURN
RETURN   DFHOC TYPE=CLOSE,DATASET=TRANSDATA,DSETID=(STAT,(AA))
         DFHPC TYPE=RETURN
         LTORG
HIGHVAL  DC    16XL1'FF'
ZEROS    DC    16XL1'00'
BLANKS   DC    80XL1'40'
FCT      DC    CL4'FCT'
PCT      DC    CL4'PCT'
PPT      DC    CL4'PPT'
TCT      DC    CL4'TCT'
ASM      DC    CL3'ASM'
COB      DC    CL3'COB'
PLI      DC    CL3'PLI'
RPG      DC    CL3'RPG'
         END
ON-LINE STATISTICS FILE
Terminal record layout
     Position  Picture   Description
     01-04     X(4) Record type constant value of TTCTU
     05-12     X(8) VTAM terminal (node) name
     13-16     9(7) comp-3    Julian date of entry
     17-20     X(4) CICS terminal-id
     21-24     X(4) Future use
     25   X(1) Binary terminal type, see CICS manual
     26-28     X(3) Future use
     29-32     9(8) comp Number of input messages
     33-36     9(8) comp Number of output messages
     37-40     9(8) comp Number of transmission errors
     41-44     9(8) comp Number of transactions
     45-48     9(8) comp Number of transaction errors
     49-80     X(32)     Future use
Program record layout
     Position  Picture   Description
     01-04     X(4) Record type constant value of TPPTU
     05-12     X(8) Program name
      13-16     9(7) comp-3    Julian date of entry
     17-19     X(3) Program lanaguage (ASM, COB, PL/I, RPG)
     20   X(1) Resident program  (Y - yes, N - no)
     21-24     9(8) comp Program size in bytes
     25-28     X(4) Future use
     29-32     9(8) comp Program use count
     33-36     9(8) comp Program fetch count
     37-80     X(46)     Future use
Transaction record layout
     Position  Picture   Description
     01-04     X(4) Record type constant value of TPCTU
     05-12     X(8) Transaction identification
     13-16     9(7) comp-3    Julian date of entry
     17-24     X(8) Initial program
     25-28     X(4) Future use
     29-32     9(8) comp Transaction accumulator
     33-36     9(8) comp Stall/purge accumulator
     37-40     9(8) comp Number of storage violations
     41-44     9(8) comp Additional storage acquired
     45-48     9(8) comp Re-start counter
     49-80     X(32)     Future use
File record layout
     Position  Picture   Description
     01-04     X(4) Record type constant value of TFCTU
     05-12     X(8) File DTF name
     13-16     9(7) comp-3    Julian date of entry
     17-28     X(12)     Future use
     29-32     9(8) comp Read request
     33-36     9(8) comp Get for updates
     37-40     9(8) comp Browse requests
     41-44     9(8) comp Add requests
     45-48     9(8) comp Update requests
     49-52     9(8) comp VSAM deletes
     53-56     9(8) comp Highest wait-on-string
     57-60     9(8) comp Total wait-on-string
     61-64     9(8) comp Number of overflow records
     64-68     9(8) comp Remote deletes
     69-80     X(12)     Future use
DCT TABLE ENTRY
         DFHDCT TYPE=INITIAL,SUFFIX=01,TRNSUFX=(AA)
        ***      CICS SUPPLIED SDSCI AND USER SDSCI ENTRIES ***
         DFHDCT TYPE=SDSCI,                                           X
               DSCNAME=DPTSTAT,    MUST MATCH DTFNAME ON DLBL         X
               TYPEFLE=OUTPUT,                                        X
               BLKSIZE=2008,                                          X
               RECFORM=FIXBLK,                                        X
               RECSIZE=80,                                            X
               SUFFIX=AA,
               DEVICE=DISK
        ***      TYPE=EXTRA AND INTRA ENTRIES ***
         DFHDCT TYPE=EXTRA,                                           X
               DESTID=STAT,                                           X
               OPEN=DEFERRED,                                         X
               RESIDNT=NO
         DFHDCT TYPE=FINAL
         END   DFHDCTBA
HISTORY FILE UPDATE JCL
/. STEP010 * PHASE SORT * SORT CICS STATISTICS FILE *
// DLBL SORTIN1,'PD.DPT.PROD.CICS.STAT',,VSAM,CAT=VSESPUC    INPUT
// DLBL SORTOUT,'PD.DPT320D.S030.CICS.STAT'                  OUTPUT
// EXEC SORT,SIZE=256K
 SORT FIELDS=(1,4,CH,A,5,8,BI,A,13,4,PD,A),WORK=1
 RECORD TYPE=F,LENGTH=80
 INPFIL BLKSIZE=2000
 OUTFIL BLKSIZE=9200
 SUM FIELDS=(29,4,33,4,37,4,41,4,45,4,49,4,53,4,57,4,61,4,65,4,69,4,73,X
                 4,77,4),FORMAT=BI
 OMIT COND=((1,4,CH,EQ,C'PCT',AND,29,4,BI,EQ,X'00000000'),OR,          X
               (1,4,CH,EQ,C'PPT',AND,29,4,BI,EQ,X'00000000'))
 END
/*
/. STEP020 * PHASE SORT * MERGE STATISTICS FILE *
// DLBL SORTIN1,'PD.DPT320D.S040.CICS.STAT.HISTORY'          INPUT
// DLBL SORTIN2,'PD.DPT320D.S030.CICS.STAT,(D)'              INPUT
// DLBL SORTOUT,'PD.DPT320D.S040.CICS.STAT.HISTORY'          OUTPUT
// EXEC SORT,SIZE=256K
 MERGE FIELDS=(1,4,CH,A,5,8,BI,A,13,4,PD,A),FILES=2
 RECORD TYPE=F,LENGTH=80
 INPFIL BLKSIZE=9200
 OUTFIL BLKSIZE=9200
 END
/*
/. STEP030 * PHASE IDCAMS * DELETE CICS STATISTICS FILE *
// EXEC IDCAMS,SIZE=AUTO               DATA
  DELETE (PD.DPT.PROD.CICS.STAT) CLUSTER -
  CATALOG(VSESP.USER.CATALOG)
/*
STEP010 will merge records with the same date into one record and delete PPT and
PCT entries that were not used by CICS.  STEP020 will create a new history file
reading the previous history file and adding new records.  STEP030 will delete
the daily statistics file after creating the new history file.

Gregory Hoyt
Systems Programmer (USA) ) Reserved 1988






































