Systems programmer MVS commands via CICS

This program has been written for our network operators who are
out at remote sites where no TSO/SDSF screens are available.

It is mainly used for VTAM VARY ACT/INACT commands but
it can also be used for any command that can be input to MVS.  It
is however no use for commands that require a response from a
console because the results cannot be seen.  The program also
uses two of our own home-grown macros and one of our user
written SVCs.  The source for these is also included.

It has been written under CICS 1.7 and MVS/XA 2.1.7.  The
macros have been used under lower releases of MVS and the
System macros and facilities manual will have the necessary
release-dependent information.

The following is an example of the OFF MACRO.

        MACRO
&NAME   OFF
        PRINT OFF
        LA    R0,2
        XR    R1,R1
        SVC   247
        PRINT ON
.END    MEND

The following is an example of the ON MACRO.

        MACRO
&NAME   ON
        PRINT OFF
        LA    R0,1
        XR    R1,R1
        SVC   247
        PRINT ON
.END    MEND

The SVC that follows must be enabled in your SVC table and in
SYS1.LPALIB  It is a type 3 user SVC and basically puts the
issuer into supervisor state.  Most MVS sites probably have one of
their own.  The one I have included is SVC 247 and is therefore
put in LPALIB as IGC0024G.

IGC0024G CSECT
         BALR  12,0
         USING *,12
         L     2,180(4)                    GET JSCB
         BCT   0,AUTHOFF                   R0Â¬=1 REQUEST
AUTHON   OI    236(2),X'01'                SET JSCBAUTH BIT
         B     RETURN                      EXIT WITH AUTHORISATION ON
AUTHOFF  NI    236(2),X'FE'                CLEAR JSCBAUTH BIT
RETURN   BR    14
         END

SY240

*-----------------------------------*
*          S Y 2 4 0.               *
*          ----------               *
* THIS PROGRAM IS WRITTEN TO ALLOW  *
* THE NETWORK STAFF TO ISSUE MVS    *
* CONSOLE COMMANDS FROM A CICS      *
* SCREEN.                           *
* CONTROL BLOCKS: NONE              *
* SVC'S         : SVC34             *
*               (IN THE FORM OF     *
*               THE MGCR MACRO)     *
*               : SVC107            *
*               (IN THE FORM OF THE *
*               MODESET MACRO)      *
*               : SVC247            *
*               (OUR OWN AUTH SVC)  *
* MODESET AND MGCR CAN BE FOUND IN  *
* SPL:SYSTEM MACRO'S AND FACILITIES *
* VOLUME 2.  (GC28-1151)            *
*-----------------------------------*
SY240 TITLE 'MVS COMMANDS FROM CICS'
SY240    CSECT
         EXEC CICS HANDLE AID CLEAR(RET)
         EXEC CICS HANDLE CONDITION MAPFAIL(RET)
SENDMAP  EXEC CICS SEND MAP('SY240M'),MAPSET('SY240S'),FREEKB,ERASE
         EXEC CICS RECEIVE MAP('SY240M'),MAPSET('SY240S')
         B    AA01
RET      EXEC CICS SEND CONTROL FREEKB
         EXEC CICS RETURN
*------------------------------------------------*
* AT THIS POINT WE HAVE READ IN THE MAP          *
* CONTAINING THE MVS COMMAND THAT WE WISH TO     *
* EXECUTE.                                       *
*------------------------------------------------*
*     MANIPULATE THE INPUT COMMAND *
AA01     LA   R7,COMMNDI          ADDRESS THE INPUT COMMAND
         MVC  CMD1,=CL40' '       SPACE FILL CMD1
         MVC  CMD1(40),0(R7)      MOVE THE COMMAND FROM THE MAP
*                                 SAVE THE
*                                      REGISTERS FOR
         STM   14,12,12(13)                  GIVING CONTROL
*                                                     BACK TO CICS
         ON                       AUTHORISE MACRO
         MODESET MODE=SUP,KEY=ZERO
         XR   R0,R0               ZEROISE R0
         MGCR PIB                 EXECUTE THE COMMAND MACRO
         MODESET MODE=PROB,KEY=NZERO
         OFF
         L    R13,4(R13)          RESTORE THE REGISTERS
         LM   R14,R12,12(R13)             AND RETURN CONTROL
         BALR  14,15                                    BACK TO CICS
         DS   0H
* START OF CONSTANTS AND STORAGE AREAS  *
PIB      DC   X'80'
         DC   AL1(PIBL1-PIB)
         DC   X'0800'
CMD1     DC   CL40' '
PIBL1    DC   AL4(ECB)
ECB      DS   F
DFHEISTG DSECT
         COPY SY240S
         COPY DFHBMSCA
         END  SY240

THE MAP SY240S

SY240S  DFHMSD TYPE=&SYSPARM,MODE=INOUT,LANG=ASM,                      *
               CTRL=FREEKB,TIOAPFX=YES
SY240M  DFHMDI SIZE=(24,80),LINE=1,COLUMN=1,MAPATTS=COLOR
        DFHMDF LENGTH=24,POS=(1,26),INITIAL='NETWORKING OPS SERVICES' ,*
               ATTRB=PROT,COLOR=YELLOW
        DFHMDF LENGTH=29,POS=(2,23),COLOR=RED,ATTRB=PROT,              *
               INITIAL='THIS IS THE COMPUTER SECTIONS'
        DFHMDF LENGTH=21,POS=(3,26),COLOR=RED,ATTRB=PROT,              *
               INITIAL='MVS COMMAND FACILITY'
        DFHMDF LENGTH=26,POS=(15,2),ATTRB=PROT,                        *
               COLOR=TURQUOISE,INITIAL='ENTER THE MVS COMMAND HERE'
        DFHMDF LENGTH=4,POS=(15,30),ATTRB=PROT,COLOR=TURQUOISE,        *
               INITIAL='===>'
COMMND  DFHMDF LENGTH=40,POS=(15,35),ATTRB=IC
        DFHMDF LENGTH=1,POS=(15,78),ATTRB=PROT
        DFHMDF LENGTH=30,POS=(20,2),ATTRB=PROT,                        *
               INITIAL='USE CLEAR TO END'
        DFHMDI TYPE=FINAL
        END


P Sedman
Systems Programmer (UK) ) Xephon 1989


