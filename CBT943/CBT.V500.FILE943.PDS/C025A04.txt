 Auto-install terminal-id assignment

With CICS 1.7 one can choose to implement the new auto-install feature, which
allows VTAM terminals to log on to CICS even though they were not previously
defined in the TCT or through Resource Definition On-line (RDO).  Besides the
reduced maintenance efforts of the CICS systems programmer, this feature can
provide significant virtual storage relief, particularly for those with large
VTAM terminal networks, as the TCTTE for an auto-installed terminal is
dynamically built when the user logs on, and then subsequently deleted when he
or she logs off.

A VTAM terminal entry consumes roughly 500 bytes of storage, so using as an
example an installation with 1000 terminals defined but having at most 300
concurrent users, one could expect to save at least 700 x 500 bytes or about
350K of storage.

One of the events that takes place during the auto-install process is the
invocation of a user-written program.  Among other things, this program must
determine whether or not the terminal is allowed to log on, and if so, what
four-character terminal-id will be assigned to it.  The program is also invoked
when an auto-installed terminal logs off, enabling the programmer to write
messages to a transient data log, to record statistics, etc.

IBM supplies a sample user program, DFHZATDX, which may suffice for some users.
However, one of its shortcomings is that, in assigning the terminal-id, it
merely uses the last four non-blank characters in the NETNAME.

An installation with a fairly sizeable network may run into problems using this
program if different terminals share the same last four NETNAME characters.  The
first of these terminals would be able to log on, but the second and subsequent
terminals would have their log-ons rejected since the terminal-id being assigned
to them would already be in use.

At our company, we've written an SMP/E usermod to DFHZATDX that uses an in-core
bit map within the program to derive a unique four-character terminal-id.  The
bit map, initialised to zeros, is scanned during each auto-install request.
Each bit corresponds to a unique number between 0 and 999.  The first bit will
assign a terminal-id of I000, the second will assign a terminal-id of I001 and
so on up to I999.  Once a bit is found and is turned on, it remains on until the
user logs off and the delete logic is invoked.  The delete logic finds the
appropriate bit using the terminal-id and turns it off, thereby freeing the bit
for use by another auto-install request.  Because the bit map is within the code
itself, DFHZATDX should be made resident, preferably via the ALT.  The usermod
is as follows:

++ USERMOD (PB00010).
++ VER (C150)  FMID (HCI1702) .
++ VER (C150)  FMID (HCI1703) .
++ MACUPD (DFHZATDX) DISTLIB (SOURCE).
./ CHANGE NAME=DFHZATDX
DBLWORD  DS    D
BITCHAR  DS    X
* INSTALL LOGIC
         LA    R7,BITMAP          ADDRESS BIT MAP
         ZAP   DBLWORD,=P'0'      INIT COUNTER
SETBIT   DS    0H
         LA    R6,X'80'           INIT FOR TM INSTRUCTION
INSTLOOP DS    0H
         EX    R6,CHECKBIT        IS BIT OFF?
         BZ    FOUNDBIT           YES, USE THIS ONE
BUMPCTR  DS    0H
         AP    DBLWORD,=P'1'      BUMP COUNTER
         SRA   R6,1               SHIFT OVER TO NEXT BIT
         BZ    NEXTBYTE           ALL BITS USED, TRY NEXT BYTE
         B     INSTLOOP           TRY NEXT BIT
CHECKBIT TM    0(R7),0
NEXTBYTE DS    0H
         LA    R7,1(R7)           BUMP OVER TO NEXT BYTE IN BIT MAP
         CP    DBLWORD,=P'999'    END OF BIT MAP?
         BH    RETURN             ERROR, RETURN
         B     SETBIT             TRY NEXT BIT
FOUNDBIT DS    0H
         EX    R6,BITON           TURN THE BIT ON
         MVI   TRMIDSEL,C'I'      SET FIRST CHAR OF TERM ID
         UNPK  TRMIDSEL+1(3),DBLWORD+6(2) SET REMAINING CHARS BASED ON
         OI    TRMIDSEL+3,X'F0'   COUNTER VALUE
* THE FOLLOWING 'EXEC CICS INQUIRE TERMINAL' IS PERFORMED FOR TWO
* REASONS: 1) THERE MAY ALREADY BE AN EXPLICITLY-DEFINED TERMINAL
* WITH THE SAME ID AS WE'VE SELECTED; AND 2) IF WE'VE JUST GONE
* THROUGH AN EMERGENCY RESTART, AN AUTO-INSTALLED TERMINAL FROM THE
* PREVIOUS BRING-UP USING THE SAME TERMINAL ID MAY STILL BE INSTALLED.
         EXEC CICS HANDLE CONDITION TERMIDERR(NOTFOUND)
         EXEC CICS INQUIRE TERMINAL(TRMIDSEL)
         B     BUMPCTR            TERM ID ALREADY IN USE; TRY NEXT ONE
NOTFOUND DS    0H                 TERM ID NOT IN USE; WE'RE OK
BITON    OI    0(R7),0
* DELETE LOGIC
         PACK  DBLWORD,DLTERMID+1(3) CONVERT LAST 3 CHARS OF TERMID
         CVB   R6,DBLWORD         TO BINARY
         LA    R7,BITMAP          ADDRESS THE BIT MAP
DELLOOP  DS    0H                 LOOP TO DETERMINE WHICH BYTE CONTAINS
*                                 THE BIT FOR THIS TERMINAL
         CH    R6,=H'8'           R6 LESS THAN 8?
         BL    DELCONT            YES, FOUND THE RIGHT BYTE
         SH    R6,=H'8'           NO, SUBTRACT EIGHT
         LA    R7,1(R7)           AND TRY NEXT BYTE
         B     DELLOOP
DELCONT  DS    0H
         STC   R6,BITCHAR
         TR    BITCHAR,BITTABLE   DETERMINE WHICH BIT TO TURN OFF
         IC    R6,BITCHAR
         NC    0(1,R7),BITCHAR    TURN OFF BIT
BITTABLE DC    B'01111111'
         DC    B'10111111'
         DC    B'11011111'
         DC    B'11101111'
         DC    B'11110111'
         DC    B'11111011'
         DC    B'11111101'
         DC    B'11111110'
* END OF TRANSLATE TABLE
         DC    C'BIT MAP STARTS HERE' EYECATCHER
BITMAP   DC    125X'00'           BIT MAP
./ ENDUP

This modification will allow up to 1000 concurrent auto-install users.  If more
are desired, one can easily modify the code to eliminate the assignment of 'I'
as the first character of the terminal-id.



Robert M Lee
CICS Systems Programmer
Pacific Bell (USA)  ) Pacific Bell 1987








