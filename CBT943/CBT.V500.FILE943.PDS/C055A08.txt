Resetting the status of VSAM files

The following article relates to an operational environment of
an IBM 4381, MVS/XA, and CICS 1.7.

THE PROBLEM

Our installation migrated from VSE to MVS/XA last year.  We
had CICS 1.6 under VSE and, when we migrated to XA, CICS
was  migrated to 1.7.  CICS 1.7 provides more facilities in
coding the FCT parameter, so we changed FILSTAT from
(ENABLED, OPENED) to (ENABLED,CLOSED) to save
time during start-up.  This parameter is the default and the file
will be opened on first reference.

There is, however, the possibility of problems during overnight
batch when CICS abends or is cancelled during the day.
Suppose two files, FILEA and FILEB, are accessed before
CICS abends or cancels.  Immediately, CICS is emergency-
restarted and, in this run, only FILEA is accessed, changing
the status to 'open'; FILEB remains closed.  After normal
shutdown, the batch job tries to open FILEB and receives a
status indicating that the file is in use by another job.  This
other job can only be CICS, but there is no CICS running.

When we asked for support IBM informed us that this is a
normal condition and an IDCAMS VERIFY would reset the
open status.

THE SOLUTION

In order to avoid this problem and having to code a job to
verify 260 files we wrote the following program to open all the
closed files.  This program runs during CICS shutdown (ie it is
included in DFHPLTSD).

We don't open 'disabled' files because they are closed
intentionally by the operator to run batch programs which use
the files while CICS is running.

ID DIVISION.
PROGRAM-ID.            PGMEXEMP.
AUTHOR.                ALICE MICHISHITA.
ENVIRONMENT DIVISION.
CONFIGURATION SECTION.
SPECIAL-NAMES.
    DECIMAL-POINT IS COMMA.
DATA DIVISION.
WORKING-STORAGE SECTION.
01  WAREA.
    03  WSTART      PIC X(10)       VALUE 'START WS=>'.
    03  WFILE       PIC X(08)       VALUE SPACES.
    03  WCLOSED     PIC S9(8) COMP  VALUE ZEROS.
    03  WDISABLED   PIC S9(8) COMP  VALUE ZEROS.
    03  WRESP       PIC S9(8) COMP  VALUE ZEROS.
    03  WACCESS     PIC S9(8) COMP  VALUE ZEROS.
    03  WMESSAGE.
        05 FILLER   PIC X(09)       VALUE 'FILE *** '.
        05 WFILEID  PIC X(08)       VALUE SPACES.
        05 FILLER   PIC X(05)       VALUE ' *** '.
        05 WMSG     PIC X(22)       VALUE SPACES.
        05 FILLER   PIC X(06)       VALUE ' CMD: '.
        05 WCMD     PIC X(05)       VALUE SPACES.
    03  WEND        PIC X(10)       VALUE 'END WS  =>'.
PROCEDURE DIVISION.
    EXEC CICS INQUIRE DATASET
                      RESP (WRESP)
                      START END-EXEC.
    IF WRESP EQUAL DFHRESP (ILLOGIC) GO TO ERROR-START.
LOOP.
    EXEC CICS INQUIRE DATASET      (WFILE)
                      OPENSTATUS   (WCLOSED)
                      ENABLESTATUS   (WDISABLED)
                      ACCESSMETHOD (WACCESS)
                      RESP         (WRESP)
                      NEXT
                      END-EXEC.
    IF WRESP    EQUAL DFHRESP (ILLOGIC) GO TO ERROR-NEXT.
    IF WRESP    EQUAL DFHRESP (END) GO TO ENDTASK.
    IF WACCESS  NOT EQUAL DFHVALUE (VSAM)     GO TO LOOP.
    IF WCLOSED  NOT EQUAL DFHVALUE (CLOSED)   GO TO LOOP.
    IF WDISABLED    EQUAL DFHVALUE (DISABLED) GO TO LOOP.
    MOVE WFILE TO WFILEID.
    EXEC CICS SET DATASET    (WFILE)
                  OPEN
                  RESP       (WRESP)
                  END-EXEC.
    IF WRESP EQUAL DFHRESP (DSIDERR) GO TO ERROR-OPEN.
    IF WRESP EQUAL DFHRESP (IOERR)   GO TO ERROR-OPEN.
    IF WRESP EQUAL DFHRESP (INVREQ)  GO TO ERROR-OPEN.
    MOVE 'SUCCESSFULLY OPENED' TO WMSG.
    MOVE 'OPEN' TO WCMD
    EXEC CICS WRITEQ TD QUEUE  ('CSSL')
                        FROM   (WMESSAGE)
                        LENGTH (55)
                        END-EXEC.
    GO TO LOOP.
ERROR-OPEN.
    MOVE 'OPEN FAILED' TO WMSG.
    MOVE 'OPEN' TO WCMD
    EXEC CICS WRITEQ TD QUEUE  ('CSSL')
                        FROM   (WMESSAGE)
                        LENGTH (55)
                        END-EXEC.
    GO TO LOOP.
ERROR-START.
    MOVE 'FAILED' TO WMSG.
    MOVE 'START' TO WCMD
    EXEC CICS WRITEQ TD QUEUE  ('CSSL')
                        FROM   (WMESSAGE)
                        LENGTH (55)
                        END-EXEC.
    GO TO ENDTASK.
ERROR-NEXT.
    MOVE 'FAILED' TO WMSG.
    MOVE 'NEXT' TO WCMD
    EXEC CICS WRITEQ TD QUEUE  ('CSSL')
                        FROM   (WMESSAGE)
                        LENGTH (55)
                        END-EXEC.
ENDTASK.
    EXEC CICS INQUIRE DATASET
                      RESP (WRESP)
                      END END-EXEC.
    EXEC CICS RETURN END-EXEC.
    GOBACK.


Alice Michishita
Systems Programmer
Duratex SA (Brazil)                                     ) Xephon 1990


