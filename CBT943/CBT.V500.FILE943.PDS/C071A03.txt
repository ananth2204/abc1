Conversing for terminal information

In the old days before TORs and AORs, whenever you needed
information on your terminal or any other terminal, you
obtained addressability to it and tested the fields defined in a
DSECT.  Since the advent of EXEC-level coding, a facility to
inquire about and set attributes of a terminal has been made
available.  Chapter 5.7 of the CICS Customization Guide
explains how to accomplish this.  The only problem is that
these INQUIRE and SET EXEC-level commands will not
transverse a TOR/AOR connection.

Until such time as this is changed, a facility call Distributed
Transaction Processing (DTP) could be used to communicate
the needed information.  SP022A shows a program that will
inquire on a terminal and obtain all the possible terminal
information or communicate to a mirror program (SP019A) in
the TOR to obtain the information.

The key to finding out if your terminal is local or remote is the
REMOTESYSTEM parameter on the EXEC CICS INQUIRE
command.  It will be non-blank if your terminal is remote and
will contain the SYSID of the TOR region where it is located.
The program must be supplied with either the terminal-id or the
network name and will provide whichever one of these input
parameters is not given.  You must issue a DEFINE
PROGRAM(SP019A) LAN(ASSEMBLER), and a DEFINE
TRANSACTION(SP10) PROGRAM(SP019A) in the TOR.
You must issue a DEFINE PROGRAM(SP022A) in each AOR
and optionally in the TOR if you have any applications running
there.


SP022A - THE LOCATE TERMINAL INFORMATION MODULE

***********************************************************************
*        PROGRAM TO OBTAIN ALL POSSIBLE TERMINAL INFORMATION          *
*        GIVEN EITHER A TERMINAL ID OR NETNAME.                       *
*                        INTERFACE TO SP022A                          *
*        CONTROL TRANSFERRED WITH A PROGRAM CONTROL LINK.             *
*        INPUT: COMMUNICATIONS AREA  (SP050F - ASSEMBLER)             *
*                                    (SP070F - COBOL)                 *
*                BYTE  1 - 4  TERMINAL ID OR BLANKS                   *
*                BYTES 5 -12  NETNAME OR BLANKS                       *
*                BYTES 13-92  TERMINAL INFORMATION FIELDS             *
*                BYTE  93     RETURN CODE FROM SP022A                 *
*                BYTE  94-95  LENGTH OF SP022A COMMAREA (COBOL ONLY)  *
*        OUTPUT: DTP AREA  (IF REQUIRED)                              *
*                BYTE  1 - 4  TRANSACTION ID                          *
*                BYTE  5 - 8  TERMINAL ID OR BLANKS                   *
*                BYTES 12-16  NETNAME OR BLANKS                       *
*                BYTES 17-96  TERMINAL INFORMATION FIELDS             *
*                BYTE  97     RETURN CODE FROM SP019A                 *
*        OUTPUT: COMMUNICATIONS AREA  (SP050F - ASSEMBLER)            *
*                                     (SP070F - COBOL)                *
*                BYTE  93     RETURN CODE FROM SP022A                 *
*                00 - NO ERROR                                        *
*                FF - INVALID REQUEST - BOTH NETNAME AND TERMINAL     *
*                                       ID ARE BLANKS OR BOTH ARE     *
*                                       NON BLANK                     *
*                01 - EXCEPTIONAL CONDITION - TERMINAL ID OR NETNAME  *
*                                       NOT FOUND                     *
***********************************************************************
WRKREG1  EQU   5                   WORK REGISTER ONE
COMPTR   EQU   6                   COMMUNICATIONS AREA BASE REGISTER
         USING TLPRMS,COMPTR       ADDRESSABILITY TO COMMUNICATIONS AR
COMREGDS DSECT                     COMMUNICATIONS AREA DEFINITION
         COPY  SP050F
DFHEISTG DSECT
SAVFREE  DS    XL1                 SAVED EIB FREE FIELD
SAVRECV  DS    XL1                 SAVED EIB RECV FIELD
RSYSID   DS    CL4                 REMOTE SYSTEM ID
DTPSES   DS    CL4                 SESSION ID
DTPLNTH  DS    H                   LENGTH OF DATA FOR DTP TRANSACTION
DTPAREA  DS    0CL99               AREA FOR DTP DATA
DTPTRAN  DS    CL4                 AREA FOR DTP TRANSACTION
DTPDTA   DS    CL95
SP022A   CSECT                     IDENTIFY CONTROL SECTION
         B     LOCPRC              BRANCH AROUND IDENT
         DC    C'SP022A V1M2'      PROGRAM IDENT
         DC    C'&SYSDATE'         DATE AND
         DC    C'&SYSTIME'         TIME OF ASSEMBLY
LOCPRC   DS    0H                  START OF PROGRAM
         L     COMPTR,DFHEICAP     LOAD ADDRESS OF COMMUNICATIONS AREA
         LA    COMPTR,0(COMPTR)    CLEAR HIGH ORDER BYTE
         LTR   COMPTR,COMPTR       WAS A COMMUNICATIONS AREA PASSED
         BZ    TRNABND             NO, ABEND
LINKPRC  DS    0H
         EXEC CICS HANDLE CONDITION TERMIDERR(BDRTN)
         CLC   TLTRMID,=C'    '    LOOKING FOR NETNAME?
         BNE   LOCNTNM             YES, BRANCH TO NETNAME PROCESSING
         CLC   TLTNTNM,=C'        ' LOOKING FOR TERMINAL ID?
         BNE   LOCTID              YES, BRANCH TO TERMINAL PROCESSING
         B     PARMERR             NO, BRANCH TO ERROR
LOCNTNM  DS    0H                  LOCATE NETNAME
         CLC   TLTNTNM,=C'        ' IS NETNAME BLANK?
         BNE   PARMERR             NO, PARAMETER ERROR
         EXEC CICS INQUIRE TERMINAL(TLTRMID) NETNAME(TLTNTNM)          X
               REMOTESYSTEM(RSYSID)                                    X
               MODENAME(TLTMDNM) TRANSACTION(TLTRNID)                  X
               TERMPRIORITY(TLTPRTY) USERAREA(WRKREG1)                 X
               USERAREALEN(TLTUALN) OPERID(TLTOPID)                    X
               USERID(TLTUSID) DEVICE(TLTDVC) TERMMODEL(TLTMODL)       X
               ACCESSMETHOD(TLTACMD) CREATESESS(TLTCRTS)               X
               ACQSTATUS(TLTAQST) SERVSTATUS(TLTSVST)                  X
               ATISTATUS(TLTATIST) TTISTATUS(TLTTTIST)                 X
               PAGESTATUS(TLTPGST) SCREENHEIGHT(TLTSCHT)               X
               SCREENWIDTH(TLTSCWT) GCHARS(TLTGCHR) GCODES(TLTGCDS)
         ST    WRKREG1,TLTUAREA    SET USER AREA LENGTH
         CLC   RSYSID,=C'    '     LOCAL TERMINAL
         BE    GDRTN               RETURN TO CALLER
         B     RMTPRC              DO DTP TRAN TO TERMINAL OWNING RGN
LOCTID   DS    0H                  LOCATE TERMINAL ID
         EXEC CICS INQUIRE NETNAME(TLTNTNM) TERMINAL(TLTRMID)          X
               REMOTESYSTEM(RSYSID)                                    X
               MODENAME(TLTMDNM) TRANSACTION(TLTRNID)                  X
               TERMPRIORITY(TLTPRTY) USERAREA(WRKREG1)                 X
               USERAREALEN(TLTUALN) OPERID(TLTOPID)                    X
               USERID(TLTUSID) DEVICE(TLTDVC) TERMMODEL(TLTMODL)       X
               ACCESSMETHOD(TLTACMD) CREATESESS(TLTCRTS)               X
               ACQSTATUS(TLTAQST) SERVSTATUS(TLTSVST)                  X
               ATISTATUS(TLTATIST) TTISTATUS(TLTTTIST)                 X
               PAGESTATUS(TLTPGST) SCREENHEIGHT(TLTSCHT)               X
               SCREENWIDTH(TLTSCWT) GCHARS(TLTGCHR) GCODES(TLTGCDS)
         ST    WRKREG1,TLTUAREA    SET USER AREA LENGTH
         CLC   RSYSID,=C'    '     LOCAL TERMINAL
         BE    GDRTN               RETURN TO CALLER
RMTPRC   DS    0H                  REMOTE LOCATE TERMINAL PROCESSING
         EXEC CICS ALLOCATE SYSID(RSYSID)
         MVC   DTPSES,EIBRSRCE     SET UP SESSION ID
         MVC   DTPTRAN,=C'SP10'    SET UP DTP TRAN ID
         MVC   DTPDTA,TLPRMS       SET UP DTP DATA
         LA    WRKREG1,TLPLEN      SET UP DTP DATA LENGTH
         AH    WRKREG1,=H'4'       .
         STH   WRKREG1,DTPLNTH     .
         EXEC CICS CONVERSE FROM(DTPAREA) FROMLENGTH(DTPLNTH)          X
               INTO(TLPRMS) TOLENGTH(DTPLNTH) SESSION(DTPSES)
         EXEC CICS FREE SESSION(DTPSES)
         B     ENDPGM              RETURN TO CALLER
PARMERR  DS    0H                  PARAMETER ERROR PROCESSING
         MVI   TLRETCD,X'FF'       SET INVALID REQUEST INDICATOR
         B     ENDPGM              GO TO RETURN POINT
GDRTN    MVI   TLRETCD,X'00'       SET GOOD RETURN CODE
         B     ENDPGM              AND RETURN
BDRTN    DS    0H                  BAD RETURN FROM INQUIRE
         MVI   TLRETCD,X'01'       SET TERMINAL/NET NOT FOUND INDICATOR
         B     ENDPGM              AND RETURN
TRNABND  DS    0H                  ABEND TRANSACTION
         EXEC CICS ABEND ABCODE('SP45')
ENDPGM   DS    0H
         EXEC CICS RETURN
         END


SP019A - LOCATE TERMINAL INFORMATION MODULE MIRROR

***********************************************************************
*        PROGRAM TO OBTAIN A TERMINAL ID GIVEN A NETNAME OR TO        *
*        OBTAIN A NETNAME GIVEN A TERMINAL ID.                        *
*                        INTERFACE TO SP019A                          *
*        CONTROL TRANSFERRED WITH A DTP TRANSACTION OF SP10.          *
*        INPUT: DTP AREA                                              *
*                BYTE  1 - 4  TRANSACTION ID                          *
*                BYTE  5 - 97 SP022A COMMUNICATIONS AREA              *
*        OUTPUT: DTP AREA                                             *
*                BYTE  5 - 96 TERMINAL INFORMATION FIELDS             *
*                BYTE  97     RETURN CODE FORM SP019A                 *
*                00 - NO ERROR                                        *
*                01 - EXCEPTIONAL CONDITION - TERMINAL ID OR NETNAME  *
*                                       NOT FOUND                     *
***********************************************************************
WRKREG1  EQU   5                   WORK REGISTER ONE
COMPTR   EQU   8                   COMMUNICATIONS AREA BASE REGISTER
         USING TLPRMS,COMPTR       ADDRESSABILITY TO COMMUNICATIONS AR
COMREGDS DSECT                     COMMUNICATIONS AREA DEFINITION
TRANID   DS    CL4                 TRANSACTION IDENTIFICATION
         COPY  SP050F
DFHEISTG DSECT
DTPLNTH  DS   H                    LENGTH OF DATA FOR DTP TRANSACTION
SAVFREE  DS   XL1                  SAVED EIB FREE AREA
SAVRECV  DS   XL1                  SAVED EIB RECV AREA
SP019A   CSECT                     IDENTIFY CONTROL SECTION
         B     LOCPRC              BRANCH AROUND IDENT
         DC    C'SP019A V1M1'      PROGRAM IDENT
         DC    C'&SYSDATE'         DATE AND
         DC    C'&SYSTIME'         TIME OF ASSEMBLY
LOCPRC   DS    0H                  START OF PROGRAM
         CLC   EIBTRNID,=C'SP10'   GOT HERE FROM DTP
         BNE   TRNABND             NO, ABEND
         EXEC CICS RECEIVE SET(COMPTR) LENGTH(DTPLNTH)
         MVC   SAVFREE,EIBFREE     SAVE EIBFREE FIELD
         MVC   SAVRECV,EIBRECV     SAVE EIBRECV FIELD
         LA    COMPTR,4(0,COMPTR)  BUMP PAST TRANID
LINKPRC  DS    0H
         EXEC CICS HANDLE CONDITION TERMIDERR(BDRTN)
         CLC   TLTRMID,=C'    '    LOOKING FOR NETNAME?
         BNE   LOCNTNM             YES, BRANCH TO NETNAME PROCESSING
         CLC   TLTNTNM,=C'        ' LOOKING FOR TERMINAL ID?
         BNE   LOCTID              YES, BRANCH TO TERMINAL PROCESSING
LOCNTNM  DS    0H                  LOCATE TERMINAL ID
         EXEC CICS INQUIRE TERMINAL(TLTRMID) NETNAME(TLTNTNM)          X
               MODENAME(TLTMDNM) TRANSACTION(TLTRNID)                  X
               TERMPRIORITY(TLTPRTY) USERAREA(WRKREG1)                 X
               USERAREALEN(TLTUALN) OPERID(TLTOPID)                    X
               USERID(TLTUSID) DEVICE(TLTDVC) TERMMODEL(TLTMODL)       X
               ACCESSMETHOD(TLTACMD) CREATESESS(TLTCRTS)               X
               ACQSTATUS(TLTAQST) SERVSTATUS(TLTSVST)                  X
               ATISTATUS(TLTATIST) TTISTATUS(TLTTTIST)                 X
               PAGESTATUS(TLTPGST) SCREENHEIGHT(TLTSCHT)               X
               SCREENWIDTH(TLTSCWT) GCHARS(TLTGCHR) GCODES(TLTGCDS)
         ST    WRKREG1,TLTUAREA    SET USER AREA LENGTH
         B     GDRTN               RETURN TO CALLER
LOCTID   DS    0H                  LOCATE TERMINAL PROCESSING
         EXEC CICS INQUIRE NETNAME(TLTNTNM) TERMINAL(TLTRMID)          X
               MODENAME(TLTMDNM) TRANSACTION(TLTRNID)                  X
               TERMPRIORITY(TLTPRTY) USERAREA(WRKREG1)                 X
               USERAREALEN(TLTUALN) OPERID(TLTOPID)                    X
               USERID(TLTUSID) DEVICE(TLTDVC) TERMMODEL(TLTMODL)       X
               ACCESSMETHOD(TLTACMD) CREATESESS(TLTCRTS)               X
               ACQSTATUS(TLTAQST) SERVSTATUS(TLTSVST)                  X
               ATISTATUS(TLTATIST) TTISTATUS(TLTTTIST)                 X
               PAGESTATUS(TLTPGST) SCREENHEIGHT(TLTSCHT)               X
               SCREENWIDTH(TLTSCWT) GCHARS(TLTGCHR) GCODES(TLTGCDS)
         ST    WRKREG1,TLTUAREA    SET USER AREA LENGTH
         BE    GDRTN               RETURN TO CALLER
GDRTN    MVI   TLRETCD,X'00'       SET GOOD RETURN CODE
         B     ENDPGM              AND RETURN
BDRTN    MVI   TLRETCD,X'01'       SET BAD RETURN CODE
         B     ENDPGM              AND RETURN
TRNABND  DS    0H
         EXEC CICS ABEND ABCODE('SP46')
ENDPGM   DS    0H
         CLI   SAVFREE,X'00'       IS SAVED FREE BYTE BINARY ZEROES
         BE    TSTRECV             YES, DON'T DO FREE
         EXEC CICS FREE
TSTRECV  CLI   SAVRECV,X'00'       IS SAVED RECV BYTE BINARY ZEROES
         BNE   NOSEND              NO, CAN'T DO SEND
         LH    WRKREG1,DTPLNTH     LOAD DTP LENGTH
         SH    WRKREG1,=H'4'       GET RID OF TRAN ID LENGTH
         STH   WRKREG1,DTPLNTH     .
         EXEC CICS SEND FROM(0(COMPTR)) LENGTH(DTPLNTH) WAIT LAST
NOSEND   DS    0H
         EXEC CICS RETURN
         END


SP050F COPY BOOK - ASSEMBLER COMMUNICATIONS AREA

SP050F   DS    0F                  ALIGNMENT
TLPRMS   DS    0CL93               COMMUNICATIONS AREA FOR SP022A
TLTRMID  DS    CL4                 TERMINAL IDENTIFICATION
TLTNTNM  DS    CL8                 TERMINAL NETWORK NAME
TLTMDNM  DS    CL8                 TERMINAL MODE NAME
TLTRNID  DS    CL4                 TERMINAL TRANSACTION
TLTPRTY  DS    F                   TERMINAL PRIORITY
TLTUAREA DS    F                   TERMINAL USER AREA ADDRESS (31 BIT)
TLTUALN  DS    H                   TERMINAL USER AREA LENGTH
TLTOPID  DS    CL3                 TERMINAL OPERATOR ID
TLTUSID  DS    CL8                 TERMINAL USER ID
TLTDVC   DS    F                   TERMINAL TERMINAL OR SESSION TYPE
TLTMODL  DS    H                   TERMINAL MODEL
TLTACMD  DS    F                   TERMINAL ACCESS METHOD
TLTCRTS  DS    F                   TERMINAL AUTOMATIC ACQUIRE
TLTAQST  DS    F                   TERMINAL SESSION STATUS
TLTSVST  DS    F                   TERMINAL SERVICE STATUS
TLTATIST DS    F                   TERMINAL ATI STATUS
TLTTTIST DS    F                   TERMINAL TTI STATUS
TLTPGST  DS    F                   TERMINAL PAGE STATUS
TLTSCHT  DS    H                   TERMINAL SCREEN HEIGHT
TLTSCWT  DS    H                   TERMINAL SCREEN WIDTH
TLTGCHR  DS    H                   TERMINAL GRAPHIC CHARACTER SET ID
TLTGCDS  DS    H                   TERMINAL CODE PAGE GLOBAL IDENTIFIER
TLRETCD  DS    XL1                 RETURN CODE FROM SP022A
TLPLEN   EQU   *-TLPRMS            LENGTH OF COMMUNICATIONS AREA


SP070F COPY BOOK - COBOL COMMUNICATIONS AREA

 01   SP022A-COMMUNICATIONS-AREA.
      05   TL-TERMINAL-ID             PIC X(4).
      05   TL-NETWORK-NAME            PIC X(8).
      05   TL-MODE-NAME               PIC X(8).
      05   TL-TRANSACTION-ID          PIC X(4).
      05   TL-TERMINAL-PRIORITY       PIC S9(8) COMP SYNC.
      05   TL-USER-AREA-ADDRESS       PIC S9(8) COMP SYNC.
      05   TL-USER-AREA-LENGTH        PIC S9(4) COMP SYNC.
      05   TL-OPERATOR-ID             PIC X(3).
      05   TL-USER-ID                 PIC X(8).
      05   TL-SESSION-TYPE            PIC S9(8) COMP SYNC.
      05   TL-MODEL                   PIC S9(4) COMP SYNC.
      05   TL-ACCESS-METHOD           PIC S9(8) COMP SYNC.
      05   TL-AUTOMATIC-ACQUIRE       PIC S9(8) COMP SYNC.
      05   TL-SESSION-STATUS          PIC S9(8) COMP SYNC.
      05   TL-SERVICE-STATUS          PIC S9(8) COMP SYNC.
      05   TL-ATI-STATUS              PIC S9(8) COMP SYNC.
      05   TL-TTI-STATUS              PIC S9(8) COMP SYNC.
      05   TL-PAGE-STATUS             PIC S9(8) COMP SYNC.
      05   TL-SCREEN-HEIGHT           PIC S9(4) COMP SYNC.
      05   TL-SCREEN-WIDTH            PIC S9(4) COMP SYNC.
      05   TL-GRAPHIC-CHARSET         PIC S9(4) COMP SYNC.
      05   TL-CODE-PAGE-GI            PIC S9(4) COMP SYNC.
      05   TL-RETURN-CODE             PIC X(1).
      05   TL-SP022A-CAL              PIC S9(4) COMP VALUE +93.


William J Myers
Technical Specialist
Ameritrust Co NA (USA)

