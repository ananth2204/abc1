Informing users of availability - local and remote

The June 1988 issue of CICS Update contained an article about
how to inform local users that CICS is down (Informing users
of CICS availability by Jesse Joyner). Since we have local and
remote terminals we needed to send messages to both kinds.
From the program presented in that issue and some examples
from pages 367 to 386 of the BTAM-ES Programming manual
(SC38-0293-01), I developed two programs: CICLOCAL to
send messages to local terminals and CICREMOT to send
messages to remote terminals.

The two programs are running under VSE/SP 3.2.0, CICS 1.7,
and BTAM using local and remote 3270 terminals with a 3705
controller for the lines.


THE SOURCE CODE FOR CICLOCAL

CICLOCAL CSECT
REGZERO  EQU   0
REG2     EQU   2
LCBREG   EQU   3
WORKREG  EQU   3
MSGADDR  EQU   4
MSGLEN   EQU   5
RLNREG   EQU   6
DTFREG   EQU   7
FMTREG   EQU   8
DSPTABRG EQU   9
LNKREG   EQU   10
BASEREG  EQU   11
BASEREG2 EQU   12
PTRTAB   EQU   13
RTNCDRG  EQU   15
REG15    EQU   15
ZERO     EQU   0
ONE      EQU   1
PTR3270  EQU   1
TWO      EQU   2
FOUR     EQU   4
EIGHT    EQU   8
PTRFLAG  EQU   16
THRTY9   EQU   39
DTFLEN   EQU   40
LCBLEN   EQU   72
FOURTY2  EQU   X'42'
SIXTY4   EQU   X'64'
PA1      EQU   X'6C'
CLEAR    EQU   X'6D'
PA2      EQU   X'6E'
ENTER    EQU   X'7D'
SEVENF   EQU   X'7F'
FOURTY1  EQU   X'41'
LASTLCB  EQU   X'80'
PRINTER  EQU   X'80'
PRNTWCC  EQU   X'D8'
HEXFF    EQU   X'FF'
BASE     BALR  BASEREG,0
         USING *,BASEREG,BASEREG2
         LR    BASEREG2,BASEREG
         AH    BASEREG2,H4096
* INITIAL PROCESSING
         LA    R3,FUNCTION
GETFUNC  BAL   R8,CONSOLE
         SR    R4,R4
         LM    R5,R7,FUNCCONS
FUNCLOOP CLC   INPUT(2),0(R5)
         BE    FUNCFD
         BXLE  R5,R6,FUNCLOOP
         LA    R3,ERROR2
         B     GETFUNC
FUNCFD   ICM   R4,7,2(R5)
         BR    R4
FUNCTBL  EQU   *
FUNCENTR DC    CL2'DN'
FUNCADR  DC    AL3(DOWN)
         DC    CL2'UP',AL3(UP),CL2'BR',AL3(BRDCAST)
FUNCEND  EQU   *-L'FUNCENTR-L'FUNCADR
FUNCCONS DC    A(FUNCTBL,L'FUNCENTR+L'FUNCADR,FUNCEND)
UP       EQU   *
         MVC   DOWNMSG(57),UPMSG
         B     DISPLAY
BRDCAST  EQU   *
         LA    R10,DOWNMSG
         XC    SCRNADR1,SCRNADR1
         XC    SCRNADR2,SCRNADR2
         XC    SCRNADR3,SCRNADR3
         XC    SCRNADR4,SCRNADR4
         XC    DOWNMSG(57),DOWNMSG
         LA    R2,4(0)
READLOOP EQU   *
         STCM  R10,7,R+1
READCON  EXCP READ
         WAIT READ
         TM   READ+4,X'01'
         BNZ  READCON
         CLC  0(2,R10),=C'/*'
         BNE  NOASTRK
         XC   0(2,R10),0(R10)
         B    DISPLAY
NOASTRK  EQU  *
         LA   R10,80(R10)
         BCT  R2,READLOOP
         B    DISPLAY
DOWN     EQU  *
         LA   R3,REASON
         BAL  R8,CONSOLE
         LM   R3,R5,KEYCONS
KEYLOOP  EQU  *
         CLC  INPUT(2),0(R3)
         BE   KEYMATCH
         BXLE R3,R4,KEYLOOP
         MVC  FREEMSG(64),INPUT+2
         B    DISPLAY
KEYTBL   EQU  *
KEYENTRY DC   CL30'EDEND OF PROCESSING DAY      '
         DC   CL30'SMSCHEDULED SYSTEM MAINTENANCE'
         DC   CL30'EMEQUIPMENT FAILURE'
         DC   CL30'PFPOWER FAILURE'
KEYEND   EQU  *-L'KEYENTRY
KEYCONS  DC   A(KEYTBL,L'KEYENTRY,KEYEND)
KEYMATCH EQU  *
         MVC  FREEMSG(28),2(R3)
DISPLAY  EQU  *
         GETIME STANDARD
         ST   R1,DWORD
         PACK TIMEOUT+8(1),DWORD+3(1)
         OI   TIMEOUT+8,X'F0'
         MVN  TIMEOUT+7(1),DWORD+2
         OI   TIMEOUT+7,X'F0'
         PACK TIMEOUT+5(1),DWORD+2(1)
         OI   TIMEOUT+5,X'F0'
         MVN  TIMEOUT+4(1),DWORD+1
         OI   TIMEOUT+4,X'F0'
         PACK TIMEOUT+2(1),DWORD+1(1)
         OI   TIMEOUT+2,X'F0'
         MVN  TIMEOUT+1(1),DWORD
         OI   TIMEOUT+1,X'F0'
         MVI  TIMEOUT+3,C':'
         MVI  TIMEOUT+6,C'.'
         B    BTAM
CONSOLE  EQU  *
         STCM R3,7,W+1
         EXCP WRITE
         WAIT WRITE
EXCPREAD EQU  *
         EXCP READ
         WAIT READ
         TM   READ+4,X'01'
         BNZ  EXCPREAD
         OI   INPUT,X'40'
         OI   INPUT+1,X'40'
         CLC  INPUT,=CL45' '
         BNER R8
         LA   R3,ERROR
         B    CONSOLE
* BTAM PROCESSING
BTAM     EQU  *
         OPEN  DTFBTL
         SR    RLNREG,RLNREG
         L     DTFREG,VDTF
         LA    LCBREG,DTFLEN(DTFREG)
         LA    PTRTAB,PTRRLNTB
         LA    DSPTABRG,DSPTAB
CHECKPTR EQU   *
         TM    PTRFLAG(LCBREG),PTR3270
         BNO   INITIAL
         OI    FLAGS,PRINTER
         ST    PTRTAB,CURRPTR
         STC   RLNREG,ZERO(PTRTAB)
         LA    PTRTAB,ONE(PTRTAB)
LCBUPDT  EQU   *
         LA    RLNREG,ONE(RLNREG)
         TM    PTRFLAG(LCBREG),LASTLCB
         BO    INITIAL
         LA    LCBREG,LCBLEN(LCBREG)
         B     CHECKPTR
INITIAL  EQU   *
         LA    MSGADDR,SCREEN
         LA    MSGLEN,SCRNLEN
         BAL   LNKREG,WRITETS
         BAL   LNKREG,RETCODE
         BAL   LNKREG,WAITD
         B     LCBUPDT
RETCODE  EQU   *
         B     RTNCDTAB(RTNCDRG)
RTNCDTAB EQU   *
         B     RTNCD0
         B     RTNCD4
         B     RTNCD8
         B     RTNCDC
         B     RTNCD10
         B     RTNCD14
         B     RTNCD18
         B     RTNCD1C
         B     RTNCD20
         B     RTNCD24
         B     RTNCD28
         B     RTNCD2C
         B     RTNCD30
         B     RTNCD34
         BC    0,*
         BC    0,*
         B     RTNCD40
RTNCD0   EQU   *
         MVC   CODERET,=CL2'00'
         BR    LNKREG
RTNCD4   EQU   *
         MVC   CODERET,=CL2'04'
         S     LNKREG,EIGHT8
         BR    LNKREG
RTNCD8   EQU   *
         MVC   CODERET,=CL2'08'
         B     CLOSE
RTNCDC   EQU   *
         MVC   CODERET,=CL2'12'
         B     ABNORMAL
RTNCD10  EQU   *
         MVC   CODERET,=CL2'10'
         B     CLOSE
RTNCD14  EQU   *
         MVC   CODERET,=CL2'14'
         B     ABNORMAL
RTNCD18  EQU   *
         MVC   CODERET,=CL2'18'
         B     ABNORMAL
RTNCD1C  EQU   *
         MVC   CODERET,=CL2'28'
         B     ABNORMAL
RTNCD20  EQU   *
         MVC   CODERET,=CL2'20'
         B     ABNORMAL
RTNCD24  EQU   *
         MVC   CODERET,=CL2'24'
         B     ABNORMAL
RTNCD28  EQU   *
         MVC   CODERET,=CL2'28'
         S     LNKREG,EIGHT8
         BR    LNKREG
RTNCD2C  EQU   *
         MVC   CODERET,=CL2'44'
         B     ABNORMAL
RTNCD30  EQU   *
         MVC   CODERET,=CL2'30'
         S     LNKREG,EIGHT8
         BR    LNKREG
RTNCD34  EQU   *
         MVC   CODERET,=CL2'34'
         B     CLOSE
WAITD    EQU   *
         WAIT  ECB=DECBD
CHK7FCC  EQU   *
         CLI   DECBD,SEVENF
         BNE   CHK41CC
         BR    LNKREG
CHK41CC  EQU   *
         CLI   DECBD,FOURTY1
         BNE   CHK64CC
         BR    LNKREG
CHK64CC  EQU   *
         CLI   DECBD,SIXTY4
         BNE   CHK42CC
         SR    FMTREG,FMTREG
         IC    RLNREG,DECBD+THIRTY9
         IC    FMTREG,0(RLNREG,DSPTABRG)
         B     WTFMTX(FMTREG)
WTFMTX   EQU   *
         B     WTFMT0
         B     ABNORMAL
WTFMT0   EQU   *
         LA    MSGADDR,SCREEN
         LA    MSGLEN,SCRNLEN
WTDVUNRL EQU   *
         WRITE DECBD,TS,DTFBTL,(MSGADDR),(MSGLEN),,(RLNREG),MF=E
         BNZ   ABNORMAL
         WAIT  ECB=DECBD
         B     CHK7FCC
CHK42CC  EQU   *
         CLI   DECBD,FOURTY2
         BNE   ABNORMAL
         TWAIT (REG2),TERMTST,ECBLIST=DECBADDR
         B     CHK7FCC
CLOSE    EQU   *
         BTRD  DTFBTL
         CLOSE DTFBTL
         EOJ
RTNCD40  EQU   *
ABNORMAL EQU   *
         STM   0,15,REGSAVE
         L     WORKREG,=A(IJLBTEND)
         PDUMP BASE,(WORKREG)
         EOJ
WRITETS  EQU   *
         WRITE DECBD,TS,DTFBTL,(MSGADDR),(MSGLEN),,(RLNREG),MF=E
         BR    LNKREG
         DS    0D
DWORD    DC    D'0'
WRITE    CCB   SYSLOG,W
W        CCW   X'09',*,0,34
READ     CCB   SYSLOG,R
R        CCW   X'0A',INPUT,X'20',64
INPUT    DC    CL64' '
H4096    DC    H'4096'
         DC    CL7'CODERET'
CODERET  DC    CL2'00'
REASON   DC    CL34'ENTER REASON FOR SYSTEM DOWN      '
FUNCTION DC    CL34'ENTER FUNCTION - (DN, UP, BR)  ?  '
ERROR2   DC    CL34'** INVALID FUNCTION **'
ERROR    DC    CL34'** NO ENTRY FOUND **'
UPMSG    DS    0CL57
         DC    CL47'THE SYSTEM WILL BE OPERATIVE IN APROXIMATELY   '
         DC    CL11'TEN MINUTES'
REGSAVE  DC    16F'0'
CURRPTR  DC    F'0'
TWO2     DC    F'2'
EIGHT8   DC    F'8'
TWOTHO   DC    F'2000'
INAREA   DC    500F'0'
DECBADDR DC    X'80'
         DC    AL3(DECBD)
VDTF     DC    V(DTFBTL)
         DS    0F
DSPTAB   DC    XL32'0'
RLNREAD  DC    X'00'
FLAGS    DC    X'00'
PTRRLNTB DC    X'FFFFFFFFFFFFFFFFF'
         DC    X'FFFFFFFFFFFFFFFFF'
         DC    X'FFFFFFFFFFFFFFFFF'
         DC    X'FFFFFFFFFFFFFFFFF'
MAXRLN   DC    H'0'
SCREEN   EQU   *
         DC    X'40'   WCC
         DC    X'1140501DC8'
         DC    CL44'DDDDDDD      OOOOOO   WW       WW   NN      NN'
         DC    X'11C1601D'
         DC    CL44'DD   DD     OO    OO  WW       WW   NN NN    NN'
         DC    X'11C2F01D'
         DC    CL44'DD    DD    OO    OO  WW       WW   NN  NN   NN'
         DC    X'11C4401D'
         DC    CL44'DD    DD    OO    OO  WW       WW   NN   NN  NN'
         DC    X'11C5501D'
         DC    CL44'DD    DD    OO    OO  WW       WW   NN    NN NN'
         DC    X'11C6601D'
         DC    CL44'DD    DD    OO    OO  WW       WW   NN    NN NN'
         DC    X'11C7F01D'
         DC    CL44'DD    DD    OO    OO  WW   WW  WW   NN     NNNN'
         DC    X'11C9401D'
         DC    CL44'DD   DD     OO    OO   WW  WW  WW   NN       NN'
         DC    X'114A501DC8'
         DC    CL44'DDDDDDD      OOOOOO       WWWW      NN       NN'
         DC    X'114CE3'
DOWNSMSG DC    CL44'THE SYSTEM IS INOPERATIVE FOR THE FOLLOWING'
         DC    CL13'REASON:'
SCRNADR1 DC    X'1150D3'
FREEMSG  DC    XL64'0'
SCRNADR2 DC    X'11D1E3'
         DC    XL64'0'
SCRNADR3 DC    X'11D2F3'
         DC    XL64'0'
SCRNADR4 DC    X'11D4C3'
         DC    XL64'0'
         DC    X'115A50'
         DC    CL18'TIME OF MESSAGE IS'
TIMEOUT  DC    CL9' HH.MM.SS'
         DC    X'115CF0'
         DC    CL43'* * * * THANK YOU FOR YOUR PATIENCE * * * *'
SCRNLEN  EQU   *-SCREEN
         LTORG
* THE VALUE 32 MEANS 32 TERMINALS
* MUST BE ALTERED IF YOU USE ANOTHER NUMBER OF TERMINALS
RMSRTAB  RMSRTAB 32
* SYS100 TO SYS131 - 1 SYS LOGICAL BY TERMINAL
* MUST BE ALTERED TO REFLECT DIFFERENT NUMBER OF TERMINALS
* MORE DETAILS, PLEASE REFER TO :
"BTAM-ES PROGRAMMING" MANUAL (SC38-0293-01), PAGES 367 TO 386.
NOTE : REMEMBER CICS TCT MAINTENANCE ? IS THE SAME THING ...
DTFBTL   DTFBT LINELST=(100,101,102,103,104,105,106,107,108,109,110,   X
               111,112,113,114,115,116,117,118,119,120,121,122,123,    X
               124,125,126,127,128,129,130,131),                       X
               CU=3272,DEVICE=3277,TERMTST=YES,MODNAME=L3270MOD,       X
               LERBADR=RMSRTAB
         READ  DECBD,TI,DTFBTL,MF=L
         LTORG
L3270MOD BTMOD L3277=YES,TST3277=YES,RMSR=YES
         END


EXECUTION JCL FOR CICLOCAL

// JOB CICLOCAL
// ASSGN SYS100,4A0
// ASSGN SYS101,4A1

        etc

// ASSGN.SYS131,4BF
// EXEC CICLOCAL,SIZE=128K
/*
/&


THE SOURCE CODE FOR CICREMOT

CICREMOT CSECT
REGZERO  EQU   0
REG2     EQU   2
WORKREG  EQU   3
MSGADDR  EQU   4
MSGLEN   EQU   5
SELREG   EQU   6
FMTREG   EQU   8
DSPTABRG EQU   9
LNKREG   EQU   10
BASEREG  EQU   11
BASEREG2 EQU   12
PTRTAB   EQU   13
RTNCDRG  EQU   15
REG15    EQU   15
ZERO     EQU   0
ONE      EQU   1
TWO      EQU   2
THREE    EQU   3
FOUR     EQU   4
FIVE     EQU   5
SIX      EQU   6
TP06     EQU   6
SEVEN    EQU   7
EIGHT    EQU   8
TP11     EQU   X'11'
SIXTN    EQU   16
TP20     EQU   X'20'
TIMOUT   EQU   X'01'
TWENTY4  EQU   24
TWENTY8  EQU   28
FIFTY4   EQU   54
TWOTHO   EQU   255
RVIRSP   EQU   X'02'
SSMSG    EQU   X'10'
EOTRSPTX EQU   X'00'
EOTRCVD  EQU   X'40'
FOURTY1  EQU   X'41'
FOURTY2  EQU   X'42'
SIXTY1   EQU   X'61'
PA1      EQU   X'6C'
CLEAR    EQU   X'6D'
PA2      EQU   X'6E'
ENTER    EQU   X'7D'
SEVENF   EQU   X'7F'
LAST     EQU   X'89'
TWO53    EQU   X'FD'
BASE     BALR  BASEREG,0
         USING *,BASEREG,BASEREG2
         LR    BASEREG2,BASEREG
         AH    BASEREG2,H4096
* INITIAL PROCESSING
         LA    R3,FUNCTION
GETFUNC  BAL   R8,CONSOLE
         SR    R4,R4
         LM    R5,R7,FUNCCONS
FUNCLOOP CLC   INPUT(2),0(R5)
         BE    FUNCFD
         BXLE  R5,R6,FUNCLOOP
         LA    R3,ERROR2
         B     GETFUNC
FUNCFD   ICM   R4,7,2(R5)
         BR    R4
FUNCTBL  EQU   *
FUNCENTR DC    CL2'DN'
FUNCADR  DC    AL3(DOWN)
         DC    CL2'UP',AL3(UP),CL2'BR',AL3(BRDCAST)
FUNCEND  EQU   *-L'FUNCENTR-L'FUNCADR
FUNCCONS DC    A(FUNCTBL,L'FUNCENTR+L'FUNCADR,FUNCEND)
UP       EQU   *
         MVC   DOWNMSG(57),UPMSG
         B     DISPLAY
BRDCAST  EQU   *
         LA    R10,DOWNMSG
         XC    SCRNADR1,SCRNADR1
         XC    SCRNADR2,SCRNADR2
         XC    SCRNADR3,SCRNADR3
         XC    SCRNADR4,SCRNADR4
         XC    DOWNMSG(57),DOWNMSG
         LA    R2,4(0)
READLOOP EQU   *
         STCM  R10,7,R+1
READCON  EXCP READ
         WAIT READ
         TM   READ+4,X'01'
         BNZ  READCON
         CLC  0(2,R10),=C'/*'
         BNE  NOASTRK
         XC   0(2,R10),0(R10)
         B    DISPLAY
NOASTRK  EQU  *
         LA   R10,80(R10)
         BCT  R2,READLOOP
         B    DISPLAY
DOWN     EQU  *
         LA   R3,REASON
         BAL  R8,CONSOLE
         LM   R3,R5,KEYCONS
KEYLOOP  EQU  *
         CLC  INPUT(2),0(R3)
         BE   KEYMATCH
         BXLE R3,R4,KEYLOOP
         MVC  FREEMSG(64),INPUT+2
         B    DISPLAY
KEYTBL   EQU  *
KEYENTRY DC   CL30'EDEND OF PROCESSING DAY      '
         DC   CL30'SMSCHEDULED SYSTEM MAINTENANCE'
         DC   CL30'EMEQUIPMENT FAILURE'
         DC   CL30'PFPOWER FAILURE'
KEYEND   EQU  *-L'KEYENTRY
KEYCONS  DC   A(KEYTBL,L'KEYENTRY,KEYEND)
KEYMATCH EQU  *
         MVC  FREEMSG(28),2(R3)
DISPLAY  EQU  *
         GETIME STANDARD
         ST   R1,DWORD
         PACK TIMEOUT+8(1),DWORD+3(1)
         OI   TIMEOUT+8,X'F0'
         MVN  TIMEOUT+7(1),DWORD+2
         OI   TIMEOUT+7,X'F0'
         PACK TIMEOUT+5(1),DWORD+2(1)
         OI   TIMEOUT+5,X'F0'
         MVN  TIMEOUT+4(1),DWORD+1
         OI   TIMEOUT+4,X'F0'
         PACK TIMEOUT+2(1),DWORD+1(1)
         OI   TIMEOUT+2,X'F0'
         MVN  TIMEOUT+1(1),DWORD
         OI   TIMEOUT+1,X'F0'
         MVI  TIMEOUT+3,C':'
         MVI  TIMEOUT+6,C'.'
         B    BTAM
CONSOLE  EQU  *
         STCM R3,7,W+1
         EXCP WRITE
         WAIT WRITE
EXCPREAD EQU  *
         EXCP READ
         WAIT READ
         TM   READ+4,X'01'
         BNZ  EXCPREAD
         OI   INPUT,X'40'
         OI   INPUT+1,X'40'
         CLC  INPUT,=CL45' '
         BNER R8
         LA   R3,ERROR
         B    CONSOLE
* BTAM PROCESSING
BTAM     EQU  *
         OPEN  DTFBTR
         LA    DSPTABRG,DSPTAB
         LA    SELREG,SELDSPLY
INITIAL  EQU   *
         LA    MSGADDR,SCREEN
         LA    MSGLEN,SCRNLEN
         BAL   LNKREG,WRITETI
         BAL   LNKREG,RETCODE
         LA    SELREG,FIVE(SELREG)
         TM    ZERO(SELREG),LAST
         BO    CLOSE
         LA    SELREG,ONE(SELREG)
         B     INITIAL
RETCODE  EQU   *
         B     RTNCDTAB(RTNCDRG)
RTNCDTAB EQU   *
         B     RTNCD0
         B     RTNCD4
         B     RTNCD8
         B     RTNCDC
         B     RTNCD10
         B     RTNCD14
         B     RTNCD18
         B     RTNCD1C
         B     RTNCD20
         B     RTNCD24
         BC    0,*
         BC    0,*
         BC    0,*
         BC    0,*
         BC    0,*
         BC    0,*
         B     RTNCD40
RTNCD0   EQU   *
         MVC   CODERET,=CL2'00'
         BR    LNKREG
RTNCD4   EQU   *
         MVC   CODERET,=CL2'04'
         S     LNKREG,EIGHT8
         BR    LNKREG
RTNCD8   EQU   *
         MVC   CODERET,=CL2'08'
         B     ABNORMAL
RTNCDC   EQU   *
         MVC   CODERET,=CL2'12'
         B     ABNORMAL
RTNCD10  EQU   *
         MVC   CODERET,=CL2'10'
         B     CLOSE
RTNCD14  EQU   *
         MVC   CODERET,=CL2'14'
         B     ABNORMAL
RTNCD18  EQU   *
         MVC   CODERET,=CL2'18'
         B     ABNORMAL
RTNCD1C  EQU   *
         MVC   CODERET,=CL2'28'
         B     ABNORMAL
RTNCD20  EQU   *
         MVC   CODERET,=CL2'20'
         B     ABNORMAL
RTNCD24  EQU   *
         MVC   CODERET,=CL2'24'
         B     ABNORMAL
CLOSE    EQU   *
         CLOSE DTFBTR
         EOJ
RTNCD40  EQU   *
         MVC   CODERET,=CL2'40'
ABNORMAL EQU   *
         STM   0,15,REGSAVE
         L     WORKREG,=A(IJLBTEND)
         PDUMP BASE,(WORKREG)
         EOJ
WRITETI  EQU   *
         WRITE DECBD,TS,DTFBTR,(MSGADDR),(MSGLEN),(SELREG),0,MF=E
         BR    LNKREG
* THE CURRENT MACRO OPERANDS ARE FOR TWO REMOTE 3270 DISPLAYS:
SELDSPLY DFTRMLST OPENLST,(606040402D,6060C1C12D)
* DISPLAY GENERAL POLLING ADDRESS
POLDSPLY DFTRMLST AUTOWLST,3732,40407F7F2D
         DS    0D
DWORD    DC    D'0'
WRITE    CCB   SYSLOG,W
W        CCW   X'09',*,0,34
READ     CCB   SYSLOG,R
R        CCW   X'0A',INPUT,X'20',64
INPUT    DC    CL64' '
H4096    DC    H'4096'
         DC    CL7'CODERET'
CODERET  DC    CL2'00'
REASON   DC    CL34'ENTER REASON FOR SYSTEM DOWN      '
FUNCTION DC    CL34'ENTER FUNCTION - (DN, UP, BR)  ?  '
ERROR2   DC    CL34'** INVALID FUNCTION **'
ERROR    DC    CL34'** NO ENTRY FOUND **'
UPMSG    DS    0CL57
         DC    CL47'THE SYSTEM WILL BE OPERATIVE IN APROXIMATELY   '
         DC    CL11'TEN MINUTES'
REGSAVE  DC    16F'0'
EIGHT8   DC    F'8'
DSPTAB   DC    XL32'0'
TWELVE   DC    F'12'
INDEX    DC    X'00'
CUDVSAVE DC    X'0000'
SELSAVE  DC    F'0'
NOPRNTR  DC    X'0000000000'
INAREA   DC    500F'0'
INAREAL  EQU   *-INAREA
         DS    0F
DECBADDR DC    X'80'
         DC    AL3(DECBD)
SCREEN   EQU   *
         DC    X'0227F5'    STX,ESC,E/W
         DC    X'40'   WCC
         DC    X'1140501DC8'
         DC    CL44'DDDDDDD     OOOOOO    WW       WW   NN      NN'
         DC    X'11C1601D'
         DC    CL44'DD   DD    OO    OO   WW       WW   NN NN    NN'
         DC    X'11C2F01D'
         DC    CL44'DD    DD   OO    OO   WW       WW   NN  NN   NN'
         DC    X'11C4401D'
         DC    CL44'DD    DD   OO    OO   WW       WW   NN   NN  NN'
         DC    X'11C5501D'
         DC    CL44'DD    DD   OO    OO   WW       WW   NN    NN NN'
         DC    X'11C6601D'
         DC    CL44'DD    DD   OO    OO   WW       WW   NN    NN NN'
         DC    X'11C7F01D'
         DC    CL44'DD    DD   OO    OO   WW   WW  WW   NN     NNNN'
         DC    X'11C9401D'
         DC    CL44'DD   DD    OO    OO    WW  WW  WW   NN       NN'
         DC    X'114A501DC8'
         DC    CL44'DDDDDDD     OOOOOO        WWWW      NN       NN'
         DC    X'114CE3'
DOWNMSG  DC    CL44'THE SYSTEM IS INOPERATIVE FOR THE FOLLOWING'
         DC    CL13'REASON:'
SCRNADR1 DC    X'1150D3'
FREEMSG  DC    XL64'0'
SCRNADR2 DC    X'11D1E3'
         DC    XL64'0'
SCRNADR3 DC    X'11D2F3'
         DC    XL64'0'
SCRNADR4 DC    X'11D4C3'
         DC    XL64'0'
         DC    X'115A50'
         DC    CL18'TIME OF MESSAGE IS'
TIMEOUT  DC    CL9' HH.MM.SS'
         DC    X'115CF0'
         DC    CL43'* * * * THANK YOU FOR YOUR PATIENCE * * * *'
         DC    X'03'      ETX
SCRNLEN  EQU   *-SCREEN
         LTORG
* 1 IN THE RMSRTAB MACRO MEANS 1 LINE
RMSRTAB  RMSRTAB 1
* SYS005 MUST BE ASSIGNED TO A LINE *
* MORE DETAILS, PLEASE REFER TO :
"BTAM-ES PROGRAMMING" MANUAL (SC38-0293-01), PAGES 367 TO 386.
NOTE : REMEMBER CICS TCT MAINTENANCE ? IS THE SAME THING ...
DTFBTR   DTFBT LINELST=(005),SWITCH=NO,CU=2703,DEVICE=BSC3,CONFIG=MPT, X
               MODELST=(0),CLTCHAR=EBCDIC,MODNAME=R3270MOD,            X
               TERMTST=YES,LERBADR=RMSRTAB
         READ  DECBD,TI,DTFBTR,MF=L
         LTORG
R3270MOD BTMOD BSCS=YES,BSCMPT=YES,BSCTEST=YES,DECBEXT=YES,RMSR=YES
         END


EXECUTION JCL FOR CICREMOT

// JOB CICREMOT
// ASSGN SYS100,53F    ASSIGN FOR LINE ON A 3705 CONTROLLER
// EXEC CICREMOT,SIZE=128K
/*
/&


Germinaro Jorge Machado (Brazil)

