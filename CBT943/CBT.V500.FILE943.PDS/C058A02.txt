CICS/DOS 1.7 file availability

In release 1.7 of CICS/DOS, dataset opening and closing has
been changed.  Previously, if a file was defined in the FCT as
OPEN=DEFERRED, when CICS came up the file was
CLOSED and unavailable to transactions until the file was
explicitly opened by a CEMT command.  This allowed CICS
to be brought up while batch processing was still being done.

CICS/DOS 1.7 now has some 'enhancements' that allow files
to open dynamically whenever they are referenced by a
transaction.  The availability or unavailability of a dataset is
determined by whether it is enabled, unenabled (a new state),
or disabled, and not by whether it is opened or closed.  While
this is of value in certain instances, it can cause problems when
the following sequence of events occurs:

1      CICS is brought down normally - closing the files.

2      A batch job is started that accesses the files.

3      CICS is brought up while the batch job is still
running!

When CICS is brought down, the files are set to ENABLED,
CLOSED, regardless of the FILSTAT parameter in the FCT.
When CICS is brought back up (assuming a WARM start is
used) the files are still ENABLED,CLOSED.  The only way to
avoid this situation is to close the files with a CEMT command
before CICS is brought down.  While this can certainly be
done, either manually or by one of the products that allows
CEMT commands to be issued from batch, it depends upon
human intervention, which is imperfect at best.

To protect against data corruption, the following program can
be run automatically at CICS shutdown by including it in the
DFHPLTSD list.  This program initiates a scan of all entries in
the FCT, tests the dataset name, and issues a SET
DATASET(...) CLOSED command for all non-CICS files.
This ensures that when CICS is brought back up, the files will
be in DISABLED, CLOSED status, and will remain closed
until explicitly opened by a CEMT command.  The easiest way
to do this is to run a batch job that issues CEMT commands for
a specific application's files.


DFHPLTSD EXAMPLE

         TITLE 'DFHPLTSD - SHUT DOWN PLT FOR CICS'
         PUNCH ' CATALOG DFHPLTSD.OBJ REP=YES'
         DFHPLT TYPE=INITIAL,SUFFIX=SD
*---------------------------------------------------------------------*
*  NOTE NOTE NOTE -- THE NAME OF THIS MODULE MUST BE IN THE PPT !!!!!!!
*---------------------------------------------------------------------*
*            VSE/SP ENTRIES FOR PLTSD FOLLOW HERE                     *
*---------------------------------------------------------------------*
         COPY  IESZPLTS            COPY VSE/SP ENTRIES
*---------------------------------------------------------------------*
*      LOCAL ENTRIES TO BE EXECUTED DURING FIRST QUIESCE PHASE        *
*                SHOULD BE MADE AFTER THIS POINT                      *
*---------------------------------------------------------------------*
         DFHPLT TYPE=ENTRY,PROGRAM=FILECLS
         DFHPLT TYPE=ENTRY,PROGRAM=DFHDELIM
*---------------------------------------------------------------------*
*      LOCAL ENTRIES TO BE EXECUTED DURING SECOND QUIESCE PHASE       *
*                SHOULD BE MADE AFTER THIS POINT                      *
*---------------------------------------------------------------------*
         DFHPLT TYPE=FINAL
         END


FILECLS SOURCE CODE

*****************************************************************
 IDENTIFICATION DIVISION.
*****************************************************************
 PROGRAM-ID.    FILECLS.
*REMARKS.
*  THIS PROGRAM WILL CLOSE ALL DATA FILES IN CICS - NOTE
*  COMMENTS ABOUT FILES THAT WILL -NOT- BE CLOSED!!!
*****************************************************************
 ENVIRONMENT DIVISION.
*****************************************************************
 CONFIGURATION SECTION.
 SOURCE-COMPUTER.  IBM-4381.
 OBJECT-COMPUTER.  IBM-4381.
*****************************************************************
 DATA DIVISION.
*****************************************************************
 WORKING-STORAGE SECTION.
*---------------------------------------------------------------*
 01  DS-NAME.
     05  COL1-3.
         10  COL1-2  PIC XX.
         10  COL3    PIC X.
     05  FILLER      PIC X(4).
*****************************************************************
 PROCEDURE DIVISION.
*****************************************************************
 BEGIN.
     EXEC CICS HANDLE CONDITION
         END(ALLDONE)
         ILLOGIC(OH-OH) END-EXEC.
     EXEC CICS INQUIRE DATASET START END-EXEC.
 NEXT-ONE.
     EXEC CICS INQUIRE DATASET(DS-NAME) NEXT END-EXEC.
* DON'T CLOSE INWFILE OR DFHCSD FILES
     IF COL1-3 = 'DFH' OR 'INW'
         GO TO NEXT-ONE.
* DON'T CLOSE IUI FILES
     IF COL1-2 = 'IE'
         GO TO NEXT-ONE.
* Any other files that are NOT to be closed may be tested, also.
*    IF DS-NAME = 'thisone' OR 'thatone'
*        GO TO NEXT-ONE.
     EXEC CICS SET DATASET(DS-NAME) CLOSED END-EXEC.
     GO TO NEXT-ONE.
 ALLDONE.
     EXEC CICS INQUIRE DATASET END END-EXEC.
 OH-OH.
     EXEC CICS RETURN END-EXEC.
     STOP RUN.


Frank A Braman
Systems Programmer
St Johnsbury Trucking Co (USA)            c St Johnsbury Trucking Co 1990


