Controlling case translation with a user exit

Having read the article Changing case within CICS in the
February 1992 issue of CICS Update, I would like to put
forward our installation's approach to the problem.

Our approach is somewhat different in that the switch from and
to upper-case translation is achieved by a change of
transaction. We use global exit XZCIN, which is invoked after
a logical message is received and runs in the TOR, if one
exists. A table of transactions is scanned for a match and, if
one is found, upper-case translation is switched off: otherwise
it is switched on. Provision is also made for partial matching
by the use of the '*' character. The table at the end of the
program can easily be maintained as requirements change.
Unfortunately, it cannot be loaded via EXEC CICS as we are
not in the appropriate environment. The exit is invoked by an
enabling program at PLTPI time.

We have not experienced any performance problems from this
exit. The version given is for CICS 3.1 but it can easily be
reworked for earlier releases. There are two limitations:

1      Firstly, as I understand it, the fields in the TCTTE that are
      modified have not been guaranteed by IBM.

2      Secondly, the exit is invoked after the transaction has been
      entered, so if upper-case translation is on and the lower-
      case version of the transaction has not been defined in the
      PCT, the correct transaction will not be invoked. However,
      a subsequent attempt will be successful as, by this time,
      upper-case translation will have been turned on.

This brings me to one of the shortcomings of CICS. While
transactions can be given profiles which suppress upper-case
translation, this will only take effect if the user's TYPETERM
also has UCTRAN=NO. This means that all transactions must
be defined in upper and lower case. Finally, it is not possible to
use an EXEC CICS SET command to change upper-case
translation for CICS 3.1 as was suggested in the
aforementioned article.


PROGRAM TO ENABLE UPPER CASE TRANSLATE EXIT

AMXTCTXE CSECT
         EXEC  CICS ENABLE PROGRAM('AMXTCTX') EXIT('XZCIN') START
         END


UPPER CASE TRANSLATE EXIT

***  This exit flicks UCTRAN off for certain transactions and
*    back on otherwise. The shortcomings are that unguaranteed
*    fields in the TCTTE are modified and that the exit comes into
*    effect AFTER the translation has been entered, ie not
***  translated if UCTRAN is on.
*    MODULE NAME : AMXTCTX
*    BASE REGISTERS R3 - EXIT PARMS
*                   R4 - TCTTE
*                   R5 - TIOA
*                   R6 - WORK
***                 R7 - WORK
         DFHUEXIT TYPE=EP,ID=XTCIN
         DFHTCTZE CICSYST=YES
TCTTEAR  EQU   4
TIOABAR  EQU   5
***      T E R M I N A L   I / O   A R E A      ***
DFHTIOA  DSECT                     DUMMY SECTION - TERMINAL I/O AREA
         USING *,TIOABAR           USING REGISTER TIOABAR
         DS    H                   STORAGE ACCOUNTING
*                                  INFORMATION; STORAGE
*                                  CLASS=TERMINAL
TIOASAL  DS    H                   STORAGE ACCOUNTING - AREA
*                                  LENGTH
TIOASCA  DS    A                   CHAIN ADDRESS OF NEXT
*                                  TERMINAL STORAGE RELATED TO
*                                  THE SAME TASK
TIOATDL  DS    H                   TERMINAL DATA LENGTH
TIOAWCI  DS    B                   WRITE CONTROL INDICATOR
TIOACLCR DS    0B                  WCC OR CCC CHARACTER
TIOALAC  DS    B                   LINE ADDRESS CONTROL
TIOACAD  EQU   *-DFHTIOA           CONTROL AREA DISPLACEMENT
TIOADBA  EQU   *                   TERMINAL DATA BEGINNING ADDRESS
         COPY  DFHTRPTY
AMXTCTX  CSECT
         SAVE  (14,12)
         LR    R11,R15
         USING AMXTCTX,R11
         LA    R13,SAVE
         LR    R3,R1
         USING DFHUEPAR,R3
         L     R4,UEPTCTTE
         L     R5,UEPTIOA
CHECK    DS    0H
         LA    R6,NUMTRAN          R6 = # transactions
         LA    R7,TRANTAB          R7 = start of table
LOOPSTRT DS    0H
         CLI   0(R7),C'*'          Asterisk found ?
         BE    NEXT1               Yes - no check
         CLC   TCTTETC+0(1),0(R7)  1st char equal ?
         BNE   BUMP                Not the same - next entry
NEXT1    DS    0H
         CLI   1(R7),C'*'          Asterisk found ?
         BE    NEXT2               Yes - no check
         CLC   TCTTETC+1(1),1(R7)  2nd char equal ?
         BNE   BUMP                Not the same - next entry
NEXT2    DS    0H
         CLI   2(R7),C'*'          Asterisk found ?
         BE    NEXT3               Yes - no check
         CLC   TCTTETC+2(1),2(R7)  3rd char equal ?
         BNE   BUMP                Not the same - next entry
NEXT3    DS    0H
         CLI   3(R7),C'*'          Asterisk found ?
         BE    NEXT4               Yes - no check
         CLC   TCTTETC+3(1),3(R7)  4th char equal ?
         BNE   BUMP                Not the same - next entry
NEXT4    DS    0H
         B     UCTOFF              Match found set UCTRAN off
BUMP     DS    0H
         LA    R7,TRANLEN (R7)     Bump to next entry
         BCT   R6,LOOPSTRT         Back to start
UCTON    DS    0H
         OI    TCTTEFIB,TCTTEFTU
         OI    TCTEUCTB,X'01'
         B     RETURN
UCTOFF   DS    0H
         NI    TCTTEFIB,255-TCTTEFTU
         NI    TCTEUCTB,X'FE'
RETURN   DS    0H
         L     R13,UEPEPSA
         RETURN (14,12),RC=UERCNORM RESTORE REGS FOR RETURN
         DS    0F
TRANTAB  EQU   *                   START OF TAB
TRANLEN  EQU   4                   LENGTH OF ENTRY
NUMTRAN  EQU   (ENDTRAN-TRANTAB)/TRANLEN  # ENTRIES
*
*  An * in an entry means this position is not checked !!!
*
         DC    CL4'*PDT'
         DC    CL4'*pdt'
         DC    CL4'LMF*'
         DC    CL4'lmf*'
         DC    CL4'*LMF'
         DC    CL4'*lmf'

               etc

         DC    CL4'OSRB"
         DC    CL4'osrb'
         DC    CL4'OSRM'
         DC    CL4'osrm'
ENDTRAN  EQU   *
         LTORG
SAVE     DS    18F
         END


John Christopher Vousden
Software Specialist
American Express (UK)                        c American Express 1992

