Cold starting sequential terminals - a response

Referring to the article by Ferdinand Roters in issue 51
(February 1990) of CICS Update,  I would like to make the
following comments.

The definition of the restart dataset has changed in the last
upgrade from 1.6 to 1.7.  Things like that make me very
suspicious about interfering with CICS system files (ie using
undocumented information - in this case, the value of the key).

The problem when using sequential terminals is that you must
end the input stream with 'CSSF LOGOFF', otherwise the
whole stream will be rejected.  This requirement (which came
along with CICS 1.7) causes the TTI status of the terminal to
be turned off, so you can't initiate transactions from that
terminal any more.  This status will be maintained until the
next cold start of CICS unless it is explicitly turned on.

I would like to suggest a third solution: let a PLTPI program
reset the TTI status.  You can use the documented EXEC CICS
SET commands to do this (see the CICS Customization guide).
To make it work just put the following line in your PLTPI:

     DFHPLT TYPE=ENTRY,PROGRAM=CRLPINIT

and put this line in your PPT:

     DFHPPT TYPE=ENTRY,PROGRAM=CRPLINIT,PGMLANG=COBOL

The program has been tested and run in a VSE/SP 3.2.1 and
CICS 1.7 environment.

 IDENTIFICATION DIVISION.
 PROGRAM-ID. CRLPINIT.
****************************************************************
*                      C R L P I N I T                         *
*  FUNCTION: RESET STATUS OF SEQUENTIAL TERMINAL.              *
*  NOTE    : ONLY THE SERVSTATUS AND TTI-STATUS  NEED TO BE    *
*             RESET, WE RESET THE OTHER ATTRIBUTES TO PROTECT  *
*            OURSELVES FROM MISUSE OF CEMT.                    *
****************************************************************
 ENVIRONMENT  DIVISION.
 CONFIGURATION SECTION.
 DATA DIVISION.
 WORKING-STORAGE SECTION.
 01  SET-INQUIRY-FIELDS.
     03  SI-TERMINAL     PIC X(4)   VALUE 'CRLP'.
     03  SI-PAGESTATUS   PIC 9(9)   COMP SYNC
                         VALUE DFHVALUE(AUTOPAGEABLE).
     03  SI-SERVSTATUS   PIC 9(9)  COMP SYNC
                         VALUE DFHVALUE(INSERVICE).
     03  SI-ATISTATUS    PIC 9(9)  COMP SYNC
                         VALUE DFHVALUE(ATI).
     03  SI-TTISTATUS    PIC 9(9)   COMP SYNC
                         VALUE DFHVALUE(TTI).
 01  CICS-MESSAGE-LENGTH PIC S9(4) COMP SYNC VALUE  +38.
 01  CICS-MESSAGE.
     03  FILLER          PIC X(19)  VALUE 'CRLPINIT: TERMINAL '.
     03  C-TERMINAL      PIC X(04).
     03  FILLER          PIC X(15)  VALUE ' HAS BEEN RESET'.
 PROCEDURE DIVISION.
 R00-MAIN  SECTION.
 R0001.
     EXEC CICS HANDLE  CONDITION
                       TERMIDERR (R0099)
                       END-EXEC.
     EXEC CICS SET     TERMINAL  (SI-TERMINAL)
                       PAGESTATUS(SI-PAGESTATUS)
                       SERVSTATUS(SI-SERVSTATUS)
                       ATISTATUS (SI-ATISTATUS)
                       TTISTATUS (SI-TTISTATUS)
                       END-EXEC.
     MOVE SI-TERMINAL TO C-TERMINAL.
*       GIVE MESSAGE UPON THE SYSTEM CONSOLE
     EXEC CICS WRITEQ  TD
                       QUEUE     ('IEP1')
                       LENGTH    (CICS-MESSAGE-LENGTH)
                       FROM      (CICS-MESSAGE)
                       END-EXEC.
*       GIVE MESSAGE UPON THE SYSTEM LOG
     EXEC CICS WRITEQ  TD
                       QUEUE     ('CSTL')
                       LENGTH    (CICS-MESSAGE-LENGTH)
                       FROM      (CICS-MESSAGE)
                       END-EXEC.
 R0099.
     EXEC CICS RETURN  END-EXEC.
 DUMMY.
     EXIT PROGRAM.


Hans Hommersom
Systems Programmer
Philips Bedrijven BV  (The Netherlands)          ) Philips Bedrijven BV 1990


