 Producing a summary of CICS dumps
INTRODUCTION
CICS uses a dump file to store all CICS-related abends.  However, there is a
problem when it comes to printing the dump dataset.  There is no way at present
of printing a specific transaction or formatted dump or even generating a list
of dumps present in the dump dataset.  If you wish to see the contents of the
dump dataset, you will have to print the entire dump file.
The problem gets worse if the dump dataset is large, since printing can take
quite a while (more than one hour), just to see what the contents of the dump
file are.
ENVIRONMENT
At present, we are running both CICS 1.5 and 1.6 under VSE.  The enhancements we
made were implemented under CICS 1.5 but will also be implemented under CICS
1.6.
SOLUTION
The solution we have implemented is to revise the IBM dump utility program
(DFHDUP) and to provide a summary listing that will give you information
regarding every transaction dump and formatted dump present in the file.  It
also gives you a choice as to whether you would like to print the full dump or
not.  Typically, we would print the summary listing and see if there are dumps
of interest to us.  It also allows us to check quickly whether the dump file
should be printed or not.
The change to the source code was applied via the IBM utility MSHP.  It may be a
good idea to keep the original copy of DFHDUP in case you want to go back to the
original version.  The change was applied to CICS/VS Release 1.5 but should be
applicable to 1.6 as well.
FUTURE ENHANCEMENTS
We are planning two enhancements to the dump utility program.  The first one is
to allow selected printing of dumps.  This would be helpful if you only wanted
to print one or more dumps in the dump dataset.  The second one is to implement
the enhancements for CICS/OS 1.6.
JCL USED TO RUN THE MODIFIED DUMP UTILITY PROGRAM
* $$ JOB JNM=DFHDUP,CLASS=0
* $$ LST LST=02E,CLASS=M
* $$ LST LST=03E,CLASS=M
// JOB DFHDUP
// ASSGN SYS010,DISK,VOL=volser,SHR
// DLBL DTFDISK,'CICS.DUMPA'
// EXTENT SYS010,volser,,,start-track,number-of-tracks
// DLBL CICSCL,'CICS.CIL'
// EXTENT ,volser
// LIBDEF CL,SEARCH=CICSCL
// ASSGN SYS005,03E      SUMMARY LISTING
*  UPSI SETTINGS
*  x1xx - SINGLE SPACING
*  xx1x - SUPPRESS DUMP LISTING
*  xxx1 - SUPPRESS SUMMARY LISTING
// UPSI 011
// PAUSE  CHANGE UPSI SETTING IF NEEDED
// EXEC DFHDUP,SIZE=200K
   DEVICE=DISK
/*
/&
* $$ EOJ
SOURCE CODE OF DFHDUP ENHANCEMENT
// EXEC MSHP,SIZE=600K
 CORRECT 5746-XX-300 : BP00001
   /* AR150                                      */
   /* MODULE :  DFHDUP -- TYPE: SOURCE           */
   /* DFHDUP IS DISTRIBUTED IN TERTIARY LIBRARY  */
 AFFECTS MACRO = DFHDUP SUBLIB = A
 INSERT 039600  /* COMRG */
         MVC   UPSI,23(1)          SAVE UPSI BYTE                ..SST
         MVC   DATET,0(1)          SAVE TODAY'S DATE             ..SST
/$
 INSERT 057900  /* ST IOREG,BUFPTR */
         L     R2,=A(DTFOUT1)                                    ..SST
         OPENR (R2)                OPEN STATISTICS LISTING       ..SST
/$
 INSERT 133500  /* BAL R14,FDPUT   */
         CLC   DCLINE(9),=C'FORMATTED'                           ..SST
         BNE   DUNEXT1                                           ..SST
         LH    R2,DCRECLEN         GET THE RECORD LENGTH         ..SST
         SH    R2,=Y(DCLINE+16-DCRECST)                          ..SST
         EX    R2,MVCREM           MOVE REMARKS                  ..SST
/$
 INSERT 134400  /* DUFDMVCL MVC   LINE(0),DCLINE */
MVCREM   MVC   STRMRK(0),DCLINE+15 EXECUTED INSTRUCTION          ..SST
/$
 INSERT 194700  /* NI FDPFLAG,X'FF'-FDHDRAVL     */
         BAL   BAL2,WRSTAT         WRITE DUMP STATISTICS         ..SST
/$
 INSERT 204600  /* CLOSER (1),(R2)               */
         L     R2,=A(DTFOUT1)                                    ..SST
         CLOSER (R2)               CLOSE STATISTICS LISTING      ..SST
/$
 INSERT 253200  /* EX R2,DUHMOVE                 */
         CLI   DCIRTSI,DCFCATIC    IS THIS A TERM. CONTROL TBL   ..SST
         BNE   NOTTCT              ..NO.                         ..SST
         MVC   STTERM,DCDATA                                     ..SST
NOTTCT   EQU   *
/$
 INSERT 309000  /* FDPUT1 EQU *                  */
         TM    UPSI,NODUMP         DUMP LISTING NEEDED?          ..SST
         BO    NOPRT               NO. DO NOT PRINT.             ..SST
/$
 INSERT 312000  /* LR R1,R10                     */
         B     SVIOREG             SAVE I/O REGISTER             ..SST
NOPRT    DS    0H                                                ..SST
         LA    IOREG,FDOUTA        SIMULATE PUT INSTRUCTION      ..SST
SVIOREG  EQU   *                                                 ..SST
/$
 INSERT 448000  /* MVC AMIDDLE,AMIDDLE-1         */
         CLC   APSW,0(R6)          IS THIS A PSW?                ..SST
         BNE   NOTPSW              NO. SKIP SAVING IT.           ..SST
         MVC   STPSW,AHEX2         SAVE PSW FOR PRINTING         ..SST
NOTPSW   DS    0H                                                ..SST
/$
 INSERT 577200  /* NI FDPFLAG,X'FF'-FDHDRAVL-FDFORMD */
         BAL   BAL2,WRSTAT         WRITE DUMP STATISTICS         ..SST
/$
 INSERT 584400  /* DFHEJECT                      */
*=====================================================================*
*                                                                     *
*              PRINT DUMP STATISTICS                             ..SST*
*                                                                     *
*=====================================================================*
WRSTAT   DS    0H                                                ..SST
         TM    UPSI,NOSTAT         NEED STATISTICS?              ..SST
         BO    NOPRT1              NO. RETURN TO CALLER          ..SST
         AP    WLINECT,=P'1'       INCREMENT LINE COUNTER        ..SST
         CP    WLINECT,=P'56'      END OF PAGE?                  ..SST
         BNH   WRSTAT1             NO. PROCEED WITH PRINTING     ..SST
         L     R1,=A(DTFOUT1)                                    ..SST
         PUT   (1),WHEAD1          PRINT HEADING                 ..SST
         L     R1,=A(DTFOUT1)                                    ..SST
         PUT   (1),WHEAD2                                        ..SST
         L     R1,=A(DTFOUT1)                                    ..SST
         PUT   (1),BLANKS                                        ..SST
         ZAP   WLINECT,=P'4'       INITIALIZE LINE COUNTER       ..SST
WRSTAT1  EQU   *                   MOVE DETAIL FIELDS            ..SST
         MVC   STDATE,YEAR           DATE                        ..SST
         MVC   STTIME,HOUR           TIME                        ..SST
         MVC   STCODE,FDDUMPID       DUMP CODE                   ..SST
         MVC   STTASK,FDTRANID       TRANSACTION ID              ..SST
         MVC   STPAGE,PAGENOED       PRINT NO.OF PAGES           ..SST
         ED    STPAGE(5),DUPAGENO                                ..SST
         MVC   STTYPE(9),FDTYPE                                  ..SST
         CLC   STTYPE(7),=CL7'STORAGE'                           ..SST
         BNE   NOMVC                                             ..SST
         MVC   STTYPE(11),=CL11'TRANSACTIONU                     ..SST
NOMVC    EQU   *                                                 ..SST
         L     R1,=A(DTFOUT1)                                    ..SST
         PUT   (1),WLINE1                                        ..SST
         MVC   STTYPE,BLANKS                                     ..SST
         MVC   STTERM,BLANKS                                     ..SST
         MVC   STPSW,BLANKS                                      ..SST
NOPRT1   BR    BAL2                BRANCH BACK TO CALLER         ..SST
         DFHEJECT                                                ..SST
/$
 INSERT 835200  /* SPACE 5                       */
*=====================================================================*
* * *          WORKING AREAS FOR DUMP STATISTICS           ..SST  * * *
*=====================================================================*
UPSI     DC    X'00'               UPSI BYTE                     ..SST
ONESPACE EQU   X'40'               SINGLE SPACING REQUESTED      ..SST
NODUMP   EQU   X'20'               DUMP LISTINGS NOT REQUIRED    ..SST
NOSTAT   EQU   X'10'               STATISTICS NOT REQUIRED       ..SST
WLINECT  DC    P'60'               LINE COUNTER FOR STATS. LIST  ..SST
         SPACE                                                   ..SST
/$
 INSERT 853200  /* SPACE                         */
         DFHEJECT                                                ..SST
*=====================================================================*
* * *          PRINT LINES FOR DUMP STATISTICS             ..SST  * * *
*=====================================================================*
WHEAD1   DC    CL133' '
         ORG   WHEAD1
         DC    C'1'
         DC    C'CUSTOMER INFORMATION CONTROL SYSTEM '
         DC    C'LIST OF DUMPS - RUN DATE IS '
DATET    DC    C'MM/DD/YYU
         ORG
WHEAD2   DC    CL133' '
         ORG   WHEAD2
         DC    C'0'
         DC    C'  DATE      TIME    CODE  TASK  NO. OF PAGESU
         DC    C'  TYPE OF DUMP  TERM          PSW        '
         DC    C'       R E M A R K S  '
         ORG
WLINE1   DC    CL133' '
         ORG   WLINE1
         DC    C' '
STDATE   DC    CL8' '
         DC    CL2' '
STTIME   DC    CL8' '
         DC    CL2' '
STCODE   DC    CL4' '
         DC    CL2' '
STTASK   DC    CL4' '
         DC    CL5' '
STPAGE   DC    CL5' '
         DC    CL6' '
STTYPE   DC    CL11' '
         DC    CL3' '
STTERM   DC    CL4' '
         DC    CL2' '
STPSW    DC    CL19' '
         DC    CL2' '
STRMRK   DC    CL35' '
         ORG
BLANKS   DC    CL133' '
/$
 INSERT 952800  /* FDOUTB DC CL133' '            */
STOUTA   DC    CL133' '            PRT BUFFER A (STATS)          ..SST
STOUTB   DC    CL133' '            PRT BUFFER B (STATS)          ..SST
/$
 INSERT 982400  /* *** SDMODW                    */
DTFOUT1  DTFPR DEVADDR=SYS005,                                   ..SST X
               IOAREA1=STOUTA,                                   ..SST X
               IOAREA2=STOUTB,                                   ..SST X
               WORKA=YES,                                        ..SST X
               CTLCHR=ASA,                                       ..SST X
               BLKSIZE=133                                       ..SST
         PRMOD CTLCHAR=ASA,WORKA=YES,IOAREA2=YES                 ..SST
/$
/*


Stewart Tan
Head of Technical Support     ) Bank of the Philippine Islands 1988
Bank of the Philippine Islands (The Philippines)










































