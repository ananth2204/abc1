 Locating the PRINTTO-id of a terminal

At our installation there are several applications that require scheduling of
output to printers that are related to specific terminals.  This can be done by
using the DFHTC CTYPE= LOCATE macro to find the TCTTE for the specific terminal
and subsequently to get the PRINTTO printer address.  The program below locates
the PRINTTO printer-id of the terminal passed to it in the TWA and returns it,
also in the TWA.

LPRBAR   EQU   R3
TCTTEAR  EQU   R4
**********************************************************************
***           D U M M Y   S E C T I O N S                          ***
**********************************************************************
         COPY  DFHCSADS
         DFHTCTZE CICSYST=YES
         DFHTCA   CICSYST=YES
**********************************************************************
*        CSECT BEGIN                                                 *
*        TERMINAL ID IS PASSED   IN 1-4TH BYTE OF TWA                *
*        PRINTER  ID IS RETURNED IN 5-8TH BYTE OF TWA                *
*        RETURN CODE IS RETURNED IN 9TH   BYTE OF TWA                *
*            RC = 1  --- PRINTER ID FOUND                            *
*            RC = 2  --- TERMINAL ID NOT FOUND                       *
*            RC = 3  --- NO ASSOCIATED PRINTER                       *
**********************************************************************
SPCPRTO  CSECT
         DS    0D                 ENFORCE INITIAL ALIGNMENT
         DC    C'****'
         DC    CL8'SPCPRTO'       NAME
         DC    C'****'
         DC    AL4(PRTONA)        ENTRY POINT ADDRESS
         DC    AL4(0)             RESERVED
PRTONA   DS    0H                 ENTRY ADDR
         BALR  LPRBAR,0           SET BASE REGISTER
         USING *,LPRBAR
MAIN     DS    0H
         L     R5,TWACOBA
         MVC   TCATPTA(4),0(5)
         MVC   4(5,5),BLANK
LOCATE   DFHTC CTYPE=LOCATE,                                         *
               DOMAIN=ALL,                                           *
               TERM=ID,                                              *
               ERROR=NOTFOUND
         L     TCTTEAR,TCATPTA         ADDRESS TCTTE
         CLC   TCTEPTO+1(3),=X'000000' ANY PRINTTO PRINTER ? X'190'
         BE    NOPRTTO                 ... NOT AVAILABLE
         L     TCTTEAR,TCTEPTO
         MVC   8(1,5),=C'1'
         MVC   4(4,5),TCTTETI   MOVE PRINTER IDENTITY
         B     RETURN
*  TERMINAL NOT FOUND
NOTFOUND EQU   *
         MVC   8(1,5),=C'3'
         B     RETURN
*  NO ASSOCIATED PRINTER
NOPRTTO  EQU   *
         MVC   8(1,5),=C'2'
RETURN   DFHPC TYPE=RETURN
BLANK    DC    CL5' '
         LTORG
         END   PRTONA


S F Chan
Computer Project Officer
 Cathay Pacific Airways Ltd (Hong Kong)  ) Cathay Pacific Airways 1987

































































