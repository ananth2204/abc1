 Dynamic Storage Area allocation display
This is a program to produce a snapshot of the CICS Dynamic Storage Area (DSA).
It displays the following information:
%    Total number of pages in the DSA
%    Number of pages currently allocated and to which subpools
%    Size of the storage cushion
%    Number of times at Short On Storage (SOS)
%    Current number of active and suspended tasks
%    Number of times the storage cushion has been released
%    Number of times at MAXTASK
%    Page size and MAXTASK settings.
The pages allocated are displayed as numbers, percentages, and as bar charts, as
shown in Figure 1.
BACKGROUND
The reason that I had to write this program was that I wanted to know how the
DSA, and especially the program subpool, was being allocated.
Our CICS response times began to fluctuate at an unacceptable rate but I
couldnUt find a direct relationship between the number of transactions in the
system and the response time, ie the problem was not necessarily caused by an
increased transaction volume.  I suspected it to be (among other things) a
problem of excessive program compression, but without a proper tool I couldnUt
tell.
As we had no CICS monitor I wrote this program to help me find out what was
actually happening.
HOW TO USE THE PROGRAM
It consists of one Assembler program written in CICS command level and one BMS
map.
The only reason that the program starts by defining the mixed-mode global bit
(&DFHEIMX) is to get the address of the CSA directly in a register.  IUm tired
of seeing the CICS command-level translator giving me a warning every time I
request the address of the CSA via an EXEC CICS ADDRESS call.
It requires the following PPT entries:
SYSTS01  DFHPPT TYPE=ENTRY,PROGRAM=SYSTS01  -  program
SYST     DFHPPT TYPE=ENTRY,PROGRAM=SYST     -  map
and the following PCT entry:
SYST     DFHPCT TYPE=ENTRY,PROGRAM=SYSTS01,TRANSID=xxxx,
                SPURGE=YES
The program may be assigned any valid transaction code as it uses the EIBTRNID
field.
The PF keys are defined as follows:
%    PF3, PF15, or CLEAR ends the transaction, clears the screen, and stops the
auto-refresh.
%    PF5 starts the auto-refresh function which updates the screen every 15
seconds.
%    Any other key just refreshes the screen.  You may refresh the screen
yourself even though auto-refresh is also running.
Unlike CEMT, the counts of active and suspended tasks include the KCP, TCP, and
journal control tasks, if any.
The program is written for CICS 1.6 under VSE/SP 2.1, but I see no reason why it
couldnUt run under MVS too as the program only accesses CICS control blocks.
With modifications it might run under CICS 1.5 as well.
PART ONE OF THE SOURCE CODE
         GBLB  &DFHEIMX                ALLOW MIXTURE OF MACRO- AND
&DFHEIMX SETB  (1)                     COMMAND-LEVEL CODING
TIOABAR  EQU   4                       POINTER TO TIOA
DCACBAR  EQU   8
PAMBAR   EQU   9
SBA      EQU   X'11'
DFHEISTG DSECT
         COPY  SYST
TIOALEN  DS    H                       LENGTH OF DATA IN TIOA
DWORD    DS    D                       USED FOR CONVERSION
WORK2    DS    0CL2                    VARIOUS EDIT FIELDS
WORK3    DS    0CL3
WORK4    DS    0CL4
WORK5    DS    0CL5
 WORK6    DS    0CL6
WORK7    DS    CL7
WORK8    DS    CL8
WORK9    DS    CL9
WORK10   DS    CL10
STARTCD  DS    CL2
SYSTREQ  DS    0CL8
SYSTERM  DS    CL4
SYSTRAN  DS    CL4
         DFHCSAD
         DFHTCA CICSYST=YES
         COPY  DFHDCADS
         COPY  DFHPAMDS
         COPY  DFHAID
         COPY  DFHBMSCA
**********************************************************************
*        START OF PROGRAM - TEST STARTUP AND PF-KEYS                 *
**********************************************************************
SYSTS01  DFHEIENT DATAREG=10
         MVC   SYSTERM,EIBTRMID        BUILD A REQUEST-ID AREA
         MVC   SYSTRAN,EIBTRNID        WITH TERMID !! TRANSID
         EXEC CICS ASSIGN STARTCODE(STARTCD)
         CLC   STARTCD,=C'S '          STARTED VIA INTERVAL CNTRL
         BE    FROMSTRT                YES, DONUT TEST PF-KEYS
         CLC   STARTCD,=C'TD'          STARTED FROM TERMINAL
         BE    FROMTERM                YES, DONUT TEST PF-KEYS
         EXEC CICS ABEND ABCODE('STRT')
FROMTERM EXEC CICS RECEIVE SET(TIOABAR) LENGTH(TIOALEN)
         CLI   EIBAID,DFHPF3           DID USER PRESS PF 3
         BE    ENDPROG                 YES, CLEAR SCREEN AND END
         CLI   EIBAID,DFHPF15          DID USER PRESS PF 15
         BE    ENDPROG                 YES, CLEAR SCREEN AND END
         CLI   EIBAID,DFHCLEAR         DID USER PRESS CLEAR
         BE    ENDPROG                 YES, CLEAR SCREEN AND END
FROMSTRT L     PAMBAR,CSAPAMA          ADDRESS PAGE ALLOCATION MAP
**********************************************************************
*        START BUILDING UP OUTPUT MAP                                *
**********************************************************************
         MVC   WORK10,=X'402021204B20204B2020'  EDIT MASK
         ED    WORK10,EIBTIME          EDIT TIME
         MVC   DATEO,WORK10+2          MOVE TO OUTPUT FIELD
         MVC   WORK4,=X'40202120'      GET EDIT MASK
         ED    WORK4,CSAKCMT           EDIT MAX TASK
         MVC   MAXTO,WORK4+1           MOVE TO OUTPUT FIELD
         MVC   WORK8,=X'4020202020202120' GET EDIT MASK
         ED    WORK8,CSAKCMTC          EDIT REACHED MAX TASK
         MVC   RMAXO,WORK8+6           MOVE TO OUTPUT FIELD
         MVC   WORK4,=X'40202120'      GET EDIT MASK
         ED    WORK4,CSASCRQ           EDIT SHORT ON STORAGE
         MVC   SHORTO,WORK4+2          MOVE TO OUTPUT FIELD
         L     R5,PAMSRCNT             GET STORAGE RECOVERY COUNT
         CVD   R5,DWORD
         MVC   WORK4,=X'40202120'      GET EDIT MASK
         ED    WORK4,DWORD+6           EDIT STG RECOVERY COUNT
         MVC   VIOLO,WORK4+2           MOVE TO OUTPUT FIELD
         MVC   WORK4,=X'40202120'      GET EDIT MASK
         ED    WORK4,CSASCCR           EDIT REACHED CUSHION
         MVC   RCUSHO,WORK4+2          MOVE TO OUTPUT FIELD
         L     R5,PAMPGNUM             GET NO OF PAGES IN DSA
         CVD   R5,DWORD
         MVC   WORK8,=X'4020202020202120' GET EDIT MASK
         ED    WORK8,DWORD+4           EDIT NO OF PAGES
         MVC   DSAO,WORK8+4            MOVE TO OUTPUT FIELD
         L     R5,PAMCTLCT             GET CTL-SUBPOOL
         A     R5,PAMTELCT             ADD  TELE
         A     R5,PAMMIXCT             ADD  MIX
          A     R5,PAMISOCT             ADD  ISO
         A     R5,PAMSHRCT             ADD  SHR
         A     R5,PAMRPLCT             ADD  RPL
         A     R5,PAMPGMCT             ADD  PGM
         CVD   R5,DWORD
         MVC   WORK8,=X'4020202020202120' GET EDIT MASK
         ED    WORK8,DWORD+4           EDIT TOTAL ALLOCATED PAGES
         MVC   ALLOCO,WORK8+4          MOVE TO OUTPUT FIELD
         L     R5,PAMCUSHN             GET CUSHION SIZE
         CVD   R5,DWORD
         MVC   WORK8,=X'4020202020202120' GET EDIT MASK
         ED    WORK8,DWORD+4           EDIT CUSHION SIZE
         MVC   CUSHO,WORK8+4           MOVE TO OUTPUT FIELD
         L     R5,PAMPGSZE             GET PAGE SIZE
         CVD   R5,DWORD
         MVC   WORK6,=X'402020202120'  GET EDIT MASK
         ED    WORK6,DWORD+5           EDIT CUSHION SIZE
         MVC   PAGESZO,WORK6+2         MOVE TO OUTPUT FIELD
**********************************************************************
*        MOVE '*' TO MAP                                             *
**********************************************************************
CTLPOOL  MVC   CTLO,=CL50' '           CLEAR FIELD
         SR    R6,R6                   CLEAR REGISTER
         L     R7,PAMCTLCT             GET CTL POOL
         LTR   R7,R7                   ANY PAGES ALLOCATED ?
         BE    MIXPOOL                 NO, SKIP COMPUTATION
         MH    R7,=H'100'
         D     R6,PAMPGNUM             COMPUTE PERCENTAGE
         SRL   R7,1                    DIVIDE BY 2
         LA    R5,CTLO                 ADDRESS OUTPUT FIELD
         EX    R7,EXMVC                MOVE THIS MANY *
MIXPOOL  MVC   MIXO,=CL50' '           CLEAR FIELD
         SR    R6,R6                   CLEAR REGISTER
         L     R7,PAMMIXCT             GET CTL POOL
         LTR   R7,R7                   ANY PAGES ALLOCATED ?
         BE    TELPOOL                 NO, SKIP COMPUTATION
         MH    R7,=H'100'
         D     R6,PAMPGNUM             COMPUTE PERCENTAGE
         SRL   R7,1                    DIVIDE BY 2
         LA    R5,MIXO                 ADDRESS OUTPUT FIELD
         EX    R7,EXMVC                MOVE THIS MANY *
TELPOOL  MVC   TELO,=CL50' '           CLEAR FIELD
         SR    R6,R6                   CLEAR REGISTER
         L     R7,PAMTELCT             GET TELE POOL
         LTR   R7,R7                   ANY PAGES ALLOCATED ?
         BE    ISOPOOL                 NO, SKIP COMPUTATION
         MH    R7,=H'100'
         D     R6,PAMPGNUM             COMPUTE PERCENTAGE
         SRL   R7,1                    DIVIDE BY 2
         LA    R5,TELO                 ADDRESS OUTPUT FIELD
         EX    R7,EXMVC                MOVE THIS MANY *
ISOPOOL  MVC   ISOO,=CL50' '           CLEAR FIELD
         SR    R6,R6                   CLEAR REGISTER
         L     R7,PAMISOCT             GET ISO POOL
         LTR   R7,R7                   ANY PAGES ALLOCATED ?
         BE    SHRPOOL                 NO, SKIP COMPUTATION
         MH    R7,=H'100'
         D     R6,PAMPGNUM             COMPUTE PERCENTAGE
         SRL   R7,1                    DIVIDE BY 2
         LA    R5,ISOO                 ADDRESS OUTPUT FIELD
         EX    R7,EXMVC                MOVE THIS MANY *
SHRPOOL  MVC   SHRO,=CL50' '           CLEAR FIELD
         SR    R6,R6                   CLEAR REGISTER
         L     R7,PAMSHRCT             GET SHARED   POOL
         MH    R7,=H'100'
         LTR   R7,R7                   ANY PAGES ALLOCATED ?
          BE    RPLPOOL                 NO, SKIP COMPUTATION
         D     R6,PAMPGNUM             COMPUTE PERCENTAGE
         SRL   R7,1                    DIVIDE BY 2
         LA    R5,SHRO                 ADDRESS OUTPUT FIELD
         EX    R7,EXMVC                MOVE THIS MANY *
RPLPOOL  MVC   RPLO,=CL50' '           CLEAR FIELD
         SR    R6,R6                   CLEAR REGISTER
         L     R7,PAMRPLCT             GET RPL   POOL
         MH    R7,=H'100'
         LTR   R7,R7                   ANY PAGES ALLOCATED ?
         BE    PGMPOOL                 NO, SKIP COMPUTATION
         D     R6,PAMPGNUM             COMPUTE PERCENTAGE
         SRL   R7,1                    DIVIDE BY 2
         LA    R5,RPLO                 ADDRESS OUTPUT FIELD
         EX    R7,EXMVC                MOVE THIS MANY *
PGMPOOL  MVC   PGMO,=CL50' '           CLEAR FIELD
         SR    R6,R6                   CLEAR REGISTER
         L     R7,PAMPGMCT             GET PGM POOL
         MH    R7,=H'100'
         LTR   R7,R7                   ANY PAGES ALLOCATED ?
         BE    TOTPOOL                 NO, SKIP COMPUTATION
         D     R6,PAMPGNUM             COMPUTE PERCENTAGE
         SRL   R7,1                    DIVIDE BY 2
         LA    R5,PGMO                 ADDRESS OUTPUT FIELD
         EX    R7,EXMVC                MOVE THIS MANY *
TOTPOOL  MVC   TOTO,=CL50' '           CLEAR FIELD
         SR    R6,R6                   CLEAR REGISTER
         L     R7,PAMCTLCT             GET CTL-SUBPOOL
         A     R7,PAMTELCT             ADD  TELE
         A     R7,PAMMIXCT             ADD  MIX
         A     R7,PAMISOCT             ADD  ISO
         A     R7,PAMSHRCT             ADD  SHR
         A     R7,PAMRPLCT             ADD  RPL
         A     R7,PAMPGMCT             ADD  PGM
         MH    R7,=H'100'
         LTR   R7,R7                   ANY PAGES ALLOCATED ?
         BE    CONTINUE                NO, SKIP COMPUTATION
         D     R6,PAMPGNUM             COMPUTE PERCENTAGE
         SRL   R7,1                    DIVIDE BY 2
         LA    R5,TOTO                 ADDRESS OUTPUT FIELD
         EX    R7,EXMVC                MOVE THIS MANY *

This article will be concluded in next monthUs issue of CICS Update when we
shall be publishing the remainder of the source code.

Steen Spuur Hansen
Systems Programmer (Denmark)  ) Xephon 1988



















