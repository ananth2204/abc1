CEDF and other key-driven utilities

INTRODUCTION

Have you ever been in the middle of debugging a multi-screen
command-level application and wanted to use CEDF, only to
remember that you have to invoke the facility before starting
the application?  Have you ever wanted to use 24 Program
Function (PF) keys but your terminal only has 12?  Have you
ever wanted the convenience of the CICS local copy function,
only to discover that a Program Attention (PA) key must be
dedicated to it?

After spending time reading the code for the Terminal Control
Program, I discovered the module DFHZATT, the task attach
program.  After additional reading and some experimentation, I
developed a utility program which gave me all of the above
features without dedicating a PF key or a PA key to them.

HOW DOES IT WORK?

The main problem was how to set CEDF mode, start local
copy, etc without passing the keystroke through to the
application program.  DFHZATT contains the code for
handling CICS local copy requests, in which the PA key
defined in the SIT is not passed through to the application
program, but is used to flag the request for local copy.  Further
research showed that the 'print key' value is stored in the TCT
prefix area and the address of the local copy printer is stored
within each TCTTE defined to have local copy.  If no print key
is defined, the value is set to X'FF'.

I developed UTILEXT, a CICS global exit program at exit-id
XZCIN (VTAM terminal input), running under CICS 1.6,
VTAM 2.1.0, and VSE/SP 2.1.4.  Whenever UTILEXT needs
to set CEDF mode etc, without passing the keystroke to the
application program, the AID value of the keystroke is changed
to X'FF' and local copy is then started to a printer defined to
CICS but not to VTAM.  True local copy requests cause the
CICS print-key value to be set to the current keystroke AID.

STEP 1

a   If the defined utility PF/PA key is pressed, the keystroke is
not passed through to the application program.  UTILEXT
utility mode is set for the terminal.  Go to step 2.

b   If any other attention key is pressed, the exit program
ends.  The keystroke is passed through to the application
program.  Return to step 1.

STEP 2

a   If the defined CEDF PF/PA key is pressed, the keystroke is
not passed through to the application program.  CEDF mode is
set and UTILEXT utility mode is turned off for the terminal.
Go to step 3.

b   If the defined local copy PF/PA key is pressed, the
keystroke is not passed through to the application program.
Normal local copy is started and UTILEXT utility mode is
turned off for the terminal.  Return to step 1.

c   If the defined pass-through PF/PA key is pressed, the
keystroke is not passed through to the application program.
UTILEXT pass-through mode is set and utility mode is turned
off for the terminal.  Go to step 3.

d   If the defined utility PF/PA key is pressed, the keystroke is
not passed through to the application program.  UTILEXT
PF13-24 mode is set and utility mode is turned off for the
terminal.  Go to step 3.

e   If any other attention key is pressed, the keystroke is
passed through to the application program.  UTILEXT utility
mode is turned off.  Return to step 1.

STEP 3

a   If CEDF mode was set:

    i    If the defined utility PF/PA key is pressed, return to
step 1a.

    ii   If any other attention key is pressed, it is passed
through to the application program.  The first CEDF screen is
displayed.  Return to step 1.

b   If UTILEXIT pass-through mode was set, any attention
key, including the defined utility PF/PA key, is passed through
to the application program.  UTILEXT pass-through mode is
turned off.  Return to Step 1.

c   If UTILEXT PF13-24 mode was set:

    i    If a key in the range PF1-PF12 is pressed, the
keystroke is translated to PF13-24 (eg PF1 to PF13, PF2 to
PF14, etc).  Return to step 1.

    ii   If any other attention key is pressed, it is passed
through to the application program.  UTILEXT PF13-24 mode
is turned off.  Return to step 1.

DCT ENTRIES

Destination CSML for UTILSTRT messages (see below),
which points directly or indirectly to SYSLST needs to be
defined.

START-UP PROGRAM LIST TABLE

    DFHPLT TYPE=INITIAL,SUFFIX=xx (xx is the suffix listed in the SIT)
    DFHPLT TYPE=ENTRY,PROGRAM=UTILSTRT
    DFHPLT TYPE=FINAL

PPT ENTRIES

    DFHPPT TYPE=ENTRY,PROGRAM=UTILEXT
    DFHPPT TYPE=ENTRY,PROGRAM=UTILTBL
    DFHPPT TYPE=ENTRY,PROGRAM=UTILSTRT,PGMLANG=COBOL
    DFHPPT TYPE=ENTRY,PROGRAM=DFHPLTxx

SIT ENTRIES

%   EXEC=YES (command-level support is required).

%   EXITS=YES (user exit interface will be used).

%   PLTPI=xx (suffix of start-up PLT).

%   PRINT=YES (if PA1, PA2, or PA3 is currently specified,
make that key each user's local copy key).

TCT ENTRIES

An entry for the printer UTIL needs to be included.

         DFHTCT TRMIDNT=UTIL,                    required              X
                NETNAME=UTIL,                    required              X
                TIOAL=300,                       may be changed        X
                TRMMODL=2,                       may be changed        X
                FEATURE=(COPY,PRINT),            required              X
                GMMSG=NO,                        required              X
                RELREQ=(NO,NO),                  required              X
                ACCMETH=VTAM,                    required              X
                TYPE=TERMINAL,                   required              X
                TRMTYPE=3270P,                   required              X
                TCTUAL=255          should be >= largest in your system

Do not define this printer to VTAM!

User terminal entries must contain:

%   PRINTTO=xxxx, where xxxx is the terminal-id of a local
copy printer, not UTIL.

%   TCTUAL=xxx, where xxx is in the range 6 - 255 to
support UTILEXT.

If other applications use the TCT user area, set the starting
position of the UTILEXT work area (see below) to the unused
area.

UTILEXT PROGRAM

Assemble and link edit the following program.

* UTILEXT GLOBAL EXIT TO DFHZCB AT EXITID XZCIN
TCTPFBAR EQU   6                  TCT PREFIX BASE REGISTER
TCTTEAR  EQU   10                 TCT TERMINAL ENTRY BASE REGISTER
CSABAR   EQU   13                 COMMON STORAGE AREA BASE REGISTER
         COPY DFHCSADS
         DFHTCTZE CICSYST=YES     TCT SYMBOLIC STORAGE DEFINITION
         COPY DFHTCTFX            TCT PREFIX STORAGE DEFINITION
         DFHUEXIT TYPE=EP         USER EXIT PARAMETER LIST
GLBLAREA DSECT
UTILPRTR DS    F                  ADDRESS OF VIRTUAL UTILITY PRINTER
EDFKEY   DS    X                  EDF KEY AID VALUE
PRINTKEY DS    X                  PRINT KEY AID VALUE
PASSKEY  DS    X                  PASS THROUGH KEY AID VALUE
UTOFFSET DS    H                  OFFSET FROM TCTTEUA START FOR DATA
TCTUA    DSECT                    TCTTE USER AREA
UTILSAVE DS    F                  SAVE AREA FOR REAL PRINTTO ADDRESS
UTILAID  DS    X                  UTILITY KEY AID VALUE
UTILWK   DS    X                  WORK AREA
UTILEXT  CSECT                    BEGIN PROGRAM
         SAVE  (14,12)
         USING UTILEXT,R4         R4 => BASE REGISTER
         LR    R4,R15             COPY OUR BASE ADDRESS
         LR    R3,R13             SAVE R13 ADDRESS IN R3
TCTUACHK CLI   TCTTECIL,X'00'     IS THERE A TCTTE USER AREA ?
         BE    RETURN             NO, RETURN
         L     R5,TCTTECIA        LOAD ADDRESS OF TCTTE USER AREA
         USING TCTUA,R5           R5  => TCTTE USER AREA
         USING DFHUEPAR,R1        R1  => PARAMETER LIST
         L     CSABAR,UEPCSA      LOAD ADDRESS OF CSA
         USING DFHCSADS,R13       R13 => CSA ADDRESS
         L     R7,UEPGAA          ADDRESS OF GLOBAL WORK AREA
         LTR   R7,R7              IS THERE A GLOBAL WORK AREA?
         BZ    RETURN             NO, RETURN
         USING GLBLAREA,R7        R7  => GLOBAL WORK AREA
         AH    R5,UTOFFSET        ADD OFFSET TO GET TO UTILEXT AREA
         L     TCTPFBAR,CSATCTBA  LOAD ADDRESS OF TCT PREFIX
BYPASSCK TM    UTILWK,B'01000000' BYPASS SWITCH ON?
         BZ    BIT7CHK            CONTINUE WITH EXIT PROCESSING
         NI    UTILWK,B'10111111' YES, TURN OFF BYPASS SWITCH
         B     RETURN             BYPASS EXIT
BIT7CHK  TM    UTILWK,B'10000000' UTILITIES REQUESTED SW ON?
         BO    FUNCTSEL           CHECK FOR UTILITY FUNCTION SELECTED
PF1324CK TM    UTILWK,B'00100000' PF13-24 REQUESTED SW ON?
         BO    NEWPFKY2           PROCESS PF KEY 13-24
PASSCHK  TM    UTILWK,B'00010000' PASS THROUGH SW ON?
         BO    PASS2              PROCESS UTILITY KEY NORMALLY
UTREQCHK CLC   UTILAID,TCTTEAID   UTILITIES REQUESTED PF/PA KEY?
         BNE   RETURN             NO, RETURN
         MVC   TCTTEAID,TCTVPNTK  SET AID TO PRINT KEY AID VALUE
         MVC   TCTEPTO,UTILPRTR   MAKE PRINTTO THE DUMMY PRINTER
         OI    TCTEIEXI,TCTEEXI   TURN ON EXCEPTIONAL INPUT RECV'D SW
         OI    UTILWK,B'11000000' TURN ON UTILITIES REQ & BYPASS SW
         B     RETURN
FUNCTSEL CLC   EDFKEY,TCTTEAID    EDF KEY PRESSED?
         BE    EDFREQ             EDF REQUESTED, GO SET BIT
         CLC   PRINTKEY,TCTTEAID  PRINT KEY PRESSED?
         BE    PRTSCRN            PRINT REQUESTED, GO DO IT
         CLC   PASSKEY,TCTTEAID   PASS THROUGH KEY REQUESTED?
         BE    PASSTHRU           PASS THROUGH REQUESTED, GO DO IT
         CLC   UTILAID,TCTTEAID   PF13-24 REQUESTED?
         BE    NEWPFKY1           YES, PROCESS ON NEXT PASS
         NI    UTILWK,B'01111111' TURN OFF UTILITIES REQUESTED SW
         B     RETURN        NO FUNCT SELECTED, PROCESS KEY PRESSED
PRTSCRN  NI    UTILWK,B'01111111' TURN OFF UTILITIES REQUESTED SW
         TM    UTILWK,B'00000010' AUTHORIZED TO USE PRINT SCREEN?
         BZ    RETURN             NO, RETURN
         MVC   TCTTEAID,TCTVPNTK  SET AID TO PRINT KEY AID VALUE
         OI    UTILWK,B'01000000' TURN ON BYPASS SWITCH
         MVC   TCTEPTO,UTILSAVE   RESTORE ACTUAL PRINTTO ADDRESS
         OI    TCTEIEXI,TCTEEXI   TURN ON EXCEPTIONAL INPUT RECV'D SW
         B     RETURN
EDFREQ   NI    UTILWK,B'01111111' TURN OFF UTILITIES REQUESTED SW
         TM    UTILWK,B'00000100' AUTHORIZED TO USE EDF?
         BZ    RETURN             NO, RETURN
         OI    TCTTEEDF,B'10000000' TURN ON EDF REQUEST BIT
         OI    UTILWK,B'01000000' TURN ON BYPASS SWITCH
         MVC   TCTTEAID,TCTVPNTK  SET AID TO PRINT KEY AID VALUE
         MVC   TCTEPTO,UTILPRTR   MAKE PRINTTO THE DUMMY PRINTER
         OI    TCTEIEXI,TCTEEXI   TURN ON EXCEPTIONAL INPUT RECV'D SW
         B     RETURN
PASSTHRU NI    UTILWK,B'01111111' TURN OFF UTILITIES REQUESTED SW
         OI    UTILWK,B'01000000' TURN ON BYPASS SWITCH
         OI    UTILWK,B'00010000' TURN ON PASS THROUGH SWITCH
         MVC   TCTTEAID,TCTVPNTK  SET AID TO PRINT KEY AID VALUE
         MVC   TCTEPTO,UTILPRTR   MAKE PRINTTO THE DUMMY PRINTER
         OI    TCTEIEXI,TCTEEXI   TURN ON EXCEPTIONAL INPUT RECV'D SW
         B     RETURN
PASS2    NI    UTILWK,B'11101111' TURN OFF PASS THROUGH SWITCH
         OI    UTILWK,B'01000000' TURN ON BYPASS SWITCH
         B     RETURN
NEWPFKY1 NI    UTILWK,B'01111111' TURN OFF UTILITIES REQUESTED SW
         OI    UTILWK,B'01000000' TURN ON BYPASS SWITCH
         TM    UTILWK,B'00000001' AUTHORIZED TO USE PF13-24?
         BZ    RETURN             NO, RETURN
         OI    UTILWK,B'00100000' TURN ON PF13-24 SWITCHES
         MVC   TCTTEAID,TCTVPNTK  SET AID TO PRINT KEY AID VALUE
         MVC   TCTEPTO,UTILPRTR   MAKE PRINTTO THE DUMMY PRINTER
         OI    TCTEIEXI,TCTEEXI   TURN ON EXCEPTIONAL INPUT RECV'D SW
         B     RETURN
NEWPFKY2 NI    UTILWK,B'11011111' TURN OFF PF13-24 SW
         NI    TCTTEAID,B'11001111' MAP PF1-12 INTO PF13-24
         B     RETURN             PROCESS PF13-24 AID
RETURN   LR    R13,R3             RESTORE R13 SAVEAREA ADDRESS
         RETURN (14,12)
         LTORG
         END   UTILEXT

This article will be continued next month when we shall be
looking at the remaining components of the system.

Stephen J Gallaway
Senior Programmer ) Xephon 1990
Workers' Compensation Insurance Rating Bureau of California (USA)


