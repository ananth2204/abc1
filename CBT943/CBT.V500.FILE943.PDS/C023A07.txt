 CICS batch debugging aid

When CICS 'hangs-up', it can sometimes be very difficult to discover what is
causing the problem.  Monitors for CICS that are dependent upon the integrity of
CICS itself normally do not allow you to investigate the internals when a severe
problem arises.  The answer to this problem is to write a debugging aid that
will display several key areas of CICS from outside the CICS partition.  Since
such an aid is not dependent upon CICS integrity, it will function at all times.
The program that is supplied here will produce output on the console as shown in
Figure 1.

This program could serve as a base to develop something much more elaborate.
For example, we have enhanced this program to display storage and cancel the
active task (all from a 3270 device).

         LCLC  &REL
&REL     SETC  '1.0'
*-------------------------------------------------------------------*
*  TSXSYSMC-     CICS/VS CROSS SYSTEM MONITOR                       *
*  PLEASE NOTE THAT ROUTINES 'SCANDCA, SCANDCAS' ARE ESSENTIALLY    *
*  IDENTICAL. THEY ARE SPLIT INTO 2 SEPARATE ROUTINES FOR           *
*  CLARITY. YOU MAY WISH TO COMBINE THEM.                           *
*  ALSO, ROUTINES 'RDCVTHC, RDWRTCN' ARE GENERAL ROUTINES TO        *
*  1....CONVERT HEX TO PRINTABLE,                                   *
*  2....WRITE A MESSAGE ON THE CONSOLE.                             *
*  THEY ARE NOT DOCUMENTED HERE.                                    *
*-------------------------------------------------------------------*
TSXSYSMC CSECT
         BALR  R3,0                SET UP FIRST BASE
         USING *,R3,R4             TELL ASM
         LA    R4,4095(R3)         SET UP SECOND BASE
         LA    R4,1(R4)            REGISTER
         MVC   PARTID(2),6(R1)     MOVE PARTID
         LA    RD,SAVEREGS         OUR SAVE AREA
         BAL   RE,INITIAL          DO INITIAL STUFF
         BAL   RE,SCANDCA          SCAN DCA CHAIN (ACTIVE)
         BAL   RE,SCANDCAS         SCAN DCA CHAIN (SUSPENDED)
         EOJ                       END OF JOB...
INITIAL  ST    RE,EXITA+6
         L     R1,X'14'            ADDRESS COMREG
         LH    R2,X'82'            ADDRESS SYSCOM
         USING SYSCOM,R2
         USING COMREG,R1           ADDRESS COMREG
         LH    R8,PIBPT            PIB POINTER
         LH    RA,PIB2PTR          PIB2 POINTER
         LH    R9,IJBNPART         NUMBER OF PARTITIONS
         LA    R9,1(,R9)           PLUS 1 FOR A/R
         DROP  R2                  DROP SYSCOM ADDRESSING
         SLR   RB,RB               CLEAN FOR INDEX COUNT
INIT010  CLC   2(2,R8),PARTID      FIND PARTITION ID
         BE    INIT020             GOTTIT...
         LA    R8,16(,R8)          NEXT PIB ENTRY
         LA    RB,1(,RB)           ADD TO INDEX COUNT
         BCT   R9,INIT010          LOOP
         CALL  RDWRTCN,(MSG1)      WRITE 'NOT FOUND' MESSAGE
         CANCEL
INIT020  SLL   RB,4                MULTIPLY BY LENGTH OF PIB ENTRY
         AR    RA,RB               POINT TO PROPER PIB/2
         LH    R1,0(RA)            LOAD ADDRESS OF COMREG
         ST    R1,COMPTR           STORE COMREG ADDRESS
         L     R6,IJBAFCB          LOAD POINTER TO CSA
         L     R2,8(R6)            LOAD CSA..
         ST    R2,ADDRCSA          CSA ADDRESS
EXITA    EX14
SCANDCA  ST    RE,EXITC+6
         USING DFHCSADS,RD         TELL ASM ABOUT CSA
          L     RD,ADDRCSA          ADDRESS CSA
         L     RC,CSACDTA          ADDRESS CURRENTLY DISP. TASK TCA
         ST    RC,ACTVTCA          SAVE IT
         L     RB,CSAACTFA         LOWEST PRIORITY ACTIVE TASK
         LA    RF,CSAACTFA-12      DATUM POINT WITHIN CSA
         ST    RF,CYCLIC           SAVE IT
         LA    RD,SAVEREGS         SAVE OUR REGS
         CALL  RDWRTCN,(MSG3)      TYPE OUT NICE MESSAGE
         L     RD,ADDRCSA          ADDRESS CSA
SCAN010  MVC   MSG4,BLANKS         CLEAN WORK AREA FOR MESSAGE
         L     RC,X'14'(RB)        LOAD TCA ADDRESS OF TASK
         USING DFHTCADS,RC         TELL ASM
         ST    RC,WORK4            TCA ADDRESS
         LA    R5,WORK4+1          SOURCE AREA FOR TRANSLATE
         LA    R6,MSG4I            OUTPUT AREA
         LA    R7,3                NUMBER OF BYTES TO BE XLATED
         LA    RD,SAVEREGS         GO HERE
         CALL  RDCVTHC             CALL XLATE MODULE
         LA    R5,TCATCDC          DISPATCH INDICATOR
         LA    R6,MSG4H            OUTPUT AREA
         LA    R7,1                NUMBER OF BYTES TO BE XLATED
         LA    RD,SAVEREGS         GO HERE
         CALL  RDCVTHC             CALL XLATE MODULE
         L     R1,TCASYAA          LOAD SYSTEM PART OF TCA
         CLC   X'34'(4,R1),=XL4'00'   NOTHING ??
         BE    SCAN040             THATS RIGHT...SKIP IT
         L     R2,X'14'(R1)        PCT ADDRESS
         MVC   MSG4A,0(R2)         MOVE TRANSID
         L     R2,X'34'(R1)        PPT ADDRESS
         MVC   MSG4B,0(R2)         MOVE CURRENT PROGRAM
         MVC   WORK6,MASK6         EDIT MASK
         MVC   MSG4C+2(3),17(R1)   JUST IN CASE WEIRD TASK NO (LLL)
         CLI   17(R1),C'A'         CHECK IT
         BNL   SCAN015             BR AROUND EDIT
         ED    WORK6,17(R1)        EDIT
         MVC   MSG4C,WORK6+1       MOVE IT TO PRINT LINE
SCAN015  L     R2,TCATCTFA         TCTTE ADDRESS
         MVC   MSG4D,=C'....'      MOVE DEFAULT TERMINAL
         CLI   0(R2),C'A'          NO TERMINAL ?
         BL    SCAN020             NOPE..THATS OK...
         MVC   MSG4D,0(R2)         MOVE TERMINAL ID
         MVC   MSG4G,37(R2)        MOVE OPERATOR ID
SCAN020  MVC   MSG4E,=C'ACTV'      INDICATE ACTIVE TASK
         CL    RC,ACTVTCA          IS THIS ACTIVE TASK?
         BNE   SCAN035             NOPE
         MVC   MSG4F,=C'<==='      INDICATE CURRENT ACTIVE TASK
SCAN035  LA    RD,SAVEREGS         MAKE SURE REGS GO HERE (NOT CSA)
         CALL  RDWRTCN,(MSG4)      TYPE OUT NICE MESSAGE
SCAN040  L     RD,ADDRCSA          RD SHOULD ALWAYS BE CSA
         L     RB,12(RB)           NEXT HIGHER TASK
         CL    RB,CYCLIC           LOOPED AROUND ?
         BNE   SCAN010             NOT YET...KEEP GOING
EXITC    EX14
SCANDCAS ST    RE,EXITD+6
         USING DFHCSADS,RD         TELL ASM ABOUT CSA
         L     RD,ADDRCSA          ADDRESS CSA
         L     RC,CSACDTA          ADDRESS CURRENTLY DISP. TASK TCA
         L     RB,CSASUSFA         LOWEST PRIORITY ACTIVE TASK
         LA    RF,CSASUSFA-12      DATUM POINT WITHIN CSA
         ST    RF,CYCLIC           SAVE IT
SCANS10  MVC   MSG4,BLANKS         CLEAN WORK AREA FOR MESSAGE
         L     RC,X'14'(RB)        LOAD TCA ADDRESS OF TASK
         USING DFHTCADS,RC         TELL ASM
         ST    RC,WORK4            TCA ADDRESS
         LA    R5,WORK4+1          SOURCE AREA FOR TRANSLATE
         LA    R6,MSG4I            OUTPUT AREA
          LA    R7,3                NUMBER OF BYTES TO BE XLATED
         LA    RD,SAVEREGS         GO HERE
         CALL  RDCVTHC             CALL XLATE MODULE
         LA    R5,TCATCDC          DISPATCH INDICATOR
         LA    R6,MSG4H            OUTPUT AREA
         LA    R7,1                NUMBER OF BYTES TO BE XLATED
         LA    RD,SAVEREGS         GO HERE
         CALL  RDCVTHC             CALL XLATE MODULE
         L     R1,TCASYAA          LOAD SYSTEM PART OF TCA
         CLC   X'34'(4,R1),=XL4'00'   NOTHING ??
         BE    SCANS30             THATS RIGHT...SKIP IT
         L     R2,X'14'(R1)        PCT ADDRESS
         MVC   MSG4A,0(R2)         MOVE TRANSID
         L     R2,X'34'(R1)        PPT ADDRESS
         MVC   MSG4B,0(R2)         MOVE CURRENT PROGRAM
         MVC   WORK6,MASK6         EDIT MASK
         MVC   MSG4C+2(3),17(R1)   JUST IN CASE WEIRD TASK NO (LLL)
         CLI   17(R1),C'A'         CHECK IT
         BNL   SCANS15             BR AROUND EDIT
         ED    WORK6,17(R1)        EDIT
         MVC   MSG4C,WORK6+1       MOVE IT TO PRINT LINE
SCANS15  L     R2,TCATCTFA         TCTTE ADDRESS
         MVC   MSG4D,=C'....'      MOVE DEFAULT TERMINAL
         CLI   0(R2),C'A'          NO TERMINAL ?
         BL    SCANS20             NOPE..THATS OK...
         MVC   MSG4D,0(R2)         MOVE TERMINAL ID
         MVC   MSG4G,37(R2)        MOVE OPERATOR ID
SCANS20  MVC   MSG4E,=C'SUSP'      INDICATE SUSPENDED
         LA    RD,SAVEREGS         SAVE OUR REGS HERE..(NOT CSA) !!
         CALL  RDWRTCN,(MSG4)      TYPE OUT NICE MESSAGE
SCANS30  L     RD,ADDRCSA          ADDRESS CSA
         L     RB,12(RB)           NEXT HIGHER TASK
         CL    RB,CYCLIC           GONE FAR ENOUGH ?
         BE    EXITD               YES..
         B     SCANS10             NOT YET...KEEP GOING
EXITD    EX14
*  W O R K   A R E A S                                              *
MASK6    DC    X'402020202020'
WORK4    DS    F                   GENERAL WORK AREA
ADDRCSA  DS    F                   CSA ADDRESS
ACTVTCA  DS    F                   ACTIVE TCA ADDRESS
CYCLIC   DS    F                   LOOP CONTROL
COMPTR   DS    F                   COMREG POINTER
PARTID   DS    CL2                 PARTITION THAT IS RUNNING CICS
TECB     DC    F'0'                TIMER ECB
SAVEREGS DS    9D                  OUR SAVE AREA
BLANKS   DC    CL80' '             TO CLEAN LINE
WORK6    DS    CL6                 WORK FIELD
MSG1     DC    CL133' 01: PARTITION NOT FOUND...'
MSG3     DC    CL80'TRAN  PROGRAM    TNUM   TERM   STAT   CURR   OPID  X
                DI     TCA'
MSG4     DC    CL80' '
MSG4A    EQU   MSG4,4
MSG4B    EQU   MSG4+6,8
MSG4C    EQU   MSG4+16,5
MSG4D    EQU   MSG4+24,4
MSG4E    EQU   MSG4+31,4
MSG4F    EQU   MSG4+38,4
MSG4G    EQU   MSG4+46,3
MSG4H    EQU   MSG4+52,2
MSG4I    EQU   MSG4+56,6
COMREG   MAPCOMR
SYSCOM   SYSCOM
         COPY  DFHCSADS            CICS CSA DSECT
         COPY  DFHTCADS            CICS TCA DSECT
         END   TSXSYSMC


James Young
Technical Support Manager
Reader's Digest Association (Canada)    ) Xephon 1987





























































