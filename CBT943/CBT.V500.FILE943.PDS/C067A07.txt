Using a Node Error Program to re-route print

The following Node Error Program was written because we
have two dial-up ports defined to our system.  Customers dial
in to place orders and check on the availability/price of
inventory.  The system allows these customers to print a copy
of the shipping request and/or order status, if they have a
printer attached to their PC or ASCII terminal.

A problem occurs when the requested printer becomes
unavailable.  If the user is disconnected before the printer
becomes available the next customer that connects to that same
port receives the last customer's printout.

By using the following Node Error Program I can scan the
AID/ICE chain off the TCT System Entry whenever the error
messages 'DFH2410 - NODE UNRECOVERABLE',
'DFH3462 - SESSION TERMINATED', and 'DFH3472 -
DEVICE END RECEIVED' occur.  If an ICE is found for a
dial-in printer I change the ICE terminal-id to that of a printer
in our computer room.  This forces the printout to our printer
and avoids a lot of confusion and telephone calls from our
customers.  This was generated under VSE/SP and CICS 1.6
but should work with other releases.


THE NODE ERROR PROGRAM

/* THE FOLLOWING JOB CONTAINS THE NODE ERROR PROGRAM CREATED
/* TO ELIMINATE DEALERNET PRINTOUT INADVERTENTLY BEING PRINTED TO
/* THE NEXT DEALER CONNECTED AS THE SAME TERMINAL/PRINTER.
/* JOB 2 CONTAINS THE NODE ERROR TABLE ASSOCIATED WITH DFHZNEP.
/* IT IS UNCLEAR WHETHER YOU CAN USE A CICS-PROVIDED SAMPLE NEP WITHOUT
/* A NODE ERROR TABLE.  NORMAL PRACTICE IS TO CODE A VERY SIMPLE ONE.
         DFHSNEP TYPE=INITIAL,    THIS IS DFHZNEP                      X
               NETNAME=DFHNET,                                         X
               NAME=DFHZNEP
         COPY  DFHAIDDS
         DFHSNEP TYPE=DEF3270
* CODE D1 =  DFH2410 - NODE UNRECOVERABLE
* CODE 49 =  DFH3462 - SESSION TERMINATED
* CODE 9E =  DFH3472 - DEVICE END RECEIVED
         DFHSNEP TYPE=ERRPROC,GROUP=03,                                X
               CODE=(D1,49,9E)
* SAVE ENTRY REGISTERS
AIDCBAR  EQU   R7                  USE REG 7 AS AID BASE
         ST    R14,TWAEPRS
         BALR  CSBASE,0
         USING *,CSBASE
GETITON  EQU    *
         CLC   TCTTETI(2),=C'DI'  IS THIS A DIAL UP TERMINAL ?
         BNE   NOAIDS             NO... RETURN
         LR    R6,TCTTEAR         SAVE TCTTE ADDRESS
         L     TCTTEAR,TCTTEIST   LOAD ADDR OF TCTSE
         L     AIDCBAR,TCSESUSF   LOAD ADDR OF FIRST AID
         LTR   AIDCBAR,AIDCBAR    CHECK IF ANY AIDS
         BZ    NOAIDS             ZERO... CLEAN BILL OF HEALTH
         LR    TCTTEAR,R6         RESTORE TCTTE ADDRESS
LOOP     EQU   *
         CLC   TCTTETI(4),=C'DI11' IS THIS DIAL UP TERMINAL 1?
         BNE   CK21               NO... CHECK IF DI21
         CLC   AIDTRMID,=C'DI1Z'  IS THIS AID FOR DIAL IN PRINTER?
         BNE   BUMP               NO... GET NEXT AID
         MVC   AIDTRMID,=C'COLP'  YES... MOVE IN COLP DESTINATION
         WTO   'DFHZNEP AIDTRMID DI1Z - PRINT ON COLP'
         B     BUMP
CK21     EQU   *
         CLC   TCTTETI(4),=C'DI21' IS THIS DIAL UP TERMINAL 2?
         BNE   NOAIDS             NO... RETURN
         CLC   AIDTRMID,=C'DI2Z'  IS THIS AID FOR DIAL IN PRINTER?
         BNE   BUMP               NO... GET NEXT AID
         MVC   AIDTRMID,=C'COLP'  YES... MOVE IN COLP DESTINATION
         WTO   'DFHZNEP AIDTRMID DI2Z - PRINT ON COLP'
BUMP     EQU   *
         L     AIDCBAR,AIDCHNF    LOAD NEXT AID
         CLI   AIDSAA,X'86'       AID SAA?
         BE    LOOP               YES... CHECK IT OUT
         CLI   AIDSAA,X'87'       ICE SAA?
         BE    LOOP               YES... CHECK IT OUT
NOAIDS   EQU   *                  NO... RETURN
         L     R14,TWAEPRS
         BR    R14
         LTORG
         DFHSNEP TYPE=FINAL
         END   DFHNEPNA


NODE ERROR TABLE

* SET UP NEP TO PURGE PRT TASKS FROM DEALERNET TERMINALS WHEN
* TERMINAL ERROR OCCURS.
         DFHSNET NAME=DFHNET,                                          X
               COUNT=100,                                              X
               NEBS=10,                                                X
               TIME=(1,MIN)
               END


Mark Johnson
Software Support Manager
The W W Williams Co

