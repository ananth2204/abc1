CICS sign-on security (continued)

This month we conclude our look at a CICS security system
with the publication of its remaining components.


DFHXSE - EXTERNAL SECURITY EXIT

This IBM exit is the heart of the security system and is
described in CICS/VS 1.7 Customization Guide SC33-0131.
The manual describes how to access the data on the sign-on
screen. You must use the same translation table as the batch
program to ensure proper password encryption.

***********************************************************************
* DFHXSE IS A SEPARATE MODULE CALLED BY DFHXSP.  IT MUST ESTABLISH ITS*
* OWN ADDRESSABILITY AND ADDRESSABILITY TO ANY AREAS IT REQUIRES.  IT *
* MUST SAVE REGISTERS 3 TO 13.  ON ENTRY TO DFHXSE REGISTER 1 POINTS  *
* TO THE PARAMETER LIST AND REGISTER 2 ADDRESSES AN 18-WORD SAVE AREA *
* FOLLOWED BY A 96-BYTE WORK AREA.  REGISTER 12 POINTS TO THE TCA AND *
* REGISTER 13 TO THE CSA.  REGISTER 15 CONTAINS THE ENTRY POINT       *
* ADDRESS, AND REGISTER 14 CONTAINS THE RETURN ADDRESS WITHIN DFHXSP  *
* TO WHICH CONTROL MUST BE RETURNED.  APPROPRIATE RETURN CODES, AS    *
* DESCRIBED FOR DFHXSP, SHOULD BE SET IN REGISTERS 0 AND 15, AND THE  *
* MODULE SHOULD FINISH BY BRANCHING TO THE CONTENTS OF REGISTER 14.   *
* NOTE THAT THIS IS A NEW INTERFACE FOR ESM CALLS AT REL 1.7.         *
* REQUEST CODES PASSED FROM DFHXSP IN THE PARAMETER LIST :            *
*       0 - INITIALIZATION CALL                                       *
*       4 - SIGN-ON WITH PASSWORD                                     *
*       8 - SIGN-ON WITHOUT PASSWORD                                  *
*      12 - RESOURCE CHECK CALL                                       *
*      16 - SIGN-OFF                                                  *
*    REGISTER USAGE R0  -- RETURN CODE ON EXIT                        *
*                   R1  -- POINTER TO PARAMETER LIST ON ENTRY         *
*                       -- AFTER ENTRY UNRELIABLE, WTO ETC            *
*                   R2  -- ADDRESS OF PASSED SAVE AREA ON ENTRY       *
*                       -- LOOP CONTROL FOR MONTH DECREMENT           *
*                       -- USED TO PICK UP LENGTH OF USERID           *
*                       -- MODIFY MOVE INSTRUCTIONS                   *
*                   R3  -- BASE OF TD Q OUTPUT AREA                   *
*                   R4  -- BASE OF FILE WORK AREA (FWA)               *
*                   R5  -- BASE OF TERMINAL I/O AREA                  *
*                   R6  -- BASE FOR TCT                               *
*                   R7  -- BASE OF PASSED SECURITY DSECT (DFHSECDS)   *
*                   R8  -- ADDRESS OF OLD PSWD, NEW PSWD, USERID      *
*                   R9  -- ADDRESS OF PASSED REGISTER SAVE AREA       *
*                   R10 -- BASE FOR SAA WORK AREA                     *
*                   R11 -- BASE REGISTER                              *
*                   R12 -- CICS TCA                                   *
*                   R13 -- CICS CSA                                   *
*                   R14 -- RETURN ADDRESS                             *
*                   R15 -- RETURN CODE ON EXIT                        *
***********************************************************************
BASEREG  EQU   11
TCTTEAR  EQU   6
CSAOPREG EQU   9
*        DSECTS
         DFHPRINT DSCT=START
         COPY  DFHCSADS
         DFHEJECT
         DFHTCA CICSYST=YES
FWACBAR  EQU   4                    ASSGN BASE REG FOR FWA
         COPY  DFHFWADS             SYMBOLICALLY DEFINE FWA
PSWRECIN DS    CL80
TDOABAR  EQU   3                   ASSGN TDQ REGISTER FOR PROGRAM
         COPY DFHTDOA
TERMDATA DS    CL4                 TRANS DATA TO PASS TERMID ONLY
TIOABAR  EQU   5                   ASSGN REGISTER FOR TIOA
         COPY DFHTIOA              COPY FOR TIOA
         COPY TSC999M              COPY FOR SECURITY HELP MAP
SAACBAR  EQU   10
         COPY DFHSAADS             COPY FOR SAA WORK AREA
SAASTRT  EQU   *                   START ADDRESS OF WORK AREA
*                                  KSDS FILE LAYOUT
PSWRECD  DS    0CL80                 DEFINE RECORD AREA
PSFUSRID DS    CL20                  USER ID WILL PROBABLY BE KEY
PSFOPID  DS    CL3                   OP IDENT MAY BE USED INSTEAD
PSFPSWD  DS    CL8                   CURRENT PASSWORD
PSFLAST  DS    CL8                   1ST PREVIOUS PASSWORD
PSF2LAST DS    CL8                   2ND PREVIOUS PASSWORD
PSFUDATE DS    CL8                   DATE LAST USED SUCCESSFULLY
PSFCDATE DS    CL8                   DATE OF LAST CHANGE OF PSWD
PSFSNATT DS    PL2                   NUMBER OF SIGN-ON ATTEMPTS
PSFDDATE DS    CL8                   DATE ID WAS DISABLED
PSFILLR  DS    CL7                   SPACE FOR FUTURE EXPANSION
*                                  OTHER WORK FIELDS
USIDKEY  DS    CL20                  STORAGE AREA FOR KEY TO PSWD FILE
INITKEY  DS    CL20                  SPACES USED TO INITIALIZE USIDKEY
USIDFMTD DS    0CL6                  GROUP NAME FOR REFMTD DATE CHG
USIDYY   DS    CL2                     TWO CHARACTER YEAR
USIDMM   DS    CL2                     TWO CHARACTER MONTH
USIDDD   DS    CL2                     TWO CHARACTER DAY
*                                  OTHER WORK FIELDS
SSYSDATE DS    CL8                 SYSTEM DATE SAVED AT INITIALIZATION
SSYSFMTD DS    0CL6                GROUP NAME FOR REFORMATTED SYS DATE
SSYSYY   DS    CL2                   TWO CHARACTER YEAR
SSYSMM   DS    CL2                   TWO CHARACTER MONTH
SSYSDD   DS    CL2                   TWO CHARACTER DAY
SYSMMP   DS    PL2                 PACKED MONTH FOR SUBTRACTION RTNE
SYSYYP   DS    PL2                 PACKED YEAR FOR SUBTRACTION RTNE
LTERMID  DS    CL4                 4 CHAR FIELD FOR TERMID FOR TDQ
NUM      DS    CL3                 3 CHAR FIELD UNPACK SIGNON ATTEMPTS
SAALNGT  EQU   *-SAASTRT           SAA LENGTH FOR GETMAIN
         DFHSEC TYPE=DSECT
         DFHTCTZE CICSYST=YES
         DFHPRINT DSCT=END
DFHXSE   CSECT
         STM   R14,R12,12(R2)      SAVE REGISTERS IN PASSED SAVE AREA
         LA    BASEREG,8(0,R15)    LOAD ADDRESS INTO BASE REGISTER
         USING *,BASEREG           SPECIFY BASE REGISTER FOR THIS PGM
         USING DFHSECDS,R7         ASSIGN BASE REGISTER FOR SEC AREA
         LR    R7,R1               LOAD ADDRESS OF PARM AREA TO BASE
         ST    R13,4(0,R2)
         LR    R9,R2
         DFHSC TYPE=GETMAIN,INITIMG=00,NUMBYTE=SAALNGT,CLASS=USER
         L     SAACBAR,TCASCSA     ESTABLISH ADDRESSABILITY OF SAA WOR
         MVC   PSWRECD,SPACE80     INITIALIZE STORAGE FOR PSW RECD
         MVC   PSFSNATT,PACK0          "         "    FOR SIGNON ATTMP
         MVC   USIDKEY,SPACE20         "         "    FOR USER KEY
         MVC   INITKEY,SPACE20         "         "    FOR FILE KEY
         MVC   USIDFMTD,SPACE6         "         "    FOR DATE FIELDS
         MVC   LTERMID,SPACE4          "         "    FOR TRMID DSPLY
         MVC   NUM,SPACE3              "         "    FOR # ATTMPT DSP
         MVC   SSYSDATE,SPACE8         "         "    FOR SYSTEM DATE
         MVC   SSYSFMTD,SPACE6         "         "    FOR FORMATED DAT
         MVC   SYSMMP,PACK0            "         "    FOR PACKED MONTH
         MVC   SYSYYP,PACK0            "         "    FOR PACKED YEAR
         XR    R15,R15             ENSURE R15 IS ZEROED OUT
         IC    R15,SECREQ          LOAD REQUEST CODE TO REGISTER
         L     FWACBAR,TCAFCAA     ESTABLISH ADDRESABILITY FOR FWA
         L     TIOABAR,TCAFCAAA    ESTABLISH ADDRESABILITY FOR TIOA
         B     SELECT(R15)         JUMP TO CORRECT SECTION
SELECT   B     INIT                INITIALIZATION        REQ 0
         B     SGONP               SIGN-ON + PASSWORD    REQ 4
         B     SGON                SIGN-ON - PASSWORD    REQ 8
         B     EXCHK               RESOURCE CHECK        REQ 12
         B     SGOF                SIGN-OFF              REQ 16
         B     REQ20               SIGN-OFF AFTER T/O    REQ 20
         B     REQ24               RETURN USER-ID        REQ 24
         B     REQ28               WAIT FOR INIT         REQ 28
         B     REQ32               RETURN T/O VALUE      REQ 32
         B     REQ36               BUILD PSEUDO SNTTE    REQ 36
         B     REQ40               DELETE PSEUDO SNTTE   REQ 40
         B     REQ44               REBUILD RSRCE PROF    REQ 44
         B     REQ48               INTERNAL INIT CALL    REQ 48
EXCHK    DS    0H
         B     GOODRET             NO CHECKING, SO GOODRET
INIT     DS    0H                  INITIALIZATION CALL
TSTOPEN  DFHFC TYPE=CHECK,NOTOPEN=OPNFAIL
OPENOK   DS    0H                  ..GOOD OPEN TO GOODRET
         B     GOODRET             EXIT FROM OPEN ROUTINE
OPNFAIL  DS    0H                  OPEN ERROR OF ANY KIND
FAILMSG  DFHWTO '** FAILURE HAS OCCURRED DURING SECURITY INITIALIZATIONX
                -- INFORM TECHNICAL SUPPORT IMMEDIATELY **'
         LA    R0,X'63'            USER RESPONSE CODE X'63' - 99
         LA    R15,X'18'           EXTERNAL SECURITY PROBLEM X'18' 24
         B     RETURN              GO TO RETURN
SGONP    DS    0H
*    PROCESS DATE TO CHECK EXPIRED PASSWORDS AND SAVE FOR FILE UPDATES
         COMRG                     GET COMMUNICATIONS AREA
         MVC   SSYSDATE,0(R1)      GET SYSTEM DATE
         MVC   SSYSYY,SSYSDATE+6   REFORMAT DATE INTO YY MM DD WITH
         MVC   SSYSMM,SSYSDATE     NO SLASHES
         MVC   SSYSDD,SSYSDATE+3
         PACK  SYSMMP,SSYSMM       PACK MONTH INTO A TEMP FIELD
         PACK  SYSYYP,SSYSYY       PACK YEAR INTO A TEMP FIELD
         LA    R2,X'03'            SET LOOP COUNTER TO THREE
MTHLOOP  DS    0H                  LOOP FOR MONTH REDUCTION
         SP    SYSMMP,PACK1        SUBTRACT ONE FROM MONTH
         BNZ   CARRYON             IF NOT ZERO BRANCH TO CONTINUE
         MVC   SYSMMP,PACK12       RESET MONTH TO TWELVE
         SP    SYSYYP,PACK1        SUBTRACT ONE FROM YEAR
CARRYON  DS    0H                  CONTINUE
         BCT   R2,MTHLOOP          REDUCE LOOP COUNT BY 1 AND BRANCH
         UNPK  SSYSMM,SYSMMP       UNPACK CALCULATED MONTH
         OI    SSYSMM+1,B'11110000' ENSURE MONTH HAS ZONE F
         UNPK  SSYSYY,SYSYYP       UNPACK CALCULATED YEAR
         OI    SSYSYY+1,B'11110000' ENSURE YEAR HAS ZONE F
         L     TCTTEAR,TCAFCAAA    LOAD TCTTE ADDRESS INTO R6
         MVC   LTERMID,TCTTETI     GET TERM-ID FROM TCTTE
* START SIGN-ON PROCEDURE HERE
         L     R8,SECUID           LOAD ADDR OF USER ID FROM PARM LST
         MVC   USIDKEY,INITKEY     INITIALIZE USER ID KEY
*  GET LENGTH OF USERID AND MODIFY MVC INSTR TO ONLY MOVE CHARACTERS
*  AND NOT THE BINARY ZEROS PADDING OUT THE FIELD
         IC    R2,0(0,R8)          PICK UP LENGTH OF USERID
         BCT   R2,LENZERO          SUBTRACT 1 FROM R2
LENZERO  STC   R2,MVUSID+1         INSERT LENGTH INTO MOVE INSTR
MVUSID   MVC   USIDKEY,1(R8)       MOVE USERID ONLY NO PADDING CHARS
*  READ THE PASSWORD FILE USING THE USERID AS THE KEY
GETRECD  DFHFC TYPE=GET,DATASET=DFHCAJS,RDIDADR=USIDKEY,               X
               TYPOPER=UPDATE,ARGTYP=KEY,SRCHTYP=FKEQ,                 X
               ERROR=READERR
         L     FWACBAR,TCAFCAA     ESTABLISH ADDRESABILITY FOR FWA
         MVC   PSWRECD,PSWRECIN    MOVE FWA RECORD TO STORAGE
         L     R8,SECOPSW          LOAD ADDR OF OLD PSWD FROM PRM LST
*  USER ID CURRENTLY DISABLED SIGN-ON ATTEMPTS > 4
         CP    PSFSNATT,PACK5      CHECK NUMBER OF INVALID SIGN-ONS
         BL    CONTON              LESS THAN 5, CARRY ON WITH ATTEMPT
         AP    PSFSNATT,PACK1      ADD 1 TO INVALID SIGN-ON ATTEMPTS
         LA    R15,X'10'           SET RC INVALID SIGN-ON
         MVC   PSFDDATE,SSYSDATE   SAVE DATE ID WAS DISABLED
         MVC   TERM1(4),LTERMID    MOVE TERMID INTO DISPLAY MESSAGE
         MVC   NAME1(20),USIDKEY   MOVE ENTERED NAME INTO DISPLAY MSG
         UNPK  NUM,PSFSNATT        UNPACK ATTEMPTS INTO DISPLAY MSG
         OI    NUM+2,B'11110000'   ENSURE DISPLAY ATTEMPTS ZONE F
         MVC   NUM1(3),NUM         MOVE UNPACKED SIGN-ON ATTEMPTS MSG
WTOM1    DFHWTO '### DISABLED ID ATTEMPTING SIGN-ON --- SECURITY BREACHX
                --- CHECK PROCEDURE MANUAL IMMEDIATELY ###'
WTOM2    DFHWTO '###                      TRML      TRY     ###'
NAME1    EQU   WTOM2+12            DEFINE NAME IN MSG 2
TERM1    EQU   WTOM2+38            DEFINE TERMID IN MSG 2
NUM1     EQU   WTOM2+47            DEFINE NUMBER OF ATTEMPTS IN MSG 2
         MVC   NAME1(20),SPACE20   RE-INIT DISPLAY FIELD
         MVC   TERM1(4),SPACE4     RE-INIT DISPLAY FIELD
         MVC   NUM1(3),SPACE3      RE-INIT DISPLAY FIELD
         MVC   PSWRECIN,PSWRECD    MOVE FWA RECORD TO STORAGE
         ST    FWACBAR,TCAFCAA     STORE ADDR OF FWA TO BE WRITTEN
WRITER1  DFHFC TYPE=PUT,RDIDADR=USIDKEY,TYPOPER=UPDATE,                X
               ERROR=READERR
         MVC   PSWRECD,SPACE80     RE-INIT RECORD WORK AREA TO HIDE IT
*      WRITE TRANSIENT DATA QUEUE WITH TERM ID TO BE DISABLED
         DFHSC TYPE=GETMAIN,CLASS=TRANSDATA,NUMBYTE=8
         L     TDOABAR,TCASCSA     SET UP ADDRESS OF TRANSDATA STORAGE
         MVC   TDOAVRL,=X'00080000' LOAD UP LLBB BYTES FOR LENGTH
         MVC   TERMDATA,LTERMID    SET UP TERMID IN TDQ AREA
*      WRITE TD QUEUE RECORD
         DFHTD TYPE=PUT,DESTID=TDIS,TDADDR=TDOAVRL
         XR    R0,R0               ENSURE R0 IS INITIALIZED
         LA    R15,X'10'           SET INVALID SIGN-ON RETURN CODE
         B     RETURN
CONTON   DS    0H                  CONTINUE FOR MORE PROCESSING
*      NOTE DSPLCMNT OF 1 TO BYPASS LENGTH ATTRIBUTE ON PSWD
         TR    PSFPSWD,LINEUPA     TRANSLATE PSWD
         CLC   1(8,R8),PSFPSWD     COMPARE ENTERED PSWD WITH FILE PSWD
         BNE   SGNERR              PASSWORD DOES NOT MATCH
MATCHPSW DS    0H
         L     R8,SECNPSW          LOAD ADDR OF NEW PSWD FROM PARM LST
*  NOTE DSPLCMNT OF 1 TO BYPASS LENGTH ATTRIBUTE ON NEW PSWD
         CLI   1(R8),X'40'         CHECK 1ST CHAR NON BLANK
         BE    NOCHGPSW            NO NEW PASSWORD ENTERED
*  CHECK LENGTH OF PASSWORD TO BE AT LEAST 5 CHARACTERS
         CLC   0(1,R8),BINARY4     COMPARE LENGTH BYTE TO '04'
         BNH   NOTCHGR              LENGTH IS 4 OR LESS
         CLC   1(8,R8),PSFPSWD     COMPARE NEW PSWD TO CURRENT PSWD
         BE    NOTCHGR              RELEASE RECORD AND PASS MSG
         CLC   1(8,R8),PSFLAST     COMPARE NEW PSWD TO PREVIOUS PSWD
         BE    NOTCHGR              RELEASE RECORD AND PASS MSG
         CLC   1(8,R8),PSF2LAST    COMPARE NEW PSWD 2ND PREVIOUS PSWD
         BE    NOTCHGR              RELEASE RECORD AND PASS MSG
         MVC   PSF2LAST,PSFLAST    SAVE PREVIOUS TO 2ND PREVIOUS
         MVC   PSFLAST,PSFPSWD     SAVE PASSWORD TO PREVIOUS
*  NOTE DSPLCMNT OF 1 TO BYPASS LENGTH ATTRIBUTE ON NEW PSWD
         MVC   PSFPSWD,1(R8)       MOVE NEW TO PSWD RECORD
         MVC   PSFCDATE,SSYSDATE   STORE DATE PSWD CHANGED
*  IF PASSWORD IS BEING CHANGED AN UPDATE TO FILE WILL OCCUR
         B     COMMRET             GO TO COMMON SIGN-ON RETURN
NOCHGPSW DS    0H
         MVC   USIDYY,PSFCDATE+6   REFORMAT DATE OF LAST CHANGE OF
         MVC   USIDMM,PSFCDATE     THE PASSWORD
         MVC   USIDDD,PSFCDATE+3
         CLC   USIDFMTD,SSYSFMTD   COMPARE RFMTD USR DATE AND SYS DATE
         BH    COMMRET             IF NOT LESS OR EQUAL GO TO RETURN
NOTCHGR  DS    0H                  ATTEMPT TO CHANGE TO PREVIOUS
*                                   MUST RELEASE RECORD
         AP    PSFSNATT,PACK1      ADD 1 TO INVALID SIGNON ATTEMPTS
         MVC   0(9,R8),RNWPSWD     RESET PASSED NEW PASSWORD FIELD
         LA    R15,X'04'           SET R15 FOR NEW PASSWORD REQUIRED
         MVC   TERM2(4),LTERMID    MOVE TERMID INTO DISPLAY MESSAGE
         MVC   NAME2(20),USIDKEY   MOVE ENTERED NAME INTO DISPLAY MSG
         UNPK  NUM,PSFSNATT        UNPACK ATTEMPTS INTO DISPLAY MSG
         OI    NUM+2,B'11110000'   ENSURE DISPLAY ATTEMPTS ZONE F
         MVC   NUM2(3),NUM         MOVE UNPACKED SIGN-ON ATTEMPTS MSG
WTOM3    DFHWTO '** INVALID CHANGE OF PASSWORD ATTEMPTED **'
WTOM4    DFHWTO '***                      TRML      TRY     ***'
NAME2    EQU   WTOM4+12            DEFINE NAME IN MSG 2
TERM2    EQU   WTOM4+38            DEFINE TERMID IN MSG 2
NUM2     EQU   WTOM4+47            DEFINE NUMBER OF ATTEMPTS IN MSG 2
         MVC   NAME2(20),SPACE20   RE-INIT DISPLAY FIELD
         MVC   TERM2(4),SPACE4     RE-INIT DISPLAY FIELD
         MVC   NUM2(3),SPACE3      RE-INIT DISPLAY FIELD
         L     TCTTEAR,TCAFCAAA    ESTABLISH ADDRESS FOR TCTTE
         DFHSC TYPE=GETMAIN,NUMBYTE=TSC999MT-TIOADBA,                  X
               CLASS=TERMINAL,INITIMG=00   GET STORAGE FOR MAP
         L     TIOABAR,TCASCSA     ESTABLISH ADDRESS FOR TIOA
         DFHBMS TYPE=(OUT,WAIT,ERASE),MAP=SECHELP,MAPSET=TSC999M,      X
               CTRL=(FREEKB,FRSET) DISPLAY THE MAP
         DFHBMS TYPE=IN,MAP=SECHELP,MAPSET=TSC999M
         L     TIOABAR,TCTTEDA     RE-ESTABLISH TIOA ADDRESS
         B     BAILOUT             GO TO RETURN SYSTEM FAILURE
COMMRET  DS    0H                  COMMON GOOD SIGN-ON RETURN
         MVC   PSFSNATT,=P'000'    RE-INITIALIZE SIGN-ON ATTEMPTS
         LA    R0,X'00'            SET UP SUCCESSFUL EXECUTION
         LA    R15,X'00'           SET UP SUCCESSFUL EXECUTION
         MVC   PSFUDATE,SSYSDATE   GET SYSTEM DATE STORED ON ENTRY
         B     BAILOUT
SGNERR   DS    0H                  SIGN ON ERROR, PSWD NOT MATCHED
         AP    PSFSNATT,PACK1      ADD 1 TO INVALID SIGNON ATTEMPTS
         LA    R15,X'10'           SET RESPONSE INVALID SIGNON
         MVC   TERM3(4),LTERMID    MOVE TERMID INTO DISPLAY MESSAGE
         MVC   NAME3(20),USIDKEY   MOVE ENTERED NAME INTO DISPLAY MSG
         UNPK  NUM,PSFSNATT        UNPACK ATTEMPTS INTO DISPLAY MSG
         OI    NUM+2,B'11110000'   ENSURE DISPLAY ATTEMPTS ZONE F
         MVC   NUM3(3),NUM         MOVE UNPACKED SIGN-ON ATTEMPTS MSG
WTOM5    DFHWTO '** SIGN-ON FAILURE -- INVALID PASSWORD ENTERED -- WATCX
                H FOR MORE ATTEMPTS **'
WTOM6    DFHWTO '***                      TRML      TRY     ***'
NAME3    EQU   WTOM6+12            DEFINE NAME IN MSG 2
TERM3    EQU   WTOM6+38            DEFINE TERMID IN MSG 2
NUM3     EQU   WTOM6+47            DEFINE NUMBER OF ATTEMPTS IN MSG 2
         MVC   NAME3(20),SPACE20   RE-INIT DISPLAY FIELD
         MVC   TERM3(4),SPACE4     RE-INIT DISPLAY FIELD
         MVC   NUM3(3),SPACE3      RE-INIT DISPLAY FIELD
BAILOUT  DS    0H                  COMMON EXIT POINT
         TR    PSFPSWD,LINEUPA     TRANSLATE PSWD BEFORE WRITE
         MVC   PSWRECIN,PSWRECD    MOVE FWA RECORD TO STORAGE
         ST    FWACBAR,TCAFCAA     STORE ADDR OF FWA TO BE WRITTEN
WRITER2  DFHFC TYPE=PUT,RDIDADR=USIDKEY,TYPOPER=UPDATE,                X
               ERROR=READERR
         MVC   PSWRECD,SPACE80     RE-INIT RECORD WORK AREA TO HIDE IT
         LA    R0,X'00'            INIT R0 FOR COMPARISON
         CR    R0,R15              COMPARE R15 TO R0
         BE    GOODRET             RETURN CODE 0, GOOD RETURN
         B     RETURN              ELSE GO TO RETURN
READERR  DS    0H                  RECORD NOT FOUND ON READ FOR USERID
         ST    FWACBAR,TCAFCAA     STORE ADDR OF FWA TO BE RELEASD
         DFHFC TYPE=RELEASE        FREE UP RECORD ON READ ERROR
         MVC   TERM4(4),LTERMID    MOVE TERMID INTO DISPLAY MESSAGE
         MVC   NAME4(20),USIDKEY   MOVE ENTERED NAME INTO DISPLAY MSG
WTOM7    DFHWTO '** INVALID SIGN-ON ATTEMPT- UNAUTHORIZED USER-ID -- WAX
                TCH FOR MORE ATTEMPTS -- CHECK SECURITY MANUAL **'
WTOM8    DFHWTO '***                      TRML      ***'
NAME4    EQU   WTOM8+12            DEFINE NAME IN MSG 2
TERM4    EQU   WTOM8+38            DEFINE TERMID IN MSG 2
         MVC   NAME4(20),SPACE20   RE-INIT DISPLAY FIELD
         MVC   TERM4(4),SPACE4     RE-INIT DISPLAY FIELD
         MVC   PSWRECD,SPACE80     RE-INIT RECORD WORK AREA TO HIDE IT
         LA    R15,X'10'           TREAT AS SIGNON ERROR
         B     RETURN              SYSTEM FAILURE
OTHRERR  DS    0H                  ALL OTHER ERRORS ON READ FOR USERID
         MVC   PSWRECD,SPACE80     RE-INIT RECORD WORK AREA TO HIDE IT
         LA    R15,X'18'           TREAT AS SECURITY FAILURE
         B     RETURN              SYSTEM FAILURE
SGON     DS    0H
         LA    R0,X'00'
         LA    R15,X'10'
         B     RETURN
SGOF     DS    0H
         B     GOODRET
GOODRET  DS    0H
         LA    R0,X'00'
         LA    R15,X'00'
RETURN   DS    0H
         LM    R1,R12,24(R9)
         L     R14,12(0,R2)
         BR    R14
* * * OTHER REQUEST CODES, SHOULD NEVER BE EXECUTED * * *
REQ20    DS    0H
         DFHWTO '*** INVALID REQUEST PASSED CODE 20 ***'
         B     GOODRET
REQ24    DS    0H
         DFHWTO '*** INVALID REQUEST PASSED CODE 24 ***'
         B     GOODRET
REQ28    DS    0H
         DFHWTO '*** INVALID REQUEST PASSED CODE 28 ***'
         B     GOODRET
REQ32    DS    0H
         DFHWTO '*** INVALID REQUEST PASSED CODE 32 ***'
         B     GOODRET
REQ36    DS    0H
         DFHWTO '*** INVALID REQUEST PASSED CODE 36 ***'
         B     GOODRET
REQ40    DS    0H
         DFHWTO '*** INVALID REQUEST PASSED CODE 40 ***'
         B     GOODRET
REQ44    DS    0H
         DFHWTO '*** INVALID REQUEST PASSED CODE 44 ***'
         B     GOODRET
REQ48    DS    0H
         DFHWTO '*** INVALID REQUEST PASSED CODE 48 ***'
         B     GOODRET
*           PROGRAM CONSTANTS
PACK0    DC    PL2'0'              PACKED CONSTANT ZERO
PACK1    DC    PL2'+1'             PACKED CONSTANT ONE
PACK5    DC    PL2'+5'             PACKED CONSTANT FIVE
PACK12   DC    PL2'+12'            PACKED CONSTANT TWELVE
BINARY0  DC    XL4'00000000'       HEX FIELD LENGTH 4, ALL ZEROES
BINARY4  DC    XL1'04'             HEX FIELD LENGTH 4, PSWD LENGTH
SPACE3   DC    CL3' '              3  SPACES FOR INIT OF WORK AREA
SPACE4   DC    CL4' '              4  SPACES FOR INIT OF WORK AREA
SPACE6   DC    CL6' '              6  SPACES FOR INIT OF WORK AREA
SPACE8   DC    CL8' '              8  SPACES FOR INIT OF WORK AREA
SPACE20  DC    CL20' '             20 SPACES FOR RE-INIT OF WORK AREA
SPACE80  DC    CL80' '             80 SPACES FOR RE-INIT OF WORK AREA
RNWPSWD  DC    CL9'0        '      RESET NEW PASSWORD LENGTH AND FIELD
*
*    ****** this translation table should be copied from ******
*    ****** DFHASC to ensure it matches exactly          ******
*
LINEUPA  DC    X'000102030405060708090A0B0C0D0E0F'

                        etc

         DC    X'F0F1F2F3F4F5F6F7F8F9FAFBFCFDFEFF'
PSFRECLN DC    F'80'               FULLWORD RECORD LENGTH PARAMETER
PSFKEYLN DC    F'20'               FULLWORD KEY LENGTH PARAMETER
         LTORG
         END


CICS TABLE ENTRIES

All users must still be in the SNT and you must activate
external security by the following:

SNT      DFHSNT TYPE=INITIAL,EXTSEC=YES

The CICS FCT entry for a VSAM KSDS is as follows:

         DFHFCT TYPE=DATASET,DATASET=DFHCAJS,                          X
               ACCMETH=(VSAM,KSDS),SERVREQ=(UPDATE),                   X
               RECFORM=(FIXED,UNBLOCKED),OPEN=INITIAL,                 X
               BUFND=2,BUFNI=2,STRNO=1

The terminal disable program and BMS help map are required
to be in the CICS PPT:

TSC910AO DFHPPT TYPE=ENTRY,PROGRAM=TSC910AO      CEMT DISABLE TERML
TSC999M  DFHPPT TYPE=ENTRY,PROGRAM=TSC999M       SECURITY HELP BMS MAP

A transient data queue is used to execute the terminal disable
function via the following CICS DCT entry:

TDIS     DFHDCT TYPE=INTRA,DESTID=TDIS,DESTFAC=FILE,DESTRCV=NO,        X
               REUSE=YES,TRANSID=TRMD,TRIGLEV=1

Also the transaction started from the DCT entry is as follows:

TRMD     DFHPCT TYPE=ENTRY,TRANSID=TRMD,PROGRAM=TSC910AO,              X
               CLASS=SHORT,SPURGE=YES,DTB=YES,TRNPRTY=100,TCLASS=1


SUMMARY

This simple security system should satisfy many of your
security needs, meet your company auditor's requirements, and
free you from the time and expense of a third-party security
package.

Allan R Kelly
System Software Programmer/Analyst (Canada)

