 Making CRTE user friendly

For users of CICS MRO or ISC, the explicit routing facility (the CRTE
transaction) is an indispensable tool.  It allows a user to simulate transaction
routing, without actually defining the transaction being routed as remote.

It is also a valuable tool for testing;  in fact, it is required in order to use
IBM's EDF interactive debugging aid with remote transactions.  It has many other
uses in the arena of debugging and problem determination, as it allows the user
to connect directly to different CICS systems, and control or manipulate the
resources in each connected region.  Thus a programmer can use CRTE to link to
an application owning region and use CEDF for testing, or to a file owning
region to allocate or de-allocate files, and so forth.

However, since it was designed primarily as a systems programming tool, it is
not very easy to use.  The user is required to know the SYSID of each connected
system.  As the SYSID is primarily an internal name, it is often rather esoteric
and not used by anyone other than systems programmers, except for certain
specialised types of application programming.

Regardless of whether or not it's easy to use, it is in fact used extensively.
What this program tries to do is to add a degree of user friendliness to CRTE.
Firstly, it provides a menu of all connected CICS systems, eliminating the need
to remember the SYSID of each system.  Also, both the SYSID and the VTAM APPLID
for each connected system are displayed.  This menu is obtained by entering the
transaction-id BIND, and is shown in Figure  1.


The user selects the target region by putting an 'X' next to the name, and
hitting enter.  The menu has an added feature:  the system names are all
highlighted, except those that are not currently available.  Those systems
cannot be selected as a target and  the cursor will skip over them.  Thus the
program doubles as an easy way to see quickly which regions are available.  In
this example, CIS1, CIS2, and CIS6 are available;  CIS3, CIS4 and CIS5 are not.

The system will also accept a direct route request.  For example, by simply
entering BIND CIS1 (where CIS1 is the SYSID of the target region), when the
session is established, the following acknowledgment will be sent:

ROUTE REQUEST SUCCESSFUL AND AUTOMATIC SIGN-ON PERFORMED SESSION NOW BOUND TO
SYSTEM - CIS1

Note the phrase 'and automatic sign-on performed'.  This is another feature of
this program that is available if you use CICS internal security facilities.
When you BIND a session to another CICS system, an automatic sign-on will be
done on your behalf.  This is one of the things about CRTE that causes the most
grumbling;  the fact that you are required to sign on to each system you connect
to can be irritating.

Automatic sign-on is very useful if, for example, you want to get from a
terminal owning region to a file owning region, via an intermediate application
owning region; CRTE would require you to CRTE to the application owning region,
sign-on there, then CRTE to the file owning region, and sign on there.

In contrast, BIND will propagate your CICS security information to the target
region, so that you are immediately signed on when you BIND there, a great
timesaver!  You may have wondered why IBM didn't do that.  There is a reason,
and, as I will explain, you may or may not want to use this feature.

The automatic sign-on is performed by passing your security bits from the source
TCTTE to the surrogate TCTTE that is built dynamically in the target region.  If
you have different sign-on tables in the different regions, and people have
different levels of security in different regions, or the security bits have
different meanings in different regions, you may not want this automatic sign-on
to take place.  This is because the security bits will probably have different

meanings in the target CICS region, and you may end up granting a user access to
a resource they would not otherwise have access to.

In this situation, or if you have an external security manager such as RACF, an
alternative is provided.  The program will, after establishing the session with
the target system, return that system's sign-on screen to the user.  While not
as good as an automatic sign-on, this is a simple way to save a little typing,
while still forcing users to sign-on in all regions, and thus ensuring full
security.

At this point, with the session established to the target system, transactions
can be entered, and the session will behave just as a normal CRTE session, and
is subject to the same restrictions.  To terminate a session, enter CANCEL, just
as you would if you had used CRTE.

To install this program, add the following to the PPT:

     DFHPPT TYPE=ENTRY,PROGRAM=BINDPGM

and add the following to the PCT:

     DFHPCT TYPE=ENTRY,TRANSID=BIND,PROGRAM=BINDPGM
     DFHPCT TYPE=ENTRY,TRANSID=BND1,PROGRAM=BINDPGM

If you use RDO, define the entries with their CEDA equivalents.  Note that this
program and these table entries have to be added to all connected systems.

What about the automatic sign-on?  How do you include or exclude this feature?
In the source code, there are conditional assembly statements to control this.
These conditional statements are controlled by the global &AUTOSCN, which is at
the front of the program.  Setting this global to 1 will include the automatic
sign-on feature;  setting it to 0 will exclude it.

Finally, note that with an external security manager, the automatic sign-on is
not performed because propagating the security bits alone would not be
sufficient.  However, depending on the security system you have, it may be
possible to modify this program to perform the sign-on for you (I didn't show an
example here because it is too dependent on the specific security manager).  If
you can determine the user's user-id and password, you can build a sign-on
request, and replace the code at subroutine SUB300 with it.  Note that with an
external security manager, you should not have the potential problem of
inconsistent security bit settings, as a true sign-on would be performed in the
target region.

         COPY  DFHCSADS
         COPY  DFHTCADS
         COPY  DFHTCTFX
         DFHTCTZE CICSYST=YES
         COPY  DFHAID
         COPY  DFHTIOA
         GBLB  &AUTOSCN          AUTOMATIC SIGN-ON SWITCH
&AUTOSCN SETB  1                 ..AUTO SIGN-ON INCLUDED
*AUTOSCN SETB  0                 ..AUTO SIGN-ON EXCLUDED
* MODULE NAME = BINDPGM                                             *
* DESCRIPTIVE NAME = CICS CRTE FRONT-END PROGRAM                    *
* STATUS = 1.6.1                                                    *
* FUNCTION - BINDPGM IS A FRONT-END PROGRAM FOR THE EXPLICIT        *
*  TRANSACTION ROUTING FACILITY OF CICS. ITS PURPOSE IS TO PROVIDE A*
*  USER-FRIENDLY MENU TO HELP OPERATORS DETERMINE WHICH SYSTEMS CAN *
*  BE ROUTED TO. THERE ARE TWO FORMATS -                            *
*                 BIND XXXX - WHERE XXXX IS THE SYSTEM YOU WISH TO  *
*                             ESTABLISH A SESSION TO.               *
*                 BIND ------ THIS WILL RETURN A MENU SHOWING THE   *
*                             CICS SYSTEMS THAT ARE AVAILABLE TO    *
*                             ROUTE TO. ACTIVE SYSTEM NAMES ARE     *
*                             HIGHLIGHTED. A SESSION IS STARTED BY  *
 *                             PLACING AN 'X' NEXT TO THE SYSID YOU  *
*                             WANT.                                 *
*            IF CICS SECURITY IS INSTALLED IN THE TARGET REGION. AN *
*            AUTOMATIC SIGN-ON WILL BE PERFORMED. USING THE SECURITY*
*            INFORMATION FROM THE ORIGINATING SYSTEM. IF THE SIGN-ON*
*            CANNOT BE PERFORMED. THE TARGET REGION'S 'CSSN' SCREEN *
*            WILL BE DISPLAYED.                                     *
*            TO TERMINATE A SESSION, SIMPLY ENTER 'CANCEL'.         *
START    CSECT
         BALR  11,0                ESTABLISH
         USING *,11                   ADDRESSABILITY
         B     L00100              BYPASS EYECATCHER
         DC    CL8'BINDPGM'        MODULE NAME
         DC    CL8'&SYSDATE'       DATE
         DC    CL8'&SYSTIME'       TIME
L00100   L     TCTTEAR,TCAFCAAA    ADDRESS TCTTE
         CLI   TCTTEAID,DFHCLEAR   CLEAR KEY ENTERED ?
         BE    L01000              ..YES - RETURN TO CICS
         L     TIOABAR,TCTTEDA     ADDRESS TIOA
         LA    R15,TIOADBA         ADDRESS INPUT DATA
         LH    R1,TIOATDL          LOAD DATA LENGTH
         CLI   0(R15),X'11'        SBA IN DATA STREAM ?
         BNE   L00200              ..NO  - BYPASS
         LA    R15,3(R15)          ..YES - POINT PAST
         SH    R1,=X'0003'                 CORRECT LENGTH
*******  DETERMINE THE TYPE OF PROCESSING REQUIRED *******
L00200   CLC   0(4,R15),INTTRAN    INTERNAL TRANSID IN DATA STREAM ?
         BNE   L00300              ..NO
*******  INTERNAL CALL. SET SECURITY INFO IN TCTTE, AND RETURN *******
         CLC   4(2,R15),SCFUNC     INTERNAL FUNCTION CALL?
         BNE   L09999              ..NO  -  EXIT
         MVC   TCTTESK,TIOADBA+6   SET SECURITY KEYS
         MVC   TCTESKE,TIOADBA+9   SET EXTENDED SECURITY KEYS
         OI    TCTTETS,TCTTESTA    SHOW TERMINAL ATTENDED
         MVC   TIOADBA(2),=C'OK'   ACKNOWLEDGE
         MVC   TIOATDL=H'2'        SET ACK LENGTH
         B     L09000              GO WRITE
*******  INITIAL CALL. IF DIRECT ROUTE REQUEST,SAVE SYSID AND CONT
L00300   CLC   0(4,R15),TRANID     TRANSID IN DATA STREAM ?
         BNE   L00500              ..NO  - ASSUME MENU INPUT
         CH    R1,=X'0009'         SYSID ENTERED?
         BL    L00400              ..NO
         ICM   R0,15,5(R15)        SAVE SYSID
         B     L00800              CONTINUE
*******  INITIAL CALL. BUILD MENU AND RETURN *******
L00400   BAL   R8,SUB100           GO BUILD MENU
         MVC   TCTTETC,TRANID      MOVE NEXT-TRAN TO TCTTE
         DFHTC TYPE=ERASE          SET ERASE ATTRIBUTE
         B     L09000              GO WRITE AND RETURN
*******  SCAN INPUT FOR SELECTED SYSID ********
L00500   XR    R4,R4               CLEAR
         LH    R5,TIOATDL          LOAD DATA LENGTH
         LA    R2,24               LOAD ENTRY LENGTH
         DR    R4,R2               COMPUTE NUMBER OF ENTRIES
         LTR   R2,R5               ANY ENTRIES?
         BZ    ERROR030            ..NO  - ERROR
         CLI   TIOADBA,X'11'       STARTING SBA?
         BNE   ERROR030            ..NO  - ERROR
         LA    R15,TIOADBA         POINT TO START OF DATA
L00600   OI    3(R15),X'40'        HANDLE LOWERCASE DATA
         CLI   3(R15),X'E7         THIS SYSID SELECTED
         BNE   L00700              ..NO
         ICM   R0,15,7(R15)        MOVE SYSID TO SAVE AREA
         B     L00800              CONTINUE
L00700   LA    R15,24(,R15)        BUMP DATA POINTER
         BCT   R2,L00600           LOOP
          B     ERROR030            ERROR
*******  AT THIS POINT, THE SELECTED SYSTEM ID HAS BEEN EXTRACTED.
*******  NOW WE USE THE SYSID TO SET UP THE ROUTING SESSION *******
L00800   MVC   TCASCNB,=AL2(CRTEMSGL)  MOVE SIZE NEEDED TO TCA
         BAL   R8,SUB200           GO VERIFY TIOA
         MVC   TIOADBA(CRTEMSGL),CRTEMSGS MOVE EMPTY CRTE COMM TO TIOA
         STCM  R0,15,TIOADBA+SYSID-CRTEMSGS  MOVE TARGET SYSID TO TIOA
         MVC   TIOATDL,=AL2(CRTEMSGL)      MOVE COMMAND LENGTH TO TIOA
         DFHPC TYPE=LINK,                                              X
               PROGRAM=DFHRTE      CALL RTE TO START SESSION
         L     TIOABAR,TCTTEDA     INSURE THAT TIOA IS STILL ADDRESSED
         LA    R15,TIOADBA         ADDRESS TIOA DATA
         CLC   0(8,R15),=C'DFH4409I' OK RESPONSE FROM DFHRTE ?
         BNE   L01000              ..NO  - BYPASS
         DFHTC TYPE=WAIT           FORCE PRIOR MESSAGE OUT
         BAL   R8,SUB300           GO DO SIGN-ON <== POSITIONALLY
         B     L09000              GO SEND ACK     | DEPENDANT
         B     L09999              GO RETURN     <== CODE
*******  ERROR DETECTED. RESET CRTE STATUS AND RETURN *******
LO1000   NI    TCTETSU,255-(TCTERTE+TCTERTNT)  RESET CRTE FLAGS
         XC    TCTTETC,TCTTETC    CLEAR OUT NEXT-TRAN FIELD
         XC    TCTESYID,TCTESYID  CLEAR OUT SYSID FIELD
         B     L09999             RETURN TO DRIVER
L09000   DFHTC TYPE=WRITE
L09999   DFHPC TYPE=RETURN
ERROR010 MVC   TIOADBA(ERR10MGL),ERR10MGS  MOVE ERROR MSG TEXT TO TIOA
         MVC   TIOATDL,=AL2(ERR10MGL)    MOVE ERROR MSG LENGTH TO TIOA
         B     L09000              RETURN TO DRIVER
ERROR020 MVC   TIOADBA(ERR20MGL),ERR20MGS  MOVE ERROR MSG TEXT TO TIOA
         MVC   TIOATDL,=AL2(ERR20MGL)    MOVE ERROR MSG LENGTH TO TIOA
         B     L09000              RETURN TO DRIVER
ERROR030 BAL   R8,SUB100           GO REBUILD MENU
         LA    R15,TIOADBA         LOAD START OF DATA
         AH    R15,TIOATDL         POINT PAST END OF DATA
         MVC   0(ERR30MGL,R15),ERR30MGS    MOVE ERROR MSG TEXT TO TIOA
         LH    R15,=AL2(ERR30MGL)  COMPUTE
         AH    R15,TIOATDL            NEW LENGTH
         STH   R15,TIOATDL         SAVE IT
         MVC   TCTTETC,TRANID      RELOAD NEXT-TRAN FIELD
         DFHTC TYPE=ERASE          SET ERASE ATTRIBUTE
         B     L09000              GO WRITE
*******  S C R E E N    B U I L D    S U B R O U T I N E  ********
*******  SCAN THE TCT SYSTEM ENTRIES, AND BUILD A MENU OF AVAILABLE
*******  SYSTEMS THAT CAN BE ROUTED TO  *******
SUB100   L     R1,=A(MENUTXTL+92)  LOAD MINIMUM LENGTH NEEDED
         L     R2,=A(MENUENTL)     LOAD SYSID ENTRY LENGTH
         MH    R2,=H'0040'         MULTIPLY BY MAX ENTRIES
         LA    R1,TRAILERL(R2,R1)  COMPUTE SPACE NEEDED FOR SCREEN
         STH   R1,TCASCNB          SAVE SIZE NEEDED
         LR    R4,R8               SAVE RETURN REGISTER
         BAL   R8,SUB200           GO VERIFY TIOA
         LR    R8,R4               RESTORE RETURN REGISTER
         LA    R15,TIOADBA         ADDRESS DATA AREA
         MVC   0(MENUTXTL,R15),MENUTXTS  MOVE MENU TO TIOA
         LA    R15,MENUTXTL(,R15)  SAVE LENGTH IN ACCUMULATOR
         L     R5,CSATCTBA         LOAD TCT ADDRESS
         LA    R0,40               LOAD LOOP CONTROL
         DFHTC CTYPE=LOCATE,                                         X
               DOMAIN=SYSTEM,                                        X
               TERM=FIRST,                                           X
               LASTTRM=ERROR010,                                     X
               ERROR=ERROR020,                                       X
               NORESP=SUB130
SUB120   DFHTC CTYPE=LOCATE,                                         X
               DOMAIN=SYSTEM,                                        X
               TERM=NEXT,                                            X
                ERROR=ERROR020,                                       X
               LASTTRM=SUB170
SUB130   L     R1,TCATPTA          LOAD TCTTE ADDRESS
         CLC   TCSESID-DFHTCTTE(8,R1),TCTVAPPN-DFHTCTFX(R5)
*                                  IS THIS OUR SYSTEM ENTRY ?
         BE    SUB160              ..YES - CONTINUE SCAN
         CLI   TCSETYPE-DFHTCTTE(R1),TCSETSYS   FULL SYSTEM ENTRY ?
         BNE   SUB160              ..NO  - CONTINUE SCAN
         CLI   TCTTETI-DFHTCTTE(R1),C'@'        SHARED BATCH ENTRY?
         BE    SUB160              ..YES - CONTINUE SCAN
         MVC   0(MENUENTL,R15),MENUENTS  MOVE MENU ENTRY TO TIOA
         MVC   SYSIDENT-MENUENTS(L'SYSIDENT,R15),TCTTETI-DFHTCTTE(R1)
*                                  MOVE SYSTEM ID TO TIOA
         MVC   APPLID-MENUENTS(L'APPLID,R15),TCSESID-DFHTCTTE(R1)
*                                  MOVE VTAM APPLID TO TIOA
         TM    TCTTETS-DFHTCTTE(R1),TCTTESOS SYS ENTRY IN SERVICE ?
         BO    SUB140              ..NO  - DON'T HIGHLIGHT
         TM    TCSEIRCF-DFHTCTTE(R1),TCSEIRNC    SYSTEM CONNECTED ?
         BNO   SUB150              ..YES  - LEAVE HIGHLIGHTED
SUB140   MVI   MENUATT1+1-MENUENTS(R15),X'F1'    DON'T HIGHLIGHT
         MVI   MENUATT2+1-MENUENTS(R15),X'F1'    DON'T HIGHLIGHT
SUB150   LA    R15,MENUENTL(,R15)  POINT AT NEXT FREE SLOT
SIB160   BCT   R0,SUB120           CONTINUE SCAN
SUB170   C     R15,=A(MENUTXTL)    ANY VALID ENTRIES FOUND?
         BE    ERROR010            ..NO  - ERROR
         MVC   0(TRAILERL,R15),TRAILER      MOVE TRAILER TO TIOA
         LA    R15,TRAILER(,R15)   BUMP TIOA POINTER
         SR    R15,TIOABAR         COMPUTE THE
         SH    R15,=AL2(TIOADBA-DFHTIOA) OUTPUT MSG
         STH   R15,TIOATDL                   DATA LENGTH
         BR    R8                  RETURN
*******  T I O A    V E R I F Y    R O U T I N E  *******
*******  VERIFY THAT WE ARE POINTING AT A TIOA, AND THAT IT
*******  IS LARGE ENOUGH. SIZE NEEDED IS PASSED IN TCASCNB  *******
SUB200   L     TIOABAR,TCTTEDA     ADDRESS CURRENT TIOA
         LTR   TIOABAR,TIOABAR     IS THERE ONE?
         BNZ   SUB220              ..YES
         L     TIOBAR,TCTTESC      ADDRESS CHAINED TIOA
         LTR   TIOABAR,TIOABAR     IS THERE ONE?
         BZ    SUB230              ..NO
SUB220   LH    R1,TCASCNB          LOAD SIZE NEEDED
         LA    R1,12(,R1)          ADD SAA LENGTH
         CH    R1,TIOASAL          THIS TIOA BIG ENOUGH?
         BNH   SUB240              ..YES
SUB230   DFHSC TYPE=GETMAIN,CLASS=TERM     GET A NEW ONE
         L     TIOABAR,TCASCSA     ADDRESS TIOA
SUB240   ST    TIOABAR,TCTTEDA     POINT TCTTE AT TIOA
         BR    R8                  RETURN
*******  A U T O M A T I C    S I G N - O N    R O U T I N E *******
*******  INVOKED TO PERFORM AN AUTOMATIC SIGN-ON AFTER CONNECTION
*******  ESTABLISHED TO TARGET REGION. ONLY A CICS SIGN-ON IS
*******  PERFORMED. THIS ROUTINE WILL NOT WORK FOR EXTERNAL SECURITY
*******  MANAGERS.
SUB300   AIF   (NOT &AUTOSCN).NOSCON
         TM    TCTTETS,TCTTESTA    ARE WE SIGNED ON?
         BNO   SUB310              ..NO
         MVC   TIOADBA(4),INTTRAN  MOVE INTERNAL TRANID TO TIOA
         MVC   TIOADBA+4(2),SCFUNC MOVE SIGN-ON FUNCTION CODE
         MVC   TIOADBA+6(3),TCTTESK MOVE IN SECURITY KEYS
         MVC   TIOADBA+9(5),TCTESKE MOVE IN EXTENDED SECURITY KEYS
         MVC   TIOATDL,=H'14'      SET DATA LENGTH
         DFHPC TYPE=LINK,                                              X
               PROGRAM=DFHRTE      SENT REQUEST TO OTHER SYSTEM
         L     TIOABAR,TCTTEDA     ADDRESS TIOA
         CLC   TIOADBA(2),=C'OK'   SIGN-ON SUCCESSFUL?
         BNE   SUB310              ..NO
 *******  AUTOMATIC SIGN-ON DONE. BUILD ACKNOWLEDGMENT AND RETURN *****
         DFHTC TYPE=WAIT           FORCE PRIOR MESSAGE OUT
         MVC   TCASCNB,=AL2(ACKMSGL)   MOVE SPACE NEEDED TO TCA
         LR    R4,R8               SAVE RETURN REGISTER
         BAL   R8,SUB200           GO VERIFY TIOA
         LR    R8,R4               RESTORE RETURN REGISTER
         MVC   TIOADBA(ACKMSGL),ACKMSGS      MOVE ACK MESSAGE TO TIOA
         MVC   TIOADBA+ACKSYSID-ACKMSGS(L'ACKSYSID),TCTESYID
*                                  MOVE SYSID INTO MESSAGE
         MVC   TIOATDL,=Y(ACKMSGL) MOVE MESSAGE LENGTH TO TIOA
         MVC   TCTTETC,=C'CRTE'    SET NEXT-TRAN FIELD
         DFHTC TYPE=ERASE          SET ERASE ATTRIBUTE
         B     SUB320              GO SEND IT
*******  AUTOMATIC SIGN-ON NOT DONE. INVOKE RTE TO DISPLAY TARGET
*******  REGION'S SIGN-ON SCREEN *******
SUB310   DFHTC TYPE=WAIT           FORCE PRIOR MESSAGE OUT
.NOSCON  ANOP
         MVC   TIOADBA(4),=C'CSSN' PUT SIGN-ON TRANID IN TIOA
         MVC   TIOATDL,=H'4'       SET LENGTH
         DFHPC TYPE=LINK,                                              X
               PROGRAM=DFHRTE      SENT REQUEST TO OTHER SYSTEM
         MVC   TCTTETC,=C'CRTE'    SET NEXT-TRAN FIELD
         LA    R8,4(,R8)           RETURN PAST 1'ST BRANCH
SUB320   BR    R8                  RETURN
MENUTXTS DS    0X
         DC    X'1140501D38'
         DC    C'EXPLICIT ROUTING DESTINATION SELECTION SCREEN'
         DC    X'11C15D1DF0'
         DC    C'EACH TARGET SYSTEM''S SYSID/APPLID IS DISPLAYED BELOW'
         DC    X'11C2601DF0'
         DC    C'CURRENTLY AVAILABLE SYSTEMS ARE'
         DC    X'1D38'
         DC    C'HIGHLIGHTED'
         DC    X'1DF0'
         DC    C'- SELECT TARGET SYSTEM WITH AN'
         DC    X'1D38'
         DC    C'''X'''
         DC    X'11C6D11DF0'
MENUTXTL EQU   *-MENUTXTS
MENUENTS DS    0X
MENUATT1 DC    X'1DC9'
         DC    C'_'
MENUATT2 DC    X'1DF9'
SYSIDENT DC    CL4' '
         DC    C'/'
APPLID   DC    CL8' '
         DC    CL4' '
MENUENTL EQU   *-MENUENTS
TRAILER  DS    0X
         DC    X'115CF31DF8'
         AIF   (NOT &AUTOSCN).ASTRAIL
         DC    CL72'IF POSSIBLE, AN AUTOMATIC SIGN-ON WILL BE PERFORMEDX
                IN THE TARGET REGION '
         AGO   .NOASTR
.ASTRAIL ANOP
         DC    CL72'THE TARGET SYSTEM''S SIGN-ON SCREEN WILL BE DISPLAYX
               ED'
.NOASTR  ANOP
TRAILERL EQU   *-TRAILER
CRTEMSGS DS    0C
         DC    C'CRTE SYSID='
SYSID    DC    C'    '
CRTEMSGL EQU   *-CRTEMSGS
ACKMSGS  DS    0C
         DC    X'11C25F1DF8'
         DC    C'ROUTE REQUEST SUCCESSFUL AND AUTOMATIC SIGN-ON PERFORMX
                ED'
         DC    X'11C3F0'
         DC    C'SESSION NOW BOUND TO SYSTEM - '
ACKSYSID DC    C'    '
         DC    X'1DC1'
ACKMSGL  EQU   *-ACKMSGS
ERR10MGS DS    0C
         DC    X'115C7D1D38'
         DC    CL52'NO ROUTING DESTINATIONS DEFINED FOR THIS SYSTEM'
ERR10MGL EQU   *-ERR10MGS
ERR20MGS DS    0C
         DC    X'115C7D1D38'
         DC    CL52'A TCT SCAN ERROR HAS OCCURRED'
ERR20MGL EQU   *-ERR20MGS
ERR30MGS DS    0C
         DC    X'115CF31DF8'
         DC    CL18' '
         DC    C'NO TARGET SYSTEM SELECTED..RE-ENTER OR EXIT '
         DC    CL18' '
ERR30MGL EQU   *-ERR30MGS
TRANID   DC    CL4'BIND'
INTTRAN  DC    CL4'BND1'
SCFUNC   DC    XL2'0001'
TCTPFBAR EQU   2
TIOABAR  EQU   9
TCTTEAR  EQU   10
         LTORG
         END


Christopher Frank
Senior Systems Programmer (USA)    ) Xephon 1987


































