Displaying logged-on terminals

We run an internal transaction to blank out screens which have
not been active for 15 minutes.  The users often forget this and
complain that their keyboards won't respond.  Also, there are
times when we need to work on a particular controller and want
to know if anyone on that line is still logged on.  For these and
other circumstances it is useful to know who is signed on and
what transactions they are running.

CEMT is cumbersome and doesn't give all the information we
need at once.  The following transaction was written to give us
that information.  It scans the TCTTE chain for terminals with
users signed on (ie attended terminals), and displays the
terminal-id, operator-id (the 3-character OPIDENT from the
SNT), and the current transaction from the terminal's TCA if
there is one.  Otherwise it displays the next transaction posted
in the TCTTE.  This covers most situations as nearly all our
applications are pseudo-conversational.  It also notes if any of
these terminals are out of service.

Without parameters, the transaction lists all terminals on the
system which comply but particular groups can be selected.
Up to four characters can be entered after a space and terminal-
ids are matched against these.  Spaces are taken as wild cards,
so that, for example, 'S space space 8' would select all logged-
on terminals beginning with S and ending with 8.

The display is refreshed by hitting ENTER and exited by
hitting CLEAR.  If there is more than a screen full (indicated
by the message 'and more' at the bottom) the next page is
retrieved by entering PF24.

This transaction was written under CICS 1.6 so it might need
checking for other releases, although it is written in command
level.  If the transaction-id is changed, the EXEC CICS
RETURN in line 93 will need changing.  If a different PF key
is required, the EXEC CICS HANDLE in line 33 must be
altered.

DFHEISTG DSECT
TCTTEST  DS    F                           TCTTEAR STORE
BUFFLEN  DS    H                           LENGTH OF BUFFER
BUFFER1  DS    CL5                         BUFFER WORKAREA
TERMNAME DS    CL4                         TERMINAL TO BE VIEWED
         DS    CL3
MESSAGE  DS    CL654                       TERMINAL MESSAGE AREA
MSGLEN   DS    H                           TERM MESSAGE AREA LEN
TCTTEAR  EQU   5
TCTPFBAR EQU   6
CSABREG  EQU   9
         COPY  DFHCSADS                    CSA
         DFHTCA CICSYST=YES                TCA
         COPY  DFHTCTFX                    TCT
         COPY  DFHTCTTE                    TCTTE
SNONQ    CSECT
         EXEC  CICS ADDRESS CSA(CSABREG)
         USING DFHCSADS,CSABREG
         L     TCTPFBAR,CSATCTBA           BASE REG FOR TCT
         L     TCTTEAR,TCTVTEBA            BASE REG FOR TCTTE
         DROP CSABREG
         DROP TCTPFBAR
         USING DFHTCADY,R9                 TCA SYS DSECT
         ST    TCTTEAR,TCTTEST             SAVE TCTTE ADDR
         MVC   BUFFLEN,=H'12'              SET SCREEN READ COUNT
         EXEC  CICS HANDLE AID ANYKEY(RET) PF24(NXT)
         EXEC  CICS IGNORE CONDITION LENGERR
         EXEC  CICS RECEIVE INTO(BUFFER1)  LENGTH(BUFFLEN)
         CLC   EIBCALEN,=H'0'              FIRST TIME?
         BE    SS10                        YES
         CLC   EIBCPOSN,=H'1'              TERMINAL INPUT?
         BH    SS00                        YES
         L     R4,DFHEICAP                 ESTABLISH ADDRESSABILITY
         MVC   BUFFER1+3(9),6(R4)          COMMAREA
SS00     MVC   BUFFER1(9),BUFFER1+3
         MVC   BUFFER1+9(3),=C'   '
SS10     LA    R7,MESSAGE-28
         SR    R8,R8
SS20     LA    R9,TCTTETI+3
         LA    R6,4
         LA    R4,TERMNAME+3
SS30     CLI   0(R4),C' '                  ANY CHAR?
         BNH   SS40                        NO
         CLC   0(1,R9),0(R4)               MATCH?
         BNE   SS90                        NO SO GET NEXT TERM
SS40     BCTR  R4,0
         BCTR  R9,0
         BCT   R6,SS30                     NEXT CHAR BACK
         TM    TCTTETS,TCTTESTA            ATTENDED?
         BNO   SS90                        NO
         CH    R8,=H'644'                  END OF SCREEN?
         BL    SS50                        NO
         MVC   28(10,R7),=X'40408195844094969985'    'AND MORE'
         LA    R8,6(R8)
         ST    TCTTEAR,TCTTEST             SAVE TCTTE ADDR
         B     SS99
SS50     LA    R7,MESSAGE(R8)
         MVI   0(R7),X'15'                 NEW LINE
         MVC   6(4,R7),TCTTETI             TERMID
         MVC   12(3,R7),TCTTEOI            USERID
         TM    TCTTETS,TCTTESPO+TCTTESOS   OOS?
         BZ    SS60
         MVC   1(3,R7),=X'96A4A3'          'OUT'
SS60     ICM   R9,15,TCTTECA               TCA ADDR
         BZ    SS70                        NONE
         L     R9,0(R9)                    TCA SYS AREA
         MVC   17(4,R7),TCAKCOID           TRANSID
         MVC   22(6,R7),=X'8183A389A585'   'ACTIVE'
         B     SS80
SS70     CLI   TCTTETC,X'0'                ANY TRANSID?
         BE    SS80
         MVC   17(4,R7),TCTTETC
SS80     LA    R8,28(R8)
SS90     AH    TCTTEAR,TCTTETEL            NEXT
         CLC   TCTTETI,=X'FFFFFFFF'        EOT?
         BNE   SS20                        NO, SO CONTIN
         CH    R8,=H'644'                  END OF SCREEN?
         BNL   SS95
         MVI   28(R7),X'15'                NL
SS95     MVC   29(3,R7),=X'859584'         'END'
SS99     LA    R8,16(R8)                   BUFFER1 ETC + END
         STH   R8,MSGLEN
         EXEC  CICS SEND TEXT FROM(BUFFER1) LENGTH(MSGLEN) ERASE
         CLC   EIBTRMID,=C'CNSL'
         BE    RETO
         EXEC  CICS RETURN TRANSID('SNON') COMMAREA(TCTTEST) LENGTH(15)
NXT      L     R4,DFHEICAP                 ESTABLISH ADDRESSABILITY
         MVC   TCTTEST(15),0(R4)           COMMAREA
         L     TCTTEAR,TCTTEST             GET LAST TERMINAL
         B     SS10
RET      MVC   MESSAGE(27),=C'SNON TRANSACTION TERMINATED'
         MVC   MSGLEN,=H'27'
         EXEC  CICS SEND TEXT FROM(MESSAGE) LENGTH(MSGLEN) ERASE
RETO     EXEC  CICS RETURN
         END


Hugh Walding
Systems Programmer
IWCC (UK)

