Purging a running task

The purpose of this program is to purge a task running on a
specified terminal.  The task can be a 'stuck' pseudo-
conversational task or a normal conversational task.
Obviously, this can be achieved by using
PURGE/FORCEPURGE in the task option of CEMT, but our
program enables this function to be given to users who may not
have the authority to use the security-sensitive CEMT.

To accomplish this goal we make use of the programmable
interface to master terminal functions (see OS/VS CICS
Customization Guide SC33-0239-1 pages 403-404).

The transaction is invoked by issuing the command PURG
termid, where termid is the terminal on which the task to be
purged is running.  The PURG transaction can be issued from
any terminal (including the console).  The PURG transaction
(or its equivalent) together with the SPURGE program must be
defined to CICS.


SPURGE SOURCE CODE

         GBLB $DFHEIMX       FOR MIX-MODE (MACRO AND COMMAND)
$DFHEIMX SETB 1
TCTTEAR  EQU  8
DFHEISTG DSECT
RCVIN    DS   0CL9
TRANIN   DS   CL4
FILIN    DS   CL1
TERMIN   DS   CL4
ERRMSG   DS   CL40
LENERR   DS   H
MSGOUT   DS   0CL35
MSG1     DS   CL12
MSGTERM  DS   CL4
MSG3     DS   CL19
LENMSG   DS   H
CMDUP    DS   0CL26
CMD1     DS   CL9
CMDNUM   DS   CL5
CMD3     DS   CL12
LENCMD   DS   H
INDCT    DS   CL1
COMMENT  DS   CL100
LENCOM   DS   H
         DS   0D
CEMTPARM DS   AL4
         DS   AL4
         DS   AL4
         DS   AL4
         DS   AL4
         DFHTCA  CICSYST=YES
         COPY    DFHCSADS
         DFHTCTZE CICSYST=YES
SPURGE   DFHEIENT DATAREG=5
         EXEC CICS HANDLE CONDITION ERROR(ERR2)
         EXEC CICS RECEIVE INTO(RCVIN)
         L    R8,TCAFCAAA      THIS TASK TCTTE ADDRESS
         N    R8,=X'00FFFFFF'  MOVING X'00' INTO REGISTER'S LEFT BYTE
         CLC  0(4,R8),TERMIN   THE REQUESTED TERMINAL IS OUR TERMINAL ?
         BE   ERR4             YES, NOT FOR THIS TERMINAL
         MVC  TCATPTA,TERMIN      TERMINAL-ID INTO TCTTPTA
         DFHTC CTYPE=LOCATE,DOMAIN=LOCAL,TERM=ID,INVID=ERR1,ERROR=ERR2
         L    TCTTEAR,TCATPTA     REQUIRED TCTTE ADDRESS
*  TCA ADDRESS OF THE TASK USING THE REQUESTED TERMINAL - TCTTECA
         L    R9,TCTTECA
         C    R9,=4X'00'
         BE   ERR3          NO TASK USING THE REQUESTED TERMINAL
         L    R9,0(R9)      TCA  SYSTEM AREA
         UNPK CMDNUM,TCAKCTTA-DFHSYTCA(3,R9)   UNPACKING TASK NUMBER
         OI   CMDNUM+4,X'F0'
*  ESTABLISHING ADDRESSES OF  DFHEMTA  PARAMETER LIST
         LA   8,CMDUP
         ST   8,CEMTPARM
         LA   8,LENCMD
         ST   8,CEMTPARM+4
         LA   8,INDCT
         ST   8,CEMTPARM+8
         LA   8,COMMENT
         ST   8,CEMTPARM+12
         LA   8,LENCOM
         ST   8,CEMTPARM+16
         MVC  CMD1,=C'SET TASK('
         MVC  CMD3,=C') FORCEPURGE'
         MVC  LENCMD,=H'26'
         MVI  INDCT,X'00'
         MVC  LENCOM,=H'100'
         EXEC CICS LINK PROGRAM('DFHEMTA') COMMAREA(CEMTPARM)          X
                       LENGTH(20) NOHANDLE
         MVC  MSG1,=CL12'THE TASK AT '
         MVC  MSGTERM,TERMIN
         MVC  MSG3,=CL19' HAS BEEN CANCELLED'
         MVC  LENMSG,=H'35'
         EXEC CICS SEND FROM(MSGOUT) LENGTH(LENMSG) ERASE
         B    RET
ERR1     MVC  LENERR,=H'40'
         MVC  ERRMSG,=CL40'THE REQUESTED TERMINAL DOES NOT EXIST   '
         EXEC CICS SEND FROM(ERRMSG) LENGTH(LENERR) ERASE
         B    RET
ERR2     MVC  LENERR,=H'40'
         MVC  ERRMSG,=CL40'  AN ERROR OCCURRED DURING EXECUTION    '
         EXEC CICS SEND FROM(ERRMSG) LENGTH(LENERR) ERASE
         B    RET
ERR3     MVC  LENERR,=H'40'
         MVC  ERRMSG,=CL40'    NO TASK AT THE REQUESTED TERMINAL   '
         EXEC CICS SEND FROM(ERRMSG) LENGTH(LENERR) ERASE
         B    RET
ERR4     MVC  LENERR,=H'40'
         MVC  ERRMSG,=CL40'   NOT FOR YOUR TERMINAL               '
         EXEC CICS SEND FROM(ERRMSG) LENGTH(LENERR) ERASE
         B    RET
RET      EXEC CICS RETURN
         END


Safran Menachem
Systems Programmer
Mivtachim Computers (Israel)

