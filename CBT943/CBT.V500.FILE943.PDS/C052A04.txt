CEDF and other key-driven utilities (continued)

We conclude our look at improving the usability of CEDF and
other key-driven utilities by examining the remaining
components of the system.

THE UTILTBL TABLE

Customise, assemble, and link edit UTILTBL.  The
customisation options are:

1     The terminal-id of your utility printer (I use 'UTIL').

2     The PF/PA value of your start CEDF key (can be PF01-
      PF24, PA01-PA03).

3     The PF/PA value of your start local copy key (can be
      PF01-PF24, PA01-PA03).

4     The PF/PA value of your pass-through key (can be PF01-
     PF24, PA01-PA03).

5     The starting position in each terminal's TCT user area
     (TCTTEUA) of a six-byte work area (can be 1-250).

6     One table entry for each terminal-id authorised to use
     UTILEXT.  The layout of each entry is:

      %     Terminal-id.

      %     Value of that terminal's utility key (can be PF01-PF24,
            PA01-PA03, but not any of the keys defined above).

      %     Authorisation to use start CEDF function (Y or N).

      %     Authorisation to use local copy function (Y or N).

      %     Authorisation to use PF13-24 function (Y or N).

As written, UTILEXT supports up to 100 terminals.

Sample UTILTBL

         TITLE 'CICS UTILITY EXIT CONTROL TABLE'
UTILTBL  START 0
* THIS TABLE CONTAINS THE TERMINAL ID OF THE UTILITY PRINTER, THE AID
* VALUES OF THE START CEDF KEY, LOCAL COPY KEY, AND UTILITY AID PASS
* THROUGH KEY
* ENTRIES MAY BE MADE FOR EACH TERMINAL AUTHORIZED TO USE THE UTILITY
* EXIT
*                +--+<-----THIS IS WHERE YOU ENTER THE TERMINAL ID OF
*                V--V      THE UTILITY PRINTER
         DC    C'UTIL'
* NOTE: VALID PF/PA KEY VALUES ARE PF01-PF24, PA01-PA03
*       THE THREE KEY VALUES MUST BE UNIQUE
*                +--+<-----THIS IS THE START CEDF KEY
*                V--V
         DC    C'PF12'
*                +--+<-----THIS IS THE LOCAL COPY KEY
*                V--V
         DC    C'PF11'
*                +--+<-----THIS IS THE UTILITY KEY PASS-THROUGH KEY
*                V--V
         DC    C'PF10'
*                +-+<------THIS IS THE STARTING POSITION IN THE TCTTEUA
*                V-V       FOR UTILEXT DATA (NEED 6 CONTIGUOUS BYTES) -
         DC    C'001'      VALUE CAN BE FROM 001 THRU 250
*       EACH USER'S UTILITY KEY MUST NOT BE THE SAME AS ANY OF THE
*       ABOVE
******* S T A R T  O F  T E R M I N A L  T A B L E ******************
* ADD ADDITIONAL ENTRIES BELOW THIS LINE
L4TC     DC    C'L4TC' TERMINAL ID
         DC    C'PA01' UTILITY KEY
         DC    C'Y'    AUTHORIZED FOR CEDF (Y/N)
         DC    C'Y'    AUTHORIZED FOR LOCAL COPY
         DC    C'Y'    AUTHORIZED FOR PF13-24
L4TD     DC    C'L4TD' TERMINAL ID
         DC    C'PA01' UTILITY KEY
         DC    C'Y'    AUTHORIZED FOR CEDF (Y/N)
         DC    C'Y'    AUTHORIZED FOR LOCAL COPY
         DC    C'N'    AUTHORIZED FOR PF13-24
L4TE     DC    C'L4TE' TERMINAL ID
         DC    C'PA01' UTILITY KEY
         DC    C'N'    AUTHORIZED FOR CEDF (Y/N)
         DC    C'Y'    AUTHORIZED FOR LOCAL COPY
         DC    C'N'    AUTHORIZED FOR PF13-24
* ADD ADDITIONAL ENTRIES ABOVE THIS LINE
******* E N D  O F  T E R M I N A L  T A B L E **********************
         DC   X'FFFFFFFFFFFFFFFFFFFFFF'
* NOTE: D O  N O T  R E M O V E  T H E S E  L I N E S
         END UTILTBL

UTILSTRT

IDENTIFICATION DIVISION.
PROGRAM-ID. 'UTILSTRT'.
REMARKS. THIS PROGRAM ENABLES AND STARTS THE EXIT PROGRAM,
    UTILEXT, LOADS THE CONTROL TABLE, INTIALIZES THE EXIT'S
    GLOBAL WORK AREA AND INITIALIZES EACH USER'S TCTTE USER
    AREA.
ENVIRONMENT DIVISON.
DATA DIVISION.
WORKING-STORAGE SECTION.
01  WORK-AREA.
    02  GALENGTH                PICTURE S9(4)    COMPUTATIONAL.
    02  LEN                     PICTURE S9(4)    COMPUTATIONAL.
    02  SUBA                    PICTURE S9(4)    COMPUTATIONAL.
    02  SAVE-FIRST-TCTTE        PICTURE S9(8)    COMPUTATIONAL.
    02  TEST-TERMID             PICTURE X(4).
    02  TEST-PF-PA-KEY          PICTURE X(4).
    02  TEST-AID                PICTURE X.
    02  UTILEXT-WORK-BINARY     PICTURE S9(4)    COMPUTATIONAL.
    02  FILLER REDEFINES UTILEXT-WORK-BINARY.
        03  FILLER              PICTURE X.
        03  UTILEXT-WORK-ENCODED PICTURE X.
    02  NUMBER-OF-ENTRIES       PICTURE 9(4)     COMPUTATIONAL.
01  AID-FILLER.
    02  FILLER                  PICTURE X(5)     VALUE 'PF011'.
    02  FILLER                  PICTURE X(5)     VALUE 'PF022'.
    02  FILLER                  PICTURE X(5)     VALUE 'PF033'.
    02  FILLER                  PICTURE X(5)     VALUE 'PF044'.
    02  FILLER                  PICTURE X(5)     VALUE 'PF055'.
    02  FILLER                  PICTURE X(5)     VALUE 'PF066'.
    02  FILLER                  PICTURE X(5)     VALUE 'PF077'.
    02  FILLER                  PICTURE X(5)     VALUE 'PF088'.
    02  FILLER                  PICTURE X(5)     VALUE 'PF099'.
    02  FILLER                  PICTURE X(5)     VALUE 'PF10:'.
    02  FILLER                  PICTURE X(5)     VALUE 'PF11#'.
    02  FILLER                  PICTURE X(5)     VALUE 'PF12@'.
    02  FILLER                  PICTURE X(5)     VALUE 'PF13A'.
    02  FILLER                  PICTURE X(5)     VALUE 'PF14B'.
    02  FILLER                  PICTURE X(5)     VALUE 'PF15C'.
    02  FILLER                  PICTURE X(5)     VALUE 'PF16D'.
    02  FILLER                  PICTURE X(5)     VALUE 'PF17E'.
    02  FILLER                  PICTURE X(5)     VALUE 'PF18F'.
    02  FILLER                  PICTURE X(5)     VALUE 'PF19G'.
    02  FILLER                  PICTURE X(5)     VALUE 'PF20H'.
    02  FILLER                  PICTURE X(5)     VALUE 'PF21I'.
    02  FILLER                  PICTURE X(5)     VALUE 'PF22?'.
    02  FILLER                  PICTURE X(5)     VALUE 'PF23.'.
    02  FILLER                  PICTURE X(5)     VALUE 'PF24<'.
    02  FILLER                  PICTURE X(5)     VALUE 'PA01%'.
    02  FILLER                  PICTURE X(5)     VALUE 'PA02>'.
    02  FILLER                  PICTURE X(5)     VALUE 'PA03,'.
01  AID-TABLE REDEFINES AID-FILLER.
    02  AID-ENTRY OCCURS 27 TIMES INDEXED BY INDX.
        03  PF-PA-KEY           PICTURE X(4).
        03  AID-VALUE           PICTURE X.
01  CSML-MESSAGE.
    02  FILLER                  PICTURE X(11)
        VALUE 'UTILSTRT - '.
    02  CSML-TEXT               PICTURE X(69).
01  CSML-ERROR-MESSAGES.
    02  UTILTBL-LOAD-ERROR      PICTURE X(69)   VALUE
        'CANNOT LOAD UTILITY CONTROL TABLE. UTILEXT NOT STARTED'.
    02  UTILEXT-START-ERROR     PICTURE X(69)   VALUE
        'ENABLE/START UTILEXT COMMAND FAILED'.
    02  GLOBAL-AREA-ACCESS-ERROR PICTURE X(69)  VALUE
        'CANNOT ADDRESS UTILEXT GLOBAL AREA.  UTILEXT STOPPED'.
    02  DUMMY-TERMID-ERROR.
        03  DUMMY-TERMID        PICTURE X(5).
        03  FILLER              PICTURE X(64)   VALUE
           'IS NOT A TERMINAL.  UTILEXT STOPPED'.
    02  USER-TERMID-ERROR.
        03  USER-TERMID         PICTURE X(5).
        03  FILLER              PICTURE X(64)   VALUE
           'IS NOT A TERMINAL AND IS BYPASSED FOR UTILEXT'.
    02  NO-TCTTEUA-ERROR.
        03  TCTTEUA-TERMID      PICTURE X(5).
        03  FILLER              PICTURE X(64)   VALUE
           'HAS NO USER AREA. TERMINAL BYPASSED FOR UTILEXT'.
    02  GLOBAL-AID-ERROR.
        03  GLOBAL-PF-PA        PICTURE X(5).
        03  FILLER              PICTURE X(64)   VALUE
           'IS NOT A VALID PF/PA KEY. UTILITY FUNCTION BYPASSED'.
    02  USER-AID-ERROR.
        03  USER-PF-PA          PICTURE X(5).
        03  FILLER              PICTURE X(64)   VALUE
           'IS INVALID PF/PA KEY. TERMINAL BYPASSED FOR UTILEXT'.
    02  NO-AUTH-ERROR.
        03  NO-AUTH-TERMID      PICTURE X(5).
        03  FILLER              PICTURE X(64)   VALUE
           'HAS NO AUTHORIZATIONS AND IS BYPASSED FOR UTILEXT'.
LINKAGE SECTION.
01  DFHBLLDS.
    02  FILLER                  PICTURE S9(8)   COMPUTATIONAL.
    02  GAPTR                   PICTURE S9(8)   COMPUTATIONAL.
    02  UTILPTR                 PICTURE S9(8)   COMPUTATIONAL.
    02  CSACBAR                 PICTURE S9(8)   COMPUTATIONAL.
    02  TCTPTR                  PICTURE S9(8)   COMPUTATIONAL.
    02  TCTTEAR                 PICTURE S9(8)   COMPUTATIONAL.
    02  TCTUAPTR                PICTURE S9(8)   COMPUTATIONAL.
01  GLOBAL-WORK-AREA.
    02  ADDRESS-OF-DUMMY-PRINTER PICTURE S9(8)  COMPUTATIONAL.
    02  EDF-KEY-AID-VALUE       PICTURE X.
    02  PRINT-KEY-AID-VALUE     PICTURE X.
    02  PASS-THROUGH-KEY-AID-VALUE PICTURE X.
    02  TCTTEUA-OFFSET          PICTURE S9(4)   COMPUTATIONAL.
01  UTILEXT-CONTROL-TABLE.
    02  DUMMY-PRINTER-TERMID    PICTURE X(4).
    02  EDF-KEY                 PICTURE X(4).
    02  PRINT-KEY               PICTURE X(4).
    02  PASS-THROUGH-KEY        PICTURE X(4).
    02  TCTTEUA-START-POSITION  PICTURE 999.
    02  TERMINAL-TABLE.
        03  TERMINAL-ENTRY OCCURS 100 TIMES
            DEPENDING ON NUMBER-OF-ENTRIES.
            04  TERMINAL-ID     PICTURE X(4).
            04  UTILITY-KEY     PICTURE X(4).
            04  EDF-AUTH        PICTURE X.
            04  PRINT-AUTH      PICTURE X.
            04  PF13-24-AUTH    PICTURE X.
01  DFHCSADS                    COPY 'DFHCSADS'.
01  FILLER REDEFINES DFHCSADS.
    02  FILLER                  PICTURE X(296).
    02  CSATCTBA                PICTURE S9(8)   COMPUTATIONAL.
01  TCT-PREFIX.
    03  FILLER                  PICTURE X(28).
    03  FIRST-TCTTE             PICTURE S9(8)   COMPUTATIONAL.
01  DFHTCTTE                    COPY 'DFHTCTTE'.
    02  FILLER                  PICTURE X(200).
01  FILLER REDEFINES DFHTCTTE.
    02  FILLER                  PICTURE X(84).
    02  TCTTETEL                PICTURE S9(4)   COMPUTATIONAL.
    02  FILLER                  PICTURE X(314).
    02  TCTEPTO                 PICTURE S9(8)   COMPUTATIONAL.
01  TCTTE-USER-AREA-FOR-UTILEXT.
    02  PRINTTO-SAVE            PICTURE S9(8)   COMPUTATIONAL.
    02  UTILITY-KEY-AID-VALUE   PICTURE X.
    02  UTILEXT-WORK            PICTURE X.
PROCEDURE DIVISION.
    EXEC CICS HANDLE CONDITION ERROR (ABEND-ROUTINE) END-EXEC.
    EXEC CICS HANDLE ABEND LABEL (ABEND-ROUTINE) END-EXEC.
ADDRESS-UTILEXT-CONTROL-TABLE.
    MOVE UTILTBL-LOAD-ERROR TO CSML-TEXT.
    MOVE 1116 TO LEN.
    EXEC CICS LOAD PROGRAM ('UTILTBL') SET (UTILPTR) LENGTH (LEN)
            END-EXEC.
    COMPUTE NUMBER-OF-ENTRIES = ((LEN - 19) / 11) - 1.
ENABLE-START-UTILEXT.
    MOVE UTILEXT-START-ERROR TO CSML-TEXT.
    EXEC CICS ENABLE PROGRAM ('UTILEXT') EXIT ('XZCIN')
            GALENGTH (10) START END-EXEC.
ADDRESS-GLOBAL-WORK-AREA.
    MOVE GLOBAL-AREA-ACCESS-ERROR TO CSML-TEXT.
    MOVE 10 TO GALENGTH.
    EXEC CICS EXTRACT EXIT PROGRAM ('UTILEXT')
            GALENGTH (GALENGTH) GASET (GAPTR) END-EXEC.
ADDRESS-CSA.
    EXEC CICS ADDRESS CSA (CSACBAR) END-EXEC.
ADDRESS-TCT-PREFIX.
    MOVE CSATCTBA TO TCTPTR.
ADDRESS-FIRST-TCTTE.
    MOVE FIRST-TCTTE TO TCTTEAR, SAVE-FIRST-TCTTE.
INITIALIZE-GLOBAL-WORK-AREA.
    MOVE DUMMY-PRINTER-TERMID TO TEST-TERMID.
    PERFORM GET-TCTTE-ADDRESS THRU GET-TCTTE-ADDRESS-EXIT.
    IF TCTTETI EQUAL HIGH-VALUES
        MOVE TEST-TERMID TO DUMMY-TERMID
        MOVE DUMMY-TERMID-ERROR TO CSML-TEXT
        GO TO ABEND-ROUTINE.
    MOVE TCTTEAR TO ADDRESS-OF-DUMMY-PRINTER.
    MOVE EDF-KEY TO TEST-PF-PA-KEY.
    PERFORM GET-AID.
    IF TEST-AID EQUAL HIGH-VALUES
        MOVE TEST-PF-PA-KEY TO GLOBAL-PF-PA
        MOVE GLOBAL-AID-ERROR TO CSML-TEXT
        PERFORM WRITE-ERROR-MESSAGE.
        MOVE TEST-AID TO EDF-KEY-AID-VALUE.
        MOVE PRINT-KEY TO TEST-PF-PA-KEY.
        PERFORM GET-AID.
        IF TEST-AID EQUAL HIGH-VALUES
            MOVE TEST-PF-PA-KEY TO GLOBAL-PF-PA
            MOVE GLOBAL-AID-ERROR TO CSML-TEXT
            PERFORM WRITE-ERROR-MESSAGE.
        MOVE TEST-AID TO PRINT-KEY-AID-VALUE.
        MOVE PASS-THROUGH-KEY TO TEST-PF-PA-KEY.
        PERFORM GET-AID.
        IF TEST-AID EQUAL HIGH-VALUES
            MOVE TEST-PF-PA-KEY TO GLOBAL-PF-PA
            MOVE GLOBAL-AID-ERROR TO CSML-TEXT
            PERFORM WRITE-ERROR-MESSAGE.
        MOVE TEST-AID TO PASS-THROUGH-KEY-AID-VALUE.
        EXEC CICS BIF DEEDIT FIELD (TCTTEUA-START-POSITION)
                LENGTH (3) END-EXEC.
        SUBTRACT 1 FROM TCTTEUA-START-POSITION GIVING TCTTEUA-OFFSET.
INITIALIZE-TCTTE-USER-AREAS.
    MOVE 1 TO SUBA.
INITIALIZE-TCTTEUA.
    IF SUBA > NUMBER-OF-ENTRIES OR TERMINAL-ENTRY (SUBA) EQUAL
            HIGH-VALUES
        GO TO RETURN-ROUTINE.
    IF EDF-AUTH (SUBA) EQUAL 'N' AND PRINT-AUTH (SUBA) EQUAL 'N'
            AND PF13-24-AUTH (SUBA) EQUAL 'N'
        MOVE TEST-TERMID TO NO-AUTH-TERMID
        MOVE NO-AUTH-ERROR TO CSML-TEXT
        PERFORM WRITE-ERROR-MESSAGE
        ADD 1 TO SUBA
        GO TO INITIALIZE-TCTTEUA.
    MOVE TERMINAL-ID (SUBA) TO TEST-TERMID.
    MOVE SAVE-FIRST-TCTTE TO TCTTEAR.
    PERFORM GET-TCTTE-ADDRESS THRU GET-TCTTE-ADDRESS-EXIT.
    IF TCTTETI EQUAL HIGH-VALUES
        MOVE TEST-TERMID TO USER-TERMID
        MOVE USER-TERMID-ERROR TO CSML-TEXT
        PERFORM WRITE-ERROR-MESSAGE
        ADD 1 TO SUBA
        GO TO INITIALIZE-TCTTEUA.
    IF TCTTECIL EQUAL LOW-VALUES
        MOVE TEST-TERMID TO TCTTEUA-TERMID
        MOVE NO-TCTTEUA-ERROR TO CSML-TEXT
        PERFORM WRITE-ERROR-MESSAGE
        ADD 1 TO SUBA
        GO TO INITIALIZE-TCTTEUA.
    MOVE TCTTECIA TO TCTUAPTR.
    ADD TCTTEUA-OFFSET TO TCTUAPTR.
    MOVE TCTEPTO TO PRINTTO-SAVE.
    MOVE UTILITY-KEY (SUBA) TO TEST-PF-PA-KEY.
    PERFORM GET-AID.
    IF TEST-AID EQUAL HIGH-VALUES
        MOVE TEST-PF-PA-KEY TO USER-PF-PA
        MOVE USER-AID-ERROR TO CSML-TEXT
        PERFORM WRITE-ERROR-MESSAGE
        ADD 1 TO SUBA
        GO TO INITIALIZE-TCTTEUA.
    MOVE TEST-AID TO UTILITY-KEY-AID-VALUE.
    MOVE 0 TO UTILEXT-WORK-BINARY.
    IF EDF-AUTH (SUBA) EQUAL 'Y'
        ADD 4 TO UTILEXT-WORK-BINARY.
    IF PRINT-AUTH (SUBA) EQUAL 'Y'
        ADD 2 TO UTILEXT-WORK-BINARY.
    IF PF13-24-AUTH (SUBA) EQUAL 'Y'
        ADD 1 TO UTILEXT-WORK-BINARY.
    MOVE UTILEXT-WORK-ENCODED TO UTILEXT-WORK.
    ADD 1 TO SUBA.
    GO TO INITIALIZE-TCTTEUA.
RETURN-ROUTINE.
    EXEC CICS RETURN END-EXEC.
ABEND-ROUTINE.
    EXEC CICS DISABLE PROGRAM ('UTILEXT') EXIT ('XZCIN')
            STOP NOHANDLE END-EXEC.
    PERFORM WRITE-ERROR-MESSAGE.
    EXEC CICS RETURN END-EXEC.
    STOP RUN.
* PERFORMED SUBROUTINES
GET-AID.
    SET INDX TO 1.
    SEARCH AID-ENTRY
        AT END MOVE HIGH-VALUES TO TEXT-AID
        WHEN TEST-PF-PA-KEY EQUAL PF-PA-KEY (INDX)
                MOVE AID-VALUE (INDX) TO TEST-AID.
GET-TCTTE-ADDRESS.
    IF TCTTETI EQUAL HIGH-VALUES OR TEST-TERMID EQUAL TCTTETI
        GO TO GET-TCTTE-ADDRESS-EXIT.
    ADD TCTTETEL TO TCTTEAR.
    GO TO GET-TCTTE-ADDRESS.
GET-TCTTE-ADDRESS-EXIT.
    EXIT.
WRITE-ERROR-MESSAGE.
    EXEC CICS WRITEQ TD QUEUE ('CSML') FROM (CSML-MESSAGE)
            LENGTH (80) NOHANDLE END-EXEC.


Stephen J Gallaway
Senior Programmer                                              ) Xephon 1990
Workers' Compensation Insurance Rating Bureau of California (USA)


