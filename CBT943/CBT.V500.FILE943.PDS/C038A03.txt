 CMS EXECs for monitoring CICS (continued)
The latest in our series of articles on using CMS EXECs to monitor CICS looks at
how to display information associated with the Program Abend Trace Table.
Keying PF9 displays the current entries (from 0 to 4) showing PSW and trace
table entries.  Keying PF11 or PF12 displays the saved registers.
PCAT EXEC
/* PCAT EXEC ****************************************************** */
/*         CICS PROGRAM CHECK ABEND TRACE TABLE                     */
/*         ====================================                     */
/*    PARAMETERS REQUIRED:   VMID                                   */
/*                           PCAT ADDRESS                           */
/* **************************************************************** */
      ARG VMID PCA .
      IF PCA = 'PCA' THEN EXIT
/* ---------------------------------------------------------------- */
/* HEADER INFORMATION                                               */
/* ---------------------------------------------------------------- */
      VADD = HEXADD(PCA,2C)         /* NO. OF PROG CHECKS */
      CALL FWORD
      PCNO = FORMAT(X2D(FWORD),3,0)
      VADD = HEXADD(PCA,30)         /* NO. OF ABENDS */
      CALL FWORD
      ABNO = FORMAT(X2D(FWORD),3,0)
      ENTRY = HEXADD(PCA,40)          /* FIRST ENTRY */
/* ---------------------------------------------------------------- */
/* FOR EACH PCAT ENTRY                                              */
/* ---------------------------------------------------------------- */
      DO I = 1 TO 4
          VADD = ENTRY                      /* END OF TABLE? */
          CALL FWORD
          IF FWORD = '00000000' THEN LEAVE
          TYPE = X2C(SUBSTR(FWORD,1,4))     /* PC OR AB */
          NUMB = SUBSTR(FWORD,5,4)          /* NUMBER */
          NUMB = RIGHT(X2D(NUMB),2,' ')
          VADD = HEXADD(ENTRY,4)        /* TCA ADDRESS */
          CALL FWORD
          TCAD = SUBSTR(FWORD,3,6)
          VADD = HEXADD(ENTRY,8)        /* LIFO ADDRESS */
          CALL FWORD
          LIFO = SUBSTR(FWORD,3,6)
          VADD = HEXADD(ENTRY,C)        /* TRACE ADDRESS */
          CALL FWORD
          TRAD = SUBSTR(FWORD,3,6)
          VADD = HEXADD(ENTRY,10)       /* TRACE WD 1  */
          CALL FWORD
          TRID = SUBSTR(FWORD,1,2)
          TRRQ = SUBSTR(FWORD,3,3)
          VADD = HEXADD(ENTRY,14)       /* TRACE WD 2 */
          CALL FWORD
          TASK = FORMAT(SUBSTR(FWORD,1,7),7,0)
          VADD = HEXADD(ENTRY,30)       /* REGISTERS */
          DO J = 0 TO 15
              CALL FWORD
              R.J = FWORD
              VADD = HEXADD(VADD,4)
              END J
          VADD = HEXADD(ENTRY,70)
          CALL FWORD
          PSW1 = FWORD                          /* PSW 1  */
          VADD = HEXADD(ENTRY,74)
          CALL FWORD
          PSW2 = FWORD                          /* PSW 2 */
          VADD = HEXADD(ENTRY,78)
          CALL FWORD
          ILC  = FORMAT(SUBSTR(FWORD,1,4),2,0)  /* ILC   */
          CC   = FORMAT(SUBSTR(FWORD,5,4),2,0)  /* CC    */
 QUEUE TYPE NUMB PSW1 PSW2 ILC CC TRAD TRID TRRQ TASK TCAD LIFO,
                R.0 R.1 R.2 R.3 R.4 R.5 R.6 R.7 ,
                R.8 R.9 R.10 R.11 R.12 R.13 R.14 R.15
          ENTRY = HEXADD(ENTRY,80)
          END I
/* ---------------------------------------------------------------- */
/* DISPLAY USING XEDIT                                              */
/* -----------------------------------------------------------------*/
      QUEUE
      'X' VMID 'PCAT A (PROFILE PCAT WIDTH 220)' PCA PCNO ABNO
      EXIT
PCAT XEDIT
/* PCAT XEDIT */
      ARG . . . . . . . . . PCA PCNO ABNO .
      EXTRACT ?FN?LSCREEN?
      PARSE VALUE FNAME.1 WITH FNAME.1 .
      'CTL' '%' 'E'
      'CTL' '&' 'P WHI     N'
      'CTL' '#' 'P YEL     N'
      'CTL' '!' 'P TUR     N'
      'SET PF02 VSTOR' FNAME.1. PCA
      'SET PF03 QQUIT'
      'SET PF15 QQUIT'
      'SET PF10 PCAV PSW'
      'SET PF11 PCAV RGA'
      'SET PF12 PCAV RGB'
      'COLOR ID WHI NON'
      'COLOR ARROW WHI NON'
      'COLOR CURL TUR REV'
      'COLOR TOF  TUR REV'
      'COLOR FILE TUR REV'
      'COLOR PREF YEL REV'
      'TOFEOF OFF'
      'LRECL 220'
      'TRUNC 220'
      'SHADOW OFF'
      'NUMBER ON'
      'CMD BOT'
      'SCALE OFF'
      'MSGLINE ON 2 OV'
      'PREFIX OFF'
      'CURL ON 3'
      'CURS CM'
      'RESER 1 GRE REV N' ' 'FNAME.1' %& ',
               'PROGRAM CHECK & ABEND TRACE TABLE   %!',
               PCNO '%#PROGRAM CHECKS  %!' ABNO '%#ABENDS'
      'RESER' LSCREEN.1-1 'WHI N' 'PF2=DUMP     ',
                                  'PF3=END      ',
                                  'PF10=PSW/TRACE    ',
                                  'PF11=REGS 0-7    ',
                                  'PF12=REGS 8-15'
     'INPUT'
     'PCAV PSW'
     'TOP'
PCAV XEDIT
/* PCAV XEDIT */
ARG X .
SELECT
  WHEN X = 'PSW' THEN DO
                      'VERIFY 1 6 6 66'
                      HEAD = 'TY NO',
                             ' PSW (EC-MODE)     IL CC',
                             'TR.ADD TRACE  TASK-NO TCA    LIFO'
                      END
  WHEN X = 'RGA' THEN DO
                      'VERIFY 1 6 66 138'
                       HEAD = 'TY NO',
                             ' REG 0    REG 1    REG 2    REG 3   ',
                             ' REG 4    REG 5    REG 6    REG 7   ,
                      END
  WHEN X = 'RGB' THEN DO
                      'VERIFY 1 6 138 211'
                      HEAD = 'TY NO',
                             ' REG 8    REG 9    REG 10   REG 11 ',
                             ' REG 12   REG 13   REG 14   REG 15 '
                      END
  OTHERWISE NOP
  END
'RESER 2 YEL REV N' HEAD


Mike Jones
Senior Systems Programmer
Wavin Building Products (UK)  ) Xephon 1989
















































