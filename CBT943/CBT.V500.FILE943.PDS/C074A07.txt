PSB directory list

With its CEMT command, CICS helps users to know what
datasets are opened or closed, what databases are stopped etc,
but there is no CICS-supplied transaction which will provide
the name and the state of PSBs on my CICS system.

The following transaction helps users to know how many PSBs
are present in CICS, whether these PSBs are remote, and
which PSBs cannot be scheduled.  The available options are
invoked through the following commands:

o      PDIR - lists all PSBs from the PDIR directory

o      PDIR,REM - lists only remote PSBs

o      PDIR,UNS - lists only PSBs that can't be scheduled.

The program is written in command-level Assembler and has
been tested on CICS 1.7 under MVS/XA.

DFHEISTG DSECT
R2       EQU   2              SIT
PDIRBAR  EQU   8              PDIR
R4       EQU   4              PDIR REMOTE
R5       EQU   5              BASE
TRAN     DS    0CL80          INPUT
         DS    CL5            .PDIR,
COND     DS    CL3            .CONDITION (UNS,REM) OR BLANK
         DS    CL72
HEAD1    DS    0CL36          HEADER FOR SEND
HEADL    DS    H
HEADP    DS    C
HEADC    DS    C
HEADD    DS    CL32
TRAIL1   DS    0CL36          TRAILER FOR SEND
TRAILL   DS    H
TRAILP   DS    C
TRAILC   DS    C
TRAILD   DS    CL32
LEN      DS    H
TEST0    DS    0CL80          LINE OF TEXT
TEST1    DS    CL8
TEST2    DS    CL8
SALT0    DS    CL1
BLANK    DS    CL63
         IDLI  PDRBASE=PDIRBAR              DL/I PDIR
         COPY  DFHCSADS                     CSA
         DFHSIT  TYPE=DSECT                 SIT
         DFHDLP  DLP=DSECT                  DLP
         DFHDLP  RPD=DSECT                  PDIR REMOTE
RMT      DFHEIENT CODEREG=(9,10)
         B     STRT
         DC    CL8'XSISTPDI'
         DC    CL8'01/01/92'
STRT     MVC   BLANK,=63C' '
         MVI   SALTO,X'15'                  NEW LINE
         MVC   HEADL,=X'0020'
         MVI   HEADP,C'*'
         MVC   HEADD,=C'PSB DIRECTORY          PAG. **  '
         MVI   HEADD+30,X'15'
         MVI   HEADD+31,X'15'
         MVC   TRAILL,=X'0020'
         MVC   TRAILD,=C'-------------  PF7-BWD  PF8-FWD '
         EXEC  CICS ADDRESS CSA(R5)
         USING DFHSCSADS,R5
*        ***********************************TEST IF NO DL/I
         L     R2,CSASITBA                  LOAD SIT ADDRESS
         USING DFHSITDS,R2
         CLC   SITDL1,=C'NO'                IF THIS CICS HAS NO DL/I
         BE    NOPDIR                        GO END
         EXEC  CICS RECEIVE INTO(TRAN)      RECEIVE INPUT
*        ***********************************PDIR (REMOTE)
         L     R4,CSAOPFLA
         USING CSAOPFL,R4                   ADDRESS OPTIONAL FEATURE
         L     R4,CSADLI
         DROP  R4
         USING DFHDLPDS,R4
         L     R4,DLPRPDIR                  ADDRESS START OF REM PSBS
         DROP  R4
         USING DFHRPDDS,R4                  REMOTE PDIR
PDIRLOOP CLC   RPDLTH,=X'FFFF'              END REMOTE PDIR ?
         BE    PDIREND
         TM    RPDFLG1,RPDRMT               FLAG REMOTE
         BO    PDIRREM
         B     PDIRGO
PDIRREM  CLC   COND,=C'UNS'                 ONLY UNSCHED PSB ?
         BE    PDIRGO
         MVC   TEST1,RPDNAME                PSB NAME
         MVC   TEST2,=C' REM  '
         MVC   LEN,=H'17'
         EXEC  CICS SEND TEXT FROM(TEST0)  LENGTH(LEN) ERASE ACCUM     *
               PAGING HEADER(HEAD1) TRAILER(TRAIL1).
PDIRGO   AH    R4,RPDLTH                    ADD PDIR LENGTH
         B     PDIRLOOP
PDIREND  DS    0H
*        ***********************************PDIR
         DROP  R4
         L     R4,CSAOPFLA
         USING CSAOPFL,R4                   ADDRESS OPTIONAL FEATURE
         L     R4,CSADLI
         DROP  R4
         USING DFHDLPDS,R4                  ADDRESS START OF PDIR
         L     PDIRBAR,DLPPDIR
PLOCLOOP CLC   PDIRPDPT,=X'00000000'        END PDIR ?
         BE    PLOCEND
         CLC   COND,=C'REM'
         BE    PLOCGO
         CLI   PDIRCODE,X'22'               FLAG UNSCHEDULED
         BNE   PLOCX
         MVC   TEST2,=C' UNSC '
         B     PDIRLOC
PLOCX    MVC   TEST2,=C'      '
         CLC   COND,=C'UNS'                 ONLY UNSCHED PSB ?
         BE    PLOCGO
PDIRLOC  MVC   TEST1,PDIRSYM
         MVC   LEN,=H'17'
         EXEC  CICS SEND TEXT FROM(TEST0) LENGTH(LEN) ERASE ACCUM      *
               PAGING HEADER(HEAD1) TRAILER(TRAIL1).
PLOCGO   L     PDIRBAR,PDIRPDPT             ADDRESS OF NEXT PDIR
         B     PLOCLOOP
PLOCEND  DS    0H
         B     RETURN
*        ***********************************CICS WITHOUT DL/I
NOPDIR   DS    0H
         MVC   TEST1,=C'*NO PSB*'
         MVC   LEN,=H'8'
         EXEC  CICS SEND FROM(TEST1) LENGTH(LEN) ERASE.
         EXEC  CICS RETURN
*        ***********************************SEND LIST OF PSBS
RETURN   DS    0H
         EXEC  CICS SEND PAGE RETAIN NOAUTOPAGE.
         EXEC  CICS RETURN
         LTORG
         END


Cozzani Mirco
System Programmer
Cassa di Risparmio della Spezia (Italy)

