Displaying the Page Allocation Map

The following transaction can be used to display the Page
Allocation Map (PAM).  Figure 1 shows a typical display.  The
total DSA size is shown together with the current amount of
storage used, broken down by individual subpool.  The user
can see how close he or she is to experiencing a 'short-on-
storage' condition and a count is maintained of the number of
times that Storage Control Recovery has been entered.  The
program has been tested under both CICS 1.6 and CICS 1.7
under VSE and runs fine.


ASSEMBLER PROGRAM NEKCP5

DFHEISTG DSECT
HEXZERO  DS X'00'
         COPY  NEKCM5
         COPY  DFHBMSCA
         COPY  DFHCSADS
         COPY  DFHTCADS
         DFHTCA CICSYST=YES
         COPY  DFHPAMDS
PROGRAM  CSECT
         EXEC CICS HANDLE AID     PF18(KRAJ)  PF6(KRAJ)                *
               PF5 (LOPET) ENTER(LOPET)  ANYKEY(LANY) PF17(LOPET)
         EXEC CICS HANDLE CONDITION ERROR(ERRORS) NOEDF
OPET     EQU *
         EXEC CICS ADDRESS CSA(R4) NOEDF
         USING DFHCSADS,4
LOPET    LA 6,NEKCM5O
         LA 7,(NEKCM5E-NEKCM5O)
         LA 8,HEXZERO
         LA 9,L'HEXZERO
         ICM 9,B'100',HEXZERO
         MVCL 6,8
         MVC POM4,=X'FFFFFFFC'      EDIT TIME
         NC  POM4,CSATODP           OF DAY
         MVC FLD1AO,EDITT
         ED  FLD1AO,POM4     TIME OF DAY HHMMSSTC EDITIRAMO
         L PAMBAR,CSAPAMA
         L 7,PAMPGNUM   # PAGES IN DSA
         CVD 7,POM
         MVC L3O,EDIT8
         ED  L3O,POM
         L 7,PAMPGSZE     PAGES SIZE
         CVD 7,POM
         MVC L3AO,EDIT8
         ED  L3AO,POM
         L 7,PAMAVAIL   # PAGES AVAILABLE RELATIVE SHORT-ON-STORAGE
         CVD 7,POM              CONDITION
         MVC L4O,EDIT8
         ED  L4O,POM
         L 7,PAMCUSHN   CUSHION SIZE IN PAGES
         CVD 7,POM
         MVC L4AO,EDIT8
         ED  L4AO,POM
         MVC L5O,EDIT2
         ED  L5O,PAMQRCNT COUNT OF REQUEST CURRENTLY QUEUED
         L 7,PAMSRCNT   # STORAGE CONTROL RECOVERY HAS BEEN ENTERED
         CVD 7,POM
         MVC L5AO,EDIT8
         ED  L5AO,POM
         L 7,PAMCTLCT   # PAGES CONTROL            SUBPOOL HEADER
         CVD 7,POM
         MVC L9O,EDIT8
         ED  L9O,POM
         L 7,PAMTELCT   # PAGES TELEPROCESSING     SUBPOOL HEADER
         CVD 7,POM
         MVC L11O,EDIT8
         ED  L11O,POM
         L 7,PAMMIXCT   # PAGES MIXED              SUBPOOL HEADER
         CVD 7,POM
         MVC L13O,EDIT8
         ED  L13O,POM
         L 7,PAMISOCT   # PAGES ISOLATED           SUBPOOL HEADER
         CVD 7,POM
         MVC L15O,EDIT8
         ED  L15O,POM
         L 7,PAMSHRCT   # PAGES SHARED             SUBPOOL HEADER
         CVD 7,POM
         MVC L17O,EDIT8
         ED  L17O,POM
         L 7,PAMRPLCT   # PAGES RPL                SUBPOOL HEADER
         CVD 7,POM
         MVC L19O,EDIT8
         ED  L19O,POM
         L 7,PAMPGMCT   # PAGES PROGRAM            SUBPOOL HEADER
         CVD 7,POM
         MVC L21O,EDIT8
         ED  L21O,POM
         B SNDMAP
ERRORS   MVC ERRMSGO,=CL79' ERRORS     CALL SYSTEM PROGRAMMER'
         B SNDMAP
LANY     MVC ERRMSGO,=CL79'            WRONG KEY       '
         B SNDMAP
SNDMAP   EXEC CICS SEND MAP('NEKCM5') ERASE    NOEDF
         EXEC CICS RECEIVE MAP('NEKCM5')  NOEDF
KRAJ     MVC PORUKA,=CL19'NORMAL END OF TASK '
         EXEC CICS SEND FROM(PORUKA) ERASE NOEDF
         EXEC CICS RETURN  NOEDF
         LTORG
PAMBAR   EQU 10
               CNOP 0,8
POM      DS 2F
POM4     DS F
PORUKA   DS CL19
EDIT8    DC XL16'60202020202020202020202020202120'
EDITT    DC XL16'40404040404021207A20207A20206120'
EDIT2    DC XL16'60606060606060606060606060202120'
         END


MAP NEKCM5

MAPSET   DFHMSD TYPE=&SYSPARM,MODE=INOUT,CTRL=(FREEKB,FRSET),          C
               TIOAPFX=YES,STORAGE=AUTO
NEKCM5   DFHMDI SIZE=(24,80)
IME      DFHMDF POS=(1,1),LENGTH=7,INITIAL='NEKCM5',ATTRB=(ASKIP,FSET)
LINE1    DFHMDF POS=(1,13),                                            C
               ATTRB=(PROT,ASKIP,BRT),LENGTH=40,                       C
               INITIAL='            PAGE ALLOCATION MAP (PAM)'
FLD1A    DFHMDF POS=(1,63),LENGTH=16,                                  C
               ATTRB=(FSET,PROT)
LINE3    DFHMDF POS=(3,1),                                             C
               ATTRB=(PROT,ASKIP),LENGTH=20,                           C
               INITIAL='    # PAGES IN DSA :'
L3       DFHMDF POS=(3,22),LENGTH=16,                                  C
               ATTRB=(FSET,PROT)
LINE3A   DFHMDF POS=(3,40),                                            C
               ATTRB=(PROT,ASKIP),LENGTH=20,                           C
               INITIAL='         PAGE SIZE :'
L3A      DFHMDF POS=(3,62),LENGTH=16,                                  C
               ATTRB=(FSET,PROT)
LINE4    DFHMDF POS=(4,1),                                             C
               ATTRB=(PROT,ASKIP),LENGTH=20,                           C
               INITIAL='AVAILABLE PAGES-SOS:'
L4       DFHMDF POS=(4,22),LENGTH=16,                                  C
               ATTRB=(FSET,PROT)
LINE4A   DFHMDF POS=(4,40),                                            C
               ATTRB=(PROT,ASKIP),LENGTH=20,                           C
               INITIAL='CUSHION SIZE-PAGES :'
L4A      DFHMDF POS=(4,62),LENGTH=16,                                  C
               ATTRB=(FSET,PROT)
LINE5    DFHMDF POS=(5,1),                                             C
               ATTRB=(PROT,ASKIP),LENGTH=20,                           C
               INITIAL='CURRENTLY COUNT REQ:'
L5       DFHMDF POS=(5,22),LENGTH=16,                                  C
               ATTRB=(FSET,PROT)
LINE5A   DFHMDF POS=(5,40),                                            C
               ATTRB=(PROT,ASKIP),LENGTH=20,                           C
               INITIAL='# ST.CON. RECOVERY :'
L5A      DFHMDF POS=(5,62),LENGTH=16,                                  C
               ATTRB=(FSET,PROT)
LINE7    DFHMDF POS=(7,1),                                             C
               ATTRB=(PROT,ASKIP),LENGTH=79,                           C
               INITIAL='NUMBER OF PAGES ALLOCATED FOR SUBPOOL HEADER:'
LINE9    DFHMDF POS=(9,28),                                            C
               ATTRB=(PROT,ASKIP),LENGTH=16,                           C
               INITIAL='       CONTROL'
L9       DFHMDF POS=(9,48),LENGTH=16,                                  C
               ATTRB=(FSET,PROT)
LINE11   DFHMDF POS=(11,28),                                           C
               ATTRB=(PROT,ASKIP),LENGTH=16,                           C
               INITIAL='TELEPROCESSING'
L11      DFHMDF POS=(11,48),LENGTH=16,                                 C
               ATTRB=(FSET,PROT)
LINE13   DFHMDF POS=(13,28),                                           C
               ATTRB=(PROT,ASKIP),                                     C
               LENGTH=16,                                              C
               INITIAL='         MIXED'
L13      DFHMDF POS=(13,48),LENGTH=16,                                 C
               ATTRB=(FSET,PROT)
LINE15   DFHMDF POS=(15,28),                                           C
               ATTRB=(PROT,ASKIP),                                     C
               LENGTH=16,                                              C
               INITIAL='      ISOLATED'
L15      DFHMDF POS=(15,48),LENGTH=16,                                 C
               ATTRB=(FSET,PROT)
LINE17   DFHMDF POS=(17,28),                                           C
               ATTRB=(PROT,ASKIP),                                     C
               LENGTH=16,                                              C
               INITIAL='        SHARED'
L17      DFHMDF POS=(17,48),LENGTH=16,                                 C
               ATTRB=(FSET,PROT)
LINE19   DFHMDF POS=(19,28),                                           C
               ATTRB=(PROT,ASKIP),                                     C
               LENGTH=16,                                              C
               INITIAL='           RPL'
L19      DFHMDF POS=(19,48),LENGTH=16,                                 C
               ATTRB=(FSET,PROT)
LINE21   DFHMDF POS=(21,28),                                           C
               ATTRB=(PROT,ASKIP),                                     C
               LENGTH=16,                                              C
               INITIAL='       PROGRAM'
L21      DFHMDF POS=(21,48),LENGTH=16,                                 C
               ATTRB=(FSET,PROT)
LINE23   DFHMDF POS=(23,1),                                            C
               ATTRB=(PROT,ASKIP),                                     C
               LENGTH=79,                                              C
               INITIAL='  PF6,PF18 - CICS   PF5,PF17 - REFRESH  '
ERRMSG   DFHMDF POS=(24,1),LENGTH=79,ATTRB=(BRT,PROT)
         DFHMSD TYPE=FINAL
         END


Nenad Kuzmanovic
Arenaturist (Yugoslavia)                   c Nenad Kuzmanovic 1990


