Protecting AORs with RACF

Like many CICS shops these days we have had to go to an
MRO set up to accommodate the varied user requirements for
accessibility as well as helping us obtain some Virtual Storage
Constraint Relief (VSCR). In doing so we have encountered a
problem in trying to keep users from signing on to some of our
Application Owning Regions (AORs) instead of the Terminal
Owning Region (TOR). The first thing we tried was using the
RACF APPL class to keep all users from the AORs. This did
not work. The routed transactions were prevented from
executing on the AOR by security violations.

To solve this problem we came up with the following scheme.
We were not using the CICS XPPT option for protecting CICS
programs so we decided to build a set of profiles in this group
and allow CICS to load them at CICS start-up. We would then
issue our own RACROUTE FASTAUTH at sign-on time in our
CICS sign-on program front end, which we already had in
place.

The details of the implementation are as follows:

o      Define a resource class for CICS to load through the XPPT
      option (we use M$T$USER in our test regions and
      M$P$USER in our production regions).

o      Define resource names to be checked for each region in
      question.  We have decided on the following:

      -      F-LOGONCICSP1 for log-on access to our CICSP1 region.
      -      F-LOGONCICST* (this is a generic) for log-on access
            to test regions.

o      Add users to the access list of the appropriate resource.

You can also set up different resources in this class to protect
other entities. We use it to allow special people to exit our
CICS architecture to native CICS. The resource is checked at
an appropriate time by a call to a security management program
we have written.

The following program is our sign-on program front end.  It
links to the supplied DFHSNP for password verification and to
get the ACEE built. It then checks the appropriate region-
specific resource class to see if the user has access to this
region. If not, a message is displayed indicating the error and
the sign-off is performed when the person presses ENTER.

We are currently running CICS 2.1.1 under MVS/ESA with
RACF 1.9.

         TITLE 'D  F  H  S  N  P  I   -  KOHLS CICS SIGN ON PROGRAM INT*
               ERFACE.'
         GBLA  &VSDSECT
&VSDSECT SETA  2                   PRINT SOME DSECTS
TIOABAR  EQU   R6
TCTTEAR  EQU   R7
LINKREG  EQU   R12
XFF      EQU   X'FF'
         COPY  DFHAID
         COPY  DFHCSADS
         COPY  DFHTIOA
         COPY  DFHTCTTE
         DFHTCA  CICSYST=YES
*        RE-ENTRANT WORKAREA
WORKAREA DSECT
SAA      DS    D
NAMEL    DC    H'0'
NAME     DC    CL20' '
PASSWRDL DC    H'0'
PASSWORD DC    CL20' '
*--------------------------------------------------------------------
*        RACF WORK AREAS
*--------------------------------------------------------------------
SAVEREGS DS    15F
SAVEADDR DS    F
RACFATTR DS    X
READATTR EQU   X'02'
RACFWRK  DS    24F
RACFCLAS DS    CL8                           RACF CLASS FOR CHECK
RACFNTTY DS    CL17                          RACF ENTITY FOR CHECK
RACFPARM DS    CL(FLSTLEN)
RACROUTW DS    CL512                         ROUTER WORK AREA
WORKL    EQU   *-WORKAREA
*        DUMMY TIOA TO BE PASSED TO DFHSNP
CSSNTIOA DSECT                    DSECT FOR CSSN TIOA
         DS    CL8               'CSSN PS='
CSSNPSWD DS    CL8               ' '
         DS    CL6               ',NAME='
CSSNNAME DS    CL20              ' '
CSSNDL   EQU   *-CSSNTIOA
*--------------------------------------------------------------------
*        DSECTS
*--------------------------------------------------------------------
         DFHSNT TYPE=DSECT                   SIGN ON TABLE
         IHAACEE                             ACEE
         IHAPSA
         IKJTCB
MYTIOT   DSECT
         IEFTIOT1
DFHSNPI  CSECT
DFHSNPIN DS    0H
         BALR  R11,0               LOAD  BASE REG
         USING *,R11               TELL  ASSEMBLER
         B     START               BRANCH AROUND CONSTANTS
         DC    C'*** DFHSNPI AT &SYSDATE &SYSTIME ***'
START    DS    0H
         L     TCTTEAR,TCAFCAAA    ADDRESS  OF MY TCTTE
         L     TIOABAR,TCTTEDA     ADDRESS  OF TERMINAL INPUT
STARTC   DS    0H
         LTR   TIOABAR,TIOABAR     TIOA PRESENT
         BZ    SENDMAP
         CLI   TCTTEAID,DFHPF3     PF3 KEY PRESSED
         BE    DISCON              ..YES, GO PROCESS
         CLI   TCTTEAID,DFHPF15    PF15 KEY PRESSED
         BE    DISCON              ..YES, GO PROCESS
         CLI   TIOADBA,X'11'       FORMATTED SCREEN
         BNE   SENDMAP             ..NO, GO SEND MAP
         CLI   TCTTEAID,DFHENTER   ENTER KEY PRESSED
         BE    MAINLINE            ..YES, GO PROCESS
         B     SENDMAP             ELSE SEND MAP
MAINLINE EQU   *
*        ACQUIRE WORKAREA
         DFHSC TYPE=GETMAIN,      ACQUIRE NEW TIOA                     X
               NUMBYTE=WORKL,     *                                    X
               INITIMG=00,        *                                    X
               CLASS=USER         *
         L     R5,TCASCSA         ADDRESS OF GOTTEN STORAGE
         USING WORKAREA,R5
*        LOCATE NAME
         LA    R8,TIOADBA+3       POINT PAST SBA
         LR    R9,R8              SAVE POINTER
         LA    R10,20             SET BCT LIMIT
LOOP1    EQU   *
         CLI   0(R8),X'11'        END OF NAME
         BE    FOUNDNAM           ..YES, GO PROCESS
         LA    R8,1(R8)           POINT TO NEXT BYTE
         BCT   R10,LOOP1          CONTINUE SCAN
FOUNDNAM EQU   *
         LR    R10,R8             SAVE POINTER
         SR    R10,R9             CALC LENGTH OF NAME
         BZ    NONAME             NAME NOT PROVIDED
         BCTR  R10,0              REDUCE LENGTH BY ONE FOR EX
         STH   R10,NAMEL          SAVE FOR LATER
         EX    R10,MOVENAM1       MOVE NAME TO TWA
*        LOCATE PASSWORD
         LA    R8,3(R8)           POINT PASS SBA
         LR    R9,R8              COPY POINTER
         LA    R10,8              SET BCT LIMIT
LOOP2    EQU   *
         CLI   0(R8),X'00'        END OF NAME
         BE    FOUNDPAS           ..YES, GO PROCESS
         LA    R8,1(R8)           POINT TO NEXT BYTE
         BCT   R10,LOOP2          CONTINUE SCAN
FOUNDPAS EQU   *
         LR    R10,R8             SAVE POINTER
         SR    R10,R9             CALC LENGTH OF NAME
         BZ    NOPASSWD           NAME NOT PROVIDED
         BCTR  R10,0              REDUCE LENGTH BY ONE FOR EX
         STH   R10,PASSWRDL       STORE LENGTH FOR LATER
         EX    R10,MOVEPSW1       MOVE PASSWORD TO TWA
         MVC   TIOATDL,CSSNLEN    SET UP DUMMY TIOA
         MVC   TIOADBA(CSSNL),CSSN
         LA    R15,TIOADBA
         USING CSSNTIOA,R15
         EX    R10,MOVEPSW2       MOVE PASSWORD TO DUMMY TIOA
         LH    R10,NAMEL          PICK UP LENGTH OF NAME
         EX    R10,MOVENAM2       MOVE NAME TO DUMMY TIOA
*     LINK TO DFHSNP TO PERFOM THE ACTUAL SIGN ON
         DFHPC TYPE=LINK,PROGRAM=DFHSNP
         TM    TCTTETS,TCTTESTA   DID OPERATOR GET SIGNED ON?
         BZ    CSSNFAIL           ..NO, CANNOT CONTINUE
*--------------------------------------------------------------------
*   CHECK RACF RESOURCE -
*     DETERMINE IF USER IS ELIGIBLE TO ACCESS THIS CICS REGION
*--------------------------------------------------------------------
AUTHCODE EQU   *
         STM   11,8,SAVEREGS                 SAVE REG (NOT 9,10)
         LR    R10,R15                       SAVE BASE
         L     R3,TCTTESNT                   POINTER TO SIGNON TABLE
         USING DFHSNNT,R3                    BASE
         L     R2,SNNTUBLK                   POINTER TO ACEE
         DROP  R3
         USING ACEE,R2
         LTR   R2,R2                         HAVE ACEE?
         BNZ   AUTH010                       YES, CONTINUE
         LM    11,8,SAVEREGS                 SAVE REGISTERS
         B     DISCON1                       DISCONNECT USER
AUTH010 EQU    *
         MVC   RACFPARM(FLSTLEN),FLSTFORM    MOVE LIST FORM OF MACRO
         MVI   RACFATTR,READATTR             ACCESS IS READ
         MVC   RACFCLAS,=CL8'M$P$USER'       PROD USER CLASS
         CLI   JOBNAME+4,C'T'                TEST REGION?
         BNE   AUTH020                       NO, USER PROD
         MVI   RACFCLAS+2,C'T'               M$T$USER  TEST CLASS NAME
AUTH020 EQU    *
         MVC   RACFNTTY(7),=C'F-LOGON'       ENTITY
         MVC   RACFNTTY+7(6),JOBNAME         ENTITY
         MVC   RACFNTTY+13(4),=CL4'    '     ENTITY
         LA    R11,0(R11)                    CLEAR BASE REG HIGHBYTE
         LA    R3,AUTH030
         ST    R3,SAVEADDR
         OI    SAVEADDR,X'80'
         L     R3,SAVEADDR
         BSM   0,R3                          SET 31-BIT ADDR MODE
AUTH030  EQU   *
         LA    R3,RACFCLAS                   USE CLASS GIVEN BY CALLER
         LA    R4,RACFWRK                    POINT AT WORK AREA
         LA    R8,RACFNTTY                   POINT TO ENTITY
         SR    R9,R9                         CLEAR REG7
         IC    R9,RACFATTR                   PLACE ATTR RIGHTMOST BYTE
         RACROUTE REQUEST=FASTAUTH,                                    C
               WORKA=RACROUTW,                                         C
               ACEE=(R2),                                              C
               CLASS=(R3),                                             C
               ATTR=(R9),                                              C
               WKAREA=(R4),                                            C
               ENTITY=(R8),                                            C
               RELEASE=1.8.1,                                          C
               MF=(E,RACFPARM)
         LR    R9,R15                        RACF RETCODE
         LR    R15,R10                       RESTORE BASE
         LM    11,8,SAVEREGS                 RESTORE REGISTERS
         LA    R3,AUTH040
         ST    R3,SAVEADDR
         NI    SAVEADDR,X'7F'
         L     R3,SAVEADDR
         BSM   0,R3                          SET 31-BIT ADDR MODE
AUTH040  EQU   *
         LTR   R9,R9                         IS AUTH ALLOWED?
         BNZ   DISCON1                       NO, DISCONNECT USER
         CLC   JOBNAME+4,=C'PS'               CICSPS?
         BE    RETURN                        NO CICS ARCHITECTURE
         CLC   JOBNAME+4,=C'P2'               CICSP2?
         BE    RETURN                        NO CICS ARCHITECTURE
STARTMNU EQU    *
         MVC   TCAICTID,TCTTETI
         DFHIC TYPE=INITIATE,                START USER MENU TRANS     X
               INTRVAL=000000,                                         X
               TRANSID=MENU                                            X
               TRMIDNT=YES
RETURN   EQU    *
         DFHPC  TYPE=RETURN
SENDMAP  EQU   *
         DFHSC TYPE=GETMAIN,      ACQUIRE NEW TIOA                     X
               NUMBYTE=MAPL,      *                                    X
               CLASS=TERM         *
         L     TIOABAR,TCASCSA    ADDRESS OF NEW TIOA
         ST    TIOABAR,TCTTEDA    ANCHOR IN TCTTE
         SR    R1,R1              SET R1 TO 0
         USING FLC,R1             *
         L     R2,PSATOLD         ADDRESS OF THIS TASKS TCB
         USING TCB,R2             *
         DROP  R1
         L     R1,TCBTIO          ADDRESS OF TIOT
         USING TIOT1,R1           *
         MVC   JOBNAME,TIOCNJOB   MOVE IN USERID
         MVC   TERMID,TCTTETI     MOVE IN TERMID
         SR    R2,R2              CLEAR R2
         LA    R0,TIOADBA         POINT AT TIOA
         LH    R1,MAPLEN          MOVE LENGTH INTO R1
         LA    R2,MAP             POINT AT SENDING DATA
         LR    R3,R1              MOVE LENGTH INTO R3
         MVCL  R0,R2              DO MOVE
         MVC   TIOATDL,MAPLEN     SET LENGTH IN TIOA
         MVI   TIOACLCR,X'C2'     WRITE CONTROL CHAR
         DFHTC TYPE=(WRITE,ERASE), WRITE THE TIOA                      X
               CTLCHAR=YES        *
         DFHPC TYPE=RETURN,       RETURN TO CICS                       X
               TRANSID=CICS       *
NONAME   EQU   *
         DFHSC TYPE=GETMAIN,      ACQUIRE NEW TIOA                     X
               NUMBYTE=MSG2L,     *                                    X
               CLASS=TERM         *
         L     TIOABAR,TCASCSA    ADDRESS OF NEW TIOA
         ST    TIOABAR,TCTTEDA    ANCHOR IN TCTTE
         MVC   TIOADBA(MSG2L),MSG2 MOVE IN MAP
         MVC   TIOATDL,MSG2LEN    SET LENGTH IN TIOA
         B     TCWRITE
NOPASSWD EQU   *
         DFHSC TYPE=GETMAIN,      ACQUIRE NEW TIOA                     X
               NUMBYTE=MSG3L,     *                                    X
               CLASS=TERM         *
         L     TIOABAR,TCASCSA    ADDRESS OF NEW TIOA
         ST    TIOABAR,TCTTEDA    ANCHOR IN TCTTE
         MVC   TIOADBA(MSG3L),MSG3 MOVE IN MAP
         MVC   TIOATDL,MSG3LEN    SET LENGTH IN TIOA
TCWRITE  EQU   *
         MVI   TIOACLCR,X'C4'     WRITE CONTROL CHAR
         DFHTC TYPE=WRITE,        WRITE THE TIOA                       X
               CTLCHAR=YES        *
         DFHPC TYPE=RETURN,       RETURN TO CICS                       X
               TRANSID=CICS       *
CSSNFAIL EQU   *
         DFHPC TYPE=RETURN,       RETURN TO CICS                       X
               TRANSID=CICS       *
DISCON   EQU   *
         DFHSC TYPE=GETMAIN,      ACQUIRE NEW TIOA                     X
               NUMBYTE=MSG1L,     *                                    X
               CLASS=TERM         *
         L     TIOABAR,TCASCSA    ADDRESS OF NEW TIOA
         ST    TIOABAR,TCTTEDA    ANCHOR IN TCTTE
         MVC   TIOADBA(MSG1L),MSG1 MOVE IN MAP
         MVC   TIOATDL,MSG1LEN    SET LENGTH IN TIOA
         MVI   TIOACLCR,X'C4'     WRITE CONTROL CHAR
         DFHTC TYPE=WRITE,        WRITE THE TIOA                       X
               CTLCHAR=YES        *
         MVI   TCTTEAID,X'00'     CLEAR AID
         DFHTC TYPE=DISC
         DFHPC TYPE=RETURN
DISCON1  EQU   *
         DFHSC TYPE=GETMAIN,      ACQUIRE NEW TIOA                     X
               NUMBYTE=MSG9L,     *                                    X
               CLASS=TERM         *
         L     TIOABAR,TCASCSA    ADDRESS OF NEW TIOA
         ST    TIOABAR,TCTTEDA    ANCHOR IN TCTTE
         MVC   TIOADBA(MSG9L),MSG9 MOVE IN MAP
         MVC   TIOATDL,MSG9LEN    SET LENGTH IN TIOA
         MVI   TIOACLCR,X'C4'     WRITE CONTROL CHAR
*        WRITE THE MESSAGE AND GIVE THE USER TIME TO READ IT
         DFHTC TYPE=(WRITE,READ), WRITE THE TIOA                       X
               CTLCHAR=YES        *
         MVI   TCTTEAID,X'00'     CLEAR AID
         DFHTC TYPE=DISC
         DFHPC TYPE=RETURN
         LTORG *
MOVENAM1 MVC   NAME(0),0(R9)          MOVE TO TWA
MOVENAM2 MVC   CSSNNAME(0),NAME       MOVE TO TWA
MOVEPSW1 MVC   PASSWORD(0),0(R9)
MOVEPSW2 MVC   CSSNPSWD(0),PASSWORD   MOVE TO DUMMY TIOA
MAP      EQU   *
         DC    X'11407C'          SET BUFFER ADDRESS
         DC    X'1DF0'            START FIELD PROT NORMAL
         DC    C'TERM:'
         DC    X'1DE8'            START FIELD PROT BRIGHT
TERMID   DC    C'XXXX'            TERMINAL ID
         DC    X'11C24A'          SET BUFFER ADDRESS
         DC    X'1DF0'            START FIELD PROT NORMAL
         DC    C'SYSTEM:'
         DC    X'1DE8'            START FIELD PROT BRIGHT
JOBNAME  DC    C'CICS     '       SYSTEM ID (JOBNAME)
         DC    X'11C26A'          SET BUFFER ADDRESS
         DC    C'                                                     '
         DC    X'11C44D'          SET BUFFER ADDRESS
         DC    X'1DF0'            START FIELD PROT NORMAL
         DC    C'SYSTEM SIGN-ON'
         DC    X'114BE3'          SET BUFFER ADDRESS
         DC    X'1DF8'            START FIELD PROT BRIGHT
         DC    C'NAME:'
         DC    X'114BE9'          SET BUFFER ADDRESS
         DC    X'1DC113'          START FIELD UNPROT NORM INSERT CURSOR
         DC    X'114B7E'          SET BUFFER ADDRESS
         DC    X'1DF0'            START FIELD PROTECTED NORMAL
         DC    X'114D7F'          SET BUFFER ADDRESS
         DC    X'1DF8'            START FIELD PROT BRIGHT
         DC    C'PASSWORD:'
         DC    X'114EC9'          SET BUFFER ADDRESS
         DC    X'1D4D'            START FIELD UNPROT DARK
         DC    X'114ED2'          SET BUFFER ADDRESS
         DC    X'1D60'            START FIELD PROTECTED NORMAL
         DC    X'115060'          SET BUFFER ADDRESS
         DC    C'* KEY IN THE ABOVE INFORMATION AND'
         DC    X'11D1F2'          SET BUFFER ADDRESS
         DC    C'PRESS ENTER TO SIGN ON TO CICS/VS'
         DC    X'11D34F'          SET BUFFER ADDRESS
         DC    C'OR'
         DC    X'11D450'          SET BUFFER ADDRESS
         DC    C'* PRESS PF3 TO RETURN TO VTAM'
         DC    X'1DF0'            START FIELD PROTECTED NORMAL
MAPL     EQU   *-MAP
         DS    0H
MAPLEN   DC    AL2(MAPL)
*               M  E  S  S  A  G  E  S
MSG1     EQU   *
         DC    X'115CF0'          SET BUFFER ADDRESS
         DC    X'1DF8'            START FIELD PROT BRIGHT
         DC    C'*** END KEY ACKNOWLEDGED; TERMINAL IS BEING DISCONNECTX
               ED ***'
MSG1L    EQU   *-MSG1
MSG1LEN  DC    AL2(MSG1L)
MSG2     EQU   *
         DC    X'115CF0'          SET BUFFER ADDRESS
         DC    X'1DF8'            START FIELD PROT BRIGHT
         DC    CL75'*** NAME FIELD IS MISSING AND IS REQUIRED'
MSG2L    EQU   *-MSG2
MSG2LEN  DC    AL2(MSG2L)
MSG3     EQU   *
         DC    X'115CF0'          SET BUFFER ADDRESS
         DC    X'1DF8'            START FIELD PROT BRIGHT
         DC    CL75'*** PASSWORD FIELD IS MISSING AND IS REQUIRED'
MSG3L    EQU   *-MSG3
MSG3LEN  DC    AL2(MSG3L)
MSG9     EQU   *
         DC    X'115CF0'          SET BUFFER ADDRESS
         DC    X'1DF8'            START FIELD PROT BRIGHT
         DC    C'*** USER NOT AUTHORIZED TO THIS SYSTEM **** PRESS ENTEX
               R TO DISCONNECT***'
MSG9L    EQU   *-MSG9
MSG9LEN  DC    AL2(MSG9L)
CSSN     EQU   *                  WORK AREA TO BE COPIED TO DUMMY TIOA
         DC    CL8'CSSN PS='
         DC    CL8' '
         DC    CL6',NAME='
         DC    CL20' '
CSSNL    EQU   *-CSSN
CSSNLEN  DC    AL2(CSSNL)
FLSTFORM RACROUTE REQUEST=FASTAUTH,RELEASE=1.8.1,MF=L
FLSTLEN  EQU  *-FLSTFORM
         END   DFHSNPIN


Bruce Borchardt
Senior Systems Programmer (USA)

