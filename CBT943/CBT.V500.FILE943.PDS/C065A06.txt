Resetting a printer's attributes

I have frequently been asked the following questions:

o      A user sends a printout to a printer, which is busy with another
application.  Even after the printer becomes free from that application,
the printout sent by CICS never prints - why?

o      Sometimes a printer refuses to print a report generated through the
CICS on-line print transaction (CSPG) - how can I solve this problem?

The answer to both of these questions is as follows.  A printer that is
not being used on CICS should have the following attributes when
displayed using the CEMT I TERM(termid) command:

      AUT  INS  ATI  REL  CRE

o      INS means that the terminal is in service (OUT means it is out of
service).  A terminal, whether a printer or a 3270-type terminal, must
be in service in order to work properly.

o      ATI means that automatic transaction initiation is allowed on this
terminal (NOATI means that automatic transaction is not allowed).
Automatic transaction initiation means initiating a transaction on a
terminal without the user entering the transaction-id.

o      REL means that this terminal is released from CICS (ACQ means it
is acquired to CICS).

o      CRE means that this terminal can be acquired to CICS if a
transaction is initiated for this terminal, for example through the
interval control START command.  This means that an internally-
generated log-on is allowed for this terminal (NOCRE means the
opposite).

Therefore, if a printer is not printing even though you sent a printout to
it, find out its four-character CICS terminal-id and issue CEMT I
TERM(termid) to display its status.  It should show as REL.  Change the
attributes to INS, ATI, and CRE.  All of these should be present.

Now the printer should become acquired to CICS and start printing.  It
should show ACQ instead of REL and the name of the transaction (eg
CSPG) that is doing the printing should also show on the CEMT panel.

If it still does not print, the printer is probably defined incorrectly in
the TCT or there may be a hardware problem with the printer or
something wrong with the cable connecting it to the local controller: if
it is a remote printer, the line connecting it to the host computer may be
down.

It has been the author's experience that many of the times a printer
would not print it simply had the NOCRE attribute.  Changing it to CRE or
changing REL to ACQ on the CEMT panel would fix the problem and the
printer would start printing.  The transaction (CSPG for example) would
show as active on the terminal issuing the CEMT I TERM display.

I wrote a small routine called FIXTERM that could be invoked from an
application program.  This routine is shown below.  It sets the attributes
of any specified terminal to INS, ATI, ACQ, and CRE.  The application
program would link to this routine before writing report lines to a
printer.  The terminal-id of the printer is passed to the routine in the
COMMAREA.

For example, suppose an application program written in command-level
COBOL prints report lines to a printer with the terminal-id A917.  Prior
to writing the lines, it would do the following:

      MOVE 'A917' TO TERMID.
      EXEC CICS LINK PROGRAM('FIXTERM') COMMAREA(TERMID) LENGTH(4)
           END-EXEC.

where TERMID is defined in the working storage as a field with PIC X(4).

FIXTERM SOURCE CODE
IDENTIFICATION DIVISION.
PROGRAM-ID. FIXTERM.
AUTHOR. BARRY K. NIRMAL.
ENVIRONMENT DIVISION.
DATA DIVISION.
WORKING-STORAGE SECTION.
01  LENGTH-160      PIC S9(4) COMP SYNC VALUE +160.
01  WS-DATA-IN      PIC X(160).
01  CEMT-CMD.
    05 FILLER       PIC X(12) VALUE 'CEMT S TERM('.
    05 WS-TERM-ID   PIC X(4).
    05 FILLER       PIC X(17) VALUE ') INS ATI CRE ACQ'.
    05 FILLER       PIC X(47) VALUE SPACES.
01  WS-MSG          PIC X(80).
01  WS-MSG-1        PIC X(80) VALUE
    'COMMAREA PASSED TO FIXTERM IS NOT OF 4 BYTES. NO ACTION TAKEN'.
01  WS-MSG-2        PIC X(80) VALUE
    'SOME ERROR OCCURRED IN FIXTERM. USE CEDF TO DEBUG THE ERROR'.
LINKAGE SECTION.
01  DFHCOMMAREA.
    10 LS-TERM-ID   PIC X(4).
PROCEDURE DIVISION.
    EXEC CICS HANDLE CONDITION ERROR(ERROR-RTN) END-EXEC.
    IF EIBCALEN NOT = 4
       MOVE WS-MSG-1 TO WS-MSG
       PERFORM SEND-MSG-END-PGM.
    MOVE LS-TERM-ID TO WS-TERM-ID.
    EXEC CICS LINK PROGRAM('DFHEMTP') COMMAREA(CEMT-CMD)
      LENGTH(80) END-EXEC.
    EXEC CICS RETURN END-EXEC.
ERROR-RTN.
    MOVE WS-MSG-2 TO WS-MSG.
SEND-MSG-END-PGM.
    EXEC CICS IGNORE CONDITION ERROR END-EXEC.
    EXEC CICS SEND FROM(WS-MSG) LENGTH(80) ERASE END-EXEC.
    EXEC CICS RECEIVE INTO(WS-DATA-IN) LENGTH(LENGTH-160) END-
EXEC.
    EXEC CICS RETURN END-EXEC.


Barry Kumar Nirmal
Systems Analyst
Saudi Consolidated Electric Company (Saudi Arabia)  c Barry Nirmal 1991

