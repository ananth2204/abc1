Who is using this file?

This program was originally conceived for our operators when
they had problems in opening a CICS file.  They would enter 'O'
on the CEMT I DAT display and would get an 'OPEN FAILED' as
a reply.  The system console would also display 'DFH0987I
OPEN OF FILE nnnnnnnn FAILED: NOT AVAILABLE FOR
TYPE OF PROCESSING VSAM CODES - 0008,00AB', which
we found in most cases meant that another user or CICS system
had that file.

The operators then had to look at the system to try to find where
the file was allocated and do the necessary to enable it to the
CICS system that wanted it.

This program uses the Global Resource Serialisation (GRS) Queue
Scanning Module (ISGQSCAN) to build a list of Resource
Information Blocks (RIBs) and Resource Information Block
Extents (RIBEs) in the GRS address space and then pass them
back to the CICS address space.  They are then put into a BMS
map and displayed on the screen.  The screen is able to output 40
users of the dataset.  This can easily be increased by moving the
display field higher up the map or resorting to BMS paging.

A beneficial feature of this program is that  not only does it look
at CICS files but also it can process any file allocated in the
system, be it a batch job, a TSO user, or a system task.

Input to the transaction must be the fully qualified dataset name.
The program cannot cope with partial names.

The program was written and is used on a 3090-200E using
MVS/XA 2.1.7 and CICS 1.7.  The program should run on any
MVS system that has use of the GQSCAN macro (see SPL:
System Macros and Facilities Volume 2 page 2-171).  The
program also includes the ISGRIB DSECT, which is from
SYS1.MACLIB.  The GQSCAN macro is also from
SYS1.MACLIB.

***                         S Y 3 0 0 .                            ***
* THIS PROGRAM IS DESIGNED AS AN AID TO THE OPERATIONS DEPARTMENT SO *
* THAT THEY CAN FIND OUT WHO IS USING A FILE THAT THEY ARE HAVING    *
* PROBLEMS OPENING OR USING.                                         *
*   MACRO'S : GQSCAN.                                                *
*   CONTROL BLOCKS : NONE                                            *
*   SVC'S : NONE                                                     *
*** TRANSID : SY30                                                 ***
RIB       DSECT
          ISGRIB
SY300     CSECT
          EXEC CICS HANDLE CONDITION MAPFAIL(INVALOP)
          EXEC CICS HANDLE AID CLEAR(RET)
START     EXEC CICS SEND MAP('SY300M'),MAPSET('SY300S'),               *
          ERASE,FREEKB,ALARM
*  G E T   T H E   M A P   B A C K.               *
RECMAP    EXEC CICS RECEIVE MAP('SY300M') MAPSET('SY300S')
*  CALCULATE THE DATA SET NAME LENGTH USING BXLE            *
          MVI  RN,X'40'               SPACE FILL THE RN FIELD
          MVC  RN+1(43),RN
DSCALC    LA   R7,INPTI               POINT TO START OF INPUT FIELD
          LA   R8,1                   LOAD AN INCREMENTER
          LA   R9,INPTI+44            POINT TO END OF INPUT FIELD
DSCALC1   CLI  0(R7),X'40'            IS IT THE END OF THE DSNAME
          BE   DSCALC2                YES IT IS THE END OF DSNAME
          BXLE R7,R8,DSCALC1          DO A BXLE TO GO UP THE FIELD
DSCALC2   LA   R9,INPTI               POINT TO START OF FIELD
          SR   R7,R9                  FIND THE LENGTH
          BCTR R7,R0                  TAKE 1 OFF FOR THE MACHINE CODE
          EX   R7,EXMVC               EXECUTE THE MOVE
          A    R7,=F'1'               PUT ONE BACK
          LR   R6,R7                  PUT THE DSN LENGTH IN R6
*  SET UP FOR THE GQSCAN MACRO                              *
          EXEC CICS GETMAIN SET(R4) LENGTH(19069)
DSNSCAN   GQSCAN AREA=((R4),19069),SCOPE=ALL,RESNAME=(QN,RN,(R6))
          LTR  R15,R15                HAVE WE FOUND THE DSN
          BNZ  ERROR1                 NO
          CNOP 6,8                    ALIGN ON FULL WORD BOUNDARY
          ST   R0,STORIT              SAVE THE 2 HALF WORDS SENT BACK
          XR   R8,R8                  ZEROISE R8
          LR   R7,R4                  SAVE THE START OF THE RIB
          USING RIB,R7                ESTABLISH ADDRESSABILITY
*  NOW WE START TO EXTRACT THE FILE NAME AND USER FROM RIB    *
          MVC  OUTPO(44),RN           MOVE THE DATASETNAME
          LH   R6,SHW                 LOAD THE LENGTH OF THE RIBE
          L    R2,RIBTRIBE            GET THE WAITING USERS NO.
          LA   R4,RIBEND              LOAD THE ADDRESS OF RIB END
          AH   R4,RIBVLEN             ADD THE VARIABLE SECTION LENGTH
          LA   R8,OUTLSTO             POINT TO START OF OUTPUT
LOOP      EQU  *
          USING RIBE,R4               ESTABLISH ADDRESSABILITY OF RIBE
          MVC  0(8,R8),RIBEJBNM       MOVE THE USER TO THE DISPLAY
          LA   R8,18(R8)              MOVE UP THE DISPLAY
          AR   R4,R6                  POINT TO NEXT RIBE
          BCT  R2,LOOP                LOOP OR DROP THROUGH
SENDIT1   EXEC  CICS SEND MAP('SY301M') MAPSET('SY300S') ERASE ALARM
RET       EXEC  CICS SEND CONTROL FREEKB
          EXEC  CICS RETURN
ERROR1    EXEC  CICS SEND TEXT FROM (ERMSG1) ERASE ALARM FREEKB
          B     RET
INVALOP   EXEC  CICS SEND TEXT FROM (ERMSG2) ERASE ALARM FREEKB
          B     RET
EXMVC     MVC   RN(0),INPTI
ERMSG1   DC  C'THE DATASET REQUESTED IS NOT IN USE PRESS CLEAR AND ENTE*
               R SY30 TO RETRY'
ERMSG2   DC  C'YOUR ENTRY WAS INVALID PLEASE USE A VALID DATA SET NAME'
RN        DC  CL44' '
QN        DC  CL8'SYSDSN'
STORIT    DS  0CL8
FHW       DS  H
SHW       DS  H
DFHEISTG  DSECT
          COPY SY300S
          END SY300

MAP

        TITLE 'WHO IS USING THIS FILE'
SY300S  DFHMSD TYPE=&SYSPARM,MODE=INOUT,LANG=ASM,COLOR=NEUTRAL,        *
               STORAGE=AUTO,TIOAPFX=YES,CTRL=(FRSET,FREEKB),           *
               EXTATT=MAPONLY
SY300M  DFHMDI SIZE=(24,80),COLOR=GREEN
        DFHMDF POS=(2,25),LENGTH=27,ATTRB=PROT,                        *
               INITIAL='COMPUTER SECTION ** CICS **',HILIGHT=UNDERLINE
        DFHMDF POS=(2,53),ATTRB=PROT,LENGTH=1
        DFHMDF POS=(5,30),LENGTH=15,ATTRB=PROT,                        *
               INITIAL='CICS FILE INFO',HILIGHT=UNDERLINE
        DFHMDF POS=(5,46),ATTRB=PROT,LENGTH=1
        DFHMDF POS=(10,1),LENGTH=44,ATTRB=PROT,COLOR=TURQUOISE,        *
               INITIAL='ENTER THE DSN OF THE FILE YOU CANNOT OPEN'
        DFHMDF POS=(12,1),LENGTH=10,ATTRB=PROT,COLOR=TURQUOISE,        *
               INITIAL='INPUT HERE'
INPT    DFHMDF POS=(12,12),LENGTH=44,ATTRB=IC
SY301M  DFHMDI SIZE=(24,80),COLOR=GREEN
        DFHMDF POS=(2,25),LENGTH=27,ATTRB=PROT,                        *
               INITIAL='COMPUTER SECTION ** CICS **',HILIGHT=UNDERLINE
        DFHMDF POS=(2,53),ATTRB=PROT,LENGTH=1
        DFHMDF POS=(5,30),LENGTH=15,ATTRB=PROT,                        *
               INITIAL='CICS FILE INFO',HILIGHT=UNDERLINE
        DFHMDF POS=(5,47),ATTRB=PROT,LENGTH=1
        DFHMDF POS=(10,1),LENGTH=72,ATTRB=PROT,COLOR=TURQUOISE,        *
               INITIAL='BELOW WILL BE LISTED THE USERS OF THE FILE YOU *
               REQUIRE INFORMATION ON'
        DFHMDF POS=(12,1),LENGTH=10,ATTRB=PROT,COLOR=NEUTRAL,          *
               INITIAL='DATASET:'
OUTP    DFHMDF POS=(12,21),LENGTH=44,ATTRB=(PROT)
        DFHMDF POS=(14,1),LENGTH=37,ATTRB=PROT,COLOR=NEUTRAL,          *
               INITIAL='IS BEING USED BY THE FOLLOWING JOBS :'
OUTLST  DFHMDF POS=(16,1),LENGTH=15,OCCURS=40,ATTRB=PROT
        DFHMSD TYPE=FINAL
        END


P Sedman
Systems Programmer (UK) ) Xephon 1989


