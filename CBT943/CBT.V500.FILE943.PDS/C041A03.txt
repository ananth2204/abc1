CICS screen capture and display facility

If you ever have to deal with users remotely by telephone
concerning problems they may have just encountered on their
terminals with your applications, you must surely be aware of the
difficulty of translating 'user speak' into meaningful error
reporting.  Here is an application that will allow authorised CICS
terminal operators to view the screen contents currently
displayed on any other VTAM terminal in that CICS system
(modifications may be made to the system to view non-VTAM
terminals if required).

The application will display the initial screen as shown in Figure 1
and, if required, a table of terminals eligible for viewing, along
with the user-id and operator-id of the associated terminal
operator as shown in Figure 2.  By positioning the cursor under
the required terminal-id and pressing 'enter', the screen contents
of that terminal are displayed.  Pressing 'enter' again refreshes
that display; any other key will re-display the terminal table.

The main control program uses the CICS 'INQUIRE' facility and will
therefore only run under CICS 1.7.  There are also some built-in
constraints on the terminals that are eligible for viewing:

%   The terminal must already be acquired by this CICS system.

%   There must be no task currently active on the desired
    terminal (ie conversational tasks are not eligible for viewing).

The application consists of two Assembler programs (VIEWPGM0
and VIEWPGM1), a mapset (VIEWMAP), and two transaction-ids
(VIEW and VIEX).  Entering VIEW on the terminal will invoke
program VIEWPGM0.  Selecting a terminal then starts transaction-
id VIEX on that terminal, which invokes program VIEWPGM1.  End
users will be unaware that they are being 'VIEWed'; their work
flow will be uninterrupted in terms of returned transaction-ids
and communication areas.

The programs as written have no built-in security checks.  If you
are concerned about the possible unauthorised display of sensitive
data you should restrict the use of the VIEW transaction via CICS
internal security or an external security monitor, and/or add RACF
(or an alternative security facility) calls to the program to
establish authorisation for viewing a particular display.


VIEWPGM0

VIEWPGM0 TITLE 'VIEWPGM0 - DISPLAY CICS SCREEN BUFFERS'
DFHEISTG DSECT
TRANID   DS    CL4
CVDA     DS    F
CVDA1    DS    F
WSLEN1   DS    H
ITEMNO   DS    H
COMMAREA DS    0CL5
PROCIND  DS    C
PASSTERM DS    CL4
TSQID    DS    0CL8
QID      DS    CL4
QTERM    DS    CL4
SCREEN   DS    CL2400
         COPY VIEWM00
         COPY DFHAID
NTSCREEN EQU  X'00'         NO TABLE PROCESSING - SCREEN JUST
DISPLAYED
NTLOGO   EQU  X'01'         NO TABLE PROCESSING - LOGO JUST
DISPLAYED
TABPROC  EQU  X'02'         TABLE PROCESSING ACTIVE
VIEWPGM0 CSECT
A000     EXEC  CICS HANDLE CONDITION ERROR(X000)
         EXEC  CICS HANDLE AID PF3(Z010) PF15(Z010)
         MVI   M00MSG1O,C' '             BLANK OUT ERROR MESSAGES
         MVC   M00MSG1O+1(39),M00MSG1O
         MVI   M00MSG2O,C' '
         MVC   M00MSG2O+1(39),M00MSG2O
         CLC   EIBCALEN(2),=H'0'         FIRST TIME IN?
         BE    A010                      YES, DISPLAY MAP
         L     R6,DFHEICAP               NO, ADDRESS COMMAREA
         MVC   COMMAREA(5),0(R6)             AND MOVE TO OWN STORAGE
         CLI   PROCIND,NTSCREEN          NON-TABLE MODE SCREEN DSPLYD?
         BNE   B000                      NO, BRANCH
         CLI   EIBAID,DFHENTER           OPERATOR PRESSED ENTER?
         BNE   A010                      NO, REDISPLAY LOGO
         MVC   M00TERMI,PASSTERM         YES, REDISPLAY SCREEN
         B     B010
A010     EXEC  CICS SEND MAP('VIEWM00') MAPSET(VIEWMAP') MAPONLY       X
               ERASE FREEKB
         MVI   PROCIND,NTLOGO            SET LOGO JUST DISPLAYED
         MVC   PASSTERM,LOWVALS          ZEROIZE TERMID IN COMMAREA
         EXEC  CICS RETURN COMMAREA(COMMAREA) LENGTH(5) TRANSID('VIEW')
B000     CLI   PROCIND,TABPROC           TERMINAL TABLE REQUIRED?
         BNE   B005                      NO, BRANCH
         LA    R7,B010                   SET EXIT ADDRESS
         CLI   EIBAID,DFHENTER           OPERATOR PRESSED ENTER?
         BE    Q120                      YES, REDISPLAY SCREEN
         MVC   COMMAREA+1(4),LOWVALS     NO, UNSET TERMID IN COMMAREA
         B     Q120                          AND REDISPLAY TABLE
B005     EXEC  CICS RECEIVE MAP('VIEWM00') MAPSET('VIEWMAP')           X
               INTO(VIEWM00I)
         CLC   M00TERML(2),=X'0000'      TERM ID ENTERED?
         BNE   B010                      YES, BRANCH
         BAL   R7,Q000                   NO, DISPLAY TABLE OF TERMINALS
B010     EXEC  CICS HANDLE CONDITION TERMIDERR(B030)
*  INQUIRE ON REQUESTED TERMINAL ID
         EXEC  CICS INQUIRE TERMINAL(M00TERMI) TRANSACTION(TRANID)     X
               ACQSTATUS(CVDA)
         CLC   CVDA(4),=F'69'            TERMINAL ACQUIRED BY CICS?
         BE    B020                      YES, OK
         MVC   M00MSG1O(40),ERRMSG2      NO, DISPLAY ERROR
         B     B040
B020     CLC   TRANID,=CL4'    '         TASK CURRENTLY ACTIVE?
         BE    B100                      NO, OK
         MVC   M00MSG1O,ERRMSG1          YES, DISPLAY ERROR
         B     B040
B030     MVC   M00MSG1O,ERRMSG3          TERMINAL NOT IN TCT
B040     TM    PROCIND,TABPROC           TERM TABLE BEING PROCESSED?
         BO    B045                      YES, BRANCH
         MVI   PROCIND,NTLOGO            SET LOGO JUST DISPLAYED
         MVC   COMMAREA+1(4),LOWVALS     NULLIFY TERM ID IN COMMAREA
         EXEC CICS SEND MAP('VIEWM00') MAPSET('VIEWMAP') ERASE         X
               ALARM FREEKB
         EXEC  CICS RETURN COMMAREA(COMMAREA) LENGTH(5) TRANSID('VIEW')
B045     MVC   M01MSG1O,M00MSG1O         SET ERROR MESSAGE IN MAP #2
         EXEC CICS SEND MAP('VIEWM01') MAPSET('VIEWMAP') ERASE         X
               ALARM FREEKB
         B     Q140                      CONTINUE TABLE PROCESSING
*   EVERYTHING OK - CAPTURE SCREEN BUFFER FROM TARGET TERMINAL
B100     EXEC  CICS START TRANSID('VIEX') TERMID(M00TERMI)
*   ATTEMPT TO READ TS QUEUE CREATED BY STARTED TASK UP TO 10 TIMES
B110     LA    R5,10
         MVC   QID,=CL4'VIEW'             PRESET TS DETAILS
         MVC   QTERM,M00TERMI
         EXEC  CICS HANDLE CONDITION QIDERR(M140)
B120     LA    R6,2400                    READ TS QUEUE
         STH   R6,WSLEN1
         EXEC  CICS READQ TS QUEUE(TSQID) INTO(SCREEN) LENGTH(WSLEN1)
*    TS QUEUE IS READ OK
         EXEC  CICS DELETEQ TS QUEUE(TSQID)
         EXEC  CICS SEND CONTROL CURSOR(0) ERASE
*    SEND SCREEN BUFFER TO TERMINAL
         EXEC  CICS SEND FROM(SCREEN) LENGTH(WSLEN1)
         B     Z000                        EXIT
*   WAIT 1 SECOND BEFORE RETRYING UP TO 10 TIMES
B140     EXEC  CICS DELAY INTERVAL(1)
         BCT   R5,B120                       UP TO 10 TIMES
         MVC   M00MSG1O,ERRMSG1            TASK ACTIVE ON TARGET TERM.
         B     B040                        DISPLAY ERROR
X000     MVC   M00MSG1O,ERRMSG4            OTHER CICS ERROR
         B     B040
Z000     MVC   COMMAREA+1(4),M00TERMI      SET TERMID IN COMMAREA
         TM    PROCIND,TABPROC             TERMINAL TABLE PROCESSED?
         BO    Z005                        YES, BRANCH
         MVI   PROCIND,NTSCREEN            NO, SET INDICATOR
Z005     EXEC  CICS RETURN TRANSID('VIEW') COMMAREA(COMMAREA) LENGTH(5)
Z010     EXEC  CICS IGNORE CONDITION QIDERR
         MVC   QID,=CL4'VIEX'              CLEAN UP BEFORE FINALLY
         MVC   QTERM,EIBTRMID                                 EXITING
         EXEC  CICS DELETEQ QUEUE(TSQID)
         EXEC  CICS SEND TEXT FROM(ENDMSG) LENGTH(40) ERASE FREEKB
         EXEC  CICS RETURN
Q000     MVI   PROCIND,TABPROC          PROCESS TERMINAL TABLE
         LA    R6,32                    SET MAX NO OF TERMS / SCREEN
         LA    R5,M01T10                CLEAR OUTPUT MAP
         MVI   VIEWM01O,X'00'
         MVC   VIEWM01O+1(256),VIEWM01O
         MVC   VIEWM01O+257(256),VIEWM01O+256
         MVC   VIEWM01O+513(256),VIEWM01O+512
         MVC   VIEWM01O+769(256),VIEWM01O+768
         MVC   VIEWM01O+1025(186),VIEWM01O+1024
*    BROWSE TCT
         EXEC  CICS INQUIRE TERMINAL START
         EXEC  CICS HANDLE CONDITION END(Q100)
*   READ NEXT TERMINAL IN TCT
Q020     EXEC  CICS INQUIRE TERMINAL(0(R5)) USERID(11(R5)) NEXT        X
               OPERID(26(R5)) ACQSTATUS(CVDA) TRANSACTION(TRANID)      X
               TTISTATUS(CVDA1)
         CLC   CVDA=F'69'            ACQUIRED?
         BNE   Q020                   NO, LOOP BACK
         CLC   CVDA1(4),=F'78'       NO TTI?
         BE    Q020                   YES, LOOP BACK
         CLC   TRANID,=CL4' '        TASK ACTIVE?
         BNE   Q020                   YES, LOOP BACK
         LA    R5,36(R5)             BUMP POINTERS
         BCT   R6,Q020                AND CONTINUE
Q040     MVC   M01MSG1O,CONTMSG      ANOTHER SCREEN TO COME
         MVC   QID,=CL4'VIEX'        WRITE NEXT TS RECORD
         MVC   QTERM,EIBTRMID
         EXEC  CICS WRITEQ TS QUEUE(TSQID) FROM(VIEWM01O)              X
               LENGTH(M01LNGTH+2)
         MVI   VIEWM01O,X'00'                BLANK OUT MAP
         MVC   VIEWM01O+1(256),VIEWM01O
         MVC   VIEWM01O+257(256),VIEWM01O+256
         MVC   VIEWM01O+513(256),VIEWM01O+512
         MVC   VIEWM01O+769(256),VIEWM01O+768
         MVC   VIEWM01O+1025(186),VIEWM01O+1024
         LA    R6,32                         SET BCT LIMITS
         LA    R5,M01T1O                     RESET POINTER
         B     Q020                            AND CONTINUE BROWSING
Q100     MVC   0(4,R5),LOWVALS               BLANK OUT FINAL DETAILS
         MVC   11(8,R5),LOWVALS
         MVC   26(3,R5),LOWVALS
         MVC   QID,=CL4'VIEX'                WRITE TS RECORD
         MVC   QTERM,EIBTRMID
         EXEC  CICS WRITEQ TS QUEUE(TSQID) FROM(VIEWM01O)              X
               LENGTH(M01LNGTH+2)
Q120     LA    R6,1                        SET ITEM NO. TO 1
         STH   R6,ITEMNO
         EXEC  CICS HANDLE CONDITION ITEMERR(Q147)
        EXEC CICS HANDLE AID PF7(Q160) PF19(Q160) PF8(Q165) PF20(Q165)
Q130     MVC   QID,=CL4'VIEX'               READ RELEVANT TS RECORD
         MVC   QTERM,EIBTRMID
         EXEC  CICS READQ TS QUEUE(TSQID) INTO(VIEWM01O) ITEM(ITEMNO)  X
               LENGTH(M01LNGTH+2)
         CLC   COMMAREA+1(4),LOWVALS        OPERATOR PRESSED ENTER?
         BE    Q135                         NO, REDISPLAY TABLE
         MVC   M00TERMI,COMMAREA+1          YES, SET TERM ID
         BR    R7                              AND RETURN TO MAIN CODE
*     DISPLAY TERMINAL TABLE
Q135     EXEC  CICS SEND MAP('VIEWM01') MAPSET('VIEWMAP') ERASE FREEKB
Q140     EXEC  CICS RECEIVE MAP('VIEWM01') MAPSET('VIEWMAP')
         LH    R5,EIBCPOSN                   VALIDATE CURSOR POSITION
         SH    R5,=H'324'                    TEST AGAINST LOWER
         BM    Q145                               AND UPPER BOUNDS
         CH    R5,=H'1280'
         BNH   Q150
Q145     MVC   M01MSG1O,ERRMSG5              DISPLAY ERROR MESSAGE
         B     Q148
Q147     LH    R6,ITEMNO
         LTR   R6,R6                         ITEMNO = ZERO?
         BNZ   Q147A                         NO, BRANCH
         LA    R6,1(R6)                      YES, RESET TO 1
         B     Q147B
Q147A    SH    R6=H'1'                        BY SUBTRACTING 1
Q147B    STH   R6,ITEMNO                     RESTORE VALID ITEM NO
         MVC   M01MSG10,ERRMSG6              AND DISPLAY SCROLL ERROR
Q148     EXEC  CICS SEND MAP('VIEWM01') MAPSET('VIEWMAP') DATAONLY     X
               FREEKB ALARM
         B     Q140
Q150     SR    R4,R4                             CURSOR POSITION
         D     R4,=F'40'                     POSITION SHOULD START ON
         CH    R4,=H'3'                          MULTIPLE OF 40
         BH    Q145                          INVALID CURSOR POSITION
         M     R4,=F'36'
         LA    R6,M01T1O(R5)                 PICK UP REQUIRED TERM ID
         MVC   M00TERMI,0(R6)
         CLC   M00TERMI,LOWVALS              LOW VALUES?
         BE    Q145                          YES - INVALID POSITION
         BR    R7                            NO, RETURN TO MAIN CODING
Q160     LH    R6,ITEMNO                     PF7/19 HIT
         SH    R6,=H'1'
         STH   R6,ITEMNO                     RETRIEVE PREVIOUS TS REC
         B     Q130
Q165     LH    R6,ITEMNO                     PF8/20 HIT
         AH    R6,=H'1'                      RETRIEVE NEXT TS REC
         STH   R6,ITEMNO
         B     Q130
M01LNGTH DC    A(VIEWM01E-VIEWM01O)
LOWVALS  DC    XL8'0000000000000000'
ERRMSG1  DC    CL40'TASK CURRENTLY ACTIVE ON THIS
TERMINAL'
ERRMSG2  DC    CL40'TERMINAL IS NOT IN USE ON THIS SYSTEM'
ERRMSG3  DC    CL40'UNKNOWN TERMINAL IDENTIFICATION'
ERRMSG4  DC    CL40'INTERNAL PROGRAM ERROR'
ERRMSG5  DC    CL40'INVALID CURSOR POSITION - TRY AGAIN'
ERRMSG6  DC    CL40'NO FURTHER SCROLLING POSSIBLE'
CONTMSG  DC    CL40'MORE TERMINALS TO COME .................'
ENDMSG   DC    CL40'VIEW PROCESSING COMPLETED'
         LTORG
VIEWEND  END   VIEWPGM0


VIEWMAP

VIEWMAP  DFHMSD TYPE=MAP,MODE=INOUT,CTRL=FREEKB,TIOAPFX=YES,           X
               STORAGE=AUTO,EXTATT=YES
VIEWM00  DFHMDI SIZE=(24,80)
         DFHMDF POS=(1,14),LENGTH=45,ATTRB=ASKIP,COLOR=YELLOW,         X
               INITIAL='VV    VV     II     EEEEEE    WW           WW'
         DFHMDF POS=(2,14),LENGTH=45,ATTRB=ASKIP,COLOR=YELLOW,         X
               INITIAL='VV   VV     II     EE         WW          WW '
         DFHMDF POS=(3,14),LENGTH=45,ATTRB=ASKIP,COLOR=YELLOW,         X
               INITIAL='VV  VV     II     EE          WW         WW  '
         DFHMDF POS=(4,14),LENGTH=45,ATTRB=ASKIP,COLOR=YELLOW,         X
               INITIAL='VV VV      II    EEEE         WW  WW    WW   '
         DFHMDF POS=(5,14),LENGTH=45,ATTRB=ASKIP,COLOR=YELLOW,         X
               INITIAL='VVVV     II     EE            WW WWWW  WW    '
         DFHMDF POS=(6,14),LENGTH=45,ATTRB=ASKIP,COLOR=YELLOW,         X
               INITIAL='VVV     II     EE             WWW   WWWW     '
         DFHMDF POS=(7,14),LENGTH=45,ATTRB=ASKIP,COLOR=YELLOW,         X
               INITIAL='VV     II     EEEEEEEE        WW     WW      '
         DFHMDF POS=(11,10),LENGTH=28,ATTRB=ASKIP,COLOR=BLUE,          X
               INITIAL='THIS FACILITY ENABLES YOU TO'
         DFHMDF POS=(11,39),LENGTH=5,ATTRB=ASKIP,COLOR=YELLOW,         X
                INITIAL='VIEW!'
         DFHMDF POS=(11,45),LENGTH=19,ATTRB=ASKIP,COLOR=BLUE,          X
                INITIAL='THE CURRENT DISPLAY'
         DFHMDF POS=(12,10),LENGTH=53,ATTRB=ASKIP,COLOR=BLUE,          X
               INITIAL='ON ANY SCREEN CURRENTLY SIGNED ON TO THIS CICS X
               SYSTEM'
         DFHMDF POS=(14,10),LENGTH=32,ATTRB=ASKIP,COLOR=BLUE,          X
               INITIAL='ENTER THE TERMINAL ID RIGHT HERE'
         DFHMDF POS=(14,45),LENGTH=4,ATTRB=(ASKP,BRT),INITIAL='===>'
M00TERM  DFHMDF POS=(14,50),LENGTH=4,ATTRB=(IC,FSET)
         DFHMDF POS=(14,55),LENGTH=1,ATTRB=(PROT,DRK)
         DFHMDF POS=(15,10),LENGTH=39,ATTRB=ASKIP,COLOR=BLUE,          X
               INITIAL='(LEAVE BLANK FOR LIST OF CURRENT USERS)'
         DFHMDF POS=(17,10),LENGTH=23,ATTRB=ASKIP,COLOR=BLUE,          X
               INITIAL='HIT ENTER TO UPDATE THE'
         DFHMDF POS=(17,34),LENGTH=5,ATTRB=ASKIP,COLOR=YELLOW,         X
               INITIAL='VIEW!'
         DFHMDF POS=(17,40),LENGTH=12,ATTRB=ASKIP,COLOR=BLUE,          X
               INITIAL='ED SCREEN OR'
         DFHMDF POS=(18,10),LENGTH=44,ATTRB=ASKIP,COLOR=BLUE,          X
               INITIAL='PRESS ANY OTHER KEY TO RETURN TO THIS SCREEN'
M00MSG1  DFHMDF POS=(20,19),LENGTH+40,ATTRB=PROT,COLOR=RED
M00MSG2  DFHMDF POS=21,19),LENGTH=40,ATTRB=PROT,COLOR=RED
         DFHMDF POS=(22,10),LENGTH=3,ATTRB=ASKIP,COLOR=BLUE,           X
               INITIAL='HIT'
         DFHMDF POS=(22,14),LENGTH=3,ATTRB=(ASKIP,BRT),                X
               INITIAL='PF3'
         DFHMDF POS=(22,18),LENGTH=17,ATTRB=ASKIP,COLOR=BLUE,          X
               INITIAL='TO EXIT FROM HERE'
VIEWM01  DFHMDI SIZE=(24,80),COLOR=GREEN
         DFHMDF POS=(1,14),LENGTH=46,ATTRB=ASKIP,COLOR=TURQUOISE,      X
               INITIAL='VIEW! - CICS SCREEN CAPTURE + DISPLAY FACILITY'
         DFHMDF POS=(3,3),LENGTH=25,ATTRB=(ASKIP,BRT),COLOR=NEUTRAL,   X
               INITIAL='TERM ID   USER ID   OP ID'
         DFHMDF POS=(3,43),LENGTH=25,ATTRB=(ASKIP,BRT),COLOR=NEUTRAL,  X
               INITIAL='TERM ID   USER ID   OP ID'
M01T1    DFHMDF POS=(5,4),LENGTH=4,ATTRB=(UNPROT,IC,FSET)
         DFHMDF POS=(5,9),LENGTH=1,ATTRB=PROT
M01T1U   DFHMDF POS=(5,12),LENGTH=8,ATTRB=(PROT,FSET)
M01T1O   DFHMDF POS=(5,24),LENGTH=3,ATTRB=(PROT,FSET)
M01T2    DFHMDF POS=(5,44),LENGTH=4,ATTRB=(UNPROT,FSET)
         DFHMDF POS=(5,9),LENGTH=1,ATTRB=PROT
M01T2U   DFHMDF POS=(5,52),LENGTH=8,ATTRB=(PROT,FSET)
M01T2O   DFHMDF POS=(5,64),LENGTH=3,ATTRB=(PROT,FSET)

         and similarly for M01T3-31, M01T3U-31U, and M01T3O-31O

M01T32   DFHMDF POS=(20,44),LENGTH=4,ATTRB=(UNPROT,FSET)
         DFHMDF POS=(5,9),LENGTH=1,ATTRB=PROT
M01T32U  DFHMDF POS=(20,52),LENGTH=8,ATTRB=(PROT,FSET)
M01T32O  DFHMDF POS=(20,64),LENGTH=3,ATTRB=(PROT,FSET)
M01MSG1  DFHMDF POS=(21,16),LENGTH=40,ATTRB=PROT,COLOR=RED
         DFHMDF POS=(22,1),LENGTH=43,ATTRB=ASKIP,COLOR=BLUE            X
               INITIAL='PLACE CURSOR UNDER DESIRED TERMINAL AND HIT'
         DFHMDF POS=(22,55),LENGTH=5,ATTRB=(ASKIP,BRT),                X
               COLOR=NEUTRAL,INITIAL='ENTER'
         DFHMDF POS=(23,1),LENGTH=19,ATTRB=ASKIP,COLOR=BLUE,           X
               INITIAL='HIT ENTER TO UPDATE'
         DFHMDF POS=(23,21),LENGTH=5,ATTRB=ASKIP,COLOR=YELLOW,         X
               INITIAL='VIEW!'
         DFHMDF POS=(23,27),LENGTH=27,ATTRB=ASKIP,COLOR=BLUE,          X
               INITIAL='ED SCREEN AND ANY OTHER KEY'
         DFHMDF POS=(23,55),LENGTH=24,ATTRB=ASKIP,COLOR=BLUE,          X
               INITIAL='TO RETURN TO THIS SCREEN'
         DFHMDF POS=(24,13),LENGTH=45,ATTRB=(ASKIP,BRT),COLOR=NEUTRAL, X
               INITIAL='PF3/15 - END    PF7/19 - BACK    PF8/20 - FOR'
VIEWEND  DFHMSD TYPE=FINAL
         END


VIEWPGM1

VIEWPGM1 TITLE 'VIEWPGM1 - GET CICS SCREEN FROM BACKGROUND TASK'
         GBLB  &DFHEIMX
&DFHEIMX SETB  1
TCTTEAR  EQU   8
         COPY DFHCSADS
         DFHTCA CICSYST=YES
         COPY DFHTCTTE
         COPY DFHEICDS
DFHEISTG DSECT
STOTETC  DS   CL4
STOTEEIA DS   CL4
CSAADD   DS   AL4
WSLEN1   DS   H
SAVESCRN DS   CL2400
TSQID    DS   0CL8
QID      DS   CL4
QTERM    DS   CL4
VIEWPGM1 CSECT
A000     L     R9,TCASYAA             ---> TCA SYSTEM AREA
         USING DFHTCADY,R9
         L     R8,TCATCUCN            ---> TCTTE
         MVC   STOTETC(4),TCTTETC     SAVE TERMINAL TRANSACTION CODE
         MVC   STOTEEIA(4),TCTTEIA         AND EXEC INTERFACE ADDRESS
         LA    R5,2400
         STH   R5,WSLEN1
*    CAPTURE SCREEN BUFFER
          EXEC  CICS IGNORE CONDITION LENGERR
          EXEC  CICS RECEIVE INTO(SAVESCRN) LENGTH(WSLEN1) BUFFER      X
                MAXLENGTH(2400) ASIS
          EXEC  CICS SEND CONTROL FREEKB
          L     R5,WSLEN1
          LTR   R5,R5                    SUCCESSFUL READ?
          BZ    A050                     NO, JUST EXIT
          MVC   QID,=CL4'VIEW'           YES, WRITE TS COMMUNICATION
          MVC   QTERM,EIBTRMID                      RECORD
          EXEC  CICS WRITEQ TS QUEUE(TSQID) FROM(SAVESCRN)             X
                LENGTH(WSLEN1)
A050      OC    TCTTETC,TCTTETC          ANY RETURN'D TRANSID ACTIVE?
          BZ    Z020                         NO, EXIT
          OC    TCTTEEIA,TCTTEEIA        ANY COMMAREA ACTIVE?
          BZ    Z010                         NO, EXIT
Z000      L     R7,TCTTEEIA                  WITH RESTORED TRANSID
          USING DFHEICDS,R7                  AND COMMAREA
          LH    R6,EICLL
          EXEC  CICS RETURN TRANSID((TCTTETC)) COMMAREA(EICDBA)        X
                LENGTH((EICLL))
*    RETURN TO CICS WITH RESTORED TRANSID
Z010      EXEC  CICS RETURN TRANSID((TCTTETC))
*    JUST RETURN TO CICS
Z020      EXEC  CICS RETURN
          LTORG
VIEWEND   END   VIEWPGM1


Geraint Waters
Systems Programmer
Irish Dairy Board (Eire) ) Xephon 1989

