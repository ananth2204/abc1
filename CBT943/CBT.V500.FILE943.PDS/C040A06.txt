A useful CICS routine

MODULE'S PURPOSE

It is occasionally useful to send messages to a specific screen
location either within an application system or as a CICS system-
wide utility.  The problem, if you are not sending messages to the
last (or first) line(s), involves CRTs with different physical screen
sizes as well as those that use the ALTERNATE screen size option
to execute different applications.  The following module gets
around this problem and has successfully been used in both CICS
1.6.1 and CICS 1.7 in MVS and MVS/XA environments.


MODULE'S FUNCTIONALITY

The module should be CALLed by a program which sends
messages to specific screen locations but has no guarantee of a
preceding ERASE/WRITE/ALTERNATE that sets the physical screen
size to the alternate screen size.

It functions by querying the TCTTE flags that indicate whether an
alternate screen was last used (and is still in use) and returning
the alternate screen height and width.  If an alternate screen is
not in use, it returns the default screen size data.

Three half-words should be passed to it.  They will be returned
following the DFHEIBLK and DFHCOMMAREA addresses, and will
contain the answers (note that the 'VL' indicator is assumed to
have been set on the fifth half-word).

If any of these parameter addresses is zero, or if a TVLU flag is set
prior to the fifth parameter or the fifth parameter does not set it,
control is returned to the caller without any action being taken.
Otherwise, the half-words addressed by parameters three, four,
and five are set to zero.  A check is then made to ensure that we
are  actually addressing a terminal TCTTE.  If we are not, we
return to the caller with all the answers set to zero.


*        ASM    CICS(NOEPILOG)
         TITLE 'CICSSCRN -- RETURN SPECIFICATIONS FOR CORRECT PHYSICALLX
                Y SET SCREEN SIZE'
CICSSCRN DSECT
         COPY   DFHCSADS             CSA
         COPY   DFHTCADS             USER TCA
         COPY   DFHTCTTE             USER TCTTE
CICSSCRN DFHEIENT CODEREG=3,DATAREG=4,EIBREG=5
         LA     R2,0(,R1)            SET BXLE TO CHECK PASSED PARMS AND
         LA     R10,4                    TO ENSURE THAT THEY'RE NOT = 0
         LA     R11,4*4(,R1)             WITH VL ON LAST ONLY
BCHKPRMA ICM    R0,B'1111',0(R2)     SET CC TO REFLECT CONTENTS
         BNP    BCHKPRMB
         BXLE   R2,R10,BCHKPRMA
         B      BRETURN              PROBLEM: EXIT
BCHKPRMB CR     R2,R11               R0 = 5TH PARM?
         BNE    BRETURN              NO: EXIT
         LA     R2,0(,R2)            CLEAR HIGH-ORDER BYTE AND CHECK
         LTR    R2,R2                    REMAINING CONTENTS NOT = 0
         BZ     BRETURN
         LM     R7,R9,8(R1)          @ PARMS 3, 4, & 5
         LA     R0,0                 CLER R0
         STH    R0,0(,R7)            PASSED PARM:  SCREEN SIZE
         STH    R0,0(,R8)                SCREEN ROWS
         STH    R0,0(,R9)                SCREEN COLUMNS
         TM     TCAFCI,TCAFCTRM      IS THE FACILITY A TERMINAL?
         BNO    BRETURN              NO: EXIT
TCTTEAR  EQU    R11                  ASSIGN BASE REG TO TCTTE
         ICM    TCTTEAR,B'0111',TCAFCAAA+1  @ CURRENT TCTTE
         BZ     BRETURN              ADDRESS IS ZERO: EXIT
         LA     R10,TCTEDSCZ         @ DEFAULT SPECS
         TM     TCTE32SF,TCTEALW+TCTELEWA    ALT SIZE NOW AND PREV?
         BNO    BANSWERS             NO: USE DEFAULT SPECS
         TM     TCTE32SF,TCTEEWN     EW/EWA NEEDED NEXT?
         BO     BANSWERS             YES, USE DEFAULT SPECS
         LA     R10,4(,R10)          NO, INCREMENT TO ALTERNATE SPECS
BANSWERS L      R10,0(,R10)          PICK UP SELECTED SPECS
         STCM   R10,B'1100',0(R7)    STORE SCREEN SIZE
         STCM   R10,B'0010',1(R8)    STORE SCREEN ROWS
         STCM   R10,B'0001',1(R9)    STORE SCREEN COLUMNS
BRETURN  DFHEIRET                    EXIT
         END   ,


Brett Jason Sinclair
Consultant
SOS Inc (USA) )  Brett Jason Sinclair 1989

