A CICS macro-coded TCT listing program

This program scans a CICS macro-coded TCT and builds a
record for every TCT entry, which it writes to the CSSL
destination.  The program was used to facilitate the conversion
from macro TCT to auto-install.  The records were loaded into
a VSAM dataset that controls CICS terminal access and assigns
PRINTTO and ALTPRT fields during the auto-install process.

         TITLE 'AUTOTCT'
* BUILDS AND WRITES A RECORD TO CSSL DESTINATION FOR EACH TCT ENTRY.
* USED FOR TCT MACRO TO AUTO-INSTALL ENVIRONMENT CONVERSION.
* THESE RECORDS WERE LATER LOADED INTO A VSAM DATASET TO CONTROL CICS
* TERMINAL ACCESS AND TO ASSIGN THE PRINTTO/ALTPRT PARAMETERS.
*    NNNNNNNNNTTTTTTTTCCCCPPPPAAAA
*    NNNNNNNN - SYSTEM NETWORK NAME - (VSAM DATASET KEY)
*    TTTTTTTT - TERMINAL TYPE (TTXX - TCT MODEL) - XX TERMINAL TYPE
*    CCCC     - CICS TERMINAL ID
*    PPPP     - CICS PRINTTO PRINTER
*    AAAA     - CICS ALTERNATE PRINTER
BASEREG  EQU   3
TCTTEAR  EQU   4
TDOABAR  EQU   5
WKREG    EQU   6
TCNIBAR  EQU   7
         COPY  DFHCSADS
         DFHTCTZE CICSYST=YES
         DFHTCA CICSYST=YES
         COPY  DFHTDOA
TERMDATA DS    0CL80
NETNAME  DS    CL8
MODEL    DS    CL8
CICSID   DS    CL4
PRINTTO  DS    CL4
ALTPRT   DS    CL4
FILLER   DS    CL52
AUTOTCT  CSECT
         BALR  BASEREG,0                 ESTABLISH ADDRESSABILITY
         USING *,BASEREG                 ...DITTO
         USING TCTENIB,TCNIBAR           NIB ADDRESSABILITY
         DFHSC TYPE=GETMAIN,CLASS=TRANSDATA,INITIMG=00,NUMBYTE=84
         L     TDOABAR,TCASCSA           ADDRESS OF TD STORAGE
         MVC   TDOAVRL,=H'80'            LENGTH OF TD STORAGE
         MVI   FILLER,X'40'              CLEAR UNUSED PORTION
         MVC   FILLER+1(51),FILLER       CLEAR UNUSED PORTION
         XC    TCATPTA(4),TCATPTA        SAY FIRST TCT ENTRY
TERMLOOP EQU   *
         DFHTC CTYPE=LOCATE,TERM=NEXT,LASTTRM=RETURN,ERROR=LOCERR
         L     TCTTEAR,TCATPTA           ADDRESS OF TCTTE
         MVC   CICSID(4),TCTTETI         GET CICS ID
         MVC   PRINTTO(4),TCTEPTO        GET PRINTTO PRINTER
         MVC   ALTPRT(4),TCTEAPT         GET ALTERNATE PRINTER
         LA    WKREG,MODELS              ADDRESS OF MODEL CONSTANTS
         CLI   TCTTETT,TCTTET77          IS THIS AN 7770 TERMINAL?
         BE    TERMTYPE                  NO OFFSET
         SR    R2,R2                     CLEAR FOR TRANSLATE
         TRT   TCTTETT,OFFSETS           OFFSET INTO CONSTANTS BY TYPE
         MH    R2,=H'8'                  EACH ENTRY 8 BYTES
         AR    WKREG,R2                  BUMP INTO CONSTANTS
TERMTYPE EQU   *
         MVC   MODEL(8),0(WKREG)         GET MODEL
         CLI   TCTTETT,TCTTEISC          IS THIS AN ISC TERMINAL?
         BE    USECICS                   YES, USE CICS ID
         CLI   TCTTETT,TCTTECON          IS THIS A CONSOLE?
         BE    USECICS                   YES, USE CICS ID
         L     TCNIBAR,TCTENIBA          ADDRESS OF VTAM NIB
         MVC   NETNAME(8),TCTENNAM       GET SYSTEM NETWORK NAME
CONTINUE EQU   *
         MVC   TCATDDI,=C'CSSL'          TD ID
         DFHTD TYPE=PUT,TDADDR=TDOAVRL
         B     TERMLOOP                  GO GET NEXT TERMINAL
USECICS  EQU   *
         MVC   NETNAME(4),TCTTETI        USE CICS ID
         MVC   NETNAME+4(4),=4C' '       CLEAR REST OF FIELD
         B     CONTINUE                  GO AND FINISH BUILD
RETURN   DS    0H
         DFHPC TYPE=RETURN
LOCERR   DS    0H
         DFHPC TYPE=ABEND,ABCODE=LOCA
* TABLE OF IBM DEFAULT TERMINAL MODELS FOR AUTO-INSTALL
MODELS   EQU   *
*                TT01    TT02    TT08    TT12    TT14
         DC    C'7770    SYSTEM/7CONSOLE SEQDISK TAPE    '
*                TT18    TT20    TT21    TT22    TT24
         DC    C'CARDLINEHARDCOPY33/35TWXTELETYPE1050    '
*                TT28    TT2A    TT2B    TT40    TT41
         DC    C'2740    2741CORR2741EBC VIDEO   L2260   '
*                TT48    TT4A    TT4C    TT50    TT80
         DC    C'2260    1053    2265    TCAM    BI-SYNC '
*                TT82    TT84    TT85    TT86    TT88
         DC    C'2770    2780    3780    2980    3735    '
*                TT89    TT8A    TT91    TT92    TT93
         DC    C'3740    3600BISYLUTYPE2 3275    3284    '
*                TT94    TT99    TT9B    TT9C    TTA0
         DC    C'LUTYPE3 L3277   L3284   L3286   BISYPROG'
*                TTA1    TTA4    TTA5    TTA6    TTB0
         DC    C'SYSTEM/3SYS370  RESVPROGUPPPROG SDLC    '
*                TTB1    TTB2    TTB4    TTB5    TTB6
         DC    C'3601    3614    3790    3790USERSCSPRT  '
*                TTB8    TTB9    TTBA    TTBB    TTBD
         DC    C'3650PIPE3653HOST3650ATT 3650USERCONTLU  '
*                TTBE    TTBF    TTC0    TTC1    TTD0
         DC    C'INTERLU BATCHLU LUTYPE6 LUTYPE4 RESERVED'
*                TTD1    TTD2    TTD3    TTDE    TTDF
         DC    C'ISCMM   LUC     LUCSESS RESERVED17503750'
* OFFSETS FOR MODEL TABLE (DEPENDING ON TERMINAL TYPE SPECIFIED IN TCT)
OFFSETS  EQU   *
*                   000102030405060708090A0B0C0D0E0F
         DC    XL16'00000100000000000200000000000000'  0
         DC    XL16'00000300040000000500000000000000'  1
         DC    XL16'06070800090000000A00000B0C000000'  2
         DC    XL16'00000000000000000000000000000000'  3
         DC    XL16'0D0E0000000000000F00100011000000'  4
         DC    XL16'12000000000000000000000000000000'  5
         DC    XL16'00000000000000000000000000000000'  6
         DC    XL16'00000000000000000000000000000000'  7
         DC    XL16'130014001516170018191A0000000000'  8
         DC    XL16'001B1C1D1E000000001F002021000000'  9
         DC    XL16'22230000242526000000000000000000'  A
         DC    XL16'272829002A2B2C002D2E2F3000313233'  B
         DC    XL16'34350000000000000000000000000000'  C
         DC    XL16'3637383900000000000000000000003A'  D
         DC    XL16'00003B003C0000000000000000000000'  E
         DC    XL16'00000000000000000000000000000000'  F
         LTORG
         END


Ray Smith
Technical Support Specialist
State of Arizona (USA)

