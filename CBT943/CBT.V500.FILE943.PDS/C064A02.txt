Displaying critical CICS information


INTRODUCTION

Systems programmers often need to know how many CICS
dumps, interruptions, runaway tasks etc have occurred during
the running of a CICS session.  The following program
examines some of the most critical events in turn and highlights
fields where counts are greater than zero.  Figure 1 illustrates
an example of how the output might be displayed to a user.
The program was developed on a CICS 1.6 system under VSE.
For CICS 1.7, some of the fields will have to be changed.


ASSEMBLER PROGRAM NEKCP2

DFHEISTG DSECT
         COPY  NEKCM2
         COPY  DFHBMSCA
         COPY  DFHCSADS
PROGRAM  CSECT
         EXEC CICS ADDRESS CSA(R11) NOEDF
         USING DFHCSADS,11
         L  10,CSAOPFLA
         USING CSAOPFL,10
         L   9,CSAMXTAD
         USING CSAMXTDS,9
         EXEC CICS HANDLE AID  PF17(OPET)PF18(KRAJ)PF6(KRAJ) PF5(OPET) *
               ANYKEY(LANY) ENTER(OPET) NOEDF
         EXEC CICS HANDLE CONDITION ERROR(ERRORS)  NOEDF
OPET     EQU *
         MVC POM16,EDIT2
         ED  POM16,CSAICRUN    # RUNAWAY TASK FLUSHED
         MVC FLD12O,POMB
         CP NULAD,CSAICRUN
         BE LAB1
         MVI FLD12A,DFHBMASB
         MVI LINE12A,DFHBMASB
LAB1     MVC POM16,EDIT2
         ED  POM16,CSASCCR  # TIMES STORAGE CUSHION RELEASED
         MVC FLD14O,POMB
         CP NULAD,CSASCCR
         BE LAB2
         MVI FLD14A,DFHBMASB
         MVI LINE12A,DFHBMASB
LAB2     MVC POM16,EDIT2
         ED  POM16,CSASCRQ  # STORAGE REQ. QUEUED DUE STORAGE NOT AV.
         MVC FLD6O,POMB
         CP NULAD,CSASCRQ
         BE LAB3
         MVI FLD6A,DFHBMASB
         MVI LINE6A,DFHBMASB
LAB3     MVC POM16,EDIT2
         ED  POM16,CSASCQZ  # TIMES STORAGE QUEUE CHAIN START FROM 0
         MVC FLD16O,POMB
         MVC POM16,EDIT2
         ED  POM16,CSAPINI  # PROGRAM  INTERRUPTS
         MVC FLD18O,POMB
         CP NULAD,CSAPINI
         BE LAB4
         MVI FLD18A,DFHBMASB
         MVI LINE18A,DFHBMASB
         MVI LINE20A,DFHBMASB
LAB4     MVC POM16,EDIT2
         ED  POM16,CSADCND  # PROGRAM  DUMPS
         MVC FLD8O,POMB
         CP NULAD,CSADCND
         BE LAB5
         MVI FLD8A,DFHBMASB
         MVI LINE8A,DFHBMASB
LAB5     LH  7,CSACGFL  # CONDITIONAL GETMAIN FAILURES
         CVD 7,POM
         MVC POM16,EDIT8
         ED  POM16,POM
         MVC FLD10O,POMB
         CLC CSACGFL,=X'0000'
         BE LAB6
         MVI FLD10A,DFHBMASB
         MVI LINE10A,DFHBMASB
LAB6     EQU *
         B SNDMAP
ERRORS   MVC ERRMSGO,=CL79' ERRORS     CALL SYSTEM PROGRAMMER'
         B SNDMAP
LANY     MVC ERRMSGO,=CL79'            WRONG TYPE      '
         B SNDMAP
SNDMAP   EXEC CICS SEND MAP('NEKCM2') ERASE  NOEDF
         EXEC CICS RECEIVE MAP('NEKCM2')  NOEDF
KRAJ     MVC PORUKA,=CL19'NORMAL END OF TASK '
         EXEC CICS SEND FROM(PORUKA) ERASE  NOEDF
         EXEC CICS RETURN NOEDF
         LTORG
               CNOP 0,8
POM      DS 2F
POM16    DS 0CL16
POMA     DS CL6
POMB     DS CL10
PORUKA   DS CL19
EDIT8    DC XL16'40202020202020202020202020202120'
EDIT2    DC XL16'40404040404040404040404040202120'
NULAD    DC XL2'000C'
         END


MAP NEKCM2

MAPSET   DFHMSD TYPE=&SYSPARM,MODE=INOUT,CTRL=(FREEKB,FRSET),          C
               TIOAPFX=YES,STORAGE=AUTO
NEKCM2   DFHMDI SIZE=(24,80)
IME      DFHMDF POS=(1,1),LENGTH=7,INITIAL='NEKCM2',ATTRB=(ASKIP,FSET)
LINE3    DFHMDF POS=(3,10),                                            C
               ATTRB=(PROT,ASKIP,BRT),                                 C
               LENGTH=68,                                              C
               INITIAL='     C R I T I C A L        D A T A      I N F C
               O R M A T I O N'
LINE6    DFHMDF POS=(6,4),                                             C
               ATTRB=(PROT,ASKIP),                                     C
               LENGTH=41,                                              C
               INITIAL=' NUMBER OF TIMES STORAGE NOT AVAIALABLE :'
FLD6     DFHMDF POS=(6,46),LENGTH=10,                                  C
               ATTRB=(FSET,PROT)
LINE8    DFHMDF POS=(8,4),                                             C
               ATTRB=(PROT,ASKIP),                                     C
               LENGTH=41,                                              C
               INITIAL='                NUMBER OF PROGRAM DUMPS :'
FLD8     DFHMDF POS=(8,46),LENGTH=10,                                  C
               ATTRB=(FSET,PROT)
LINE10   DFHMDF POS=(10,4),                                            C
               ATTRB=(PROT,ASKIP),                                     C
               LENGTH=41,                                              C
               INITIAL=' NUMBER OF CONDITIONAL GETMAIN FAILURES :'
FLD10    DFHMDF POS=(10,46),LENGTH=10,                                 C
               ATTRB=(FSET,PROT)
LINE12   DFHMDF POS=(12,4),                                            C
               ATTRB=(PROT,ASKIP),                                     C
               LENGTH=41,                                              C
               INITIAL='        NUMBER OF FLUSHED RUNAWAY TASKS :'
FLD12    DFHMDF POS=(12,46),LENGTH=10,                                 C
               ATTRB=(FSET,PROT)
LINE14   DFHMDF POS=(14,4),                                            C
               ATTRB=(PROT,ASKIP),                                     C
               LENGTH=41,                                              C
               INITIAL='  NUMBER TIMES STORAGE CUSHION RELEASED :'
FLD14    DFHMDF POS=(14,46),LENGTH=10,                                 C
               ATTRB=(FSET,PROT)
LINE16   DFHMDF POS=(16,4),                                            C
               ATTRB=(PROT,ASKIP),                                     C
               LENGTH=41,                                              C
               INITIAL='  NUMBER OF TIMES STORAGE QUEUE CHAIN=0 :'
FLD16    DFHMDF POS=(16,46),LENGTH=10,                                 C
               ATTRB=(FSET,PROT)
LINE18   DFHMDF POS=(18,4),                                            C
               ATTRB=(PROT,ASKIP),                                     C
               LENGTH=41,                                              C
               INITIAL='           NUMBER OF PROGRAM INTERRUPTS :'
FLD18    DFHMDF POS=(18,46),LENGTH=10,                                 C
               ATTRB=(FSET,PROT)
LINE20   DFHMDF POS=(20,4),                                            C
               ATTRB=(DRK,ASKIP),                                      C
               LENGTH=60,                                              C
               INITIAL=' MORE INFORMATION ABOUT PROGRAM INTERRUPTS : PF4
               4,PF16'
LINE23   DFHMDF POS=(23,1),                                            C
               ATTRB=(PROT,ASKIP),                                     C
               LENGTH=79,                                              C
               INITIAL='        PF5,PF17 - REFRESH    PF6,PF18 - CICS'
ERRMSG   DFHMDF POS=(24,1),LENGTH=79,ATTRB=(BRT,PROT)
         DFHMSD TYPE=FINAL
         END


Nenad Kuzmanovic
Arenaturist (Yugoslavia)                  c Nenad Kuzmanovic 1991

