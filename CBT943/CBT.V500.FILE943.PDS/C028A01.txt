 Locating the VSE/SP log-on user-id
We are running CICS 1.6 under VSE/SP and using ICCF for program development.
PROBLEM
When a programmer is executing programs in CICS outside ICCF we needed to
determine the four-character user-id the programmer signed on with.
SOLUTION
After looking around in core the user-ids were found and a little Assembler
program was written that when linked to will return the user-id in a four-
character communication area that has been passed to it.
The program loads IESSVL and finds the address of a terminal table list at
offset 32.  The table has 8-byte entries and is terminated with XTFFU in the
first byte of an entry.  The first 4 bytes contain the terminal identification
and the last four a new address.  Take the new address,  offset it by 32, and
this should point to the same terminal; take the new address, offset it by 36,
and it points to the user-id.
We have been using the routine since April 1987 without any problems, but being
unable to locate any documentation for this procedure I would be unsure of its
use on different systems.
*** PGM TO GET 4 CHAR. USERID  USING TERMINAL ***
DFHEISTG DSECT
TERM     DS    CL4
COMREG   DSECT
RTNUSER  DS    CL4
Z89T12A1 DFHEIENT DATAREG=11,EIBREG=10,CODEREG=3
         L     R2,DFHEICAP
         USING COMREG,R2
         EXEC  CICS LOAD PROGRAM('IESSVL') SET(R4)
         L     R4,32(R4)           LOAD ADDRS OF TERM VECTOR TABLE
AGAIN    EQU   *
         CLC   EIBTRMID,0(R4)      FOUND TERM
         BE    GETUSER             YES
         CLI   0(R4),X'FF'         END OF TABLE
         BE    ERROR
         LA    R4,8(R4)            SKIP TO NXT ELEMENT
         B     AGAIN
ERROR    EQU   *
         MVC   RTNUSER,=C'ERR1'
         B     EXIT
ERR2     EQU   *
         MVC   RTNUSER,=C'ERR2'
         B     EXIT
GETUSER  EQU   *
         L     R4,4(R4)            LOAD ADDRS OF CNTRL BLK TO USERID
         LTR   R4,R4               VALID ADDRS
         BZ    ERR2                NO
         MVC   RTNUSER,36(R4)      LOAD IN USERID
EXIT     EQU   *
         EXEC  CICS  RETURN
         LTORG
         END


Don Coleman
Senior Software Specialist
Hillsborough County Data Processing (USA)    ) Hillsborough County 1988











