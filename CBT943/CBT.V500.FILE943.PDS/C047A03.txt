TSO/ISPF CICS dump dataset utility (continued)

This month, we conclude our look at a method of processing
CICS dumps through the facilities of TSO/ISPF by publishing the
remaining components of this utility.

TSO CLIST DSCNC01

This CLIST should be placed in a library concatenated to the TSO
userUs SYSPROC dataset.

PROC 0
CONTROL NOLIST NOMSG
DSCNM01A:-
   SET &ZCMD =
   ISPEXEC VGET (DSCNSPC DSNCTRN)
   IF &DSCNSPC = THEN +
      SET &DSCNSPC = SINGLE
   IF &DSCNTRN = THEN +
      SET &DSCNTRN = LC
   ISPEXEC CONTROL DISPLAY SAVE
   ISPEXEC DISPLAY PANEL(DSCNM01A)
   SET PNCC = &LASTCC
   ISPEXEC CONTROL DISPLAY RESTORE
   IF &PNCC ¬= 0 THEN GOTO EXIT
   ISPEXEC VPUT (DSCNDDS DSCNSPC DSCNTRN)
   IF &SYSDSN('&DSCNDDS') ¬= OK THEN +
      GOTO DSCN003E
   IF &ZCMD = P THEN +
      DO
         SET DSCNCNT = 0
         ISPEXEC VPUT (DSCNCNT)
         ISPEXEC FTOPEN TEMP
         ISPEXEC FTINCL DSCNS01
         ISPEXEC FTCLOSE
         ISPEXEC VGET ZTEMPF
         SUBMIT '&ZTEMPF'
         ISPEXEC SETMSG MSG(DSCN001I)
      END
   IF &ZCMD =   THEN +
      GOTO DSCNP01
   GOTO DSCNM01A
DSCN003E:-
   ISPEXEC SETMSG MSG(DSCN003E)
   GOTO DSCNM01A
DSCNP01:-
   FREE  DD(DFHDMPDS)
   ALLOC DD(DFHDMPDS) DA('&DSCNDDS') SHR
   ISPEXEC SELECT PGM(DSCNP01)
   ISPEXEC TBTOP    DSCNT01
DSCNM01B:-
   SET TBRC = 0
   DO WHILE &TBRC < 8
      ISPEXEC TBDISPL DSCNT01 PANEL(DSCNM01B)
      SET TBRC = &LASTCC
      DO WHILE &ZTDSELS > 0
         SET &SELERR = Y
         IF &DSCNSEL = P THEN +
            DO
               SET DSCNSKP   = &EVAL(&DSCNR01 - 1)
               SET DSCNCNT   = &EVAL(&DSCNR02 - &DSCNSKP)
               ISPEXEC VPUT (DSCNCNT DSCNSKP)
               ISPEXEC FTOPEN TEMP
               ISPEXEC FTINCL DSCNS01
               ISPEXEC FTCLOSE
               ISPEXEC VGET ZTEMPF
               SUBMIT '&ZTEMPF'
               ISPEXEC SETMSG MSG(DSCN001I)
               SET &SELERR = N
            END
         IF &SELERR = Y THEN +
            DO
               SET &ZTDSELS = 0
               ISPEXEC SETMSG MSG(DSCN002E)
            END
         IF &ZTDSELS > 1 THEN +
            ISPEXEC TBDISPL  DSCNT01
         ELSE +
            SET ZTDSELS = 0
      END
   END
   ISPEXEC TBEND    DSCNT01
   FREE  DD(DFHDMPDS)
   GOTO DSCNM01A
EXIT:-
END

ISPF MESSAGE SET DSCN00

This message set must be placed in a library concatenated to the
TSO userUs ISPMLIB dataset.

DSCN001I 'DUMP PRINT SUBMITTED' .ALARM=YES
'A JOB HAS BEEN SUBMITTED TO PRINT THE DUMP YOU SELECTED'
DSCN002E 'INVALID OPTION' .ALARM=YES
'THE OPTION YOU SPECIFIED IN THE SELECTION FIELD IS INVALID'
DSCN003E 'DATASET NOT CATALOGED' .ALARM=YES
'''&DSCNDDS'' WAS NOT FOUND IN ANY CATALOG'

ISPF PANEL DSCNM01A

This panel must be placed in a library concatenated to the TSO
userUs ISPPLIB dataset.

)BODY
%------------------------   CICS System Dump Utility  ------------------------
--
%OPTION  ===>_ZCMD
+
%  blank+- Display dump list               %P+- Print entire dump dataset
+
+   DUMP DATASET %===>_Z                                           +
+
+Specify dump options below
+   DUMP SPACING %===>_Z     +(SINGLE or DOUBLE)
+   TRANSLATION  %===>_Z    + (UC, FOLD or LC)
+
+
+
+
%*+The following line commands will be available when the list is displayed
+
%    P+- Print dump                     %S+- Select dump
%
+
+
+
+
+Press %END +key to exit
)INIT
  .ZVARS = '(DSCNDDS,DSCNSPC,DSCNTRN)'
  .CURSOR = ZCMD
)PROC
   VER(&DSCNDDS,NONBLANK)
   VER(&DSCNDDS,DSNAME)
   VER(&DSCNSPC,LIST,SINGLE,DOUBLE)
   VER(&DSCNTRN,LIST,UC,FOLD,LC)
)END

ISPF PANEL DSCNM01B

This panel must be placed in a library concatenated to the TSO
userUs ISPPLIB dataset.

)ATTR
  @ TYPE(OUTPUT) INTENS(LOW)
  | TYPE(OUTPUT) INTENS(HIGH)
  ! TYPE(OUTPUT) INTENS(HIGH) PAD(-)
)BODY
%INDEX ----!Z
%
%COMMAND ===>_Z                                               %SCROLL ===>_Z
+
+  TRAN CODE   TIME    DATE                      DESCRIPTION
)MODEL
_Z@Z
)INIT
   .ZVARS = '(DSCNDDS,ZCMD,DSCNSCR,DSCNSEL,DSCNDET)'
  .CURSOR = &ZCMD
)END

ISPF SKELETON DSCNS01A

This skeleton must be placed in a library concatenated to the TSO
userUs ISPSLIB dataset.  It should be typed exactly as shown with
the exception of the job cards which should be tailored to suit your
installation.

//&ZUSER.DMP JOB 'your accnt code','CICSDUMP',
//   CLASS=?,MSGCLASS=?,NOTIFY=&ZUSER
/*JOBPARM xxxxxxxxxxx
)SEL &DSCNCNT ¬= 0
//* EXTRACT SINGLE DUMP FROM CICS DUMP DATASET
//REPRO   EXEC PGM=IDCAMS
//SYSPRINT DD SYSOUT=*
//IFILE    DD DISP=SHR,DSN=&DSCNDDS
//OFILE    DD DISP=(,PASS),DSN=&&DSCNEXT,
//            UNIT=SYSDA,SPACE=(CYL,(10,2)),
//            DCB=&DSCNDDS
//SYSIN    DD *
  REPRO   IFILE(IFILE)                                               -
          OFILE(OFILE)                                               -
          REUSE                                                      -
          SKIP(&DSCNSKP)                                             -
          COUNT(&DSCNCNT)
//*
//* RUN DFHDUP OVER EXTRACTED DUMP
)ENDSEL
)SEL &DSCNCNT = 0
//*
//* PRINT ENTIRE CONTENTS OF CICS DUMP DATASET
)ENDSEL
//CICDUMP EXEC PGM=DFHDUP,REGION=128K,
//             PARM='&DSCNSPC,TRANSLATE=&DSCNTRN'
//STEPLIB  DD DISP=SHR,DSN=CICSVS.LOADLIB
//DFHPRINT DD SYSOUT=U
)SEL &DSCNCNT ¬= 0
//DFHDMPDS DD DISP=(OLD,DELETE),DSN=&&DSCNEXT
)ENDSEL
)SEL &DSCNCNT = 0
//DFHDMPDS DD DISP=SHR,DSN=&DSCNDDS
)ENDSEL


B Perryman
Systems Programmer
Confederation Life Insurance Co (UK) ) Confederation Life Insurance 1989


