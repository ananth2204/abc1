CICS run-time PL/I message display


INTRODUCTION

CICS run-time PL/I messages are written to the CPLI queue,
which is an extra-partition destination usually directed to a
SYSOUT dataset becoming available when CICS ends. The
PL/I message layout is as follows:

Byte 1      Printer control character
Bytes 2-5      Terminal-id
Bytes 6-9      Transaction-id
Bytes 10-113      PL/I message text.

In order to get a more useful on-line PL/I message display I
have diverted this extra-partition destination to an intra-
partition destination with the same name (CPLI) and have
written a simple CICS transaction (PLIO) to browse it for
messages associated with either a specific terminal-id or
transaction-id. The selected messages are deleted from the
queue and are displayed with the CICS paging transaction. The
transaction invocation syntax is as follows:

      PLIO ÝtermidÝ,tranid¨¨

If only the termid parameter is entered, all PL/I messages
associated with that terminal are selected, regardless of
transaction-id. If only the tranid parameter is entered, all PL/I
messages associated with that transaction-id are selected,
regardless of terminal-id. If no parameters are entered, the
program defaults to the user's terminal-id.


REQUIRED TABLE ENTRIES

1      The PPT and PCT entries are as follows:

      DFHPPT TYPE=ENTRY,PROGRAM=CICSPLIO
      DFHPCT TYPE=ENTRY,TRANSID=PLIO,PROGRAM=CICSPLIO

2      Replace the IBM-supplied DCT entries CPLI and CPLD with:

      DFHDCT TYPE=INTRA,DESTID=CPLI,DESTFAC=FILE,REUSE=YES
      DFHDCT TYPE=EXTRA,DESTID=CPLD,DSCNAME=PLIMSG


CICSPLIO SOURCE CODE

*ASM XOPTS(CICS PROLOG EPILOG FE)
DFHEISTG DSECT      DYNAMIC STORAGE FOR VARIABLES
*--- SCREEN INPUT DATA DEFINITION ---*
SCREENAL DS    H                       SCREEN INPUT AREA LENGTH
SCREENA  DS    0CL1920                 SCREEN INPUT AREA LAYOUT:
USERMSG  DS    0CL14                    USER INPUT MESSAGE:
TRAN     DS    CL4                        TRANSACTION IDENTIFIER 'PLIO'
TRANDEL  DS    C                          TRANSACTION DELIMITER
PARMS    DS    0CL9                       PARAMETERS:
USERTERM DS    CL4                          PLIO MSGS SELECTION TERMID
PARMDEL  DS    C                            PARAMETERS DELIMITER
USERTRAN DS    CL4                          PLIO MSGS SELECTION TRANID
FILLER   DS    CL1906                   FILLER
*----- PLIO MESSAGE DEFINITION ------*
PLIOMSGL DS    H                       PLIO MESSAGE LENGTH
PLIOMSG  DS    0CL133                  PLIO MESAGE LAYOUT:
PLIOPCC  DS    C                        PRINTER CONTROL CHARACTER
PLIOTERM DS    CL4                      TERMINAL IDENTIFIER
PLIOTRAN DS    CL4                      TRANSACTION IDENTIFIER
PLIOMSGT DS    CL124                    MESSAGE TEXT
*------ PAGE TITLE DEFINITION  ------*
TITLE    DS    0H                      HEADER DATA AREA FORMAT
TITLELEN DS    H                        TITLE LENGTH
PAGENUMC DS    C                        PAGE NUMBERING CHARACTER '@'
RESERVE  DS    C                        RESERVED ONE-BYTE FIELD
TITLEDAT DS    0CL79                    TITLE DATA LAYOUT
TRANTEXT DS    CL36                      'P/N  *PLIO MSGS FOR TRAN(S)'
TITLETRN DS    CL4                       TRANSACTION IDENTFIER
TERMTEXT DS    CL26                      ' ASSOCIATED WITH TERMINAL '
TITLETRM DS    CL4                       TERMINAL IDENTIFIER
PAGETEXT DS    CL8                       ' * PAGE '
PAGENUMB DS    C                         '@' - PAGE NUMBER POSITION
*---- BROWSE SELECTION VARIABLES ----*
EOBROWSE DS    0CL37                   END OF BROWSE DELIMITER LAYOUT:
PCC      DS    C                        PRINTER CONTROL CHARACTER: C'1'
TERMID   DS    CL4                      PLIOMSGS TERMID SELECT PATTERN
TRANID   DS    CL4                      PLIOMSGS TRANID SELECT PATTERN
EOPLIOMS DS    CL28                     '*** END OF PLIO MESSAGES ***'
*---------- WORK VARIABLES ----------*
PLIQ     DS    CL4                     PLIOMSGS REWRITE QUEUE IDENT
BLANKS   DS    CL4                     FOUR BLANKS CHARACTER STRING
CICSPLIO CSECT            PROGRAM ENTRY POINT
         B     PROGINIT
         DC    CL12'PGM=CICSPLIO'      PROGRAM NAME
         DC    CL16'ASMDATE=&SYSDATE'  ASSEMBLY DATE
         DC    CL16'ASMTIME=&SYSTIME'  ASSEMBLY TIME
PROGINIT DS    0D                      *----- PROGRAM INITIATION -----*
         EXEC  CICS HANDLE CONDITION QZERO(QZERO) NOSPACE(NOSPACE)
         EXEC  CICS HANDLE AID PF3(RETURN) CLEAR(RETURN)
         MVC   TITLELEN,=H'79'         SET PAGE TITLE LENGTH
         MVI   PAGENUMC,C'@'           SET PAGE NUMBER CHARACTER
         MVC   TRANTEXT,=CL36'P/N  * PLI MESSAGES FOR TRANSACTION '
         MVC   TITLETRN,BLANKS         CLEAR PAGE TITLE TRANID
         MVC   TERMTEXT,=CL26' ASSOCIATED WITH TERMINAL '
         MVC   TITLETRM,BLANKS         CLEAR PAGE TITLE TERMID
         MVC   PAGETEXT,=CL8' * PAGE '
         MVI   PAGENUMB,C'@'           INIT PAGE NUMBER POSITION
         MVI   PCC,C'1'                SET PCC INTO THE EOBR PATTERN
         MVC   TERMID,=CL4' '          SET BLANK TERMID INTO PATTERN
         MVC   TRANID,=CL4' '          SET BLANK TRANID INTO PATTERN
         MVC   EOPLIOMS,=CL28'*** END OF PLIO MESSAGES ***'
         MVC   PLIQ,=C'CPLI'           SET DEFAULT NONSELECT MSGS QUEUE
         MVC   BLANKS,=CL4' '          INIT BLANKS
*-------------------- READ INPUT DATA FROM SCREEN --------------------*
         MVC   USERMSG(L'USERMSG+3),=CL(L'USERMSG+3)' ' CLEAR USERFIELD
         MVC   SCREENAL,=H'1920'       EXPECTED MAX LENGTH OF INP DATA
         EXEC  CICS RECEIVE INTO(SCREENA) LENGTH(SCREENAL) NOHANDLE
         CLI   SCREENA,X'11'           IF SBA IN INPUT
         BNE   GETINPUT                                THEN
         MVC   SCREENA(L'USERMSG),SCREENA+3                 REMOVE SBA
GETINPUT DS    0H                      *------- GET INPUT DATA -------*
         CLC   PARMS,=CL(L'PARMS)' '   IF NO PARAMETERS ARE ENTERED
         BNE   PARMTERM                THEN
         MVC   TERMID,EIBTRMID              SET THE DEFAULT TERMID,
         MVC   TITLETRM,TERMID              MOVE IT INTO PAGE TITLE
         B     WRITEBRD                     AND GOTO WRITE BROWSE DELIM
PARMTERM CLC   USERTERM,BLANKS        IF PARAMETER TERMID ISN'T ENTERED
         BE    PARMTRAN                THEN GO TO TEST FOR TRANID
         MVC   TERMID,USERTERM         ELSE SET THE ENTERED TERMID
         MVC   TITLETRM,TERMID              AND MOVE IT INTO PAGE TITLE
PARMTRAN CLC   USERTRAN,BLANKS        IF PARAMETER TRANID ISN'T ENTERED
         BE    WRITEBRD                THEN GOTO WRITE BROWSE DELIMITER
         MVC   TRANID,USERTRAN         ELSE SET THE ENTERED TRANID
         MVC   TITLETRN,TRANID              AND MOVE IT INTO PAGE TITLE
*---------------------- WRITE BROWSE DELIMITER  ----------------------*
WRITEBRD DS    0H
         EXEC  CICS WRITEQ TD QUEUE('CPLI') FROM(EOBROWSE)             -
               LENGTH(L'EOBROWSE)
*--- READ ITEMS PROM QUEUE('CPLI') WHILE BROWSE DELIMITER NOT FOUND --*
READITEM DS    0H
         MVC   PLIOMSGL,=H'133'        EXPECTED MAXLENGTH OF QUEUE ITEM
         EXEC  CICS READQ TD QUEUE('CPLI') INTO(PLIOMSG)               -
               LENGTH(PLIOMSGL)
         CLC   PLIOMSG(37),EOBROWSE    IF BROWSE DELIMITER NOT REACHED
         BNE   TERMTRAN                THEN GOTO TEST TERM-TRAN MATCH
         MVI   PLIOMSG,X'15'           ELSE ACCUMULATE EOBR DELIMITER
         EXEC  CICS SEND TEXT FROM(PLIOMSG) LENGTH(PLIOMSGL)           -
               HEADER(TITLE) PAGING ACCUM NOHANDLE
         B     RETURN                  GO TO RETURN TO CICS
TERMTRAN DS    0H *-SELECT REQUESTED PLIOMSGS ON TERM-TRAN MATCH BASE-*
         CLC   TERMID,BLANKS           IF TERMID IS IN SELECTION
         BNE   TESTTERM                 THEN GO TO TEST TERMID MATCHING
         CLC   TRANID,BLANKS           IF TRANID IS IN SELECTION
         BNE   TESTTRAN                 THEN GO TO TEST TRANID MATCHING
TESTTERM DS    0H                      *---- TEST TERMID MATCHING  ---*
         CLC   PLIOTERM,TERMID         IF TERMID NOT MATCH TO PATTERN
         BNE   REWRITE                  THAN REWRITE MSG INTO PLIQ
         CLC   TRANID,BLANKS           IF TRANID IS IN SELECTION ALSO
         BNE   TESTTRAN                 THAN GOTO TEST TRANID MATCHING
         B     ACCUMMSG                 ELSE GOTO ACCUM MSG FOR DISPLAY
TESTTRAN DS    0H                      *---- TEST TRANID MATCHING  ---*
         CLC   PLIOTRAN,TRANID         IF TRANID NOT MATCH TO PATTERN
         BNE   REWRITE                 THEN GO TO REWRITE MSG INTO PLIQ
ACCUMMSG DS    0H *- ACCUMULATE THE SELECTED PLIO MESSAGE FOR DISPLAY-*
         MVI   PLIOMSG,X'15'           SET NEW LINE CHARACTER
         EXEC  CICS SEND TEXT FROM(PLIOMSG) LENGTH(PLIOMSGL)           -
               HEADER(TITLE) PAGING ACCUM NOHANDLE
         B     READITEM                GO TO READ NEXT 'CPLI' ITEM
REWRITE  DS    0H *--- PUT NONSELECTED MESSAGE INTO PLIQ(CPLI/CPLD) --*
         EXEC  CICS WRITEQ TD QUEUE(PLIQ)                              -
               FROM(PLIOMSG) LENGTH(PLIOMSGL)
         B     READITEM                GO TO READ NEXT 'CPLI' ITEM
*------------------- HANDLE EXCEPTIONAL CONDITIONS -------------------*
NOSPACE  DS    0H                      *--- NO MORE SPACE IN 'CPLI' --*
         MVC   PLIQ,=C'CPLD'           REDIRECT PLIOMSGS TO QUEUE CPLD
         B     READITEM                GO TO READ THE FIRST 'CPLI' ITEM
QZERO    DS    0H                      *-- NO MORE ITEMS INTO 'CPLI' -*
         MVC   PLIOMSG,=CL133' THE NONSELECTED PLIO MESSAGES ARE REDIRE-
               CTED TO THE PLIO DUMP QUEUE ''CPLD''.'
         MVC   PLIOMSGL,=H'133'
         EXEC  CICS SEND TEXT FROM(PLIOMSG) LENGTH(PLIOMSGL)           -
               HEADER(TITLE) PAGING ACCUM NOHANDLE
RETURN   DS    0H            RETURN TO CICS
         EXEC  CICS SEND PAGE NOHANDLE
         EXEC  CICS RETURN
         END


Tsvetan Ivanov Vassilev
Systems Consultant (Bulgaria)

