 Resident program loader
At an installation where I worked recently we had just implemented a new records
management system and had a problem with the size of the programs involved.  The
system comprised  a few very large modules and the first transaction that
invoked that module tended to have a very long wait - up to about 15 seconds.
Upon investigation I found that CICS, at initialisation time, allocated space
for its resident programs but didnUt actually load them in.  This prompted me to
write this very simple program.  It scans the PPT chain and issues a load to any
program that has been defined as resident.  This program should be run at
initialisation time via the PLTPI mechanism.  It issues messages to the console
informing operators of programs that could not be loaded.
WORKSECT DSECT
PPTADDR  DS    F
FLAG     DS    XL1
HADONE   EQU   X'01'
         DS    XL3
ERRMSG   DS    CL44
ERRMSG2  DS    CL12
WORKEND  DS    0F
PPTCBAR  EQU   8
         DFHTM MF=(D,DIRELEM)
         DFHTM MF=(D,SKTTBLE)
         DFHTM MF=(D,PARMLIST)
         DFHTM MF=(D,TMSTATIC)
         COPY  DFHPPTDS
         COPY  DFHCSADS
SYSALOAD CSECT
         EXEC  CICS HANDLE CONDITION                                   *
               PGMIDERR(NOTFOUND)                                      *
               NOTAUTH(NOTFOUND)                                       *
               LENGERR(RETURN)                                         *
               NOSTG(RETURN)
         EXEC  CICS GETMAIN                                            *
               SET(R5)                                                 *
               LENGTH(WORKLGTH)
         USING WORKSECT,R5
         MVC   ERRMSG(44),MESSAGE
         MVC   ERRMSG2(12),MESSAGE2
         MVI   FLAG,X'00'
         EXEC  CICS ADDRESS                                            *
               CSA(R1)
         USING DFHCSADS,R1
         DFHTM TYPE=(GETFIRST,INLINE,(R1),(PPTCBAR)),                  *
               TABLE=PPT
         ST    R1,PPTADDR
         LTR   R1,R1
         BZ    RETURN
         DROP  R1
         USING DFHPPTDS,R1
LOADPROG TM    PPTTLR,PPTPCRES
         BNO   GETNEXT
         EXEC  CICS LOAD                                               *
               PROGRAM(PPTPI)
         B     GETNEXT
NOTFOUND TM    FLAG,HADONE
         BO    NOHEADER
         WTO   MF=(E,ERRMSG)
NOHEADER MVC   ERRMSG2+4(8),PPTPI
         WTO   MF=(E,ERRMSG2)
         MVI   FLAG,HADONE
GETNEXT  L     R1,PPTADDR
         DFHTM TYPE=(GETNEXT,INLINE,(R1),(PPTCBAR)),                   *
               TABLE=PPT
         ST    R1,PPTADDR
         LTR   R1,R1
         BZ    RETURN
          B     LOADPROG
RETURN   EXEC  CICS RETURN
         LTORG
WORKLGTH DC    AL2(WORKEND-WORKSECT)
MESSAGE  DC    AL2(MSGLGTH)
         DC    AL2(0)
         DC    CL40' UNABLE TO LOAD THE FOLLOWING PROGRAMS  '
MSGLGTH  EQU   *-MESSAGE
MESSAGE2 DC    AL2(MSG2LGTH)
         DC    AL2(0)
         DC    CL8'********'
MSG2LGTH EQU   *-MESSAGE2
         END   SYSALOAD


G Todesco
Systems Programmer
Xcell Computers Ltd (UK) ) Xcell Computers 1988
















































