 A CSD program finder

Many times when Resource Definition On-line (RDO) is used to define all your
programs, the number of programs in all the groups tends to get large and
unmanageable.

Sometimes you need to step back and ask yourself what are all the program names
you have defined in every group on your CICS System Definition (CSD) file.

The following program will read the CSD file and locate all the program
definitions.  It produces an 80 byte record containing the program name followed
by the group it was defined in.  The record can be input to any listing utility
to produce a report of all the programs in your CSD.

       IDENTIFICATION DIVISION.
       PROGRAM-ID.  SP096P.
       .
      ******************************************************************
      *        THIS PROGRAM READS THE CICS CSD (CICS SYSTEM DEFINITION)
      *        DATA SET AND PRODUCES RECORDS OF ALL PROGRAMS FOUND.
      *        THE RECORDS ARE COMPOSED OF PROGRAM-ID AND GROUP-ID.
       ENVIRONMENT DIVISION.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
           SELECT CSDINPT ASSIGN TO CSDINPT
                   ORGANIZATION IS INDEXED
                   ACCESS IS SEQUENTIAL
                   RECORD KEY IS CSD-KEY.
           SELECT PPTOUTP ASSIGN TO PPTOUTP.
       DATA DIVISION.
       FILE SECTION.
       FD  CSDINPT
           LABEL RECORDS ARE STANDARD
           BLOCK CONTAINS 0 RECORDS
           DATA RECORD IS CSD-RECORD.
       01  CSD-RECORD.
           05  CSD-KEY.
               10  CSD-KEY1.
                   15  CSD-KEY11  PIC X(5).
                   15  FILLER  PIC X(3).
               10  FILLER      PIC X(4).
               10  CSD-KEY2    PIC X(2).
               10  CSD-PRGMID  PIC X(8).
           05  FILLER          PIC X(350).
       FD  PPTOUTP
           LABEL RECORDS ARE STANDARD
           RECORDING MODE IS F
           BLOCK CONTAINS 0 RECORDS
           DATA RECORD IS PPT-OUT.
       01  PPT-OUT.
           05  PPT-PRGMID      PIC X(8).
           05  PPT-GRPNM       PIC X(8).
           05  FILLER          PIC X(64).
       WORKING-STORAGE SECTION.
       77  CSD-SWT                 PIC X(1)  VALUE '0'.
       01  PGM-CNST1.
           05  FILLER              PIC S9(4) COMP VALUE +4000.
      *                                       EQUIVALENT TO X'0FA0'
           05  FILLER              PIC S9(4) COMP VALUE +1000.
      *                                       EQUIVALENT TO X'03E8'
       01  PGM-CNST2 REDEFINES PGM-CNST1.
           05  PRGM-ID1            PIC X(2).
           05  PRGM-ID2            PIC X(2).
       PROCEDURE DIVISION.
           PERFORM INITIALIZE.
           PERFORM PROCESS-CSD THRU PR-XT
             UNTIL CSD-SWT IS EQUAL TO '1'.
           CLOSE PPTOUTP.
           STOP RUN.
       INITIALIZE.
           OPEN INPUT CSDINPT.
           OPEN OUTPUT PPTOUTP.
       PROCESS-CSD.
           PERFORM READ-CSD THRU READ-XT.
           IF CSD-SWT IS EQUAL TO '1'
              GO TO PR-XT.
           IF CSD-KEY2 IS EQUAL TO PRGM-ID1 OR
              CSD-KEY2 IS EQUAL TO PRGM-ID2
              MOVE SPACES TO PPT-OUT
              MOVE CSD-KEY1 TO PPT-GRPNM
              MOVE CSD-PRGMID TO PPT-PRGMID
              PERFORM WRITE-PPT.
       PR-XT.
           EXIT.
       READ-CSD.
           READ CSDINPT AT END CLOSE CSDINPT
                           MOVE '1' TO CSD-SWT
                           GO TO READ-XT.
       READ-XT.
           EXIT.
       WRITE-PPT.
           WRITE PPT-OUT.


William Myers
Technical Specialist
Ameritrust Company NA (USA)   ) Xephon 1987



































