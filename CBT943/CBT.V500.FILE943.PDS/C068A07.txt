CEDF ZAP to continue session - 3.1.1 update

The November 1989 issue of CICS Update contained a
USERMOD to change the 'continue EDF session' default value
from NO to YES.  This was an excellent idea and I have found
it very handy.  The November 1990 issue of CICS Update
provided a CICS 2.1.1 equivalent for this USERMOD.  We are
currently using CICS/ESA 3.1.1 in test, and as I was
determined to keep this feature with CICS/ESA, I have
modified it to work with CICS/ESA also.

I chose not to implement this as a USERMOD, however.  I
dislike USERMODs because they complicate the maintenance
process and, by modifying CICS itself, can contribute to
increased difficulty when trying to isolate problems with CICS.
Rather, I wrote a short program to, at PLTPI time, load
DFHEDFD into storage, then locate and modify the appropriate
instructions in core, rather than on the target or distribution
LOADLIBs.  This approach has several advantages:

o      No USERMOD means no implications when installing
      PTF maintenance.

o      By not modifying the object module I can install the
      modification in some, but not all, of my CICS systems
      without the need to maintain two versions (modified and
      unmodified) of DFHEDFD.

o      Should PTF maintenance cause the instructions I want to
      change to move even slightly, I must again research the
      object module to determine where to re-apply the
      modification.  This program, however, searches for the
      target instructions, so that if they move around a bit the
      ZAP will still install properly.

o      If I am suspicious that the modification is causing
      problems with CEDF, I can remove it easily by simply
      using CEMT to NEWCOPY the program DFHEDFD.

The supplied program will work for both CICS/MVS and
CICS/ESA, so if you have installed the USERMOD on your
system already you can remove it if you wish.  I suspect it will
work with CICS 1.7 as well, but I do not use that release of
CICS.  It is also set up to be easily enhanced for future releases
of CICS.

The program contains two tables, one for CICS/MVS and one
for CICS/ESA.  Each table contains an entry for each of the
three instructions to be changed, as well as information needed
to validate each instruction's position in the EDF program by
verifying the instructions that precede and follow the target
instructions.  Two tables are needed because the instructions
adjacent to the target instructions are different for CICS/MVS
and CICS/ESA; however, the CICS/MVS table should also
work for CICS 1.7, and the CICS/ESA table will very likely
work in the future with CICS 3.2.  If not, this program can be
modified to serve as a means for locating the target instructions
should they change radically in the future.

Installation is simple - just add the program to the PPT and
second-phase PLTPI of the CICS system(s) where you want to
see the change.  We use it in test but not production.  Also, if
you have CICS modules in the LPA you will want to be sure
DFHEDFD is not there - either remove it completely or use
PRVMOD=DFHEDFD in the systems where you want this
change in effect.

Messages are written to the CSMT destination if the overlay is
successful, as well as for error conditions that can cause the
overlay to fail - ie if DFHEDFD is not in the PPT, if
DFHEDFD is in the LPA, if there is no table for the release of
CICS being used, or if the target instructions cannot be located
in the DFHEDFD load module.  These situations could require
changes to this program but, as noted, you can modify this
program and use it (and EDF itself!) as a tool to determine
what changes are necessary to correct the problem.

Like the authors of the above-mentioned articles, I have found
this small change to CICS to be extremely handy, and
recommend it to any readers who have not yet tried it.  I have
found my approach advantageous, but for any readers who
prefer the USERMOD approach, perhaps because you want
DFHEDFD to reside in the MVS LPA, here are the
AMASPZAP commands for CICS 3.1.1:

      NAME DFHEDFD DEFEDFD
      VER  02CC 94C0A04C      NI  X'4C'(,R10),X'C0'
      VER  0420 9680204C      OI  X'4C'(,R2),X'80'
      VER  0424 9680204E      OI  X'4E'(,R2),X'80'
      REP  02CC 94D0A04C      NI  X'4C'(,R10),X'D0'
      REP  0420 9690204C      OI  X'4C'(,R2),X'90'
      REP  0424 9400204E      NI  X'4E'(,R2),X'00'


PROGRAM SOURCE CODE

         TITLE 'ADC - PROGRAM TO CHANGE EDF CONTINUATION DEFAULT'
*** MODULE NAME = ZEDFPGM                                           ***
* MODULE TYPE = ASSEMBLER, RENT, AMODE=31, RMODE=ANY                  *
* MODULE SIZE = 1,304 BYTES                                           *
* DESCRIPTIVE NAME = PROGRAM TO OVERLAY EDF SESSION CONTINUATION      *
*                    OPTION FROM "NO" TO "YES".                       *
* STATUS = CICS/ESA 3.1.1                                             *
* FUNCTION - THIS PROGRAM WILL OVERLAY PARTS OF THE EDF CONTROL       *
*            PROGRAM, IN ORDER TO CHANGE THE EDF SESSION CONTINUATION *
*            OPTION FROM "NO" TO "YES".                               *
* TO INSTALL: CEDA DEFINE PROGRAM(ZEDFPGM)                            *
*                  LANGUAGE(ASSEMBLER)                                *
*                  GROUP(XXXXXX)                                      *
*                  RES(NO)                                            *
*                  DFHPLT TYPE=ENTRY,PROGRAM=ZEDFPGM                  *
* MESSAGES: "ZEDF00I: EDF CONTINUATION DEFAULT OVERRIDDEN"            *
*            THE EDF CONTROL PROGRAM WAS LOADED AND MODIFIED          *
*            USING THE SPECIFIED ZAPS.                                *
*           "ZEDF10E: PROGRAM 'DFHEDFD' COULD NOT BE LOCATED"         *
*            THE EDF CONTROL PROGRAM COULD NOT BE FOUND. IBM          *
*            MAY HAVE CHANGED THE NAME OF THIS PROGRAM TO             *
*            SOMETHING OTHER THAN "DFHEDFD".                          *
*           "ZEDF20E: THE DFHEDFD ZAPS COULD NOT BE INSTALLED"        *
*            THE EDF CONTROL PROGRAM "DFHEDFD" COULD NOT BE           *
*            MODIFIED BECAUSE THE INSTRUCTIONS TO BE OVERLAID         *
*            COULD NOT BE FOUND, PERHAPS DUE TO CHANGES IN            *
*            DFHEDFD AS A RESULT OF MAINTAINANCE BEING APPLIED.       *
*            YOU WILL HAVE TO INVESTIGATE THE CAUSE AND EITHER        *
*            UPDATE THE APPROPRIATE ZAP TABLE ENTRY OR ADD A NEW      *
*            ONE TO REFLECT THE CURRENT RELEASE OF CICS.              *
*           "ZEDF30E: DFHEDFD ZAP NOT INSTALLED - ABEND XXXX"         *
*            THE EDF CONTROL PROGRAM "DFHEDFD" WAS NOT MODIFIED       *
*            BECAUSE THIS PROGRAM SUFFERED ABEND "XXXX". THE ABEND    *
*            WAS INTERCEPTED TO PREVENT CICS FROM ABENDING AT         *
*            PLTPI TIME, BUT NO CORRECTIVE ACTION TAKEN.              *
*           "ZEDF40E: NO ZAP TABLE PRESENT FOR THIS RELEASE..."       *
*            THE EDF CONTROL PROGRAM "DFHEDFD" WAS NOT MODIFIED       *
*            BECAUSE THERE IS NO "ZAP TABLE" FOR THIS RELEASE OF      *
*            CICS IN THE PROGRAM. YOU WILL HAVE TO CREATE A NEW       *
*            ZAP TABLE ENTRY FOR THIS RELEASE OF CICS, ADJUSTING      *
*            THE TABLE (IF NECESSARY) TO REFLECT ANY CHANGES IN THE   *
*            TARGET INSTRUCTIONS IN DFHEDFD.                          *
* NOTES: 1) PROGRAM "DFHEDFD" MUST NOT BE IN THE MVS LPA. EITHER      *
*           REMOVE IT OR USE "PRVMOD=" TO TELL CICS NOT TO USE THE    *
*           LPA COPY. AN "ASRA" WILL RESULT IF DFHEDFD IS IN THE LPA. *
*        2) TO REMOVE THE ZAPS WHILE CICS IS RUNNING USE CECI TO      *
*           RELEASE THE HOLD ON DFHEDFD AND THEN NEWCOPY IT USING     *
***         CEMT.                                                   ***
*        W O R K I N G    S T O R A G E
         DFHEISTG
IOBUFF   DS    CL80                I/O BUFFER FOR TD WRITEQ
IOLEN    DS    H                   LENGTH OF DATA FOR TD WRITE
ZAPCNT   DS    PL1                 COUNT OF SUCCESSFUL ZAPS
         DS    X                   UNUSED
ZAPWORK  DS    6F                  SAVE AREA FOR VERIFIED ZAPS
EDFDEPA  DS    F                   SAVE AREA FOR DFHEDFD ENTRY POINT
EDFDLEN  DS    F                   SAVE AREA FOR DFHEDFD LENGTH
ZAPTBLA  DS    F                   ADDRESS OF SELECTED ZAP TABLE
INQRESP  DS    CL4                 "INQ SYSTEM" RESPONSE AREA
*        P R O G R A M    E N T R Y
ZEDFPGM  DFHEIENT DATAREG=(10),CODEREG=(9)
         B     L00100
         DC    CL8'ZEDFPGM'
         DC    CL8'&SYSDATE'
         DC    CL8'&SYSTIME'
***      ESTABLISH PROGRAM ABEND EXIT. SINCE THIS PROGRAM EXECUTES
*        AT PLTPI TIME WE DO THIS TO AVOID BRINGING CICS DOWN SHOULD
***      THIS PROGRAM FAIL.
L00100   DS    0H
         EXEC  CICS HANDLE ABEND                                       X
               LABEL(L01000) .     SET PROGRAM ABEND HANDLER
*        LOCATE THE ZAP TABLE FOR THE CURRENT RELEASE OF CICS.
         EXEC  CICS INQUIRE SYSTEM                                     X
               RELEASE(INQRESP) .  FETCH THE CICS RELEASE NUMBER
         CLC   INQRESP,=C'0211'    CICS 2.1.1?
         BNE   L00150              ..NO
         LA    R1,ZAPTBL21         ADDRESS CICS 2.1.1 ZAP TABLE
         ST    R1,ZAPTBLA          SAVE THE ADDRESS FOR LATER
         B     L00250              CONTINUE
L00150   DS    0H
         CLC   INQRESP,=C'0311'    CICS 3.1.1?
         BNE   L00200              ..NO
         LA    R1,ZAPTBL31         ADDRESS CICS 3.1.1 ZAP TABLE
         ST    R1,ZAPTBLA          SAVE THE ADDRESS FOR LATER
         B     L00250              CONTINUE
L00200   DS    0H
         MVC   IOBUFF(L'ERR40M),ERR40M
*                                   MOVE "NO ZAP" MESSAGE TO BUFFER
         MVC   IOBUFF+L'ERR40M(4),INQRESP
*                                   MOVE RELEASE NUMBER TO BUFFER
         MVC   IOLEN,=Y(L'ERR40M+4) SET MESSAGE LENGTH
         B     L01100               GO LOG EVENT TO CSMT
*        LOAD THE EDF PROGRAM AND "FIX" IT IN CORE.
L00250   DS    0H
         EXEC  CICS LOAD                                               X
               PROGRAM(EDFPGMID)                                       X
               FLENGTH(EDFDLEN)                                        X
               ENTRY(R2)                                               X
               NOHANDLE                                                X
               HOLD .              LOAD THE EDF PROGRAM
         CLC   EIBRESP,=F'00'      LOAD SUCCESSFUL?
         BE    L00300              ..YES - CONTINUE
         MVC   IOBUFF(L'ERR10M),ERR10M
*                                  MOVE "PGMIDERR" MESSAGE TO BUFFER
         MVC   IOLEN,=Y(L'ERR10M)  SET MESSAGE LENGTH
         B     L01100              GO LOG EVENT TO CSMT
***      SCAN DFHEDFD AND SEARCH FOR THE INSTRUCTIONS WE ARE GOING
*        TO REPLACE, AND INSURE THAT THEY ARE ALL THERE. WE WILL
*        ONLY OVERLAY THEM IF ALL THE INSTRUCTIONS ARE WHERE WE
***      THINK THEY ARE.      ---
L00300   DS    0H
         ST    R2,EDFDEPA          REMEMBER EDFD ADDRESS
         L     R1,ZAPTBLA          ADDRESS THE ZAP TABLE
         L     R3,EDFDLEN          LOAD PROGRAM LENGTH
         LA    R5,ZAPWORK          ADDRESS AREA FOR ZAP LIST
         ZAP   ZAPCNT,=P'0'        INITIALIZE VERIFY COUNT
L00400   DS    0H
         CLC   0(4,R1),=A(-1)      END OF ZAP TABLE?
         BE    L00800              ..YES
L00500   DS    0H
         CLC   0(4,R2),0(R1)       TARGET INSTRUCTION FOUND?
         BE    L00700              ..YES
L00600   DS    0H
         LA    R2,1(,R2)           BUMP POSITION POINTER
         BCT   R3,L00500           LOOP
         B     L00800              EOD - GO FINISH
L00700   DS    0H
         LR    R4,R2               ADDRESS CURRENT INSTRUCTION
         SH    R4,4(,R1)           ADDRESS PREVIOUS INSTRUCTION
         CLC   0(2,R4),6(R1)       PREVIOUS INSTRUCTION VALID?
         BNE   L00600              ..NO  - CONTINUE SCAN
         LR    R4,R2               ADDRESS CURRENT INSTRUCTION
         AH    R4,8(,R1)           ADDRESS NEXT INSTRUCTION
         CLC   0(2,R4),10(R1)      NEXT INSTRUCTION VALID?
         BNE   L00600              ..NO  - CONTINUE SCAN
         ST    R2,0(,R5)           ..YES  - SAVE ADDRESS AND VALUE
         MVC   4(4,R5),12(R1)               OF NEW INSTRUCTION
         AP    ZAPCNT,=P'1'        BUMP SUCCESSFUL VERIFY COUNT
         LA    R1,16(,R1)          ADDRESS NEXT ZAP TABLE ENTRY
         LA    R5,8(,R5)           ADDRESS NEXT ENTRY IN WORK AREA
         B     L00400              CONTINUE SCAN
*        IF ALL THE TARGET INSTRUCTIONS WERE FOUND, OVERLAY THEM
*        USING THE NEW VALUES IN THE ZAP TABLE WE JUST BUILT.
L00800   DS    0H
         CLC   ZAPCNT,=P'3'        ALL TARGET INSTRUCTIONS FOUND?
         BL    L00900              ..NO  - ERROR
         L     R1,ZAPWORK+0        LOAD ADDRESS OF FIRST ZAP
         MVC   0(4,R1),ZAPWORK+4   OVERLAY EXISTING CODE WITH ZAP
         L     R1,ZAPWORK+8        LOAD ADDRESS OF SECOND ZAP
         MVC   0(4,R1),ZAPWORK+12  OVERLAY EXISTING CODE WITH ZAP
         L     R1,ZAPWORK+16       LOAD ADDRESS OF THIRD ZAP
         MVC   0(4,R1),ZAPWORK+20  OVERLAY EXISTING CODE WITH ZAP
         MVC   IOBUFF(L'OKMSG),OKMSG MOVE "ZAP OK" MESSAGE TO BUFFER
         MVC   IOLEN,=Y(L'OKMSG)   SET MESSAGE LENGTH
         B     L01100              GO LOG EVENT TO CSMT
*        THE ZAPS COULD NOT BE INSTALLED. RELEASE THE HOLD ON
*        DFHEDFD AND SEND AN ERROR MESSAGE TO THE CSMT LOG.
L00900   DS    0H
         EXEC  CICS RELEASE                                            X
               PROGRAM(EDFPGMID) .  RELEASE THE PROGRAM
         MVC   IOBUFF(L'ERR20M),ERR20M
*                                  MOVE "BAD INST." MESSAGE TO BUFFER
         MVC   IOLEN,=Y(L'ERR20M)  SET MESSAGE LENGTH
         B     L01100              GO LOG EVENT TO CSMT
*        ABEND INTERCEPT ROUTINE.
L01000   DS    0H
         EXEC  CICS ASSIGN                                             X
               ABCODE(IOBUFF+L'ERR30M)
*                                   RETRIEVE ABEND CODE
         EXEC  CICS RELEASE                                            X
               PROGRAM(EDFPGMID)                                       X
               NOHANDLE .           RELEASE THE PROGRAM
         MVC   IOBUFF(L'ERR30M),ERR30M
*                                   SET ABEND MESSAGE IN BUFFER
         MVC   IOLEN,=Y(L'ERR30M+4) SET MESSAGE LENGTH
         B     L01100               GO LOG EVENT TO CSMT
*        LOG THE RESULT (WHATEVER IT WAS) TO CSMT, AND RETURN.
L01100   DS    0H
         EXEC  CICS WRITEQ TD                                          X
               QUEUE(ERRDEST)                                          X
               FROM(IOBUFF)                                            X
               LENGTH(IOLEN) .     LOG EVENT TO CSMT LOG
         EXEC  CICS RETURN .       RETURN TO CICS
*        Z A P    T A B L E
*        THE EDF ZAP TABLE CONTAINS AN ENTRY FOR EACH INSTRUCTION TO BE
*        ALTERED, EACH ENTRY CONTAINING THE VALUE OF THE INSTRUCTION TO
*        BE OVERLAID, THE RELATIVE OFFSET/VALUE OF THE INSTRUCTIONS ON
*        EITHER SIDE OF THE TARGET INSTRUCTION, AND THE VALUE OF THE
*        REPLACEMENT INSTRUCTION. THERE IS A SEPARATE ZAP TABLE FOR
*        CICS 2.1.1 AND CICS 3.1.1. THERE ARE THREE ENTRIES IN THE
*        TABLES, EACH ENTRY HAVING THE FOLLOWING FORMAT:
*        X'94C0A04C',X'0006',X'D203',X'0004',X'58A0',X'94D0A04C'
*         ---------  -----   -----   -----   -----   ---------
*             ¨        ¨       ¨       ¨       ¨         ¨
*             ¨        ¨       ¨       ¨       ¨          - REPLACEMENT
*             ¨        ¨       ¨       ¨       ¨            INSTRUCTION
*             ¨        ¨       ¨       ¨        - FIRST 2 BYTES OF
*             ¨        ¨       ¨       ¨          FOLLOWING INSTRUCTION
*             ¨        ¨       ¨        - LENGTH OF TARGET INSTRUCTION
*             ¨        ¨        - FIRST 2 BYTES OF PRIOR INSTRUCTION
*             ¨         - LENGTH OF PRIOR INSTRUCTION
*              - VALUE OF TARGET INSTRUCTION
ZAPTBL21 DS    0H        <======== ZAP TABLE FOR CICS/MVS 2.1.1
         DC    X'94C0A04C',X'0006',X'D203',X'0004',X'58A0',X'94D0A04C'
         DC    X'9680204C',X'0006',X'D203',X'0004',X'9680',X'9690204C'
         DC    X'9680204E',X'0004',X'9680',X'0004',X'9602',X'9400204E'
         DC    X'FFFFFFFF'
ZAPTBL31 DS    0H        <======== ZAP TABLE FOR CICS/ESA 3.1.1
         DC    X'94C0A04C',X'0006',X'D203',X'0004',X'58A0',X'94D0A04C'
         DC    X'9680204C',X'0004',X'4250',X'0004',X'9680',X'9690204C'
         DC    X'9680204E',X'0004',X'9680',X'0004',X'9602',X'9400204E'
         DC    X'FFFFFFFF'
*        C O N S T A N T S
EDFPGMID DC    CL8'DFHEDFD'        NAME OF EDF CONTROL PROGRAM
ERRDEST  DC    CL4'CSMT'           NAME OF CICS MESSAGE LOG
OKMSG    DC    C'ZEDF00I: EDF CONTINUATION DEFAULT OVERRIDDEN'
ERR10M   DC    C'ZEDF10E: PROGRAM "DFHEDFD" COULD NOT BE LOCATED'
ERR20M   DC    C'ZEDF20E: THE "DFHEDFD" ZAP COULD NOT BE INSTALLED'
ERR30M   DC    C'ZEDF30E: "DFHEDFD" ZAP NOT INSTALLED DUE TO ABEND '
ERR40M   DC    C'ZEDF40E: NO ZAP TABLE PRESENT FOR THIS RELEASE OF CICSX
                - '
         END


Christopher Frank
Lead Systems Programmer
