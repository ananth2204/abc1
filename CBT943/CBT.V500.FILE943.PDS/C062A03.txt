CICS automatic master console sign-on

An article under the above title in CICS Update, October 1989,
outlined a solution to the problem of console operators needing
to log-on to CICS and thus exposing their passwords.  An
alternative solution has been developed at our site that is
somewhat simpler and worth considering.

The technique takes advantage of the fact that there is normally
a RACF started task user-id applicable to the whole CICS
system.  If the TCT definitions for the consoles have this user-
id hard coded into them, their access can be controlled by this
user-id.

The only action necessary is to have the pseudo sign-on table
entries for all consoles plugged with the address of the region
Access Control Environment Element (ACEE) in field
SNNTUBLK at CICS start-up time.  A PLT post-initialization
module can be used to do this.

This method is working under CICS 1.7, RACF 1.8.0,
MVS/XA 2.2.0, and JES2 2.2.0.


STEP ONE

Assemble and link a Program Load Table (PLT) with an entry
for CSGCONS.


STEP TWO

Include an entry for CSGCONS in the PPT.


STEP THREE

Include the PLT name in the PPT.


STEP FOUR

Specify the PLT name in your CICS start-up parameters.


STEP FIVE

Add the following parameters to all console definitions in the
CICS System Definitions (CSD) file:

      TERMINAL=CNnn      -    consoles are identified as CN00 to CN99
      OPERID=CON         -    not critical this value
      USERID=STU007      -    use CICS region userid
      OPERSECURITY=1     -    no need worry about security keys


STEP SIX

The SIT should specify the following parameters:

      EXTSEC=(YES,PREFIX) - you can use NOPREFIX but this
                            would be altered by the RDEFINE
      XTRAN=YES


STEP SEVEN

Define the transaction authorities for this user-id.  If the started
task user-id has very broad authorities it may be necessary to
do a number of PERMITs with the ACCESS(NONE)
parameter.  Some typical illustrative commands have been
included to show the effect that we wish to achieve but actual
commands and resource names will be unique to each user
environment.

      RDEFINE GCICSTRN CONS ADDMEM(STU007.CEMT) UACC(NONE)
      PERMIT CONS CLASS(GCICSTRN) ID(STU007) ACCESS(READ) PERMIT OTHERS
       CLASS(GCICSTRN) ID(STU007) ACCESS(NONE)


STEP EIGHT

Assemble and link the following program:

DFHEISTG  DSECT
          DFHCSAD   TYPE=DSECT
          DFHTCTZE  CICSYST=NO
          DFHSSAD   TYPE=DSECT
          DFHTM     MF=(D,SKTTBLE)
          DFHTM     MF=(D,TMSTATIC)
          DFHTM     MF=(D,DIRSEG)
          DFHTM     MF=(D,DIRELEM)
          DFHSNT    TYPE=DSECT
          IHAASCB   DSECT=YES,LIST=NO
          IHAASXB   DSECT=YES,LIST=NO
          IHAACEE
CSGCONS   DFHEIENT  CODEREG=9,DATAREG=10,EIBREG=11
*         FIND CICS REGION ACEE
          EXEC CICS ADDRESS CSA(R13)
          L         R2,16              GET ADR OF CVT
          L         R2,0(,R2)          GET TCB/ASCB PTRS
          L         R2,12(,R2)         GET ADR CICS ASCB
          USING     ASCB,R2            TELL ASM
          L         R3,ASCBASXB        GET ADR OF ASXB
          DROP      R2
          USING     ASXB,R3            TELL ASM
          L         R2,ASXBSENV        GET ADR CICS ACEE
          DROP      R3
*         FIND TABLE MANAGER TERMINAL DIRECTORY DATA
          ICM       R3,15,CSAOPFLA     GET ADR OPT FEATS
          USING     CSAOPFL,R3         TELL ASM
          ICM       R4,15,CSASSA       GET STATIC STORAGE
          USING     DFHSSADS,R4        TELL ASM
          ICM       R5,15,SSATMP       GET TABLE MGR SSA
          USING     DFHTMSSA,R5        TELL ASM
          ICM       R6,15,TMASKT7      GET TCTE SCATTER
          USING     DFHTMSKT,R6        TELL ASM
          ICM       R3,15,SKTDIRSA     GET TM DIR SEG
          DROP      R3,R4,R5,R6
          USING     DFHTMDSG,R3        TELL ASM
NEWSEG    EQU       *
          LA        R4,8(R3)           GET TM DIR ELEM
          USING     DFHTMDEL,R4        TELL ASM
*         FIND CONSOLE ENTRIES AND PLUG POINTER
COMPARE   EQU       *
          ICM       R5,15,DIRTEA       GOT A TCTTE?
          BZ        NEXTONE            NO - FORGET IT
          USING     DFHTCTTE,R5        TELL ASM
          CLC       DIRKEY(2),=CL2'CN' IS IT CONSOLE?
          BNE       NEXTONE            GO SET ACEE
MVCACEE   EQU       *
          ICM       R6,15,TCTTESNT     GET CONSOLE SNTTE
          BZ        NEXTONE            NO - ONTO NEXT
          USING     DFHSNNT,R6         TELL ASM
          STCM      R2,15,SNNTUBLK     SET ACEE ADR
*         FIND NEXT CONSOLE ENTRY
NEXTONE   EQU       *
          ICM       R4,15,DIRGNCHN     GET NEXT ELEMENT
          BNZ       COMPARE            YES - DO COMPARE
          ICM       R3,15,DIRSGCHN     GET NEXT SEGMENT
          BZ        FINISH             NO - THEN FINISH
          B         NEWSEG             YES - PROCESS
FINISH    EQU       *
          EXEC CICS RETURN
          END

CONCLUSION

At the conclusion of the above steps the operators will not need
to log-on to CICS through the consoles at all, but will have
sufficient authority to do whatever tasks are required of them.
Their authority can be extended or restricted through normal
RACF commands.  In our site, the started task user-ids have no
TSO segment, and cannot be used to log-on to TSO.
However, this causes no problem with the above technique as a
RACINIT is never issued for these user-ids.


David Evans
CICS Consultant
Telecom Australia (Australia)

