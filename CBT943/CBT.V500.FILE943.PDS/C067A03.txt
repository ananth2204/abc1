Displaying currently logged-on CICS users

To date, our computer operators have always had difficulty
shutting down CICS as they have to ensure that everybody has
logged off from the system before bringing it down.  They
usually enquire on the active tasks continuously through CEMT
for about five minutes and hope that they have displayed all the
active tasks.  All our tasks are pseudo-conversational, hence
they will not be displayed in the active task list during a
terminal I/O wait.  This makes the current method of simply
displaying the active task list somewhat unreliable.

From CICS 1.7, IBM provided a very useful facility to
examine and modify system resources such as programs,
terminals, datasets, and tasks, through its new command-
language interface EXEC CICS INQUIRE/SET commands.
This facility is described in detail in the CICS Customization
Guide (reference number SC33-0239 for CICS 1.7).  Through
this facility we developed a COBOL program that will browse
through all the allocated terminals (including TCT or MRO
defined and auto-installed terminals) and pick up the user-id
field.  The program then builds a BMS page containing all the
user-ids and the active tasks, if any.

We force all our users to sign on by securing all their
transactions, therefore our computer operators are pretty sure
that they are not interrupting anybody when they shut down the
system.  They can also call or send each signed-on user a
warning message that the system is about to be shut down.
Our users appreciate being informed of such interruptions so
they can end their tasks normally.

The BMS page uses simple EXEC CICS SEND TEXT
commands making the output displayable through the MVS
console.  If invoked through a normal terminal, the list can be
browsed back and forth through the single key retrieval PF
keys set in the SIT - normally PF7 and PF8, respectively.

As an additional function of this user enquiry program, we also
included resetting all 'out-of-service' terminals to 'in service'.
We figured that since we are already examining the terminals,
why not modify the status as well?

IDENTIFICATION DIVISION.
PROGRAM-ID.  ZUSERINQ.
ENVIRONMENT DIVISION.
CONFIGURATION SECTION.
SOURCE-COMPUTER. IBM4381.
OBJECT-COMPUTER. IBM4381.
DATA DIVISION.
WORKING-STORAGE SECTION.
01  PAGE-CTR                    PIC S99   COMP-3 VALUE +1.
01  LINE-CTR                    PIC S999  COMP-3.
01  LINE-SCR                    PIC S9999 COMP.
01  TERM-ST                     PIC S9(8) COMP.
01  HDG1.
    05  FILLER                  PIC X(57)  VALUE
    'TERM     NET-NAME     USER-ID     TRAN    STATUS    PAGE '.
    05  PAGE-DSP                PIC 99.
    05  FILLER                  PIC X(20)  VALUE SPACES.
01  HDG2                        PIC X(79)  VALUE
    '----     --------     -------     ----    ------'.
01  HDG3                        PIC X(79)  VALUE SPACES.
01  ACT-TERM.
    05  TERM-ID                 PIC X(4).
    05  FILLER                  PIC X(5)   VALUE SPACES.
    05  NET-NAME                PIC X(8).
        88  NO-NET-NAME         VALUE SPACES.
    05  FILLER                  PIC X(5)   VALUE SPACES.
    05  USER-ID                 PIC X(8).
        88  NO-USER             VALUE SPACES.
    05  FILLER                  PIC X(4)   VALUE SPACES.
    05  TRAN-ID                 PIC X(4).
        88  NO-TRAN             VALUE SPACES.
    05  FILLER                  PIC X(4)   VALUE SPACES.
    05  TERM-STAT               PIC X(36).
PROCEDURE DIVISION.
    EXEC CICS HANDLE CONDITION END  (999-END)
         END-EXEC.
    EXEC CICS IGNORE CONDITION ERROR
         END-EXEC.
    EXEC CICS ASSIGN  SCRNHT(LINE-SCR)
         END-EXEC.
    EXEC CICS INQUIRE TERMINAL START
         END-EXEC.
010-SEND-HDG.
    MOVE PAGE-CTR TO PAGE-DSP.
    EXEC CICS SEND TEXT FROM(HDG1) LENGTH(79) PAGING ACCUM ERASE
         END-EXEC.
    EXEC CICS SEND TEXT FROM(HDG2) LENGTH(79) PAGING ACCUM ERASE
         END-EXEC.
    EXEC CICS SEND TEXT FROM(HDG3) LENGTH(79) PAGING ACCUM ERASE
         END-EXEC.
    MOVE +3 TO LINE-CTR.
020-NEXT-TERM.
    EXEC CICS INQUIRE TERMINAL    (TERM-ID)
                      NETNAME     (NET-NAME)
                      USERID      (USER-ID)
                      TRANSACTION (TRAN-ID)
                      SERVSTATUS  (TERM-ST)
                      NEXT
         END-EXEC.
    IF NO-NET-NAME  OR
       (NO-USER    AND
        TERM-ST = DFHVALUE (INSERVICE))
       GO TO 020-NEXT-TERM.
    IF TERM-ST = DFHVALUE (OUTSERVICE)
       MOVE 'OUT - MADE INSERVICE'  TO TERM-STAT
       MOVE DFHVALUE (INSERVICE)    TO TERM-ST
       EXEC CICS SET     TERMINAL    (TERM-ID)
                         SERVSTATUS  (TERM-ST)
            END-EXEC
    ELSE
       MOVE SPACES                  TO TERM-STAT.
    ADD +1 TO LINE-CTR.
    EXEC CICS SEND TEXT FROM(ACT-TERM) ERASE
         LENGTH(79) PAGING ACCUM
         END-EXEC
    IF LINE-CTR EQUAL LINE-SCR
       ADD +1 TO PAGE-CTR
       PERFORM 010-SEND-HDG.
    GO TO 020-NEXT-TERM.
999-END.
    EXEC CICS SEND PAGE AUTOPAGE RELEASE
         END-EXEC
    EXEC CICS INQUIRE TERMINAL END
         END-EXEC.
    EXEC CICS RETURN
         END-EXEC.
    GOBACK.


Pedronilo T Perez
RCG/Vectron Systems Inc (USA)

