Job submission from CICS to VSE/POWER


INTRODUCTION

In CICS 1.7 there is a new facility that enables jobs to be sent
to the VSE/POWER reader queue.  In this article we present an
example which consists of a calling program and a subroutine
to help others use the facility and avoid some of the pitfalls
which we encountered.

In the subroutine, section one could be used to check that
installation standards are met.  The example uses a GETMAIN
in order to save on memory.


CALLING PROGRAM

 IDENTIFICATION  DIVISION.
 PROGRAM-ID. SYTESTX.
 ENVIRONMENT DIVISION.
 DATA DIVISION.
 WORKING-STORAGE SECTION.
 01  WS-COMMAREA.
     03  DJNAME             PIC X(08).
     03  DJCLASS            PIC X(01).
     03  DJPRI              PIC X(01).
     03  DJUSRNM            PIC X(16).
     03  DCICS              PIC X(08).
     03  DTCTP              PIC X(04).
     03  DACARDS            PIC S9(08) COMP.
     03  DNCARDS            PIC S9(04) COMP.
     03  DRC                PIC X(08).
     03  DRCMSG             PIC X(80).
     03  DFUSE              PIC X(160).
 01  EFES           PIC 9  VALUE 0.
 01  WS-JOB.
     03  FILLER      PIC X(80)
         VALUE '// JOB SYXXXX'.
     03  FILLER      PIC X(80)
         VALUE '// PAUSE'.
     03  FILLER      PIC X(80)
         VALUE '/*'.
     03  FILLER      PIC X(80)
         VALUE '/&'.
 01  WS-JOBR REDEFINES WS-JOB.
     03  WS-LINE  OCCURS 4 TIMES PIC X(80).
 LINKAGE SECTION.
 01  PARMLIST.
     03  FILLER         PIC S9(08) COMP.
     03  SHETAH-POINTER PIC S9(08) COMP.
 01  SHETAH.
     03  SHETAH-LINE  OCCURS 10 TIMES PIC X(80).
 PROCEDURE DIVISION.
     PERFORM 100-GETMAIN.
     PERFORM 200-JOB.
     PERFORM 300-TRANS
             VARYING DNCARDS FROM 1 BY 1 UNTIL DNCARDS > 4.
     MOVE 4 TO  DNCARDS.
     EXEC CICS LINK PROGRAM ('SUBRDR')
                    COMMAREA (WS-COMMAREA)
                    LENGTH (292)
     END-EXEC.
     EXEC CICS RETURN END-EXEC.
*-----------------------*
 100-GETMAIN  SECTION.
*-----------------------*
     EXEC CICS GETMAIN SET (SHETAH-POINTER OF PARMLIST)
                       LENGTH (800)
                       INITIMG (EFES)
     END-EXEC.
     MOVE SHETAH-POINTER OF PARMLIST TO DACARDS.
*-----------------------*
 200-JOB      SECTION.
*-----------------------*
     MOVE 'YECHEZKEL' TO DJUSRNM.
     MOVE 'SYXXXX'    TO DJNAME.
     MOVE 'P'         TO DJCLASS.
     MOVE '9'         TO DJPRI.
     EXEC CICS ASSIGN APPLID (DCICS) END-EXEC.
     MOVE '    '      TO DTCTP.
*-----------------------*
 300-TRANS      SECTION.
*-----------------------*
     MOVE WS-LINE(DNCARDS)  TO SHETAH-LINE(DNCARDS).


SUBRDR SUBROUTINE

DPARMS   DSECT
DJNAME   DS    CL8       JOBNAME
DJCLASS  DS    CL1       CLASS
DJPRI    DS    CL1       PRI
DJUSRNM  DS    CL16      USER
DCICS    DS    CL8       NETNAME OF THE CICS(FOR FUTURE USE)
DTCTP    DS    CL4       CICS TERMID OF USER'S PRINTER (FUTURE USE)
D@CARDS  DS    4X        ADDRESS OF CARDS
DNCARDS  DS    2X        NO OF CARDS
DRC      DS    CL8       ERROR TEXT
DRCMSG   DS    CL80      FUTURE USE
         DS    CL160     FUTURE USE
         DFHREGS ,                 EQUATE REGISTERS
BALREG   EQU 10
COMPTR   EQU 9
DFHEISTG DSECT ,
TOK      DS D
RECOUT   DS CL80
CL16     DS CL16
* ERROR FIELDS
CONTROL  DS CL1
SAVR6    DS F
SAVR4    DS F
RESP1    DS CL4
RESP2    DS CL4
SUBRDR   CSECT ,
         L COMPTR,DFHEICAP
         USING DPARMS,COMPTR
***********************************************************************
* *  MAIN                                                           * *
***********************************************************************
* 1 VERIFY INSTALLATION STANDARDS
* 2 BUILD * $$ JOB CARD
* 3 OPEN SPOOL
* 4 WRITE SPOOL * $$ JOB
* 5 WRITE SPOOL ALL RECORDS
* 6 WRITE SPOOL * $$ EOJ
* 7 CLOSE SPOOL
* 8 RETURN
***********************************************************************
* 1 VERIFY INSTALLATION STANDARDS
***********************************************************************
* CHECK THE PARAMETERS FOR INSTALLATION STANDARDS
* DJNAME  = JOB NAME
* DJCLASS = JOB CLASS
* DJPRI   = JOB PRIORITY
* DJUSRNM = USER NAME
***********************************************************************
* 2 BUILD * $$ JOB CARD
***********************************************************************
         MVI RECOUT,X'40'                 SPACES TO RECOUT
         MVC RECOUT+1(L'RECOUT-1),RECOUT
*  JOB1JNM=
         LA   R15,RECOUT
         MVC  0(L'JOB1,R15),JOB1
         LA   R15,L'JOB1(R15)
         LA   R6,8-1
         LA   R14,DJNAME(R6)
         BAL  BALREG,$$JOBP
** CLASS=
         MVC  0(L'JOB2,R15),JOB2
         LA   R15,L'JOB2(R15)
         MVC  0(1,R15),DJCLASS
         LA   R15,1(R15)
** PRI=
         MVC  0(L'JOB3,R15),JOB3
         LA   R15,L'JOB3(R15)
         MVC  0(1,R15),DJPRI
         LA   R15,1(R15)
** USER=
         MVC  0(L'JOB4,R15),JOB4
         LA   R15,L'JOB4(R15)
         LA   R6,16-1
         LA   R14,DJUSRNM(R6)
         BAL  BALREG,$$JOBP
         MVC  0(1,R15),=C''''
***********************************************************************
* 3 OPEN SPOOL
***********************************************************************
         MVC  CONTROL,=C'4'
         EXEC CICS SPOOLOPEN  REPORT(JOBNAM) TOKEN(TOK) JCL            X
                              RESP(RESP1) RESP2(RESP2)
         CLC  RESP1,DFHRESP(NORMAL)
         BNE  ERROPEN
***********************************************************************
* 4 WRITE SPOOL * $$ JOB
***********************************************************************
         EXEC CICS SPOOLWRITE REPORT(JOBNAM) TOKEN(TOK)                X
                              FROM(RECOUT)                             X
                              RESP(RESP1) RESP2(RESP2)
         CLC  RESP1,DFHRESP(NORMAL)
         BNE  ERRWRIT1
***********************************************************************
* 5 WRITE SPOOL ALL RECORDS
***********************************************************************
         MVC  CONTROL,=C'5'
*        LH   R6,DNCARDS
         SR   R6,R6
         ICM  R6,B'0011',DNCARDS
*        LA   R4,D@CARDS
         SR   R4,R4
         ICM  R4,B'1111',D@CARDS
LOOPRITE DS 0H
         MVC  RECOUT(80),0(R4)
         EXEC CICS SPOOLWRITE REPORT(JOBNAM) TOKEN(TOK)                X
                              FROM(RECOUT)                             X
                              RESP(RESP1) RESP2(RESP2)
         CLC  RESP1,DFHRESP(NORMAL)
         BNE  ERRWRIT2
         LA   R4,80(R4)
         BCT  R6,LOOPRITE
***********************************************************************
* 6 WRITE SPOOL * $$ EOJ
***********************************************************************
* LASTCARD
         MVC  CONTROL,=C'L'
         MVC  RECOUT(80),CRDEOJ
         EXEC CICS SPOOLWRITE REPORT(JOBNAM) TOKEN(TOK)                X
                              FROM(RECOUT)                             X
                              RESP(RESP1) RESP2(RESP2)
         CLC  RESP1,DFHRESP(NORMAL)
         BNE  ERRWRIT3
***********************************************************************
* 7 CLOSE SPOOL
***********************************************************************
         MVC  CONTROL,=C'6'
         EXEC CICS SPOOLCLOSE REPORT(JOBNAM) TOKEN(TOK)                X
                              RESP(RESP1) RESP2(RESP2)
         CLC  RESP1,DFHRESP(NORMAL)
         BNE  ERRCLOSE
***********************************************************************
* 8 RETURN
***********************************************************************
         B    RETURN
***********************************************************************
ERRS     DS   0H
ERROPEN  DS   0H
ERRWRIT1 DS   0H
ERRWRIT2 DS   0H
ERRWRIT3 DS   0H
ERRCLOSE DS   0H
         ST    R6,SAVR6
         ST    R4,SAVR4
         CLC   RESP2,DFHRESP(NOSPACE)
         BNE   CONT0
         MVC   DRC,=CL8'NOSPACE'
CONT0    B     RETURN
***********************************************************************
* MOVE JOB NAME WITHOUT BLANKS
***********************************************************************
$$JOBP   DS 0H
         EX   R6,$MVCIN
         SR   R14,R6
         EX   R6,$TRT
         BZ   ALLSPACE
         LA   R5,CL16
         SR   R1,R5
         SR   R6,R1
         EX   R6,$MVC
         LA   R15,1(R6,R15)
         BR   BALREG
ALLSPACE DS   0H
         EX   R6,$MVC2
         BR   BALREG
$MVCIN   MVCIN CL16(*-*),0(R14)
$TRT     TRT   CL16(*-*),TRTTAB
$MVC     MVC   0(*-*,R15),0(R14)
$MVC2    MVC   0(*-*,R15),=8C'@'
TRTTAB   DC 256C'*-TRTTAB'
         ORG  TRTTAB+X'40'
         DC   X'0'
         ORG
RETURN   DS    0H
         EXEC CICS RETURN
JOB1     DC  C'* $$ JOB JNM='
JOB2     DC  C',CLASS='
JOB3     DC  C',PRI='
JOB4     DC  C',USER='''
CRDEOJ   DC  CL80'* $$EOJ          CCC'
JOBNAM   DC  CL8'JOB'
         END SUBRDR


Yechezkel Ida and Lior Hadas
Systems Programmers
Menora Insurance Co Ltd (Israel)

