 A user-enhanced DFHPEP
DFHPEP (program error program) is an IBM-supplied program which is activated by
DFHACP (abnormal control program) in the process of a task abend.  The source is
contained in the IBM-supplied source library; any user may modify and enhance
this code, adjusting it to his installation needs.  More information about this
program can be found in: Customisation Guide SC33-0239-0, pages 51-52, and
Recovery and Restart Guide SC33-0231-0, pages 95-97 - the books are CICS OS/VS
1.7 publications.
The purpose of the attached program is to record on a VSAM file information
about all abends in the CICS region as they occur.  This information can be
later analysed using standard batch or on-line tools.  The VSAM KSDS file should
be defined with a 100-byte record length and a key of 18 bytes starting at
offset 0.  An appropriate entry for this file must appear in the FCT.
The fileUs key is composed of the abend date (YYMMDD), time (HHMMSSTH), and the
task facility (usually a terminal).  More information about the abended task is
contained in subsequent fields - the recordUs layout is contained in the source.
As the program is written in macro-level Assembler, it has no predefined working
storage area, so we found it convenient to place work fields in the file itself
hoping this would not cause too great a waste of disk storage.
Obviously, the file format (the content of its key as well as the data) may be
regarded merely as a proposal.  Any changes, however, must of course be
reflected in the program code.  This program is currently implemented on CICS
1.7 under MVS/SP.
TCASBAR  EQU   9                   TCA SYSTEM AREA REGISTER
PEPBAR   EQU   10                  PEP BASE REGISTER
WORKREG  EQU   4                   WORKING REGISTER
FWACBAR  EQU   7                   FILE WORK AREA REGISTER
         COPY DFHFWADS
RECDATA  DS  0CL104
RECLEN   DS  H
         DS  H
RECAREA  DS  0CL100
RECKEY   DS  0CL18
KEYYY    DS  CL2     YY-YEAR
KEYMM    DS  CL2     MM-MONTH
KEYDD    DS  CL2     DD-DAY
KEYTIME  DS  CL8     TIME IN HHMMSSTH (TH-TENTH AND HUNDRETH OF SECOND)
KEYFAC   DS  CL4     TASK'S FACILITY (MAINLY A TERMINAL)
RECSTRG  DS  0CL82
RECTRAN  DS  CL4     TRANSACTION IDENTIFICATION
RECPGM   DS  CL8     ABENDED PROGRAM NAME
RECCODE  DS  CL4     ABEND CODE
RECOPID  DS  CL3     USER OPID (FROM TCTTE-IF FACILITY IS A TERMINAL)
WORKSTRG DS  0CL63
         DS  CL3
SAVER01  DS  0D
SAVER0   DS  F
SAVER1   DS  F
CURRYY   DS  H
CURRMM   DS  H
CURRDD   DS  H
SWT29    DS  H
WORK1    DS  D
UNPKR01  DS  CL16
         DS  CL20     FOR FUTURE AND ADDITIONAL INFORMATION
* * *          D U M M Y   S E C T I O N S                        * * *
         COPY  DFHCSADS
         DFHTCA  CICSYST=YES
         DFHPRINT  DSCT=START
         DFHPRINT  DSCT=END
* * * * *              P R O G R A M   E R R O R              * * * * *
* * * * *                    P R O G R A M                    * * * * *
DFHPEP   CSECT                     PROGRAM ERROR PROGRAM CSECT
         DFHVM PEP                 GENERATE HEADING CONSTANT
         ENTRY DFHPEPNA            ESTABLISH ENTRY POINT
DFHPEPNA DS    0H                  ENTRY POINT
          BALR  PEPBAR,0            ESTABLISH ADDRESSABILITY
         USING *,PEPBAR            .. AND BASE REGISTER
         L   TCASBAR,TCASYAA       LOAD TCA SYSTEM AREA ADDRESS
         USING DFHSYTCA,TCASBAR
****       HERE BEGINS THE USER'S CODE       ****
* THE DATASET NAME IS THE FILE'S  DD  NAME
         DFHFC  TYPE=GETAREA,DATASET=PEPTEST,INITIMG=00,ERROR=ERR1
         L      FWACBAR,TCAFCAA    ADDRESSABILITY TO FWA
         MVC    RECLEN,=H'104'
         TIME   ZONE=LT              MVS  TIME  MACRO
         STM    0,1,SAVER01
         UNPK   UNPKR01,SAVER01
         MVC    KEYTIME,UNPKR01+1    SAVING THE TIME IN DISPLAY FORMAT
         BAL    3,CONVERT
         L      WORKREG,TCAFCAAA          FACILITY ADDRESS
         MVC    KEYFAC,0(WORKREG)         FACILITY ID
         CLC    TCAFCAAA(1),=X'01'        TERMINAL INDICATOR ?
         BNE    SKIPOPID                  SKIP OVER THE OPID
*  THE OPID IS IN TCTTE - FIELD TCTTEOI
         MVC    RECOPID,X'25'(WORKREG)    USER OPID
SKIPOPID MVC    RECCODE,TCACRABC          CURRENT ABEND CODE
         L      WORKREG,TCATCPC           PCT ADDRESS
         MVC    RECTRAN,0(WORKREG)        TRANSACTION ID
*  T A C B - TASK ABEND CONTROL BLOCK - IS POINTED AT BY  TCAPCACB  AND
* ITS FIELD  ABNDPRG  CONTAINS THE ABENDED PROGRAM NAME
         L      WORKREG,TCAPCACB          TACB ADDRESS
         MVC    RECPGM,X'20'(WORKREG)     PROGRAM ID
         ST     FWACBAR,TCAFCAA        PLACING(BACK) FWA ADDRESS IN TCA
         DFHFC  TYPE=PUT,TYPOPER=NEWREC,RDIDADR=RECKEY,ERROR=ERR1
ERR1     DFHPC  TYPE=RETURN            RETURN TO DFHACP
*   THIS ROUTINE CONVERTS THE DATE RECEIVED IN REG1 USING THE
*  TIME  MACRO FROM 00YYDDDF FORMAT TO YYMMDD DISPLAY FORMAT
CONVERT  XR   5,5
         LH   5,SAVER1
         SLA  5,4
         O    5,=X'0000000F'  'PACKING' THE YEAR
         ST   5,WORK1+4
         CVB  5,WORK1
         STH  5,CURRYY     SAVING THE YEAR
         CH   5,=H'0100'   FOR THE SECOND MILLENIUM - THE YEAR 2000
         BE   GOON         IN FEB 2000 - 28 DAYS
         XR   4,4
         D    4,=F'4'
         CH   4,=H'0000'   NO REMAINDER ?
         BNE  GOON
         MVC  SWT29,=H'1'  29 DAYS IN FEBRUARY
GOON     MVC  SAVER1(2),=X'0000'   NULLING THE YEAR
         MVC  WORK1+4(4),SAVER1
         CVB  5,WORK1
         LA   8,FEB28
         XR   6,6
         LA   6,1
         CLC  SWT29,=H'1'
         BNE  LOOP1
         LA   8,FEB29        IN REG8 THE APPROPRIATE DATE'S TABLE
LOOP1    CH   5,0(8)
         BNH  OUTNH1
         LA   6,1(6)         ADVANCING TO NEXT NAME
         LA   8,2(8)         ADVANCING TO NEXT DATE'S TABLE
         B    LOOP1
OUTNH1   STH  6,CURRMM       SAVING THE MONTH
         SH   8,=H'2'        GOING BACKWARDS TO PREVIOUS DATE'S TABLE
         SH   5,0(8)         THE DIFFERENCE IS THE CURRENT DAY
         STH  5,CURRDD       AND SAVING IT
*  UNPACKING THE YEAR
         XR   5,5
          LH   5,CURRYY
         CVD  5,WORK1
         UNPK KEYYY,WORK1
         OI   KEYYY+1,X'F0'
*  UNPACKING THE MONTH
         LH   5,CURRMM
         CVD  5,WORK1
         UNPK KEYMM,WORK1
         OI   KEYMM+1,X'F0'
*  UNPACKING THE DAY
         LH   5,CURRDD
         CVD  5,WORK1
         UNPK KEYDD,WORK1
         OI   KEYDD+1,X'F0'
         BR   3
         LTORG
         DC   H'0'
FEB28    DC   H'31'
         DC   H'59'
         DC   H'90'
         DC   H'120'
         DC   H'151'
         DC   H'181'
         DC   H'212'
         DC   H'243'
         DC   H'273'
         DC   H'304'
         DC   H'334'
         DC   H'365'
         DC   H'0'
FEB29    DC   H'31'
         DC   H'60'
         DC   H'91'
         DC   H'121'
         DC   H'152'
         DC   H'182'
         DC   H'213'
         DC   H'244'
         DC   H'274'
         DC   H'305'
         DC   H'335'
         DC   H'366'
DFHPEPEA DS      0H                MODULE END ADDRESS
         DFHEND  DFHPEPNA          ASSEMBLY END - PROGRAM ERROR


Safran Menachem
Systems Programmer
Mirtachim Computers (Israel)  ) Xephon 1988

















