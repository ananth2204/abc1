An implemention of the auto-install facility

Our installation has several large CICS regions in which DSA is at
a premium.  The auto-install feature of CICS 1.7 was attractive to
us since we have many terminals in these regions; eliminating
their TCTTEs when possible would be helpful.

The common problem with the auto-install scheme is creating a
unique CICS terminal-identification for the incoming terminal.
The new terminal-id is usually generated by using part of the
NETNAME or using a hashing algorithm.

Our applications require the terminal-id to be consistent from
install to install.  In other words, a given NETNAME must always
generate the same terminal-id.  Since we have several NETNAME
conventions, deriving the terminal-id from the NETNAME itself
was difficult.  We also wanted to verify the incoming NETNAME
for security reasons.

The solution we chose was to create a table called a terminal auto-
install table.  It is used to cross-reference the incoming VTAM
NETNAME with the correct terminal-id for the device.  Each
table entry also contains printer and alternative printer-ids for
PRINTTO as well as the name of the terminal model on RDO to
be used for installation.

The auto-install program, and the terminal auto-install table are
maintained and assembled separately.  They are link-edited
together as one load module whose name must be defined in the
PPT (or via RDO).  The auto-install program name is specified in
the AUTINST parameter of the SIT.

Dynamic changes to the auto-install table described above can be
implemented quickly by editing the table source, re-assembling
the table, link-editing it to the auto-install program, and
performing the CEMT NEWCOPY function for the program.

This method of implementing auto-install has proved reliable for
us and has saved a considerable amount of DSA storage.  The
source code for our auto-install program, based on the auto-install
user program DFHZATDX provided by IBM, together with the
macro source for an auto-install table entry and a sample auto-
install table, follows.

DFHZATDX

***        MODULE NAME = DFHZATDX                                  ***
* DESCRIPTIVE NAME = C.I.C.S./VS (VTAM) AUTOINSTALL USER PROGRAM     *
*   TERMINAL-ID AND AUTOINSTALL MODEL TERMINAL NAME ARE DERIVED FROM *
*   A TABLE CALLED TAITABLE BASED ON A MATCH OF INCOMING VTAM        *
*   NETNAME.  THE TAITABLE IS LINKEDITED TO THIS MODULE TO FORM      *
*   THE FINAL VERSION OF DFHZATDX.                                   *
*** BASED ON: IBM AUTOINSTALL USER PROGRAM, DFHZATDX               ***
DFHEISTG DSECT ,
INPARMDS DSECT ,                   DEFINE INSTALL PARAMETER LIST
INPARM0  DS    F                   FUNCTION FIELD
         ORG   INPARM0               DEFINE FUNCTION FIELD
INRQTYPE DS    XL1                   - INSTALL REQUEST TYPE
INPARM02 DS    XL1                   - RESERVED
INPARM03 DS    XL1                   - RESERVED
INPARM04 DS    XL1                   - RESERVED
         ORG   ,
INPARM1  DS    A                   POINTER TO NETNAME
INPARM2  DS    A                   POINTER TO MODEL NAME LIST
INPARM3  DS    A                   POINTER TO RETURN FIELD
INPARM4  DS    A                   POINTER TO CINIT_RU
DLPARMDS DSECT ,                   DEFINE DELETE PARAMETER LIST
DLPARM0  DS    F                   FUNCTION FIELD
         ORG   DLPARM0               DEFINE FUNCTION FIELD
DLRQTYPE DS    XL1                   - DELETE REQUEST TYPE
DLPARM02 DS    XL1                   - RESERVED
DLPARM03 DS    XL1                   - RESERVED
DLPARM04 DS    XL1                   - RESERVED
         ORG   ,
DLTERMID DS    CL4                 TERMID BEING PROCESSED
MODLSTDS DSECT ,                   DEFINE MODEL NAME ENTRY
MODLNUM  DS    XL2                 NUMBER OF ENTRIES IN LIST
MODLNAME DS    CL8                 FIRST ENTRY IN LIST
OUTPRMDS DSECT ,                   DEFINE OUTPUT PARAMETERS
MODNMSEL DS    CL(L'MODLNAME)      MODEL NAME SELECTED
TRMIDSEL DS    CL4                 TERMINAL-ID SELECTED
PRTTOSEL DS    CL4                 PRINTTO SELECTED
ALTPTSEL DS    CL4                 ALTPRT SELECTED
INSTATUS DS    CL1                 STATUS INFORMATION
NETNAMDS DSECT ,                   DEFINE NETNAME FIELD
NETNAMLN DS    XL2                 LENGTH OF NETNAME
NETNAME  DS    CL8                 START OF NETNAME
TAITE    DSECT                     TERMINAL AUTOINSTALL TABLE ENTRY
TAITERM  DS    CL4                 TERMINAL-ID TO SPECIFY
TAINET   DS    CL8                 NETNAME TO SEARCH FOR
TAIMOD   DS    CL8                 TERMINAL MODEL TO SPECIFY
TAIPRTTO DS    CL4                 PRINTTO TERMINAL TO SPECIFY
TAIALTPR DS    CL4                 ALTPRINT TERMINAL
TAITENXT DS    0X                  POINTER TO NEXT TABLE ENTRY
DFHEISTG DSECT
HOLDNET  DS    CL8                 HOLD BLANK FILLED NETNAME FOR...
*                                  ...COMPARISON TO TAINET
DFHZATDX CSECT ,
* *                  I N I T I A L I S A T I O N                     *
         DFHREGS ,                EQUATE REGISTERS
         OC    EIBCALEN,EIBCALEN  ANY COMMAREA?
         BZ    RETURN             ...NO, GET OUT
         L     R2,DFHEICAP        ADDRESS INPUT PARAMETER LIST
         USING INPARMDS,R2        BASE DSECT
         CLI   INRQTYPE,C'0'      INSTALL REQUEST?
         BNE   DELPROC1           ...NO, CHECK DELETE
* *               I N S T A L L  P R O C E S S I N G                 *
         L     R4,INPARM2         ADDRESS MODEL NAME LIST
         USING MODLSTDS,R4        BASE DSECT
         OC    MODLNUM,MODLNUM    ANY MODEL NAMES?
         BZ    RETURN             ...NO, GET OUT
         L     R5,INPARM3         ADDRESS OUTPUT PARAMETER LIST
         USING OUTPRMDS,R5        BASE DSECT
         L     R2,INPARM1         ADDRESS NETNAME INPUT FIELD
         USING NETNAMDS,R2        BASE DSECT
*        REGISTER USAGE:
*          R6 - BASE REGISTER FOR TERM AUTOINSTALL TABLE ENTRY
*          R7 - RUNAWAY LOOP CONTROL COUNT
*          R8 - LENGTH REGISTER FOR EXECUTE
* *      ATTEMPT TO LOCATE NETNAME IN TABLE
         MVC   HOLDNET,=C'        ' INITIALIZE NETNAME PADDING AREA
         L     R6,=V(TAITABLE)     GET ADDRESS OF TAI TABLE
         LH    R8,NETNAMLN         SET NETNAME LENGTH FOR MVC EX
         L     R7,=F'750'          SET MAX LOOPS IF NO EOT MARKER
TAILOOP  EQU   *                   LOOK AT...
         USING TAITE,R6            ...NEXT ENTRY IN TAI TABLE
         CLI   TAINET,X'FF'        END OF TABLE MARKER INDICATED?
         BE    NOTAITE             NETNAME NOT FOUND
         EX    R8,TAIMOVE          MOVE X BYTES OF INCOMING NETNAME
         CLC   HOLDNET,TAINET      COMPARE INCOMING NETNAME TO TAITE
         BE    TAIFOUND            A MATCH, LET'S SET PARMS AND GO!!
         LA    R6,TAITENXT         ELSE, POINT TO NEXT TABLE ENTRY
         BCT   R7,TAILOOP          DECREMENT LOOP COUNT, RE-LOOP!
         B     TABLEEND            MAXED OUT! NETNAME NOT FOUND
TAIMOVE  MVC   HOLDNET(0),NETNAME  MOVE X BYTES OF INCOMING NETNAME
*        NOW THAT NETNAME IS FOUND IN TAI TABLE, SET TERMID AND MODEL
TAIFOUND MVC   MODNMSEL,TAIMOD     SET AUTOINSTALL MODEL NAME TO USE
         MVC   TRMIDSEL,TAITERM    SET TERMINAL-ID TO USE
         MVI   INSTATUS,X'00'      SET GOOD RETURN CODE
         CLI   TAIPRTTO,X'40'      IS PRINTTO TERM SPECIFIED?
         BNH   CHKALTPR            NO, CHECK FOR ALTPRINT
         MVC   PRTTOSEL,TAIPRTTO   YES, SET PRINTTO ID
CHKALTPR CLI   TAIALTPR,X'40'      IS ALTPRINT TERM SPECIFIED?
         BNH   RETURN              NO, RETURN
         MVC   ALTPTSEL,TAIALTPR   YES, SET ALTPRINT ID
         B     RETURN              EXIT
*        SET ERROR CONDITION CODES
NOTAITE  MVI   INSTATUS,X'08'     INDICATE NETNAME IS UNRECOGNIZED
         B     RETURN             EXIT
TABLEEND MVI   INSTATUS,X'0C'     INDICATE END OF TABLE REACHED
         B     RETURN             EXIT
         LTORG
* *                D E L E T E   P R O C E S S I N G                 *
DELPROC1 DS    0H
         USING DLPARMDS,R2        BASE DSECT
         CLI   DLRQTYPE,C'1'      DELETE REQUEST?
         BNE   RETURN             ...NO, ERROR, EXIT
* ==> PUT DELETE CODE HERE
         B     RETURN             EXIT PROGRAM
RETURN   DS    0H
         EXEC CICS RETURN ,
         END   DFHZATDX

TERMINAL AUTO-INSTALL TABLE MACRO SOURCE

         MACRO
&LABEL   TAIDEF &TYPE=,&NETNAME=,&TERMID=,&MODEL=,&PRINTTO=,&ALTPRT=
         AIF   ('&TYPE' EQ 'INITIAL').INITIAL
         AIF   ('&TYPE' EQ 'FINAL').FINAL
         AIF   ('&TYPE' EQ 'TERMINAL').CHKNET
         MNOTE 8,'*** ERROR, TYPE=&TYPE IS INVALID ***'
         AGO   .EXIT
.CHKNET  AIF   ((K'&NETNAME GT 0) AND (K'&NETNAME LT 9)).CHKTERM
         MNOTE 8,'*** NETNAME=&NETNAME IS INVALID ***'
         AGO   .EXIT
.CHKTERM AIF   ((K'&TERMID GT 0) AND (K'&TERMID LT 5)).CHKMODL
         MNOTE 8,'*** ERROR, TERMID=&TERMID IS INVALID ***'
         AGO   .EXIT
.CHKMODL AIF   ((K'&MODEL GT 0) AND (K'&MODEL LT9)).DEFINE
         MNOTE 8,'ERROR, MODEL=&MODEL IS INVALID ***'
         AGO   .EXIT
.*
.DEFINE  ANOP
&LABEL   DC    CL4'&TERMID',CL8'&NETNAME',CL8'&MODEL'
&NETNAME DS    0X    C H E C K   F O R   D U P   N E T N A M E
.*
         AIF   ((K'&PRINTTO GT 0) AND (K'&PRINTTO LT 5)).DEFPRTT
         DC    CL4'    '       D E F A U L T   P R I N T T O
         AGO   .CHKALT
.DEFPRTT DC    CL4'&PRINTTO'   P R I N T T O
.*
.CHKALT  ANOP
         AIF   ((K'&ALTPRT GT 0) AND (K'&ALTPRT LT 5)).DEFALT
         DC    CL4'    '       D E F A U L T   A L T P R I N T
         AGO   .EXIT
.DEFALT  DC    CL4'&ALTPRT'    A L T P R I N T
         AGO   .EXIT
.*
.INITIAL ANOP
TAITABLE CSECT                T A B L E   B E G I N N I N G
         AGO   .EXIT
.FINAL   ANOP
&LABEL   DC    28X'FF'        T A B L E   E N D
.EXIT    ANOP
         MEND

SAMPLE TERMINAL AUTO-INSTALL TABLE

         TAIDEF TYPE=INITIAL    <<< REQUIRED ENTRY
T001     TAIDEF TYPE=TERMINAL,NETNAME=L101T001,TERMID=T001,          X
               MODEL=LU2M4,PRINTTO=P001
T002     TAIDEF TYPE=TERMINAL,NETNAME=L300T002,TERMID=T002,          X
               MODEL=LU2M4,PRINTTO=P002,ALTPRT=P003
LOC1     TAIDEF TYPE=TERMINAL,NETNAME=LOC1,TERMID=LOC1,MODEL=3278M2
GW05     TAIDEF TYPE=TEMINAL,NETNAME=OSCGW05,TERMID=GW05,            X
               MODEL=LU2M2
         TAIDEF TYPE=FINAL     <<< REQUIRED ENTRY
         END


Alan Ferrante
Systems Programmer ) New York State Office of the State Comptroller 1989
New York State Office of the State Comptroller (USA)


