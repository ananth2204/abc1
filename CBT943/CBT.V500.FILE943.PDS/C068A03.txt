CICS console shut-down command

CICS 3.1 introduced a new EXEC CICS command which
allows a program to shut CICS down.  By checking the console
terminal-id to restrict the execution of the shut-down program,
a CICS transaction can now be set up to allow shutting down a
CICS region by entering the following single unsecured
transaction on the system console:

      F jobname,SHUT

The program can also scan the list of tasks that are still
executing, and purge any that will not be terminated by the
PLT.  By including any specific shut-down processing in this
program, it results in fewer SA03 abends and less operator
intervention.

*ASM XOPTS(CICS,SP)
***      SHUTDOWN CICS FROM CONSOLE
*       R3  => BASE REG
*       R4  =  COUNT OF NUMBER OF TASKS IN TASKLIST
*       R6  => LIST OF TASKIDS ON CICS
*       R11 => EIB BASE REG
***     R13 => EISTG BASE REG
SHUTDOWN CSECT
* CHECK TO MAKE SURE THIS IS FROM THE CONSOLE.  IF NOT DON'T EXECUTE.
PREPCEMT EQU   *
         CLC   EIBTRMID,=C'CNSL'   CONSOLE??
         BNE   RETURN              NO, THEN EXIT
* GET LIST OF TASKIDS AND THEN PURGE ALL TASKS OTHER THAN DSNC
         EXEC CICS INQUIRE TASK LIST SET(R6) LISTSIZE(LISTC)
         L     R4,LISTC
CHKTASK  EQU   *
         MVC   TASKID,0(R6)        GET TASKID
         EXEC CICS INQUIRE TASK(TASKID) TRANSACTION(TRAN) RESP(RC)
         CLC   RC,DFHRESP(TASKIDERR)  CHECK IF TASK STILL THERE
         BE    NXTTRAN             NO, THEN GO TO NEXT TASKID
         CLC   TRAN,=C'DSNC'
         BE    NXTTRAN
         CLC   TRAN,=C'SHUT'
         BE    NXTTRAN
         MVC   SPURGE,DFHVALUE(FORCEPURGE)
         EXEC CICS SET TASK(TASKID) PURGETYPE(SPURGE) RESP(RC)
* POINT TO NEXT TASK IN TASKLIST. IF NO MORE TASKS THEN SHUTDOWN CICS.
NXTTRAN  EQU   *
         LA    R6,4(R6)            POINT TO NEXT TASKID
         CLC   0(4,R6),=X'FFFFFFFF'      END OF TABLE?
         BE    PERFSHUT
         BCT   R4,CHKTASK
PERFSHUT EQU   *
         EXEC CICS PERFORM SHUTDOWN
RETURN   EQU   *
         EXEC  CICS RETURN
         LTORG
DFHEISTG DSECT
LISTC    DS    F
TASKID   DS    F
TRAN     DS    F
SPURGE   DS    F
RC       DS    F
         COPY DFHAID
         END


Noland Weisser
Systems Programmer
Omaha Public Power District (USA)                       Noland Weisser 1991

