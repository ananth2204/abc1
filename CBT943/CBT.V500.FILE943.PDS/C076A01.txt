ESDS back-out exit programs under CICS/ESA

For VSAM ESDS datasets, there is no delete function during
transaction and dynamic back-out processing. To back out any
changes, you must use an appropriate exit program to mark the
record which has been added as deleted, according to the logic
of the application.


CICS TABLE ENTRIES FOR ESDS BACK-OUT EXIT PROGRAMS

The following CICS table entries are required.

SIT

         DFHSIT TYPE=CSECT,TBEXITS=(,,XRCFCEX,),...

PLT

         DFHPLT TYPE=INITIAL,SUFFIX=PI
         DFHPLT TYPE=ENTRY,PROGRAM=DFHDELIM
         DFHPLT TYPE=ENTRY,PROGRAM=XDBENA


ENABLING THE DYNAMIC BACK-OUT EXIT PROGRAM

XDBENA enables the dynamic back-out exit program like this:

XDBENA   CSECT
         EXEC CICS ENABLE PROGRAM('XDBFERX') EXIT('XDBFERR') START
         EXEC CICS RETURN
         END


XRCFCEX

XRCFCEX is the transaction back-out exit program used for
resource back-out during emergency restart processing.

FCTDSBAR EQU   R7
FWACBAR  EQU   R9
         COPY  DFHFBODS
         COPY  DFHFCTDS
         COPY  DFHFWADS
         DFHUEXIT TYPE=EP,ID=XRCFCER      GENERATE DSECT DFHUEPAR
XRCFCEX  CSECT
XRCFCEX  AMODE 31
XRCFCEX  RMODE 24
         DS    0H
         STM   R14,R12,12(R13)
         BALR  R8,0
         USING *,R8
         USING DFHUEPAR,R1
         L     R7,UEPFCTE           LOAD ADDRESS OF FCT ENTRY
         USING DFHFCTDS,FCTDSBAR
         TM    FCTDSVR3,X'80'
         BNO   RETURN0
         TM    FCTVSVR1,X'40'       VSAM ESDS ?
         BNO   RETURN0
         L     R3,UEPFBOTE          LOAD ADDRESS OF THE FBO ENTRY
         USING DFHFBODS,R3
         SR    R4,R4
         L     R4,UEPERR
         TM    0(R4),TBFEWA         FILE ERROR WRITE ADD ?
         BNO   RETURN0
         L     R9,UEPFWA            LOAD ADDRESS OF THE FILE WORK AREA
         USING DFHFWADS,FWACBAR
         MVI   20(R9),X'FF'         MARK THE EXISTING RECORD ON THE FILE
         LM    R14,R12,12(R13)                                AS DELETED
         LA    R15,UERCRTRY         REAPPLY THE LOGICALLY DELETED RECORD
         B     0(,R14)                                    TO THE DATASET
RETURN0  LM    R14,R12,12(R13)
         LA    R15,UERCNORM         CONTINUE PROCESSING
         B     0(,R14)
         END   XRCFCEX


XDBFERX

XDBFERX is the exit program used for resource back-out
during dynamic back-out processing.

FCTDSBAR EQU   R7
FWACBAR  EQU   R6
         COPY  DFHFCTDS
         COPY  DFHDBRDS
         COPY  DFHFWADS
         DFHUEXIT TYPE=EP,ID=XDBFERR   GENERATE DSECT DFHUEPAR
XDBFERX  CSECT
XDBFERX  AMODE 31
XDBFERX  RMODE 24
         DS    0H
         STM   R14,R12,12(R13)
         BALR  R5,0
         USING *,R5
         USING DFHUEPAR,R1
         L     R7,UEPFCTE        LOAD ADDRESS OF THE FCT ENTRY
         USING DFHFCTDS,FCTDSBAR
         TM    FCTDSVR3,X'80'
         BNO   RETURN0
         TM    FCTVSVR1,X'40'    VSAM ESDS ?
         BNO   RETURN0
         L     R8,UEPDLOGR       LOAD ADDRESS OF THE DYNAMIC LOG RECORD
         USING DFHDBRDS,R8
         CLC   DBRERRCD,=X'10'   WRITE ADD ERROR ?
         BNE   RETURN0
         L     R6,UEPFWA           LOAD ADDRESS OF THE FILE WORK AREA
         USING DFHFWADS,FWACBAR
         MVC   20(1,R6),=X'FF'     MARK THE EXISTING RECORD ON THE FILE
         LM    R14,R12,12(R13)                               AS DELETED
         LA    R15,UERCRTRY        REAPPLY THE LOGICALLY DELETED RECORD
         BR    R14                                       TO THE DATASET
RETURN0  EQU   *
         LM    R14,R12,12(R13)
         LA    R15,UERCNORM        CONTINUE PROCESSING
         BR    R14
         LTORG
         END   XDBFERX


Gerrit Herlyn
CICS Systems Programmer
TWS Stuttgart (Germany)

