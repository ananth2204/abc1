Duplicate entries in the CSD dataset

Our environment is CICS 2.1 and MVS/ESA.  When we use
CEDA for on-line resource definition, it sometimes happens
that we define resources with the same names as existing
definitions in different groups.  This may create problems when
the group is installed as it overrides the existing definition
attributes of the same resource.  Often it is not desirable to
have such duplicate definitions.  The purpose of the following
program is to read the CSD file and list all the duplicate
resource definitions along with the resource type, group, and
time stamp.  This is used in our installation to help the systems
programmer to identify duplicate definitions and retain
whichever are appropriate.

The CSD file is a VSAM KSDS cluster with a key length of 22
bytes.  The key consists of the following fields:

o      Group/list name (character) - 8 bytes
o      Sequence number (integer) - 4 bytes
o      Resource type (integer) - 2 bytes
o      Resource name (character) - 8 bytes.

The resource type field can have any of the following values:

o      Connection - 500
o      Mapset - 1000
o      Partition set - 2000
o      Profile - 3000
o      Program - 4000
o      Session - 4333
o      Typeterm - 4555
o      Terminal - 4666
o      Transaction - 5000.


PROGRAM TO LIST THE DUPLICATE ENTRIES IN A CSD FILE

RDODUPL   CSECT
          SAVE   (14,12)           SAVE REGS
          LR     12,15             SET UP BASE REG
          USING  RDODUPL,12        ESTABLISH ADDRESSABILITY
          ST     13,SAVE+4         FORWARD POINTER
          LA     11,SAVE
          ST     11,8(13)          BACKWARD POINTER
          LR     13,11             NEW SAVE AREA ADDRESS
          OPEN   RDOACB            OPEN RDO FILE
          LTR    15,15             OPEN OK ?
          BNZ    ERROR1            OPEN ERROR QUIT
          OPEN   (OUTFILE,(OUTPUT))   OPEN OUTPUT
          LTR    15,15             OK ?
          BNZ    ERROR2            OUTPUT OPEN ERROR
          GETMAIN  R,LV=240000     ACQUIRE STORAGE FOR TABLE
          LTR    15,15
          BNZ    ERROR3            STORAGE NOT OBTAINED
          LR     11,1              ADDRESS OF ACQUIRED STORAGE
          LR     10,11             CURRENT ENTRY ADDRESS
          SLR    9,9               COUNT OF ENTRIES OBTAINED
*  READ RECORD
          USING  RESTAB,10         ADDRESSABILTY IN TABLE
NEXTREC   GET    RPL=RDORPL        READ NEXT RECORD
          LTR    15,15             READ NOT OK
          BNZ    ENDDATA           END OF DATA
          LH     2,RDOTYPE         GET TYPE OF ENTRY
          CH     2,=H'500'         NOT RESOURCE DEFINITION ENTRY
          BL     NEXTREC           THEN SKIP IT
          MVC    RESNAME,RDONAME   SAVE ENTRY NAME  IN TABLE
          MVC    RESTYPE,RDOTYPE   SAVE  RESOURCE TYPE IN TABLE
          MVC    RESGROUP,RDOGROUP SAVE GROUP IN TABLE
          MVC    RESDAY,RDODAY     SAVE DATE OF DEFINITION IN TABLE
          LA     10,24(10)         ADDRESS OF NEXT ENTRY
          LA     9,1(9)            COUNT = COUNT + 1
          B      NEXTREC
ENDDATA   LTR    9,9               NO ENTRIES FOUND
          BZ     NORECMSG          NO ENTRIES MESSAGE AND EXIT
          C      9,=F'1'           ONLY ONE
          BE     NODUPMSG          NO DUPLICATE ENTRIES
*  SORT THE TABLE
          LR     8,11              ADDRESS OF TABLE
          LR     6,9               COUNT
          S      6,=F'1'           DECREASE COUNT
SRTLOOP1  LA     7,24(8)           NEXT ENTRY AFTER FIRST
          LR     5,6               LOOP COUNT
SRTLOOP2  CLC    0(24,8),0(7)      COMPARE TWO ENTRIES
          BNH    NOSWAP            THEN NO SWAP
          MVC    TEMP,0(8)         SAVE FIRST
          MVC    0(24,8),0(7)      MOVE SECOND TO FIRST
          MVC    0(24,7),TEMP      SWAP OVER
NOSWAP    LA     7,24(7)           POINT TO NEXT
          BCT    5,SRTLOOP2        LOOP TILL END
          LA     8,24(8)           INCREMENT OUTER ADDRESS
          BCT    6,SRTLOOP1        REPEAT OUTER LOOP
          SLR    8,8               INITIALIZE LINES USED PER PAGE
          LA     7,1               DUPLICATE ENTRY COUNT = 1
          LR     6,11              TABLE ADDRESS
          LR     5,9               COUNT OF ENTRIES
DUPLOOP   CLC    0(10,6),24(6)     COMPARE ADJACENT NAME & TYPE
          BNE    NOTSAME           NOT THE SAME
          MVI    DUPFLAG,C'Y'      SET A FLAG TO INDICATE DUPLICATE
          BAL    14,DUPWRITE       WRITE THE FIRST ENTRY
          LA     6,24(6)           POINT TO NEXT
          BCT    5,DUPLOOP         REPEAT TILL END
          B      OVER
NOTSAME   CLI    DUPFLAG,C'Y'      DUP FLAG SET
          BNE    NOWRITE           THEN DO NOT WRITE LAST ENTRY
          BAL    14,DUPWRITE       ELSE WRITE LAST ENTRY
          LA     7,1(7)            INCREMENT DUPLICATE COUNTER
NOWRITE   MVI    DUPFLAG,C'N'      SET FLAG OFF
          MVI    FIRSTFLG,C'Y'     FIRST ENTRY OF DUPLICATE SET
          LA     6,24(6)           POINT TO NEXT
          BCT    5,DUPLOOP         REPEAT TILL END
OVER      C      7,=F'1'           NO DUPLICATE ENTRIES FOUND
          BE     NODUPMSG
CLOSE1    CLOSE  RDOACB
          CLOSE  OUTFILE
          B      RETURN
NORECMSG  PUT    OUTFILE,MSG2      NO RECORD MESSAGE
          B      CLOSE1
NODUPMSG  PUT    OUTFILE,MSG1      NO DUPLICATE MESSAGE
          B      CLOSE1
ERROR1    WTO    '  ERROR IN OPENING CSD FILE ',ROUTCDE=(1,10),DESC=(6)
          B      RETURN
ERROR2    WTO    '  ERROR IN OPENING OUTPUT   ',ROUTCDE=(1,10),DESC=(6)
          B      RETURN
ERROR3    WTO    '  ERROR IN GETMAIN  ',ROUTCDE=(1,10),DESC=(6)
          B      RETURN
RETURN    L      13,4(13)
          RETURN (14,12),RC=0
* SUBROUTINE TO WRITE DUPLICATE ENTRY
DUPWRITE  ST     14,SAVE14         SAVE RETURN ADDRESS
          CLI    PAGFLAG,C'Y'      NEW PAGE FLAG
          BNE    NOHEADER          IF NOT NO PAGE HEADER
          PUT    OUTFILE,HEADER1   WRITE FIRST HEADER
          PUT    OUTFILE,HEADER2   SECOND HEADER
          MVI    PAGFLAG,C'N'      SET NEW PAGE FLAG OFF
NOHEADER  EQU    *
          MVC    OUTREC,SPACE            INITIALIZE THE RECORD
          CLI    FIRSTFLG,C'Y'           FIRST ENTRY OF DUPLICATE SET
          BNE    SKIP1                   IF NOT SKIP THIS
          PUT    OUTFILE,SPACE           A BLANK SEPARATOR LINE
          LA     8,1(8)                  INCREMENT LINES USED
          CVD    7,DWORD                 SEQ NO
          MVC    OUTSEQ,PATTERN1         PUT THE EDIT PATTERN
          ED     OUTSEQ,DWORD+6          EDIT THE SEQ NUMBER
          MVC    OUTNAME,0(6)            PUT THE NAME
          LA     4,TYPETAB               RESOURCE TYPE TABLE
TYPELOOP  LH     3,8(6)                  RESOURCE TYPE
          C      3,0(4)                  TYPE MATCHES
          BE     TYPMATCH
          LA     4,16(4)                 POINT TO NEXT
          B      TYPELOOP                CHECK NEXT
TYPMATCH  MVC    OUTTYPE,4(4)            MOVE THE RESOURCE TYPE
          MVI    FIRSTFLG,C'N'           SET THE FIRST FLAG OFF
SKIP1     EQU    *
          MVC    OUTGROUP,10(6)
          MVC    DATE+1(3),18(6)         GET DATE PORTION
          OI     DATE+3,X'0F'            SET THE SIGN
          MVC    OUTDATE,PATTERN2
          ED     OUTDATE,DATE+1          EDIT THE DATE
          MVC    TIME,20(6)              TIME PORTION
          L      2,TIME
          SLL    2,4                     REMOVE HIGH 4 BITS
          ST     2,TIME
          MVC    OUTTIME,PATTERN3        EDIT PATTERN
          ED     OUTTIME,TIME            EDIT TIME
          PUT    OUTFILE,OUTREC
          LA     8,1(8)                  LINE COUNT
          C      8,=F'55'                PAGE SIZE
          BL     SKIP2
          MVI    PAGFLAG,C'Y'            SET NEW PAGE FLAG
          MVI    FIRSTFLG,C'Y'           FIRST ENTRY FLAG
          SLR    8,8                     INITIALIZE PAGE SIZE
SKIP2     L      14,SAVE14
          BR     14
*  FILE AND DATA DEFINITIONS
RDOACB    ACB    AM=VSAM,DDNAME=DFHCSD,MACRF=(KEY,SEQ,IN)
RDORPL    RPL    AM=VSAM,ACB=RDOACB,AREA=RDOREC,AREALEN=1100,          X
               OPTCD=(KEY,SEQ,MVE)
OUTFILE   DCB    DDNAME=OUTFILE,DSORG=PS,MACRF=(PM),RECFM=FBA,         X
               LRECL=81,BLKSIZE=81
RDOREC    DS    0F
RDOGROUP  DS    CL8                   GROUP NAME
RDOSEQNO  DS    F                     RECORD SEQUENCE NUMBER
RDOTYPE   DS    H                     ENTRY TYPE
RDONAME   DS    CL8                   RESOURCE NAME
RDOLENG   DS    H                     RECORD LENGTH
RDODAY    DS    CL6                   DATE & TIME IN PACKED DECIMAL
          DS    268F                  REMAINING PORTION OF RECORD
OUTREC    DS    0CL81
          DC    CL1' '                CARRIAGE CONTROL
          DC    CL2' '
OUTSEQ    DS    CL4
          DC    CL4' '
OUTNAME   DS    CL8
          DC    CL4' '
OUTTYPE   DS    CL12
          DC    CL4' '
OUTGROUP  DS    CL8
          DC    CL4' '
OUTDATE   DS    CL7
          DC    CL4' '
OUTTIME   DS    CL9
          DC    CL10' '
HEADER1   DS    0CL81
          DC    CL47'1    NO    NAME        TYPE           GROUP   '
          DC    CL34'     DATE        TIME '
HEADER2   DS    0CL81
          DC    CL47'     --    ----        ----           -----   '
          DC    CL34'     ----        ---- '
SPACE     DC    CL81' '              BLANK LINE
MSG1      DC    CL81'  NO DUPLICATE ENTRIES OBTAINED '
MSG2      DC    CL81'  NO RECORDS FOUND IN CSD FILE  '
TYPETAB   DS    0F                    RESOURCE TYPE TABLE
          DC    F'500'
          DC    CL12'CONNECTION  '
          DC    F'1000'
          DC    CL12'MAPSET      '
          DC    F'2000'
          DC    CL12'PARTITION   '
          DC    F'3000'
          DC    CL12'PROFILE     '
          DC    F'4000'
          DC    CL12'PROGRAM     '
          DC    F'4333'
          DC    CL12'SESSION     '
          DC    F'4555'
          DC    CL12'TYPETERM    '
          DC    F'4666'
          DC    CL12'TERMINAL    '
          DC    F'5000'
          DC    CL12'TRANSACTION'
PAGFLAG   DC    C'Y'
FIRSTFLG  DC    C'Y'
DUPFLAG   DC    C'N'
DATE      DC    F'0'
TIME      DC    F'0'
DWORD     DS    D
TEMP      DS    CL24
PATTERN1  DC    XL4'40202020'
PATTERN2  DC    XL7'4020204B202020'
PATTERN3  DC    XL9'4020207A20207A2020' .
SAVE      DS    18F
SAVE14    DS    F
          LTORG
RESTAB    DSECT                          TABLE TO HOLD DEFINITIONS
RESNAME   DS    CL8
RESTYPE   DS    CL2
RESGROUP  DS    CL8
RESDAY    DS    CL6
          END   RDODUPL


M V George
Systems Programmer
Ministry of the Interior (Qatar)

