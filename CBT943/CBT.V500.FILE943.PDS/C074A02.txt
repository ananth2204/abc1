CICS and system GETVIS

Back in the fall of 1989, our shop migrated to VSE/SP 3.2.0
from VSE 1.3.5.  After that migration it was reported that
CICS programs were being loaded without a NEWCOPY
command being issued.  Investigation into the problem ensued,
during which an article in a prior issue of CICS Update was
found.  The article, CICS and VSE directory entries (October
1988), described the program load process with VSE/SP and
the situations which occur with that process if the system
GETVIS area is too small.  In short, VSE/SP uses what's
called mirror Directory Entries (DEs) to load a program but, if
these mirror DEs are not built because of an inadequate system
GETVIS allocation, the load process loads the program via a
search of the LIBDEF chain (which can result in increased I/O
and/or the loading of CICS programs/maps without the
intervening NEWCOPY).  So, as a solution, our system
GETVIS allocation was increased, and the problem went away.

Well, recently, our two VSE production machines were merged
together to form a single VSE VAE machine running V=R
under VM, and back came the predicament mentioned above.
Obviously, the short-term response was again to increase the
system GETVIS - but, by how much and would that be
enough?  And since there is no indication of when the situation
arises, how would it be known when it happened again?

In an effort to respond satisfactorily to these questions, the
following was decided:

1      To implement a modification to VSE to issue a message to
      the system console when a mirror DE was not built thus
      providing notification of the problem situation.

2      To create a program which would provide some type of
      status information as to system GETVIS usage and
      possible CICS requirements of that area for mirror DEs.  If
      an increase was deemed necessary, the information would
      provide the needed data to calculate that increment.


VSE MODIFICATION

Even though this is a publication for CICS users, it seems
appropriate to mention and show the VSE modification since
the scenario previously mentioned is one that impacts the CICS
environment greatly.  The VSE modification is to the SGDFCH
macro, the main function of which is to issue a message to the
VSE console when a mirror DE cannot be built because a
request for system GETVIS storage could not be satisfied.  The
main reason for this would be that the system GETVIS area is
exhausted.  It's interesting to note that only 80% of the system
GETVIS area is available for mirror DEs and other  'excessive'
requestors (as they are termed).  When that limit is reached,
mirror DEs will not be built even if there is some available
system GETVIS.  The message text contains the phase for
which the mirror DE was to be built, but it's important to
realize that the main thing is the provision of a warning or alert
to the system console that mirror DEs are not being built.
Hence, after the application of the modification, receiving this
message on the system console would be an indication that the
problems associated with not building mirror DEs may be
occurring and that it's time to re-evaluate the system GETVIS
allocation.  The source of the modification to the SGDFCH
macro is presented later in the article.

Note that our shop uses the product DOCS from Smartech
Systems for console support.  A fix to the DOCS software was
required for the VSE modification to function properly.  The
modification was also tested in an environment without a
vendor package for console support and worked without any
problems.


System GETVIS Status Program (XSGVSTAT)

The system GETVIS status program (called XSGVSTAT) is a
CICS Assembler program which runs at CICS initialization
time via the PLT mechanism.  The program counts the number
of non-resident PPT programs/maps and uses that value to
approximate the possible additional system GETVIS
requirements, ie mirror DEs for all non-resident PPT
programs/maps - those that are resident should have had their
mirror DEs already built before PLT time.  From our
investigations, it was found that about 51 mirror DEs could be
built on a 4K GETVIS page.  It should be pointed out that
approximating the possible additional needs of the system
GETVIS area is not a foolproof method since there are many
variables involved.  For instance, every time a CICS
program/map whose characteristics have changed (such as
phase length) is NEWCOPYed, a new mirror DE is built.
Also, non-resident CICS programs/maps, which are not used
during a CICS session, do not require a mirror DE to be built.
So, it can be seen that the number of mirror DEs needed is very
dynamic, and therefore an accurate method of approximation is
not possible.  Program XSGVSTAT also retrieves the value of
the current available system GETVIS for mirror DEs.  Finally,
messages are issued to the system console with the related
information (see below).

Now, using all this data, one can assess the system GETVIS
situation for the CICS environment.  As an example, consider
the following messages appearing on the system console from
the system GETVIS status program at CICS initialization time:

      CICSP1   - NO. OF NON-RESIDENT PPT PROGRAMS/MAPS:   1085
      CICSP1   - REQUIRED SYSTEM GETVIS FOR MIRROR DE:     84K
      CICSP1   - AVAILABLE SYSTEM GETVIS FOR MIRROR DE:     0K

With the above, it can be seen that there is no system GETVIS
available for mirror DEs.  So, as described before, all program
loads for non-resident programs/maps will require a search of
the LIBDEF chain which could lead to the undesirable results
of excessive I/Os and/or CICS programs/maps loaded without a
NEWCOPY.  But, knowing the stated information, one could
calculate a value for the additional system GETVIS needed.
84K of system GETVIS may be needed for mirror DEs
depending on whether all non-resident programs/maps are
loaded during this CICS session.  If CICS is re-cycled every
day, one may only want to account for a percentage of the
possible system GETVIS.  For example, in the case above,
60K may be enough if CICS is brought down at 10.00 pm
every night.  This value, in a sense, represents a 'comfort'
level and may vary from person to person; ie one person may
want to account for the worst while another may want to
account for what is probable.  Once decided and, remembering
that only 80% of system GETVIS is available for mirror DEs,
one would multiply that value by 1.25.  Let's say that the
desired value was 84K.  Multiplying that by 1.25 results in
105K which rounded to a 4K boundary becomes 108K.  This
would be the amount added to the current system GETVIS
allocation - ie the GETVIS parameter of the ASI SVA
command.  It must be stressed that this calculation is an
estimate of the system GETVIS increment needed based on the
comfort level chosen.


Conclusion

Together, the VSE modification and the system GETVIS status
program provide a way of not only finding out about the
problem situation discussed in this article, but also of
calculating additional system GETVIS needs based on
displayed information.  Both have been implemented in our
environment only a short time but have already proved helpful.
Without these enhancements, the VSE program load process
will continue to function.  With them, however, a known
VSE/SP problem area for CICS can be detected and
intelligently resolved.


VSE Modification to SGDFCH Macro

* THIS MODIFICATION TO THE SGDFCH MACRO CHANGES THE CODE SO THAT IF
* A MIRROR DE CANNOT BE BUILT BECAUSE OF INSUFFICIENT SYSTEM GETVIS,
* A MESSAGE IS ISSUED TO THE SYSTEM CONSOLE FOR NOTIFICATION THAT THIS
* SITUATION HAS OCCURRED.  THE OUTPUT OF THE MESSAGE CONTAINS THE
* NOTIFICATION TEXT INCLUDING THE PHASE NAME FOR WHICH THE MIRROR
* DE WAS REQUESTED TO BE BUILT.
// EXEC MSHP,SIZE=600K
 CORRECT 5666-301-06-A43 : AI00003 REV
 AFF MACRO = SGDFCH SUB = E
 REPLACE 579759 : 579759
         BZ    BLDSVA             YES --->                    @AI00003 57975902
         TM    SYSFLAG2,IPLBIT    IPL IN PROGRESS ?           @AI00003 57975903
         BO    BLDNOSVA           YES ---> NO MSG             @AI00003 57975904
         MVC   MSGPHASE,DFWKNAME                              @AI00003 57975905
         LA    R1,SLOGCCB         MSG TO SYSLOG               @AI00003 57975906
         EXCP  (1)                                            @AI00003 57975907
         WAIT  (1)                                            @AI00003 57975908
         B     BLDNOSVA           BRANCH TO PROPER LOCATION   @AI00003 57975909
         CNOP  0,4                ALIGN CCBS TO FULLWORD      @AI00003 57975910
SLOGCCB  CCB   SYSLOG,SLOGCCW                                 @AI00003 57975911
SLOGCCW  CCW   X'09',MSGTXTAR,X'20',L'MSGTXTAR                @AI00003 57975912
MSGTXTAR DS    0CL68                                          @AI00003 57975913
         DC    CL38'*WNG UNABLE TO ALLOCATE SYSTEM GETVIS '   @AI00003 57975914
         DC    CL22'FOR MIRROR DE - PHASE '                   @AI00003 57975915
MSGPHASE DS    CL8                                            @AI00003 57975916
/$
 RESOLVES -
'MOD. FETCH WHEN MIRROR DE NOT BUILT'
/*


System GETVIS Status Program (XSGVSTAT)

         TITLE 'XSGVSTAT --  CICS PLT PROGRAM FOR SYSTEM GETVIS STATUS'
*** CALLED FROM:  CICS PLT PROCESSING
*   THIS PROGRAM IS DESIGNED TO BE RUN AS PART OF THE CICS PLT
*   INITIALIZATION PROCESSING.  THE FUNCTION OF THE PROGRAM IS TO
*   PROVIDE A STATUS OF THE SYSTEM GETVIS AREA AT CICS INITIALIZATION
*   TIME AND THE POSSIBLE REQUIREMENTS OF THAT AREA BY CICS PPT
*   PROGRAMS (NON-RESIDENT) FOR MIRROR DES.  THE PROGRAM ACCOMPLISHES
*   THE TASK BY THE FOLLOWING:
*   1)  FIRST, IT SCANS THE PPT, TOTALLING THE NUMBER OF NON-RESIDENT
*       PROGRAMS AND MAPS; THUS, THE POSSIBLE REQUIREMENTS OF THE
*       SYSTEM GETVIS AREA CAN BE CALCULATED (IE 51 ENTRIES WILL
*       REQUIRE ABOUT 4K OF SYSTEM GETVIS FOR MIRROR DES)
*   2)  SECOND, IT RETRIEVES SYSTEM GETVIS INFORMATION FROM THE
*       SYSTEM GETVIS CONTROL INFORMATION AND CALCULATES AVAILABLE
*       SYSTEM GETVIS.
*   OUTPUT:       INFORMATIONAL MESSAGES TO THE SYSTEM CONSOLE CON-
***               TAINING THE ABOVE INFORMATION.
*        THE FOLLOWING GLOBAL BIT MUST BE SET ON.  THIS INDICATES THAT
*        BOTH COMMAND-LEVEL AND MACRO-LEVEL CODE IS IN USE.
         GBLB  &DFHEIMX
&DFHEIMX SETB  (1)
SYSCOM   SYSCOM                  VSE SYSTEM COMMUNICATION REGION DSECT
MAPGVCTL MAPGVCTL     VSE SYSTEM GETVIS AREA CONTROL INFORMATION DSECT
         COPY  DFHCSADS               CICS/VS COMMON SYSTEM AREA DSECT
         COPY  DFHTCADS                CICS/VS TASK CONTROL AREA DSECT
PPTCBAR  EQU   8
         COPY  DFHPPTDS         CICS/VS PROGRAM PROCESSING TABLE DSECT
DFHEISTG DSECT                   CICS/VS EXECUTE INTERFACE BLOCK DSECT
         DFHEISTG
         DFHTM MF=(D,DIRELEM)
         DFHTM MF=(D,SKTTBLE)
         DFHTM MF=(D,PARMLIST)
         DFHTM MF=(D,TMSTATIC)
XSGVSTAT CSECT ,
         LA    R7,0               INITIALIZE COUNTER TO ZERO
* ASSIGN CICS APPLID FOR MESSAGES
         EXEC CICS ASSIGN APPLID(MSGAPPL)
* GET FIRST PPT ENTRY
         DFHTM TYPE=(GETFIRST,INLINE,(R9),(PPTCBAR)),TABLE=PPT
         USING DFHPPTDS,PPTCBAR   ESTABLISH ADDRESSABILITY TO PPT
PPTLOOP  LTR   PPTCBAR,PPTCBAR    AT END OF TABLE ?
         BZ    MSG1               YES ---> GO ISSUE 1ST MSG
         TM    PPTTLR,PPTPCRES    IS PPT ENTRY RESIDENT ?
         BO    PPTN               YES ---> GET NEXT PPT ENTRY
         LA    R7,1(R7)           ELSE NON-RESIDENT, INCREMENT COUNTER
PPTN     EQU   *
* GET NEXT PPT ENTRY
         DFHTM TYPE=(GETNEXT,INLINE,(R9),(PPTCBAR)),TABLE=PPT
         B     PPTLOOP
MSG1     CVD   R7,DWORD           MOVE TOTAL COUNT TO WORKFIELD
         MVC   MSGTEXT,MSGTXT1    MOVE 1ST MESSAGE TEXT INTO MSG
         ED    MSGTXTAR+50(6),DWORD+5  EDIT TOTAL COUNT INTO MSG
         LA    R1,SLOGCCB         WRITE 1ST MESSAGE TO SYSLOG
         EXCP  (1)
         WAIT  (1)
* CALCULATE APPROXIMATE SYSTEM GETVIS REQUIREMENT & OUTPUT 2ND MSG
         XR    R6,R6              CLEAR REGISTER FOR DIVIDE
         D     R6,APPFACT         DIVIDE TO GET APPROX POSSIBLE REQ
         SLL   R7,2               MULTIPLY BY 4 TO EQUATE PAGES TO K
MSG2     CVD   R7,DWORD           MOVE POSS REQ GETVIS TO WORKFIELD
         MVC   MSGTEXT,MSGTXT2    MOVE 2ND MESSAGE TEXT INTO MSG
         ED    MSGTXTAR+51(4),DWORD+6  EDIT POSS REQ GETVIS INTO MSG
         LA    R1,SLOGCCB         WRITE 2ND MESSAGE TO SYSLOG
         EXCP  (1)
         WAIT  (1)
* CALCULATE AVAILABLE SYSTEM GETVIS & OUTPUT 3RD MSG
         L     R9,X'80'           ADDRESS OF SYSTEM COMM. REGION
         L     R9,IJBSVIS-SYSCOM(R9)  ADDRESS OF SYSTEM GETVIS AREA
         LH    R4,GTVSPGCT-MAPGVCTL(R9)  NUMBER OF CURRENT USED PAGES
         LH    R5,GTVSEXCT-MAPGVCTL(R9)  MAX NUMBER OF PAGES TO BE USED
         CR    R4,R5               AVAILABLE GETVIS PAGES ?
         BL    CALCAVL             YES ---> GO CALCULATE HOW MUCH
         LA    R5,0                ELSE NO AVAILABLE GETVIS
         B     MSG3                GO ISSUE 3RD MSG
CALCAVL  SR    R5,R4               SUBTRACT FOR AVAILABLE GETVIS PAGES
         SLL   R5,2                MULTIPLY BY 4 TO EQUATE PAGES TO K
MSG3     CVD   R5,DWORD            MOVE AVAILABLE GETVIS TO WORKFIELD
         MVC   MSGTEXT,MSGTXT3     MOVE 3RD MESSAGE TEXT INTO MSG
         ED    MSGTXTAR+49(6),DWORD+5  EDIT AVAIL GETVIS INTO MSG
         LA    R1,SLOGCCB          WRITE 3RD MESSAGE TO SYSLOG
         EXCP  (1)
         WAIT  (1)
* RETURN TO CICS
         EXEC CICS RETURN
         LTORG
APPFACT  DC    F'51'              FACTOR F/APPROXIMATION OF GETVIS REQ
MSGTXT1  DS    0CL45
         DC    C'NO. OF NON-RESIDENT PPT PROGRAMS/MAPS: '
         DC    X'402020202120'    EDIT MASK F/TOTAL COUNT OF PGMS/MAPS
MSGTXT2  DS    0CL45
         DC    C'REQUIRED SYSTEM GETVIS FOR MIRROR DE:   '
         DC    X'40202120'        EDIT MASK F/POSS REQ SYSTEM GETVIS
         DC    C'K'
MSGTXT3  DS    0CL45
         DC    C'AVAILABLE SYSTEM GETVIS FOR MIRROR DE:'
         DC    X'402020202120'    EDIT MASK F/AVAILABLE SYSTEM GETVIS
         DC    C'K'
DWORD    DC    D'0'
SLOGCCB  CCB   SYSLOG,SLOGCCW
SLOGCCW  CCW   X'09',MSGTXTAR,X'20',L'MSGTXTAR
MSGTXTAR DS    0CL56
MSGAPPL  DS    CL8
         DC    CL3' - '
MSGTEXT  DS    CL45
    END


Andrew M Jause
Technical Support Analyst
Albany International Corporation (USA)

