 Eliminating the SNT under RACF

Some CICS users rely on the three character operator-id for application
security.  When RACF is used, the SNT is an inconvenient overhead for the
following reasons:

1    New users have to be added into RACF and the SNT.

2    Users who can access several CICS systems need multiple SNT entries, with
the possibility of multiple operator-ids.

3    Large SNTs need high CPU time and elapsed time for the assembly jobs.

The following scheme will show how to eliminate the SNT in a RACF environment.

A TYPE=(ENTRY,DEFAULT) is still required in the SNT.  This assigns the non-
security parameters to each user.


OVERVIEW

The scheme exploits the name field in RACF by using 4 bytes for storing the
operator-id.  I used an '@' as the first character of the name field, and this
serves as an indication that an operator-id follows.  This technique prevents
the use of bad data as the operator-id.  The three characters following the '@'
make up the operator-id itself, and the remaining 16 characters are used for the
user's name.  The CSSN and CESN transaction definitions are changed so they
point to a different program - DFHGMMZZ in my implementation.  In my case, CSGM
(good morning) was also changed to use the same program; this puts the user into
a sign-on screen rather than the good-morning logo and speeds entry into CICS.

The main program links to a second program - DFHSNPZZ.  The DFH prefix was used
to avoid interception by monitoring products such as INTERTEST if terminal
monitoring is left in operation.


ENVIRONMENT

The programs were written and implemented on an IBM 3090 running MVS/XA, CICS
1.7, and RACF 1.7.


DETAILED DESCRIPTION


Program 1 (DFHGMMZZ)

This is a command-level Assembler program, linked with AMODE (31) because the
RACF Accessor Environment Element (ACEE), RACF's control block, is above the
16MB line.


Program 2 (DFHSNPZZ)

This is a macro-level Assembler program.  The program entered initially is
program 1 (DFHGMMZZ), and addressability is established for the TCA, CSA, ACEE,
and SNT DSECTs.

It links to program 2 (DFHSNPZZ) which links to DFHSNP after checking the
transaction code.  If the transaction code is CSSN or CESN, no further action is
required.  If a transaction such as CSGM was the originating transaction, the
TCA and TIOA codes are changed to CSSN.  This is necessary so that DFHSNP is
fooled into working normally.

On return from DFHSNP, program 2 returns to program 1.  At this point program 1
addresses the TCTTE via the CSA and current TCA pointer.  If the terminal status
 is 'not attended', the sign-on failed and no further action is required.  In my
case, the default entry in the SNT is set up with OPID=RAC.  If the TCTTE
operator-id is not 'RAC', no further action is required.  This caters for a
mixed RACF/non-RACF situation in the SNT.  Three blanks are treated in the same
way as 'RAC', ie as a candidate for replacement by the RACF operator-id.

The TCTTE is used to address the pseudo SNT entry, and this in turn is used to
address the ACEE.  The ACEE is used to address the user name string, and if no
operator-id is present (byte 1 not '@'), the program returns.  If an operator-id
is present, it is used to replace the TCTTE operator-id and the program returns
to CICS.

SUMMARY

This scheme uses two small programs and the change is transparent to the user,
unless you use the CSGM option.  The operator-id is maintained in one central
(secure) place by RACF.  Conversion can be easily implemented by writing a
simple program to read the SNT source.  I generated ALTUSER statements which
were processed by TSO in batch.  All name fields were changed in one fell swoop,
but this relies on the name being in the SNT entry as a comment.


PROGRAM 1

DFHEISTG   DSECT
           COPY DFHCSADS
           DFHTCA CICSYST=YES
TCTTEAR    EQU  7
           COPY DFHTCTTE
           IHAACEE
           DFHSNT TYPE=DSECT
TIOABAR    EQU  6
           COPY DFHTIOA
TIOASTRT   DS   CL11
DFHGMMZZ   CSECT
           BALR  10,0
           USING *,10
           DROP  12
           USING TCASYAA,3                   TCA ADDRESSABILITY
           USING DFHCSADS,4                  CSA ADDRESSABILITY
           USING ACEE,5                      ACEE ADDRESSABILITY
           USING DFHSNNT,6                   PSEUDO SNT ADDRESSABILITY
           EXEC  CICS LINK PROGRAM('DFHSNPZZ')
           SR    TCTTEAR,TCTTEAR
           EXEC  CICS ADDRESS CSA(4)
           SR    3,3
           ICM   3,B'0111',CSACDTA+1            -> CURRENT TCA
           ICM   TCTTEAR,B'0111',TCAFCAAA+1     -> TCTTE
           TM    TCTTETS,TCTTESTA               ATTENDED ?
           BNO   RETURN
           CLC   TCTTEOI,=C'RAC'                DEFAULT OPID
           BE    CONTINUE                       YES..OK TO REPLACE
           CLC   TCTTEOI,=C'   '                OPID ?
           BNE   RETURN                         YES...DO NOT REPLACE
CONTINUE   SR    6,6
           ICM   6,B'0111',TCTTESNT+1           -> PSEUDO SNT
           LTR   6,6
           BZ    RETURN
           SR    5,5
           ICM   5,B'1111',SNNTUBLK           -> ACEE (USER SEC BLOCK)
           LTR   5,5
           BZ    RETURN
           SR    8,8
           ICM   8,B'1111',ACEEUNAM           -> USER NAME STRING
           LTR   8,8
           BZ    RETURN
            CLI   1(8),C'@'                    OPID PRESENT ?
           BNE   RETURN
           MVC   TCTTEOI,2(8)                 OPID FROM USER NAME
RETURN     EXEC  CICS RETURN
           LTORG
           END


PROGRAM 2

           COPY  DFHCSADS
           DFHTCA CICSYST=YES
TCTTEAR    EQU   7
           COPY  DFHTCTTE
TIOABAR    EQU   6
           COPY  DFHTIOA
TIOASTRT   DS    CL4
DFHSNPZZ   CSECT
           BALR  10,0
           USING *,10
           USING DFHSYTCA,2
           SR    TCTTEAR,TCTTEAR
           L     TCTTEAR,TCAFCAAA              --> TCTTE
           L     2,TCASYAA                     --> TCA
           CLC   TCAKCOID,=C'CSSN'             CSSN TRANSACTION ?
           BE    CONTINUE                      YES...STAY WITH THIS
           CLC   TCAKCOID,=C'CESN'             CESN TRANSACTION ?
           BE    CONTINUE                      YES...STAY WITH THIS
           MVC   TCAKCOID,=C'CSSN'             NO...REPLACE WITH CSSN
           DFHSC TYPE=GETMAIN,CLASS=TERMINAL,NUMBYTE=4
           L     TIOABAR,TCASCSA
           ST    TIOABAR,TCTTEDA
           MVC   TIOATDL(2),=H'4'
           MVC   TIOASTRT(4),=C'CSSN'         SET UP TIOA WITH CSSN
           DFHTC TYPE=ERASE
CONTINUE   DFHPC TYPE=LINK,PROGRAM=DFHSNP  LINK TO REGULAR SIGN-ON PGM
RETURN     DFHPC TYPE=RETURN
           LTORG
           END

Clive Edwards
Senior Systems Programmer
Executive Life Insurance Company (USA)  ) Xephon 1987























