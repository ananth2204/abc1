Automatically switching dump datasets

The SET DUMP AUTOSWITCH facility of the CEMT
transaction can be executed to activate the automatic switching
of dump datasets when the active dataset becomes full.  When
a switch occurs, AUTOSWITCH is de-activated.  Use of
automatic switching is limited by the requirement that it has to
be activated manually.

Using the programmable interface to the master terminal
transaction, the following program (SYSDUMP) takes
automatic dump switching two steps further.  SYSDUMP is
initially invoked as a PLTPI program and issues EXEC CICS
START commands to restart itself at regular intervals (30
minutes is used here).

SYSDUMP issues the INQUIRE DUMP master terminal
command to determine the status of the current dump dataset.
If AUTOSWITCH is not active, SYSDUMP issues the SET
DUMP AUTOSWITCH command to activate it.

The active dump dataset name (actually, only the last
character, A or B) from the INQUIRE DUMP command is
saved for use in detecting a dump dataset switch.  When a
switch is detected, the inactive dump dataset is de-allocated
and a new GDG dataset is allocated using dynamic
allocation/de-allocation software (DADS/PLUS from On-Line
Software is used here).

This program is currently running with CICS 1.7 and
MVS/XA, but should work with other current CICS releases
and operating systems.  The program can be run above the 16-
megabyte line with MVS/XA.

The required RDO and table definitions are:

    CEDA DEFINE GROUP(yourgroup) TRansaction(SDMP)
            PROGram(SYSDUMP)
    CEDA DEFine Group(yourgroup) PROGram(SYSDUMP)
            Language(Assembler) RESident(YES)

with the following entry in the PLTPI table:

        DFHPLT TYPE=ENTRY,PROGRAM=SYSDUMP

For DADS/PLUS, the following should be specified in the Add
Queue DMPx, where x is A or B.  Both A and B should be
defined.

    DDNAME=DFHDMPx  DSNAME=your.dsn  MEMBER= + 1
    DISP=(NEW,CATLG,CATLG)  DSCB=your.model
    SPACE=(xxx,(nn,nn,0),RLSE)  UNIT=SYSDA
    ALLOCATE IF NOT IN DCT=Y

SYSDUMP

         MACRO
&NAME    ASMID &BRANCH=YES,
         PUSH PRINT
         PRINT ON,GEN
* Identify assembly.
         AIF   ('&BRANCH' NE 'YES').NOBRNCH
         B     ASMIDX
.NOBRNCH ANOP
ASMID    DC    C'&SYSECT ASSEMBLED ON &SYSDATE AT &SYSTIME.'
ASMIDX   DS   0H
         POP   PRINT
         MEND
SYSDUMP  TITLE 'automated switch of dump datasets'
         DFHREGS
SYSDUMP  CSECT
         ASMID
         MVC   CEMTCMD(L'CEMTCMDD),CEMTCMDD  get current dump status
         L     R1,=A(L'CEMTCMDD)             .
         STH   R1,CEMTLEN                    .
         MVI   CEMTIND,X'00'                 .
         L     R1,=A(L'CEMTOUT)              .
         STH   R1,CEMTOUTL                   .
         LA    R14,CEMTCMD                   .
         LA    R15,CEMTLEN                   .
         LA    R0,CEMTIND                    .
         LA    R1,CEMTOUT                    .
         LA    R2,CEMTOUTL                   .
         STM   R14,R2,CEMTPARM               .
         EXEC CICS LINK                                                C
               PROGRAM('DFHEMTA')                                      C
               COMMAREA(CEMTPARM)
         CLC   TRNRC,=H'0'              check return codes
         BNE   ERR1                     .
         CLC   EXERC,=H'0'              .
         BNE   ERR1                     .
         EXEC CICS ASKTIME ABSTIME(TIME)
         EXEC CICS FORMATTIME ABSTIME(TIME)                            X
               DATE(MSGDATE) DATESEP                                   X
               TIME(MSGTIME) TIMESEP
         MVI   MSGNUM,C'1'              set-up and write message
         MVC   MSGSTAT,=C'CURRENT'      .
         MVC   MSGSTUFF,MSGDATA         .
         EXEC CICS WRITEQ TD                                           X
               QUEUE('CSMT')                                           X
               FROM(MESSAGE)                                           X
               LENGTH(58)
         CLI   ACTDUMPD,C'A'            validate dump dataset char
         BL    ERR2                     .
         CLI   ACTDUMPD,C'B'            .
         BH    ERR2                     .
         CLC   DUMPDSTA,=C'CLO'         validate status
         BE    SWITCH                   .
         CLC   DUMPDSTA,=C'OPE'         .
         BNE   ERR3                     .
         CLC   DUMPASWI,=C'   '         .
         BE    SETAUTO                  .
         CLC   DUMPASWI,=C'AUT'         .
         BNE   ERR4                     .
         CLI   PREVDUMP,C' '            ok, has dataset switched?
         BE    RETURN                   .
         CLC   PREVDUMP,ACTDUMPD        .
         BE    RETURN                   no, we are done...
         B     ARCHIVE                  yes, archive inactive dataset
SWITCH   DS   0H
         MVC   CEMTCMD(L'CEMTOPEN),CEMTOPEN  switch 'em
         L     R1,=A(L'CEMTOPEN)             .
         STH   R1,CEMTLEN                    .
         MVI   CEMTIND,X'00'                 .
         L     R1,=A(L'CEMTOUT)              .
         STH   R1,CEMTOUTL                   .
         LA    R14,CEMTCMD              build address list for CEMT
         LA    R15,CEMTLEN                   and call program
         LA    R0,CEMTIND               .
         LA    R1,CEMTOUT               .
         LA    R2,CEMTOUTL              .
         STM   R14,R2,CEMTPARM          .
         EXEC CICS LINK                                                C
               PROGRAM('DFHEMTA')                                      C
               COMMAREA(CEMTPARM)
         CLC   TRNRC,=H'0'              check return codes
         BNE   ERR1                     .
         CLC   EXERC,=H'0'              .
         BNE   ERR1                     .
SETAUTO  DS   0H
         MVC   CEMTCMD(L'CEMTAUTO),CEMTAUTO  no, set it
         L     R1,=A(L'CEMTAUTO)             .
         STH   R1,CEMTLEN                    .
         MVI   CEMTIND,X'00'                 .
         L     R1,=A(L'CEMTOUT)              .
         STH   R1,CEMTOUTL                   .
         LA    R14,CEMTCMD              build address list for CEMT
         LA    R15,CEMTLEN                   and call program
         LA    R0,CEMTIND               .
         LA    R1,CEMTOUT               .
         LA    R2,CEMTOUTL              .
         STM   R14,R2,CEMTPARM          .
         EXEC CICS LINK                                                C
               PROGRAM('DFHEMTA')                                      C
               COMMAREA(CEMTPARM)
         CLC   TRNRC,=H'0'              check return codes
         BNE   ERR1                     .
         CLC   EXERC,=H'0'              .
         BNE   ERR1                     .
         MVI   MSGNUM,C'2'              set-up and write message
         MVC   MSGSTAT,=C'REVISED'      .
         MVC   MSGSTUFF,MSGDATA         .
         EXEC CICS WRITEQ TD                                           X
               QUEUE('CSMT')                                           X
               FROM(MESSAGE)                                           X
               LENGTH(58)
         CLI   PREVDUMP,C' '            is it initial entry?
         BE    RETURN                   yes, bypass archive.
ARCHIVE  DS   0H                        archive inactive dump dataset
         MVC   INACTDMP,=C'DMPB    '
         CLI   ACTDUMPD,C'B'            if active dump is 'B',
         BNE   *+8
         MVI   INACTDMP+3,C'A'          then inactive dump is 'A'
         EXEC CICS ASKTIME ABSTIME(TIME)
         EXEC CICS FORMATTIME ABSTIME(TIME)                            X
               DATE(MSGDATE) DATESEP                                   X
               TIME(MSGTIME) TIMESEP
         MVI   MSGNUM,C'3'              set-up and write message
         MVC   MSGSTAT,=C'ARCHIVE'      .
         MVC   MSGSTUFF(4),INACTDMP     .
         EXEC CICS WRITEQ TD                                           X
               QUEUE('CSMT')                                           X
               FROM(MESSAGE)                                           X
               LENGTH(39)
         MVI   PICAFUNC,C'D'            de-allocate.
         MVI   PICATYPE,C'Q'            transient data queue.
         MVC   PICANAME,INACTDMP        inactive dump dataset.
         EXEC CICS LINK PROGRAM('DADSAPI') COMMAREA(PICACOMM)          X
               LENGTH(=AL2(PICALEN))
         CLC   PICARETC,=H'0'           request successful?
         BE    ALLOCATE                 yes, ok
         CLC   PICARETC,=H'4'           request successful w/ warning?
         BNE   ERR5                     no, error...
ALLOCATE DS   0H
         MVI   PICAFUNC,C'A'            allocate.
         MVI   PICATYPE,C'Q'            transient data queue.
         MVC   PICANAME,INACTDMP        inactive dump dataset.
         EXEC CICS LINK PROGRAM('DADSAPI') COMMAREA(PICACOMM)          X
               LENGTH(=AL2(PICALEN))
         CLC   PICARETC,=H'0'           request successful?
         BE    RETURN                   yes, ok
         CLC   PICARETC,=H'4'           request successful w/ warning?
         BNE   ERR5                     no, error...
RETURN   DS   0H
         MVC   PREVDUMP,ACTDUMPD        save dump dataset char
         EXEC CICS START                                               X
               INTERVAL(003000)                                        X
               TRANSID('SDMP')                                         X
               REQID('SYSDUMP')
RETURNX  DS   0H
         EXEC CICS RETURN
ERR1     DS   0H
         EXEC CICS ABEND ABCODE('MSTR')
         B     RETURNX
ERR2     DS   0H
         EXEC CICS ABEND ABCODE('DUMP')
         B     RETURNX
ERR3     DS   0H
         EXEC CICS ABEND ABCODE('STAT')
         B     RETURNX
ERR4     DS   0H
         EXEC CICS ABEND ABCODE('AUTO')
         B     RETURNX
ERR5     DS   0H
         EXEC CICS ABEND ABCODE('DADS')
         B     RETURNX
CEMTCMDD DC    C'CEMT INQ DUMP'
CEMTOPEN DC    C'CEMT SET DUMP SWI AUTO'
CEMTAUTO DC    C'CEMT SET DUMP AUTO'
PREVDUMP DC    CL1' '                   previous active dump dataset
MESSAGE  DS   0CL58
         DC    CL7'SYSDUMP'
MSGNUM   DS    CL1
         DC    CL1' '
MSGSTAT  DS    CL7
         DC    CL1' '
MSGDATE  DS    CL8
         DC    CL1' '
MSGTIME  DS    CL8
         DC    CL1' '
MSGSTUFF DS    CL23
DFHEISTG DSECT
CEMTPARM DS    5A
LKREGL01 DS    A
CEMTLEN  DS    H
CEMTOUTL DS    H
CEMTIND  DS    XL1
CEMTCMD  DS    CL80
         DS    0D
CEMTOUT  DS    0CL80
MSGDATA  EQU   CEMTOUT+16,23
         DS    H
TRNMSG#  DS    H                        number of msgs from translate
TRNRC    DS    H                        translation stage return code
         DS    H
EXELINES DS    H                        number of lines from exec phase
EXERC    DS    H                        execution phase return code
         DS    CL16
ACTDUMPD DS    CL1                      active dump dataset character
         DS    XL3
DUMPDSTA DS    CL3                      dump dataset status (OPE/CLO)
         DS    XL1
DUMPASWI DS    CL3                      dump auto switch status (AUT)
         DS    XL41
INACTDMP DS    CL8                      inactive dump dataset name
TIME     DS    F
         COPY  DADPCOMX (supplied as dadpcomm, 'x' excludes DSECT)
         END


Ralph Robison
Analyst, System Software
Corning Inc (USA) ) Corning Inc 1989


