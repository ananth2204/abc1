 CICS terminal status display
INTRODUCTION
It is often necessary to determine the status of a terminal or group of
terminals in a CICS network.  The master terminal transaction (CEMT) can be used
to display certain information but it has drawbacks.  CEMT limits the average
display to nine terminals per page, so looking at even a modest network can
require some effort.
I decided to write a simple CICS transaction to display the following
information for each terminal: the terminal-id, whether the terminal is in-
service or out-of-service to CICS, whether the terminal is in-session or not-in-
session with VTAM, the transaction-id (if one is active), and the operator-id
(if an operator is signed-on via CSSN/CESN).  BMS mapping is used and a maximum
of 84 terminals are displayed per page.  The BMS trailer on the last page
contains the transaction-id rather than a paging command, allowing the
transaction to be restarted ad nauseam.  Simply hitting the clear key ends the
process.
Each terminal is represented using the following format:
     TERM   T/S   TRAN OPR
     tttt   w/x   yyyy zzz
where tttt is the terminal-id, w is TIU for in service to CICS or TOU for out of
service, x is T+U for in session with VTAM or T-U for not in session, yyyy is
the transaction-id, and zzz is the operator-id.
CONCLUSION
You can take a look at the status of many terminals quickly and can tell at a
glance if there are terminals that need to be varied active to VTAM.  You can
also spot potential CICS problems.  An unusually high number of active
transactions might indicate that a required system resource is not available.
This program can be useful to system operators or Help Desk personnel on small-
to medium-sized networks.  It builds pages for all terminals with existing
TCTTEs, so it wouldnUt be appropriate for a very large network.
**********************************************************************
*        THIS PGM DISPLAYS THE STATUS OF TERMINALS KNOWN TO CICS     *
*        INCLUDING TERMID, INSERV/OUTSRV, IN SESSION/NOT IN SESSION, *
*        OPERATOR ID'S AND ACTIVE TRANSACTION ID'S. MORE THAN ONE    *
*        PAGE IS DISPLAYED THROUGH STANDARD BMS PAGING COMMANDS      *
*        DATASETS USED:  NONE                                        *
*        MAPS USED:      MAPSET=SPSTATM MAP=SMAP  (INTERNAL)         *
*        ENVIRONMENT:    MVS/370  CICS 1.7  VTAM 2.2                 *
*        TYPE:           CICS MACRO LEVEL                            *
*        TWASIZE:        ZERO                                        *
**********************************************************************
SAVEADDR EQU   0                           .SAVE TCTTE ADDRESS
TCTTEAR  EQU   2                           .ADDRESS TCTTE
TIOABAR  EQU   3                           .ADDRESS TIOA
PCTCBAR  EQU   4                           .ADDRESS PCT
BASEREG  EQU   5                           .PROGRAM BASE REGISTER
TRALADDR EQU   10                          .SAVE END OF TIOA ADDRESS
WKREG    EQU   15                          .MISC WORK REGISTER
         COPY DFHCSADS
         DFHTCA CICSYST=YES
         DFHTCTZE CICSYST=YES
         COPY DFHPCTDS
         COPY DFHTIOA
*        SYMBOLIC MAP
         DS    0H                     ENSURE ALIGNMENT
SMAPO DS    0C .
         DS    CL2 .                    INPUT DATA FIELD LENGTH
LINE01A    DS    C .                      DATA FIELD ATTRIBUTE
LINE010    DS    CL79 .                DATA FIELD
         DS    CL2 .                    INPUT DATA FIELD LENGTH
LINE02A    DS    C .                      DATA FIELD ATTRIBUTE
LINE020    DS    CL79 .                DATA FIELD
                 .
     and similarly for LINE03 to LINE20
                 .
         DS    CL2 .                    INPUT DATA FIELD LENGTH
 LINE21A    DS    C .                      DATA FIELD ATTRIBUTE
LINE210    DS    CL79 .                DATA FIELD
         DS    CL2 .                    INPUT DATA FIELD LENGTH
TRAILA    DS    C .                      DATA FIELD ATTRIBUTE
TRAILO    DS    CL6 .                DATA FIELD
SMAPE  EQU   *                     END OF MAP DEFINITION
          ORG
SPSTATT EQU *       * END OF MAP SET
MAPLEN   EQU   SMAPE-SMAPO
*        DUMMY TERMINAL DISPLAY ENTRY
STATAB   DSECT
         USING STATAB,R11
TERM     DS    CL5
         DS    CL1
TST      DS    CL2
LST      DS    CL1
         DS    CL1
TRANS    DS    CL4
         DS    CL1
OPR      DS    CL3
STAT     CSECT
         BALR  BASEREG,0
         USING *,BASEREG
*        GET A TERMINAL OUTPUT AREA INITIALIZED TO LOW VALUES        *
GETMAIN  DFHSC TYPE=GETMAIN,CLASS=TERMINAL,INITIMG=00,NUMBYTE=MAPLEN
         L     TIOABAR,TCASCSA             .GET TIOA ADDRESS
         LA    R11,LINE01O                 .OUTPUT AREA - BEGIN ADDR
         LA    TRALADDR,TRAILA             .OUTPUT AREA - ENDING ADDR
         LA    R1,4                        .NUMBER ENTRIES PER LINE
*        LOOP THROUGH TCTTE'S AND BUILD OUTPUT PAGES                 *
         XC    TCATPTA(4),TCATPTA          .INDICATE FIRST TCTTE
TERMLOOP CR    R11,TRALADDR                .COMPARE CURRENT TO END
         BH    PAGEBLD                     .FULL SCREEN, BMS PAGE BUILD
         DFHTC CTYPE=LOCATE,TERM=NEXT,LASTTRM=ENDIT,ERROR=LOCERR
         L     TCTTEAR,TCATPTA             .GET ADDR OF TCTTE
MOVTERM  MVI   TERM,X'40'                  .1ST BYTE IS BLANK
         MVC   TERM+1(4),TCTTETI           .MOVE TERM ADDRESS
         TM    TCTTETS,TCTTESOS            .TERM OUT OF SERVICE?
         BZ    TINS                        .NOPE-
         MVC   TST,=C'O|'                  .INDICATE TERM OUT SERVICE
         B     CKSESS                      .CHECK IF TERM IN SESSION
TINS     MVC   TST,=C'I|'                  .TERM IN SERVICE
CKSESS   TM    TCTEILOS,TCTENIS            .TERMINAL IN SESSION?
         BO    INSESS                      .YES-->
         MVI   LST,C'-'                    .NOT IN SESSION (RELEASED)
         B     MOVOPI
INSESS   MVI   LST,C'+'                    .IN SESSION (ACQUIRED)
MOVOPI   MVC   OPR(3),TCTTEOI              .MOVE OPID
CKTRAN   L     WKREG,TCTTECA               .LOAD TCA POINTER
         LTR   WKREG,WKREG                 .IS TRANSACTION ACTIVE?
         BNP   NOTASK                      .NO, DON'T DISPLAY TRANS
         L     WKREG,TCASYAA-DFHTCADS(WKREG)
         L     PCTCBAR,TCATCPC-DFHTCADY(WKREG)
         MVC   TRANS(4),PCTTI              .MOVE TRANSID
         TR    TRANS(4),TRTAB              .REMOVE ILLEGAL CHARACTERS
NOTASK   LA    R11,20(0,R11)       .BUMP R11 TO NEXT OUTPUT AREA
         BCT   R1,TERMLOOP         .R1 > 0?  IF SO, NEXT TERMINAL
         LA    R11,2(0,R11)        .ADJ 2 BYTES FOR NEWLINE (82-80=2)
*        LINE=79 + ATTRB=1 + LENGTH=2  MINUS (STATAB=21 X 4 PER LINE) *
         LA    R1,4                        .NUMBER OF ENTRIES PER LINE
         B     TERMLOOP                    .NEXT TERMINAL
PAGEBLD  LR    SAVEADDR,TCTTEAR            .STORE BUMPED TCTTE ADDRESS
         L     TCTTEAR,TCAFCAAA            .ADDRESS OUR TCTTE
         ST    TIOABAR,TCTTEDA             .ADDRESS OF TIOA
         LA    WKREG,MAPLEN                .GET MAP LENGTH
         STH   WKREG,TIOATDL               .STORE LENGTH
          DFHBMS TYPE=(PAGEBLD,STORE,SAVE,ERASE),DATA=YES,MAP=SMAP,     X
               MSETADR=SPSTATM,ERROR=PAGERR
         LR    TCTTEAR,SAVEADDR            .RESTORE BUMPED -
         ST    TCTTEAR,TCATPTA             .     TCTTE ADDR
         LA    R11,LINE01O                 .OUTPUT AREA BEGIN ADDR
         LA    R1,4                        .NUMBER OF ENTRIES PER LINE
         LA    R6,TIOADBA                  .ADDR OF RECEIVING FIELD
         LA    R7,MAPLEN                   .LENGTH OF RECEIVING FIELD
         SR    R8,R8                       .ADDR OF SENDING FIELD
         SR    R9,R9                       .LENGTH OF SENDING FIELD
         MVCL  R6,R8                       .CLEAR OUPUT AREA TO X'00'
         B     TERMLOOP
ENDIT    MVC   TRAILO,=C'STAX  '           .LAST TRAILER HAS TRANSID
         L     TCTTEAR,TCAFCAAA            .ADDRESS OUR TCTTE
         ST    TIOABAR,TCTTEDA
         LA    WKREG,MAPLEN
         STH   WKREG,TIOATDL
         DFHBMS TYPE=(PAGEBLD,STORE,ERASE),DATA=YES,MAP=SMAP,          X
               MSETADR=SPSTATM,ERROR=PAGERR
         DFHBMS TYPE=PAGEOUT,CTRL=(PAGE,RELEASE),EODPURG=AUTO,         X
               ERROR=PAGERR
*        RETURN TO CICS                                              *
         DFHPC TYPE=RETURN
*        ERROR ROUTINES                                              *
LOCERR   DFHPC TYPE=ABEND,ABCODE=LOCE
PAGERR   DFHPC TYPE=ABEND,ABCODE=PAGE
*        TRANSID TRANSLATE TABLE                                     *
TRTAB    DC    74C' '
         DC    XL07'4A4B4C4D4E4F50'
         DC    09C' '
         DC    XL08'5A5B5C5D5E5F6061'
         DC    08C' '
         DC    XL06'6A6B6C6D6E6F'
         DC    09C' '
         DC    XL07'797A7B7C7D7E7F'
         DC    01C' '
         DC    XL09'818283848586878889'
         DC    07C' '
         DC    XL09'919293949596979899'
         DC    07C' '
         DC    XL09'A1A2A3A4A5A6A7A8A9'
         DC    22C' '
         DC    XL10'C0C1C2C3C4C5C6C7C8C9'
         DC    06C' '
         DC    XL10'D0D1D2D3D4D5D6D7D8D9'
         DC    08C' '
         DC    XL08'E2E3E4E5E6E7E8E9'
         DC    06C' '
         DC    XL11'F0F1F2F3F4F5F6F7F8F9FA'
         DC    05C' '
*        END   TRTAB
         LTORG
SPSTAT   DFHMSD LANG=ASM,MODE=OUT,TERM=3270-2,CTRL=FREEKB,TYPE=MAP
SMAP     DFHMDI LINE=1,COLUMN=1,SIZE=(24,80)
         DFHMDF POS=(02,01),LENGTH=39,                                 X
               INITIAL=' TERM T/S TRAN OPR   TERM T/S TRAN OPR '
         DFHMDF POS=(02,41),LENGTH=39,                                 X
               INITIAL=' TERM T/S TRAN OPR   TERM T/S TRAN OPR '
LINE01   DFHMDF POS=(03,01),LENGTH=79
LINE02   DFHMDF POS=(04,01),LENGTH=79
                 .
     and similarly for LINE03 to LINE20
                 .
LINE21   DFHMDF POS=(23,01),LENGTH=79
TRAIL    DFHMDF POS=(24,72),LENGTH=06,ATTRB=(UNPROT,BRT,IC,FSET),      X
               INITIAL='P/N   '
          DFHMDF POS=(24,79),LENGTH=01
         DFHMSD TYPE=FINAL
         END


Rick Widdifield
Senior Systems Programmer
South Carolina Dept of Mental Health (USA)   ) SC Dept of Mental Health 1988


























































