 Printing screens from mapset load modules
I was very interested in the Printing screens from mapset load modules program
by Mike Lamkins, which appeared in the April 1988 issue.  Since we are a VSE
shop running Release 1.7 of CICS, I had to modify the program a little.  This
program should work with Release 1.6 maps as well.  The map header is longer and
the extended attribute indicator seems to be in a different location on Release
1.7 maps.
MSFAX    CSECT
         BALR  12,0
         USING *,12
BEGIN    LR    2,1                 SAVE PARAMETER LIST ADDRESS
         L     2,0(2)              GET THE ADDRESS OF PARAMETER
         LA    3,MAPSET            POINT R3 TO THE MAPSET NAME
*** GET THE PARAMETER VALUE ***
         SR    5,5                 CLEAR R5
         LH    5,0(2)              GET THE LENGTH OF THE PARAMETER
         LTR   5,5                 CHECK IF NO PARAMETER GIVEN
         BZ    RETURN              NO PARAMETER, NO NEED TO PROCEED
         SR    4,4                 CLEAR R4
         LA    4,8(3)              POINT R4 TO END OF MAPSET NAME
         LA    5,2(5,2)            POINT TO END OF PARAMETER
         LA    2,2(2)              POINT TO BEGINNING OF PARAMETER
MOVEPARM MVC   0(1,3),0(2)         MOVE BYTE OF PARAMETER
         LA    3,1(3)              BUMP TO NEXT STORED BYTE
         LA    2,1(2)              BUMP TO NEXT PARAMETER BYTE
         CR    3,4                 CHECK FOR END OF MAPSET NAME
         BNL   LOADMAP             AT END, LOAD THE MAP
         CR    2,5                 CHECK FOR END OF PARAMETER
         BL    MOVEPARM            NOT AT END, GET NEXT BYTE
*** LOAD THE MAPSET ***
LOADMAP  OC    MAPSET,=C'       '  CONVERT PARAMETER TO UPPERCASE
         LA    3,MAPSET            LOAD ADDRESS OF MAPSET IN R3
         CDLOAD (3)                LOAD THE MAPSET ADDRESS INTO R1
         LR    3,0                 GET THE MAPSET ADDRESS
         USING MSMSD,3             ESTABLISH ADDRESSABILITY
         MVC   IDNTMPST,MSSETNM    MOVE THE MAPSET NAME TO IDNTING
GOTOMAP  LA    4,12(3)             BUMP PAST MAPSET LEAD
         USING MSMDI,4             ESTABLISH ADDRESSABILITY TO MAP
         ST    4,MSADR             SAVE MAP ADDRESS
         SR    10,10
*** INITIALIZE THE DISPLAY AREA TO SPACES ***
GOTMAP   MVC   IDNTMAP,MSNAME      MOVE THE MAP NAME TO IDNTING
         LA    6,SPACE             LOAD ADDRESS OF SPACE IN R6
         LA    8,DISPAREA          POINT R8 TO THE MAP AREA
         L     7,SPACES            SET UP R7 FOR CLEAR OPERATION
         LA    9,1920              LOAD MAP AREA LENGTH IN R9
         MVCL  8,6                 CLEAR THE MAP AREA
*** CHECK TO SEE WHAT RELEASE THE MAP WAS CATALOGED UNDER ***
         TM    MSREL,MSREL7        CHECK IF RELEASE 7 MAP
         BO    REL7                IF RELEASE 7, BRANCH AROUND
         LA    5,42(4)             BUMP PAST MAP LEAD
         TM    MSIND,MSEXTAT       EXTENDED ATTRIBUTES USED?
         BO    EXTATTR             EXTENDED ATTRIBUTES PRESENT
         LA    10,8                FIXED PORTION ONLY 8 BYTES
         B     CONTINUE            BRANCH AROUND RELEASE 7 INSTRUCTIONS
REL7     LA    5,79(4)             REL 7 MAPS HAVE AN EXTRA 37 BYTES
         CLC   MSIND7,MSEXTT7      EXTENTED ATTRIBUTES USED?
         BE    EXTATTR             EXTENDED ATTRIBUTES PRESENT
         LA    10,8                FIXED PORTION ONLY 8 BYTES
         B     CONTINUE
EXTATTR  LA    10,12               FIXED PORTION 12 BYTES LONG
CONTINUE USING MSMDF,5             ESTABLISH ADDRESSABILITY TO FIELDS
*** MOVE THE FIELDS 'INITIAL=' VALUE TO THE DISPLAY AREA ***
CHKVALUE TM    MSDESC,MSVALUE      DOES FIELD HAVE INITIAL VALUE?
         BO    MOVEFLD             INITIAL VALUE PRESENT, MOVE IT?
         LA    5,0(10,5)           BUMP PAST FIXED PORTION
          CLI   0(5),X'FF'          CHECK IF END OF MAP
         BNE   CHKVALUE            NOT END OF MAP, GET NEXT FIELD
         B     PRINTFAX            END OF MAP, PRINT THE MAP
MOVEFLD  LA    1,DISPAREA          POINT R1 TO MAP AREA
         SR    8,8                 CLEAR R8
         LH    8,MSPOS             GET FIELD DISPLACEMENT
         AR    8,1                 POINT INTO MAP AREA
         LA    8,1(8)              ADD 1 FOR FIELD POSITION
         SR    9,9                 CLEAR R9 FOR MVCL
         LH    9,MSFLTH            GET FIELD LENGTH
         LA    6,0(10,5)           POINT R6 TO INITIAL DATA
         LR    7,9
         MVCL  8,6                 MOVE THE INITIAL VALUE DATA
         LA    5,0(6,9)            POINT TO NEXT FIELD
         CLI   0(5),X'FF'          CHECK FOR END OF MAP
         BNE   CHKVALUE            NOT END OF MAP, GET NEXT FIELD
*** START THE PRINT OPERATION ***
PRINTFAX ST    3,AREAADR           SAVE R3 VALUE
         SR    3,3                 CLEAR R3
         SR    4,4                 CLEAR R4
         LA    3,DISPAREA          POINT R3 TO MAP AREA
         LA    4,DISPEND           POINT R4 TO THE END OF THE MAP AREA
         MVI   PRNTCCW,PTOP        SET UP NEW PAGE
         MVI   PRNTAREA,C'*'       MOVE ALL * TO THE
         MVC   PRNTAREA+1(79),PRNTAREA      PRINT AREA
         MVC   PRNTCNT1,ASTERISK   MOVE * INTO
         MVC   PRNTCNT2,ASTERISK         COUNTERS
         ZAP   COUNTER,=PL1'0'     CLEAR THE LINE COUNTER
TOP      EXCP  PRNTCCB             PRINT A LINE OF THE MAP
         WAIT  PRNTCCB
         MVI   PRNTCCW,PSPACE1     SET TO SINGLE SPACE
         AP    COUNTER,=PL1'1'     BUMP COUNTER
         CP    COUNTER,=PL2'12'    CHECK OF DONE WITH TOP MARGIN
         BL    TOP                 NOT DONE, PRINT SOME MORE
         MVC   PRNTAREA,COLTENS    MOVE THE TENS LINE FOR COLUMNS
         EXCP  PRNTCCB             PRINT THE TENS LINE FOR COLUMNS
         WAIT  PRNTCCB
         MVC   PRNTAREA,COLONES    MOVE THE ONES LINE FOR COLUMNS
         EXCP  PRNTCCB             PRINT THE ONES LINE FOR COLUMNS
         WAIT  PRNTCCB
         ZAP   ROWCNTR,=PL1'0'     CLEAR THE ROW COUNTER
*** PRINT THE SCREEN FACSIMILE A ROW AT A TIME ***
PRINTROW AP    ROWCNTR,=PL1'1'     BUMP THE ROW COUNTER
         MVC   ROWDISP,ROWMASK     EDIT IN THE ROW COUNT
         ED    ROWDISP,ROWCNTR          TO THE WORK AREA
         MVC   PRNTCNT1,ROWCHAR    MOVE THE ROW COUNT TO
         MVC   PRNTCNT2,ROWCHAR         THE PRINT LINE
         MVC   PRNTAREA,0(3)       MOVE THE MAP ROW TO THE PRINT LINE
         EXCP  PRNTCCB             PRINT THE MAP ROW
         WAIT  PRNTCCB
         LA    3,80(3)             BUMP TO THE NEXT MAP ROW
         CR    3,4                 CHECK FOR END OF MAP
         BL    PRINTROW            NOT END OF MAP, PRINT NEXT ROW
         MVC   PRNTCNT1,ASTERISK   MOVE * TO
         MVC   PRNTCNT2,ASTERISK               ROW COUNTERS
         MVC   PRNTAREA,COLTENS    MOVE THE TENS LINE FOR COLUMNS
         EXCP  PRNTCCB             PRINT THE TENS LINE FOR COLUMNS
         WAIT  PRNTCCB
         MVC   PRNTAREA,COLONES    MOVE THE ONES LINE FOR COLUMNS
         EXCP  PRNTCCB             PRINT THE ONES LINE FOR COLUMNS
         WAIT  PRNTCCB
         MVI   PRNTAREA,C'*'       MOVE ALL * TO
         MVC   PRNTAREA+1(79),PRNTAREA           THE PRINT AREA
         MVC   PRNTCNT1,ASTERISK   MOVE * TO
         MVC   PRNTCNT2,ASTERISK           ROW COUNTERS
         ZAP   COUNTER,=PL1'0'     CLEAR THE LINE COUNTER
 BOTTOM   EXCP  PRNTCCB             PRINT THE MARGIN LINE
         WAIT  PRNTCCB
         MVI   PRNTCCW,PSPACE1     SET TO SINGLE SPACE
         AP    COUNTER,=PL1'1'     BUMP THE LINE COUNTER
         CP    COUNTER,=PL2'12'    CHECK FOR MARGIN END
         BL    BOTTOM              NO PRINT ANOTHER MARGIN LINE
         EXCP  PRNTCCB             YES, PRINT THE IDNTING
         WAIT  PRNTCCB
*** CHECK FOR MORE MAPS TO PRINT ***
         L     4,MSADR             POINT BACK TO THE MAP BEGINNING
         AH    4,MSLNGTH           BUMP TO NEXT MAP
         ST    4,MSADR             SAVE THE MAP ADDRESS
         CLI   0(4),X'FF'          CHECK FOR THE END OF THE MAP
         BNE   GOTMAP              NOT END OF MAP, PRINT NEXT MAP
*** ALL MAPS HAVE BEEN PRINTED ***
RETURN   EQU   *                   END OF MAP, TIME TO QUIT
         LR    3,15
         EOJ
MAPSET   DC    CL8'        '
PRNTCCB  CCB   SYSLST,PRNTCCW
PRNTCCW  CCW   9,PRNTLINE,0,132
PTOP     EQU   X'8B'               TOP OF PAGE
PSPACE1  EQU   X'09'               SPACE ONE AFTER PRINT
PRNTLINE DS    0CL132
         DC    23C'*'
PRNTCNT1 DC    CL2'  '
PRNTAREA DS    CL80
PRNTCNT2 DC    CL2'  '
         DC    24C'*'
IDNTING  DS    0CL132
         DC    25C' '
IDNTAREA DS    0CL80
         DC    15C' '
         DC    C'MAPSET NAME - '
IDNTMPST DC    CL8'        '
         DC    9C' '
         DC    C'MAP NAME - '
IDNTMAP  DC    CL8'        '
         DC    15C' '
         DC    26C' '
DISPAREA DS    0CL1920
         DC    1920C' '
DISPEND  DS    0H
MSADR    DC    F'0'
AREAADR  DC    F'0'
ROWDISP  DS    CL4
         ORG   ROWDISP
         DS    CL2
ROWCHAR  DS    CL2
ROWCNTR  DC    PL2'0'
COLTENS  DC    C'*********11111111112222222222333333333344444444445'
         DC    C'555555555666666666677777777778'
COLONES  DC    C'12345678901234567890123456789012345678901234567890'
         DC    C'123456789012345678901234567890'
ROWMASK  DC    X'5C202020'
ASTERISK DC    CL2'**'
SPACE    DC    XL1'40'
MSEXTT7  DC    X'01020304'   EXTENDED ATTRB INDICATOR FOR REL 7
         DS    0F
SPACES   DC    XL4'40000000'
COUNTER  DC    PL2'0'
MSMSD    DSECT
MSSETNM  DS    CL8
         DS    CL1
MSREL    DS    CL1
MSREL7   EQU   X'70'
          DS    CL2
MSMDI    DSECT
         DS    CL8
MSNAME   DS    CL8
MSLNGTH  DS    AL2
         DS    CL15
MSIND    DS    AL1           EXTENDED ATTRB INDICATOR FOR REL 6
MSEXTAT  EQU   X'40'
         DS    CL16
MSIND7   DS    CL4
MSMDF    DSECT
         DS    CL2
MSFLTH   DS    AL2
MSDESC   DS    AL1
MSVALUE  EQU   X'02'
         DS    CL1
MSPOS    DS    AL2
MSINIT   DS    0CL1
         END


Steve Ronk
Department Co-ordinator
State Technical Institute at Memphis (USA)   ) Xephon 1988










































