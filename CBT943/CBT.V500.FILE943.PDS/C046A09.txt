Extracting information from the CSD

We operate about a dozen CICS regions (CICS 1.6 under
MVS/XA) including AORs, TORs, combinations of AORs and
TORs, and stand-alone regions.  We use the on-line resource
definition facility extensively and have some common or utility-
type definitions in multiple regions.

We have several CSD files shared by the regions according to
their applications.  Occasionally we have 'lost' a program, map, or
transaction definition.  That is, we have forgotten which group it
was defined in.  Our choices for locating the definition were to
use the DFHCSDUP utility to print out the dataset in batch mode
or to search through the groups on-line.

Since both methods were time consuming, we decided to write an
on-line program to search the CSD file for particular transaction,
program, and mapset entries.  However, we could not find any
documentation describing the CSD record layout.

We used IDCAMS to dump a part of one of the CSD files and
deciphered the codes used to distinguish the different record
types.  We found the hexadecimal code used to determine the
record type in offset 12 of the CSD record.  The codes are as
follows:

%   X'0006' - group definition record

%   X'0FA0' - program definition record

%   X'1388' - transaction definition record

%   X'03E8' - mapset definition record

%   X'0BB8' - profile definition record.

The group name starts at offset 0 and the entry name starts at
offset 14.

The program reads the whole CSD dataset and displays up to 12
group names if an entry is defined in multiple groups.  Since the
CSD dataset is normally closed, we had to open and close it
explicitly.  The program issues a SETL macro to position to the
beginning of the file and a READNEXT to read the records.  The
group names are accumulated in an area and displayed when the
end of file is reached.  If the requested entry is not on the file, a
message tells the requestor to check the PPT or PCT.  Also, if
there is an error closing the CSD file, a message is displayed.

The transaction entry (ie the PCT or CSD definition) requires a
value for the TWA.

With CICS 1.7 the CSD file is expanded and this program may
have to be changed to reflect changes in the location of the
hexadecimal codes and the group and entry names within the
record or to extract other information.

         TITLE 'DISPLAY GROUP NAME FROM CSD FILE'
BASEREG  EQU   3
TCTTEAR  EQU   4
TIOABAR  EQU   5
FWACBAR  EQU   6
         COPY  DFHAID
         COPY  DFHCSADS
         COPY  DFHTCADS
KEY      DS    CL22
         COPY  DFHTCTTE
         COPY  DFHTIOA
AREA     DS    0CL160
TRN      DS    CL4       ----    DCSD
         DS    CL1       ----    BLANK
TYPE     DS    CL5       ----    PROG/TRAN/MAPS
NAME     DS    CL8       ----    XXXXXXXX
         DS    CL2
REST     DS    CL140     ----
         COPY  DFHFWADS
FILEAREA DS    CL300
************************************
DCSD     CSECT
         BALR  BASEREG,0
         USING *,BASEREG
BEGIN    DS    0H
         L     TCTTEAR,TCAFCAAA
         L     TIOABAR,TCTTEDA
         MVC   SVTYPE(4),TYPE
         MVC   GRPNAMES,=CL140' '
         MVC   SVNAME,=CL8' '
         MVI   SW,C'0'
         ZAP   COUNT,=P'0'
CKPROF   CLC   TYPE(4),=C'PROF'       |
         BNE   CKPROG                 |
         MVC   CHAR,PROF              |
         B     CKNAME                 |
CKPROG   CLC   TYPE(4),=C'PROG'       |
         BNE   CKTRAN                 |
         MVC   CHAR,PROG              |    CHECK INPUT FOR
         B     CKNAME                 |     RECORD TYPE
CKTRAN   CLC   TYPE(4),=C'TRAN'       |      AND MOVE
         BNE   CKMAPS                 |       CORRECT CODE
         MVC   CHAR,TRAN              |        TO FIELD.
         B     CKNAME                 |
CKMAPS   CLC   TYPE(4),=C'MAPS'       |
         BNE   ERRIN                  |
         MVC   CHAR,MAPS              |
         B     CKNAME
ERRIN    MVC   REST(36),ERRINMSG
         MVC   AREA(20),=XL20'00'
         MVC   TIOATDL,=H'54'
         ST    TIOABAR,TCTTEDA
         B     WRITE
CKNAME   LA    R7,9                  ---  BUMP THRU NAME AND
         LA    R8,NAME                     MOVE TO SVNAME
         LA    R9,SVNAME                    FIELD.
CKNULLS  CLI   0(R8),X'00'                   MAXIMUM
         BE    OPEN                           LENGTH
         MVC   0(1,R9),0(R8)                   OF EIGHT.
         LA    R9,1(R9)                           ---
         LA    R8,1(R8)
         BCT   R7,CKNULLS
         B     ERRIN
OPEN     DFHOC TYPE=OPEN,DATASET=DATABASE,DSETID=DFHCSD
         MVC   KEY(22),=XL22'0'           --START AT FIRST RECORD
         DFHFC TYPE=SETL,DATASET=DFHCSD,RDIDADR=KEY,                   X
               NOTFND=CLOSE,ERROR=CLOSE
         L     FWACBAR,TCAFCAA
READ     DFHFC TYPE=GETNEXT,ENDFILE=EOF
         L     FWACBAR,TCAFCAA
         CLC   KEY+12(2),CHAR             -- CHECK FOR CORRECT
         BNE   READ                       CHARACTERS FOR TYPE CODE --
         CLC   KEY+14(8),SVNAME           -- CHECK FOR NAME ENTERED --
         BNE   READ
RESULT   EQU   *
         CLI   SW,C'0'                 -- SW INDICATES IF
         BNE   MVCKEY                      FIRST TIME THRU --
         LA    R7,GRPNAMES
MVCKEY   MVC   0(8,R7),KEY             -- GRPNAMES FIELD WILL
         LA    R7,10(R7)                   HOLD UP TO 12 GROUPNAMES -
         MVI   SW,C'1'
         AP    COUNT,=P'1'
         CP    COUNT,=P'12'
         BE    EOF
         B     READ
EOF      EQU   *
CLOSE    DFHFC TYPE=ESETL
         DFHOC TYPE=CLOSE,DATASET=DATABASE,DSETID=DFHCSD,CHECK=CLSERR
         CLI   SW,C'0'
         BE    NOTFND
         MVC   REST(140),=CL140' '
         MVC   REST(20),=CL20'GROUPNAME   :'
         MVC   REST+20(140),GRPNAMES
         MVC   AREA(20),=XL20'00'
         ST    TIOABAR,TCTTEDA
         MVC   TIOATDL,=H'160'
         B     WRITE
CLSERR   MVC   AREA(20),=XL20'00'
         MVC   REST(38),CLSERMSG
         ST    TIOABAR,TCTTEDA
         MVC   TIOATDL,=H'60'
         B     WRITE
NOTFND   MVC   AREA(20),=XL20'00'
         MVC   REST(28),NTFNDMSG
         MVC   TRN(4),SVTYPE
         MVC   TRN+6(8),SVNAME
         ST    TIOABAR,TCTTEDA
         MVC   TIOATDL,=H'50'
WRITE    DFHTC TYPE=(ERASE,WRITE)
END      DFHPC TYPE=RETURN
**                         -- CODES AT OFFSET 12 IN CSD RECORD --
CHAR     DC    X'0000'
GROUP    DC    X'0006'       --  GROUP NAME TYPE RECORD
PROG     DC    X'0FA0'       --  PROGRAM TYPE RECORD
TRAN     DC    X'1388'       --  TRANSACTION TYPE RECORD
MAPS     DC    X'03E8'       --  MAPSET TYPE RECORD
PROF     DC    X'0BB8'       --  PROFILE TYPE RECORD
SVTYPE   DC    CL4' '
SVNAME   DC    CL8' '
SW       DC    C'0'          -- INDICATOR SWITCH
COUNT    DC    P'00'         -- GROUPNAME COUNTER
GRPNAMES DC    CL140' '     -- AREA TO SAVE GROUPNAMES
NTFNDMSG DC    CL28'NOT FOUND. CHECK PPT OR PCT.'
ERRINMSG DC    CL36'ENTER: DCSD TRAN/MAPS/PROG XXXXXXXX '
CLSERMSG DC    CL38'ERROR CLOSING DFHCSD. CHECK FOR CLOSE.'
         LTORG
         END


Jim Smith
Communications Network Analyst
Onondaga County (USA)  ) Onondaga County 1989


