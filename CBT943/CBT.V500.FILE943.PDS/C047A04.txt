Close file status display

Our programmers frequently need to know whether a given CICS
VSAM file is open or closed, but they cannot be trusted with the
extensive capabilities of the CSMT or CEMT transactions.

This small CICS transaction displays on the terminal a list of the
files in the FCT which are in closed status.  When PF1 is hit, a list
of the files in open status is presented.  The files are listed in
alphabetical order.

We run this transaction on CICS 1.6 running under VSE.

SAMPLE SCREEN LAYOUT

-------------------------------------------------------------------------------
QFCT    FCT STATUS - THESE FILES ARE OPENED:        09/14/89  9:18:14  JTWT400

 AGTCTL    BATCNTL   DESCTY    DEXMSTR   RATEFLE   UFOBPT   UFOBTP     UFOIOXI
 UFOIOXR   VSMICS

PROGRAM SOURCE CODE

INQFCT   CSECT
TCTTEAR  EQU    11
         COPY   DFHCSADS
         COPY   DFHTCADS
         COPY   DFHTCTTE
TIOABAR  EQU    10
         COPY   DFHTIOA
OUTPUT   DS     CL1916
INQFCT   CSECT
         BALR   R9,0
         USING  *,R9
         L      TCTTEAR,TCAFCAAA            POINT AT TCTTE
         L      TIOABAR,TCTTEDA        POINT AT INPUT DATA
         DFHSC  TYPE=GETMAIN,CLASS=TERMINAL,INITIMG=40,NUMBYTE=1916
         L      TIOABAR,TCASCSA
         ST     TIOABAR,TCTTEDA
         MVC    TIOATDL,=AL2(1916)
         MVI    OUTPUT,X'11'
         MVC    OUTPUT+1(10),=C'  QFCT    '
         MVC    OUTPUT+11(36),=C'FCT STATUS - THESE FILES ARE CLOSED:'
         L      R1,20
         MVC    OUTPUT+57(8),0(R1)       DATE (FROM COMRG)
         MVC    OUTPUT+65(9),=X'4021207A20207A2020'
         ED     OUTPUT+65(9),CSATODP     TIME
         MVC    OUTPUT+75(3),TCTTEOI     OPID
         MVC    OUTPUT+79(4),TCTTETI     TERMID
         LA     R8,OUTPUT+164          DATA BEGIN
         CLI    TCTTEAID,C'1'       WAS PF1 HIT?
         BNE    LOCFCT                     NO, LIST CLOSED FILES
PF1      MVC    OUTPUT+40(4),=C'OPEN'      YES, LIST OPENED FILES
LOCFCT   DS     0H                 LOCATE FCT
         L      R7,CSAFCTBA       GET FCT PTR FROM CSA
         SH     R7,=H'12'                 BACK UP TO ADDR LIST
         L      R7,0(R7)              GET A POINTER
         L      R7,0(R7)              GET ANOTHER POINTER
FCTLOOP  DS     0H
         L      R6,8(R7)
         TM     8(R6),X'01'      IS THE FILE OPEN?
         BZ     FCLOS              NO, CLOSED
FOPEN    CLI    TCTTEAID,C'1'      YES, OPEN.  DISPLAYING OPEN (PF1)?
         BNE    FCTNEXT                         NO.  SKIP.
         B      FCTDSPL                         YES, DISPLAY IT.
FCLOS    CLI    TCTTEAID,C'1'      CLOSED.  DISPLAYING OPEN (PF1)?
         BE     FCTNEXT                         YES, SKIP.
FCTDSPL  MVC    0(7,R8),0(R7)  MOVE FILE NAME
         LA     R8,10(R8)      BUMP IN SCREEN
         CLC    0(10,R8),20(R8)   STILL ON SCREEN
         BE     FCTNEXT           YES, OK
         MVC    0(10,R8),=C'*OVERFLOW*'
         B      WRAP
FCTNEXT  LA     R7,12(R7)        BUMP IN FCT LIST
         CLI    0(R7),X'FF'
         BL     FCTLOOP
WRAP     DS     0H
         CLC    =CL10' ',OUTPUT+1444       IF SPACE PERMITS,
         BNE    *+10                        DISPLAY THIS COMMENT:
         MVC    OUTPUT+1524(25),=C'PF1 INDICATES OPEN FILES '
         DFHTC  TYPE=WRITE
         DFHTC  TYPE=WAIT
         DFHSC  TYPE=FREEMAIN,RELEASE=ALL
         DFHPC  TYPE=RETURN
         LTORG
         END    INQFCT


Jim Wagner
Senior Systems Programmer ) Xephon 1989


