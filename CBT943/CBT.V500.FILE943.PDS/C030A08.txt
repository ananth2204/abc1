 CICS 1.6 dynamic installation of terminals
In the past we have been faced with the problem of frequently adding and
deleting terminals in many CICS nuclei.  It required often long assemblies and
link-edits, and always needed a restart of CICS.  Adding every terminal to every
CICS nucleus used a large amount of virtual storage some of it wasted.  To avoid
these problems and to be more flexible the following program was developed.
SUTTCT1 runs in the CICS post initialisation phase and reads a VSAM file
sequentially.  Each record contains a CICS terminal-id and the VTAM terminal
name of the terminal that needs to access that CICS system.  The TCT is pre-
generated with symbolic VTAM terminal names (parameter NETNAME).  After reading
a record the program performs a TCT LOCATE with the just-read CICS terminal-id.
When the TCT entry is found, the VTAM terminal is entered in the associated NIB.
The VSAM file is created in an IDCAMS REPRO step from a PDS, before the CICS
step.  Editing takes place under TSO.
When installing a terminal after CICS initialisation it is possible to make a
transaction that takes its input from a terminal and then performs the same
function as SUTTCT1.
***  MOVES COMBINATION OF CICS TERMINAL ID AND NETNAME FROM VSAM    ***
*    FILE INTO AN EXISTING TCT ENTRY  FOR THE CICS TERMINAL ID.       *
*    THE VSAM FILE IS SCANNED FROM BEGIN TO END. THIS PROGRAM RUNS    *
***  IN THE POST INITIALIZATION PHASE.                              ***
         COPY  DFHCSADS                     CSA
         DFHTCA CICSYST=YES                 TCA
TCTTEAR  EQU   R6
         DFHTCTZE CICSYST=YES               TCTTE
FWACBAR  EQU   R7
         COPY  DFHFWADS                     FWA
FWAREC   DS    0CL12
FWAKEY   DS    CL4                          KEY IS CICS TERMINAL NAME
FWANETN  DS    CL8                          VTAM TERMINAL NAME
SUTTCT1  CSECT
         BALR  R3,R0                        LOAD R3 WITH STARTING ADDR
         USING *,R3                         USE R3 AS A BASE
STARTBR  EQU   *                            START BROWSE ON TERMINAL
         DFHFC TYPE=SETL,                   DATASET FIRST RECORD
               DATASET=TERMS,               DATASETID IS TERMS
               RDIDADR=KEY,
               SRCHTYP=FKGE,
               ERROR=ERROR,
               NOTFND=NOTFND,
               NOTOPEN=NOTOPEN
         L     FWACBAR,TCAFCAA              ADDRESS FILE WORK AREA
BROWSE   ST    FWACBAR,TCAFCAA              STORE ADDRESS FWA
         DFHFC TYPE=GETNEXT,                GET NEXT RECORD
               ERROR=ERROR,
               ENDFILE=ENDFILE
PROCESS  CLI   FWAKEY,C'*'                  COMMENT RECORD?
         BE    BROWSE                       YES, SKIP PROCESSING
         L     FWACBAR,TCAFCAA
         MVC   TCATPTA,FWAKEY               MOVE CICS TERMINAL NAME
         DFHTC CTYPE=LOCATE,TERM=ID,NORESP=FOUND   LOCATE TCT ENTRY
NOTFOUND MVC   NFWTO+45(4),FWAKEY           TCT ENTRY NOT FOUND
NFWTO    DFHWTO 'SUTTCT01I: TCT ENTRY NOT FOUND ,          .',U
         B     BROWSE
FOUND    L     TCTTEAR,TCATPTA              LOAD ADDRESS TCT ENTRY
         L     R8,TCTENIBA                  ADDRESS NIB
         USING TCTENIB,R8
         MVC   TCTENNAM,FWANETN             MOVE NETNAME INTO NIB
         B     BROWSE
NOTOPEN  DFHWTO 'SUTTCT02A: TERMINAL DATASET NOTOPEN',U
         DFHWTO 'SUTTCT02A: ABEND TERMINAL INSTALLATION',U
         DFHWTO 'SUTTCT02A: CONTACT SUPPORT',U
         B     RETURN
NOTFND   DFHWTO 'SUTTCT03A: TERMINAL DATASET NOT FOUND',U
         DFHWTO 'SUTTCT03A: ABEND TERMINAL INSTALLATION',U
         DFHWTO 'SUTTCT03A: CONTACT SUPPORT',U
          B     RETURN
ERROR    DFHWTO 'SUTTCT04A: ERROR WHILE PROCESSING TERMINAL DATASET',U
         DFHWTO 'SUTTCT04A: ABEND TERMINAL INSTALLATION',U
         DFHWTO 'SUTTCT04A: CONTACT SUPPORT',U
         B     RETURN
ERRORE   DFHWTO 'SUTTCT05I: ERROR ENDING BROWSE ON TERMINAL DATASET',U
         B     RETURN
ENDFILE  ST    FWACBAR,TCAFCAA
         DFHWTO 'SUTTCT06I: TERMINAL INSTALLATION COMPLETE',U
         DFHFC TYPE=ESETL,                  END BROWSE
               ERROR=ERRORE
         B     RETURN
RETURN   DFHPC TYPE=RETURN                  RETURN TO CICS
KEY      DC    CL4'    '
         END


Jan Bormans
Systems Programmer  ) Computer Centre Netherlands BV 1988
Computer Centre Netherlands BV (Netherlands)














































