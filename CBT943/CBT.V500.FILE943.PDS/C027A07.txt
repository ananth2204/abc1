 Making CICS programs resident
One problem with using the CEMT NEWCOPY command with a resident program is that
afterwards it wonUt be resident any more.  This has kept us from performing CICS
Sign-on Table (SNT) updates in the afternoon.  After seeing the DFHTM macros in
an earlier issue of CICS Update we wrote the following program to make resident
any CICS program defined in the PPT.  Now we can update DFHSNT any time and just
make it resident afterwards.  We have the following code working on CICS 1.6.1
and CICS 1.7 on a 3083 with MVS/SP.
         TITLE 'PROG01 - MAKE CICS PROGRAMS RESIDENT, LENGTH OF 8'
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*        FORMAT IS 'XXXX DFHSNT'    <== SPACES NOT NEEDED AT END      *
*        XXXX IS YOUR TRANID                                          *
*        DFHSNT IS PROGRAM TO BE MARKED AS RESIDENT                   *
*        XXXX WILL RESPOND WITH COMPLETION MESSAGE                    *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
SAACBAR  EQU   10
PPTCBAR  EQU   9
SAVEREG  EQU   8
TCTTEAR  EQU   7
TIOABAR  EQU   6
         COPY  DFHCSADS
         COPY  DFHPPTDS
         COPY  DFHTCTTE
         DFHTCA CICSYST=YES
         DFHTM MF=(D,DIRELEM)
         DFHTM MF=(D,SKTTBLE)
         DFHTM MF=(D,PARMLIST)
         DFHTM MF=(D,TMSTATIC)
         COPY  DFHTIOA
SCREEN   EQU   *
S$TRAN   DS    CL4
         DS    CL1
S$LENGTH EQU   *-SCREEN
S$PROGID DS    CL8
         COPY  DFHSAADS
SAABEGIN EQU   *
DOUBLE   DS    D
PROGID   DS    CL8
CHANGED  DS    CL1
SAALEN   EQU   *-SAABEGIN
PROG01   CSECT
         BALR  R3,0                     INITIAL BASE REGISTER
         USING *,R3                     ESTABLISH ADDRESSABILITY
         B     MAIN00                   BRANCH AROUND CONSTANT
         DC    C'**PROG01**'            PROGRAM NAME
MAIN00   DS    0H
         BAL   R11,RTN00                HOUSEKEEPING
         BAL   R11,RTN01                SETUP SAA
         BAL   R11,RTN02                SEARCH FOR PPT ENTRY
         BAL   R11,RTN03                WRITE
MAIN99   DS    0H
         DFHPC TYPE=RETURN
RTN00    DS    0H
         L     TCTTEAR,TCAFCAAA         POINT TO TCTTE
         L     TIOABAR,TCTTEDA          POINT TO TIOA
         BR    R11                      RETURN
RTN01    DS    0H
         DFHSC TYPE=GETMAIN,CLASS=USER,INITIMG=00,NUMBYTE=SAALEN
         L     SAACBAR,TCASCSA
         MVC   PROGID,SPACES            INIT TO SPACES FIRST
         LH    R1,TIOATDL               SETUP TIOA LENGTH
         SH    R1,=AL2(S$LENGTH+1)      SUBTRACT LEADING LENGTH
         EX    R1,MVC01                 MOVE CORRECT PROG NAME LENGTH
         MVI   CHANGED,C'N'             INIT FIELD TO NO
         BR    R11                      RETURN
MVC01    MVC   PROGID(0),S$PROGID       MOVE PROGRAM NAME
 RTN02    DS    0H
         DFHTM TYPE=(GETFIRST,INLINE,(R15),(PPTCBAR)),TABLE=PPT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*              GET FIRST PPT ENTRY                                    *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         LR    SAVEREG,R15              SAVE DIRECTORY ADDRESS
         B     RTN02B
RTN02A   DS    0H
         LR    R15,SAVEREG              LOAD DIRECTORY ADDRESS
         DFHTM TYPE=(GETNEXT,INLINE,(R15),(PPTCBAR)),TABLE=PPT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*              GET NEXT PPT ENTRY                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         LR    SAVEREG,R15              SAVE DIRECTORY ADDRESS
RTN02B   DS    0H
         LTR   PPTCBAR,PPTCBAR          END OF PPT ?
         BZ    RTN02C                   YES..
         USING DFHPPTDS,PPTCBAR         ESTABLISH ADDRESSABILITY
         CLC   PPTPI,PROGID             HAVE WE GOT IT ?
         BNE   RTN02A                   NO...
         OI    PPTTLR,PPTPCRES          MAKE PROGRAM RESIDENT
         MVC   TCAPCPI,PPTPI            LOAD PROGRAM NAME
         DFHPC TYPE=LOAD                LOAD IT
         OI    PPTTLR,PPTRESTG          MAKE PROGRAM STORAGE RESIDENT
         MVI   CHANGED,C'Y'             SET CHANGED FLAG
RTN02C   DS    0H
         BR    R11                      RETURN
RTN03    DS    0H
         DFHSC TYPE=GETMAIN,CLASS=TERMINAL,INITIMG=00,NUMBYTE=35
         L     TIOABAR,TCASCSA          POINT TO ACQUIRED AREA
         ST    TIOABAR,TCTTEDA          MAKE THIS THE CURRENT TIOA
         MVC   TIOATDL,=H'35'           SETUP LENGTH
         MVC   SCREEN(8),PROGID         MOVE PROGRAM NAME
         CLI   CHANGED,C'Y'             CHANGED PPT ?
         BNE   RTN03A                   NO...
         MVC   SCREEN+10(25),=C'WAS UPDATED IN PPT       '
         B     RTN03B
RTN03A   DS    0H
         MVC   SCREEN+10(25),=C'WAS NOT UPDATED IN PPT   '
RTN03B   DS    0H
         DFHTC TYPE=(ERASE,WRITE)
         BR    R11                      RETURN
SPACES   DC    CL8' '
         LTORG
         END


Steven Durfey
Senior Software Analyst
Weber Marking Systems Inc (USA)    ) Xephon 1988
















