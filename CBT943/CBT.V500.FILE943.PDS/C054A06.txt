Master terminal commands for dump autoswitch

This article outlines our methods of setting our dump datasets
to switch automatically when they become full.  Our current
operating environment is CICS release 1.7 under MVS/XA.

In response to the article Executing master terminal commands
in batch from issue 46 (September 1989), we were surprised as
well to find that CICS 1.7 will not automatically switch to the
alternative dump dataset when the current one becomes full.
However, we implemented a different approach to handle
setting our dump datasets to autoswitch.  Our goals in
designing such a technique included:

1     To minimize operator intervention.

2     To avoid or minimize the loss of dump information.

3     To initiate autoswitch for both dump datasets - this
      required the dump datasets to be switched.

4     To initiate autoswitch through the CEMT S DUMP AUT
      command at system initialization per CICS region - this
      was important as different regions may be shut down and
      started at varying intervals during the day (eg development
      regions).

5     To log the command and response for setting dump
     autoswitch.

To accomplish our goals we made use of the programmable
interface to master terminal functions (see page 390 in the
CICS/OS/VS Customization Guide, SC33-0239-0).  This
interface allows application programs to link to program
DFHEMTA and pass a parameter list specifying what the
CEMT transaction is to accomplish.  These programs can then
be added to the CICS initialisation PLT and used to perform
any CEMT transactions required at CICS start-up time.

Possible scenarios to consider when implementing this or
another method of dump autoswitch include:

1     What if both dump datasets fill and both are set to
      autoswitch?

2     What if both dump datasets fill and the first is set to
     autoswitch?

3     What if a dump dataset is manually switched for printing
     and only the first dump dataset is set to autoswitch?

To set both dump datasets to autoswitch during CICS
initialization you must switch them as well.  This switch opens
the dump dataset for output.  Therefore, as the Installation
Guide (page 271) specifies that dump data will be lost when the
dataset is next opened for output, you can no longer count on
the SIT parameter, DUMPDS=AUTO, providing you with
access to yesterday's dump data from the region shutdown.  In
our experience, if you switch dump datasets again, the previous
data will not be lost until it is written over, but this cannot be
guaranteed.  Any concerns regarding possible dump data loss
should be resolved by printing or copying the data after region
shutdown, before the next start-up.

Based upon the priorities you establish for automatically
switching dump datasets, the following program can be
enhanced or stripped as desired.  This program causes the
dump dataset that is opened during CICS start-up to be set to
autoswitch, initiates a dump dataset switch, sets the second
dump dataset to autoswitch, and switches the dump datasets
again.  The reason for switching the dump datasets twice is to
keep the open dump dataset in synchronization with the
DFH0700 message indicating which dump dataset was opened
at start-up.  The following steps are necessary to implement the
program as coded below:

1     An entry in the PPT for ZDUMPAUT program.

2     An entry in the PLT for ZDUMPAUT program to execute
      at CICS region initialization.

3     An entry in the DCT to record dump autoswitch setting in
     the CICS statistics ie:

      DFHDCT TYPE=INDIRECT,DESTID=DUMP,INDDEST=CSSL

4     Assemble and link the following program:

* NAME:  ZDUMPAUT - CICS DUMP AUTOSWITCH PROGRAM   CICS 1.7  *
* FUNCTION: SET THE CICS DUMP DATSETS TO AUTOSWITCH AT PLT   *
*           INITIALIZATION                                   *
* REQUIREMENTS: PPT ENTRY, PLT ENTRY, DCT ENTRY              *
* REFERENCE: CICS CUSTOMIZATION GUIDE PAGE 390               *
ZDUMPAUT TITLE ' DUMP AUTOSWITCH INITIALIZATION '
ZDUMPAUT CSECT
         DFHEIENT
* INITIALIZE FLAGS FOR LOOP CONTROL
         MVC   ENDFLAG,ONE              * SET END FLAG TO ONE TIME
         MVC   AUTFLAG,YES              * SET AUTOSWITCH FLAG YES
PROCESS  CLC   ENDFLAG,YES              * 2 AUTS, 2 SWITCHS COMPLETED?
         BE    RETURN                   * YES, EXIT PROGRAM
LOOP     CLC   AUTFLAG,YES              * AUTOSWITCH FLAG YES?
         BE    DUMPAUT                  * YES, SET UP AUTOSWITCH
DUMPSW   MVC   FLD1,FLD1SW              * CEMT SWITCH COMMAND
         MVC   FLD4,BLANKS              * CLEAR RETURN AREA
         MVC   AUTFLAG,YES              * SET AUTOSWITCH FLAG TO YES
         CLC   ENDFLAG,TWO              * SECOND TIME THROUGH HERE?
         BE    SETENDFL                 * YES, THIS IS LAST TIME
         MVC   ENDFLAG,TWO              * SET END FLAG TO TWO
         B     EXECCICS                 * CEMT SWITCH IS READY
SETENDFL MVC   ENDFLAG,YES              * YES, SET END FLAG YES
         B     EXECCICS                 * CEMT SWITCH IS READY
DUMPAUT  MVC   FLD1,FLD1AUT             * CEMT AUTOSWITCH COMMAND
         MVC   FLD4,BLANKS              * CLEAR RETURN AREA
         MVC   AUTFLAG,NO               * SET ACTION FLAG FOR SWITCH
*  PERFORM CEMT COMMAND, LOG COMMAND AND RESPONSE TO CICS STATISTICS
*  THROUGH INDIRECT TD QUEUE
EXECCICS EXEC  CICS LINK PROGRAM('DFHEMTA') COMMAREA(COMREG) LENGTH(20)
         EXEC  CICS WRITEQ TD QUEUE('DUMP') FROM(BLANKS)
         EXEC  CICS WRITEQ TD QUEUE('DUMP') FROM(FLD1)
         EXEC  CICS WRITEQ TD QUEUE('DUMP') FROM(FLD4)
         EXEC  CICS WRITEQ TD QUEUE('DUMP') FROM(BLANKS)
         B     PROCESS                  * COMPLETED, GOTO PROCESS
RETURN   EXEC CICS RETURN
COMREG   DS    0F
         DC    A(FLD1)                    * ADDR OF CEMT COMMAND
         DC    A(FLD2)                    * ADDR OF CEMT COMMAND LNGTH
         DC    A(FLD3)                    * ADDR OF INDICATOR FIELD
         DC    A(FLD4)                    * ADDR OF CEMT OUTPUT AREA
         DC    A(FLD5)                    * ADDR OF CEMT OUTPUT LENGTH
FLD1     DS    CL17                       * CEMT COMMAND AREA
FLD2     DC    H'17'                      * CEMT COMMAND LENGTH
FLD3     DC    XL1'00'                    * INDICATOR FOR OUTPUT AREA
FLD4     DS    CL80                       * CEMT MESSAGE RETURN AREA
FLD5     DC    H'80'                      * LENGTH OF RETURN AREA
FLD1AUT  DC    CL20'CEMT SET DUMP AUT'    * CEMT COMMAND AREA
FLD1SW   DC    CL20'CEMT SET DUMP SW '    * TO SET BOTH DATSETS TO AUT
BLANKS   DC    CL80' '                    * INITIALIZE BLANK LINE
AUTFLAG  DS    CL3                        * AUTOSWITCH/SWITCH FLAG
ENDFLAG  DS    CL3                        * END LOOPS FLAG
NO       DC    CL3'NO '                   * VALUE FOR FLAGS
ONE      DC    CL3'ONE'                   * VALUE FOR FLAGS
TWO      DC    CL3'TWO'                   * VALUE FOR FLAGS
YES      DC    CL3'YES'                   * VALUE FOR FLAGS
         DFHEISTG
         END   ZDUMPAUT


Ian Phillips
Systems Programmer (Canada)           ) Ian Phillips 1990


