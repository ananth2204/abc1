Deleting temporary storage queues

Several of our CICS regions use a security package which contains an
exit that causes users to time out (ie sign-off) after ten minutes of
inactivity.  Frequently, they time out in the middle of one or more
transactions, causing stray temporary storage queues.  We use an
auto-install program which randomly assigns CICS terminal-ids at
sign-on time.  Our temporary storage queues begin or end with this
terminal-id.  This means that if a terminal is assigned a terminal-id
that accesses a stray queue, the user will receive data relevant to the
previous user.

To prevent this, the following program was written to delete all
temporary storage queues associated with a terminal-id at sign-off.
The program is invoked via the DFHZNEP program to ensure deletion
whether users time out or sign-off, or if a terminal error occurs.  It
scans the temporary storage unit table and saves all temporary storage
queue keys that begin or end with the terminal-id of the terminal
signing off.  Once the scan is complete, it deletes all associated
temporary storage queues and writes a message to CSSL.

The table used to save the temporary storage queue keys only allows
for 20 keys.  If your application has the capability to create more
entries per terminal, you will need to add more space to TSTABLE
(in increments of 8) and initialize the added space by increasing the
load to register 7 by the total number of keys in the table (table length
divided by 8).


TSDELETE SOURCE CODE

         TITLE 'TSDELETE - TEMPORARY STORAGE DELETION'
         GBLB  &DFHEIMX
&DFHEIMX SETB  1
         DFHREGS
         DFHTCA CICSYST=YES
         COPY  DFHCSADS
TSDELETE CSECT
         L     R4,CSATSMTA           ADDRESS OF THE TS UNIT TABLE
         LA    R6,TSTABLE            TABLE OF TS KEYS TO BE DELETED
         L     R7,=F'20'             INITIALIZE TABLE WITH HIGH VALUES
         ZAP   ENTRIES,=P'0'
INITTABL EQU   *
         MVC   0(8,R6),=X'FFFFFFFFFFFFFFFF'
         LA    R6,8(R6)
         BCT   R7,INITTABL
         LA    R6,TSTABLE            POINT TO BEGINNING OF TABLE
         L     R8,TCAFCAAA           ADDRESS OF THE TASK CONTROL BLOCK
         MVC   FULLWORD,0(R8)        STORE TERM ID
         L     R5,12(R4)             ADDRESS OF THE FIRST TS RECORD
         L     R9,16(R4)             ADDRESS OF LAST TS RECORD
         CR    R5,R9                 ANY TS RECORDS?
         BE    EXIT2                 NO, GET OUT
FINDID   EQU   *
         LA    R5,4(R5)              POINT TO FIRST 4 CHARACTERS OF ID
         MVC   TSQTERM,0(R5)         STORE FIRST 4 CHARACTERS OF ID
         LA    R5,4(R5)              POINT TO LAST 4 CHARACTERS OF ID
         MVC   TSQTYPE,0(R5)         STORE LAST 4 CHARACTERS OF ID
         CLI   4(R5),X'A0'           NOT PUT OUT BY A GET OR PUT MACRO?
         BNE   NEXTUNIT              NO, THEN CAN'T DELETE IT
         CLC   TSQTERM,FULLWORD      FIRST 4 CHARACTERS OF ID MATCH?
         BE    SAVENAME              YES, DELETE THIS TS UNIT
         CLC   TSQTYPE,FULLWORD      LAST 4 CHARACTERS OF ID MATCH?
         BE    SAVENAME              YES, DELETE THIS TS UNIT
NEXTUNIT EQU   *
         LA    R5,8(R5)              POINT TO NEXT TS RECORD
         CR    R5,R9                 PAST LAST TS RECORD?
         BH    DELETE                YES, GO DELETE TS RECORDS
         B     FINDID                NO, KEEP LOOKING
SAVENAME EQU   *
         MVC   0(8,R6),TSQNAME       PUT TS KEY IN TABLE
         LA    R6,8(R6)              BUMP TO EMPTY SLOT IN TABLE
         B     NEXTUNIT              GO LOOK AT NEXT RECORD
DELETE   EQU   *
         LA    R6,TSTABLE            POINT TO BEGINNING OF TABLE
         LA    R8,SMSG               POINT TO BEGINNING OF
         LA    R8,9(R8)                 MESSAGE TABLE
         CLC   0(8,R6),=X'FFFFFFFFFFFFFFFF' A RECORD TO DELETE?
         BE    EXIT2                 NO, GET OUT
NEXTKEY  EQU   *
         CLC   0(8,R6),=X'FFFFFFFFFFFFFFFF' A RECORD TO DELETE?
         BE    EXIT                  NO, GET OUT
         MVC   TSQNAME,0(R6)
         MVC   0(8,R8),TSQNAME       PUT TS KEY IN TABLE
         EXEC CICS IGNORE CONDITION QIDERR
         EXEC CICS DELETEQ TS QUEUE(TSQNAME)
         LA    R6,8(R6)              BUMP TO NEXT KEY
         CP    ENTRIES,=P'12'        REACH 13 ENTRIES?
         BH    DONEXT                YES, DON'T ADD MORE
         LA    R8,9(R8)              BUMP TO NEXT MESSAGE ENTRY
         AP    ENTRIES,=P'1'
DONEXT   EQU   *
         B     NEXTKEY
EXIT     EQU   *
         MVC   ERRMSGX(SMSGLEN),SMSG
         EXEC CICS IGNORE CONDITION NOTOPEN
         EXEC CICS WRITEQ TD QUEUE('CSSL')                             *
               FROM(ERRMSGX)                                           *
               LENGTH(=AL2(SMSGLEN))
         MVC   ERRMSGX,BLANKS
         MVC   SMSG+9(117),BLANKS
EXIT2    EQU   *
         EXEC CICS RETURN
BLANKS   DC    CL126' '
ENTRIES  DS    PL2
FULLWORD DS    F
TSQNAME  DS    0CL8
TSQTERM  DS    CL4
TSQTYPE  DS    CL4
TSTABLE  DS    CL160
SMSG     DS    0CL126
         DC    C'DELETEQ= '
         DC    CL117' '
SMSGLEN  EQU   *-SMSG
ERRMSGX  DS    CL126
         LTORG
         DFHEISTG
         END


NETWORK ERROR PROGRAM

***********************************************************************
*  THIS ADDITION TO DFHZNEP WILL BE INVOKED AT CICS SESSION TERMIN-   *
*  ATION WHEN THE FOLLOWING ERROR CODE IS ISSUED BY CICS:             *
*    ERROR MSG=DFH34621   ERROR CODE=X'49'   SYM. LABEL=TCZCLSIN      *
*  ITS PURPOSE IS TO LINK TO THE TSDELETE PROGRAM. WHICH IN TURN WILL *
*  DELETE ANY TEMPORARY STORAGE ASSOCIATED WITH THE TERMINAL WHOSE    *
*  SESSION HAS ENDED.                                                 *
***********************************************************************
NEPROC03 CSECT
* SETUP BASE REGISTERS                                                *
BASEREG  EQU   R6                  BASE REGISTER
         BALR  BASEREG,R0          ESTABLISH BASE REGISTER
         USING *,BASEREG           BASE REGISTER
* CHECK ERROR CODE IN TWAEC                                           *
CHKSEND  CLI   TWAEC,TCZCLSIN      Q CHECK FOR ERROR CODE = X'49'
         BNE   RETURN              NO, THEN RETURN
         ST    R10,SAVEREG         ADDRESS TCA
         MVC   TCAFCAAA+1(3),SAVEREG+1     ADDRESS TCTTE
         DFHPC TYPE=LINK,PROGRAM=TSDELETE  LINK TSDELETE
         MVC   TCAFCAAA,=X'00000000'       CLEAR TCTTE
RETURN   DS    0H
         DFHPC TYPE=RETURN         RETURN TO DFHZNEP
SAVEREG  DS    F
         LTORG


Judy Syrell and Mark Ehde
Systems Programmers
Empire Blue Cross/Blue Shield (USA)

