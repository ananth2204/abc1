 Changing CICS sign-on names dynamically
One problem with CICS is when you need to remove a person from the CICS sign-on
table, you have to update and re-assemble DFHSNT and perform a NEWCOPY.  We have
an alternative solution in which we modify a resident DFHSNT and change the
first four characters of a personUs sign-on name.  We just execute this on-line
program and immediately the name we specify is changed, thus preventing this
person from signing on.  This program can easily be modified to put any value,
for example the time-of-day, into the DFHSNT name field.  Just remember you
still need to update and run DFHSNT later in the day.  We have this code running
on CICS 1.7 on an IBM 3083 with MVS/SP.  For CICS 1.6.1 you can easily change
table entry DSECT and add a TNEXTENT EQU *U after it; use this for 1.6
addressability instead of 1.7Us SNNTLNG. There will be some other simple
modifications needed too, eg DFHSNT name offset, etc.
***      FORMAT IS 'XXXX JONES INDIANA'                             ***
*        XXXX IS TRANID                                               *
*        'JONES INDIANA' IS DFHSNT SIGNON NAME                        *
*        DFHSNT ENTRY CHANGED TO '#P6#S INDIANA'                      *
*        XXXX WILL RESPOND WITH COMPLETION MESSAGE                    *
***      WORKS WITH CICS 1.7                                        ***
SAACBAR  EQU   10
PPTCBAR  EQU   9
SAVEREG  EQU   8
TCTTEAR  EQU   7
TIOABAR  EQU   6
         COPY  DFHCSADS
         COPY  DFHPPTDS
         COPY  DFHTCTTE
         DFHTCA CICSYST=YES
         DFHTM MF=(D,DIRELEM)
         DFHTM MF=(D,SKTTBLE)
         DFHTM MF=(D,PARMLIST)
         DFHTM MF=(D,TMSTATIC)
         COPY  DFHTIOA
SCREEN   EQU   *
S$TRAN   DS    CL4
         DS    CL1
S$LENGTH EQU   *-SCREEN
S$NAME   DS    CL20
         COPY  DFHSAADS
SAABEGIN EQU   *
DOUBLE   DS    D
NAME     DS    CL20
CHANGED  DS    CL1
NAMELEN  DS    H
SAALEN   EQU   *-SAABEGIN
PROG02   CSECT
         BALR  R3,0                     INITIAL BASE REGISTER
         USING *,R3                     ESTABLISH ADDRESSABILITY
         B     MAIN00                   BRANCH AROUND CONSTANT
         DC    C'**PROG02**'            PROGRAM NAME
MAIN00   BAL   R11,RTN00                HOUSEKEEPING
         BAL   R11,RTN01                SETUP SAA
         BAL   R11,RTN02                SEARCH FOR PPT ENTRY
         BAL   R11,RTN03                WRITE
MAIN99   DFHPC TYPE=RETURN
RTN00    L     TCTTEAR,TCAFCAAA         POINT TO TCTTE
         L     TIOABAR,TCTTEDA          POINT TO TIOA
         BR    R11                      RETURN
RTN01    DFHSC TYPE=GETMAIN,CLASS=USER,INITIMG=00,NUMBYTE=SAALEN
         L     SAACBAR,TCASCSA
         MVC   NAME,SPACES              INIT TO SPACES FIRST
         LH    R4,TIOATDL               SETUP TIOA LENGTH
         SH    R4,=AL2(S$LENGTH+1)      SUBTRACT LEADING LENGTH
         STH   R4,NAMELEN               STORE NAME LENGTH
         EX    R4,MVC01                 MOVE CORRECT NAME LENGTH
         MVI   CHANGED,C'N'             INIT FIELD TO NO
          BR    R11                      RETURN
MVC01    MVC   NAME(0),S$NAME           MOVE NAME
RTN02    DFHTM TYPE=(GETFIRST,INLINE,(R15),(PPTCBAR)),TABLE=PPT
*                                       GET FIRST PPT ENTRY
         LR    SAVEREG,R15              SAVE DIRECTORY ADDRESS
         B     RTN02B
RTN02A   LR    R15,SAVEREG              LOAD DIRECTORY ADDRESS
         DFHTM TYPE=(GETNEXT,INLINE,(R15),(PPTCBAR)),TABLE=PPT
*                                       GET NEXT PPT ENTRY
         LR    SAVEREG,R15              SAVE DIRECTORY ADDRESS
RTN02B   LTR   PPTCBAR,PPTCBAR          END OF PPT ?
         BZ    RTN02E                   YES..
         USING DFHPPTDS,PPTCBAR         ESTABLISH ADDRESSABILITY
         CLC   PPTPI,=C'DFHSNT  '       HAVE WE GOT IT ?
         BNE   RTN02A                   NO...
         L     R5,PPTCSA                LOAD DFHSNT CORE ADDRESS
         LA    R5,24(R5)                POINT TO FIRST ENTRY
         USING DFHSNNT,R5               POINT TO DSECT
RTN02C   CLC   SNNTLNG,=X'0000'         END OF TABLE ?
         BE    RTN02E                   YES..
         SR    R4,R4                    CLEAR REG 4
         LH    R4,NAMELEN               SET ENTRY LENGTH
         EX    R4,COMPARE1              MATCH ?
         BE    RTN02D                   YES..
         AH    R5,SNNTLNG               NO...ADD ENTRY LENGTH
         B     RTN02C                   LOOP
COMPARE1 CLC   NAME(0),SNNTN            MATCH ?
RTN02D   MVC   SNNTN(4),=C'#P6#'        MOVE CHANGE VALUE(YOUR VALUE ?)
         MVI   CHANGED,C'Y'             SET CHANGED FLAG
RTN02E   BR    R11                      RETURN
RTN03    DFHSC TYPE=GETMAIN,CLASS=TERMINAL,INITIMG=00,NUMBYTE=50
         L     TIOABAR,TCASCSA          POINT TO ACQUIRED AREA
         ST    TIOABAR,TCTTEDA          MAKE THIS THE CURRENT TIOA
         MVC   TIOATDL,=H'50'           SETUP LENGTH
         MVC   SCREEN(20),NAME          MOVE PROGRAM NAME
         CLI   CHANGED,C'Y'             CHANGED PPT ?
         BNE   RTN03A                   NO...
         MVC   SCREEN+25(25),=C'WAS UPDATED IN DFHSNT    '
         B     RTN03B
RTN03A   MVC   SCREEN+25(25),=C'WAS NOT UPDATED IN DFHSNT'
RTN03B   DFHTC TYPE=(ERASE,WRITE)
         BR    R11                      RETURN
SPACES   DC    CL20' '
         LTORG
DFHSNNT  DSECT                     DUMMY SECTION SIGN-ON TABLE
SNNTLNG  DS    H                   TABLE ENTRY LENGTH
SNNTIND  DS    B                   INDICATOR BYTE
SNNTEXTS EQU   X'80'               EXTERNAL SECURITY REQUIRED
SNNTPSDO EQU   X'40'               PSEUDO ENTRY CREATED DYNAMICALLY
SNNTDFLT EQU   X'20'               DEFAULT ENTRY
SNNTSNOF EQU   X'10'               SIGNOFF AT EOT REQUIRED
SNNTTMOU DS    B                   SIGN-OFF TIME
SNNTTIME DS    0F                  SIGN-OFF TIME STAMP AT TC FREE
SNNTPS   DS    CL8                 PASSWORD
SNNTUBLK DS    A                   USER SECURITY BLOCK ADD.
SNNTSKE  DS    BL5                 OP SECURITY KEY EXTENSION
SNNTSK   DS    BL3                 OPERATOR SECURITY KEY
SNNTASK  DS    BL3                 OPERATOR RESOURCE LEVEL KEY
SNNTOP   DS    B                   OPERATOR PRIORITY
SNNTOC   DS    BL3                 OPERATOR CLASS
SNNTID   DS    CL3                 OPERATOR IDENTIFICATION
         DS    B                   .. RESERVED
SNNTSSF  DS    0CL9                SUB SYSTEM FIELD
SNNTUIDL DS    B                   LENGTH OF USERID FIELD
SNNTUSID DS    CL8                 8-CHARACTER USERID
SNNTUSE  DS    H                   USE COUNT
 SNNTPSE  DS    0H                  END OF PSEUDO PART OF SNT ENTRY
SNNTNL   DS    B                   OPERATOR NAME LENGTH - 1
SNNTN    DS    CL20                OPERATOR NAME
SNNTEND  DS    0F                  ROUND TO FULL WORD
         END


Steven Durfey
Senior Software Analyst
Weber Marking Systems Inc (USA)    ) Xephon 1988
























































