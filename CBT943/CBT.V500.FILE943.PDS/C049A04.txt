VSAM ESDS back-out

THE PROBLEM

An ESDS VSAM dataset, by its nature, does not allow
physical deletes to take place.  There are only two ways to
remove a record from an ESDS file.  One is to view it as
'logically' deleted within the application.  The other is to re-
organise the file and skip over it during the recreate process.
Obviously, this presents a problem for CICS when an ESDS
file is involved in any kind of transaction backout.  This can
occur either during a transaction abend or emergency restart.
In either case, CICS by itself is unable to deal with the ESDS
record, since it would need to know what logically causes this
record to appear 'deleted'.

THE SOLUTION

Typically, the logical deletion of a record involves setting a
byte to some value which has meaning to the application.
Although CICS cannot intuitively know what the deletion logic
is, a method is available for providing this information.  As
stated earlier, there are two instances where this information is
required.  When a transaction backout occurs, exit XDBFERR
is available for this purpose.  When emergency restart occurs,
exit XRCFCER is available.  Once control is passed to these
exits, it is a fairly straightforward process to determine first
that it is an ESDS file, and secondly to apply any necessary
back-out changes.  I have written two programs which contain
basic code required by these exits.  Program ESDSDLET is
used by exit XDBFERR and program ESDSBKOT is used by
exit XRCFCER.  The only changes required for other
environments would be the file-id you are using, along with the
proper offset and setting for the 'delete' byte.  Each program
requires a different method for activation under CICS.  The
ESDSDLET program is activated by enabling exit XDBFERR.
I do this in my start-up PLT using another program called
ENAEXIT, which is also illustrated.  The entry in the PLT
would appear as follows:

    DFHPLT TYPE=ENTRY,PROGRAM=ENAEXIT

The ESDSBKOT program is initialised by using the following
SIT parameter at start-up time:

    TBEXITS=(,,ESDSBKOT)

These programs are currently running under CICS 1.7 in an
MVS 2.2 environment.

ESDSDLET

*  THIS CODE IS USED BY EXIT XDBFERR TO LOGICALLY BACKOUT VSAM ESDS
*  RECORDS WHEN A TRANSACTION ABEND OCCURS
         COPY DFHDBRDS
ESDSDLET CSECT
         BALR  12,0
         USING *,12
         STM   14,12,12(13)
         ST    13,SAVE13
         USING DFHDBRDS,3
         CLI   DBRERRCD,X'10'        IS THIS AN ESDS RECORD?
         BNE   SETRC0                IF NOT, GET OUT
         CLC   DBRFID,=CL8'RSSLOGF ' WHICH FILE IS IT?
         BNE   SETRC0                IF NOT THE ONE WE WANT, GET OUT
         MVC   26(,6),=C'I'          MARK THE DELETE BYTE APPROPRIATELY
*                                    NOTE:  MY OFFSET INTO THE RECORD
*                                    IS ACTUALLY 7 BYTES, ALLOW FOR 19
*                                    BYES OF FWA HEADER INFORMATION
         L     13,SAVE13
         LM    14,12,12(13)
         LA    15,8              TELL BACKOUT PROGRAM TO USE OUR CHANGE
         BR    14
SETRC0   L     13,SAVE13
         LM    14,12,12(13)
         LA    15,0              TELL BACKOUT PROGRAM TO CONTINUE AS IS
         BR    14
SAVE13   DS    F
         LTORG
         END

ESDSBKOT

*  THIS CODE IS USED BY EXIT XRCFCER TO LOGICALLY BACKOUT VSAM ESDS
*  RECORDS WHEN A CICS EMERGENCY RESTART TAKES PLACE
ESDSBKOT CSECT
         BALR  12,0
         USING *,12
         STM   14,12,12(13)
         ST    13,SAVE13
         CLI   TBXITCOM,TBFEWA       IS THIS AN ESDS RECORD
         BNE   DONTWANT              IF NOT, GET OUT
         CLC   =C'RSSLOGF',0(1)      WHICH FILE IS IT?
         BNE   DONTWANT              IF NOT THE ONE WE WANT, GET OUT
         MVI   26(FWACBAR),'I'       MARK THE DELETE BYTE APPROPRIATELY
*                                    NOTE: MY OFFSET INTO THE RECORD IS
*                                    ACTUALLY 7 BYTES, ALLOW FOR 19
*                                    BYTES OF FWA HEADER INFORMATION
         L     13,SAVE13
         LM    14,12,12(13)
         B     0(,14)            TELL BACKOUT PROGRAM TO USE OUR CHANGE
DONTWANT L     13,SAVE13
         LM    14,12,12(13)
         B     4(,14)            TELL BACKOUT PROGRAM TO CONTINUE AS IS
SAVE13   DS    F
         LTORG

ENAEXIT

*  THIS PROGRAM IS USED IN THE CICS STARTUP PLT TO ENABLE THE
*  PROGRAM FOR EXIT XDBFERR
        TITLE    'ENABLE CICS GLOBAL EXIT XDBFERR'
ENAEXIT CSECT
        EXEC CICS HANDLE CONDITION INVEXITREQ(ERROR)
        EXEC CICS ENABLE EXIT('XDBFERR') PROGRAM('ESDSDLET') START
        WTO '--> EXIT XDBFERR HAS BEEN ENABLED <--'                    X
               ROUTCDE=(2,11),DESC=7
        B RETURN
ERROR   WTO '--> ERROR OCCURRED WHILE ENABLING XDBFERR <--',DESC=(11), X
               ROUTCDE=(1,11)
RETURN  EXEC CICS RETURN
        END


Ernie Ishman
Senior Systems Programmer
Geisinger System Services (USA) ) Xephon 1989


