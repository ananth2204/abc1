Sending messages to CICS terminals

According to several different articles I had read, it seemed that
anyone who wanted to send a message to another CICS user
always did so using transient data queues only.  This wasn't the
way I wanted to do it.  I created a program that utilizes new
command-level commands and BMS paging techniques, as
well as transient data queues.  The program was made so that it
would never destroy or disrupt the information on a user's
screen.  The message appears at the bottom of the screen and
goes away when the user strikes his or her ENTER key.

The program was designed so that a message could be
broadcast to a single user or to all users from a CICS terminal
or the system console.  If the message is sent to all users or to
the operator, it is automatically logged to the CICS partition
SYSLST output file as a permanent record.

The program will only send messages to terminals that are
acquired.  Additional qualifying checks could be incorporated
into the code to meet your installation's requirements.  The
program will send a message to up to 300 terminals.  After it
has sent the message, it tells the sender how many terminals it
has sent the message to, using BMS paging and routing
commands.

To execute this program, make appropriate entries in the PPT
and PCT, and define an intra-partition dataset called IEP1 with
DESTINATION=(TERMINAL,CNSL) in you DCT.

The program is currently running under CICS 1.7 and VSE.

         GBLB  &DFHEIMX               GLOBAL DEFINE AND SET COMMAND
&DFHEIMX SETB  1                      SO R13 AND R12 GETS SET TO THE
*                                     CSA AND TCA IN DFHEIENT MACRO
*****      THIS PROGRAM WILL ALLOW YOU TO SEND MESSAGES        *****
*          UP TO 300 TERMINALS AT A TIME.                          *
*****      IF MORE ARE NEEDED THEN INCREASE TERMTAB.           *****
TIOABAR  EQU   10
TCTTEAR  EQU   11                       TCTTE BASE REGISTER
CSACBAR  EQU   13
         COPY  DFHCSADS
         DFHTCA CICSYST=YES
         DFHTCTZE CICSYST=YES
         COPY  DFHTIOA
DFHEISTG DSECT
TINPUT   DS    0CL80
TRANSIN  DS    CL4
         DS    CL1
TERMIN   DS    CL4
MSG      DS    0CL71
         DS    CL1
MSGIN    DS    CL70
IOLENGTH DS    H
TOUTPUT  DS    0CL77
CONOUT   DS    0CL77
TERMOUT  DS    CL4
EQ       DS    CL3
MSGOUT   DS    CL70
TERMINAL DS    CL4
DEVICE   DS    F
STATUS   DS    F
TOTSENDS DS    D
ORIGTERM DS    CL4
NUMBOUT  DS    CL5
SAVE14   DS    F
         DS    0F
TERMTAB  DS    300CL16
EOT      DS    H
*****    D F H E I E N T  MACRO                                   *****
*        BASE REG = 3                              - CODE STORAGE     *
*        DATA REGISTERS = R2 AND R4                - WORKING STORAGE  *
*****    DOES BALR, USING, AND GET STORAGE FOR YOU                *****
POSTMSG  DFHEIENT CODEREG=(3),DATAREG=(2,4)
         XR    R8,R8                   INIT REG 8
         MVI   TERMIN-1,C' '           MOVE SPACE TO INPUT
         MVC   MSG,MSGIN-1             CLEAR INPUT AREA
         MVC   MSGOUT,MSGIN-1          CLEAR OUTPUT AREA
         MVC   IOLENGTH,=AL2(L'TINPUT) STORE LENGTH
*****    R E C E I V E INPUT FROM SCREEN                          *****
         EXEC  CICS RECEIVE                                            C
               INTO(TINPUT)                                            C
               LENGTH(IOLENGTH)                                        C
               MAXLENGTH(IOLENGTH)                                     C
               NOTRUNCATE
         MVC   ORIGTERM,EIBTRMID       SAVE ORIG TERMID
         CLC   IOLENGTH,=H'8'          CHECK LENGTH OF INPUT
         BE    TOOSHORT
         CLC   TERMIN,=C'ALL '         CHECK INPUT FOR ALL TERMS
         BE    ALLRTNE
         CLC   TERMIN,=C'CNSL'         CHECK INPUT FOR CNSL MSG
         BE    CNSLRTNE
         B     TERMRTNE                BR TO 1 TERMINAL RTNE
CNSLRTNE DS    0H
         BAL   R14,VERLEN              GET LENGTH ROUTINE
         B     SENDIT
ALLRTNE  DS    0H
         BAL   R14,VERLEN1             GET LENGTH ROUTINE
         BAL   R14,INITRTNE            INIT TABLE ROUTINE
         BAL   R14,LOC1RTNE            GO LOCATE FIRST TERM ID
SEARCH   DS    0H
         BAL   R14,LOCNRTNE            GO LOCATE REST
         B     SEARCH
LOC1RTNE DS    0H                      TURN ON LOCATE
         ST    R14,SAVE14
         EXEC CICS INQUIRE TERMINAL START
         XR    R9,R9                   INIT REGISTER 9
         LA    R9,TERMTAB               LOAD ADDRESS OF TERMTAB
         L     R14,SAVE14
         BR    R14
*****    E N T R Y - LOCATE TERMINAL IDS                          *****
LOCNRTNE DS    0H                      LOCATE NEXT TERMINAL ID
         ST    R14,SAVE14              SAVE R14
         EXEC  CICS HANDLE CONDITION                                   C
               END(ENDLOOP)
         EXEC  CICS HANDLE CONDITION                                   C
               ILLOGIC(LOC1RTNE)
         EXEC CICS INQUIRE NEXT                                        C
               TERMINAL(TERMINAL)                                      C
               DEVICE(DEVICE)                                          C
               ACQSTATUS(STATUS)
         CLC   TERMINAL,=C'CNSL'       COMPARE FOR CNSL
         BE    RETLOOP                 IGNORE AND RETURN
         CLC   STATUS,DFHVALUE(RELEASED) COMPARE FOR RELEASE COND
         BE    RETLOOP
         CLC   DEVICE,DFHVALUE(T3277R)   COMPARE FOR REM 3270 TYPE TERM
         BE    BUILDTAB                  BUILD TABLE
         CLC   DEVICE,DFHVALUE(T3277L)   COMPARE FOR LOC 3270 TYPE TERM
         BE    BUILDTAB                  BUILD TABLE
         B     RETLOOP                   RETURN
*****    MOVE TERMINAL IDS TO TABLE -- LISTTAB                    *****
BUILDTAB DS    0H
         CLI   0(R9),X'FF'               COMPARE FOR E-O-F
         BE    NOROOM                    BRANCH IF E-O-F
         MVC   0(4,R9),TERMINAL          MOVE TERMID INTO TABLE
         LA    R9,16(0,R9)               BUMP ADDRESS UP BY 16 BYTES
         AH    R8,=H'1'                  ADD CNT OF TERMS SENT MSG
*  RETURN TO CALLING ROUTINE
RETLOOP  DS    0H                        ORDINARY RETURN
         L     R14,SAVE14                LOAD SAVE AREA
         BR    R14                       AND RETURN
ENDLOOP  DS    0H                        END OF BUILD TABLE ROUTINE
*****    STOP TERMINAL BROWSE FUNCTION                         *****
         EXEC CICS INQUIRE TERMINAL END
         MVC   0(2,R9),=H'-1'            MARK EOT
         XR    R14,R14
         ST    R14,SAVE14                INIT SAVE AREA
         B     SENDIT                    BR TO SEND MSG ROUTINE
TERMRTNE DS    0H
         BAL   R14,INITRTNE              INIT TABLE RTNE
         BAL   R14,VERLEN                GET LEBGTH ROUTINE
*****    LOCATE & VERIFY TERMINAL ID                           *****
         EXEC  CICS HANDLE CONDITION                                   C
               TERMIDERR(INVIDR1)
         MVC   TERMINAL(4),TERMIN        MOVE TERMINAL FOR SEARCH
         EXEC CICS INQUIRE                                             C
               TERMINAL(TERMINAL)                                      C
               DEVICE(DEVICE)                                          C
               ACQSTATUS(STATUS)
         CLC   STATUS,DFHVALUE(RELEASED) COMPARE FOR RELEASE COND
         BE    NOTACQ
MOVETERM DS    0H
         XR    R9,R9                     INIT REGISTER 9
         LA    R9,TERMTAB                LOAD ADDRESS OF TERMTAB
         MVC   0(4,R9),TERMINAL          MOVE TERMID INTO TABLE
         LA    R9,16(0,R9)               BUMP ADDRESS UP BY 16 BYTES
         MVC   0(2,R9),=H'-1'            SET EOT CONDITION
         AH    R8,=H'1'                  ADD CNT OF TERMS SENT MSG
SENDIT   DS    0H                        SEND MSG ROUTINES
         L     TCTTEAR,TCAFCAAA          GET ADDRESSABILITY
         CLC   TERMIN,=C'CNSL'           COMPARE FOR CNSL MSG
         BE    SKIPPED
SKIPPED  DS    0H
         MVC   EQ,=C' = '                SET UP OUTPUT
         MVC   TERMOUT(4),ORIGTERM       SET UP OUTPUT
         CLC   TERMIN,=C'ALL '           COMPARE FOR ALL TERMS MSG
         BE    LOADALL
         LA    R6,MSGIN                  IF 1 TERM LOAD ADDR OF MSG
         B     LOADR7
LOADALL  LA    R6,MSGIN-1                IF ALL TERM LOAD ADDR OF MSG
LOADR7   LA    R7,MSGOUT                 LOAD ADDRESS OF MSG OUT AREA
LOOP     DS    0H                        MOVE MSG ROUTINE
         MVC   0(1,R7),0(R6)
         LA    R6,1(0,R6)
         LA    R7,1(0,R7)
         BCT   R5,LOOP
         MVC   IOLENGTH,=AL2(L'TOUTPUT)  GET LENGTH OF TOTAL OUTPUT
         CLC   TERMIN,=C'CNSL'           CHECK FOR CNSL MSG
         BE    SENDCNSL
*****    ROUTE  THE TABLE LIST CREATED AND SEND TEXT MSG       *****
         EXEC CICS ROUTE                                               C
              INTERVAL(0)                                              C
               LIST(TERMTAB)
         EXEC CICS SEND TEXT                                           C
              FROM(CONOUT)                                             C
              FREEKB                                                   C
              JUSLAST                                                  C
              ALARM                                                    C
              PAGING                                                   C
              LENGTH(IOLENGTH)
         EXEC CICS SEND PAGE RELEASE
         CLC   TERMIN,=C'ALL '           COMPARE FOR ALL TERMS MSG
         BE    SENDCNSL                  BR TO SEND TO TERMINAL
*****   IF SENDING TERM NOT CONSOLE SEND MSG WITH CTRL CHARACTERS *****
SENDSMSG DS    0H
         CVD   R8,TOTSENDS               CONVERT REGISTER 8 TO DECIMAL
         UNPK  NUMBOUT(5),TOTSENDS+5(3)  UNPACK TERMS SENT CTR
         OI    NUMBOUT+4,X'F0'           RESET SIGN
         CLI   NUMBOUT+2,X'F0'           COMPARE FOR LEADING 0
         BNE   MOVEIT                    IF NOT 0 BR
         MVI   NUMBOUT+2,X'40'           IF LEADING 0 MOVE SPACE TO IT
         CLI   NUMBOUT+3,X'F0'           COMPARE FOR LEADING 0
         BNE   MOVEIT                    IF NOT 0 BR
         MVI   NUMBOUT+3,X'40'           IF LEADING 0 MOVE SPACE TO IT
MOVEIT   MVC   SENTMSG+30(3),NUMBOUT+2  MOVE NUMBER OF TERMS TO OUTPUT
         CLC   ORIGTERM,=C'CNSL'         COMPARE FOR WHO SEND MSG
         BE    SENDTEXT                  IF CNSL BR - SEND NO CTRL CHAR
         MVC   SENTMSG(5),CONTRLB        MOVE CTRL CHARS TO OUTPUT
         MVC   IOLENGTH,=AL2(L'SENTMSG)  GET LENGTH OF TOTAL OUTPUT
         EXEC  CICS SEND                                               C
               FROM(SENTMSG)                                           C
               LENGTH (IOLENGTH)
         EXEC  CICS RETURN
*****    IF SENDING TERM NO CONSOLE SEND MSG W/O  CTRL CHARACTERS *****
SENDTEXT DS    0H
         MVC   IOLENGTH,=AL2(L'SENTMSG-5)
         EXEC  CICS SEND                                               C
               FROM(SENTMSG+5)                                         C
               LENGTH(IOLENGTH)
         EXEC  CICS RETURN
*****    SEND TO CNSL ROUTINE                                  *****
SENDCNSL DS    0H
         XR    R5,R5                    SUBTRACT THE CTRL CHARACTERS
         LH    R5,IOLENGTH              AND STORE LENGTH
         SH    R5,=H'5'
         STH   R5,IOLENGTH
*****    WRITE TO TD - 1 TO IEP1 FOR CONSOLE MSG               *****
*****    WRITE TO TD - 1 TO CSMT FOR LOG PURPOSES              *****
         EXEC  CICS WRITEQ TD                                          C
               QUEUE('IEP1')                                           C
               FROM(CONOUT)                                            C
               LENGTH(IOLENGTH)
         EXEC  CICS WRITEQ TD                                          C
               QUEUE('CSMT')                                           C
               FROM(CONOUT)                                            C
               LENGTH(IOLENGTH)
         AH    R8,=H'1'                 ADD CNT OF TERMS SENT MSG
         B     SENDSMSG
INITRTNE DS    0H
         MVC   EOT,=H'-1'                 MOVE EOT MARKER
         ST    R14,SAVE14               SAVE REG 14
         XR    R9,R9                    INIT REGISTER 9
         LA    R9,TERMTAB               LOAD ADDRESS OF TERMTAB
INITTAB  DS    0H
         CLI   0(R9),X'FF'              COMPARE FOR E-O-F
         BE    ENDINIT                  BRANCH IF E-O-F
         MVC   0(16,R9),=C'                ' INIT 16 BYTES TO SPACES
         LA    R9,16(0,R9)              BUMP ADDRESS UP BY 12 BYTES
         B     INITTAB                  BRANCH TO INITTAB
ENDINIT  DS    0H
         L     R14,SAVE14              RELOAD R14
         BR    R14                      AND RETURN
*****    V E R I F Y  LENGTH OF 1 TERMINAL MESSAGE                *****
VERLEN   DS    0H
         ST    R14,SAVE14              SAVE REG14
         XR    R5,R5                    INIT R5
         LH    R5,IOLENGTH              EXTRACT LENGTH
         SH    R5,=H'9'                  OF MESSAGE WITHOUT
         CH    R5,=H'1'                   TRANSID AND TERMID
         BL    TOOSHORT                    IF MSG = 0 LENGTH - GET OUT
         STH   R5,IOLENGTH              IF NOT STORE LENGTH
         L     R14,SAVE14              LOAD REG 14
         BR    R14                      RETURN
*****    V E R I F Y  LENGTH OF ALL TERMINAL MESSAGE              *****
VERLEN1  DS    0H
         ST    R14,SAVE14              SAVE REG14
         XR    R5,R5                    INIT R5
         LH    R5,IOLENGTH              EXTRACT LENGTH
         SH    R5,=H'8'                  OF MESSAGE WITHOUT
         CH    R5,=H'1'                   TRANSID AND TERMID
         BL    TOOSHORT                    IF MSG = 0 LENGTH - GET OUT
         STH   R5,IOLENGTH              IF NOT STORE LENGTH
         L     R14,SAVE14              LOAD REG 14
         BR    R14                      RETURN
*****    SEND TOO SHORT ON INPUT MESSAGE                       *****
TOOSHORT DS    0H
         CLC   ORIGTERM,=C'CNSL'        COMPARE FOR SENDING TERM
         BE    TOOSHORC
         MVC   IOLENGTH,=AL2(L'SHORTMSG)
         MVC   SHORTMSG(5),CONTRLB      MOVE CTRL CHARS IF NOT CNSL
         EXEC  CICS SEND                                               C
               FROM(SHORTMSG)                                          C
               LENGTH(IOLENGTH)
         EXEC  CICS RETURN
TOOSHORC DS    0H
         MVC   IOLENGTH,=AL2(L'SHORTMSG-5)
         EXEC  CICS SEND                                               C
               FROM(SHORTMSG+5)                                        C
               LENGTH(IOLENGTH)
         EXEC  CICS RETURN
*****    SEND TERMINAL TABLE OUT OF ROOM MESSAGE ROUTINE       *****
NOROOM   DS    0H
         CLC   ORIGTERM,=C'CNSL'        COMPARE FOR SENDING TERM
         BE    NOROOMC
         MVC   IOLENGTH,=AL2(L'NORMMSG)
         MVC   NORMMSG(5),CONTRLB       MOVE CTRL CHARS IF NOT CNSL
         EXEC  CICS SEND                                               C
               FROM(NORMMSG)                                           C
               LENGTH(IOLENGTH)
         EXEC  CICS RETURN
NOROOMC  DS    0H
         MVC   IOLENGTH,=AL2(L'NORMMSG-5)
         EXEC  CICS SEND                                               C
               FROM(NORMMSG+5)                                         C
               LENGTH(IOLENGTH)
         EXEC  CICS RETURN
*****    ROUTINE TO FOR ONE TERM ERROR WHEN TERMID CANNOT BE FOUND ****
INVIDR1  DS    0H
         MVC   INVTERM+14(4),TERMIN
         CLC   ORIGTERM,=C'CNSL'        COMPARE FOR SENDING TERM
         BE    INVIDRC
         MVC   IOLENGTH,=AL2(L'INVTERM)
         MVC   INVTERM(5),CONTRLB
         EXEC  CICS SEND                                               C
               FROM(INVTERM)                                           C
               LENGTH(IOLENGTH)
         EXEC  CICS RETURN
INVIDRC  DS    0H
         MVC   IOLENGTH,=AL2(L'INVTERM-5)
         EXEC  CICS SEND                                               C
               FROM(INVTERM+5)                                         C
               LENGTH(IOLENGTH)
         EXEC  CICS RETURN
* ROUTINE TO FOR ONE TERM ERROR WHEN TERMID IS FOUND NOT TO ACQUIRED
NOTACQ   DS    0H
         MVC   NOTACQM+14(4),TERMIN
         CLC   ORIGTERM,=C'CNSL'        COMPARE FOR SENDING TERM
         BE    NOTACQC
         MVC   NOTACQM(5),CONTRLB
         MVC   IOLENGTH,=AL2(L'NOTACQM)
         EXEC  CICS SEND                                               C
               FROM(NOTACQM)                                           C
               LENGTH(IOLENGTH)
         EXEC  CICS RETURN
NOTACQC  DS    0H
         MVC   IOLENGTH,=AL2(L'NOTACQM-5)
         EXEC  CICS SEND                                               C
               FROM(NOTACQM+5)                                         C
               LENGTH(IOLENGTH)
         EXEC  CICS RETURN
         DC    C'*************************************'
CONTRLB  DC    X'115CF01DC8'
SENTMSG  DS    0CL46
         DC    CL5' '
         DC    CL41'MESSAGE HAS BEEN SENT TO     TERMINAL(S) '
SHORTMSG DS    0CL49
         DC    CL5' '
         DC    CL44'NO MESSAGE FOUND IN INPUT - PLEASE TRY AGAIN'
NORMMSG  DS    0CL51
         DC    CL5' '
         DC    CL46'TERMINAL TABLE RAN OUT OF ROOM CONTACT SYSTEMS'
ERRORMSG DS    0CL47
         DC    CL5' '
         DC    CL42'ERROR DETECTED ON INPUT - PLEASE TRY AGAIN'
INVTERM  DS    0CL46
         DC    CL5' '
         DC    CL41'TERMINAL XXXX DOES NOT EXIST - TRY AGAIN '
         DS    0F
NOTACQM  DS    0CL46
         DC    CL5' '
         DC    CL41'TERMINAL XXXX IS NOT ACQUIRED - TRY AGAIN'
         LTORG
         END


Bryan P Changaris
Systems Programmer
Hackensack Water Company (USA)                         ) Xephon 1990


