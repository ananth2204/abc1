 Executing CICS commands from batch
Our users wanted to execute CICS commands from batch.  Instead of purchasing a
third-party software package, we developed the attached program.
To execute the program, the following JCL is required:
     //STEPXXX EXEC PGM=CICSWTO,PARM='F CICST,CEMT ...'
where PARM is the command to be executed.  If a command is attempted that is not
allowed in the program, a message is issued indicating the error and a return
code of 16 is returned to the job.
In order to provide some additional checking, a message appears on the console
indicating what command is about to be executed.  The operator has to confirm or
deny the execution by replying TYU or TNU.  An example of the message would be:
     *00 BATCH JOB ISSUING CICS xxxx COMMAND! REPLY Y OR N
where xxxx is the command being executed.  If the operator replies with a Y, the
command will be submitted to the CICS system indicated in the PARM parameter and
a return code of 00 will be returned to the job.  If the operator replies with
an N, a message will be issued and a return code of 08 will be returned to the
job.
Our environment is MVS/SP 1.3.4 with CICS 1.6.1.  This will also work with
MVS/XA 2.2.
CICSWTO
CICSWTO  CSECT
         USING CICSWTO,R15
         B     @L1
         DC    AL1(L'@L0)
@L0      DC    C'CICSWTO ASSEMBLED AT &SYSTIME ON &SYSDATE'
         DROP  R15
         USING CICSWTO,R11
@L1      STM   R14,R12,12(R13)          SAVE HIS
         LR    R11,R15                  EST ADDR
         LR    R10,R13                  SAVE R13 TEMP
         LA    R13,SAVEAREA             R13 POINTS TO MINE
         ST    R13,8(R10)               SAVE ADDR OF MINE IN HIS
         ST    R10,SAVEAREA+4           SAVE ADDR OF HIS IN MINE
         L     R1,0(R1)                 R1 POINTS TO PARM
         CLC   =X'0000',0(R1)           IS LENGTH ZERO? (NO PARM)
         BE    EOJ                      THEN GET OUT
         ST    R1,PARMADDR              SAVE ADDRESS OF PARM
*   THESE NEXT TWO LINES CAN BE REPEATED TO ALLOW MORE TRANSACTIONS
         CLC   =C'CEMT',12(R1)          IS IT AN CEMT COMMAND?
         BNE   SECRETRN                 NO, DON'T ALLOW IT RC=16
         LA    R12,*+55                 POINT TO XXXX IN WTOR MESSAGE
         MVC   0(4,R12),12(R1)          MOVE COMMAND TO XXXX
WTOR     XC    ECB,ECB                  CLEAR OUT ECB
         WTOR  'BATCH JOB ISSUING CICS XXXX COMMAND! REPLY Y OR N',    X
               REPLY,1,ECB
         WAIT  ECB=ECB
         CLI   REPLY,C'N'
         BE    BADRETRN                 ISSUE MESSAGE RC=8
         CLI   REPLY,C'Y'
         BNE   WTOR                     REISSUE INITIAL WTOR
         LA    R1,PARMADDR              R1 POINTS TO PARM
         LINK  EP=CICSVC34              ISSUE SVC 34
         B     EOJ
BADRETRN MVC   RETCODE,BADCODE
         WTO   'CICS COMMAND TERMINATED DUE TO OPERATOR REPLY',        X
               ROUTCDE=2
         B     EOJ
SECRETRN MVC   RETCODE,SECCODE
         LA    R12,*+26                 POINT TO XXXX IN WTO MESSAGE
         MVC   0(4,R12),12(R1)          MOVE COMMAND TO XXXX
         WTO   'COMMAND XXXX NOT ALLOWED THROUGH BATCH',ROUTCDE=2
EOJ      L     R15,RETCODE              LOAD RETURN CODE
         L     R13,SAVEAREA+4           RESTORE R13
         L     R14,12(R13)              RESTORE R14
         LM    R0,R12,20(R13)           RESTORE R0->R12
         BR    R14                      GOODBYE
 SECCODE  DC    F'16'
BADCODE  DC    F'8'
RETCODE  DC    F'0'
ECB      DC    F'0'
REPLY    DC    C' '
PARMADDR DS    F
SAVEAREA DC    18F'-1'
         LTORG
         END   CICSWTO
CICSVC34
         START 0
         PRINT &SYSPARM.GEN
CICSVC34 CSECT
         USING CICSVC34,R15
         B     INT
         DC    C'CICSVC34 ASSEMBLED AT &SYSTIME ON &SYSDATE'
         DC    C'LINKED WITH AC=1'
         DROP  R15
         USING CICSVC34,R11
INT      STM   R14,R12,12(R13)          SAVE HIS
         LR    R11,R15                  ESTABLISH BASE REG
         L     R1,0(R1)       LLTTT...  POINT TO PARM
         LH    R4,0(R1)                 R4 = LL
         LTR   R4,R4
         BZ    MISPARM                  GO ISSUE NO PARM MSG
         CH    R4,H80
         BH    BADPARM                  GO ISSUE BAD PARM
         LA    R5,2(R1)                 R5 POINTS TO PARM TEXT
         LR    R3,R4
         AH    R3,H6                    ADD 6 TO LENGTH FOR LLZZ +2
         STH   R3,COMLEN
         SH    R4,H1                    DECREMENT LENGTH FOR REL 0
         STC   R4,MOVTXT+1              MODIFY MOVE LENGTH OF NEXT MVC
MOVTXT   MVC   COMTXT,0(R5)             MOVE PARM TEXT TO COMMAND BUF
         MODESET KEY=ZERO,MODE=SUP      PUT PROG IN SUPER STATE
         SR    R0,R0                    0 IS COMMAND ORIGINATOR
         LA    R1,COMLEN                R1 POINTS TO COMMAND
         SVC   34                       ISSUE COMMAND
         MODESET KEY=NZERO,MODE=PROB    GET OUT OF SUPER STATE
EXIT     LM    R14,R12,12(R13)          RESTORE REGS
         SR    R15,R15                  ZERO RETURN CODE
         BR    R14                      RETURN
H1       DC    H'1'
H6       DC    H'6'
H80      DC    H'80'
COMLEN   DC    F'0'           LLZZ
COMTXT   DC    82C' '         TTTTTTT
MISPARM  WTO   ' ** MSG ** PARM WAS MISSING FOR CICSVC34'
         LM    R14,R12,12(R13)          RESTORE REGS
         LA    R15,4                    ISSUE RETURN CODE 4
         BR    R14                      RETURN
BADPARM  WTO   ' ** MSG ** PARM EXCEEDED 80 BYTES FOR CICSVC34'
         LM    R14,R12,12(R13)          RESTORE REGS
         LA    R15,8                    ISSUE RETURN CODE 8
         BR    R14                      RETURN
         END


Marilyn Carlson
System Support Analyst
UOP Inc (USA)  ) Xephon 1988





