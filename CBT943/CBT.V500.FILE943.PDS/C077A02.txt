Automating CICS/ESA dump printing with REXX


Background

One of the many enhancements to CICS/ESA is in the
transaction dump print utility program, DFHDUP. Unlike the
earlier CICS releases where all dumps in the dump dataset are
printed, CICS/ESA's DFHDUP allows dumps to be selected by
control cards specified in the SYSIN DDNAME. In addition,
one can code a SCAN control statement to get an index of the
dumps contained in DFHDMPA and DFHDMPB.

Taking advantage of these new features, I have written a
REXX EXEC using ISPF's table services to fully automate the
entire dump selection and printing process.

TSO/ISPF was chosen as opposed to CICS as the execution
environment for dump selection, printing, and analysis because
most programmers invariably go to TSO/ISPF to access their
source and to compile or assemble their programs. In addition,
TSO/ISPF has a lot of rich built-in features one can use in
navigating through a dump. One can even go one step further
to enhance this utility by building ISPF's edit macros as
commands to locate commonly accessed dump areas such as
the trace table, EIBLOCK, TACB, etc, interactively - a feature
not unalike those found in IPCS or PD/MVS.

This utility, with some enhancements, can potentially replace
or nullify a need to purchase a third-party product for
transaction dump printing and analysis.

Related publications detailing the DFHDUP utility, REXX, and
ISPF's table services are as follows:

o      CICS/ESA Operations Guide SC33-0668

o      Procedures Language MVS/REXX User's Guide SC28-1882

o      Procedures Language MVS/REXX Reference SC28-1883

o      ISPF Dialog Management Services and Examples SC34-4113.


Implementation and Installation Notes

This print dump utility was written using CICS/ESA Version
3.2.1 and TSO/ISPF Version 3.2. To install it, copy the source
for the REXX EXEC RXDFHDUP to an ISPF library
concatenated in either the SYSPROC or SYSEXEC
DDNAME. Change the CICS/ESA LOADLIB dataset, the
DFHDMPA/DFHDMPB datasets, and the job card information
to suit those used in your shop. Copy the source for the panel
RXDFHDUP to an ISPF ISPPLIB library.


Usage and Execution Notes

To invoke the REXX EXEC, type 'TSO RXDFHDUP' in any
ISPF screen that accepts commands. The user will be prompted
for a CICS region-id.

RXDFHDUP then makes two foreground executions of
DFHDUP to scan the index of DFHDMPA and DFHDMPB
respectively. An ISPF TBDISPLAY service is subsequently
called to display the dump selection list sorted by date and time
in descending sequence. Figure 1 shows the format and
contents of the selection panel.

The user types an 'S' line command to select the dump to be
printed. RXDFHDUP then builds batch DFHDUP JCL and
submits it in background mode. The dump selection panel is
redisplayed for the user to select another dump or to press PF3
to terminate execution.

The user can then analyse the dump interactively by browsing
the job's held output queue using ISPF's standard navigation
commands or release the output queue for hardcopy analysis.


Enhancement Options

Instead of routing the dump output to SYSOUT, one can print
it to a file and thereby take advantage of ISPF's powerful edit
macros to build commands for dump analysis. For example,
one could create 'TACB' or 'EIB' commands to facilitate
access to these control blocks. Shown below are edit macro
members illustrating how to do this. These members need to be
placed in a SYSPROC library.

Member TACB

  ISREDIT MACRO NOPROCESS
  ISREDIT FIND TACB FIRST

Member EIB

  ISREDIT MACRO NOPROCESS
  ISREDIT FIND EIB FIRST

Although the examples shown are simple, you can build from
these to enhance them to your liking.

One enhancement would be to execute the dump printing in
foreground mode (ie instead of a batch SUBMIT you can do a
CALL on DFHDUP). In this way, one can code a modification
in the REXX EXEC to edit the dump automatically once
printing is completed.

RXDFHDUP REXX EXEC source code

Copy this as member RXDFHDUP to a SYSPROC or
SYSEXEC library.

/* REXX - RXDFHDUP ************************************************** */
/* Function: CICS transaction dump selection and printing             */
/* Input   : Region ID and dump selection from user.                  */
/*           DFHDMPA/DFHDMPB datasets                                 */
/* Output  : Dump selection screen                                    */
/*           Batch JCL to print dump                                  */
/* Called objects:                                                    */
/*           Load module  - DFHDUP                                    */
/*           ISPF panel   - RXDFHDUP                                  */
/*           ISPF services- various table services                    */
/* Pseudocode:                                                        */
/*   Initialization                                                   */
/*     Prompt user for region ID                                      */
/*     Assign and allocate files                                      */
/*     Scan index of DFHDMPA                                          */
/*     Scan index of DFHDMPB                                          */
/*   Mainline                                                         */
/*     Read dump index file                                           */
/*     Format and sort index entries                                  */
/*     Display selection screen                                       */
/*     Build batch JCL to print dump                                  */
/*   Finalization                                                     */
/*     Exit                                                           */
/* ****************************************************************** */
  SIGNAL ON ERROR
/* ************************************************************ */
/* INITIALIZATION                                               */
/* ************************************************************ */
/* Prompt user for region id                                          */
  SAY  'Enter CICS Region ID (RGT3,RGT4,RGT5)'
  PULL regid
/* Assign dataset names                                              */
  userid   = userid()
  tick     = ''''
  dumpds   = 'DFHDMPA'
  dump_dsn = tick||'CICS.R321.'||regid||'.'||dumpds||tick
  prnt_dsn = tick||userid||'.TEMP.DFHPRINT'||tick
  indx_dsn = tick||userid||'.TEMP.DFHTINDX'||tick
syin_dsn = tick||userid||'.TEMP.SYSIN(SCANCARD)'||tick
/* Allocate ddnames                                                  */
  SIGNAL OFF ERROR
  "FREE FI(DFHDMPDS,DFHPRINT,DFHTINDX,SYSIN)"
  SIGNAL ON ERROR
  "ALLOC FI(DFHDMPDS) DA("dump_dsn") SHR"
  IF SYSDSN(prnt_dsn) = 'OK' THEN
    "ALLOC FI(DFHPRINT) DA("prnt_dsn") OLD "
  ELSE
    "ALLOC FI(DFHPRINT) DA("prnt_dsn") NEW ",
    "  DSORG(PS) SPACE(1,1) TRACKS",
    "  LRECL(133) BLKSIZE(6650)"
  IF SYSDSN(indx_dsn) = 'OK' THEN
    "ALLOC FI(DFHTINDX) DA("indx_dsn") OLD "
  ELSE
    "ALLOC FI(DFHTINDX) DA("indx_dsn") NEW ",
    "  DSORG(PS) SPACE(1,1) TRACKS",
    "  LRECL(080) BLKSIZE(3120)"
  IF SYSDSN(syin_dsn) = 'OK' THEN
    "ALLOC FI(SYSIN)    DA("syin_dsn") OLD "
  ELSE
    "ALLOC FI(SYSIN)    DA("syin_dsn") NEW ",
    "  DSORG(PO) SPACE(1,1) TRACKS DIR(20)",
    "  LRECL(080) BLKSIZE(3120)"
  PUSH '  SELECT TYPE=SCAN'
  "EXECIO 1 DISKW SYSIN (FINIS"
/* Scan index of DFHDMPA                                    */
  SAY  'Scanning DFHDMPA index ...'
  "CALL 'CICS.R321.SDFHLOAD(DFHDUP)' 'SINGLE,LC'"
/* Re-allocate and reassign files for second run            */
  "FREE  FI(DFHDMPDS,DFHPRINT,DFHTINDX)"
  dumpds = 'DFHDMPB'
  dump_dsn = tick||'CICS.R321.'||regid||'.'||dumpds||tick
  "ALLOC FI(DFHDMPDS) DA("dump_dsn") SHR "
  "ALLOC FI(DFHPRINT) DA("prnt_dsn") MOD "
  "ALLOC FI(DFHTINDX) DA("indx_dsn") MOD "
/* Scan index of DFHDMPB                                    */
  SAY  'Scanning DFHDMPB index ...'
  "CALL 'CICS.R321.SDFHLOAD(DFHDUP)' 'SINGLE,LC'"
/* ************************************************************ */
/* MAINLINE                                                     */
/* ************************************************************ */
/*  TBCREATE- create table with the following columns:          */
/*            LINEOUT - dump entries produced by DFHDUP program */
/*            DUMPDS  - source of dump (DFHDUMPA or DFHDUMPB)   */
/*  TBTOP   - move table pointer to top row.                    */
"ISPEXEC TBCREATE rxdfhdup NAMES (lineout dumpds) NOWRITE"
"ISPEXEC TBTOP    rxdfhdup"
/* Read in dump entries from index file produced by DFHDUP  */
/* and put them in a stack.                                 */
"EXECIO * DISKR DFHTINDX (STEM retline. FINIS"
/* Format index entries                                  */
/* If first line indicate that there is dump then proceed*/
IF SUBSTR(retline.1,13,5) = 'INDEX'
THEN
/* Move dump entries from stack and add row to table (TBADD).*/
/* If line contains dump dsn save and read next line.        */
  DO i = 1 TO retline.0
    LINEOUT = SUBSTR(retline.I,2,70)
    IF SUBSTR(lineout,12,5) = 'INDEX'
    THEN
      DO
      dumpds = SUBSTR(lineout,53,7)
      i = i + 1
      ITERATE
    END
  "ISPEXEC TBADD rxdfhdup"
  END
/* Sort index.                                                */
"ISPEXEC TBSORT  rxdfhdup FIELDS(lineout,C,D)"
/* Prepare table display panel.  Initialize variables.        */
msg=' '; csr=' '; csrr=1; rc=0
/* Display selection screen; build batch JCL                  */
SIGNAL OFF ERROR
DO FOREVER
  "ISPEXEC TBDISPL rxdfhdup PANEL(rxdfhdup)",
  "    MSG(&msg) CURSOR(&csr) CSRROW(&csrr)",
  "    POSITION(crp) AUTOSEL(NO)"
  IF rc = 8 THEN LEAVE        /* if PF3 pressed then exit loop */
  CSR=CMD
  PARSE VAR lineout dumpid applid ddate dtime tran abend
  dump_dsn = tick||'CICS.R321.'||regid||'.'||dumpds||tick

  SAY 'Dump selected:' lineout
  SAY 'Press enter to proceed'
/* Re-allocate files */
  "FREE  FI(SYSIN)"
  syin_dsn=tick||userid||'TEMP.SYSIN(JOBJCL)'||tick
  "ALLOC FI(SYSIN)    DA("syin_dsn") OLD "
/* Build batch JCL */
  QUEUE '//'||userid||'D JOB (USA00640,SY,Z000000),CICSDUMP,'
  QUEUE '//  CLASS=A,MSGCLASS=H,REGION=1024K,NOTIFY='||USERID
  QUEUE '//PRINT  EXEC PGM=DFHDUP,PARM=SINGLE'
  QUEUE '//STEPLIB  DD DISP=SHR,DSN=CICS.R321.SDFHLOAD'
  QUEUE '//DFHDMPDS DD DISP=SHR,DSN='dump_dsn
  QUEUE '//DFHTINDX DD SYSOUT=*'
  QUEUE '//DFHPRINT DD SYSOUT=*,DCB=(BLKSIZE=133)'
  QUEUE '//SYSIN    DD *'
  QUEUE '  SELECT TYPE=AND'
  QUEUE '  DUMPID='||dumpid
  QUEUE '  END'
  "EXECIO * DISKW SYSIN(FINIS"
  "SUBMIT" syin_dsn
END
/* ************************************************************ */
/* FINALIZATION                                                 */
/* ************************************************************ */
"ISPEXEC TBCLOSE rxdfhdup"
"FREE FI(DFHDMPDS,DFHPRINT,DFHTINDX,SYSIN)"
RETURN
  EXIT
/* ************************************************************ */
/* CALLED ROUTINES                                              */
/* ************************************************************ */
/* Error handling routine.  Displays error line and return code */
ERROR:
  SAY 'Error on Line: ' SIGL 'RC=' RC
 EXIT


RXDFHDUP ISPF panel definition

Save this as member RXDFHDUP in an ISPPLIB library.

)Attr
  ! type (output) intens (low)   just (left ) caps (off) pad (' ')
  Ý type (input)  intens (high ) just (left ) caps (on) padc ('_')
)Body expand(//)
%-/-/- CICS Transaction Dumps - &REGID -/-/-
%Command ===>_PCMD                                / / %Scroll ===>+_AMT   +
+
%S DUMPID      APPLID     DATE       TIME    TRANID     ABEND  DUMPDS
+
)Model
ÝZ!lineout                                                    !dumpds
)Init
 .zvars = '(CMD)'
 &cmd=' '
)Reinit
 &cmd=' ' refresh (cmd)
)Proc
 ver (&cmd, nonblank)
)End


Larry Prestosa (USA)

