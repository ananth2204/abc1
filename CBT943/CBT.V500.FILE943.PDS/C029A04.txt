 Closing VSE/VSAM user catalogs
If a CICS VSE/VSAM user catalog is re-organised, ie it has a volume added or
removed in its IDCAMS definition (or a device address changed), operator
intervention messages will be issued if any of its datasets are re-opened.  The
CICS partition will wait on the reply.  Similar problems can arise if volumes
are changed for CICS VSAM datasets.  The problem is that the CICS control block
information for the user catalog cannot be refreshed until CICS is restarted,
since it cannot be closed and re-opened in the CICS partition.  The following
Assembler CICS command-level program allows a user catalog to be closed.
CATCLOSE assumes that all owned CICS files are closed.  It issues a VSAM SHOWCAT
macro to obtain the ACB address required for the close.   It should be noted
that SHOWCAT requires register 13 to point to a 72-byte VSAM save area.  Since
CICS objects to such unauthorised use of register 13, it is saved just before
the SHOWCAT and restored just after it.  Some useful diagnostics are included in
case of problems with the SHOWCAT or CLOSE.
CATCLOSE was first developed to overcome problems associated with VSAM user
catalogs on demountable 3330 DASD.  But it may also be of use where user
catalogs are frequently reorganised and CICS up-time is of critical importance.
There is no need to explicitly re-open the user catalog - this is done
automatically when any of its files are re-opened.  The program (with file names
changed) was tested under VSE/SP CICS 1.6.0.  A CICS transaction (TWASIZE=512)
needs to be defined to drive CATCLOSE.
*** REGISTER USAGE                                         ***
*   R5  - SAVE REGISTER FOR R13                              *
*   R6  - VSAM WORK AREA PTR                                 *
*   R7  - CATALOG NAME PTR, CATALOG ACB PTR                  *
*   R8  - FILE NAME PTR                                      *
*   R9  - VSAM WORK AREA BASE ADDRESS                        *
*   R11 - PROGRAM BASE                                       *
*** R13 - VSAM SAVE AREA PTR - ON BRIEF LOAN FROM CICS     ***
* DEFINE TWA VSAM AREAS AND MSG FIELDS
VSWABAR  EQU   R9
VSAMWA   DSECT
         USING VSAMWA,VSWABAR
         DS    0F                 FULL-WORD ALIGN
VSAMSAVE DS    CL72               VSAM SAVE AREA
VSAMAREA EQU   *                  VSAM WORK AREA
AREALEN  DS    H                  WORK AREA LENGTH
CTLINFO  DS    H                  FOR VSAM USE
CATACB   DS    F                  (RETURNED) ACB ADDRESS
FILL1    DS    CL56               64 BYTES IS MINIMUM WORKAREA
VSAMALEN EQU   *-VSAMAREA
         ORG   CTLINFO            RETURN AREA IF SHOWCAT FAILS
RETCODE  DS    XL2                VSAM RETURN CODE
RESCODE  DS    XL2                VSAM REASON CODE
FILL2    DS    CL58
MSGAREA  DS    CL80               MSG LINE 1
MSGLEN   EQU   *-MSGAREA
         ORG   MSGAREA+NAMELEN    POINT PAST CATNAME
USERMSG  DS    CL35
NEW1     DS    XL1                NL CHAR
MSGLIN2  DS    CL79               MSG LINE 2
NEW2     DS    XL1                NL CHAR
MSGLIN3  EQU   *                  MSG LINE 3
R15VALUE DS    CL11               DIAGNOSTIC INFO
FILL3    DS    CL10
RETC     DS    CL11
FILL4    DS    CL10
RESC     DS    CL11
FILL5    DS    CL26
NEW3     DS    XL1
WORK1    DS    F                  WORK FIELDS FOR HEX CONVERSION
WORK2    DS    XL10
VSAMLEN  EQU   *-VSAMWA           TOTAL LENGTH REQD FOR TWA
DFHEISTG DSECT
TWALENG  DS    H
 CATCLOSE CSECT
         BALR  R11,0              SET PROGRAM BASE
         USING *,R11
         EXEC  CICS HANDLE CONDITION ERROR(ERRORS)
         B     START               GO AROUND ERROR HANDLER
ERRORS   EXEC  CICS DUMP DUMPCODE('CATC')      JUST GET A DUMP
         B     EXIT                            AND BAIL OUT
* SET UP ADDRESSABILITY FOR WORKAREAS
START    EXEC  CICS ASSIGN TWALENG(TWALENG)    HAVE WE GOT A TWA?
         OC    TWALENG,TWALENG
         BZ    ERRORS                          IF NOT, GIVE UP HERE
         EXEC  CICS ADDRESS TWA(VSWABAR)       GET WORKAREA ADDRESS
* MOVE CATALOG NAME TO MSGAREA
         MVC   MSGAREA(NAMELEN),CATNAME
* SET UP PARMS FOR SHOWCAT - NOTE THAT R13 MUST POINT TO VSAM
* SAVE AREA. R13 IS SAVED AND RESTORED JUST AFTER SHOWCAT
         MVC   AREALEN,=AL2(VSAMALEN)       SET WORKAREA LENGTH
         LR    R5,R13                       SAVE 13
         LR    R13,VSWABAR                  R13 -> SAVE AREA
         LA    R6,VSAMAREA                  R6  -> WORK AREA
         LA    R7,CATNAME                   R7  -> CAT NAME
         LA    R8,FILENAME                  R8  -> FILE NAME
* ISSUE SHOWCAT TO GET ACB ADDRESS
         SHOWCAT NAME=(8),CATDSN=(7),AREA=(6)
         LR    R13,R5                           RESTORE R13 ASAP
         LTR   R15,R15
         BZ    CLOSEIT                          CLOSE IF OK
         MVC   USERMSG(ERRLEN),SHOWFAIL         IDENTIFY ERROR
         B     FAILRTN                          AND ANALYSE IT
* CLOSE USERCATALOG
CLOSEIT  LA    R7,CATACB                        R7  -> ACB
         CLOSE (7)
         LTR   R15,R15
         BZ    ALLOK                            GO IF NORMAL CLOSE
         MVC   USERMSG(ERRLEN),CLOSFAIL         IDENTIFY ERROR
         B     FAILRTN                          AND ANALYSE IT
* SEND 'CATALOG CLOSED' MSG
ALLOK    MVC   USERMSG(L'CLOSEOK),CLOSEOK
         EXEC  CICS SEND TEXT FROM(MSGAREA) LENGTH(80) ERASE
EXIT     EXEC  CICS RETURN
FAILRTN  MVC   MSGLIN2(MSGLEN),ERRLINE          PREPARE HEADERS
         MVI   MSGLIN3,C' '                    CLEAR REST OF MSG
         MVC   MSGLIN3+1(L'MSGLIN3-1),MSGLIN3
         MVI   NEW1,X'15'                       3 MSG LINES
         MVI   NEW2,X'15'
         MVI   NEW3,X'15'
* MOVE R15 VALUE TO ERROR MSG
         ST    R15,WORK1                        CONVERT R15
         UNPK  WORK2(9),WORK1(5)                TO PRINTABLE HEX
         TR    WORK2(8),HEXTAB-240
         MVC   R15VALUE,WORK2                   MOVE TO MSG
* PRINT VSAM RETURN & REASON CODES IF SHOWCAT, ELSE JUST GIVE R15
         CLC   USERMSG(ERRLEN),SHOWFAIL
         BE    SHOWMSG
         MVI   RETC+10,C'?'                     NO CODES GIVEN
         MVI   RESC+10,C'?'                     FOR CLOSE
         B     ERRMSG
* VSAM RETURN CODE
SHOWMSG  XC    WORK1,WORK1
         MVC   WORK1+2(2),RETCODE
         UNPK  WORK2(9),WORK1(5)
         TR    WORK2(8),HEXTAB-240
         MVC   RETC(L'WORK2),WORK2
* VSAM REASON CODE
         XC    WORK1,WORK1
         MVC   WORK1+2(2),RESCODE
          UNPK  WORK2(9),WORK1(5)
         TR    WORK2(8),HEXTAB-240
         MVC   RESC(L'WORK2),WORK2
* SEND ERROR MSG (3 LINES)
ERRMSG   EXEC  CICS SEND TEXT FROM(MSGAREA) LENGTH(240) ERASE
         B     EXIT                           RETURN TO CICS
CLOSEOK  DC    CL36'*** NORMAL CLOSE ***'
CLOSFAIL DC    C'CLOSE FAILED  '
SHOWFAIL DC    C'SHOWCAT FAILED'
ERRLEN   EQU   *-SHOWFAIL
FILENAME DC    CL44'ANY.FILE.IN.USER.CATALOG'
CATNAME  DC    CL44'USER.CATALOG.NAME'
NAMELEN  EQU   *-CATNAME
ERRLINE  EQU   *
R15HEAD  DC    CL11'(HEX) R15'         ERROR MSG HEADERS
FILLA    DC    CL10' '
RETHEAD  DC    CL11'RETURN CODE'
FILLB    DC    CL10' '
RESHEAD  DC    CL11'REASON CODE'
FILLC    DC    CL26' '
         DC    X'15'
HEXTAB   DC    C'0123456789ABCDEF'      HEX CHARS FOR TRANSLATE
         END


David Roth
Computer Centre Director
Australian High Commission (UK)    ) Xephon 1988






































