Deleting outstanding AIDs

One of the most annoying problems with CICS is the deletion
of dynamically-installed terminals failing because of 'pending
AIDs'.  CICS does not yet provide the facility to delete
pending AIDs, nor does it allow for dynamically-installed
terminals to be deleted if there is any outstanding work.

The reasons that pending AIDs exist are many and varied,
including hardware errors and network managers timing out
user sessions.  In most cases CICS is processing work on
behalf of a terminal when a terminal error occurs and CICS is
notified of a LOSTERM condition.  CICS will attempt to
delete the terminal session, but in some cases this is not
possible because of the outstanding work.

It is possible in some networks for the terminal session to be
reactivated by logging on to the CICS system with the same
network name and CICS terminal-id.  However, when using
network managers such as NetView/Access and Net/Master,
the network name could well be different and the CICS-id the
same, or vice versa, which would prevent the session from
being re-established.  The hung terminal cannot be deleted
without first removing the AID, which in most cases is
impossible, and a restart of CICS is required.

IBM has recognized the problem, and the following quote is
from APAR PL41689, INFO entry E345516:

      "The requirement that outstanding work, ie an AID, should
      not prevent the deletion of a terminal, either auto-installed
      or one installed using RDO, is recognized.  CICS will
      provide a mechanism to address this requirement."

I have been advised by IBM that this mechanism is not
installed in any versions of CICS currently available.

As this situation was occurring at least four times a day, and
the network help desk and systems programmers were spending
about an hour a day trying to correct problems, an automatic
solution was required.  The following code was added to the
VTAM Node Error Program, DFHZNEP, to ensure that AIDs
are automatically deleted prior to CICS performing the terminal
deletion.

This program is currently running in CICS 2.1.1 with
MVS/ESA, and in the 6 months that it has been running, no
problems with 'hung terminals' have been reported.  This
represents a saving of 180 hours at systems programmer
charge-out rates.

The coding uses an undocumented CICS macro, DFHAL, and
IBM has verified that this is the correct way to delete AIDs.


PROGRAM SOURCE CODE

* THIS ERRPROC HANDLES 'SESSION TERMINATED' REQUESTS.
* IE ALL ATTEMPTS BY CICS TO TERMINATE A SESSION ARE PASSED
* THROUGH THIS CODE PRIOR TO CICS ATTEMPTING TO DELETE THE
* TERMINAL.
          DFHAL TYPE=AID
AIDCBAR   EQU   6
          DFHSNEP TYPE=ERRPROC,GROUP=04,CODE=(49)
          STM   R14,R11,TWAEPRS             SAVE ENTRY REGISTERS
          LR    NEPBAR,R15
          USING NEPROC04,NEPBAR             PROC BASE REGISTER
          LR    R3,TCTTEAR                  SAVE ADDRESS OF TCT
          CLI   TCTTETT,TCTTET37            3270 RANGE ?
          BL    NEP04#99                    NO ... DONT PROCESS
          CLI   TCTTETT,TCTTETL6            3270 RANGE ?
          BH    NEP04#99                    NO ... DONT PROCESS
          NI    TCTTEIC,255-TCTTEOIC        TURN OFF TIME CONTROL
          USING DFHAIDDS,AIDCBAR
NEP04#1   DS    0H
* SCAN THE AID CHAIN FOR AN AID WHICH IS RELATED TO OUR
* TERMINAL. OUR TERMINAL IS ADDRESSED BY R3.
          L     R1,CSATCTBA                 ADDRESS TCT PREFIX
          L     TCTTEAR,TCTVSEBA-DFHTCTFX(R1)
NEP04#5   DS    0H
          LTR   TCTTEAR,TCTTEAR             IS THERE A SYSTEM ENTRY ?
          BZ    NEP04#99                    NO ... FINISH
          LA    R2,TCSEAID                  SIGNIFIES END OF CHAIN
          L     AIDCBAR,TCSESUSF            GET FIRST AID ADDRESS
NEP04#10  DS    0H
          CR    R2,AIDCBAR                  ANY MORE AIDS ?
          BE    NEP04#15                    NO ...
          CLC   AIDTRMID,0(R3)              DOES IT BELONG TO OUR TERM ?
          BE    NEP04#20                    YES ...
          L     AIDCBAR,AIDCHNF             ADDRESS NEXT AID
          B     NEP04#10                    GO CHECK IT OUT
NEP04#15  DS    0H
          L     TCTTEAR,TCSENEXT            ADDRESS NEXT SYSTEM ENTRY
          B     NEP04#5
NEP04#20  DS    0H
* WE HAVE FOUND AN AID, SO DELETE IT.
          DFHAL TYPE=UNCHAIN,PLIST=AIDCBAR
          CLI   AIDTYPE,AIDPUT              IS THERE A TS QUEUE FOR IT
          BNE   NEP04#25                    NO ...
          MVC   TCAICQID,AIDRQID            YES ... GET REQID
          DFHTS TYPE=RELEASE                AND DELETE TS QUEUE
NEP04#25  DS    0H
          ST    AIDCBAR,TCASCSA             ADDRESS OF AID
          DFHSC TYPE=FREEMAIN               FREE STORAGE FOR AID
          B     NEP04#1                     GO SEE IF MORE AIDS FOR
*                                           THIS TERMINAL
NEP04#99  DS    0H
          LR    TCTTEAR,R3                  RESTORE TCT ENTRY ADDR
          LM    R14,R11,TWAEPRS             RESTORE ENTRY REGISTERS
          BR    R14                         RETURN TO CALLER
          LTORG


Malcolm Barr
Consultant (Australia)                         c Malcolm Barr 1991

