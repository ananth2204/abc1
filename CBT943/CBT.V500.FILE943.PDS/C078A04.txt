CICS sign-on security


INTRODUCTION

We are a medium-sized company running VM/SP 5.1, VSE/SP
4.1.2, and CICS 1.7.0 on an HDS EX 25. Our users had to
sign on to CICS, then again to many of our application
packages. As the number of users grew, multiple sign-ons
compounded, and our auditors wanted tighter front-end
security. We were faced with acquiring a third-party security
package or developing our own system.


SOLUTION

We did not want the expense of a third-party security system,
so the IBM-supplied security exit program DFHXSE has been
modified to provide a security system with the following
features:

o      Passwords revised by users every three months and archived

o      Password encryption and maintenance outside the CICS partition

o      Password length between 5 and 8 characters

o      All invalid sign-on attempts logged on the system console

o      Disable terminal and CICS-id after five invalid sign-on attempts.

This system has evolved over the last three years and has
worked without any failures in this time.


INSTALLATION STEPS

Installing the security system involves performing the
following steps.

1      Define the security file (VSAM KSDS).
2      Create the translation table for the batch program to
      encrypt  the passwords and assemble the batch program.
3      Assemble the BMS map.
4      Assemble the terminal disable program - note it is written
      in command-level Assembler.
5      Copy the translation table from step two into DFHXSE
      code and assemble it.
6      Revise and assemble the required CICS tables.
7      Add the security file DLBL to the CICS start-up JCL and
      cycle it.


PASSWORD FILE DEFINITION

The password file is a VSAM KSDS with the key being the
CICS operator name (full 20 characters). Please note the share
options are set so the file can be updated from both the CICS
partition and from other partitions to allow batch updates by the
security officer.

// EXEC IDCAMS,SIZE=AUTO
  DELETE YOUR.FILE.NAME CL PURGE CAT(YOUR.USER.CATALOG)
  DEFINE CLUSTER (                    -
         NAME(YOUR.FILE.NAME)         -
         INDEXED                      -
         RECORDSIZE(80 80)            -
         RECORDS(350 20)              -
         KEYS(20 0)                   -
         FREESPACE(5 5)               -
         SHAREOPTIONS(3 3)            -
         VOLUMES(XXXXXX))             -
   DATA (NAME(YOUR.FILE.NAME.DATA))   -
  INDEX (NAME(YOUR.FILE.NAME.INDEX))  -
  CATALOG(YOUR.USER.CATALOG)

BATCH PASSWORD FILE UPDATE

This batch program is used to maintain the password file. It
performs adds for new users, revises passwords (in case
someone forgets), revises operator-ids, and deletes obsolete
users. You must create your own translation table in this
program - only the first and last line are included.

***    THIS PROGRAM IS TO PROVIDE TECHNICAL SUPPORT WITH THE ABILITY
*      TO LOAD INITIAL USER DATA TO THE PASSWORD FILE AND ALSO TO
***    CHANGE A PASSWORD FOR A USER IF THEY HAPPEN TO FORGET IT.
DFHASC   START
*    REGISTER USAGE R10 - USER RESPONSE CODE VSAM ERRORS
*                   R11 - BASE REGISTER
*                   R13 - ADDRESS OF 72-BYTE VSAM WORK AREA
*                   R15 - VSAM RESPONSE CODE
BEGIN    BALR R11,0                ASSIGN AND SET BASE
         USING *,R11
         OPEN  CARDIN,DFHCAJS,PRINTER  OPEN ALL FILES
         MVI   CTLCHAR,X'8B'       INITIALIZE CONTROL CHARACTER
         PUT   PRINTER,PRINT       WRITE TOP FOR PRINTER
         LA    R13,VSEVSMWK        LOAD ADDR 72-BYTE VSAM WORK AREA
*                                  CHECK PSW FILE AVAILABLE
         TESTCB ACB=DFHCAJS,AM=VSAM,ERET=OPNFAIL,OFLAGS=OPEN
         BE    GOODOPEN            ..GOOD OPEN GO TO READ CARD LOOP
OPNFAIL  DS    0H                  OPEN ERROR OF ANY KINK
         LA    R10,X'55'           OPEN FAIL RETURN CODE
         B     BADEOJ              GO TO JOB ABORT
GOODOPEN DS    0H
         COMRG                     GET ADDRESS OF COMM REGISTER
         MVC   TODDATE,0(R1)       GET DATE FROM COMM AREA
READCARD DS    0H                  SUCCESSFUL OPEN TESTED
         GET   CARDIN,CARDREC      READ CARD AND MOVE TO RECD LAYOUT
         MVC   USIDKEY,INITKEY     INITIALIZE USER ID KEY
         MVC   USIDKEY,CRUSERID    SETUP KEY FROM CARD RECORD
         CLI   CRDELREC,X'C4'      CHECK FOR RECORD DELETION
         BE    DELRECD              GO TO DELETE ROUTINE
         CLI   CRCHGOP,X'C3'       CHECK FOR OP-ID CHANGE
         BE    CHGOPID              GO TO CHANGE OP-ID ROUTINE
         CLI   CRADDPSW,X'40'      CHECK 1ST OF PSWD FOR ADD
         BE    CHGEPSWD            NOT AN ADD NEW PERSON AND PASSWD
*   ADD ROUTINE FOR NEW ID
         MVC   PSFUSRID,CRUSERID   SET UP PSWFILE USER ID FIELD
         MVC   PSFOPID,CROPID      SET UP PSWFILE OPERATOR ID FIELD
         MVC   PSFPSWD,CRADDPSW    SET UP PSWFILE OLD PSWD FIELD
         MVC   PRINT1ID,CRUSERID   SET UP PRINT LINE MSG
         MVC   PRINT,PRINT1        SET UP PRINT LINE
         TR    PSFPSWD,LINEUPA     TRANSLATE PSWD
         MVC   PSFCDATE,EXPDATE    FORCE AN EXPIRED DATE FOR ADDED RCD
         MVC   PSFUDATE,=C'        ' INIT LAST USED DATE
         MVC   PSFDDATE,=C'        ' INIT DISABLED DATE
         MVC   PSFLAST,=C'        '  INIT PREVIOUS PSWD
         MVC   PSF2LAST,=C'        ' INIT 2ND PREVIOUS PSWD
         MVC   PSFSNATT,=P'000'      INIT NUMBER OF SIGN-ON ATTEMPTS
         MVC   PSFILLR,=C'       '   INIT FILLER
         PUT   RPL=ADDPUT          REWRITE THE USER'S RECORD
         LTR   R15,R15             CHECK FOR BAD RETURN CODE
         BNZ   PUTERR              BAD WRITE, GO TO WRITE ERROR
         MVI   CTLCHAR,X'11'       SET UP CONTROL CHARACTER
         PUT   PRINTER,PRINT       WRITE PRINT LINE
         B     READCARD            CONTINUE READ LOOP FOR NEXT CARD
*  READ THE PASSWORD FILE USING THE USER-ID AS THE KEY
CHGEPSWD DS    0H
         GET   RPL=GETPUT          CALL VSAM GET ROUTINE
         LTR   R15,R15             TEST R15 FOR RETURN CODE
         BNZ   READERR             ERROR ON READ
         TR    PSFPSWD,LINEUPA     TRANSLATE PSWD
         MVC   PSF2LAST,PSFLAST    SAVE 2ND PREVIOUS PASSWORD
         MVC   PSFLAST,PSFPSWD     SAVE PREVIOUS PASSWORD
         MVC   PSFPSWD,CRCHGPSW    MOVE NEW TO PSWD RECORD
         MVC   PRINT2ID,CRUSERID   SET UP PRINT LINE MSG USER ID
         MVC   UDATE2,PSFUDATE     SET UP PRINT LINE MSG LAST USE DATE
         MVC   DDATE2,PSFDDATE     SET UP PRINT LINE MSG DISABLED DATE
         UNPK  SNATT2,PSFSNATT     SET UP PRINT LINE MSG SIGN-ON ATTPT
         OI    SNATT2+2,B'11110000'  ENSURE DISPLY ATTEMPTS ZONE F
         MVC   PRINT,PRINT2        SET UP PRINT LINE
         TR    PSFPSWD,LINEUPA     TRANSLATE PSWD
         MVC   PSFCDATE,EXPDATE    FORCE EXPIRED DATE ON CHGE OF PSWD
         MVC   PSFSNATT,=P'000'    RESET NUMBER OF SIGN-ON ATTEMPTS
         PUT   RPL=GETPUT          REWRITE THE USER'S RECORD
         LTR   R15,R15             CHECK FOR BAD RETURN CODE
         BNZ   PUTERR              BAD WRITE, GO TO WRITE ERROR
         MVI   CTLCHAR,X'11'       SET UP CONTROL CHARACTER
         PUT   PRINTER,PRINT       WRITE PRINT LINE
         B     READCARD            CONTINUE READ LOOP FOR NEXT CARD
*  READ THE PASSWORD FILE USING THE USER-ID AS THE KEY FOR DELETION
DELRECD  DS    0H
         GET   RPL=GETPUT          CALL VSAM GET ROUTINE
         LTR   R15,R15             TEST R15 FOR RETURN CODE
         BNZ   READERR             ERROR ON READ
         MVC   PRINT3ID,CRUSERID   SET UP PRINT LINE MSG
         MVC   UDATE3,PSFUDATE     SET UP PRINT LINE MSG LAST USE DATE
         MVC   PRINT,PRINT3        SET UP PRINT LINE
         ERASE RPL=GETPUT          DELETE THE USER'S RECORD
         LTR   R15,R15             CHECK FOR BAD RETURN CODE
         BNZ   PUTERR              BAD WRITE, GO TO WRITE ERROR
         MVI   CTLCHAR,X'11'       SET UP CONTROL CHARACTER
         PUT   PRINTER,PRINT       WRITE PRINT LINE
         B     READCARD            CONTINUE READ LOOP FOR NEXT CARD
*  READ THE PASSWORD FILE USING THE USER-ID AS THE KEY
CHGOPID  DS    0H
         GET   RPL=GETPUT          CALL VSAM GET ROUTINE
         LTR   R15,R15             TEST R15 FOR RETURN CODE
         BNZ   READERR             ERROR ON READ
         MVC   PSFOPID,CROPID      MOVE NEW OP-ID TO RECORD
         MVC   PRINT2ID,CRUSERID   SET UP PRINT LINE MSG USER-ID
         MVC   UDATE2,PSFUDATE     SET UP PRINT LINE MSG LAST USE DATE
         MVC   DDATE2,PSFDDATE     SET UP PRINT LINE MSG DISABLED DATE
         UNPK  SNATT2,PSFSNATT     SET UP PRINT LINE MSG SIGN-ON ATTPT
         OI    SNATT2+2,B'11110000'  ENSURE DISPLY ATTEMPTS ZONE F
         MVC   PRINT,PRINT2        SET UP PRINT LINE
         PUT   RPL=GETPUT          REWRITE THE USER'S RECORD
         LTR   R15,R15             CHECK FOR BAD RETURN CODE
         BNZ   PUTERR              BAD WRITE, GO TO WRITE ERROR
         MVI   CTLCHAR,X'11'       SET UP CONTROL CHARACTER
         PUT   PRINTER,PRINT       WRITE PRINT LINE
         B     READCARD            CONTINUE READ LOOP FOR NEXT CARD
PUTERR   DS    0H                  FALLS THROUGH ON BAD WRITE
         LA    R10,X'66'           WRITE ERROR ON PSW FILE
         B     BADEOJ              SYSTEM FAILURE
READERR  DS    0H                  BRANCHES IN ON BAD READ
         LA    R10,X'77'           READ ERROR ON PSW FILE
         B     BADEOJ              SYSTEM FAILURE
LASTCARD DS    0H
         CLOSE CARDIN,PRINTER,DFHCAJS
         EOJ                       GOOD END OF JOB
BADEOJ   DS    0H
         DC    CL1' '              FORCE DUMP
*    THIS 72 BYTE WORK AREA IS FOR USE BY VSE/VSAM MACROS AND ITS
*    ADDRESS STORED IN R13 BEFORE THEY ARE CALLED
VSEVSMWK DS    18F                 72-BYTE VSAM MACRO WORK SPACE
USIDKEY  DC    CL20' '             STORAGE AREA FOR KEY TO PSWD FILE
INITKEY  DC    CL20' '             SPACES USED TO INITIALIZE USIDKEY
TODDATE  DC    CL8' '              STORAGE FIELD FOR DATE
EXPDATE  DC    CL8'11/01/87'       EXPIRED DATE FOR FILE ADDITONS
*
********  you must insert your own translation table here *******
********  it must be a complete 256 character table and   *******
********  be copied into module DFHXSE                    *******
*
LINEUPA  DC    X'000102030405060708090A0B0C0D0E0F'
                       etc
         DC    X'F0F1F2F3F4F5F6F7F8F9FAFBFCFDFEFF'
*    THIS IS THE ACB (ACCESS CONTROL BLOCK) FOR THE FILE
*    CONTAINING THE PASSWORDS
DFHCAJS  ACB   AM=VSAM,             SPECIFY VSAM ACCESS METHOD         X
               BUFND=2,             # OF BUFFERS FOR CTRL INTVLS       X
               BUFNI=2,             # OF BUFFERS FOR INDICES           X
               DDNAME=DFHCAJS,      DLBL NAME                          X
               MACRF=(KEY,NDF,DIR,OUT,NRM,NRS,NSR,NUB), ACC OPTNS      X
               PARMS=(CLOSDSP=KEEP), DISPOSITION OF FILE AT CLOSE      X
               STRNO=2              # OF UPDATE STRINGS
*    THIS RPL IS FOR UPDATE MODE ONLY NOTE THE GET-PUT
GETPUT   RPL   ACB=DFHCAJS,         FILE FOR REQUEST PARM LIST         X
               AM=VSAM,             VSAM ACCESS METHOD                 X
               AREA=PSWRECD,        NAME OF RECORD LAYOUT              X
               AREALEN=80,          WORK AREA LENGTH                   X
               ARG=USIDKEY,         NAME OF WORKING FIELD KEY          X
               KEYLEN=20,           LENGTH OF KEY                      X
               OPTCD=(KEY,DIR,FWD,ARD,UPD,KEQ,FKS,MVE), PARM LIST      X
               RECLEN=80            RECORD LENGTH
*    THIS RPL IS FOR NEW RECORDS ONLY NOT THE NOT-UPDATE
ADDPUT   RPL   ACB=DFHCAJS,         FILE FOR REQUEST PARM LIST         X
               AM=VSAM,             VSAM ACCESS METHOD                 X
               AREA=PSWRECD,        NAME OF RECORD LAYOUT              X
               AREALEN=80,          WORK AREA LENGTH                   X
               ARG=USIDKEY,         NAME OF WORKING FIELD KEY          X
               KEYLEN=20,           LENGTH OF KEY                      X
               OPTCD=(KEY,DIR,FWD,ARD,NUP,KEQ,FKS,MVE), PARM LIST      X
               RECLEN=80            RECORD LENGTH
PSWRECD  DS    0CL80               DEFINE RECORD AREA
PSFUSRID DC    CL20' '             USER-ID WILL PROBABLY BE KEY
PSFOPID  DC    CL3' '              OP IDENT MAY BE USED INSTEAD
PSFPSWD  DC    CL8' '              CURRENT PASSWORD
PSFLAST  DC    CL8' '              1ST PREVIOUS PASSWORD
PSF2LAST DC    CL8' '              2ND PREVIOUS PASSWORD
PSFUDATE DC    CL8' '              DATE LAST USED SUCCESSFULLY
PSFCDATE DC    CL8' '              DATE OF LAST CHANGE OF PSWD
PSFSNATT DC    PL2'0'              NUMBER OF SIGN-ON ATTEMPTS
PSFDDATE DC    CL8' '              DATE ID WAS DISABLED
PSFILLR  DC    CL7' '              SPACE FOR FUTURE EXPANSION
*    THIS IS THE DEFINITION OF THE INPUT CARD FILE
CARDIN   DTFCD DEVADDR=SYS010,IOAREA1=CRDBUFF1,BLKSIZE=80,             X
               DEVICE=2540,EOFADDR=LASTCARD,WORKA=YES
CRDBUFF1 DC    CL80' '             CARD FILE I/O BUFFER
CARDREC  DS    0CL80               INPUT CARD RECORD
CRUSERID DC    CL20' '             USER-ID KEY FOR FILE
CROPID   DC    CL3' '              OPERATOR-ID
CRADDPSW DC    CL8' '              PASSWORD IF ADD SET UP FOR NEW USR
CRCHGPSW DC    CL8' '              PASSWORD IF CHG FOR USER
CRDELREC DC    CL1' '              D FOR DELETE FLAG
CRCHGOP  DC    CL1' '              C FOR REVISE OPERATOR-ID
CRFILL   DC    CL39' '             FUTURE EXPANSION
PRINTER  DTFPR BLKSIZE=121,DEVADDR=SYS011,DEVICE=1403,RECFORM=FIXUNB,  X
               PRINTOV=YES,CTLCHR=YES,IOAREA1=IOARPR1,WORKA=YES
PRINTOV  PRTOV PRINTER,12
IOARPR1  DC    CL121' '            PRINT FILE BUFFER
PRINT    DS    0CL121              REAL PRINT LINE
CTLCHAR  DC    CL1' '              CONTROL CHAR POSITION
PRTLINE  DC    CL120' '            PRINT LINE
PRINT1   DS    0CL120
         DC    CL21' ** USER-ID ADDED -- '
PRINT1ID DC    CL20' '
         DC    CL79' '
PRINT2   DS    0CL120
         DC    CL23' ** USER-ID CHANGED -- '
PRINT2ID DC    CL20' '
         DC    CL16' ** DISABLED -- '
DDATE2   DC    CL8' '
         DC    CL24' ** SIGN-ON ATTEMPTS -- '
SNATT2   DC    CL3' '
         DC    CL17' ** LAST USED -- '
UDATE2   DC    CL8' '
         DC    CL2' '
PRINT3   DS    0CL120
         DC    CL23' ** USER-ID DELETED -- '
PRINT3ID DC    CL20' '
         DC    CL17' ** LAST USED -- '
UDATE3   DC    CL8' '
         DC    CL52' '
         END


Sample JCL with all four options of file maintenance

// LIBDEF *,SEARCH=LIB.SUBLIB,TEMP
// ASSGN SYS010,SYSIPT
// ASSGN SYS011,F1E
// DLBL DFHCAJS,'YOUR.FILE.NAME',,VSAM,CAT=YOURUC
// EXEC DFHASC,SIZE=300K
BATMAN              BATNEWPASS               ADD NEW USER
BATMAN                         JOKER         CHANGE USERS PASSWORD
BATMAN              ROB                 C    CHANGE USERS OPERATOR ID
BATMAN                                 D     DELETE A USER
/* FIRST 20 USERID  OPI--ADD --++CHG ++DC
*       PLEASE USE 5 CHARACTER PSWDS   EH
*                                      LG


BMS SIGN-ON HELP MAP

Frequent calls were received when users had to start changing
their passwords on a regular basis. They always seem to forget
the restrictions for passwords (mainly the minimum length and
attempts to reuse the same passwords) so a help screen is
displayed when errors occur during changing passwords. For
assistance in assembling this BMS map see the Application
Programmers Reference Manual, Macro Level SC33-0079.

TSC999M  DFHMSD TYPE=DSECT,CTRL=(FREEKB,FRSET),EXTATT=YES,             X
               LANG=ASM,MODE=INOUT
SECHELP  DFHMDI LINE=1,COLUMN=1,JUSTIFY=(LEFT,FIRST),SIZE=(24,80)
         DFHMDF POS=(2,25),ATTRB=(PROT,BRT),LENGTH=29,                 X
               INITIAL='CICS  SIGN-ON  SECURITY  HELP'
         DFHMDF POS=(5,14),ATTRB=PROT,LENGTH=33,                       X
               INITIAL='YOU HAVE ENCOUNTERED ERRORS WHILE'
         DFHMDF POS=(5,48),ATTRB=PROT,LENGTH=18,                       X
               INITIAL='ATTEMPTING TO GAIN'
         DFHMDF POS=(6,14),ATTRB=PROT,LENGTH=32,                       X
               INITIAL='ACCESS TO THE MAINFRAME SYSTEMS.'
         DFHMDF POS=(8,14),ATTRB=PROT,LENGTH=34,                       X
               INITIAL='ONE OF THE FOLLOWING HAS OCCURRED '
         DFHMDF POS=(8,49),ATTRB=PROT,LENGTH=19,                       X
               INITIAL='DURING THE CHANGING'
         DFHMDF POS=(9,14),ATTRB=PROT,LENGTH=17,                       X
               INITIAL='OF YOUR PASSWORD:'
         DFHMDF POS=(11,18),ATTRB=(PROT,BRT),LENGTH=41,                X
               INITIAL='-  YOU HAVE USED THIS NEW PASSWORD BEFORE'
         DFHMDF POS=(12,18),ATTRB=(PROT,BRT),LENGTH=41,                X
               INITIAL='   TRY A NEW PASSWORD YOU HAVE NEVER USED'
         DFHMDF POS=(14,18),ATTRB=(PROT,BRT),LENGTH=41,                X
               INITIAL='-  NEW PASSWORD IS LESS THAN 5 CHARACTERS'
         DFHMDF POS=(15,21),ATTRB=(PROT,BRT),LENGTH=43,                X
               INITIAL='IN LENGTH - USE A PASSWORD 5 - 8 CHARACTERS'
         DFHMDF POS=(15,65),ATTRB=(PROT,BRT),LENGTH=4,                 X
               INITIAL='LONG'
         DFHMDF POS=(18,14),ATTRB=PROT,LENGTH=34,                      X
               INITIAL='YOUR NUMBER OF SIGN-ON ATTEMPTS IS'
         DFHMDF POS=(18,49),ATTRB=PROT,LENGTH=19,                      X
               INITIAL='LIMITED, HIT ENTER'
         DFHMDF POS=(19,14),ATTRB=PROT,LENGTH=11,                      X
               INITIAL='TO CONTINUE'
INFLD    DFHMDF POS=(21,14),ATTRB=(UNPROT,NORM),LENGTH=1,              X
               INITIAL=' '
         DFHMSD TYPE=FINAL


TERMINAL DISABLE PROGRAM

This program was written in command level to call CEMT to
put a terminal with five invalid sign-on attempts out of service.
Sample command-level assembly JCL can be found in
CICS/VS 1.7 Programmers Programmers Reference Manual -
Command Level SC33-0077.

*   TSC910AO -- ASSEMBLER ROUTINE WRITTEN TO DISABLE THE TERMINAL
CSACBAR  EQU   13
         USING DFHCSADS,CSACBAR    SET UP BASE USING CSA REGISTER
         COPY  DFHCSADS            COPY FOR COMMON SYSTEM AREA
         COPY  DFHTCADS            COPY FOR TASK CONTROL AREA
DFHEISTG DSECT
TSC910AO CSECT
         BALR R3,0                 SET BASE REGISTER FOR PROGRAM
         USING *,R3                INIT BASE REGISTER
PROCLOOP EQU   *                   START LOOP FOR TD Q DATA
         EXEC CICS HANDLE CONDITION QZERO(EXITLOOP) ERROR(EXITLOOP)
         EXEC CICS READQ TD QUEUE('TDIS') INTO(TDTERMID)
         MVC CEMTMESG,=C'CEMT SET TER (XXXX) OUT'
         LA    R5,X'03'
         STC   R5,MTRM+1
MTRM     MVC CEMTMESG+14,TDTERMID
         EXEC CICS LINK PROGRAM('DFHEMTA') COMMAREA(CEMTPARM)
         B     PROCLOOP            RETURN TO START OF LOOP
EXITLOOP EQU   *                   AT Q ZERO ESCAPE LOOP AND
         EXEC CICS RETURN
         DC    CL12'--COMMAREA--'  EYE CATCHER FOR COMM AREA
CEMTPARM DC    A(CEMTMESG)         ADDRESS FOR CEMT COMMAND
         DC    A(COMDLENG)         ADDRESS FOR LENGTH OF CEMT COMMAND
         DC    A(BYTEFLAG)         ADDRESS FOR INDICATOR BYTE
         DC    A(RETNDATA)         ADDRESS FOR RETURN INFO
         DC    A(RETNLENG)         ADDRESS FOR LENGTH OF RETURN INFO
BLANKS   DC    CL80' '             BLANKS TO INIT CEMTMESG
CEMTMESG DS    CL80                MESSAGE AREA
COMDLENG DC    H'80'               MESSAGE LENGTH 80 BYTES
BYTEFLAG DC    X'00'               BYTE FLAG TO RETURN DATA PGM
RETNDATA DC    115C' '             AREA FOR DFHEMTA OUTPUT
RETNLENG DC    H'115'              LENGTH OF RETURN AREA
CEMTERR1 DS    CL1                 DFHEMTA ERROR FLAG NO PPT
CEMTERR2 DS    CL1                 DFHEMTA ERROR FLAG BAD CEMT
TDTERMID DS    CL4                 TD Q TERMINAL ID AREA
         END

This article will be concluded next month when we shall be
publishing full details of the remaining components of this
CICS security system.


Allan R Kelly
System Software Programmer/Analyst (Canada)                        c Xephon 1992

