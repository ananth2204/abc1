CICS security - part four

Part four completes the major components of the security
system.  It consists of two maps and two programs.  The files
used by each program have already been described in the
previous parts.

The first program, SNP4, is the program which manages the
message area  on  the sign-on screen presented at log-on.  It is
strongly recommended that only authorized personnel are
allowed access to this program, because there is only one
message per CICS system.  It should therefore be allocated to
either operations, technical support, or both, since the major
use of it will be to inform users of any planned down time or
maintenance.

The program, in its present state, has been written on the
assumption that knowledgeable personnel will use it, in that it
opens the V998 file  for update, writes the data to the screen
(see Figure 1), reads  the  changes back, and updates the V998
file.  This ensures that only one person is able to update the file
at any one time.  Users must ensure that they do not call up the
file and then leave the screen displayed while they wander off
for a cup of coffee.  Users attempting to sign on will not be
affected, but any other authorized user will not be able to
update the message area while one user locks out the file in this way.

The PCT entry is simple, with no TWA specified.  The
transaction will not require a high priority position in the PCT.
PPT entries are required for the map set (SIGNMP2) and for
SNP4, which is an Assembler program.

Note that on the map (SIGNMP2), the input fields all have the
Modified Data Tag (MDT) set on.  This is to enable message
lines to be erased using the Erase EOF key (which does not set
the  MDT on, so a line cleared with this key would not
otherwise be altered).  To use, merely type the text you wish to
appear in the message area of the sign-on screen in the box
provided, and press ENTER.

Program SNP5 is used to monitor terminals which are being
denied access to CICS because of too many password guesses
by a user.  It displays the terminal identifiers (as in the
TCTTETI) of those terminals so affected, the operator name
which was associated with the incorrect password, and the date
and time the last attempt to sign on with the incorrect password
was made.  Because CICS only provides Julian dates in its
control blocks, the current Julian date is displayed in the top
right hand corner for comparison purposes.

The screen layout in Figure 2 shows the screen as presented to
the user.  In this case, the variable fields have been filled in for
illustration purposes, while on normal entry to the transaction,
these fields are blank.  The screen could be the result of
browsing the V997 file using PF2 or of entering the terminal-id
LU01 and pressing ENTER.  Note that the date of the example
is 22 November, 1991, with the failed sign on taking place the
day before at 10.20 am.

The PCT will require an entry for a transaction which points to
program SNP5.  A TWA of 51 bytes is required.  The PPT
entry should specify a program SNP5 as an Assembler
program, with a map set also specified for SIGNMP5.  The
security key assigned to the transaction should be allocated
only to the security administrator.  This will prevent
unauthorized persons from releasing terminals held in this way.

Use of the program is described on the screen itself.  In
practice however, the security administrator will probably want
to  browse the file containing the errors (V997), before freeing
any of them.  Browsing is performed using the PF2 key.  To
call up a particular record, type in the key (ie the terminal-id),
press ENTER, and then  press  PF9.  It is recommended that a
note is taken of the details.

To satisfy computer audit requirements, or possibly to pre-empt
them, it is advisable to contact the owners of operator names
found on the V997 file, and ask them for an explanation.
Because they are warned of impending problems after the
penultimate attempt at getting their password right, they should
not have got into this position if the operator name was being
used by the proper user.  It is suggested that if no reasonable
explanation is offered, the user is allocated a new operator
sign-on code and password, and the old one is deleted.

The only note to be made regarding the code is to point out that
only a loaded file can be made available to CICS.  A dummy
record must therefore be loaded into V997.  SNP5 assumes a
dummy record with a key of character zeros in place of the
terminal-id.  SNP5 will check for that key before processing
any record it reads.  It therefore goes without saying that if you
have a terminal with an identifier of 0000, the code and the
dummy record key should be changed to a key of your choice.


SNP4

         DFHCOVER
         COPY  KEYEQ
         COPY  DFHCSADS
         COPY  DFHTCADS
FWACBAR  EQU   8
BASE     EQU   9
TIOABAR  EQU   10
TCTTEAR  EQU   11
         COPY  DFHTCTTE
         COPY  DFHTIOA
         COPY  SIGNMP2
         COPY  DFHFWADS
RECORD   DS    0CL540
RL1      DS    CL60
RL2      DS    CL60
RL3      DS    CL60
RL4      DS    CL60
RL5      DS    CL60
RL6      DS    CL60
RL7      DS    CL60
RL8      DS    CL60
RL9      DS    CL60
SNP4     CSECT
         BALR  BASE,0
         USING *,BASE
         L     TCTTEAR,TCAFCAAA
         L     TIOABAR,TCTTEDA
         BAL   R7,TFREE
         DFHSC TYPE=GETMAIN,CLASS=TERMINAL,INITIMG=00,                 *
               NUMBYTE=SIGNMP2E-TIOADBA
         L     TIOABAR,TCASCSA
         ST    TIOABAR,TCTTEDA
         DFHFC TYPE=GET,DATASET=V998,ARGTYP=RBA,                       *
               MODE=MOVE,RDIADR=RKEY,TYPOPER=UPDATE,NOTFND=FINISH,     *
               ERROR=DUMP
         L     FWACBAR,TCAFCAA
         MVC   SM2MSG1O,RL1
         MVC   SM2MSG2O,RL2
         MVC   SM2MSG3O,RL3
         MVC   SM2MSG4O,RL4
         MVC   SM2MSG5O,RL5
         MVC   SM2MSG6O,RL6
         MVC   SM2MSG7O,RL7
         MVC   SM2MSG8O,RL8
         MVC   SM2MSG9O,RL9
         DFHBMS TYPE=(OUT,ERASE,WAIT),DATA=YES,MAP=SIGNMP2
         DFHBMS TYPE=IN,MAP=SIGNMP2
         L     TIOABAR,TCTTEDA
         CLI   TCTTEAID,ENTER
         BNE   CLEARED
         MVC   RL1,SM2MSG1I
         MVC   RL2,SM2MSG2I
         MVC   RL3,SM2MSG3I
         MVC   RL4,SM2MSG4I
         MVC   RL5,SM2MSG5I
         MVC   RL6,SM2MSG6I
         MVC   RL7,SM2MSG7I
         MVC   RL8,SM2MSG8I
         MVC   RL9,SM2MSG9I
         ST    FWACBAR,TCAFCAA
         DFHFC TYPE=PUT,RDIADR=RKEY,TYPOPER=UPDATE,ERROR=DUMP
CLEARED  EQU   *
         BAL   R7,TFREE
         MVC   TCASCNB,=H'20'
         DFHSC TYPE=GETMAIN,CLASS=TERMINAL,INITIMG=00
         L     TIOABAR,TCASCSA
         ST    TIOABAR,TCTTEDA
         MVC   TIOATDL,=H'20'
         MVC   TIOADBA(20),=C'MESSAGE AREA UPDATED'
         DFHTC TYPE=(ERASE,WRITE),CTLCHAR=C2
FINISH   EQU   *
         DFHPC TYPE=RETURN
TFREE    EQU   *                       * FREE TERMINAL STORAGE
         ST    TIOABAR,TCASCSA
FREEST   EQU   *
         DFHSC TYPE=FREEMAIN
         BR    R7
DUMP     EQU   *                       * SYSTEM ERROR - DUMP
         DFHDC TYPE=CICS,DMPCODE=SNP4
         B     FINISH
RKEY     DC    F'1'
         LTORG
         END


SIGNMP2

SIGNMPS  DFHMSD TYPE=&SYSPARM,MODE=INOUT
SIGNMP2  DFHMDI CTRL=FREEKB,SIZE=(24,80)
         DFHMDF POS=(2,8),ATTRB=(ASKIP,NORM),LENGTH=63,INITIAL='YOU HAV*
               E THE SPACE ENCLOSED IN THE BOX BELOW TO WRITE ANY MESS-*
               '
         DFHMDF POS=(3,8),ATTRB=(ASKIP,NORM),LENGTH=63,INITIAL='AGES.  *
                HAVING TYPED THE MESSAGE, PRESS ENTER.    ANY OTHER KEY*
               '
         DFHMDF POS=(4,17),ATTRB=(ASKIP,NORM),LENGTH=44,INITIAL='WILL R*
               ESULT IN YOUR EXITING THIS TRANSACTION'
         DFHMDF POS=(10,8),ATTRB=(ASKIP,NORM),LENGTH=64,INITIAL='------*
               --------------------------------------------------------*
               --'
         DFHMDF POS=(11,8),ATTRB=(ASKIP,NORM),LENGTH=1,INITIAL='|'
SM2MSG1  DFHMDF POS=(11,10),ATTRB=(UNPROT,NORM,FSET,IC),LENGTH=60
         DFHMDF POS=(11,71),ATTRB=(ASKIP,NORM),LENGTH=1,INITIAL='|'
         DFHMDF POS=(12,8),ATTRB=(ASKIP,NORM),LENGTH=1,INITIAL='|'
SM2MSG2  DFHMDF POS=(12,10),ATTRB=(UNPROT,NORM,FSET),LENGTH=60
         DFHMDF POS=(12,71),ATTRB=(ASKIP,NORM),LENGTH=1,INITIAL='|'
         DFHMDF POS=(13,8),ATTRB=(ASKIP,NORM),LENGTH=1,INITIAL='|'
SM2MSG3  DFHMDF POS=(13,10),ATTRB=(UNPROT,NORM,FSET),LENGTH=60
         DFHMDF POS=(13,71),ATTRB=(ASKIP,NORM),LENGTH=1,INITIAL='|'
         DFHMDF POS=(14,8),ATTRB=(ASKIP,NORM),LENGTH=1,INITIAL='|'
SM2MSG4  DFHMDF POS=(14,10),ATTRB=(UNPROT,NORM,FSET),LENGTH=60
         DFHMDF POS=(14,71),ATTRB=(ASKIP,NORM),LENGTH=1,INITIAL='|'
         DFHMDF POS=(15,8),ATTRB=(ASKIP,NORM),LENGTH=1,INITIAL='|'
SM2MSG5  DFHMDF POS=(15,10),ATTRB=(UNPROT,NORM,FSET),LENGTH=60
         DFHMDF POS=(15,71),ATTRB=(ASKIP,NORM),LENGTH=1,INITIAL='|'
         DFHMDF POS=(16,8),ATTRB=(ASKIP,NORM),LENGTH=1,INITIAL='|'
SM2MSG6  DFHMDF POS=(16,10),ATTRB=(UNPROT,NORM,FSET),LENGTH=60
         DFHMDF POS=(16,71),ATTRB=(ASKIP,NORM),LENGTH=1,INITIAL='|'
         DFHMDF POS=(17,8),ATTRB=(ASKIP,NORM),LENGTH=1,INITIAL='|'
SM2MSG7  DFHMDF POS=(17,10),ATTRB=(UNPROT,NORM,FSET),LENGTH=60
         DFHMDF POS=(17,71),ATTRB=(ASKIP,NORM),LENGTH=1,INITIAL='|'
         DFHMDF POS=(18,8),ATTRB=(ASKIP,NORM),LENGTH=1,INITIAL='|'
SM2MSG8  DFHMDF POS=(18,10),ATTRB=(UNPROT,NORM,FSET),LENGTH=60
         DFHMDF POS=(18,71),ATTRB=(ASKIP,NORM),LENGTH=1,INITIAL='|'
         DFHMDF POS=(19,8),ATTRB=(ASKIP,NORM),LENGTH=1,INITIAL='|'
SM2MSG9  DFHMDF POS=(19,10),ATTRB=(UNPROT,NORM,FSET),LENGTH=60
         DFHMDF POS=(19,71),ATTRB=(ASKIP,NORM),LENGTH=1,INITIAL='|'
         DFHMDF POS=(20,8),ATTRB=(ASKIP,NORM),LENGTH=64,INITIAL='------*
               --------------------------------------------------------*
               --'
SIGNMPS2 DFHMSD TYPE=FINAL


SNP5

         DFHCOVER
         COPY  KEYEQ
         COPY  DFHCSADS
         COPY  DFHTCADS
** TWA **
TODCONV  DS    D
TIME     DS    D
RECKEY   DS    0CL5
KEYLEN   DS    CL1
TERMID   DS    CL4
ERR      DS    CL25
TODAY    DS    CL5
FWACBAR  EQU   8
BASE     EQU   9
TIOABAR  EQU   10
TCTTEAR  EQU   11
         COPY  DFHTCTTE
         COPY  DFHTIOA
TREC     DS    0CL30
         COPY  SIGNMP5
         COPY  DFHFWADS
RECORD   DS    0CL20
BBTERM   DS    CL4                     TERMINAL ID
BBNAME   DS    CL8                     OPERATOR NAME
BBDATE   DS    PL4                     DATE OF FAILURE
BBTIME   DS    PL4                     TIME OF FAILURE
SNP5     CSECT
         BALR  BASE,0
         USING *,BASE
         L     TCTTEAR,TCAFCAAA
         L     TIOABAR,TCTTEDA
         BAL   R7,TFREE
         ZAP   TODCONV,CSAJYDP         CONVERT TIME OF DAY TO CHAR
         UNPK  TODAY,TODCONV
         OI    TODAY+4,X'F0'
START    EQU   *
         BAL   R7,GETSTOR              GET TERMINAL STORAGE
         DFHBMS TYPE=(OUT,ERASE,WAIT),DATA=NO,MAP=SIGNMP5
A01      EQU   *                       READ RESPONSE
         DFHBMS TYPE=IN,MAP=SIGNMP5
         L     TIOABAR,TCTTEDA
         CLI   TCTTEAID,CLEAR          FINISHED ?
         BE    CLEARED
         CLI   TCTTEAID,PF2            BROWSE ?
         BE    C01
         CLC   SM5TIL,=H'0'            WAS A NAME TYPED? (VSAM KEY)
         BNE   A05
         BAL   R7,TFREE
         MVC   ERR(17),=C'NO TERMID ENTERED' - ONLY OK FOR BROWSE
A02      EQU   *
         LA    R7,A01                  WRITE ERROR AND RESTART
         B     TWRITE
A05      EQU   *
         MVC   TERMID,SM5TII           MOVE TERMINAL ID TO TWA
         BAL   R7,TFREE
         DFHFC TYPE=GET,DATASET=V997,RDIDADR=TERMID,SRCHTYP=FKEQ,      *
               MODE=MOVE,ERROR=DUMP,NOTFND=A30
         L     FWACBAR,TCAFCAA
         BAL   R7,GETSTOR
         MVC   SM5TIO,BBTERM           FILL IN OUTPUT FIELDS
         MVC   SM5NAMO,BBNAME
         MVC   SM5TODO,TODAY           PUT TODAYS DATE ON SCREEN
         ZAP   TODCONV,BBDATE          PUT BADBOY DATE ON SCREEN
         UNPK  SM5DATO,TODCONV
         OI    SM5DATO+4,X'F0'
         ZAP   TODCONV,BBTIME          PUT BADBOY TIME ON SCREEN
         UNPK  TIME,TODCONV
         MVC   TIME(2),TIME+1
         MVI   TIME+2,C':'
         MVC   SM5TIMO(5),TIME
         MVC   TCASCSA,TCAFCAA         FREE OFF FILE STORAGE
         BAL   R7,FREEST
         DFHBMS TYPE=(OUT,WAIT),DATA=ONLY,MAP=SIGNMP5
         DFHBMS TYPE=(IN),MAP=SIGNMP5
         L     TIOABAR,TCTTEDA
         CLI   TCTTEAID,CLEAR
         BE    CLEARED
         CLI   TCTTEAID,PF9
         BNE   A10
         BAL   R7,TFREE
         DFHFC TYPE=DELETE,DATASET=V997,RDIDADR=TERMID,ARGTYP=KEY,     *
               ERROR=DUMP
         B     START
A10      EQU   *
         CLC   SM5TIL,=H'0'            WAS A NAME TYPED? (VSAM KEY)
         BNE   A05
         LA    R7,START
         B     TFREE
A30      EQU   *
         DFHFC TYPE=RELEASE
         B     START
C01      EQU   *                       BROWSE
         MVC   TERMID,=X'10000000'     KEY TO ZERO IF WHOLE FILE
         MVI   KEYLEN,X'01'            SET UP DEFAULT KEY LENGTH
         CLC   SM5TIL,=H'0'            NOW CHECK IF START POS ENTERED
         BE    *+16
         MVC   KEYLEN,SM5TIL+1         1ST CHAR IS KEY LENGTH
         MVC   TERMID,SM5TII
         BAL   R7,TFREE
         DFHFC TYPE=SETL,DATASET=V997,RDIDADR=RECKEY,ARGTYP=KEY,       *
               SRCHTYP=GKGE,MODE=MOVE,NOTFND=C30,ERROR=DUMP
         L     FWACBAR,TCAFCAA
C10      EQU   *                       GET NEXT RECORD
         ST    FWACBAR,TCAFCAA
         DFHFC TYPE=GETNEXT,NOTFND=C30,ENDFILE=C30,ERROR=DUMP
         CLC   BBTERM,=C'0000'         DUMMY RECORD ?
         BE    C30
         BAL   R7,GETSTOR
         MVC   SM5TIO,BBTERM           FILL IN OUTPUT FIELDS
         MVC   SM5NAMO,BBNAME
         MVC   SM5TODO,TODAY           PUT TODAYS DATE ON SCREEN
         ZAP   TODCONV,BBDATE          PUT BADBOY DATE ON SCREEN
         UNPK  SM5DATO,TODCONV
         OI    SM5DATO+4,X'F0'
         ZAP   TODCONV,BBTIME          PUT BADBOY TIME ON SCREEN
         UNPK  TIME,TODCONV
         MVC   TIME(2),TIME+1
         MVI   TIME+2,C':'
         MVC   SM5TIMO(5),TIME
         DFHBMS TYPE=(OUT,WAIT),DATA=ONLY,MAP=SIGNMP5
         DFHBMS TYPE=(IN),MAP=SIGNMP5
         L     TIOABAR,TCTTEDA
         CLI   TCTTEAID,CLEAR
         BE    CLEARED
         BAL   R7,TFREE
         CLI   TCTTEAID,PF2
         BE    C10
C30      EQU   *                       BACK TO START
         ST    FWACBAR,TCAFCAA
         DFHFC TYPE=ESETL
         B     START
GETSTOR  EQU   *
         DFHSC TYPE=GETMAIN,CLASS=TERMINAL,INITIMG=00,                 *
               NUMBYTE=SIGNMP5E-TIOADBA
         L     TIOABAR,TCASCSA
         ST    TIOABAR,TCTTEDA
         BR    R7
TFREE    EQU   *                       * FREE TERMINAL STORAGE
         ST    TIOABAR,TCASCSA
FREEST   EQU   *
         DFHSC TYPE=FREEMAIN
         BR    R7
TWRITE   EQU   *                       WRITE SHORT MESSAGES TO SCREEN
         MVC   TCASCNB,=H'30'
         DFHSC TYPE=GETMAIN,CLASS=TERMINAL,INITIMG=00
         L     TIOABAR,TCASCSA
         ST    TIOABAR,TCTTEDA
         MVC   TIOATDL,=H'30'
         MVC   TREC,ERRSCR
         MVC   TREC+5,ERR
         XC    ERR,ERR
         DFHTC TYPE=(WRITE,WAIT),CTLCHAR=C2
         BR    R7
GETAREA  EQU   *                       GET FILE AREA FOR NEW RECS
         DFHFC TYPE=GETAREA,DATASET=V997,INITIMG=00,ERROR=DUMP
         L     FWACBAR,TCAFCAA
         BR    R7
ADDREC   EQU   *                       ADD NEW RECORD
         ST    FWACBAR,TCAFCAA
         DFHFC TYPE=PUT,RDIDADR=TERMID,TYPOPER=NEWREC,ERROR=DUMP
         BR    R7
GETREC   EQU   *                       GET SPECIFIED RECORD
         DFHFC TYPE=GET,DATASET=V997,RDIDADR=TERMID,SRCHTYP=FKEQ,      *
               MODE=MOVE,TYPOPER=UPDATE,NOTFND=EH,ERROR=DUMP
         L     FWACBAR,TCAFCAA
         BR    R7
EH       EQU   *
         DFHFC TYPE=RELEASE,ERROR=DUMP
         MVC   ERR(16),=C'RECORD NOT FOUND'
         LA    R7,A01
         B     TWRITE
UPDATE   EQU   *                       UPDATE RECORD
         ST    FWACBAR,TCAFCAA
         DFHFC TYPE=PUT,RDIDADR=TERMID,TYPOPER=UPDATE,ERROR=DUMP
         BR    R7
DUMP     EQU   *                       * SYSTEM ERROR - DUMP
         DFHDC TYPE=PARTIAL,LIST=(TERMINAL,PROGRAM,TRANSACTION),       *
               DMPCODE=FERR
CLEARED  EQU   *                       LOCAL NON-SNA TERMINALS REQUIRE
         MVC   TCASCNB,=H'1'              THIS CODE TO RESET KEYBOARD
         DFHSC TYPE=GETMAIN,CLASS=TERMINAL,INITIMG=00
         L     TIOABAR,TCASCSA
         ST    TIOABAR,TCTTEDA
         MVC   TIOATDL,=H'1'
         MVI   TREC,C' '
         DFHTC TYPE=(ERASE,WRITE)
FINISH   EQU   *
         DFHPC TYPE=RETURN
ERRSCR   EQU   *
         SCFMT SBA,(5,40),SF,PRHI
         DC    CL25' '
         LTORG
         END


SIGNMP5

SIGNMPS  DFHMSD TYPE=&SYSPARM,MODE=INOUT
SIGNMP5  DFHMDI CTRL=FREEKB,SIZE=(24,80)
         DFHMDF POS=(1,14),ATTRB=(PROT,NORM),LENGTH=44,                *
               INITIAL='CICS SECURITY - FIVE FAILED SIGN-ON ATTEMPTS'
SM5TOD   DFHMDF POS=(1,68),ATTRB=(PROT,NORM),LENGTH=5
         DFHMDF POS=(2,14),ATTRB=(PROT,NORM),LENGTH=44,                *
               INITIAL='============================================'
         DFHMDF POS=(5,24),ATTRB=(PROT,NORM),LENGTH=7,INITIAL='TERMID:'
SM5TI    DFHMDF POS=(5,32),ATTRB=(UNPROT,BRT,IC),LENGTH=4
         DFHMDF POS=(5,37),ATTRB=(PROT,NORM),LENGTH=1
         DFHMDF POS=(8,17),ATTRB=(PROT,NORM),LENGTH=14,                *
               INITIAL='OPERATOR NAME:'
SM5NAM   DFHMDF POS=(8,32),ATTRB=(PROT,NORM),LENGTH=8
         DFHMDF POS=(10,17),LENGTH=31,ATTRB=(PROT,NORM),               *
               INITIAL='DATE OF FAILED SIGN-ON ATTEMPT:'
SM5DAT   DFHMDF POS=(10,49),LENGTH=5,ATTRB=(PROT,NORM)
         DFHMDF POS=(12,17),LENGTH=31,ATTRB=(PROT,NORM),               *
               INITIAL='TIME OF FAILED SIGN-ON ATTEMPT:'
SM5TIM   DFHMDF POS=(12,49),LENGTH=6,ATTRB=(PROT,NORM)
         DFHMDF POS=(17,6),LENGTH=59,ATTRB=(PROT,NORM),                *
               INITIAL='TO ACCESS NAMED TERMINAL, ENTER TERMINAL ID AND*
                PRESS ENTER'
         DFHMDF POS=(19,6),LENGTH=26,ATTRB=(PROT,NORM),                *
               INITIAL='TO PAGE THROUGH, PRESS PF2'
         DFHMDF POS=(21,6),LENGTH=44,ATTRB=(PROT,NORM),                *
               INITIAL='TO DELETE AN ENTRY, DISPLAY IT AND PRESS PF9'
SIGNMAPS DFHMSD TYPE=FINAL


Robin Lynch                                            c Xephon/Reserved 1992

