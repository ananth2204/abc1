An improvement to the RACF-CICS interface

ENVIRONMENT

Our installation has some large IBM mainframes running CICS
1.7 along with MVS/XA 2.2 and RACF 1.7.  Also, we have
TORs and AORs in both production and test CICS systems
defined with the ATTACHSEC(IDENTIFY) option.

PROBLEM

We needed to grant users immediate authorisation to some
transactions in the middle of a CICS session.  You can achieve
this by means of RACF.  When you sign on in the TOR, your
new security profile is loaded in it, but a configuration of
TORs and AORs doesn't allow the automatic refresh of the
security profile in the AORs (only if the user doesn't use CICS
in a period of over 30 minutes).

You can find a better explanation in page 76 of CICS/OS/VS
Intercommunication Facilities Guide SC33-0230-1.

SOLUTION

I was reading CICS guides and investigating CICS macros and
source libraries.  I discovered the AOR keeps a table with the
users that have executed transactions from the TOR.

The AOR updates the table, writing the timestamp in the
corresponding element of the user, each time the user executes
a transaction from the TOR.  If a particular user has no
transactions executed for a period of more than 30 minutes, this
user is marked as deleted in the table.  When the user doesn't
exist in the table or has been deleted, and he executes a
transaction from the TOR to the AOR, he is included in the
table and his security profile is loaded in the AOR.

I have developed a program whose associated transaction is
started up in an AOR from the sign-on transaction in the TOR
after the sign-on has been found to be correct.  The program
marks the user in the table as deleted.

I do not think this is an ideal solution (it uses IBM internal
macros) and I hope future CICS versions will contain a better
way of dealing with this problem.

The following program uses structured Assembler macros that
we have at our installation, but I think it can be translated into
Assembler code easily.

*ASM CICS(NOEPILOG)
         GBLB  &DFHEIMX
&DFHEIMX SETB  1
         COPY  DFHCSADS
         DFHTCTZE CICSYST=YES
         DFHTCA CICSYST=YES
***********************************************************************
*   CICC605. - MARK A USER AS DELETED IN THE IRC USER TABLE OF
*              THIS CICS SYSTEM.
*            - THE TRANSACTION ASSOCIATED WITH THIS PROGRAM IS
*              STARTED FROM THE TOR TO THE DESTINATION AOR FROM
*              THE SIGN-ON, WITH THE OBJECT OF MARKING THE USER
*              AS DELETED SO THAT THE USER PROFILE CAN BE LOADED
*              FROM NEW WHEN THIS CICS SYSTEM IS ACCESSED THROUGH
*              MRO.
*            - SOURCES: TCPR.CIC.MACLIB(DFHZUTM)
*                       TCPR.CIC.SOURCE(DFHCRNP)
***********************************************************************
DFHEISTG DSECT
TCTTEAR  EQU   5    BASE TCTTE
RDAT     EQU   8
         ENTORNO SKIP=YES       installation-dependent routine
*        WORK FIELDS
LONG     DS    H         LENGTH
USUARIO  DS    CL8       USER
ARRANQUE DS    CL2       START
         DFHZUTM TYPE=DSECT        MAP USERID TABLE
*        WORK AREA FOR RECEIVED DATA
         USING DDAT,RDAT
DDAT     DSECT
DUSUARIO DS    CL8       NOMBRE DE USUARIO
*        PROGRAM CODE
CICC605  DFHEIENT CODEREG=3,DATAREG=9,EIBREG=11
PROGRAMA BLOCK
* ---> IF THIS IS IS A TRANSACTION WITH NO TERMINAL VALUE ----
         IF   (EIBTRMID,NE,=X'00000000')
           LEAVE BLOCK=PROGRAMA
         ENDIF
* ---> IF IT STARTS AT 'START' VALUE ----------------
         MVC   LONG,=H'8'
         EXEC CICS ASSIGN STARTCODE(ARRANQUE)
         IF   (ARRANQUE,NE,=C'SD')
           LEAVE BLOCK=PROGRAMA
         ENDIF
* ---> REVIVE THE USER ------------------------
         MVC   LONG,=H'8'
         EXEC CICS RETRIEVE SET(RDAT)                                  X
               LENGTH(LONG)
         IF   (LONG,NE,=H'8')
           LEAVE BLOCK=PROGRAMA
         ENDIF
         MVC   USUARIO,DUSUARIO    MOVE THE USER TO 'WORKING'
         EXEC CICS SUSPEND
* ---> FIND THE LINKS FOR THIS CICS SYSTEM -------
         SR    RF,RF               CLEAR FOR FIRST
ZUT2LC   DS    0H
         DFHTC CTYPE=LOCATE,       GET FIRST|NEXT LU6.2 SYSTEM ENTRY   -
               TERM=NEXT,                                              -
               DOMAIN=SYSTEM,                                          -
               LASTTRM=ZUT1LC,                                         -
               OPTIONS=NOWAIT,                                         -
               TRMADDR=(RF)
         ICM   RE,B'1111',TCSEUTA-DFHTCTTE(RF) ADDR USERID TABLE
         USING DFHZUTDS,RE         POINT TO THE USER TABLE
ZUT3LC   DS    0H
         BZ    ZUT2LC              END OF TABLE.  GO TO NEXT LINK
* ---> MARK THE USER AS DELETED ------
         IF    (USUARIO,EQ,USTEACSD)
           OI   USTEFLAG,USTEPOEL
         ENDIF
ZUT4LC   DS    0H
         ICM   RE,B'1111',USTEFCHN ADDRESS NEXT ENTRY
         B     ZUT3LC              FIND THE NEXT USER
         DROP  RE
ZUT1LC   DS    0H                  END
         ENDBLK
*               END OF PROGRAM
FINPROG  EXEC   CICS RETURN
         TESTEND
         LTORG
         END


Carlos Sainz-Verdejo
Banco de Espaa (Spain) ) Banco de Espaa 1990


