CICS emergency restart - the solution

This article answers the question posed in CICS Update June
1989, CICS emergency restart - a cry for help.  When an
emergency restart of CICS abends with the message DFH2802I
LOG RECORD INVALID - RESTART ABORTED, you cannot
restore the saved log extents to one disk extent (look at the journal
control label record description) because there is special
information in the journal control records, such as the track
pointer for disks, for instance.

We simulate tape logging for unlabelled tapes.  We copy the back-
up tapes for all extents from the last CICS run to a pool of NL
tapes, which are input to the emergency restart.  Another journal
control table for tape logging needs to be specified and a statement
for DFHJ01A with the highest number of the pool should be
included. Then we perform the following changes to the journal
control records:

1   The CICS start time constant (JCLRRST) is altered.

2   The fields JCLRLBW and JCLRTBAL are cleared to low
values for the first tape.  For the next tapes, fill the fields with
the date and the volume sequence number of the previous
tape.

3   The field JCLRBLKN is re-numbered.

4   One is added to the field JCLRVSN (the volume sequence
number) for each new tape.  If there is a change of date, the
new tape will get the sequence number one.

5   Duplicate records are removed if a back-up of the disk extent
fails and is repeated.

CICSCOPY CSECT
         SAVRT (10,11,12),RC=(15)
         OPEN  (INPUTC,(INPUT))             INPUT TAPE
         OPEN  (OUTPUTC,(OUTPUT))           OUTPUT TAPE
         OPEN  (SYSPRINT,(OUTPUT))
         USING IO,R2
GETC     DS    0H
         GET   INPUTC
         LR    R2,R1
         MVC   OUTPUTC+62(2),INPUTC+82
*                            RECORD LENGTH ---> BLKSIZE
         MVC   DISK,LOESCH       CLEAR SPECIALS FOR DISKS
WEICHE   BC    0,WEICHE2         (AT SECOND TAPE: NEW INFOS)
         OI    WEICHE+1,X'F0'
         MVC   RUN,RUNTIME        CICS START TIME
         MVC   HDATE,JDATE        VOLUME CREATION DATE
         PUT   SYSPRINT,CLIST
         XC    CLIST,CLIST
         MVI   CLIST,C'-'
         PUT   SYSPRINT,CLIST
         PUT   SYSPRINT,CZEILE             PRINT LIST
         XC    CZEILE,CZEILE
         MVC   CZEILE+1(50),=50C'_'
         PUT   SYSPRINT,CZEILE
         XC    CZEILE,CZEILE
         MVI   SCHRAEG,C'/'
         UNPK  HILFL(7),RUN(4)
         MVC   HHMMSS,HILFL
WEICHE2  BC    0,SECOND
         OI    WEICHE2+1,X'F0'
         MVC   BBDATE,BDATE
         UNPK  YYDDD(5),HDATE+1(3)
         OI    YYDDD+4,X'F0'
         UNPK  NNN(3),VOLSEQ
         OI    NNN+2,X'F0'
         LA    R3,OUTPUTC
         USING IHADCB,R3
         L     R4,DCBDEBAD
         USING DEBBASIC,R4
         L     R3,DEBSUCBA
         USING UCBCMSEG,R3
         MVC   VOLSER,UCBVOLI      AKTUELL VOL SER NUMBER
         PUT   SYSPRINT,CZEILE
SECOND   DS    0H
*      IF THE BACKUP OF DISK-EXTENTS FAILS AND IS REPEATED,
*     THERE CAN BE DUPLICATE RECORDS ON THE BACKUP TAPE.
DUPP     BC    15,NODUPP        DUPLICATE RECORDS?
         CLC   HBDATE,BDATE     DATE ASCENDING?
         BC    8,EQUAL          OLD DATE = NEW DATE
         BC    4,NODUPP         OLD DATE < NEW DATE
         B     GETC  OLD DATE > NEW DATE (DUPLICATE)
EQUAL    DS    0H
         CLC   BBTIME,BTIME     BLOCKTIME ASCENDING?
         BC    4,NODUPP         YES
         BC    2,GETC           OLD TIME > NEW TIME
         CLC   HJBLKNR,JBLOCKNR OLD TIME = NEW TIME
         BNL   GETC   IF NEW BLOCKNR < = (DUPLICATE)
NODUPP   DS    0H
         MVC   BBTIME,BTIME
         MVC   HBDATE,BDATE
         MVC   HJBLKNR,JBLOCKNR
         MVC   RUNTIME,RUN   CICS START TIME CONSTANT
         MVC   JDATE,HDATE
         MVC   JBLOCKNR,HILC       BLOCKCOUNT
         CLC   HILC,=PL3'99999'    TOO BIG?
         BL    OK
         ZAP   HILC,=P'0'          NEW COUNT
OK       DS    0H
         MVC   VOLSEQNR,VOLSEQ  VOLUME SEQUENCE NUMBER
         AP    HILC,=P'1'          INCREASE BLOCK COUNT
         WRITE DECB,SF,OUTPUTC,(R2),'S',MF=E
         CHECK DECB
         NI    DUPP+1,X'0F'
         B     GETC
NEWVOLC  DS    0H
         ST    R14,S14
         LA    R15,SAVEAREA
         ST    R15,8(R13)
         ST    R13,4(R15)
         LR    R13,R15
         MVC   LOESCH+4(2),VOLSEQ  OLD VOLUME SEQ NUMBER
         MVC   LOESCH(4),HDATE     OLD VOLUME CREATION DATE
         ZAP   HILC,=P'1'          CLEAR BLOCK COUNT
         CLC   BBDATE,BDATE        DATE CHANGED?
         BE    NONEWDAT
         MVC   HDATE,BDATE         NEW VOLUME CREATION DATE
         ZAP   VOLSEQ,=P'0'        NEW VOLUME SEQ NUMBER
NONEWDAT DS    0H
         AP    VOLSEQ,=P'1'   INCREASE VOLUME SEQ NUMBER
         NI    WEICHE2+1,X'0F'
         L     R13,4(R13)
         SR    R15,R15
         ST    R15,8(R13)
         L     R14,S14
         BR    R14
COPYENDE DS    0H
         CLOSE (INPUTC)
         CLOSE (OUTPUTC)
         B     RELEASE
ERROR    NULLT
         WTO   'CICSLOG: -COPY-  SYNAD-FEHLER',ROUTCDE=11
         ABEND 100
         BR    R14
VOLSEQ   DC    PL2'1'
HILC     DC    PL3'1'
LOESCH   DC    6X'0'
HILFL    DS    CL7
BBTIME   DS    CL4
HJBLKNR  DS    CL3
HBDATE   DS    CL4
BBDATE   DS    CL4
HDATE    DS    CL4
RUN      DS    CL4
LAUF     DS    F
NR       DS    CL4
D        DS    D
INPUTC   DCB   DSORG=PS,MACRF=GL,RECFM=U,BLKSIZE=19069,EODAD=COPYENDE, X
               DDNAME=BAND
OUTPUTC  DCB   DSORG=PS,MACRF=W,RECFM=U,BLKSIZE=19069,BUFNO=1,         X
               DDNAME=NLBAND,SYNAD=ERROR,EXLST=EXLC
WRITELOG WRITE DECB,SF,OUTPUTC,MF=L
EXLC     DS    0F
         DC    X'86'
         DC    AL3(NEWVOLC)
CLIST    DS    CL80
         ORG   CLIST
         DC    C'1'
         DC    C'LISTE DER NL-BAENDER FUER *FORWARD RECOVERY           X
                  * BZW. *EMERGENCY RESTART*'
         ORG
CZEILE   DS    CL80
         ORG   CZEILE
         DC    C'0'
         DC    CL5' '
VOLSER   DC    CL6'VOLSER'
         DC    CL10' '
YYDDD    DC    CL5'YYDDD'
SCHRAEG  DC    CL1'/'
NNN      DC    CL3'NNN'
         DC    CL10' '
HHMMSS   DC    CL6'HHMMSS'
         ORG
         LTORG
IO       DSECT
         DS    CL15
JBLOCKNR DS    CL3
JDATE    DS    CL4
VOLSEQNR DS    CL2
DISK     DS    CL6
BTIME    DS    CL4
RUNTIME  DS    CL4
BDATE    DS    CL4
         DCBD  DSORG=PS,DEVD=TA
         IEZDEB LIST=YES
         IEFUCBOB LIST=YES,PREFIX=YES
         END

This program works under CICS 1.5 and, after comparing the
journal control records for disk logging and NL tape logging, I
think that it will run under CICS 1.6 and CICS 1.7.0 without too
many changes.

Ferdinand Roters
Systems Programmer
DVO(West Germany) ) DVO 1989


