Subroutine to enhance testing

When testing on-line COBOL programs using CEDF, it can be
very time-consuming and prone to error locating various areas
in working storage.  This is particularly true when working
storage is rather large, some of the areas you are looking for
are not near the beginning, or the different areas you want to
look at are not near each other.

To shorten the testing cycle, the following subroutine was
written to provide a list of selected storage addresses that can
be quickly looked up while viewing working storage under
CEDF.

A COBOL program supplies the following:

o      An area to contain the 'address list'.

o      The uniform length of the 'eyecatcher' that precedes each
      address in the list (zero if none).

o      One or more field names to include.

Incidentally, procedure names can be included in the list as
well as field names, if you also need to browse through a piece
of machine code.  Also, this subroutine can be used for batch
programs to shorten dump analysis.

Here is part of a sample COBOL program using the subroutine.
It is best to put ADDRESS-LIST at the top of working storage:
the location of EYECATCHER-LENGTH and the other fields
can be anywhere.  Since the ADDRESS-LIST will contain
static addresses, not the contents of the fields, it makes most
sense to issue the call once, near the beginning of the
procedure division.

     WORKING-STORAGE SECTION.

     01  ADDRESS-LIST.
         03  FILLER           PIC X(4) VALUE 'FLDA'.
         03  FILLER           PIC X(4).
         03  FILLER           PIC X(4) VALUE 'FLDB'.
         03  FILLER           PIC X(4).

     01  EYECATCHER-LENGTH    PIC S9(4) COMP VALUE +4
     01  FIELD-A              PIC ...
     01  FIELD-B              PIC ...

     PROCEDURE DIVISION.

         CALL 'ADDRLIST' USING ADDRESS-LIST,
                               EYECATCHER-LENGTH,
                               FIELD-A,
                               FIELD-B.


ADDRLIST SOURCE CODE

* PROGRAM NAME:  ADDRLIST                                             *
* ATTRIBUTES:    REENTRANT                                            *
* PURPOSE:       PROVIDE A HIGH-LEVEL PROGRAM WITH A LIST OF          *
*                STORAGE ADDRESSES CORRESPONDING TO FIELD NAMES.      *
* PARAMETERS:    1. AREA TO CONTAIN THE RESULTS,                      *
*                   INCLUDING EYECATCHERS IF ANY (SEE PARM 2).        *
*                   FULLWORD ALIGNMENT NOT REQ'D BUT RECOMMENDED.     *
*                2. LENGTH OF EYECATCHER PRIOR TO EACH ADDRESS        *
*                   IN RESULTS.  MAY BE ZERO.  RECOMMENDED VALUE      *
*                   DIVISIBLE BY 4, SUCH AS 4 OR 8.                   *
*                   LENGTH IS 2 BYTE BINARY, ALIGNMENT NOT REQ'D.     *
*              3-?. FIELD(S) TO LIST ADDRESSES OF.                    *
* RETURN CODE:   ALWAYS ZERO.                                         *
ADDRLIST CSECT
         STM   R14,R12,12(R13)         SAVE CALLER'S REGISTERS.
         LR    R12,R15                 SET UP R12 AS PGM BASE.
         USING ADDRLIST,R12            (NOTIFY ASSEMBLER).
         B     STRT                    ==> BYPASS LITERALS.
         DC    CL8'ADDRLIST'           SOME INTERNAL ID.
STRT     DS    0H
         L     R2,0(,R1)               RESULTS AREA.
         L     R3,4(,R1)               EYECATCHER LENGTH.
         SR    R4,R4                   PUT EYECATCHER LENGTH
         ICM   R4,B'0011',0(R3)         INTO REGISTER.
         LA    R1,4(,R1)               FIRST FIELD IN PLIST LESS 4.
LOOP     DS    0H
         CLI   0(R1),X'80'             ANY (MORE) FIELDS IN PLIST ?
         BE    EXIT                    ==> NO...DONE.
         LA    R1,4(,R1)               1ST/NEXT FIELD IN PARMLIST.
         AR    R2,R4                   MOVE PAST EYECATCHER, IF ANY.
         MVC   0(4,R2),0(R1)           COPY ADDRESS TO RESULTS.
         LA    R2,4(,R2)               BUMP RESULTS POINTER FORWARD.
         B     LOOP                    ==> SEE IF ANY MORE.
EXIT     DS    0H
         LM    R14,R12,12(R13)         RESTORE CALLER'S REGISTERS.
         SR    R15,R15                 CLEAR RETURN CODE.
         BR    R14                     ==> RETURN TO CALLER.
         END   ADDRLIST


Lawrence Holder
Senior Programmer/Analyst
University of Tennessee at Martin (USA)                   c UTM 1991

