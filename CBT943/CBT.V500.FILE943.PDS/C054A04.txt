Automatic transaction initiator

I have written a simple program that will start a transaction on
a terminal or on a group of terminals at PLT time.  This
program temporarily modifies each terminal's TCTTE
(specifically field TCTTETC) with the transaction to be
executed when the terminal first receives an input operation.
This was written so that our users would be automatically
logged on to a menu instead of requiring them to enter a
transaction.

The program does not issue the EXEC CICS START command
since this will put unattended or unattached terminals out of
service.  The instant the user hits any attention key, the
transaction will be executed.

This program is currently running in a CICS 1.7, VSE/SP 3.1
environment.

STRTMENU DFHEIENT CODEREG=(3),DATAREG=(12),EIBREG=6
         TITLE '*** STRTMENU ***'
CSAPTR   EQU   R13                 CICS/VS COMMON SYSTEM AREA POINTER
TCTPFBAR EQU   R1                  CICS/VS TERMINAL CONTROL TABLE
TCTTEAR  EQU   R5
COMPTR   EQU   R11                 COMMAREA POINTER
         USING DFHCSADS,CSAPTR     MAKE CSA ADDR.
         USING DFHTCTTE,TCTTEAR    MAKE TCTTE ADDR.
         USING DFHTCTFX,TCTPFBAR   MAKE TCT ADDR.
GETADDR  DS    0H
         EXEC CICS HANDLE CONDITION,                                   X
               ERROR(ENDSCAN)
         EXEC  CICS ADDRESS,      GET ADDRESS OF CICS/VS CSA           X
               CSA(CSAPTR)
         L     R1,CSATCTBA         POINT R1 TO BEGINNING OF TCT (X'128'
*                                  PAST BEG OF CSA IS ADDR. OF TCT)
         L     TCTTEAR,TCTVTEBA    POINT R5 AT BEGIN TCTTE (X'1C' PAST
*                                  BEG OF TCT IS ADDR OF 1ST TCTTE)
TCTSCAN  DS    0H
         CLI   TCTTETI,X'FF'       END OF TABLE?
         BE    ENDSCAN             YES... RETURN
         CLI   TCTTETT,TCTTECON    ENTRY FOR CONSOLE?
         BE    NXTENTRY            YES... GET NEXT ENTRY
         CLI   TCTTETT,TCTTET37    3270 REMOTE?
         BE    SETTRANS            YES, SET UP MENU TRANS
         CLI   TCTTETT,TCTTETL7    3270 LOCAL?
         BE    SETTRANS            YES, SET UP MENU TRANS
NXTENTRY DS    0H                  GET NEXT TCTTE ENTRY
         AH    TCTTEAR,TCTTETEL    ADD LTH OF ENTRY TO BEGIN ADDR
*                                  TO GET NEXT ENTRY ADDR
         B     TCTSCAN
SETTRANS DS    0H                  PLUG IN TRANS
*
*----Insert code here to omit specific terminals----*
*
         MVC   TCTTETC,MENU        MOVE IN MENU TRANS
         B     NXTENTRY            GET NEXT ENTRY
ENDSCAN  DS    0H
         DFHWTO 'MMISMENU: ALL TERMINALS HAVE BEEN INITIALIZED'
         EXEC CICS RETURN
MENU     DC    CL4'MENU'
         LTORG
         COPY  DFHCSADS            COPY CICS/VS CSA DSECT
         COPY  DFHTCTFX            COPY CICS/VS TCT DSECT
         COPY  DFHTCTTE            COPY CICS/VS TCTTE DSECT
DFHEISTG DSECT
         END   STRTMENU


Carl Gustafson
Senior Systems Programmer
American Bar Association (USA)                    ) Xephon 1990


