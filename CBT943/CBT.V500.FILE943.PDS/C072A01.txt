PLT program sequence logger

The purpose of the following program is to log at the operator
console the sequence in which programs defined in the PLT are
executed.  The name of each program is logged before it is
executed.  These messages allow us to know the name of the
active program should a problem occur in our PLT phases.  A
second benefit is knowing the duration of each PLT program.

Our PLT looks like this:

         DFHPLT TYPE=INITIAL,SUFFIX=T1
         DFHPLT TYPE=ENTRY,PROGRAM=(CICSPLTT)
         DFHPLT TYPE=ENTRY,PROGRAM=(AFCP2029)
         DFHPLT TYPE=ENTRY,PROGRAM=(CICSPLTT)
         DFHPLT TYPE=ENTRY,PROGRAM=(AFCP2DLI)
         DFHPLT TYPE=ENTRY,PROGRAM=(CICSPLTT)
         DFHPLT TYPE=ENTRY,PROGRAM=(D21H00)
         DFHPLT TYPE=ENTRY,PROGRAM=(CICSPLTT)
         DFHPLT TYPE=ENTRY,PROGRAM=(D21HMRO)
         DFHPLT TYPE=ENTRY,PROGRAM=(CICSPLTT)
         DFHPLT TYPE=FINAL
         END    DFHPLTBA

This causes the following messages to be written to the console log:

05.09.26 JOB03232  +DFH1562I - ABOUT TO LINK TO PLT PROGRAMS
05.09.28 JOB03232  +CICSPLTT PLTPI TPE1 NEXT PLT PROGRAM.. NR 01 AFCP2029
05.09.29 JOB03232  +AFCP2031 13/10/91 0509296 R3.2.01 INITIALIZED
05.09.29 JOB03232  +AFCPWARM: PCT WARM START DECLINED
05.09.29 JOB03232  +AFCPWARM: DLI PRE-ALLOCATION DECLINED
05.09.29 JOB03232  +AFCPWARM: DLI WARM START DECLINED
05.09.30 JOB03232  +AFCPWARM: FCT WARM START DECLINED
05.09.30 JOB03232  +AFCPWARM: DCT WARM START DECLINED
05.09.30 JOB03232  +AFCP2029 13/10/91 0509294 R3.2.01 INITIALIZED
05.09.30 JOB03232  +CICSPLTT PLTPI TPE1 NEXT PLT PROGRAM.. NR 02 AFCP2DLI
05.09.30 JOB03232  +CICSPLTT PLTPI TPE1 NEXT PLT PROGRAM.. NR 03 D21H00
05.09.30 JOB03232  +
05.09.30 JOB03232  +CICS.Help   By    Data 21
05.09.30 JOB03232  +Copyright (C) 1984 - 1988
05.09.30 JOB03232  + >>>   Release 4.1D   <<<
05.09.30 JOB03232  +
05.09.30 JOB03232  +BTAM, VTAM, Conversational Support
05.09.30 JOB03232  +CICS.Help Activated
05.09.30 JOB03232  +
05.09.30 JOB03232  +CICSPLTT PLTPI TPE1 NEXT PLT PROGRAM.. NR 04 D21HMRO


PROGRAM SOURCE CODE

 PLTT     TITLE 'CICS LOG PLT PROGRAM SEQUENCE'
***********************************************************************
*** IMPLEMENTATION
***        THE PROGRAM MUST BE INSERTED IN EACH PLT BEFORE THE NORMAL
***        PLT PROGRAM, BEFORE DFHDELIM, AND AFTER THE LAST PLT
***        PROGRAM.
*** DRIVEN
***        IN THE CICS START-UP AND SHUT-DOWN PLT PHASE, NON-TERMINAL.
*** TEST   JUMP INTO THE PROGRAM WITH A TRANSACTION CODE OF YOUR CHOICE
***        THE NAME OF THE TEST TRANSACTION CAN BE PLTT.
***        EACH INPUT IS A NEW SELECTION IN THE CICSPLTT PROGRAM IN THE
***        SEQUENCE OF THE PLT. THE SELECTION OF PLTPI OR PLTSD MUST
***        BE FORCED WITH A ZAP IN THE CICSPLTT PROGRAM.
***        PAY ATTENTION TO THE CWA COUNTERS.
***        THE PLT AND THE CICSPLTT PROGRAM TO BE TESTED MUST NOT BE
***        AVAILABLE FOR THE STARTUP, TO AVOID PROBLEMS IN THE STARTUP.
***        THEREFORE, MAKE THE COMPONENTS AVAILABLE AFTER STARTUP
***        WITH CEMT NEW.
*** OUTPUT LOG THE NAME OF THE PLT-PROGRAM BEFORE IT IS ENTERED,
***        AND LOG A MESSAGE AFTER THE LAST PROGRAM HAS ENDED.
*** CICS AREAS
***        2 DS H FIELDS ARE USED IN THE CWA, EACH FOR ONE TYPE OF PLT.
***        DFHCSADS
***        DFHSITDS
***      REGISTER USAGE
***      R3    BASISREG. (COMMAND-LEVEL)
***      R4    BASISREG. (COMMAND-LEVEL)
***      R5    BASISREG. (COMMAND-LEVEL)
***      R6    EXEC INTERFACE DYNAMIC STORAGE
***      R7    SIT
         USING DFHSITDS,SITCBAR
SITCBAR  EQU   R7
***      R8    CWA
         USING CICSCWAL,R8
***      R9    CSA
         USING DFHCSADS,R9
***      R10
         USING DFHPLTDS,PLTCBAR
PLTCBAR  EQU   R10
***      R11   EXEC INTERFACE BLOCK
***      R12
***      R13
***      R14   WORK
***      R15   WORK / DSECTS
         INAREG BIT       SET REGISTER AND BITS EQU
***      LOAD THE REGISTERS
CICSPLTT DFHEIENT CODEREG=(3,4,5),DATAREG=(6)
***      VARIABLES
DFHEISTG DSECT
VARIDENT DS    CL40
DOWO     DS    D                       DOUBLE WORD
MESSSAVE DS    F                       MESS ROUT SAVE R14
SYSID    DS    CL4                     SYSID
PLTNAME  DS    0CL8' '                 PLTNAME
PLTNAMED DS    CL6' '                  PLTNAME BEGIN
PLTNAMES DS    CL2' '                  PLTNAME END
RESP     DS    F                       EXEC CICS RESPONSE
ZWAPLT   DS    H                       CWA COUNTER ACCORDING TO PLT
SEQPLT   DS    H                       CICSPLTT SEQUENCE IN PLT
***      PROGRAM OUTPUT IS A MESSAGE WITH DFHWTO, NOT MORE...
CICSPLTT CSECT
         B     NACIDENT
ISTIDENT DC    CL40'CICSPLTT - PLT PROGRAM SEQUENCE LOGGER'
NACIDENT DS    0H
         MVC   VARIDENT,ISTIDENT       DUMP EYE CATCHER
         MVC   MSGTEXT,BLANK100        BLANK  MESSAGE AREA
         EXEC CICS ASSIGN SYSID (SYSID)
         MVC  MSGSYSID,SYSID           CICS SYSID IN MESSAGE
         MVC  MSGPROGN,PROGNAME        PROGRAM NAME IN MESSAGE
         EXEC CICS ADDRESS CSA (R9)
         L    SITCBAR,CSASITBA         ADDRESS SIT  R7
         EXEC CICS ADDRESS CWA (R8)
***       PROCESSING.....
*         ASK FOR PLTPI OR PLTSD PHASE, ASK FOR PLT SUFFIX NAME
*         GET CWA COUNTER ACCORDING TO PLT, ADD PLT COUNTER
** CSA SIT CWA ********************************************************
C010     DS    0H
         SR    R14,R14               HELP FOR ADD
         MVC   PLTNAMED,KDFHPLT      BUILD PLT NAME BEGIN
         TM    CSASSI2,CSAPLTPI      WHEN PLTPI PHASE ENDED
         BO    C020
         MVC   SWPISD,KPI            SAY  PI PHASE
         MVC   PLTNAMES,SITPLTPI     ADD  PLT NAME SUFFIX
         LH    R14,CWAPLTPI          GET  CWA COUNTER
         LA    R14,1(R14)            ADD  CWA COUNTER
         STH   R14,CWAPLTPI          STORE CWA COUNTER
         MVC   ZWAPLT,CWAPLTPI       GET  CWA COUNTER AS EYE CATCHER
         B     C090
C020     DS    0H
         MVC   SWPISD,KSD            SAY  SD PHASE
         MVC   PLTNAMES,SITPLTSD     BUILD PLT NAME SUFFIX
         LH    R14,CWAPLTSD          GET  CWA COUNTER
         LA    R14,1(R14)            ADD  CWA COUNTER
         STH   R14,CWAPLTSD          STORE CWA COUNTER
         MVC   ZWAPLT,CWAPLTSD       GET  CWA COUNTER FOR PLT SEARCH
C090     DS    0H
         MVC   ZWAPLTC,ZWAPLT        REMEMBER CWA COUNTER AS EYECATCHER
         MVC   MSGPHASE,SWPISD       PHASE TYPE IN MESSAGE  PI OR SD
*         LOAD  DFHPLT..
D010     DS    0H
         EXEC CICS LOAD PROGRAM (PLTNAME) ENTRY (PLTCBAR) RESP (RESP)
         OC    RESP,RESP
         BZ    D090
         MVC   TEXTLOAD+0(L'PLTNAME),PLTNAME  PLT NAME IN MESSAGE
         MVC   MSGTEXT,TEXTLOAD               MOVE MESSAGE TEXT
         BAL   R14,MESS                       MESSAGE GO OUT..
         B     RETURN                         PROGRAMS END
TEXTLOAD DC    CL60'XXXXXXXX LOAD ERROR, CICSPLTT ENDS'
D090     DS    0H
         ST    PLTCBAR,ADDRPLTE               REMEMBER PLT ADDRESS
*         FIND THE CALL NUMBER OF CICSPLTT IN THIS PLT
E010     DS    0H
         XC    SEQPLT,SEQPLT            CICSPLTT PLT SEQUENCE COUNT
         SR    R14,R14                  LOCATE PLT BEGIN - 1 ENTRY
         LA    R14,PLTEL                LENGTH PLT ENTRY
         SR    PLTCBAR,R14              LOCATE PLT BEGIN - 1 ENTRY
E015     DS    0H
         LA    PLTCBAR,PLTEL(PLTCBAR)   TRY FIRST OR NEXT ENTRY
         CLC   PLTPID,K8FF               WHETHER IT IS THE END...
         BE    E018
         CLC   PLTPID,PROGNAME          SEARCH PROG NAME CICSPLTT
         BE    E020                      FOUND, IS IT CURRENT...
         B     E015                      SEARCH AGAIN IN PLT
E018     DS    0H                             EOF PLT
         MVC   MSGTEXT,TEXTEOFP               MOVE MESSAGE TEXT
         BAL   R14,MESS                       MESSAGE SEND
         B     W010                           GO TO DELETE PLT
TEXTEOFP DC    CL60'EOFPLT AND CICSPLTT POSITION NOT FOUND'
E020     DS    0H
         SR    R14,R14                 HELP FOR ADD
         LH    R14,SEQPLT              INCREMENT PLT SEQUENCE COUNT
         LA    R14,1(R14)
         STH   R14,SEQPLT
         CLC   ZWAPLT,SEQPLT           ACTUAL PLT LOCATION FOUND
         BE    F010                     YES
         B     E015                     NO
*         SEARCH THE PROGRAM NAME AFTER THIS CICSPLTT CALL
*         LOG IT ON THE CONSOLE
*         OR  LOG  LAST PLT PROGRAM HAS ENDED
F010     DS    0H
         LA    PLTCBAR,PLTEL(PLTCBAR)   TRY NEXT ENTRY
         CLC   PLTPID,K8FF               WHETHER IT IS THE END..
         BNE   F015
         MVC   MSGTEXT,TEXTLETZ               MOVE MESSAGE TEXT
         BAL   R14,MESS                       MESSAGE SEND
         B     W010                           GO TO DELETE PLT
TEXTLETZ DC    CL60'LAST PROGRAM IN PLT HAS ENDED'
F015     DS    0H
         SR    R14,R14
         LH    R14,SEQPLT
         CVD   R14,DOWO
         OI    DOWO+L'DOWO-1,X'0F'            MAKE A PRINTABLE SIGN
         UNPK  TEXTPRON+22(2),DOWO+6(2)       MOVE PROGNR IN MESSAGE
         MVC   TEXTPRON+25(L'PLTPID),PLTPID   MOVE PROG NAME IN MESS
         MVC   MSGTEXT,TEXTPRON               MOVE MESSAGE TEXT
         BAL   R14,MESS                       MESSAGE SEND
         B     W010                           GO TO DELETE PLT
TEXTPRON DC    CL60'NEXT PLT PROGRAM.. NR XX XXXXXXXX'
*         DELETE LOADED DFHPLT..
W010     DS    0H
         EXEC CICS RELEASE PROGRAM (PLTNAME) RESP (RESP)
         OC    RESP,RESP
         BZ    W090
         MVC   TEXTDELE+0(L'PLTNAME),PLTNAME  PLTNAME IN MESSAGE
         MVC   MSGTEXT,TEXTDELE               MOVE MESSAGE TEXT
         BAL   R14,MESS                       SEND MESSAGE
         B     W090                           PROGRAM END
TEXTDELE DC    CL60'XXXXXXXX DLET ERROR, CICSPLTT ENDS'
W090     DS    0H
RETURN   EXEC  CICS RETURN
***      ROUTINES
* CONSOLE MESSAGE OUTPUT
MESS     DS   0H
         ST   R14,MESSSAVE
         DFHWTO MSGANFA
         L    R14,MESSSAVE
         BR   R14
         DS    0H
KPI      DC    C'PI'                 REMEM PI PHASE
SWPISD   DS    CL2                   REMEM PLT PI OR SD
KSD      DC    C'SD'                 REMEM SD PHASE
ZWAPLTC  DS    H                     CWA COUNTERS ACCORDING TO PLT
KDFHPLT  DC    CL6'DFHPLT'           NAME BEGIN OF PLT
ADDRPLTE DS    F                     ENTRY ADDRESS OF PLT
PROGNAME DC    CL8'CICSPLTT'            PROGRAM NAME
K8FF     DC    XL8'FFFFFFFFFFFFFFFF'    PLT END  MARKER
MSGANFA  DFHMSG (MSGPROGN,CL8' ',                                      *
               CL1' ',                                                 *
               CL3'PLT',                                               *
               MSGPHASE,CL2' ',                                        *
               CL1' ',                                                 *
               MSGSYSID,CL4' ',                                        *
               CL1' ',                                                 *
               MSGTEXT,CL60' ')
BLANK100 DC    CL100' '                BLANKS
         LTORG
***      DSECTS
         COPY DFHCSADS
CICSCWAL DSECT
CWAPLTPI DS    H                       PLTPI
CWAPLTSD DS    H                       PLTSD
         DFHSIT TYPE=DSECT,REG=SITCBAR
         COPY DFHPLTDS
         END


Guenther Hassold
INA Werk Schaeffler (Germany)
