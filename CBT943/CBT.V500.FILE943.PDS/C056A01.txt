More on CICS screen prints

THE PROBLEM

The replacement to the supplied CICS screen print facility
written by Nivaldo Moura (Issue 42) and amended by Brian
Price (Issue 49) is a vast improvement.  However, when
implementing the code in a CICS 1.7, MVS/XA 2.2
environment, I experienced a problem when two or more
screen prints were scheduled against a printer simultaneously.

What happened in this instance depended on whether a
conversational or pseudo-conversational transaction was
running at the terminal that issued the print command.  If the
transaction was pseudo-conversational, no problem existed.  If,
however, the transaction was conversational, it caused CICS to
issue the message 'DFH2497I UNAVAILABLE PRINTER'
and the print was cancelled.

THE SOLUTION

I attempted to get round this problem by implementing the
CICS sample Node Error Program (NEP).  This consists of
IBM-supplied code to reschedule the print request if it is likely
that the unavailable printer will become available soon.  To
determine this it checks for two transaction-ids running against
the printer: CSPP and CSCY.

This did not work with the updated screen print facility
implemented since the transaction that the code schedules
against the printer is CSPK not CSPP.  To fix this, I changed
the code for DFHPRK so that the transaction started against the
printer would be CSPP and so DFHPRK would identify re-
entry into the program through an indicator set up in the
COMMAREA.

These were the only changes I made to the code published in
issue 42.  With these fixes in place the screen print facility
works just fine.

         COPY  DFHTCADS
         DFHTCA CICSYST=YES
         COPY  DFHCSADS
         DFHTCTZE CICSYST=YES
         DFHREGS
SAVECOM  DSECT
         DS    CL32768
*        SET UP ENVIRONMENT
DFHPRK   DFHEIENT EIBREG=RB,CODEREG=(R3),DATAREG=RA
         L     TCTTEAR,TCAFCAAA
         LA    TCTTEAR,0(TCTTEAR)
         EXEC  CICS HANDLE CONDITION                                   X
               ERROR (SUPRT999)                                        X
               IOERR (SUPRT999)                                        X
               ENDDATA (SUPRT010)
         EXEC  CICS RETRIEVE                                           X
               SET (R8)                                                X
               LENGTH (WLENGTH)
         LH    R7,WLENGTH
         STH   R7,SAVELEN
         CLI   0(R8),X'FF'          2ND TIME THRU ?
         BE    SUPRT500             IF SO WRITE TO PRINTER
SUPRT010 LA    R0,WSHAREDA          POINT TO COMMON AREA
         LH    R1,WSHAREDL
         XR    R14,R14
         XR    R15,R15
         ICM   R15,B'1000',=X'40'
         MVCL  R0,R14               INITIALIZE COMMON AREA
         CLC   EIBTRNID,=CL4'CSPP'  CONV TRAN AGAINST PRINTER?
         BE    CONVTRAN             NO NEED TO FIND PRINTER
         XC    PRINTER,PRINTER
         MVC   PRINTER,TCTEPTO+4    STORE PRINTTO PRINTER
         CLC   PRINTER,NOPRINT
         BE    SUPRT999
         CLC   EIBTRNID,=CL4'CSPK'  PSEUDO CONV ?
         BE    READSCRN             READ DATA FROM SCREEN
CONVTRAN LA    R4,WSCRBUF
         LH    R5,SAVELEN           PROVIDED BY CICS
         LR    R9,R5
         MVCL  R4,R8                STORE BUFFER PASSED BY CICS
         MVC   PRINTER,EIBTRMID
         B     SKIPREAD             SKIP AROUND SCREEN READ
READSCRN LA    R7,WSCRBUFL
         STH   R7,WLENGTH           RECEIVE SCREEN INTO WSCRBUF
         EXEC  CICS RECEIVE                                            X
               INTO (WSCRBUF)                                          X
               MAXLENGTH (WLENGTH)                                     X
               BUFFER                                                  X
               NOHANDLE
SKIPREAD EXEC  CICS LINK                                               X
               PROGRAM ('RISFMT')                                      X
               COMMAREA (WSCRBUF)                                      X
               LENGTH (EDITL)                                          X
               NOHANDLE
         EXEC  CICS ASSIGN                                             X
               USERID (WUSERID)                                        X
               SYSID (WSYSID)
         MVC   TERMID,EIBTRMID
         MVI   IDENT,X'FF'          TO IDENTIFY SECOND TIME THRU
         LH    R7,WSHAREDL
         STH   R7,WLENGTH           STORE LENGTH OF COMMON AREA
         EXEC  CICS START                                              X
               TRANSID ('CSPP')                                        X
               FROM (WSHAREDA)                                         X
               LENGTH (WLENGTH)                                        X
               TERMID (PRINTER)
         EXEC  CICS SEND CONTROL                                       X
               FREEKB
         XR    R7,R7                CLEAR OUT R7 FOR COMPARE
         CH    R7,EIBCALEN          DO WE HAVE A COMMAREA
         BE    SKIPCOM              NO - NORMAL RETURN
         L     R7,DFHEICAP          LOAD COMMAREA ADDRESS
         USING SAVECOM,R7
         EXEC  CICS RETURN                                             X
               TRANSID (TCTTETC)                                       X
               COMMAREA (SAVECOM)                                      X
               LENGTH (EIBCALEN)
SKIPCOM  EXEC  CICS RETURN
*        PRINT BUFFER ROUTINE
SUPRT500 LA    R4,WSHAREDA          ENTER HERE 2ND TIME THRU
         LH    R5,WLENGTH
         LR    R9,R5
         MVCL  R4,R8                STORE RETRIEVED DATA IN COMM
*                                   PRINT LINE OF *S
         EXEC  CICS SEND TEXT                                          X
               FROM (LINECICS)                                         X
               LENGTH (LINECICL)                                       X
               ERASE                                                   X
               PRINT                                                   X
               NLEOM
         MVC   WLINE(WLINESL),WLINES
         EXEC  CICS ASKTIME                                            X
               ABSTIME (WLTIME)
         EXEC  CICS FORMATTIME                                         X
               ABSTIME (WLTIME)                                        X
               MMDDYY (WLDATE)                                         X
               DATESEP ('/')                                           X
               TIME (WLTIME)                                           X
               TIMESEP (':')
         MVC   WLUSERID,WUSERID
         MVC   WLTERMID(4),TERMID
         MVC   WLSYSID(4),WSYSID
         LH    R7,=AL2(WLINESL)
         STH   R7,WLENGTH           PRINT BANNER
         EXEC  CICS SEND TEXT                                          X
               FROM (WLINE)                                            X
               LENGTH (WLENGTH)                                        X
               ERASE                                                   X
               PRINT                                                   X
               NLEOM
*                                   PRINT LINE OF *S
         EXEC  CICS SEND TEXT                                          X
               FROM (LINECICS)                                         X
               LENGTH (LINECICL)                                       X
               ERASE                                                   X
               PRINT                                                   X
               NLEOM
         LA    R7,2
         STH   R7,WLENGTH
         LA    R15,BUFFOUT
         USING OUTLINE,R15
         LA    R5,WSCRBUF
         LA    R4,24
LOOP1    MVC   OUTDATA,0(R5)        SET UP 24 LINES OF DATA
         MVI   OUTCC,NEWLINE        WITH NEWLINE CC
         LA    R5,80(R5)
         LA    R15,80(R15)
         BCT   R4,LOOP1
         DROP  R15
         EXEC  CICS SEND TEXT                                          X
               FROM (BUFFOUT)                                          X
               LENGTH (DATALEN)                                        X
               ERASE                                                   X
               PRINT                                                   X
               NLEOM
         MVI   WCC,FORMFEED
         MVI   WCC+1,ENDMSG
         LA    R7,2
         STH   R7,WLENGTH           SEND END OF MESSAGE
         EXEC  CICS SEND TEXT                                          X
               FROM (WLINE)                                            X
               LENGTH (WLENGTH)                                        X
               ERASE                                                   X
               PRINT                                                   X
               NLEOM
SUPRT999 EQU   *
         EXEC  CICS RETURN
TCTTEAR  EQU   6
WLINES   EQU   *
WLINE1   EQU   *
         DC    CL18'CICS PA1 PRINT KEY'
         DC    X'15'
         DC    CL17'SYSTEM ID:.....   '
         DC    CL8' '
         DC    X'15'
         DC    CL17'DATE:..........   '
         DC    CL8' '
         DC    X'15'
         DC    CL17'TIME:..........   '
         DC    CL8' '
         DC    X'15'
         DC    CL17'USER ID........   '
         DC    CL8' '
         DC    X'15'
         DC    CL17'TERMINAL ID:...   '
         DC    CL4' '
WLINESL  EQU   *-WLINES
NEWLINE  EQU   X'15'
ENDMSG   EQU   X'19'
FORMFEED EQU   X'0C'
NOPRINT  DC    F'0'
PRINTERL DC    H'4'
LINECICS DC    X'15',79C'*'
LINECICL DC    H'080'
EDITL    DC    H'2564'
DATALEN  DC    AL2(BUFFOUTL)
WSHAREDL DC    AL2(ENDSHRDA-WSHAREDA)
         LTORG
DFHEISTG DSECT                      DYNAMIC STORAGE
SAVELEN  DS    H                    RETRIEVE SAVED AREA
WSHAREDA EQU   *                    COMMON AREA
IDENT    DS    C
WUSERID  DS    CL8
WSYSID   DS    CL4
WLENGTH  DS    H
PRINTER  DS    F
TERMID   DS    F
WLINE    DS    0C
WCC      DS    CL36
WLSYSID  DS    CL4
         DS    CL18
WLDATE   DS    XL8
         DS    CL18
WLTIME   DS    XL8
         DS    CL18
WLUSERID DS    CL8
         DS    CL18
WLTERMID DS    CL4
         DS    CL4
WSCRBUF  DS    2560C
WSCRBUFL EQU   *-WSCRBUF
         DS    F
BUFFOUT  DS    26CL80
BUFFOUTL EQU   *-BUFFOUT
ENDSHRDA EQU   *
OUTLINE  DSECT
OUTALL   DS    0CL80
OUTCC    DS    X
OUTDATA  DS    CL79
         END   DFHPRK


Roland Gross
Systems Programmer
Canada Life Assurance Co (UK)                               ) Xephon 1990


