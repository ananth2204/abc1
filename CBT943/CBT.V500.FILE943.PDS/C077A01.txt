CICS Version 3 and menu systems

We are currently running CICS Version 2 under MVS/ESA
Version 3. Like many other CICS customers we've
implemented a menu system in CICS so that users aren't
confronted with unformatted screens.

Our system runs with CA-ACF2 and consists of our own sign-
on program, a menu program, and an exit. The sign-on
program is used for both the sign-on process and to pick up
user information from the CA-ACF2 database to store in the
TCTUA.

After going through the sign-on we guide the user to a panel
where he is presented with information concerning the
application, default printer, possible transactions, etc. This
panel is also the only panel where a user can enter a
transaction. If a user ends a transaction he is brought back to
this panel. He is never allowed to enter transaction codes on
unformatted screens.

To achieve this we use the global user exit XKCREQ. Under
CICS Version 2 the task termination is handled by KCP and
therefore at task termination time we pass through the exit. In
the exit we test whether we pass there because of task
termination and whether a terminal rather than a printer is
involved. If all conditions are met we chain the TCTTE from
the TCA and check whether the TCTTETC is filled. If not we
plug our menu transaction in.

To be able to convert to CICS Version 3 we had to provide our
users with the same menu facility in the new environment.
After going through the CICS/ESA Customization Guide we
didn't think it would be a problem since XKCREQ was still
there. We tried to implement our exit in CICS Version 3 but
we never saw a task termination passing through the exit.

After making inquiries with IBM and going through Dial IBM
we found an informational APAR (PL81238) concerning our
problem. It said that from Version 3 onwards the exit
XKCREQ would no longer be driven for a task termination
request to DFHKCP because of changes to the dispatcher. It
also stated that the documentation in the Customization Guide
should be brought up to date.

To provide the same functionality under CICS Version 3 we
implemented the following exit (MENUEXT) at the exit point
XZCOUT, which is invoked before an output event.


MENUEXT code

         COPY  DFHTCTTE
TCTTEAR  EQU   11
         DFHUEXIT TYPE=EP,ID=XZCOUT
MENUEXT  CSECT
         STM   R14,R12,12(R13)        save regs
         LR    R10,R13                save R13
         BALR  R3,R0                  R3 will be base
MENUBASE DS    0H
         USING *,R3
         B     MENUSTRT               Branch around literals
         DC    CL8'MENUEXT '
         DC    CL8'&SYSDATE'
         DC    CL8'&SYSTIME'
MENUSTRT DS    0H
         LR    R4,R1                  save parameter address
         USING DFHUEPAR,R4            map parm list
         L     R2,UEPEXN              exit number address
         CLI   0(R2),XZCOUT           my exit ?
         BNE   MENUEXIT               no, get out
         L     TCTTEAR,UEPTCTTE       address TCTTE
         CLC   TCTTETC,=F'0'          return transid given ?
         BNE   MENUEXIT               yep, nothing to do
         TM    TCTTETS,TCTTEATP       dummy terminal
         BO    MENUEXIT               yep, ignore
         CLI   TCTTETT,TCTTET37       3277 remote
         BE    MENUPLUG               yep, use it
         CLI   TCTTETT,TCTTETL7       3277 local
         BNE   MENUEXIT               yep, use it
MENUPLUG DS    0H
         MVC   TCTTETC,=C'MENU'       plug menu in
MENUEXIT DS    0H
         LR    R13,R10                restore R13
         LM    R14,R12,12(R13)        restore regs
         BR    R14
         LTORG
         END

The following program is used from PLTPI to attach the
MENUEXT program to the exit points.

       IDENTIFICATION DIVISION.
       PROGRAM-ID. MENUENA.
       ENVIRONMENT DIVISION.
       DATA DIVISION.
       PROCEDURE DIVISION.
           EXEC CICS IGNORE CONDITION ERROR END-EXEC.
           EXEC CICS ENABLE EXIT('XZCOUT')
                            PROGRAM('MENUEXT')
                            START
                            END-EXEC.
           EXEC CICS RETURN END-EXEC.


Marc Cop
Systems Programmer
Jeeves Consulting NV (Belgium)

