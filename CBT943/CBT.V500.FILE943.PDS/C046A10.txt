VSAM information from your FCT - a response

I would like to make the following points concerning the supplied
DFHFCT macro that accompanied the article VSAM information
from your FCT by William J Myers in the June 1989 issue of
CICS Update.

1   The macro will not work if you have coded
ACCMETH=(VSAM,KSDS) unless it is changed so that only
the first sublist parameter of ACCMETH is compared.

2   The macro could be made considerably more efficient (and
easier to key in) by using substring notation to left-justify the
DATASET parameter value and right-justify the numeric
parameter values.

The following version of the DFHFCT macro illustrates these
points.  Text is underlined where changes have been made.

         MACRO
&LABEL   DFHFCT &TYPE=,&JREQ=,&SUFFIX=,&RELTYPE=,&FILSTAT=,&PASSWD=,   X
               &DATASET=,&BUFFERS=,&KEYLEN=,&ACCMETH=,&SERVREQ=,&RSL=, X
               &LOG=,&DISP=,&BUFND=,&BUFNI=,&RECFORM=,&LRECL=,&JID=,   X
               &BLKKEYL=,&BLKSIZE=,&DSNSHR=,&DSNAME=,&LSRPOOL=,&BASE=, X
               &STRNO=,&RMTNAME=,&SYSIDNT=
         GBLC  &SF
         LCLC  &LIND,&SNO,&DSID,&LSRP,&BFD,&BFI
         AIF   ('&TYPE' NE 'DATASET').NOPCH
         AIF   ('&ACCMETH(1)' NE 'VSAM').NOPCH
         AIF   ('&LSRPOOL' EQ 'NONE').NOLSR
         AIF   ('&LSRPOOL' EQ '').LSR1
&LIND    SETC  'LSR'
&LSRP    SETC  '&LSRPOOL'
         AGO   .STPRM
.LSR1    ANOP
&LIND    SETC  'LSR'
&LSRP    SETC  '1'
         AGO   .STPRM
.NOLSR   ANOP
&LIND    SETC  'NSR'
&LSRP    SETC  ' '
.STPRM   ANOP
&DSID    SETC  '&DATASET        '(1,8)
&SNO     SETC  '000&STRNO'(K'&STRNO+1,3)
         AIF   ('&SNO' NE '000').NZ1
&SNO     SETC  '001'
.NZ1     ANOP
&BFD     SETC  '000&BUFND'(K'&BUFND+1,3)
         AIF   ('&BFD' NE '000').NZ2
&BFD     SETC  '   '
.NZ2     ANOP
&BFI     SETC  '000&BUFNI'(K'&BUFNI+1,3)
         AIF   ('&BFI' NE '000').NZ3
&BFI     SETC  '   '
.NZ3     ANOP
         PUNCH '&SF&LIND&DSID&SNO&LSRP&BFD&BFI&DSNAME'
         MEXIT
.NOPCH   ANOP
         AIF   ('&TYPE' NE 'INITIAL').NOPCH1
&SF      SETC  '&SUFFIX'
.NOPCH1  ANOP
         MEND


Andrew Armstrong
Consultant
HPO Computer Services Pty Ltd (Australia) ) Xephon 1989


