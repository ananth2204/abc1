 A CICS print program
Our application development department was asked to write some CICS programs
that could print small lists to the CICS printers instead of using the
restricted hardcopy function but nobody was sure how to manage it.  I wrote the
following interface routine, which takes a print line with ASA control
characters in the CICS communication area and does three things:
1    Translates the ASA order to an equivalent forms feed and/or new line order.
2    Suppresses trailing blanks.
3    Sends the prepared data string  to the terminal.
The print application transaction must be started via EXEC CICS START to the
printer terminal before building the print lines and linking the interface
routine for printing each line.  The program is running with CICS 1.6 and VTAM
Version 2 under DOS/VSE 3.1, but it should run in earlier and future
environments too.
X10001   CSECT
         B     BEGIN
         DC    CL8'X10001'             PROGRAM NAME
         DC    C'&UDATE'               DATE
         DC    C'&UTIME'               TIME
         DC    C'WILLI J. KUHN'        AUTHOR
BEGIN    CLC   EIBCALEN,=AL2(X001LEN)  COMMON AREA PRESENT
         BL    C90                     NO - RETURN WITHOUT ACTION
         L     R12,DFHEICAP       LOAD COMMUNICATION AREA INPUT ADDR
         USING X001,R12
         LA    R2,PLINE           LOAD OUTPUT AREA ADDR
         CLI   X1ASA,C' '         SKIP ONE LINE
         BE    C30                YES - PREPARE
         CLI   X1ASA,C'+'         NO LINE SKIP
         BNE   C21                NO - CHECK OTHER
         MVI   PLINE,CR           SET CARRIAGE RETURN ORDER
         LA    R2,1(R2)           SET PTR FOR DATA
         B     C60                PREPARE DATA
C21      CLI   X1ASA,C'1'         SKIP TO CHANNEL ONE
         BNE   C22                NO - CHECK OTHER
         MVI   PLINE,FF           SET FORMS FEED ORDER
         MVI   PLINE+1,CR           AND CARRIGAE RETURN
         LA    R2,2(R2)           SET PTR FOR DATA
         B     C60                PREPARE DATA
C22      CLI   X1ASA,C'0'         SKIP 2 LINES
         BNE   C23                NO - CHECK OTHER
         MVI   PLINE,NL           SET NEW LINE ORDER
         MVI   PLINE+1,NL           ..
         LA    R2,R(R2)           SET PTR FOR DATA
         B     C60                PREPARE DATA
C23      CLI   X1ASA,C'-'         SKIP 3 LINES
         BNE   C30                NO - SET TO ONE LINE
         MVI   PLINE,NL           SET NEW LINE ORDER
         MVI   PLINE+1,NL           ..
         MVI   PLINE+2,NL           ..
         LA    R2,3(R2)           SET PTR FOR DATA
         B     C60                PREPARE DATA
C30      MVI   PLINE,NL           SET NEW LINE ORDER
         LA    R2,1(R2)           SET PTR FOR DATA
C60      MVC   0(L'X1ZEI132,R2),X1ZEI132    MOVE PRINT DATA
         LA    R2,L'X1ZEI132-1(R2)     POINT LAST CHAR
         LA    R0,L'X1ZEI132-1         LOOP COUNT
C61      CLI   0(R2),C' '         IS IT BLANK
         BH    C62                YES - INSERT END OF MESSAGE ORDER
         BCTR  R2,0               -1
         BCT   R0,C61             LOOP
C62      MVI   1(R2),CR           INSERT CARRIAGE RETURN
         MVI   2(R2),EM           INSERT END OF MESSAGE
         LA    R2,3(R2)           SET PTR FOR DATA
         LA    R0,PLINE           LOAD OUTPUT AREA ADDR
         SR    R2,R0              CALCULATE LENGTH
         STH   R2,PLENG           AND STORE FOR SEND
          EXEC  CICS SEND                                               *
               FROM(PLINE)                                             *
               LENGTH(PLENG)                                           *
               ERASE                                                   *
               CTLCHAR(=X'C8')
C90      EXEC  CICS RETURN
FF       EQU   X'0C'         FORMS FEED
CR       EQU   X'0D'         CARRIAGE RETURN
NL       EQU   X'15'         NEW LINE
EM       EQU   X'19'         END OF MESSAGE
         LTORG
*  DUMMY SECTIONS                                                     *
X001     DSECT
X1HV     DS    CL2           PRINTER NUMBER
X1ASA    DS    CL1           ASA CHARACTER
X1ZEI132 DS    CL132         PRINT DATA
X001LEN  EQU   *-X001        DSECT LENGTH
*  CICS/VS DYNAMIC USER STORAGE                                       *
DFHEISTG DSECT
DW       DS    D             WORK
PLENG    DS    H             PRINT LINE LENGTH
PLINE    DS    CL150         PRINT LINE
         END   X10001


Willi Kuhn
Systems Programmer
Strief AG (West Germany) ) Willi Kuhn 1988






































