 Writing your own terminal error program
OVERVIEW
Some CICS specialists working in a BTAM environment have been unaware that CICS
comes with a means of keeping remote lines and local terminals in service
whenever a network error occurs.  This article shows what those means are, the
way they work together, and how you can invoke them.
When an error occurs on the network, the default action in CICS is for control
to be passed to DFHTACP - the Terminal Abnormal Condition Program.  The default
actions that DFHTACP undertakes (usually to put the device out of service
immediately!) can be overridden from the sample Terminal Error Program (DFHTEP).
This program and its associated Terminal Error Program Table (DFHTEPT) are
generated by coding the DFHSG PROGRAM=CSO macro at system generation time.  This
will provide you with default error processing for your network.
A listing of the default error thresholds can be seen in Table 8 of Chapter 2.10
in the CICS Customization Guide immediately after the section on Define error
status elements.  The meanings of the codes are found a few pages further on in
the section Format description of TABLE DSECT.  Although the defaults are useful
as a guide I have always found a need to change at least some of them wherever I
have set up CICS systems.
If you find a need to change the defaults for your network you will need to
generate your own Terminal Error Program and Terminal Error Program Table.
DFHTEP is generated by coding and assembling macro DFHTEPM.  As well as
generating DFHTEP you can also insert your own code in exits.  This exit
facility is provided so that installation-specific action can be taken for any
given error type.
In close association with DFHTEP comes DFHTEPT - the Terminal Error Program
Table.  This is also generated by coding a macro (DFHTEPT).  In this table you
can specify how many times you will allow a certain type of error to occur
before letting the line fall out of service.
THE TERMINAL ERROR PROGRAM
The basic philosophy behind TEP seems to be Tkeep trying until you have to give
upU.  Over the years I have found that it is a successful measure in overcoming
transient errors on a BTAM network and in keeping the remote lines Tin serviceU
- although it must be stated that if a network component is completely non-
functional to Tkeep tryingU is a waste of time.  ThatUs when you call out the
engineer.
The sample jobs shown below are for a TEP and a TEPT.  The CICS level is 1.6.0
although I have assembled the same code under CICS 1.5.
The comments embedded with the examples explain much of what is happening but a
few more words of explanation might be helpful here.
For the TEP you can select the options you want supported.  In the example shown
all messages are to be written to transient data, support for remote 3270
(3270R) devices is required but not for 7770 (NO7770), an exit facility is
present (EXITS), and we do want timed threshold support (TIME), ie count how
many errors are occurring over a given time span.
In the TEP we can also specify what information we want written out to transient
data.  In the example shown everything is suppressed except for the original
error message.  The reason for this is that the other information has never
proved to be very useful and you can end up wasting a lot of time trying to
explain that the control block data isnUt going to say whether it is a fault in
the controller or the modem.  So it is better not to let it appear at all.
The exit in the example is used to put dropped remote lines back into service
and also to put local screens that have been Tunit checkedU back into service.
For example, you would get such an error if you were running under VSE under VM
and sent a message to a local screen that wasnUt TDIALLEDU in yet - in that case
you wouldnUt want the device flagged Tout of serviceU.
AN EXAMPLE OF A TERMINAL ERROR PROGRAM
TEP      TITLE 'DFHTEP FOR CICS 1.6.0'
         DFHTEPM TYPE=INITIAL,DSEWCTPR=NO,                             X
               OPTIONS=(TD,3270R,NO7770,EXITS,TIME),                   X
               PRINT=(ERRORS,NOTACPACTION,NOTEPACTION,                 X
               NOTID,NODECB,NOTACLE,NOESE)
* ******************************************************************* *
* THE FOLLOWING CODE SENDS A MESSAGE TO THE CONSOLE OPERATOR          *
* WHENEVER A UNIT CHECK OCCURS ON A LOCAL 3270 AND THE TERMINAL       *
* IS PLACED BACK IN SERVICE.                                          *
 * REMOTE LINES ARE ALSO MAINTAINED IN SERVICE.                        *
* ******************************************************************* *
         DFHTEPM TYPE=EXIT
TEPEXIT  STM   R14,R7,TWASAVE           SAVE REGISTERS
USRTEP   EQU   *
         CLI   TCTTETT,TCTTET37         REMOTE 3270?
         BNE   NOTREM                   NO-GO CHECK OTHER
         NI    TCTLEECB+1,X'73'         PUT LINE & TERM IN SERVICE
*                                       DO NOT ABEND TASK
         MVC   TERMID(4),=C'LINE'       INDICATE LINE IN MESSAGE
         B     SENDMSG
NOTREM   EQU   *
         CLI   TCTTETT,TCTTETL7         LOCAL 3270?
         BNE   TEPEND                   NO-RETURN AT ONCE
         CLI   TCTLEPFL,TCEMCUC         UNIT CHECK (X'94')?
         BNE   TEPEND                   NO-RETURN AT ONCE
         NI    TCTlEECB+1,X'F7'         PUT TERMINAL IN SERVICE
         MVC   TERMID(4),TCTTETI        SET TERMID IN MESSAGE
SENDMSG  EQU   *
WTOM     DFHWTO '*** DFHTEP *** TERMINAL XXXX PLACED BACK IN SERVICE'
TERMID   EQU   WTOM+32
TEPEND   LM    R14,R7,TWASAVE           RESTORE ENTRY REGS
         BR    R14                      RETURN TO CALLER
         DFHTEPM TYPE=FINAL
         END   DFHTEPNA
THE TERMINAL ERROR PROGRAM TABLE
In the case of the TEPT below things are fairly self-explanatory.  The TACP
passes over an error code (eg 86 POLLING LIST ERROR), which the TEP will action
according to the values given in TEPT.  In the example the device will be
dropped after five occurrences of error 86.
Where COUNT and TIME are set to zero the meaning being conveyed is that you can
never achieve this threshold so never place the device Tout of serviceU.
Some error types are given a weighting as well as a code.  The weightings are
accumulated and if the limit is reached within a specified time period the
device is put out of service (in other words if things are that bad, give up).
AN EXAMPLE OF A PROGRAM ERROR PROGRAM TABLE
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                         TEPT                                        *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         DFHTEPT TYPE=INITIAL,MAXTIDS=157,MAXERRS=25,OPTIONS=TIME
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*        PERMANENT ERROR BLOCKS FOR ALL TERMINALS                     *
*        TO ENSURE ERROR PROCESSORS ARE ALWAYS EXECUTED               *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         DFHTEPT TYPE=PERMTID,TRMIDNT=SCN2
         DFHTEPT TYPE=PERMTID,TRMIDNT=PEV1
         DFHTEPT TYPE=PERMTID,TRMIDNT=PEV2
         DFHTEPT TYPE=PERMTID,TRMIDNT=L77A
         DFHTEPT TYPE=PERMTID,TRMIDNT=L77B
         DFHTEPT TYPE=PERMTID,TRMIDNT=L77C
         DFHTEPT TYPE=PERMTID,TRMIDNT=L77D
         DFHTEPT TYPE=PERMTID,TRMIDNT=L77E
         DFHTEPT TYPE=PERMTID,TRMIDNT=L77F
         DFHTEPT TYPE=PERMTID,TRMIDNT=L77G
         DFHTEPT TYPE=PERMTID,TRMIDNT=L77H
         DFHTEPT TYPE=PERMTID,TRMIDNT=L77I
         DFHTEPT TYPE=PERMTID,TRMIDNT=L77J
         DFHTEPT TYPE=PERMTID,TRMIDNT=L77K
         DFHTEPT TYPE=PERMTID,TRMIDNT=L77L
         DFHTEPT TYPE=PERMTID,TRMIDNT=L77M
         DFHTEPT TYPE=PERMTID,TRMIDNT=L77N
         DFHTEPT TYPE=PERMTID,TRMIDNT=L77O
         DFHTEPT TYPE=PERMTID,TRMIDNT=L77P
         DFHTEPT TYPE=PERMTID,TRMIDNT=L77Q
         DFHTEPT TYPE=PERMTID,TRMIDNT=L77R
         DFHTEPT TYPE=PERMTID,TRMIDNT=L77S
          DFHTEPT TYPE=PERMTID,TRMIDNT=L77T
         DFHTEPT TYPE=PERMTID,TRMIDNT=L77U
         DFHTEPT TYPE=PERMTID,TRMIDNT=L77V
         DFHTEPT TYPE=PERMTID,TRMIDNT=L77W
         DFHTEPT TYPE=PERMTID,TRMIDNT=L77X
         DFHTEPT TYPE=PERMTID,TRMIDNT=L77Y
         DFHTEPT TYPE=PERMTID,TRMIDNT=L77Z
         DFHTEPT TYPE=PERMTID,TRMIDNT=L771
         DFHTEPT TYPE=PERMTID,TRMIDNT=L772
         DFHTEPT TYPE=PERMTID,TRMIDNT=L773
         DFHTEPT TYPE=PERMTID,TRMIDNT=L774
         DFHTEPT TYPE=PERMTID,TRMIDNT=L775
         DFHTEPT TYPE=PERMTID,TRMIDNT=L776
         DFHTEPT TYPE=PERMTID,TRMIDNT=L777
         DFHTEPT TYPE=PERMTID,TRMIDNT=L778
         DFHTEPT TYPE=PERMTID,TRMIDNT=DM01
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*  COUNT = ZERO                                                       *
*  TIME  = ZERO                                                       *
*            THE ERROR THRESHOLD WILL NEVER BE REACHED                *
*            SO THE DEVICE/UNIT/LINE WILL NOT BE DROPPED              *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*            OVERIDES OF DFHTEP DEFAULT VALUES NOW FOLLOW             *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*  81        MESSAGE TOO LONG                                         *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         DFHTEPT TYPE=ERRCODE,CODE=81,COUNT=0,TIME=0
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*  84        TCT SEARCH ERROR                                         *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         DFHTEPT TYPE=ERRCODE,CODE=84,COUNT=0,TIME=0
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*  85        INVALID WRITE                                            *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         DFHTEPT TYPE=ERRCODE,CODE=85,COUNT=0,TIME=0
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*  86        POLLING LIST ERROR                                       *
*            5 ERRORS WILL CAUSE DROP                                 *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         DFHTEPT TYPE=ERRCODE,CODE=86,COUNT=5,TIME=0
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*  87        UNSOLICITED INPUT                                        *
*            WEIGHT 100   1 MINUTE WILL CAUSE DROP                    *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         DFHTEPT TYPE=ERRCODE,CODE=87,COUNT=100,TIME=(1,MIN)
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*  88        INPUT EVENT REJECTED. LET TERMINALS ASSGND IGN DROP      *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         DFHTEPT TYPE=ERRCODE,CODE=88,COUNT=3,TIME=0
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*  89        STATUS MESSAGE RECEIVED                                  *
*            WEIGHT 100   1 MINUTE WILL CAUSE DROP                    *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         DFHTEPT TYPE=ERRCODE,CODE=89,COUNT=600,TIME=(1,MIN)
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*  8C        OUTPUT EVENT REJECTED                                    *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         DFHTEPT TYPE=ERRCODE,CODE=8C,COUNT=0,TIME=0
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*  8D        OUTPUT LENGTH ZERO                                       *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         DFHTEPT TYPE=ERRCODE,CODE=8D,COUNT=0,TIME=0
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*  8E        NO OUTPUT AREA                                           *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         DFHTEPT TYPE=ERRCODE,CODE=8E,COUNT=0,TIME=0
 * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*  8F        OUTPUT AREA EXCEEDED                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         DFHTEPT TYPE=ERRCODE,CODE=8F,COUNT=0,TIME=0
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*  94        UNIT CHECK                                               *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         DFHTEPT TYPE=ERRCODE,CODE=94,COUNT=0,TIME=0
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*  96        UNIT EXCEPTION                                           *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         DFHTEPT TYPE=ERRCODE,CODE=96,COUNT=0,TIME=0
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*  98        NEGATIVE RESPONSE                                        *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         DFHTEPT TYPE=ERRCODE,CODE=98,COUNT=0,TIME=0
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*  99        UNDETERMINED IO ERROR                                    *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         DFHTEPT TYPE=ERRCODE,CODE=99,COUNT=0,TIME=0
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*  9C        INVALID MESSAGE BLOCK                                    *
*            10  TIMES WILL CAUSE DROP                                *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         DFHTEPT TYPE=ERRCODE,CODE=9C,COUNT=0,TIME=0
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*  9D        INCOMPLETE MESSAGE                                       *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         DFHTEPT TYPE=ERRCODE,CODE=9D,COUNT=0,TIME=0
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*  9E        NO PRINTER AVAILABLE FOR 3270 PRINT REQUEST              *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         DFHTEPT TYPE=ERRCODE,CODE=9E,COUNT=0,TIME=0
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                  END   OF   OVERIDE   VALUES                        *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         DFHTEPT TYPE=FINAL
         END
FURTHER INFORMATION
A full explanation of the Terminal Error Program and its associated control
blocks and macros can be found in the manual CICS/VS Customization Guide (SC33-
0131-1) in Chapter 2.10 The Terminal Error Program.  The explanation of what is
going on is perhaps a little daunting.  Hopefully the examples given above will
provide some orientation and clarification.
VTAM users should turn to the next chapter in the Customization Guide (2.11)
where a similar strategy is outlined for a Node Error Program using macros
DFHSNEP and DFHZNEP.

Thomas Harper
Senior Systems Programmer (UK)     ) Xephon 1988
















