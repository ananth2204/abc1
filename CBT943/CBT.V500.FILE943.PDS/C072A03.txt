CICS security - part three

Part three of our look at CICS security will describe the 'meat'
of the system, ie the programs which actually allow a CICS
user to sign on and change his password.  However, before
describing the programs, two more files need to be defined.

The first, a VSAM KSDS file called V997, contains details of
failed sign-on attempts.  To prevent the guessing of passwords,
a count is kept of the number of attempts made, where the
password entered is incorrect.  Once that total is reached, a
record is written to V997, which contains the operator name in
use, the terminal-id (also the key - 4 bytes taken from the
TCTTETI field), and the time and date of the failure.  Because
this count of failed attempts is stored in the record of the
operator making the attempts, it will survive disconnections as
well as CICS down times.

V997 records can be read by SNP5 (which will be described in
part four of this series).  This program will enable the security
administrator to look through the records in the file and record
details of potential security breaches.  While a terminal has an
outstanding record on V997, SNP1 will not allow it to go
through the sign-on procedure, effectively preventing access to
CICS.  Once satisfied that he understands the reason for a
terminal-id existing on this file, the security administrator can
delete the record, allowing CICS users to use that terminal.

The V997 file contains the following fields:

o      Terminal (bytes 1-4) - CICS terminal-id.

o      Name (bytes 5-12) - Operator name (first 8 bytes).

o      Date (bytes 13-16) - Date of failure (packed Julian format).

o      Time (bytes 17-20) - Time of failure (packed in format
      returned by DFHIC GETTIME FORM=PACKED macro).

It is recommended that this file is defined in the same way as
the V999 file, in that it should be password protected.  The
IDCAMS definition for V999 should suffice with KEYS(4 0)
and RECSZ(20 20) specified.  The  size should depend on how
many failures of this type you would expect.  In reality, it is
unlikely  that many records will be written to this file, so the
IMBED and  REPLICATE parameters can probably be
dispensed with.  Similarly, the FCT definition needs little
change from the V999 definition described in part two.

Remember that CICS requires a file to be loaded, so a dummy
record must be created on V997.  The V997 maintenance
program will ignore records with a key of 0000 (character
zeros), so it is recommended that a record is loaded with that
key.

The second file, V998, is a VSAM ESDS which contains a
single 540-byte record.  That record is read in, split into nine
lines of 60 bytes, and put into the message area of the sign-on
screen.  It is used by the systems administrator or operations
personnel to inform CICS users of any event, for example
when the system will be unavailable for maintenance.

Like the V997 and V999 files, password protection will
probably be required.  The following IDCAMS definition will
create a V998 file:

      DEFINE CL (NAME(V998.PROD) FILE(V998) SHR(3) VOL(vvvvvv) -
                TRK(1) RECSZ(540 540) NUMBERED -
                READPW(filepassword) ) -
                CAT(vvvvvv.UCAT/catalogpassword)

The FCT entry described in part two for V999 will  suffice
(with minimal change) for V998.

The program displays are shown in Figures 1 and 2.  Figure 1
shows the sign-on screen itself.  The heading line should be
changed on the map SIGNMP1.  On the top right-hand corner
is the terminal-id (as in TCTTETI) and the VTAM system
name (ie the APPLID).  This information is provided to assist
the systems or network administrator, or the help desk in
identifying which terminal a CICS user is having trouble with,
and which CICS system he is using.  Experience shows that
often problems are encountered because the user is at a
terminal which was previously logged on to a CICS system
which the user is not entitled to use.  The help desk operator
can use these fields to check if this is the case.

The user then merely types in his CICS operator name and
password, and presses ENTER or PF1.  On successful sign-on,
the user will be returned to CICS (with a sign-on completion
message), unless PF1 is pressed.  If PF1 has been pressed,
Figure 2 will be presented to the user.

Because the two fields are in non-display mode, the password
should then be entered twice to ensure correct typing.  During
sign on, any error messages are displayed to the right of the
password, in high intensity.  Once the password has been
verified, if the password is due to expire within 30 days and
PF1 has not been pressed, a high intensity message will be
displayed.  This can be ignored by pressing ENTER, or the
user can press PF1 to enter the password change panel
described above.

Sometimes, especially among occasional CICS users, the 30-
day countdown will be exceeded.  In these circumstances, a
message will be displayed informing the user that he has one
last chance before his password is de-activated.  He will be
forced into the password change screen, and, should he still
refuse to change his password, his password will be de-
activated.  Any further attempt to sign on will not be permitted
until the security administrator has re-activated the sign-on
code.

A PCT entry is required with any transaction-id, although, if
preferred, the program pointed to by CSSN can be used.  One
approach which could be used would be to copy the CSSN
entry to  CSTN perhaps, and rename the program parameter to
SNP1 instead of DFHSNP.  A further entry is required for
SNP2, because SNP1 performs a CICS return to the transaction
SNP2 (as opposed to the program SNP2).  The reason for
doing it this way will be obvious to users of any of the CICS
monitors, who are probably fed up with having CSSN
transactions filling up their monitor screens.  These
transactions are the result of users returning to the CICS sign-
on screen and, quite reasonably, leaving that screen displayed -
however, CSSN sits waiting on terminal control I/O.  To avoid
this happening, SNP1 outputs the map to the screen and
transfers control to a new transaction (SNP2), which performs
the BMS MAP command.

VTAM users might like to alter the CSGM transaction code to
point to SNP1 as well.  The result of this will be to present the
user with the sign-on screen on entry to CICS from VTAM.  It
is recommended that both CSSN and CSGM are specified as
pointing to SNP1, to allow entry to the sign-on program from
both VTAM and CICS.

The PCT entry (or entries) for program SNP1 does not require
a TWA, while SNP2 requires a TWA of 164 bytes.  Note that
if you do not wish to use a transaction code of SNP2 for
program SNP2, the DFHPC TYPE=RETURN must be altered
to reflect the transaction code you do wish to use.

PPT entries are required for the mapset (SIGNMP1), and the
Assembler programs SNP1 and SNP2.  If you choose to
include  the code which initializes the terminal time-out
transaction, the SNP1 program entry must specify the program
as resident.

Finally, it should be noted that, because this system is merely
an interface to the full CICS security mechanism, DFHSNP is
perfectly adequate for sign off.  The standard CICS CSSF
transaction should therefore be used, in fact it is strongly
recommended that the CSSF PCT entry is not altered.


SNP1

         DFHCOVER
         COPY  KEYEQ                                                     note 1
         COPY  DFHCSADS
         COPY  DFHTCADS
         COPY  DFHTCTFX                                                  note 2
         DFHSNT TYPE=DSECT                                               note 3
         USING DFHSNNT,SNTTEAR                                           note 3
SNTTEAR  EQU   1
TCTPFBAR EQU   6
FWACBAR  EQU   8
BASE     EQU   9
TIOABAR  EQU   10
TCTTEAR  EQU   11
         COPY  DFHTCTTE
         COPY  DFHTIOA
TREC     DS    CL20
         ORG   TREC
         COPY  SIGNMP1
         COPY  DFHFWADS
RECORD   DS    0CL540
RL1      DS    CL60
RL2      DS    CL60
RL3      DS    CL60
RL4      DS    CL60
RL5      DS    CL60
RL6      DS    CL60
RL7      DS    CL60
RL8      DS    CL60
RL9      DS    CL60
         ORG   RECORD
BBREC    DS    0CL20
BBTERM   DS    CL4
SNP1     CSECT
         BALR  BASE,0
         USING *,BASE
         L     TCTTEAR,TCAFCAAA
         L     TIOABAR,TCTTEDA
         L     TCTPFBAR,CSATCTBA                                         note 2
         XC    TCTTEOI,TCTTEOI         CLEAR SYSTEM FIELDS               note 4
         MVC   TCTTESK,=X'000001'
         L     SNTTEAR,TCTTESNT                                          note 3
         MVC   SNNTSK,=X'000001'                                         note 3
         XC    TCTTEOP,TCTTEOP                                           note 4
         MVI   TCTTETS,X'04'                                                |
         MVI   TCTTEIC,X'0D'                                             note 4
*              NOW WE WRITE THE INITIAL SCREEN
         BAL   R7,TFREE
*                       THE FOLLOWING CODE INITIALIZES THE TIME OUT
*                       TRANSACTION.  THE CANCEL IS TO ENSURE THAT
*                       IT IS NOT INITIALIZED TWICE
INITBR   BC    0,NOTINIT                                                 note 5
         DFHIC TYPE=CANCEL,REQID=DFHCSSC                                    |
         DFHIC TYPE=INITIATE,REQID=DFHCSSC,TRANSID=CSSC,               *    |
               INTRVAL=003000                                            note 5
         OI    INITBR+1,X'F0'
NOTINIT  EQU   *
         DFHFC TYPE=GET,DATASET=V997,RDIDADR=TCTTETI,SRCHTYP=FKEQ,     * note 6
               MODE=MOVE,NOTFND=NOTBAD,ERROR=DUMP
         LA    R1,FAILSZ
         STH   R1,TCASCNB
         DFHSC TYPE=GETMAIN,CLASS=TERMINAL,INITIMG=00
         L     TIOABAR,TCASCSA
         ST    TIOABAR,TCTTEDA
         LA    R1,FAILSZ
         STH   R1,TIOATDL
         MVC   FAILMSG+9(4),TCTTETI
         MVC   TREC(FAILSZ),FAILSCR
         DFHTC TYPE=(ERASE,WRITE),CTLCHAR=C2
         DFHPC TYPE=RETURN
NOTBAD   EQU   *
         DFHSC TYPE=GETMAIN,CLASS=TERMINAL,INITIMG=00,                 * note 7
               NUMBYTE=SIGNMP1E-TIOADBA
         L     TIOABAR,TCASCSA
         ST    TIOABAR,TCTTEDA
         MVC   SM1TIO,TCTTETI          MOVE TERMINAL ID TO SCREEN
         DFHFC TYPE=GET,DATASET=V998,ARGTYP=RBA,                       *
               MODE=MOVE,RDIDADR=RKEY,NOTFND=FINISH,NOTOPEN=NOMSG,     *
               ERROR=DUMP
         L     FWACBAR,TCAFCAA
         MVC   SM1MSG1O,RL1
         MVC   SM1MSG2O,RL2
         MVC   SM1MSG3O,RL3
         MVC   SM1MSG4O,RL4
         MVC   SM1MSG5O,RL5
         MVC   SM1MSG6O,RL6
         MVC   SM1MSG7O,RL7
         MVC   SM1MSG8O,RL8
         MVC   SM1MSG9O,RL9
         MVC   SM1SYSO,TCTVAPPN                                          note 2
NOMSG    EQU   *
         DFHBMS TYPE=(OUT,ERASE),DATA=YES,MAP=SIGNMP1
FINISH   EQU   *
         DFHPC TYPE=RETURN,TRANSID=SNP2                                  note 8
TFREE    EQU   *                       * FREE TERMINAL STORAGE
         ST    TIOABAR,TCASCSA
FREEST   EQU   *
         DFHSC TYPE=FREEMAIN
         BR    R7
DUMP     EQU   *                       * SYSTEM ERROR - DUMP
         DFHDC TYPE=CICS,DMPCODE=CSSN
RKEY     DC    F'1'
FAILSCR  EQU   *
         SCFMT SBA,(1,1),SF,PRHI
FAILMSG  DC    C'TERMINAL XXXX IS UNAVAILABLE - CONTACT I.S.'
FAILSZ   EQU   *-FAILSCR
         LTORG
         END

The following notes are to sections of the code, which you may
wish to omit, put in your own systems, or simply understand
what they are doing.  To understand the system fully will
require an understanding of the basic principles of the CICS
security system, which will be explained as appropriate.

1      This copy book was fully described in part two.  It is used
      to represent the keyboard Attention Identification (AID)
      bytes.

2      The TCT prefix is used to access the VTAM APPLID.
      The DFHTCTFX copy  book uses TCTPFBAR as its
      address register, which comes from the CSA in field
      CSATCTBA.  The VTAM APPLID is stored in
      TCTVAPPN.

3      Before CICS 1.7, security data from DFHSNT was put
      into the Terminal Control Table Terminal Entry (TCTTE)
      for the terminal at which the operator was logging on.
      CICS 1.7 introduced the concept of an SNNT entry to
      contain security information.  The address of this control
      block is stored in the TCTTE in TCTTESNT, which points
      to a general use SNNT entry when the user first gets into
      CICS.  This default entry gives security key 1, which is
      required to enter the standard sign-on transaction.  Once
      logged on, a terminal-related SNNT entry is created which
      contains the user's individual security information and the
      address in TCTTESNT is replaced by the address of this
      terminal-specific SNNT.  Under special circumstances, for
      example where all users of a particular system could access
      all transactions on that system, it would be possible to
      change the standard SNNT to allow all users to get into
      particular transactions without signing on at all.  The
      DFHSNT TYPE=DSECT creates a DSECT which enables
      the SNNT to be accessed by standard names.  While the
      individual SNNT entry is not created until after name and
      password verification, on entry to SNP1, the security keys
      are set to 1.  This is to allow for the situation where a user,
      who is already signed on, enters the sign-on transaction
      code.  If SNP1 did not reset the security keys, it would be
      possible for the user to clear from the program, but leave
      the old security keys intact.  The terminal could then be
      used for the appropriate transactions with no one signed on.

4      The TCTTE security fields are initialized.  This code is for
      compatibility with CICS releases before 1.7.

5      This code initiates the terminal time-out transaction,
      CSSC.  Firstly, an Interval Control CANCEL is issued, in
      case a CSSC Interval Control Element already exists (it
      should not, but just to be sure).  Then the Interval Control
      INITIATE starts CSSC.  Note the INTRVAL parameter.
      This is specified in hhmmss format, so in this case the
      interval is set to 30 minutes.  The interval can therefore be
      simply altered by changing this parameter.  To ensure that
      the CSSC transaction is not entered more than once, this
      code commits what to some CICS programmers is the
      cardinal sin.  It is not re-entrant, in that it changes itself so
      that the branch at INITBR is always taken after the first
      time through.  This is why SNP1 must be resident,
      otherwise every time the code is replaced in storage, the
      CSSC transaction is cancelled, and then re-initiated.  If
      you really dislike this way of achieving the single pass of
      the code, you might consider using an indicator in the CSA
      which can be checked to see if CSSC has been initiated.
      SNP1 will not need to be resident if this approach is
      adopted.  In addition to initiating the CSSC transaction, it
      will be necessary to code SIGNOFF=YES in the TCT (or
      equivalent).  IBM had problems with SIGNOFF=
      LOGOFF in early releases of CICS 1.7, although they are
      probably fixed by now.  Additionally, the SNNTTMOU
      field of the SNNT has to be set with the time-out period in
      minutes (see SNP2) - in this case it is set to 30.

6      This section of code reads V997 to check whether the
      terminal is to be denied access because of too many
      password error attempts.

7      The code from here creates the output screen, fills in any
      messages, and writes it out.

8      Control is passed back to CICS, specifying SNP2 as the
      next transaction to be initiated.


SOURCE CODE FOR SIGNMPS MAP

SIGNMPS  DFHMSD TYPE=&SYSPARM,MODE=INOUT
SIGNMP1  DFHMDI CTRL=FREEKB,SIZE=(24,80)
         DFHMDF POS=(1,2),ATTRB=(PROT,BRT),LENGTH=32,                  *
               INITIAL='WHATEVER HEADING YOU LIKE       '
         DFHMDF POS=(1,54),ATTRB=(PROT,NORM),LENGTH=12,                *
               INITIAL='TERMINAL ID:'
SM1TI    DFHMDF POS=(1,67),LENGTH=4
         DFHMDF POS=(2,2),ATTRB=(PROT,BRT),LENGTH=32,                  *
               INITIAL='--------------------------------'
         DFHMDF POS=(2,54),ATTRB=(PROT,NORM),LENGTH=12,                *
               INITIAL='THIS SYSTEM:'
SM1SYS   DFHMDF POS(2,67),LENGTH=8
         DFHMDF POS=(4,7),ATTRB=(PROT,NORM),INITIAL='TO SIGN ON, TYPE Y*
               OUR PASSWORD AND NAME, THEN PRESS ENTER',LENGTH=57
         DFHMDF POS=(6,7),ATTRB=(PROT,NORM),INITIAL='IF YOU WISH TO CHA*
               NGE YOUR PASSWORD, PRESS PF1 INSTEAD OF ENTER',LENGTH=63
         DFHMDF POS=(8,7),ATTRB=(PROT,BRT),LENGTH=58
         DFHMDF POS=(11,7),ATTRB=(PROT,NORM),                          *
               INITIAL='******* NAME:',LENGTH=13
SM1NAME  DFHMDF POS=(11,21),ATTRB=(UNPROT,NORM,IC),LENGTH=20
         DFHMDF POS=(11,42),ATTRB=(ASKIP,NORM),INITIAL='PASSWORD:',    *
               LENGTH=9
SM1PW    DFHMDF POS=(11,52),ATTRB=(UNPROT,DRK),LENGTH=4
         DFHMDF POS=(11,57),ATTRB=(ASKIP,BRT),LENGTH=12
         DFHMDF POS=(13,7),ATTRB=(PROT,BRT),INITIAL='MESSAGES:',       *
               LENGTH=9
         DFHMDF POS=(14,7),ATTRB=(PROT,BRT),INITIAL='---------',       *
               LENGTH=9
SM1MSG1  DFHMDF POS=(16,10),ATTRB=(PROT,NORM),LENGTH=60
SM1MSG2  DFHMDF POS=(17,10),ATTRB=(PROT,NORM),LENGTH=60
SM1MSG3  DFHMDF POS=(18,10),ATTRB=(PROT,NORM),LENGTH=60
SM1MSG4  DFHMDF POS=(19,10),ATTRB=(PROT,NORM),LENGTH=60
SM1MSG5  DFHMDF POS=(20,10),ATTRB=(PROT,NORM),LENGTH=60
SM1MSG6  DFHMDF POS=(21,10),ATTRB=(PROT,NORM),LENGTH=60
SM1MSG7  DFHMDF POS=(22,10),ATTRB=(PROT,NORM),LENGTH=60
SM1MSG8  DFHMDF POS=(23,10),ATTRB=(PROT,NORM),LENGTH=60
SM1MSG9  DFHMDF POS=(24,10),ATTRB=(PROT,NORM),LENGTH=60
SIGNMAPS DFHMSD TYPE=FINAL

Part three of our look at CICS security will be continued next
month, when we shall be publishing the source code for the
program, SNP2.

Robin Lynch
Software Support Manager (UK)`                  c Xephon/Reserved 1991

