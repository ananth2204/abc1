CP commands under CICS

We have VSE/SP 2.1.7 under VM/SP 5.0 and CICS 1.6.

We wrote two programs to solve the following problems:

o      We needed to print listings under CICS, but the terminals
      and printers are under VM/SP.

o      When our users need to go to CICS, they use the DIAL
      command.

o      The printers are attached to CICS by the CP NET
      ATTACH or CP ATTACH commands.  We attached these
      printers before CICS start-up, but sometimes, during the
      opening of the terminals, there is a waiting period until the
      remote or local printers are turned on.

So, we decided to write two programs: the first one, CPCMDS,
calls the second one, $$BCPCMD, which executes the CP
command and returns to the caller program.


SOURCE CODE FOR CPCMDS

* FUNCTION  - EXECUTE A CP COMMAND UNDER CICS.
*           - UNATTACH VIRTUAL ADDRESS AND CALL $$BCPCMD THAT
*             EXECUTE NET ATT TO REMOTE/LOCAL PRINTERS.
CPCMDS   CSECT
         LA     7,LOGCCB                    CONSOLE CCB
         LA     8,TABCPC-10                 TABLE OF  RESOURCES
LOOPATT  LA     8,10(8)                     POINT TO NEXT ADDR.
         CLC    0(10,8),=C'FFFFFFFFFF'      END OF TABLE ?
         BE     RETURN                      YES, RETURN
         MVC    CPCMD,=CL80'VARY ONL XXX'   VARY ONLINE
         MVC    CPCMD+9(3),0(8)             MOVE REAL ADDRESS
         BAL    9,EXECTRNS                  EXEC. CP COMMAND
         CLC    CPCMD+72(8),=C'00000040'    TEST RETURN CODE, IF IS A
         BNE    VERIFY                      LINE. YES, GO TO
         MVC    MSG,=CL60'ENDA001I LINE XXX NOT ON SYSTEM '
         MVC    MSG+15(3),0(8)              MOVE REAL ADDR. TO MSG
         B      EXECEXCP                    SEND MESSAGE TO CONSOLE
VERIFY   CLC    6(1,8),=C' '                LINE OF 3705 ?
         BNE    EXVARONL                    NO, GO TO
         MVC    CPCMD,=CL80'DISA XXX'       PUT ADDR. LINE IN DISABLE
         MVC    CPCMD+5(3),0(8)             MOVE REAL ADDR. TO CP CMD
         BAL    9,EXECTRNS                  EXEC. CP COMMAND
         MVC    CPCMD,=CL80'ATT XXX * XXX'  ATTACH REAL ADDR. VIR. MAC
         MVC    CPCMD+4(3),0(8)             MOVE REAL ADDR.
         MVC    CPCMD+10(3),7(8)            MOVE VIRT. ADDR.
         BAL    9,EXECTRNS                  EXEC. CP COMMAND
         B      TESTRETC                    VERIFY IF THERE WAS ERROR
EXVARONL MVC    CPCMD,=CL80'NET VARY ONL XXXX'
         MVC    CPCMD+13(4),3(8)            MOVE RESID TO CP CMD
         BAL    9,EXECTRNS                  EXEC. CP CMD
         MVC    CPCMD,=CL80'NET ENA XXXX'   ENABLE RESOURCE ID
         MVC    CPCMD+8(4),3(8)             MOVE RESID TO CP CMD
         BAL    9,EXECTRNS                  EXEC. CP COMMAND
         MVC    CPCMD,=CL80'NET ATT XXXX * XXX'
         MVC    CPCMD+8(4),3(8)             ATTACH RESID TO VIR. MACH.
         MVC    CPCMD+15(3),7(8)            MOVE RESID TO CP CMD
         BAL    9,EXECTRNS                  EXEC. CP CMD
TESTRETC CLC    CPCMD+72(8),=C'00000000'    TEST RETURN CODE OF
         BE     SENDMSG                        CP COMMAND
         CLC    CPCMD+72(8),=C'00000122'
         BE     LOOPATT
         CLC    CPCMD+72(8),=C'00000120'
         BE     DETGRAF
         MVC    MSG,=CL60'ENDA002I NET ATTACH RC=XXXXXXXX - FATAL ERROX
                R'.
         MVC    MSG+23(8),CPCMD+72
         B      EXECEXCP
SENDMSG  MVC    MSG,=CL60'ENDA003I RESOURCE ID XXXX ATTACHED AS XXX.'
         MVC    MSG+21(4),3(8)
         MVC    MSG+40(3),7(8)
EXECEXCP EXCP   (7)
         B      LOOPATT
DETGRAF  MVC    CPCMD,=CL80'DET XXX'
         MVC    CPCMD+4(3),7(8)
         BAL    9,EXECTRNS
         B      VERIFY
EXECTRNS EQU    *
         CNOP   0,4
         LA     1,=CL8'$$BCPCMD'
         BAL    0,SVC2
         DC     A(CPCMD)
         DC     F'80'
SVC2     SVC    2
         BR     9
RETURN   EXEC   CICS RETURN
         LTORG
         DS     0D
DOUBWORD DS     D
CPCMD    DC     CL80' '
* TABLE OF RESOURCES
TABCPC   DS     0D
*        WHERE:
*              CUU  = VM PRINTER REAL ADDR. (REMOTE/LOCAL)
*              RSID = RESOURCE ID (REMOTE LINES UNDER VM)
*              VAD  = CICS PRINTER VIRTUAL ADDR.
*              RADD = REAL ADDR. TO LOCAL PRINTERS UNDER VM.
*  CHANGE THIS TABLE ACCORDING YOUR INSTALATION.
*        DC     C'CUU',C'RSID',C'VAD' REMOTE PRINTER UNDER VM
         DC     C'0DA',C'1A08',C'5D7'    ACCESSED BY CICS.
*        DC     C'CUU',C'RADD',C'0D0' LOCAL PRINTER UNDER VM
         DC     C'05D',C'0BD ',C'0D0'    ACCESSED BY CICS.
         DC     C'FFFFFFFFFF'
MSG      DS     CL60
         DS     0D
LOGCCB   CCB   SYSLOG,LOGCCW
LOGCCW   CCW   X'09',MSG,X'20',L'MSG
         END


SOURCE CODE FOR $$BCPCMD

// LIBDEF PHASE,CATALOG=IJSYSRS.SYSLIB
// OPTION CATAL,PROD
   PHASE $$BCPCMD,+0
// EXEC ASSEMBLY
$$BCPCMD START 0
         USING *,15
         DC    CL8'$$BCPCMD'
         NOPR  0
INICIO   LR    1,0          R0 RECEIVE PARAMETERS
         SR    0,0
         SVC   22
         MVC   DB,96        SAVE NEW PSW
         NI    96(0),X'FC'
         LA    6,NEWPSW
         STCM  6,7,101      ALTER IN THE NEW PSW THE ADDR. OF INSTR.
         SVC   255
NEWPSW   MVC   96(8,0),DB
         LM    2,3,0(1)
         STIDP DB
         CLI   DB,X'FF'
         BE    DIAG
         MVC   72(8,2),=CL8'NOT VM'
         B     EXIT
DIAG     LR    12,2
         LA    13,80
         DC    X'83CD0008'  DIAGNOSE INSTRUCTION
         CVD   13,DB
         LR    3,2
         UNPK  72(8,3),DB
         OI    79(3),X'F0'
EXIT     LA    0,1
         SVC   22
         SVC   11           RETURN TO CICS
DB       DC    D'0'
         LTORG
         END
/*
// EXEC LNKEDT
/*


PPT ENTRY

CPCMDS   DFHPPT TYPE=ENTRY,                                            X
               PROGRAM=CPCMDS,                                         X
               RSL=1,                                                  X
               PGMLANG=ASSEMBLER


PCT ENTRY

ATNA     DFHPCT TYPE=ENTRY,                                            X
               TRANSID=ATNA,                                           X
               PROGRAM=CPCMDS,                                         X
               RSL=1


INSTALLATION

Compile and catalogue the first program into a library
accessible to CICS.  Put the new PPT and PCT entries into
your CICS tables.  If you want you can put an entry for
CPCMDS in your PLTPI.  Assemble and link $$BCPCMD into
the IJSYSRS.SYSLIB library.


Sebastiao Antonio Campos
Technical Support Manager (Brazil)


