 Displaying dataset characteristics
The following transaction is used to display the dataset characteristics of any
of our CICS files.  For VSAM datasets the string and buffer statistics are also
displayed.  Figure 1 illustrates an example of how an ESDS dataset would be
displayed, Figure 2 shows a KSDS file, Figure 3  shows an alternate index,
Figure 4 shows a remote dataset, and Figure 5 shows a DL/I database.
This program is written for CICS 1.6.1 and MVS/SP 1.3.7.
*** Macro TESTM is used to test value in DFHFCTDS and give the   ***
*       result in a desired position on the screen.                *
*   Macro EDHLF is used to transform half word fields in editabl   *
*       fields and give it on the screen.                          *
*   Macro EDONE is the same as EDHLF but with one byte fields.     *
*** Macro EDPAK is used for DL/I stats fields in packed format.  ***
         MACRO                        * TEST DFHFCTDS *
         TESTM &DS=DS,&TM=TM,&POS=POS,&VAR=VAR
         TM    &DS,&TM
         BZ    *+10
         MVC   &POS,=CL8'&VAR'
         DS    0H
         MEND
         MACRO
         EDHLF &BIN=BIN,&POSI=POSI    * EDITABLE FORMAT FOR HALF WORD *
         XR    WREG,WREG
         LH    WREG,&BIN
         CVD   WREG,NUMRC
         MVC   &POSI,=XL16'40202020202020202020202020202120'
         ED    &POSI,NUMRC
         MEND
         MACRO
         EDONE &BI1=BI1,&POS1=POS1     * EDITABLE FORMAT FOR 1 BYTE *
         XR    WREG,WREG
         IC    WREG,&BI1
         CVD   WREG,NUMRC
         MVC   &POS1,=XL16'40202020202020202020202020202120'
         ED    &POS1,NUMRC
         MEND
         MACRO
         EDPAK &PO2=PO2,&PO21=PO21,&VA2=VA2,&DS2=DS2  * STATS DL/I *
         MVC   &PO2,=CL6'&VA2'
         MVC   &PO21,=XL6'402020202120'
         ED    &PO21,&DS2
         MEND
DFHEISTG DSECT
         DS    0F
LNG      DS    H                   * Length of screen
SCR      DS    CL1800              * Screen
TRINP    DS    0CL13               * Trans,dataset name
         DS    CL5                 * trans,
NAMEDS   DS    CL8                 * dataset name
         DS    0H
NUMRC    DS    D                   * Work Area
FCTDSBAR EQU   5                   * Base Register for FCT
WREG     EQU   7                   * Work Register
         COPY DFHCSADS             * Copy CSA Dsect
         COPY DFHFCTDS             * Copy FCT
VSAMLST  DFHEIENT CODEREG=(2)
         EXEC  CICS ADDRESS CSA(WREG)
         USING DFHCSADS,WREG                 * Give addr to CSA
         L     FCTDSBAR,CSAFCTBA
         USING DFHFCTDS,FCTDSBAR             * Give addr to FCT
         MVC   NAMEDS,=CL8'         '        * Move blank in nameds
         MVC   LNG,=H'13'                    * Receive trans,dsn
         EXEC  CICS RECEIVE INTO(TRINP) LENGTH(LNG)
LOOP     CLI   FCTDSID,X'FF'                 * Is this last FCT entry
         BE    ENDPRG                        * Yes, skip end program
         CLC   FCTDSID,=CL8'DFHFCTSR'        * FCT ended. Vsam SR
          BE    ENDPRG                        * Yes, skip end program
         CLC   FCTDSID,NAMEDS                * Match Name
         BE    TESTTM                        * Dataset elab.
         XR    WREG,WREG                     * Clear work reg.
         ICM   WREG,3,FCTDSTEL               * Entry length
         AR    FCTDSBAR,WREG                 * Add next table entry
         B     LOOP                          * Go next entry
*** Vsam - DL/I - Isam - Bdam - Remote
TESTTM   MVC   SCR+16(8),=CL8'Close    '
         TESTM DS=FCTDSOPN,TM=FCTOPNIM,POS=SCR+16(8),VAR=Open
         TESTM DS=FCTDSVSM,TM=FCTVSAMI,POS=SCR+96(8),VAR=Vsam
         TM    FCTDSVSM,FCTVSAMI
         BNZ   XVSAM
         TESTM DS=FCTDSDLI,TM=FCTDLII,POS=SCR+96(8),VAR=DL/I
         TM    FCTDSDLI,FCTDLII
         BNZ   XDLI
         TESTM DS=FCTDSVR3,TM=FCTDSRBM,POS=SCR+96(8),VAR=Remote
         TM    FCTDSVR3,FCTDSRBM
         BNZ   XREMOTE
         TESTM DS=FCTDSISM,TM=FCTISAMI,POS=SCR+96(8),VAR=Isam
         TESTM DS=FCTDSBDM,TM=FCTBDAMI,POS=SCR+96(8),VAR=Bdam
         B     STAMPA
*** Only DL/I
XDLI     EDPAK PO2=SCR+256(6),PO21=SCR+262(6),VA2=Gu____,DS2=FCTDLGU
         EDPAK PO2=SCR+336(6),PO21=SCR+342(6),VA2=Gn____,DS2=FCTDLGN
         EDPAK PO2=SCR+416(6),PO21=SCR+422(6),VA2=Gnp___,DS2=FCTDLGNP
         EDPAK PO2=SCR+496(6),PO21=SCR+502(6),VA2=Ghu___,DS2=FCTDLGHU
         EDPAK PO2=SCR+576(6),PO21=SCR+582(6),VA2=Ghn___,DS2=FCTDLGHN
         EDPAK PO2=SCR+656(6),PO21=SCR+662(6),VA2=Ghnp__,DS2=FCTDLGHP
         EDPAK PO2=SCR+736(6),PO21=SCR+742(6),VA2=Isrt__,DS2=FCTDLISR
         EDPAK PO2=SCR+816(6),PO21=SCR+822(6),VA2=Dlet__,DS2=FCTDLET
         EDPAK PO2=SCR+896(6),PO21=SCR+902(6),VA2=Repl__,DS2=FCTDLREP
         B     STAMPA
*** Only Remote
XREMOTE  MVC   SCR+176(16),=CL16'Remote dataset :'
         MVC   SCR+193(8),FCTDSRMN
         MVC   SCR+256(16),=CL16'Remote sysid   :'
         MVC   SCR+273(4),FCTDSSYS
         B     STAMPA
*** Only Vsam
XVSAM    TESTM DS=FCTDSKSD,TM=FCTKSDS,POS=SCR+104(8),VAR=(ksds)
         TESTM DS=FCTDSESD,TM=FCTESDS,POS=SCR+104(8),VAR=(esds)
         TESTM DS=FCTDSRRD,TM=FCTRRDS,POS=SCR+104(8),VAR=(rrds)
         TESTM DS=FCTDSBAS,TM=FCTBASE,POS=SCR+114(8),VAR=Aix.Base
         TESTM DS=FCTDSPAT,TM=FCTPATH,POS=SCR+114(8),VAR=Aix.Path
         MVC   SCR+176(8),=CL8'Enabled '
         TESTM DS=FCTDSDSA,TM=FCTDSABL,POS=SCR+176(8),VAR=Disabled
         TESTM DS=FCTDSRI,TM=FCTRDIM,POS=SCR+185(8),VAR=Read
         TESTM DS=FCTDSUPD,TM=FCTUPDIM,POS=SCR+194(8),VAR=Update
         TESTM DS=FCTDSADD,TM=FCTADDIM,POS=SCR+203(8),VAR=Add
         TESTM DS=FCTBRWSE,TM=FCTBRZIM,POS=SCR+212(8),VAR=Browse
         TESTM DS=FCTDSDI,TM=FCTDELIM,POS=SCR+221(8),VAR=Delete
         TESTM DS=FCTDSEXC,TM=FCTEXCIM,POS=SCR+230(8),VAR=Excl
         MVC   SCR+256(8),=CL8'Non LSR '
         TESTM DS=FCTDSSHR,TM=FCTSHRIM,POS=SCR+256(8),VAR=Shared
         TESTM DS=FCTDSLOG,TM=FCTLOG,POS=SCR+336(8),VAR=Syslog
         MVC   SCR+496(17),=CL17'Record Length   :'
         TESTM DS=FCTDSVLI,TM=FCTVRLIM,POS=SCR+514(8),VAR=Variable
         TESTM DS=FCTDSVLI,TM=FCTFIXIM,POS=SCR+514(8),VAR=Fixed
         MVC   SCR+576(24),=CL24'Record Blocking : Unblkd'
         TESTM DS=FCTDSNBK,TM=FCTBLKIM,POS=SCR+594(8),VAR=Blocked
*** Only for Vsam Opened Dataset
         TM    FCTDSOPN,FCTOPNIM
         BZ    STAMPA
         EDHLF BIN=FCTDSBLK,POSI=SCR+742(16)     * Block Size *
         EDHLF BIN=FCTDSREC,POSI=SCR+822(16)     * Record Length *
          EDHLF BIN=FCTDSRKP,POSI=SCR+902(16)     * Rel Key Pos *
         EDONE BI1=FCTDSKL,POS1=SCR+982(16)      * Key Length *
         MVC   SCR+736(17),=CL17'Block size      :'
         MVC   SCR+816(17),=CL17'Record length   :'
         MVC   SCR+896(17),=CL17'Relative Key Pos:'
         MVC   SCR+976(17),=CL17'Key Length      :'
         EDHLF BIN=FCTDSRD+2,POSI=SCR+1123(16)   * Read *
         EDHLF BIN=FCTDSWRA+2,POSI=SCR+1203(16)  * Add *
         EDHLF BIN=FCTDSWRU+2,POSI=SCR+1283(16)  * Update *
         EDHLF BIN=FCTDSGU+2,POSI=SCR+1363(16)   * Get for Update *
         EDHLF BIN=FCTDSBR+2,POSI=SCR+1443(16)   * Browse *
         MVC   SCR+1533(6),=XL6'402020202120'    * Delete *
         ED    SCR+1533(6),FCTDSDEL              *   "    *
         MVC   SCR+1120(13),=CL13'Read.........'
         MVC   SCR+1200(13),=CL13'Add..........'
         MVC   SCR+1280(13),=CL13'Update.......'
         MVC   SCR+1360(13),=CL13'Get.for.Upd..'
         MVC   SCR+1440(13),=CL13'Browse.......'
         MVC   SCR+1520(13),=CL13'Delete.......'
         MVC   SCR+1150(12),=CL12'STRING COUNT'  * STRING COUNT
         MVC   SCR+1246(4),=XL4'40202120'        * Active *
         ED    SCR+1246(4),FCTDSASC
         MVC   SCR+1235(12),=CL12'Active......'
         MVC   SCR+1326(4),=XL'40202120'         * Maximum *
         ED    SCR+1326(4),FCTDSMSC
         MVC   SCR+1315(12),=CL12'Maximum.....'
         MVC   SCR+1406(4),=XL4'40202120'        * Up/Add max *
         ED    SCR+1406(4),FCTDSPMS
         MVC   SCR+1395(12),=CL12'Up/Add.max..'
         MVC   SCR+1470(12),=CL12'STRING WAIT '  * STRING WAIT
         MVC   SCR+1566(4),=XL4'40202120'        * Wait tot *
         ED    SCR+1566(4),FCTDSTSW
         MVC   SCR+1555(12),=CL12'Total.......'
         MVC   SCR+1644(6),=XL6'402020202120'    * Wait high *
         ED    SCR+1644(6),FCTDSHSW
         MVC   SCR+1635(12),=CL12'Highest.....'
         MVC   SCR+1175(12),=CL12'BUFFER WAIT ' * BUFFER WAIT
         EDHLF BIN=FCTDSCBW,POSI=SCR+1259(16)   * Current *
         MVC   SCR+1260(12),=CL12'Current.....'
         EDHLF BIN=FCTDSTBW+2,POSI=SCR+1339(16) * Total *
         MVC   SCR+1340(12),=CL12'Total.......'
         EDHLF BIN=FCTDSHBW,POSI=SCR+1419(16)   * Highest *
         MVC   SCR+1420(12),=CL12'Highest.....'
STAMPA   MVC   SCR(8),FCTDSID
         MVC   LNG,=H'1800'
         EXEC  CICS SEND FROM(SCR) LENGTH(LNG) ERASE
ENDPRG   DS    0H
         EXEC  CICS RETURN
         END


Cozzani Mirco
Systems Programmer
Cassadi Risparmio della Spezia (Italy)  ) Xephon 1988












