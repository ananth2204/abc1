 Updating RDO external security flag in batch
INTRODUCTION
The enclosed sample programs were written in order to update the external
security indicator in the Resource Definition On-line (RDO) file when installing
an external security system.  The programs were written with EASYTRIEVE PLUS
Release 5.0.  I have used the programs with both 1.6.1 and 1.7.0 versions of
CICS.
The following program will print the group, transaction, and external security
flag for all transactions in all groups (1 indicates external security = NO and
2 indicates external security = YES).
THE PRINT PROGRAM
//STEP1    EXEC PGM=EZTPA00
//STEPLIB  DD   DSN=EZPPROD.LOAD.PDS,DISP=SHR
//SYSPRINT DD   SYSOUT=A,COPIES=1
//SYSOUT   DD   SYSOUT=*
//SYSSNAP  DD   SYSOUT=*
//FILEA    DD   DSN=ONLPROD.ONLINE.CSD,DISP=SHR
//SYSIN    DD   *
FILE FILEA VS
INKEY         01  22 A
INGROUP   INKEY     8   A
INRECIND  INKEY +12 2   B
INTRAN    INKEY +14 8   A
VAR-LEN       23  02 B
NO-VAR-FIELDS 45  01 B
NO-FIX-FIELDS 46  01 B
FIXED-FIELD 47  1 B INDEX FIX-SUB
FIX-COUNT W 5 P 0
W-SECIND  W 1 B
JOB INPUT FILEA
IF INRECIND NE 5000
   GO TO JOB
END-IF
FIX-SUB = 0
FIX-COUNT = 1
W-SECIND = 0
DO WHILE FIX-COUNT LS NO-FIX-FIELDS + 1
  IF FIXED-FIELD = X'14'
      FIX-SUB = FIX-SUB + 1
      W-SECIND = FIXED-FIELD
      FIX-SUB = FIX-SUB + 1
    ELSE
      FIX-SUB = FIX-SUB + 2
  END-IF
  FIX-COUNT = FIX-COUNT + 1
END-DO
IF W-SECIND NQ 1
    GO TO JOB
END-IF
PRINT REP1
REPORT REP1 NOADJUST NODATE NOPAGE TITLESKIP 0 NOHEADING
 LINE COL 01 INGROUP COL 10 INTRAN COL 20 W-SECIND
/*
The  output  will have no headings, and can be used directly as input to the
update program. The output  from  the  above job will look like the following :
CAFC     AFCB        1
CAFC     AFCC        1
CAFC     AFCM        1
CAFC     AFCS        1
CAFC     AFCT        1
CAFC     AFC1        1
CFC      AFC2        1
CPI      ALTP        2
CPI      CCFD        2
CPI      CCFU        2
CPI      CCTD        2
      .
    etc
     .
THE UPDATE PROGRAM
//STEP1    EXEC PGM=EZTPA00
//STEPLIB  DD   DSN=EZPPROD.LOAD.PDS,DISP=SHR
//SYSPRINT DD   SYSOUT=A,COPIES=1
//SYSOUT   DD   SYSOUT=*
//SYSSNAP  DD   SYSOUT=*
//FILEA    DD   DSN=ONLPROD.ONLINE.CSD,DISP=SHR
//FILEB    DD   *
CAFC     AFCB        1
CAFC     AFCC        1
CAFC     AFCM        1
CAFC     AFCS        1
CAFC     AFCT        1
CAFC     AFC1        1
CAFC     AFC2        1
CPI      ALTP        2
CPI      CCFD        2
CPI      CCFU        2
CPI      CCTD        2
     .
     etc
     .
/*
//SYSIN    DD   *
FILE FILEA VS(UPDATE)
INKEY         01  22 A
INGROUP   INKEY     8   A
INRECIND  INKEY +12 2   B
INTRAN    INKEY +14 8   A
NO-FIX-FIELDS 46  01 B
FIXED-FIELD 47  1 B INDEX FIX-SUB
FILE FILEB
GROUP     1   8  A
TRANS     10  8  A
SECFLAG   20  8  A
WORKKEY   W   22 A
WORKGRP   WORKKEY       8   A
WORKFILL1 WORKKEY  +8   4   B
WORKFILL2 WORKKEY  +12  2   B
WORKTRANS WORKKEY  +14  8   A
FIX-COUNT W 5 P 0
OLD-SECIND W 1 B
NEW-SECIND W 1 B
FOUND      W 1 A
JOB INPUT FILEB
WORKGRP = GROUP
WORKTRANS = TRANS
WORKFILL1 = 0
WORKFILL2 = 5000
READ FILEA KEY WORKKEY, STATUS
IF FILE-STATUS EQ 16
   DISPLAY WORKKEY 'NOT FOUND'
   GO TO JOB
END-IF
IF FILE-STATUS NOT ZERO
   DISPLAY '*** ERROR ***   FILE STATUS =' FILE-STATUS
   STOP
END-IF
FIX-SUB = 0
FIX-COUNT = 1
FOUND = 'N'
* STEP THROUGH LOOKING FOR EXTSEC INDICATOR X'14'
DO WHILE FIX-COUNT LS NO-FIX-FIELDS + 1
   IF FIXED-FIELD = X'14'
      FIX-SUB = FIX-SUB + 1
      IF FIXED-FIELD = 1
          OLD-SECIND = FIXED-FIELD
          FIXED-FIELD = 2
          NEW-SECIND = FIXED-FIELD
          FOUND = 'Y'
        ELSE
          DISPLAY WORKKEY '*** ERROR *** EXTERNAL SECURITY IND. NOT = 1'
          STOP
      END-IF
      FIX-SUB = FIX-SUB + 1
    ELSE
      FIX-SUB = FIX-SUB + 2
  END-IF
  FIX-COUNT = FIX-COUNT + 1
END-DO
IF FOUND = 'N'
   DISPLAY WORKKEY 'EXTERNAL SECURITY FIELD NOT FOUND'
   STOP
END-IF
WRITE FILEA UPDATE STATUS
IF FILE-STATUS NOT ZERO
   DISPLAY WORKKEY 'UPDATE FAILED'
   STOP
END-IF
PRINT REP1
GO TO JOB
REPORT REP1 NOADJUST
 LINE INGROUP INTRAN OLD-SECIND NEW-SECIND
/*
The above update program will:
1    Build the VSAM key from the input file.
2    Read the record.
3    Locate the External Security Flag.
4    Change the External Security Flag.
5    Write the record.
6    Print a detail line showing the change.
Figure 1 shows how output from the update program might appear.
Notes
1    Strip out the transactions you do not want changed from the output of the
first job and use it for input to the update job.
2    The IBM (DFH type groups) can also be updated.
3    If you do not have the VSAM update ability with EASYTRIEVE PLUS you may
want to try setting UPDTVS=YES in the EZPOPTNS macro or write the program in
another language.

David Fletchall
System Software Analyst
Leader Federal S & L (USA)    ) Xephon 1988
















