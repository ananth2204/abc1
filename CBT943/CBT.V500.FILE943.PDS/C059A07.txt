Encoding the Sign-on Table

Any programmer can browse the Sign-on Table (DFHSNT) in
CICS, and can view all of the passwords, including the system
programmer's.

For example, the easiest way is to load the table by:

      EXEC CICS LOAD PROGRAM('DFHSNT') HOLD

and then browse it with the help of CEDF or any other tool.

In order to handle this problem, one can write one's own Sign-
on program (DFHSNP) and attach it to the CSSN transaction-
id.  I have decided to take a different approach and encode the
Sign-on Table.

The idea is to make DFHSNT a resident program and, during
CICS start-up, invoke a program (ENCSNTI  in  this example),
which will go through the user entries and translate the
passwords using a translation table.

The next thing to do is to write a user-exit program for the
terminal control program (exit-id XZCIN) which is given
control when an input operation occurs at a terminal.

The user-exit program (ENCSNTX in this example) checks
whether it is the CSSN transaction, and, if so, scans the TIOA
for the password keyed, and translates it using the translation
table that was used during CICS start up.

In my PLTPI program I put the following entries:

      DFHPLT TYPE=ENTRY,PROGRAM=ENCSNTXE   (Enable ZCP Exit)
      DFHPLT TYPE=ENTRY,PROGRAM=ENCSNTI    (Encode Sign-On table)

Testing has been done under VSE/SP 2. 1 and CICS 1.6.


ENCSNTI SOURCE CODE

         DFHSNT TYPE=DSECT
ENCSNTI  CSECT
*   LOAD THE SIGN-ON TABLE AND MAKE IT RESIDENT (IF IT IS NOT)
         EXEC CICS LOAD PROGRAM('DFHSNT')                              X
               HOLD   SET(5)   ENTRY(4)
         USING DFHSNNT,4
LOOP     DS    0H
         CLC   0(4,4),=X'FFFFFFFF'      end of snt..??
         BE    LOOPE                    yes..end loop
         TR    SNNTPS(8),TABTRLT        encode password
LOOPN    DS    0H
         LA    4,SNNTELGH(,4)           next entry
         B     LOOP                     go loop
LOOPE    DS    0H
         EXEC  CICS RETURN
TABTRLT  DC    X'000102030405060708090A0B0C0D0E0F'
         DC    X'101112131415161718191A1B1C1D1E1F'
         DC    X'202122232425262728292A2B2C2D2E2F'
         DC    X'303132333435363738393A3B3C3D3E3F'
         DC    X'404142434445464748494A4B4C4D4E4F'
         DC    X'505152535455565758595A5B5C5D5E5F'
         DC    X'606162636465666768696A6B6C6D6E6F'
         DC    X'707172737475767778797A7B7C7D7E7F'
         DC    X'808182838485868788898A8B8C8D8E8F'
         DC    X'909192939495969798999A9B9C9D9E9F'
         DC    X'A0A1A2A3A4A5A6A7A8A9AAABACADAEAF'
         DC    X'B0B1B2B3B4B5B6B7B8B9BABBBCBDBEBF'
         DC    X'C0C1C2C3C4C5C6C7C8C9CACBCCCDCECF'
         DC    X'D0D1D2D3D4D5D6D7D8D9DADBDCDDDEDF'
         DC    X'E0E1E2E3E4E5E6E7E8E9EAEBECEDEEEF'
         DC    X'F0F1F2F3F4F5F6F7F8F9FAFBFCFDFEFF'
         ORG   TABTRLT+C'A'
         DC    X'20'
         ORG   TABTRLT+C'B'
         DC    X'15'
         ORG   TABTRLT+C'C'
         DC    X'F2'
         ORG   TABTRLT+C'D'
         DC    X'41'
         ORG   TABTRLT+C'E'
         DC    X'75'
         ORG   TABTRLT+C'F'
         DC    X'35'
         ORG   TABTRLT+C'G'
         DC    X'22'
         ORG   TABTRLT+C'H'
         DC    X'F7'
         ORG   TABTRLT+C'I'
         DC    X'50'
         ORG   TABTRLT+C'J'
         DC    X'43'
         ORG   TABTRLT+C'K'
         DC    X'52'
         ORG   TABTRLT+C'L'
         DC    X'61'
         ORG   TABTRLT+C'M'
         DC    X'72'
         ORG   TABTRLT+C'N'
         DC    X'81'
         ORG   TABTRLT+C'O'
         DC    X'90'
         ORG   TABTRLT+C'P'
         DC    X'9F'
         ORG   TABTRLT+C'Q'
         DC    X'A0'
         ORG   TABTRLT+C'R'
         DC    X'B0'
         ORG   TABTRLT+C'S'
         DC    X'C5'
         ORG   TABTRLT+C'T'
         DC    X'B7'
         ORG   TABTRLT+C'U'
         DC    X'38'
         ORG   TABTRLT+C'V'
         DC    X'66'
         ORG   TABTRLT+C'W'
         DC    X'78'
         ORG   TABTRLT+C'X'
         DC    X'87'
         ORG   TABTRLT+C'Y'
         DC    X'91'
         ORG   TABTRLT+C'Z'
         DC    X'07'
         ORG   TABTRLT+C'0'
         DC    X'10'
         ORG   TABTRLT+C'1'
         DC    X'19'
         ORG   TABTRLT+C'2'
         DC    X'7F'
         ORG   TABTRLT+C'3'
         DC    X'5F'
         ORG   TABTRLT+C'4'
         DC    X'47'
         ORG   TABTRLT+C'5'
         DC    X'35'
         ORG   TABTRLT+C'6'
         DC    X'9C'
         ORG   TABTRLT+C'7'
         DC    X'AA'
         ORG   TABTRLT+C'8'
         DC    X'BB'
         ORG   TABTRLT+C'9'
         DC    X'CC'
         ORG
         END


ENCSNTXE SOURCE CODE

ENCSNTXE CSECT
         EXEC CICS ENABLE PROGRAM('ENCSNTX')                           X
               EXITID('XZCIN') START
         EXEC CICS RETURN
         END


ENCSNTX SOURCE CODE

         COPY  DFHCSADS
         DFHTCA CICSYST=YES
         COPY  DFHPCTDS
         COPY  DFHPPTDS
         COPY  DFHTCTTE
TCTTEAR  EQU   10
         COPY  DFHTIOA
TIOABAR  EQU   8
         DFHUEXIT TYPE=EP
         USING DFHUEPAR,1
ENCSNTX  CSECT
         USING ENCSNTX,11
         STM   14,12,12(13)            store caller's regs
         LR    11,15                   code addressability
         L     2,UEPEXN                get exit number
         CLI   0(2),XZCIN              is it zcp input exit..??
         BNE   RETRN                   no..return
         L     12,TCTTECA              address tca of terminal
         LTR   12,12                   is there one..??
         BZ    RETRN
         L     6,TCASYAA               address system tca
         USING DFHTCADY,6
         L     7,TCATCPC               address pct entry
         USING DFHPCTDS,7              of task on terminal
         CLC   PCTTI,=CL4'CSSN'        is it sign_on transaction..??
         BNE   RETRN                   no..return
         L     7,TCAPCTA               address current ppt entry
         DROP  7
         USING DFHPPTDS,7
         CLC   PPTPI,=CL8'DFHSNP'      is it sign_on program..??
         BNE   RETRN                   no..return
         LA    4,TIOADBA               r4 addresses tioa data
         LH    5,TIOATDL               get tioa data length
         BCTR  5,0
         CLI   0(4),X'11'   first byte should be x'11'
         BNE   RETRN        no..don't know what to do
         LA    4,3(,4)      point to after sba etc
         LA    6,3
         SR    5,6          set length = length-3.
         LTR   5,5          more to go..??
         BL    RETRN        no..return
         LA    1,1(4,5)     r1 points to end of data
         EX    5,SRCHX11    scan for next x'11' (preceding pswd)
         LR    6,1
         SR    6,4          r6 is length of name
         LR    4,1          r4 to rest of tioa
         SR    5,6          r5 length of rest of tioa
         LTR   5,5          more to go..??
         BL    RETRN        no..return
         LA    4,3(,4)      point to after sba etc
         LA    6,3
         SR    5,6          set length = length-3.
         LTR   5,5          more to go..??
         BL    RETRN        no..return
         LA    1,1(4,5)     r1 points to end of data
         EX    5,SRCHX11    scan for next x'11' (after pswd)
         LR    5,1
         SR    5,4          r5 is length of password in tioa
         BCTR  5,0
         LTR   5,5          is there one..??
         BL    RETRN
         EX    5,TRNSLT     encode pswd in tioa by tabtrlt
RETRN    DS    0H
         LM    14,12,12(13)           restore caller registers
         BR    14                     return to caller
SRCHX11  TRT   0(0,4),TABX11
TABX11   DC    256AL1(0)
         ORG   TABX11+X'11'
         DC    X'11'
         ORG
TRNSLT   TR    0(0,4),TABTRLT
TABTRLT  DC    X'000102030405060708090A0B0C0D0E0F'
         DC    X'101112131415161718191A1B1C1D1E1F'
         DC    X'202122232425262728292A2B2C2D2E2F'
         DC    X'303132333435363738393A3B3C3D3E3F'
         DC    X'404142434445464748494A4B4C4D4E4F'
         DC    X'505152535455565758595A5B5C5D5E5F'
         DC    X'606162636465666768696A6B6C6D6E6F'
         DC    X'707172737475767778797A7B7C7D7E7F'
         DC    X'808182838485868788898A8B8C8D8E8F'
         DC    X'909192939495969798999A9B9C9D9E9F'
         DC    X'A0A1A2A3A4A5A6A7A8A9AAABACADAEAF'
         DC    X'B0B1B2B3B4B5B6B7B8B9BABBBCBDBEBF'
         DC    X'C0C1C2C3C4C5C6C7C8C9CACBCCCDCECF'
         DC    X'D0D1D2D3D4D5D6D7D8D9DADBDCDDDEDF'
         DC    X'E0E1E2E3E4E5E6E7E8E9EAEBECEDEEEF'
         DC    X'F0F1F2F3F4F5F6F7F8F9FAFBFCFDFEFF'
         ORG   TABTRLT+C'A'
         DC    X'20'
         ORG   TABTRLT+C'B'
         DC    X'15'
         ORG   TABTRLT+C'C'
         DC    X'F2'
         ORG   TABTRLT+C'D'
         DC    X'41'
         ORG   TABTRLT+C'E'
         DC    X'75'
         ORG   TABTRLT+C'F'
         DC    X'35'
         ORG   TABTRLT+C'G'
         DC    X'22'
         ORG   TABTRLT+C'H'
         DC    X'F7'
         ORG   TABTRLT+C'I'
         DC    X'50'
         ORG   TABTRLT+C'J'
         DC    X'43'
         ORG   TABTRLT+C'K'
         DC    X'52'
         ORG   TABTRLT+C'L'
         DC    X'61'
         ORG   TABTRLT+C'M'
         DC    X'72'
         ORG   TABTRLT+C'N'
         DC    X'81'
         ORG   TABTRLT+C'O'
         DC    X'90'
         ORG   TABTRLT+C'P'
         DC    X'9F'
         ORG   TABTRLT+C'Q'
         DC    X'A0'
         ORG   TABTRLT+C'R'
         DC    X'B0'
         ORG   TABTRLT+C'S'
         DC    X'C5'
         ORG   TABTRLT+C'T'
         DC    X'B7'
         ORG   TABTRLT+C'U'
         DC    X'38'
         ORG   TABTRLT+C'V'
         DC    X'66'
         ORG   TABTRLT+C'W'
         DC    X'78'
         ORG   TABTRLT+C'X'
         DC    X'87'
         ORG   TABTRLT+C'Y'
         DC    X'91'
         ORG   TABTRLT+C'Z'
         DC    X'07'
         ORG   TABTRLT+C'0'
         DC    X'10'
         ORG   TABTRLT+C'1'
         DC    X'19'
         ORG   TABTRLT+C'2'
         DC    X'7F'
         ORG   TABTRLT+C'3'
         DC    X'5F'
         ORG   TABTRLT+C'4'
         DC    X'47'
         ORG   TABTRLT+C'5'
         DC    X'35'
         ORG   TABTRLT+C'6'
         DC    X'9C'
         ORG   TABTRLT+C'7'
         DC    X'AA'
         ORG   TABTRLT+C'8'
         DC    X'BB'
         ORG   TABTRLT+C'9'
         DC    X'CC'
         ORG
         END


NOTES

The translation tables in program ENCSNTI and in program
ENCSNTX must  be the same.  The translation in the programs
is a simple one: you could make it more complicated if you
wished.  For example you could maintain three translation
tables, and, according to the first byte in the password plus the
number of bytes of the password, decide what translation table
to use.

Anyone who is signing on in the form:

      CSSN NAME=XXXXXX, PS=YYYYY

will get a sign-on error.  In that case you have to write a
program, which extracts the 'YYYYY', translates it, and links
to DFHSNP.  This program will be the initial program of the
CSSN transaction-id.

In order to terminate this feature, disable the ZCP exit
program, release the DFHSNT program, and do a CEMT
NEWCOPY for DFHSNT.


Asher Shoshan
Systems Programmer
Migdal Insurance Company (Israel)


