Temporary storage queues

In our installation most CICS programs write temporary storage
queues to main storage and do not use the AUX option in the
'EXEC CICS WRITEQ QUEUE...' command.  However, when
there is high task activity, all these queues in main storage can
contribute to a 'short-on-storage' situation in CICS.

We needed a tool that would direct WRITEQ MAIN requests to
auxiliary storage when a certain amount of main storage had been
used up by temporary storage queues or on a queue name basis.
We have solved this by customising the user exit for the
temporary storage program.

For each WRITEQ request, the exit checks whether it is to use
main storage, and if so it compares the current number of bytes
used by temporary storage queues in main storage (field
TSMSTA6A in the temporary storage common area) against some
limit (a constant in the user exit program).  If the limit has been
reached, 'MAIN' is replaced with 'AUX' (the TCATSTR byte in
the TCA is changed).

The limit constant is a percentage of the number of bytes in the
DSA, so the same phase can be used for more than one CICS.  It
is calculated and set once (see SETLIMIT section, below).

Note that the CICS temporary storage program ignores the
MAIN/AUX request when WRITEQ is not the first item of the
queue.  This means that all the subsequent items of a queue will
reside in the same destination (ie MAIN or AUX) as the first item.

The source code of the user exit follows, the limit constant is set
to 15% of the DSA bytes, and the queue, identified by the name
'KUKUKUKU', is always written to auxiliary temporary storage.

The exit is defined in the PPT by the name 'TSPEXIT', and the
following line is included in my PLTPI program:
    EXEC CICS ENABLE PROGRAM(TSPEXIT) EXIT(XTSREQ) START

This user exit has been tested under VSE/SP 2.1 and CICS 1.6.


SOURCE CODE

         COPY DFHCSADS
         DFHTCA CICSYST=YES
         COPY DFHTSMDS
TSMAPBAR EQU 11
         COPY DFHPAMDS
PAMBAR   EQU 10
         DFHUEXIT TYPE=EP
         USING DFHUEPAR,1
TSPEXIT  CSECT
         USING *,15                     CODE ADDRESSABILITY
         STM   14,12,12(13)             STORE CALLER'S REGS
         LR    14,13                    SAVE R13
         L     13,UEPCSA                ADDRESS CSA
         L     12,UEPTCA                ADDRESS TCA
         TM    TCATSTR,B'01000000'      IS IT WRITE REQ..??
         BNO   END                      NO... RETURN
SETLIMIT NOP   BEGIN                    1ST TIME DO THE FOLLOWING
         L     PAMBAR,CSAPAMA           ADDRESS PAM
         L     5,PAMPGNUM               R5=#DSA PAGES
         M     4,=F'15'
         D     4,=F'100'
         M     4,PAMPGSZE               R5=15%(#DSA PAGES) * PAGESIZE
         ST    5,STGLIMIT               STORE R5 IN STGLIMIT
         OI    SETLIMIT+1,X'F0'         SET ONE TIME SWITCH ON
BEGIN    CLC   TCATSDI(8),=CL8'KUKUKUKU'    IS IT KUKUKUKU..??
         BE    AUX                          YES..WRITE AUX
         TM    TCATSTR,B'00001000'      IS IT WRITEQ MAIN.(X'48' X'49')
         BNO   END                      NO... RETURN
         L     TSMAPBAR,CSATSATA        ADDRESS T.S. COMMON AREA
         CLC   TSMSTA6A,STGLIMIT        CRNT MAIN USED LT STGLIMIT
         BL    END                      YES.. RETURN
AUX      NI    TCATSTR,B'11110111'      PUT WRITE AUX.  (X'40' X'41')
END      LR    13,14                    RESTORE R13
         LM    14,12,12(13)             RESTORE CALLER REGISTERS
         BR    14                       RETURN TO CALLER
STGLIMIT DC    F'0'
         END


Asher Shoshan
Systems Programmer
Migdal Insurance Company (Israel) ) Xephon 1989

