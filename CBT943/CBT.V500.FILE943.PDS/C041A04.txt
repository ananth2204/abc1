Communicating after an AKCT time-out

As  the developers of a CICS transaction abend catching  product,
one  of the early problems we encountered was how to
communicate with a 3270 terminal after an AKCT (terminal read
time-out) abend.  One of the key requirements of CICS ABEND-
CATCHER was to  capture the screen contents of the terminal
where the abend occurred  for later  review.  The product also
sends a screen to  the  terminal operator, informing him or her of
the abend in better English than CICS normally does.

Through  research  and  trial  and error, we  were  able  to  put
together the following macro-level Assembler program which can
be linked  to  after  an abend has occurred in  a  transaction  with
either  an EXEC CICS LINK PROGRAM('TSYNC') END-EXEC  or  DFHPC
TYPE=LINK,PROGRAM=TSYNC,COND=YES.   Those  readers  who
have  attempted  to write their own abend-handling  routines
may appreciate  having this ability.  After linking to  the
program, you  may  then  proceed  just as you would  in  any
other  abend situation.

The  program  will  work on all  3270  type  terminals  including
LUTYPE2.  It has also been proven to work on LUTYPE1 devices.
If there is no terminal associated with the task, the program  will
simply  return  to  the  calling  program.   It  has  been   used
successfully  in CICS releases 1.6.0, 1.6.1 and 1.7 and has  been
used  in both VSE and MVS environments.  It should  be  noted
that the macros used in this programs may not have been
intended by  IBM for other than its own use.  This would mean
that  there are no guarantees that they will work in future CICS
releases.


         TITLE 'TSYNC - TERMINAL SYNCHRONIZATION'
BASEREG  EQU   2
         TITLE ' CSA DSECT'
         COPY  DFHCSADS
         TITLE ' TCA DSECT'
TCASBAR  EQU   11
         DFHTCA CICSYST=YES
         TITLE ' TCTTE DSECT'
TCTTEAR  EQU   5
         DFHTCTZE CICSYST=YES
         TITLE ' TSYNC CSECT'
TSYNC    CSECT
         BALR  BASEREG,0
         USING *,BASEREG
         TM    TCAFCI,TCAFCTRM          IS THIS TERMINAL TASK?
         BZ    RETRN                    NO, QUIT
         L     TCASBAR,TCASYAA          ACCESS SYSTEM AREA
         USING DFHTCADY,TCASBAR
         CLC   TCACRABC,=CL4'AKCT'      IS THIS READ TIMEOUT?
         BNE   RETRN                    NO, QUIT
         L     TCTTEAR,TCAFCAAA         GET TCTTE
         TM    TCTIVTAM,TCTEVTAM        IS ACCESS METHOD VTAM?
         BZ    RETRN                    NO, QUIT
         DFHTC CTYPE=RESETSR,CACS=CS    RESET IO REQUEST
         DFHTC TYPE=WAIT,COND=YES       WAIT FOR RESETSR
         TM    TCTEVTSF,TCTEVTSI        IS THIS VTAM 3270?
         BO    RETRN                    YES, QUIT  (NOT SNA)
         MVI   TCATPAPR,00              CLEAR REQUEST BYTE
         MVC   TCTESIDO,=X'00010000'    SET UP SIGNAL DATA
         DFHTC CTYPE=(COMMAND,WAIT),COMMAND=SIGNAL  SIGNAL LU
         DFHTC TYPE=(READ,WAIT)               READ FOR CHG DIRECT.
RETRN    EQU   *
         DFHPC TYPE=RETURN              RETURN TO CALLER
         LTORG
         END


Randy Benderman
Vice-President
Computer Application Services Inc (USA) ) La Bayne & Associates Inc 1989

