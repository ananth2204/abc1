Scanning the ICE without using the CSA

The stated IBM direction under CICS Version 3 includes
removing direct addressability to CICS internal control blocks,
including the CSA.  It is possible that future releases of CICS
could remove access to CSAICEBA, the base address of the
Interval Control Element (ICE) chain, which several of our
utility programs use to keep track of timed tasks or to
reschedule them.  Therefore we needed to find another
mechanism to obtain the ICE chain address.

The CICS 3.1 global user exit, XICEXP, gets control
whenever an ICE expires.  On entry, it receives the address of
the ICE just expired (UEPICE).  Since this entry was at the top
of the ICE chain, the next ICE pointer (ICECHNAD) points to
the new top of the ICE chain.  The XICEXP exit program can
save this address in the Global Work Area (GWA), from where
it can be obtained by a CICS EXTRACT EXIT command.

To ensure that the ICE address is as current as possible, the
EXTRACT program should first issue an EXEC CICS DELAY
for one second to force an ICE expiry (one second is the
minimum DELAY interval).  The program should also check
for the possibility that the ICE address is zero (ie there are no
entries on the ICE chain).

The following sample subroutine illustrates the use of the
EXTRACT EXIT command.  Incorporation of this subroutine
into our 'ICE chain' programs allowed us to convert them with
minimal coding changes (ie replacing all references to
CSAICEBA with a reference to ICECBAR).  These programs
have been tested under CICS 3.1.

********************************************************************
* EXTRACT GWA DATA FOR 'XICEXP' EXIT
* RETURNS ICECBAR (ADDR OF 1ST ELEMENT IN ICE CHAIN)
********************************************************************
EXTRACT  DS    0H
* FORCE EXPIRY OF AN ICE
         EXEC  CICS DELAY FOR SECONDS(1) REQID('EXPIRY') RESP(RESP)
* EXTRACT GWAICEBA
         USING GWADSECT,GWABASE
         EXEC  CICS EXTRACT EXIT PROGRAM('SXICEXP') GASET(GWABASE)     +
               GALENGTH(GALEN) RESP(RESP)
* CHECK FOR ERRORS
         CLC   RESP,DFHRESP(INVEXITREQ)   INVALID EXIT ?
         BE    INVEXIT                    YES, SAY EXIT NOT ACTIVE
         LTR   GWABASE,GWABASE            VALID GWA ?
         BZ    NOGWA                      NO, TELL
         OC    GALEN,GALEN                VALID GWA ?
         BZ    NOGWA                      NO, TELL
         L     ICECBAR,GWAICEBA           SET ICECBAR
         LTR   ICECBAR,ICECBAR            ANY ICE ENTRIES ?
         BZ    NOICE                      NO, TELL
         BR    LINK
         DROP  GWABASE

Program SXICEXP is an example of the required global user
exit program, enabled as follows:

    EXEC CICS ENABLE EXIT('XICEXP') PROGRAM('SXICEXP') GALENGTH(4) START
***********************************************************************
* SXICEXP - GLOBAL USER EXIT FOR CICS Version 3
*           GETS CONTROL AFTER AN ICE HAS EXPIRED
* INPUT PARM - UEPICE = ICE ENTRY FOR EXPIRED ELEMENT
* OUTPUT     - GWAICEBA (GWA SAVE AREA FOR 1ST ENTRY IN ICE CHAIN)
*              NOTE : IF THERE IS NOTHING ON THE ICE CHAIN, GWAICEBA
*              WILL BE SET TO 0.
*            - UERCNORM (NORMAL RETURN CODE)
***********************************************************************
UEPBASE  EQU   1                   DFHUEH PARM LIST PTR
GWABASE  EQU   6                   GWA PTR
ICECBAR  EQU   7                   ICE PTR
BASEREG  EQU   11                  PROGRAM BASE
         DFHAL TYPE=ICE            ICE DSECT
GWADSECT DSECT                     GLOBAL WORK AREA DSECT
GWAICEBA DS    F                   1ST ENTRY IN ICE CHAIN
GWAL     EQU   *-GWADSECT          LENGTH OF GWA
         DFHUEXIT TYPE=EP,ID=XICEXP     USER EXIT DSECT FOR XICEXP
***********************************************************************
SXICEXP  CSECT
         STM   R14,R12,12(R13)     SAVE ENTRY REGS
         BALR  BASEREG,0           BASE REGISTER
         USING *,BASEREG
         USING DFHUEPAR,UEPBASE    POINT TO DFHUEH PARM LIST
         L     GWABASE,UEPGAA      POINT TO GWA
         USING GWADSECT,GWABASE
* FIND NEXT ENTRY IN ICE CHAIN AND MOVE IT TO GWA
         L     ICECBAR,UEPICE      GET CURRENT ENTRY ADDR
         LTR   ICECBAR,ICECBAR     VALID ?
         BZ    NOICE               NO, FLAG IT
         MVC   GWAICEBA,ICECHNAD   SAVE 1ST ENTRY ADDR IN GWA
         B     EXIT
NOICE    DS    0H
         ST    ICECBAR,GWAICEBA    ICECBAR = 0
EXIT     DS    0H
         L     R14,12(R13)         RELOAD ALL REGS EXCEPT R15
         LM    R0,R12,20(R13)      WHICH IS 'RETURN NORMAL'
         LA    R15,UERCNORM
         BR    R14                 RETURN TO CICS
         LTORG  ,
         DFHEND  SXICEXP


David Roth
CICS Systems Programmer (Australia)

