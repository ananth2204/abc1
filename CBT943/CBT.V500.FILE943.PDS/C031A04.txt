 CICS SVC Screening
DESCRIPTION
The CICS SVC Screening (CSS) system allows datasets to be allocated to CICS only
when opened.  The technique used is a standard MVS facility known as SVC
Screening, which is an authorised way of intercepting supervisor calls.
Using CSS gives you several benefits:
%    You can start a CICS system, even if you are missing one or more datasets,
as long as these are not vital to your system.  When the datasets become
available, they can be immediately opened in CICS.
%    During normal CICS operation, you may close files, thereby making the
associated datasets available for updating by batch jobs.  When updated, the
files can again be opened in CICS.
%    You can replace one dataset with another in-flight by changing the name,
closing the file, and opening it again.
%    The system supports all types of datasets, even DL/I files at the DDNAME
level.
%    The CSS system produces a log dataset (normally a print file) showing all
open and close activity in the CICS system.  This is nice to have when
investigating CICS problems.
The CSS system makes no hooks whatsoever in the MVS or CICS system, but acts as
a filter between the CICS system and MVS, causing three supervisor calls (SVCs
19, 20, and 64) to be intercepted, allowing code to be added before and after
the supervisor call is handed over to MVS.
When CICS issues a request for open (SVC 19), CSS will check whether the file is
allocated.  If not, the CSSIN dataset is scanned for the file name and, if
found, the dataset is allocated with DISP=SHR and FREE=CLOSE, causing the
dataset to be freed again when later closed.
The log dataset CSSOUT contains one entry for each request for OPEN or CLOSE.
The entry contains the time of day, the DDNAME, the allocated dataset name (if
allocated), and the ACB or DCB address.  Even VTAM OPEN/CLOSE is recorded in
this dataset.
If VSAM Shared Resources are being used, CICS needs to know the Control Interval
size for VSAM datasets.  CICS gets this information by first issuing a RDJFCB
request to get the name of the dataset, followed by a SHOWCAT request to obtain
the Control Interval size.  This is the reason for screening RDJFCB (SVC 64).
If the DDNAME involved in a RDJFCB request is not allocated, and if it is found
in the CSSIN dataset, the dataset is not allocated.  Instead, the dataset name
is inserted directly into the JFCB area and control is given back to CICS with
return code zero, indicating successful completion.
Using the CSS system introduces quite a lot of overhead during OPEN processing,
since opening unallocated files causes the CSSIN dataset to be opened, scanned,
and closed again.  This overhead could be reduced significantly by reading the
information into storage once but we have chosen the first technique in order to
change the content of the CSSIN dataset dynamically.  We have never experienced
problems doing so, because OPEN/CLOSE activity is normally low during prime
shift.  For datasets allocated via DD cards the overhead is very low.
You should also notice that CSS only works with datasets allocated with DISP=SHR
and that concatenated datasets cannot be handled.  If needed, it should be
fairly easy to modify the program to handle these situations, but we have never
had the need.
INSTALLATION
The system consists of two programs - CSSINIT and CSSMAIN.  When the source code
has been entered, the two load modules can be created using the following sample
JCL:
//CSSGEN   JOB  ...
//CSSGEN   PROC SOURCE='sourcelib',TARGET='loadlib'
//ASSEMBLE EXEC PGM=IEV90,PARM='DECK,NOLOAD,NOXREF'
//SYSLIB   DD   DSN=SYS1.MACLIB,DISP=SHR
//         DD   DSN=SYS1.AMODGEN,DISP=SHR
//SYSUT1   DD   DSN=&&SYSUT1,UNIT=VIO,SPACE=(CYL,(10,10))
//SYSPRINT DD   SYSOUT=*
//SYSIN    DD   DSN=&SOURCE(&MEMBER),DISP=SHR
//SYSPUNCH DD   DSN=&&OBJ,UNIT=VIO,SPACE=(400,(400,200,20)),
//     DISP=(MOD,PASS),DCB=(LRECL=80,BLKSIZE=400,RECFM=FB)
//LINK     EXEC PGM=IEWL,PARM='XREF,LET,LIST,AC=1'
//SYSLIN   DD   DSN=&&OBJ,DISP=(OLD,DELETE)
 //SYSLMOD  DD   DSN=&TARGET(&MEMBER),DISP=SHR
//SYSUT1   DD   DSN=&&SYSUT1,UNIT=VIO,SPACE=(CYL,(10,10))
//SYSPRINT DD   SYSOUT=*
//         PEND
//CSSINIT  EXEC CSSGEN,MEMBER=CSSINIT
//CSSMAIN  EXEC CSSGEN,MEMBER=CSSMAIN
/*
The load modules must be placed in an APF-authorised library, accessible by the
CICS start-up procedure.
To activate CSS, change the program name in the EXEC card from DFHSIP to
CSSINIT, add DD cards for the CSSIN and CSSOUT files, remove DD cards for the
datasets you want to allocate dynamically and add these to the CSSIN dataset
instead.
The CSSIN dataset is a sequential dataset or a member of a PDS.  It contains
fixed length 80-byte records containing DDNAMEs and associated dataset names.
The DDNAME must begin in column 1 and the dataset name in column 10.
The CSSOUT dataset is a sequential dataset with RECFM=FBA and LRECL=81.  This is
normally allocated as a SYSOUT dataset.
The following illustrates the changes needed to the CICS start-up procedure:
//CICS    EXEC PGM=DFHSIP,PARM=....
          .
          .
//FILE1   DD   DSN=USER.FILE1,DISP=SHR
//FILE2   DD   DSN=USER.FILE2,DISP=SHR
          .
          .
is changed to:
//CICS    EXEC PGM=CSSINIT,PARM=....
          .
          .
//CSSIN   DD   DSN=SYS2.PARMLIB(CSSDSN),DISP=SHR
//CSSOUT  DD   SYSOUT=A
where the CSSDSN member contains:
1...5...10...15...20...25...30...35...40...45
FILE1    USER.FILE1
FILE2    USER.FILE2
MESSAGES
%    CSS001E - CONTROL DATASET TCSSINU MISSING.  There is no CSSIN DD card in
the start-up procedure.  This message is followed by CSS003E.
%    CSS002E - TRACE DATASET TCSSOUTU MISSING.  There is no CSSOUT DD card in
the start-up procedure.  This message is followed by CSS003E.
%    CSS003E - CICS SVC SCREENING NOT ACTIVATED.  The start-up procedure will
continue but without SVC screening.  This will probably cause errors later on
during file open.
%    CSS004I - CICS SVC SCREENING HAS BEEN ACTIVATED.  The pre-requisites for
SVC screening have been met, the start-up procedure continues.
%    CSS005W - ESTAE ROUTINE HAS BEEN ACTIVATED.  An error occurred during SVC
processing.  The CSS recovery procedure re-establishes the screening environment
and returns to CICS.
%    CSS006E - ALLOCATION ERROR - DDNAME=xxxxxxxx - ERROR CODE=xxxx - INFO
CODE=xxxx.  An error occurred when CSS attempted to dynamically allocate a
dataset.  The error code and information code from the S99RB control block are
displayed in hexademical format.
%    CSS007E - OPEN ERROR ON CONTROL DATASET TCSSINU.  Investigate SYSLOG for
MVS messages that describe the reason for the open error.  This might lead to an
OPEN error in the CICS system, because of DD STATEMENT MISSING.
EXPERIENCES
We have been using this technique for the dynamic allocation of datasets in a
CICS environment for quite a few years, during different MVS, CICS, and DL/I
releases.  When CICS Release 1.7 was introduced, we ran into problems because
screening was only activated for one task (the DFHSIP task) and in 1.7 the OPEN
processing for ordinary files was moved to another task.
Even though CICS 1.7 introduces automatic dynamic allocation at OPEN time, this
does not fit our needs, so we decided to re-work the CSS system, correcting some
minor cosmetic points as well.  The result has been tested under MVS 2.1.7 with
CICS 1.6.1 and 1.7, and with DL/I Release 1.3, although it should work with any
 CICS and DL/I release under any MVS release, as long as the Tpropagate SVC
screeningU bit in the TCB control block is supported.
Although no modifications are made to CICS or MVS system components, you should
be aware that the TCB structure is changed, since CSSINIT runs as a separate
task.  This is known to have caused problems using the OLIVER product, but a fix
to OLIVER should be available now.
The system has been tested only with CICS running in protect key 8, but it is
designed to work with CICS running in any protect key.
CSSINIT SOURCE LISTING
*** This is the initialization part of the CICS SVC screening. The  ***
*   program first checks if the necessary ddnames CSSIN and CSSOUT    *
*   are allocated. If these two datasets are present, an SVC screen-  *
*   ing environment is established by loading the screening module    *
*   CSSMAIN, building the necessary control blocks in LSQA and con-   *
*   necting these to the current TCB. Next, the TCB screening bits    *
*   are turned on, causing the current task and all subsequent atta-  *
*   ched tasks to be screened. Whether screening is activated or not, *
*   the program starts CICS by attaching the DFHSIP program.  DFHSIP  *
*   will run as a separate subtask, while CSSINIT waits for DFHSIP to *
*   terminate. The DCB for the trace dataset CSSOUT is placed in this *
*   module, and the address of the DCB is placed in a fullword in the *
*   LSQA, immediately following the 256 bytes screening table, where  *
*   it can be picked up by CSSMAIN.  The CSSOUT dataset is opened by  *
*** CSSINIT as part of the initialization process.                  ***
CSSINIT  CSECT ,                   PROGRAM START.
CSSINIT  AMODE 24                  ADDRESSING MODE.
CSSINIT  RMODE 24                  RESIDENCY MODE.
         SPLEVEL SET=2             MVS/XA MACRO EXPANSIONS.
         USING *,R10               DECLARE BASE.
         SAVE  (14,12)             SAVE REGISTERS.
         LR    R10,R15             LOAD BASE REGISTER.
         LA    R2,REGSAVE          POINT AT NEW SAVE AREA.
         ST    R2,8(R13)           CHAIN SAVE
         ST    R13,4(R2)           AREAS TOGETHER.
         LR    R13,R2              SHIFT SAVE AREAS.
         LR    R3,R1               SAVE PARAMETER LIST POINTER.
**********         Check prereq's for svc screening.         **********
         LA    R2,=CL8'CSSIN'      CHECK ALLOCATION
         DEVTYPE (R2),DEVCHAR      FOR DDNAME=CCSIN.
         LTR   R15,R15             CSSIN ALLOCATED ?
         BZ    CSSINOK             - YES, CONTINUE...
         WTO   'CSS001E - CONTROL DATASET ''CSSIN'' MISSING'
         B     NOSCREEN            CONTINUE WITHOUT SCREENING...
CSSINOK  EQU   *                   CSSIN ALLOCATION OK.
         LA    R2,=CL8'CSSOUT'     CHECK ALLOCATION
         DEVTYPE (R2),DEVCHAR      FOR DDNAME=CSSOUT.
         LTR   R15,R15             CSSOUT ALLOCATED ?
         BZ    CSSOUTOK            - YES, CONTINUE...
         WTO   'CSS002E - TRACE DATASET ''CSSOUT'' MISSING'
         B     NOSCREEN            CONTINUE WITHOUT SCREENING...
CSSOUTOK EQU   *                   CSSOUT ALLOCATION OK.
**********        Initiate svc screening environment.        **********
         LOAD  EP=CSSMAIN          LOAD SVC SCREENING ROUTINE.
         ST    R0,SVCENT           STORE ADDRESS IN SVC TABLE ENTRY.
         USING PSA,0               ESTABLISH PSA ADDRESSABILITY.
         L     R4,PSATOLD          LOAD POINTER TO CURRENT TCB.
         USING TCB,R4              ESTABLISH TCB ADDRESSABILITY.
         GETMAIN R,LV=268,SP=254   OBTAIN STORAGE IN LSQA.
         LR    R5,R1               SAVE THE ADDRESS.
         MODESET KEY=ZERO          GET INTO KEY ZERO.
         MVC   0(8,R5),SVCENT      MOVE SVC ENTRY TO LSQA.
         MVC   8(256,R5),SVCTAB    MOVE SCREENING TABLE TO LSQA.
         ST    R5,TCBSVCA2         SAVE LSQA ADDRESS IN CURRENT TCB.
         MVC   264(4,R5),=A(CSSOUT) SAVE ADDRESS OF CSSOUT DCB.
         OI    TCBFLGS7,TCBSVCS+TCBSVCSP ACTIVATE SCREENING.
         MODESET KEY=NZERO         GET BACK IN USER KEY.
          OPEN  (CSSOUT,(OUTPUT))   OPEN CSSOUT DCB.
**********     Write result on console, and start CICS.      **********
         WTO   'CSS004I - CICS SVC SCREENING HAS BEEN ACTIVATED'
         B     STARTUP             INITIATE CICS STARTUP...
NOSCREEN EQU   *                   NO SVC SCREENING.
         WTO   'CSS003E - CICS SVC SCREENING NOT ACTIVATED'
STARTUP  EQU   *                   INITIATE CICS STARTUP.
         LR    R1,R3               RELOAD PARAMETER LIST POINTER.
         ATTACH EP=DFHSIP,ECB=WAITECB CICS STARTUP...
         ST    R1,TCBAD            SAVE TCB ADDRESS FOR DFHSIP.
**********    Cleanup and exit after CICS has terminated.    **********
         WAIT  ECB=WAITECB         WAIT FOR CICS TO COMPLETE.
         DETACH TCBAD              SUBTASK CLEANUP.
         L     R13,4(R13)          SHIFT SAVE AREAS.
         RETURN (14,12),RC=0       TERMINATE EXECUTION...
**********                  Storage areas.                   **********
REGSAVE  DS    18F                 REGISTER SAVE AREA.
SVCENT   DC    A(0),X'C0',X'000000' SVC ENTRY.
SVCTAB   DC    X'80808080808080808080808080808080' SVC 0-15.
         DC    X'80808080808080808080808080808080' SVC 16-31.
         DC    X'80808080808080808080808080808080' SVC 32-47.
         DC    X'80808080808080808080808080808080' SVC 48-63.
         DC    X'00808080808080808080808080808080' SVC 64-79.
         DC    X'80808080808080808080808080808080' SVC 80-95.
         DC    X'80808080808080808080808080808080' SVC 96-111.
         DC    X'80808080808080808080808080808080' SVC 112-127.
         DC    X'80808080808080808080808080808080' SVC 128-143.
         DC    X'80808080808080808080808080808080' SVC 144-159.
         DC    X'80808080808080808080808080808080' SVC 160-175.
         DC    X'80808080808080808080808080808080' SVC 176-191.
         DC    X'80808080808080808080808080808080' SVC 192-207.
         DC    X'80808080808080808080808080808080' SVC 208-223.
         DC    X'80808080808080808080808080808080' SVC 224-239.
         DC    X'80808080808080808080808080808080' SVC 240-255.
TCBAD    DS    F                   TCB ADDRESS FOR DFHSIP SUBTASK.
WAITECB  DC    F'0'                ECB FOR DFHSIP SUBTASK.
DEVCHAR  DS    2F                  USED BY DEVTYPE MACRO.
CSSOUT   DCB   DDNAME=CSSOUT,DSORG=PS,RECFM=FBA,LRECL=81,MACRF=PM
**********            Dummy sections and equates.            **********
         IHAPSA ,                  PSA DSECT.
         IKJTCB ,                  TCB DSECT.
         END   CSSINIT             END OF SOURCE.
The Assembler source code for CSSMAIN will appear in next month's issue of CICS
Update.

Claus Jensen
Systems Programmer
Kreditforeningen Danmark (Denmark) ) Kreditforeningen Danmark 1988


















