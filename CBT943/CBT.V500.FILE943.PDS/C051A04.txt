Another look at avoiding duplicate sign-ons

We run VSE under VM and our users dial to VSE and select
one of several CICSs.  With 300 user-ids defined to CICS and
128 terminal lines defined to VTAM and CICS, we could not
allow the same user to sign on several times to CICS.
Avoiding duplicate sign-ons (CICS Update, July 1987), by
Ralph Johnson, was just the security program we needed.

When we migrated to CICS 1.7, we also implemented the auto-
install feature, hoping to reclaim some space by not having a
TCT; but our security program stopped detecting duplicate
sign-ons.  It was expecting a TCT with all the TCTTEs in a
contiguous area and this is not how auto-installed TCTTEs are
managed.  Our program found an END-OF-TCE the first time
and quit.  The program assumed that the operator-id was not
duplicated.

Our solution was to use the CICS INQUIRE TERMINAL
command to search the TCTTEs, letting CICS find the NEXT
entries, and Harold Dunlap's program, Quick logoff from CICS
(CICS Update, December 1986), to sign-off the offending user
and return him to the VTAM selection menu.  Of course, you
could use EXEC CICS RETURN with a transaction-id of
CSSF, for those not needing to return to VTAM.

The following program will work with either a TCT, auto-
installed terminals, or both.

PCT ENTRY

         DFHPCT TYPE=ENTRY,TRANSID=CSSN,PROGRAM=SYS100

PPT ENTRIES

         DFHPPT TYPE=ENTRY,PROGRAM=SYS100,PGMLANG=COBOL
         DFHPPT TYPE=ENTRY,PROGRAM=SYSM01,PGMLANG=ASSEMBLER

PROGRAM SOURCE CODE

Compile SYS100 with the LANGLVL(1) option.

 IDENTIFICATION DIVISION.
 PROGRAM-ID.    SYS100.
* THIS PROGRAM LINKS TO DFHSNP AND SIGNS A USER ON.  THEN IT
* RETRIEVES THE OPERATOR-ID FROM THE TCTTE AND ALL THE TCTTES ARE
* SCANNED TO SEE IF THE USER-ID IS SIGNED ON TO ANY OTHER TERMINAL
* IF IT IS, SYSM01 IS SENT TO THE TERMINAL AND THE TERMINAL IS
* SIGNED OFF THE CICS SYSTEM.
 ENVIRONMENT DIVISION.
 DATA DIVISION.
 WORKING-STORAGE SECTION.
 01  BEGIN-WS.
     03  FILLER            PIC X(20) VALUE '* WORKING STORAGE *'.
     03  OP-ID             PIC X(3).
     03  TERM-ID           PIC X(4).
     03  CURRENT-OPID      PIC X(3).
     03  END-TCT-TABLE     PIC  X    VALUE 'N'.
         88  END-OF-TCT              VALUE 'Y'.
 01 MAP01 COPY SYSM01.
 PROCEDURE DIVISION.
 0000-MAIN-LINE SECTION.
 0000-ALLOW-SIGN-ON.
     EXEC CICS HANDLE CONDITION ERROR(2100-CSSF-LOGOFF) END-EXEC.
     EXEC CICS HANDLE AID ANYKEY(2100-CSSF-LOGOFF)
                          CLEAR (2200-CLEAR-KEY) END-EXEC.
     EXEC CICS LINK PROGRAM('DFHSNP') END-EXEC.
     EXEC CICS ASSIGN OPID(CURRENT-OPID) END-EXEC.
*  FIND 1ST TERMINAL IN TCT.
     EXEC CICS INQUIRE TERMINAL START END-EXEC.
     PERFORM 1000-TCT-LOOP UNTIL END-OF-TCT.
     EXEC CICS INQUIRE TERMINAL END END-EXEC.
     EXEC CICS RETURN END-EXEC.
     GOBACK.
 1000-TCT-LOOP SECTION.
 1000-LOOK-AT-ALL-TERMINALS.
     EXEC CICS INQUIRE TERMINAL(TERM-ID) OPERID(OP-ID) NEXT
          END-EXEC.
     IF EIBRESP = 83
         MOVE 'Y' TO END-TCT-TABLE
     ELSE,
         PERFORM 2000-CHECK-TERM.
 1000-TCT-LOOP-XIT.     EXIT.
*  SEE IF SIGNED ON TWICE
 2000-CHECK-TERM SECTION.
 2000-CHECK-TERMINAL.
     IF TERM-ID NOT = EIBTRMID
         IF OP-ID = CURRENT-OPID
             EXEC CICS INQUIRE TERMINAL END END-EXEC
             MOVE CURRENT-OPID TO MAPIDO
             MOVE TERM-ID TO MAPTERMO
             EXEC CICS SEND MAP('SYSM01')
                  FROM(MAP01) ERASE END-EXEC
             EXEC CICS RECEIVE MAP('SYSM01')
                  INTO(MAP01) END-EXEC
             GO TO 2100-CSSF-LOGOFF
         ELSE,
             GO TO 2000-CHECK-TERM-XIT
     ELSE,
         GO TO 2000-CHECK-TERM-XIT.
 2100-CSSF-LOGOFF.
* PROGRAM 'GOODBYE' CICS UPDATE ISSUE 13, DEC '86, PAGE 20.
     EXEC CICS XCTL PROGRAM('GOODBYE') END-EXEC.
 2200-CLEAR-KEY.
     EXEC CICS RETURN TRANSID('CSSF') END-EXEC.
 2000-CHECK-TERM-XIT.     EXIT.

THE MAP

SYSM01   DFHMSD TYPE=&SYSPARM,                                         *
               CTRL=(FREEKB,FRSET),                                    *
               DATA=FIELD,                                             *
               LANG=COBOL,                                             *
               MODE=INOUT,                                             *
               TIOAPFX=YES
*           M A I N   M A P
MAP01    DFHMDI SIZE=(24,80)
MAPTRAN  DFHMDF POS=(1,1),LENGTH=11,ATTRB=(DRK,PROT,FSET),             *
               INITIAL='CSSF LOGOFF'
         DFHMDF POS=(2,3),LENGTH=6,ATTRB=(PROT,NORM),INITIAL='SYSM01'
         DFHMDF POS=(3,28),LENGTH=29,ATTRB=(PROT,NORM),                *
               INITIAL='EASTERN WASHINGTON UNIVERSITY'
         DFHMDF POS=(4,28),LENGTH=29,ATTRB=(PROT,NORM),                *
               INITIAL='------- ---------- ----------'
         DFHMDF POS=(8,29),LENGTH=27,ATTRB=(UNPROT,BRT),               *
               INITIAL='S E C U R I T Y   E R R O R'
MAPID    DFHMDF POS=(12,23),LENGTH=3,ATTRB=(PROT,BRT),INITIAL='XXX'
         DFHMDF POS=(12,27),LENGTH=30,ATTRB=(PROT,NORM),               *
               INITIAL='IS ALREADY SIGNED ON TERMINAL:'
MAPTERM  DFHMDF POS=(12,58),LENGTH=4,ATTRB=(PROT,BRT),INITIAL='XXXX'
         DFHMDF POS=(19,6),LENGTH=72,ATTRB=(PROT,NORM),                *
               INITIAL='IF SOMEONE IS USING YOUR SIGN-ON OR YOUR TERMIN*
               AL HAS BEEN DISCONNECTED,'
         DFHMDF POS=(20,12),LENGTH=62,ATTRB=(PROT,NORM),               *
               INITIAL='COPY DOWN THE ABOVE INFORMATION AND CALL INFORM*
               ATION  SERVICES.'
         DFHMDF POS=(23,23),LENGTH=39,ATTRB=(BRT,PROT),                *
               INITIAL='HIT ENTER TO RETURN TO VTAM MENU SCREEN'
         DFHMSD TYPE=FINAL
         END


A James Logon
Systems Programmer
Eastern Washington University (USA) ) Xephon 1990


