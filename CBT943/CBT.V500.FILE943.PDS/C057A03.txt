Issuing CEMT commands from VS/COBOL II

The CICS/MVS Customization  Guide (SC33-0507) outlines a
method of interfacing a user-written Assembler application
program to DFHEMTA, the CEMT command processor.  The
result is, in essence, a programmed operator capable of issuing
selected CEMT commands to CICS without the necessity of an
actual terminal.  The program itself analyses the results of each
call and determines a course of action.  An example entails
using this facility to  manipulate groups of VSAM files or DL/I
databases with a single command via a user-written transaction.

The CICS/MVS Customization Guide provides excellent detail
for programming an Assembler linkage to DFHEMTA.  We
felt, however, that we could exploit DFHEMTA's potential
more fully by combining its power with the expanded,
structured coding capabilities of VS/COBOL II.   Selecting
VS/COBOL II as the language also permitted maintenance of
any such application by programmers unfamiliar with
Assembler language.

This simple VS/COBOL II program illustrates a technique for
passing CEMT commands to DFHEMTA.  The program
merely directs DFHEMTA to open a file whose name is passed
from within.  This shell could easily be expanded into more
complex operations with a little creative thinking.  The above
mentioned document provides a complete explanation of each
parameter used in the LINK to DFHEMTA.

The program was developed in an MVS/XA 2.2 environment
with CICS 2.1.1 and VS/COBOL II Release 3.0.

 IDENTIFICATION DIVISION.
 PROGRAM-ID.  COB2CEMT.
 ENVIRONMENT DIVISION.
 CONFIGURATION SECTION.
 DATA DIVISION.
 WORKING-STORAGE SECTION.
 01  WS-CEMT-FCT-OPE.
     05  FILLER                         PIC X(9)  VALUE 'CEMT SET '.
     05  FILLER                         PIC XXX   VALUE 'FIL'.
     05  FILLER                         PIC X     VALUE '('.
     05  WS-FILE-ID                     PIC X(8)  VALUE SPACES.
     05  FILLER                         PIC X     VALUE SPACE.
     05  FILLER                         PIC X(6)  VALUE ') OPE '.
 01  WS-MESAGES.
     05  WS-TRANS-ERROR                 PIC X(30)
            VALUE 'TRANSLATOR ERROR HAS OCCURRED'.
     05  WS-EXEC-ERROR                  PIC X(30)
            VALUE 'EXECUTION ERROR HAS OCCURRED'.
     05  WS-FINISH-OK                   PIC X(30)
            VALUE 'COMMAND COMPLETED OK'.
 01  WS-POINTER-FIELDS.
     05  WPF-CMD-ADDR                   USAGE IS POINTER.
     05  WPF-CMD-LENGTH-ADDR            USAGE IS POINTER.
     05  WPF-TERM-ADDR                  USAGE IS POINTER.
     05  WPF-EMTA-OUTPUT-ADDR           USAGE IS POINTER.
     05  WPF-OUT-MAX-ADDR               USAGE IS POINTER.
 LINKAGE SECTION.
* DFHEMTA COMMUNICATION AREAS.
 01  EMTA-PARM-AREA.
     05  EMTA-CMD                       PIC X(1022).
     05  EMTA-CMD-LENGTH                PIC S9(4)   COMP.
     05  EMTA-TERM-FLAG                 PIC X.
     05  EMTA-OUT-MAX                   PIC S9(4)   COMP.
     05  EMTA-OUTPUT.
         10  EMTA-TRANS-OUTPUT.
             15 EMOT-OUT-LENGTH         PIC S9(4)   COMP.
             15 EMOT-NUMBER-LINES       PIC S9(4)   COMP.
             15 EMOT-RETURN-CODE        PIC S9(4)   COMP.
         10  EMTA-EXEC-OUTPUT.
             15 EMOE-OUT-LENGTH         PIC S9(4)   COMP.
             15 EMOE-NUMBER-LINES       PIC S9(4)   COMP.
             15 EMOE-RETURN-CODE        PIC S9(4)   COMP.
             15 EMOE-MESSAGE            PIC X(400).
 PROCEDURE DIVISION.
* ACQUIRE STORAGE TO BUILD DFHEMTA PARAMETER LIST
* AND IO AREA.
     EXEC CICS GETMAIN
          SET(ADDRESS OF EMTA-PARM-AREA)
          LENGTH(LENGTH OF EMTA-PARM-AREA)
          END-EXEC.
* ESTABLISH ADDRESSABILITY TO PARAMETERS FOR DFHEMTA.
     SET WPF-CMD-ADDR TO ADDRESS OF EMTA-CMD.
     SET WPF-CMD-LENGTH-ADDR TO ADDRESS OF EMTA-CMD-LENGTH.
     SET WPF-TERM-ADDR TO ADDRESS OF EMTA-TERM-FLAG.
     SET WPF-EMTA-OUTPUT-ADDR TO ADDRESS OF EMTA-OUTPUT.
     SET WPF-OUT-MAX-ADDR TO ADDRESS OF EMTA-OUT-MAX.
* BUILD CEMT COMMAND IN SOURCE FORM.  INDICATE LENGTH OF
* COMMAND AND REQUIREMENT FOR IMMEDIATE RETURN TO THE
* PROGRAM.  INDICATE MAXIMUM OUTPUT TO BE RETURNED.
     MOVE 'MYFILE ' TO WS-FILE-ID.
     MOVE WS-CEMT-FCT-OPE TO EMTA-CMD.
     MOVE LENGTH OF WS-CEMT-FCT-OPE TO EMTA-CMD-LENGTH.
     MOVE LOW-VALUES TO EMTA-TERM-FLAG.
     MOVE LENGTH OF EMTA-OUTPUT TO EMTA-OUT-MAX.
     EXEC CICS LINK
          PROGRAM('DFHEMTA')
          COMMAREA(WS-POINTER-FIELDS)
          END-EXEC.
* SEND MESSAGE IF COMMAND DID NOT TRANSLATE WITH RC=0.
     IF EMOT-RETURN-CODE NOT = ZEROES
        EXEC CICS SEND TEXT FROM(WS-TRANS-ERROR)
            LENGTH(LENGTH OF WS-TRANS-ERROR) ALARM FREEKB
            ERASE END-EXEC
        GO TO RETURN-TO-CICS.
* SEND MESSAGE IF COMMAND DID NOT EXECUTE WITH RC=0.
     IF EMOE-RETURN-CODE NOT = ZEROES
        EXEC CICS SEND TEXT FROM(WS-EXEC-ERROR)
            LENGTH(LENGTH OF WS-EXEC-ERROR) ALARM FREEKB
            ERASE END-EXEC
        GO TO RETURN-TO-CICS.
* SEND MESSAGE IF COMMAND COMPLETED SUCCESSFULLY.
        EXEC CICS SEND TEXT FROM(WS-FINISH-OK)
            LENGTH(LENGTH OF WS-FINISH-OK) FREEKB
            ERASE END-EXEC.
 RETURN-TO-CICS.
     EXEC CICS RETURN END-EXEC.


George A Soley
Senior Systems Programmer (USA)


