CICS IMS buffer statistics

After a recent database tuning exercise, the need arose to
examine the usage of the IMS buffer subpools allocated to
CICS.  Turning to our shutdown statistics, I discovered that,
unlike CICS under VSE, CICS under MVS did not produce
any database buffer subpool information.  To produce these
statistics I wrote the following simple COBOL program using
the DL/I STAT call.  When run, the first call receives the
statistics for the smallest pool defined, with each successive
call returning statistics for the next largest pool.  The final call
returns the  totals for all the pools.

Once read in, the data can be displayed in any suitable way,
written to the CSSL transient data queue for output to the log
as shown, or written to a file or terminal.  The program can be
invoked by a transaction to produce statistics up to that point
and/or added to the shutdown PLT to give a full set of
shutdown statistics.  Figure 1 shows some sample output.

The following types of STAT call are available.

o      VBASF - VSAM full formatted (three 120 byte lines - 360 bytes).

o      VBASU - VSAM full unformatted (18 fullwords of binary - 72 bytes).

o      VBASS - VSAM summary formatted (three 60 byte lines - 180
      bytes).

o      DBASF - ISAM/OSAM full formatted (three 120 byte lines - 360
      bytes).

o      DBASU - ISAM/OSAM full unformatted (18 fullwords of binary - 72
      bytes).

o      DBASS - ISAM/OSAM summary formatted (three 60 byte lines - 180
      bytes).

This program has been tested under CICS 1.7 and I can see no
reason why it should not work under any other release of
CICS, with little or no tailoring.  A full explanation of the
output can be found in the Application Programming for CICS
Users manual.


PROGRAM SOURCE CODE


 IDENTIFICATION DIVISION.
 REMARKS.        PRODUCE DLI BUFFER STATS.
 ENVIRONMENT DIVISION.
 CONFIGURATION SECTION.
 SOURCE-COMPUTER.  IBM-370.
 DATA DIVISION.
 WORKING-STORAGE SECTION.
 01  W01-STATS.
     03  W01-STATS1                  PIC X(120).
     03  W01-STATS2                  PIC X(120).
     03  W01-STATS3                  PIC X(120).
* PSBNAME - YOU CAN USE ANY PSB, AS YOU ONLY NEED IT FOR CALL
* STATUS RETURNS.
* NOTE - MAKE SURE THE NUMBER OF PCBS IN YOUR LINKAGE SECTION
* MATCHES THE NUMBER IN YOUR CHOSEN PSB.
 01  W01-PSBNAME                     PIC X(8) VALUE '????????'.
 01  W02-PASS-FLAG                   PIC 9 VALUE 1.
 01  FUNC-PCB                        PIC XXXX  VALUE 'PCB '.
 01  FUNC-TERM                       PIC XXXX  VALUE 'TERM'.
 01  FUNC-STAT                       PIC XXXX  VALUE 'STAT'.
* SEE ABOVE FOR CALL TYPES.
 01  FUNC-TYPE                       PIC X(9) VALUE 'VBASF    '.
 LINKAGE SECTION.
 01  DFHBLLDS SYNC.
     02  BLLCBAR                     PIC XXXX.
     02  CSACBAR                     PIC XXXX.
     02  CSAOPBAR                    PIC S9(8) COMP.
     02  TCACBAR                     PIC S9(8) COMP.
     02  B-PCBPTRS                   PIC S9(8) COMP.
     02  B-PCB1                      PIC S9(8) COMP.
 01  DFHCSADS COPY DFHCSADS SUPPRESS.
 01  DFHTCADS COPY DFHTCADS SUPPRESS.
 01  PCBPTRS.
     02  PCB1-PTR                    PIC S9(8) COMP.
 01  PCB1.
     03 DBD-NAME1                    PIC X(8).
     03 SEG-LEVEL1                   PIC XX.
     03 STATUS1                      PIC XX.
     03 OPTIONS1                     PIC X(4).
     03 FILLER                       PIC S9(5) COMP.
     03 SEG-NAME1                    PIC X(8).
     03 LENGTH-FB-KEY1               PIC S9(5) COMP.
     03 NUMB-SENS-SEGS1              PIC S9(5) COMP.
     03 KEY-FB-AREA1                 PIC X(80).
 PROCEDURE DIVISION.
 A10-CONTROL SECTION.
 A10-010.
     MOVE CSACDTA TO TCACBAR.
     MOVE CSAOPFLA TO CSAOPBAR.
     CALL 'CBLTDLI' USING FUNC-PCB, W01-PSBNAME.
     IF TCAFCRC NOT = LOW-VALUES
        EXEC CICS ABEND ABCODE ('SCHE') END-EXEC.
     MOVE TCADLPCB TO B-PCBPTRS.
     MOVE PCB1-PTR TO B-PCB1.
     PERFORM B10-STAT-LOOP UNTIL STATUS1 = 'GA'.
     CALL 'CBLTDLI' USING FUNC-TERM.
     EXEC CICS RETURN END-EXEC.
     GOBACK.
 A10-EXIT.
     EXIT.
 B10-STAT-LOOP SECTION.
 B10-010.
     CALL 'CBLTDLI' USING FUNC-STAT, PCB1, W01-STATS, FUNC-TYPE.
* THE EXEC DLI FORMAT FOR THE CALL IS
*     EXEC DLI STAT USING PCB(1)
*          INTO    (W01-STATS)
*          LENGTH  (WO1-STATS-LENGTH)
*          VSAM|NONVSAM
*          FORMATTED|UNFORMATTED|SUMMARY
*     END-EXEC.
     IF W02-PASS-FLAG = 1 THEN
        EXEC CICS WRITEQ TD
             QUEUE  ('CSSL')
             FROM   (W01-STATS1)
             LENGTH (120)
        END-EXEC
        EXEC CICS WRITEQ TD
             QUEUE  ('CSSL')
             FROM   (W01-STATS2)
             LENGTH (120)
        END-EXEC
        ADD 1 TO W02-PASS-FLAG.
     EXEC CICS WRITEQ TD
          QUEUE  ('CSSL')
          FROM   (W01-STATS3)
          LENGTH (120)
     END-EXEC.
 B10-EXIT.
     EXIT.


David Weaver
Systems Programmer
Bendix Ltd (UK)

