Program to generate passwords

It is easy for an individual to come up with a new password for
himself now and then.  However, for an individual who has the
responsibility of periodically thinking up dozens or hundreds of
new passwords for others, it can be a chore.  This program was
written to assist the person who has the chore of originating
large numbers of random character string passwords.  Of
course, it can also be used by anyone else who is too lazy to
think of a password without the aid of a computer or who
enjoys looking at random strings of letters.

Installation is simple - just add the following entries to you
PPT and PCT:

      DFHPPT TYPE=ENTRY,PROGRAM=JHOGP

      DFHPCT TYPE=ENTRY,PROGRAM=JHOGP,TRANID=JHGP,   *
            SPURGE=YES,TPURGE=YES,TWASIZE=0

To use it, type JHGP (or whatever transaction-id you care to
give it) and press ENTER.  A string of six characters will be
displayed on your screen.  Press ENTER again and a
completely different character string will appear.  Press
ENTER again and I well, you should get the picture by now.
Pressing CLEAR ends the session.  You can see by looking at
the source code how simple it is to create longer or shorter
character strings if six isn't to your liking.

How does it work?  You simply take a large number and divide
it by 26.  The remainder is used as an index to a character
between A and Z.  The quotient is then divided by 26 to create
another character index and a new quotient.  With each
iteration the sum of all previous remainders is added to the
current dividend.  This allows longer character strings to be
developed.

CICS provides the current time of day accurate to 1/300th of a
second in the field CSACTODB as a fullword binary value.
Using this value as the seed dividend provides the random
factor needed to make it work.  The value of the program's
base register is added to the seed value only to make it larger,
since early in the morning the time of day isn't a very large
value.  If you aren't pleased with the 'flavour' of passwords
being generated, adjust the initial seed with a value different
from the base register, or try using the time of day unadjusted.

JHGP     TITLE 'JHOGP - GENERATE PASSWORDS'
JHOGP    CSECT
TCTTEAR  EQU     11
TIOABAR  EQU     10
BASEREG  EQU     9
         DFHCSAD TYPE=DSECT
         DFHTCA  CICSYST=YES
         COPY    DFHTCTTE
         COPY    DFHAID
         COPY    DFHTIOA
JHOGP    CSECT
         BALR  BASEREG,0
         USING *,BASEREG
         L     TCTTEAR,TCAFCAAA           TCTTE ADDRESSABILITY
         DFHSC TYPE=GETMAIN,NUMBYTE=48,CLASS=TERMINAL,INITIMG=00
         L     TIOABAR,TCASCSA            GET A TIOA
         ST    TIOABAR,TCTTEDA
         CLI   TCTTEAID,DFHCLEAR          WAS CLEAR KEY HIT
         BNE   CONTINUE                   ....NO, CONTINUE
         MVC   TIOATDL,=H'0'              ...YES
         DFHTC TYPE=(WRITE,ERASE)         CLEAR SCREEN, FREE KEYBOARD
RETURN   DFHPC TYPE=RETURN
CONTINUE MVC   TIOATDL,=H'14'              SET TIOA LENGTH FOR OUTPUT
         MVC   TIOADATA(8),=X'1140401DF0114ED5'    SET SBA AND ATTRB
         SR    R8,R8                       CLEAR REG 8
         LR    R7,BASEREG                  GET A LARGE VALUE
         A     R7,CSACTODB                 TIME OF DAY MAKES IT RANDOM
         BAL   R14,RANDOM                  GET A RANDOM CHAR A-Z
         MVC   TIOADATA+8(1),0(R6)         MOVE 1ST CHARACTER TO TIOA
         BAL   R14,RANDOM                  GET A RANDOM CHAR A-Z
         MVC   TIOADATA+9(1),0(R6)         MOVE 2ND CHARACTER TO TIOA
         BAL   R14,RANDOM                  GET A RANDOM CHAR A-Z
         MVC   TIOADATA+10(1),0(R6)        MOVE 3RD CHARACTER TO TIOA
         BAL   R14,RANDOM                  GET A RANDOM CHAR A-Z
         MVC   TIOADATA+11(1),0(R6)        MOVE 4TH CHARACTER TO TIOA
         BAL   R14,RANDOM                  GET A RANDOM CHAR A-Z
         MVC   TIOADATA+12(1),0(R6)        MOVE 5TH CHARACTER TO TIOA
         BAL   R14,RANDOM                  GET A RANDOM CHAR A-Z
         MVC   TIOADATA+13(1),0(R6)        MOVE 6TH CHARACTER TO TIOA
         DFHTC TYPE=(WRITE,ERASE)          WRITE PASSWORD TO TERMINAL
         L     R14,TCASYAA               GET ADDRESS OF TCA SYSTEM AREA
         MVC   TCANXTID,TCAKCOID-DFHSYTCA(R14)      SET NEXT TRAN ID
         B     RETURN
RANDOM   SR    R6,R6                       CLEAR REG 6
         AR    R7,R8                       ADD TOTAL REG TO LARGE VALUE
         D     R6,=F'26'                   DIVIDE LARGE VALUE BY 26
         AR    R8,R6                       ACCUM TOTAL OF REMAINDERS
         LA    R6,TABLE(R6)                POINT TO A CHARACTER A-Z
         BR    R14                         RETURN TO CALLER
TABLE    DC    C'ABCDEFGHIJKLMNOPQRSTUVWXYZ'
         LTORG
         END   JHOGP


James E Holler
Little Blue Systems (USA)                c  Little Blue Systems/Xephon 1990


