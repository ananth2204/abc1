 DFHPEP transaction abend error message
Our shop needed a real-time method of detecting abending CICS programs.  This
would help us respond to problems faster and prevent problems caused by a full
dump dataset.  I chose to modify the CICS-provided Program Error Program,
DFHPEP, to send a message to the system console whenever a program abended.
DFHPEP is called by the CICS Abnormal Condition Program, DFHACP, after program
error exits have been taken and after dynamic transaction back-out has been
performed.  The abend code is available in the TCA, the transaction code is
retrieved from the PCT, the current time is kept in the CSA, and the current
Gregorian date is found in the communication region.  If a terminal is
associated with the task, I pull the terminal-id and user-id from the TCTTE.
The CICS Write-to-Operator macro (DFHWTO) is used to send the message to the VSE
system console.
I originally made these modifications to the DFHPEP source code provided with
CICS 1.5.  The program was reassembled to update the DSECTS when CICS 1.6 was
installed.  I also made these modifications to the DFHPEP source code provided
by VSE/SP 2.1.4 for CICS 1.6.  In the VSE/SP source code, I placed the
modifications ahead of the XCTL to the Interactive Interface.
*  THE CICS PROVIDED DFHPEP WAS ALTERED TO ISSUE AN 'ABEND'
*  MESSAGE AT THE SYSTEM OPERATOR CONSOLE FOR ALL ABENDING
*  TRANSACTIONS.
*  THE BASIS FOR THIS PROGRAM WAS THE DFHPEP PROVIDED WITH CICS 1.5/
*  SIPO 3.1.  THE ADDED EXECUTABLE CODE BEGINS AT LABEL A010 AND ENDS
*  AT LABEL Z010.  THE STORAGE ACCOUNTING AREA AND TEMPORARY STORAGE
*  AREAS FOLLOWING THE DFHSAADS (FROM LABEL STRTUSER TO LABEL ENDUSER)
*  WERE ADDED TO THE DSECT DEFINITION PORTION OF THE PROGRAM.  THE
*  DFHTCTTE DSECT WAS ALSO COPIED INTO THE PROGRAM. REGISTER 7 IS
*  DEFINED AS SAACBAR FOR ADDRESSABILITY TO THE TEMP STORAGE AREA.
*  REGISTER 11 IS DEFINED AS TCTTEAR FOR ADDRESSING THE DFHTCTTE DSECT.
* * *          R E G I S T E R   D E F I N I T I O N              * * *
SAACBAR  EQU   7                   SAA BASE REGISTER                  @
PCTCBAR  EQU   8                   PCT BASE REGISTER                  @
TCASBAR  EQU   9                   TCA SYSTEM AREA REGISTER           @
PEPBAR   EQU   10                  PEP BASE REGISTER                  @
TCTTEAR  EQU   11                  TCTTE BASE REGISTER
* * *          D U M M Y   S E C T I O N S                        * * *
         COPY  DFHCSADS                                               @
         DFHTCA CICSYST=YES                                           @
         COPY  DFHPCTDS                                               @
         COPY  DFHTCTTE
         COPY  DFHSAADS
STRTUSER EQU   *               START OF USER GETMAIN AREA
WTOMSG   DS    0CL76
         DS    CL4             WTO PREFIX
         DS    CL12
WTOMON   DS    CL2
WTODAY   DS    CL2
WTOYEAR  DS    CL2
         DS    CL1
WTOHOUR  DS    CL2
WTOMIN   DS    CL2
         DS    CL1
WTOTRAN  DS    CL4
         DS    CL9
WTOABND  DS    CL4
         DS    CL1
WTOOI    DS    CL3
         DS    CL1
WTOTI    DS    CL4
         DS    CL21
WORKTIME DS    0CL8
         DS    CL1
WORKHOUR DS    CL2
WORKMIN  DS    CL2
         DS    CL3
ENDUSER  EQU   *               END OF USER GETMAIN AREA
 * * * *          E X E C U T A B L E   C O D E                 * * * *
DFHPEP   CSECT                       PROGRAM ERROR PROGRAM CSECT
         DFHVM PEP                   GENERATE HEADING CONSTANT       @
         ENTRY DFHPEPNA              ESTABLISH ENTRY POINT           @
DFHPEPNA DS    0H                    ENTRY POINT                     @
         BALR  PEPBAR,0              ESTABLISH ADDRESSABILITY        @
         USING *,PEPBAR              .. AND BASE REGISTER            @
         L     TCASBAR,TCASYAA       LOAD TCA SYSTEM AREA ADDRESS    @
         USING DFHSYTCA,TCASBAR
A010     EQU   *               STORAGE CONSTANTS
         B     A020
EM01     DFHMSG 'DFHPEP.01.I mmddyy hhmm tttt ABENDED aaaa ooo tttt',U
TIMEMASK DC    X'4021202020202020' TIME EDIT MASK
         DS    F
A020     EQU   *               GETMAIN FOR USER STORAGE
         DFHSC TYPE=GETMAIN,       USER STORAGE                       X
               CLASS=USER,                                            X
               NUMBYTE=ENDUSER-STRTUSER,                              X
               INITIMG=00
         L     SAACBAR,TCASCSA     LOAD USER STORAGE ADDRESS REG
         MVC   WORKTIME,TIMEMASK
         ED    WORKTIME,CSATODP
         L     PCTCBAR,TCATCPC     LOAD PCT DSECT ADDRESS REGISTER
D010     EQU   *               SEND MESSAGE EM01...TRANSACTION ABEND
         MVC   WTOMSG,EM01
         COMRG
         MVC   WTOMON,0(1)         COMM REG DATE
         MVC   WTODAY,3(1)
         MVC   WTOYEAR,6(1)
         MVC   WTOHOUR,WORKHOUR
         MVC   WTOMIN,WORKMIN
         MVC   WTOTRAN,PCTTI
         MVC   WTOABND,TCACRABC
         TM    TCAFCI,TCAFCTRM     IS THIS A TERMINAL?
         BNO   F010
         L     TCTTEAR,TCAFCAAA
         MVC   WTOOI,TCTTEOI
         MVC   WTOTI,TCTTETI
F010     EQU   *
         DFHWTO WTOMSG
         B     Z010
Z010     EQU   *
         DFHPC TYPE=RETURN        ISSUE CICS RETURN MACRO            @
         LTORG *                                                     @
DFHPEPEA DS    0H                 MODULE END ADDRESS                 @
         DFHEND  DFHPEPNA         ASSEMBLY END - PROGRAM ERROR@BBDA20X
         END


Ed Newcomb
Senior Systems Programmer
Wacker Siltronic Corp (USA)   ) Wacker Siltronic Corp 1989














