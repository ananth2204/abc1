 Balancing CICS response times


INTRODUCTION

At National Pacific Corporation we implemented Michel Vanwesemael's CICS/VS
stabilising program (CICS Update November 1986) by using the XKCDISP KCP Exit.
Using this method, we do not need the source of CICS/VS to run our exit and it
is also very easy to activate/de-activate the exit as required.

The exit can be initiated by a PLT program using the command:

EXEC CICS ENABLE PROGRAM('TCSGSCRT') EXIT('XKCDISP') START


THE PROGRAM

* * *                      T C S G S C R T                       * * *
*        THIS PROGRAM IS NATIONAL PACIFIC'S VERSION OF THE PROGRAM
*        WRITTEN BY MICHEL VANWESEMAEL OF SECURA SA (BELGIUM) AND
*        PUBLISHED IN THE NOVEMBER '86 ISSUE OF "CICS UPDATE".
*        WHILE THE CONCEPT OF THIS PROGRAM IS FROM "CICS UPDATE",
*        NO "CICS UPDATE" CODE HAS BEEN USED, AS THE ORIGINAL CODE
*        WAS INTENDED AS A STATIC TASK MANAGEMENT EXIT, WHEREAS
*        THIS PROGRAM USES THE KCP "XKCDISP" (PRE-DISPATCH) EXIT
DCACBAR  EQU   3                       *
TCSGBAR  EQU   4                       BASE REGISTER
TCTTEAR  EQU   5                       TCT TERMINAL ENTRY
TCASBAR  EQU   6                       SYSTEM TCA AREA
DCAABAR  EQU   7                       DCA POINTER REGISTER
DCABBAR  EQU   8                       DCA POINTER REGISTER
TCABAR   EQU   12                      TASK CONTROL AREA
CSABAR   EQU   13                      COMMON SYSTEM AREA (R13)
EUA      EQU   X'12'                   ERASE-UNPROT-TO-ADDR ORDER
GE       EQU   X'08'                   GRAPHIC-ESCAPE ORDER
IC       EQU   X'13'                   INSERT-CURSOR ORDER
MF       EQU   X'2C'                   MODIFY-FIELD ORDER
PT       EQU   X'05'                   PROGRAM-TAB ORDER
RTA      EQU   X'3C'                   REPEAT-TO-ADDRESS ORDER
SF       EQU   X'1D'                   START-FIELD ORDER
SFE      EQU   X'29'                   START-FIELD-EXTENDED ORDER
SA       EQU   X'28'                   SET-ATTRIBUTE ORDER
SBA      EQU   X'11'                   SET-BUFFER-ADDRESS ORDER
SBAL     EQU   3                       LENGTH OF A SBA FIELD
         COPY  DFHCSADS
         DFHTCA CICSYST=YES
         DFHTCTZE CICSYST=YES
         COPY  DFHDCADS
         DFHUEXIT TYPE=EP
         COPY  DFHAID
* * *                      TCSGSCRT MAINLINE                     * * *
TCSGSCRT CSECT
         SAVE  (14,12)
         USING DFHUEPAR,R1             R1  => PARAMETER LIST
         USING DFHDCADS,R3             R3  => DCA ADDRESS
         USING TCSGSCRT,R4             R4  => BASE REGISTER
         USING DFHTCTTE,R5             R5  => TCTTE ADDRESS
         USING DFHTCADY,R6             R6  => SYSTCA ADDRESS
         USING DFHTCADS,R12            R12 => TCA ADDRESS
         USING DFHCSADS,R13            R13 => CSA ADDRESS
         LR    R4,R15                  COPY OUR BASE ADDR
         LR    R2,R13                  SAVE R13 ADDR IN R2
*        ADDRESS THE TCA (R12) AND CSA (R13)
         L     TCABAR,UEPTCA           R12 => TCA ADDRESS
         L     CSABAR,UEPCSA           R13 => CSA ADDRESS
*        ENSURE THAT TCAFCAAA ADDRESSES A TERMINAL
          CLI   TCAFCI,TCAFCTRM         TEST FOR TCAFCAAA = TERM
         BNE   RETURN
*        PICK UP THE TCTTE AND SYSTEM TCA ADDRESSES
         L     TCTTEAR,TCAFCAAA        R5  => TCTTE ADDRESS
         L     TCASBAR,TCASYAA         R6  => SYSTEM TCA ADDR
*        JUST IN CASE THINGS GET ALL OUT OF HAND
         CLC   =C'V822',TCTTETI        RESTRICT ACTIVITY TO MY
         NOP   RETURN                  TERMINAL ONLY (FOR NOW)
*        BYPASS THE TASK IF...
*              1) OPERATOR PRIORITY (TCTTEOP) = 255
*              2) TERMINAL PRIORITY (TCTTETP) = 255
*              3) TRANSACT PRIORITY (TCATCDP) = 255
*              4) IT IS A CICS SYSTEM TASK
         CLI   TCTTEOP,255             CHECK OPER PRIORITY
         BE    RETURN
         CLI   TCTTETP,255             CHECK TERM PRIORITY
         BE    RETURN
         CLI   TCATCDP,255             CHECK TRAN PRIORITY
         BE    RETURN
         TM    TCAKCTTA+2,X'0C'        TEST FOR NUMERIC TASK NBR
         BNO   RETURN                  NO, MUST BE A SYSTEM TASK
         LM    DCAABAR,DCABBAR,DCAKCFA     GET NEXT HI/LO PRTY DCA
         ST    DCAABAR,DCAKCFA-DFHDCADS(DCABBAR) STORE HI INTO LOW
         ST    DCABBAR,DCAKCBA-DFHDCADS(DCAABAR) STORE LOW INTO HIGH
         LA    DCAABAR,CSAACTOF        GET OFFSET TO ACTIVE TASK CHAIN
         XR    R1,R1                   GET THE PRIORITY OF THE TASK TO
         IC    R1,DCATCDP              BE INSERTED INTO THE DCA CHAIN
NEXTHPR  L     DCAABAR,DCAKCFA-DFHDCADS(DCAABAR) LOAD ADDR OF NEXT DCA
         CLM   R1,1,DCATCDP-DFHDCADS(DCAABAR)    COMPARE THE PRIORITES
         BH    NEXTHPR                           GET NEXT HIGHER PRTY
         L     DCABBAR,DCAKCBA-DFHDCADS(DCAABAR) LOAD NEXT TASK ADDRESS
         STM   DCAABAR,DCABBAR,DCAKCFA           SAVE CHAINS IN THE DCA
         ST    DCACBAR,DCAKCFA-DFHDCADS(DCABBAR) STORE ADDR OF LOW DCA
         ST    DCACBAR,DCAKCBA-DFHDCADS(DCAABAR) STORE ADDR OF HIGH DCA
RETURN   LR    R13,R2                  RESTORE R13 SAVEAREA ADDR
         RETURN (14,12)
         LTORG
         END   TCSGSCRT


W M Anderson
Senior Systems Programmer
National Pacific Corporation (New Zealand)   ) National Pacific Corp 1987























