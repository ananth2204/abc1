./ ADD NAME=ENQ      0100-06020-06053-1357-00050-00040-00000-T7796MT
/* -- REXX --------------------------------------------------------- */
/* -- Purpose: To test to see if an ENQ is held against a DSN or any */
/* --          RNAME pattern.  No input defaults to RNAME=*; i.e.    */
/* --          ALL RNAMEs for ALL QNAMEs are returned.  The ENQ exec */
/* --          can be invoked via "TSO %ENQ pattern", or a 3.4 line  */
/* --          cmd, where pattern can be null, a partial RNAME/DSN,  */
/* --          or a fully qualified, quoted DSN.  Don't include "*". */
/* --          The table returned from QUERYENQ is displayed in a    */
/* --          pop-up Window if anything holds an ENQ on ARG EQDSN   */
/* -- Related: . Panels Named OSGP2ENQ & OSGP4ENQ                    */
/* -- By Jim Moore - Concentrated Logic Inc - 1/15/2005              */
/* ----------------------------------------------------------------- */
/* Maintenance:                                                      */
/* -- By Mike Tomkins - DaimlerChrysler Corp. - 01/20/2006           */
/* --    Updated to allow RNAME patterns; i.e. not limited to DSNs   */
/* --    Updated to allow larger popup window width - 77 vs. 46      */
/* --    Updated to allow larger popup window depth - old value of 8 */
/* --               replaced by 39 for mod 4 or 20 for anything else */
/* --    Updated to allow > 5,000 output table rows via LIMIT(0)     */
/* ----------------------------------------------------------------- */
Parse Upper Arg EQDSN
Address Ispexec 'LIBDEF ISPPLIB DATASET,
         ID(''Your_panel_library'') STACK'
Ispexec "CONTROL ERRORS CANCEL"  /* Ensure that errors cancel REXX   */
EQDSN = Strip(''EQDSN'',,"'")    /* Strip off quotes if DSN quoted   */
MAJOR = '*'                      /* Set Major to any Name            */
MINOR = EQDSN'*'                 /* Set Minor to Arg DSN w/asterisk  */
ZTDMARK = CENTER('------------ End of ENQ Holder List ------------',77)
Ispexec "QUERYENQ TABLE(DSNENQ) QNAME(MAJOR) RNAME(MINOR) XSYS LIMIT(0)"
QRC = RC                         /* Save RC from QUERYENQ            */
If QRC < 5 Then Do               /* If 0-2-4, an ENQ is being held   */
  TRC = 0                        /* Force "TableRC" (TRC) = 0        */
  Ispexec "VGET (ZSCRMAXD)"      /* Get the max scrn depth for popup */
  If ZSCRMAXD = 43 Then osgpanel = "OSGP4ENQ"   /* If Mod4, depth=39 */
                   Else osgpanel = "OSGP2ENQ" /* If Â¬ Mod4, depth=20 */
  Do Until TRC > 0               /* Display panel until END          */
    Ispexec "ADDPOP"             /* Add pop-up window                */
    Ispexec "TBDISPL DSNENQ PANEL("osgpanel")"     /* TBDISPL panel  */
    TRC = RC                     /* Save RC as "TRC"                 */
    Ispexec "REMPOP"             /* Remove pop-up window             */
  End                            /* Out on TRC Not = 0               */
  Ispexec "TBEND DSNENQ"         /* Destroy table from QUERYENQ      */
End                              /* -------------------------------- */
If QRC = 8 Then Do               /* If 8, No ENQ held                */
  ZMSG000S = " "                 /* No short message                 */
  ZMSG000L = "There are no enqueues on resource" EQDSN
  Ispexec 'SETMSG MSG(ISPZ000)'  /* Only a long message              */
End                              /* -------------------------------- */
Ispexec "LIBDEF ISPPLIB"         /* Destroy panel LIBDEF             */
Exit
./ ADD NAME=OSGP2ENQ 0100-06020-06020-2200-00018-00018-00000-T7796MT
)ATTR
? TYPE(OUTPUT) CAPS(ON)  INTENS(LOW)
_ TYPE(INPUT)  CAPS(ON)  INTENS(HIGH)
% TYPE(TEXT)             INTENS(HIGH) SKIP(ON)
+ TYPE(TEXT)             INTENS(LOW)  SKIP(ON)
)BODY WINDOW(77,20)
%
%Command ===>_ZCMD                                         %Scroll ===>_Z   %
%
%--Holder---How--Type--Scope--System--Qname---Rname--------------------------
)MODEL
 ?Z       ?Z     ?Z   ?Z      ?Z     ?Z      ?Z
)INIT
.ZVARS = '(ZSCED,ZENJOB,ZENDISP,ZENHOLD,ZENSCOPE,ZENSYST,+
ZENQNAME,ZENRNAME)'
&ZWINTTL = &EQDSN
)PROC
)END
./ ADD NAME=OSGP4ENQ 0100-06020-06020-2200-00018-00018-00000-T7796MT
)ATTR
? TYPE(OUTPUT) CAPS(ON)  INTENS(LOW)
_ TYPE(INPUT)  CAPS(ON)  INTENS(HIGH)
% TYPE(TEXT)             INTENS(HIGH) SKIP(ON)
+ TYPE(TEXT)             INTENS(LOW)  SKIP(ON)
)BODY WINDOW(77,39)
%
%Command ===>_ZCMD                                         %Scroll ===>_Z   %
%
%--Holder---How--Type--Scope--System--Qname---Rname--------------------------
)MODEL
 ?Z       ?Z     ?Z   ?Z      ?Z     ?Z      ?Z
)INIT
.ZVARS = '(ZSCED,ZENJOB,ZENDISP,ZENHOLD,ZENSCOPE,ZENSYST,+
ZENQNAME,ZENRNAME)'
&ZWINTTL = &EQDSN
)PROC
)END
