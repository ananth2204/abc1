/* -- REXX --------------------------------------------------------- */
/* -- Purpose: To test to see if an ENQ is held against a DSN        */
/* --          The table returned from QUERYENQ is displayed in a    */
/* --          pop-up Window if anything holds an ENQ on ARG DSN     */
/* -- Related: . Panel Named CLCPQENQ                                */
/* -- By Jim Moore - Concentrated Logic Inc - 1/15/2005              */
/* ----------------------------------------------------------------- */
/* Maintenance:                                                      */
/* ----------------------------------------------------------------- */
Parse Upper Arg EQDSN
Address Ispexec 'LIBDEF ISPPLIB DATASET,
         ID(''pds.with.CLCPQENQ.panel'') STACK'
Ispexec "CONTROL ERRORS CANCEL"  /* Ensure that errors cancel REXX   */
EQDSN = Strip(''EQDSN'',,"'")    /* Strip off quotes if DSN quoted   */
X = LISTDSI(''''EQDSN'''' DIRECTORY NORECALL)
IF SYSREASON /= 0 Then Do        /* If DSN not Cataloged             */
  ZMSG000S = " "                 /* No short message                 */
  ZMSG000L = "Dataset "EQDSN" Not in Catalog"
  Ispexec 'SETMSG MSG(ISPZ000)'  /* If DSN NOT Catlg'd, message+exit */
  Exit                           /* -------------------------------- */
End
MAJOR = '*'                      /* Set Major to any Name            */
MINOR = EQDSN                    /* Set Minor to Arg DSN             */
If SYSDSORG = "PO" Then Do       /* If a PDS                         */
 If Left(SYSRECFM,1) = "F" Then Do /* If "Fixed"                     */
    MINOR = EQDSN'*'             /* Append asterisk to Minor         */
 End                             /* (to pick up other ENQUEUEs)      */
End
ZTDMARK = CENTER('---End of ENQ Holder List---',44)
Ispexec "QUERYENQ TABLE(DSNENQ) QNAME(MAJOR) RNAME(MINOR) XSYS"
QRC = RC                         /* Save RC from QUERYENQ            */
If QRC < 5 Then Do               /* If 0-2-4, an ENQ is being held   */
  TRC = 0                        /* Force "TableRC" (TRC) = 0        */
  Do Until TRC > 0               /* Display panel until END          */
    Ispexec "ADDPOP"             /* Add pop-up window                */
    Ispexec "TBDISPL DSNENQ PANEL(CLCPQENQ)"   /* TBDISPL panel      */
    TRC = RC                     /* Save RC as "TRC"                 */
    Ispexec "REMPOP"             /* Remove pop-up window             */
  End                            /* Out on TRC Not = 0               */
  Ispexec "TBEND DSNENQ"         /* Destroy table from QUERYENQ      */
End                              /* -------------------------------- */
If QRC = 8 Then Do               /* If 8, No ENQ held                */
  ZMSG000S = " "                 /* No short message                 */
  ZMSG000L = "No Other job holds a SYSDSN Enq on "EQDSN
  Ispexec 'SETMSG MSG(ISPZ000)'  /* Only a long message              */
End                              /* -------------------------------- */
Ispexec "LIBDEF ISPPLIB"         /* Destroy panel LIBDEF             */
Exit
