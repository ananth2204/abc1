*---------------------------------------------------------------------*
*                                                                     *
*                         C A P X T R A N                             *
*                         = = = = = = = =                             *
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
* Copyright (C) 2007  Kevin Wailes                                    *
*                                                                     *
* This program is free software; you can redistribute it and/or modify*
* it under the terms of the GNU General Public License as published by*
* the Free Software Foundation; either version 2 of the License, or   *
* (at your option) any later version.                                 *
*                                                                     *
* This program is distributed in the hope that it will be useful,     *
* but WITHOUT ANY WARRANTY; without even the implied warranty of      *
* MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the       *
* GNU General Public License for more details.                        *
*                                                                     *
* You should have received a copy of the GNU General Public License   *
* along with this program; if not, write to the Free Software         *
* Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA            *
* 02111-1307  USA                                                     *
*                                                                     *
* Author:                                                             *
* Kevin Wailes <kevin.wailes@yahoo.com>                               *
*                                                                     *
*---------------------------------------------------------------------*
         DFHREGS                        REGISTER EQUATES
*
         COPY DFHAID
*
* Working storage
*
DFHEISTG DSECT
*
RESP     DS     F
RESP2    DS     F
ABSTIME  DS     PL8
*
ITEM          DS H
LENGTH        DS H
*
TRANSACTIONS DS CL(16*4)
*
         COPY CAPXTRN
*
* Here we go, here we go, here we go
*
CAPXTRAN RMODE  ANY
CAPXTRAN AMODE  31
CAPXTRAN DFHEIENT EIBREG=(R10),CODEREG=(R11,R12),DATAREG=(R13)
*
         CLC   EIBCALEN,ZERO
         BNE   NOT_FIRST
         LA    R1,16*4
         STH   R1,LENGTH
         LA    R1,1
         STH   R1,ITEM
         EXEC CICS READQ TS QNAME('CSQCAPX') INTO(TRANSACTIONS)        X
               LENGTH(LENGTH) ITEM(ITEM)                               X
               RESP(RESP) RESP2(RESP2)
         CLC   RESP,DFHRESP(NORMAL)
         BE    FORMAT_MAP
         LA    R2,TRANSACTIONS
         LA    R3,L'TRANSACTIONS
         LR    R4,R2
         XR    R5,R5
         MVCL  R2,R4
         B     FORMAT_MAP
*
NOT_FIRST DS 0H
         L     R1,DFHEICAP
         MVC   TRANSACTIONS,0(R1)
         EXEC CICS RECEIVE MAP('CAPXTRN') MAPSET('CAPXTRN')            X
               INTO(CAPXTRNI)                                          X
               RESP(RESP) RESP2(RESP2)
         CLC   RESP,DFHRESP(NORMAL)
         BNE   THATS_ALL_FOLKS
         CLI   EIBAID,DFHENTER
         BE    PROCESS_IT
         CLI   EIBAID,DFHPF3
         BE    WRITE_QUEUE
         CLI   EIBAID,DFHPF15
         BE    WRITE_QUEUE
         CLI   EIBAID,DFHPF12
         BE    PURGE_QUEUE
         CLI   EIBAID,DFHPF24
         BE    PURGE_QUEUE
         B     FORMAT_MAP
*
THATS_ALL_FOLKS DS 0H
         EXEC CICS SEND CONTROL ERASE FREEKB                           x
               RESP(RESP) RESP2(RESP2)
         EXEC CICS RETURN
*
PURGE_QUEUE DS 0H
         EXEC CICS DELETEQ TS QNAME('CSQCAPX')                         X
               RESP(RESP) RESP2(RESP2)
         B     THATS_ALL_FOLKS
*
WRITE_QUEUE DS 0H
         EXEC CICS DELETEQ TS QNAME('CSQCAPX')                         X
               RESP(RESP) RESP2(RESP2)
         EXEC CICS WRITEQ TS QNAME('CSQCAPX') MAIN FROM(TRANSACTIONS)  X
               RESP(RESP) RESP2(RESP2)
         B     THATS_ALL_FOLKS
*
PROCESS_IT  DS    0H
         LA    R2,TRANSACTIONS
         LA    R3,L'TRANSACTIONS
         LR    R4,R2
         XR    R5,R5
         MVCL  R2,R4
         LA    R2,TRANSACTIONS
         LA    R3,16
         LA    R4,TRAN1L
NEXT_TRANSACTION DS 0H
         CLC   0(2,R4),ZERO
         BE    NO_TRANSACTION
         MVC   0(4,R2),4(R4)
         LA    R2,4(R2)
NO_TRANSACTION DS 0H
         LA    R4,TRAN2L-TRAN1L(R4)
         BCT   R3,NEXT_TRANSACTION
FORMAT_MAP DS  0H
         LA    R8,CAPXTRNO
         LA    R9,CAPXTRNL
         LR    R2,R8
         XR    R3,R3
         MVCL  R8,R2
         LA    R2,TRAN1O
         LA    R3,TRANSACTIONS
         LA    R4,16
NEXT_ONE DS    0H
         CLC   0(4,R3),ZERO
         BE    NO_MORE
         MVC   0(4,R2),0(R3)
         LA    R2,TRAN2L-TRAN1L(R2)
         LA    R3,4(R3)
         BCT   R4,NEXT_ONE
NO_MORE DS 0H
         EXEC  CICS ASKTIME ABSTIME(ABSTIME)                           X
               RESP(RESP) RESP2(RESP2)
         EXEC CICS FORMATTIME ABSTIME(ABSTIME)                         X
               DDMMYYYY(DATEO)                                         X
               TIME(TIMEO) DATESEP('/') TIMESEP(':')                   X
               RESP(RESP) RESP2(RESP2)
SEND_MAP DS    0H
         EXEC CICS SEND MAP('CAPXTRN') MAPSET('CAPXTRN')               X
               FROM(CAPXTRNO) ERASE FREEKB                             X
               RESP(RESP) RESP2(RESP2)
         EXEC  CICS RETURN TRANSID(EIBTRNID) COMMAREA(TRANSACTIONS)
*-------------------------------------------------------------*
* CONSTANTS, EQUATES & MESSAGES                               *
*-------------------------------------------------------------*
         LTORG
*
*
ZERO     DC  A(0)
*
         END  CAPXTRAN
