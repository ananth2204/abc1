/* Rexx Dialog to Submit a Suite of Generic RACF ICETOOL Reports     */
/* ----------------------------------------------------------------- */
/*                                                                   */
/* Updated, 19Mar20, Lionel B. Dyck                                  */
/* . Convert to use a dynamic table with more reports                */
/*                                                                   */
/* Updated, 22Jan20, William J. Smith, Trident Services, Inc.        */
/* . Corrected return code error display in two error messages.      */
/*                                                                   */
/* Created, 03Dec19, William J. Smith, Trident Services, Inc.        */
/* . An ISPF dialog that uses file tailoring to generate 13 generic  */
/*   RACF reports using DFSort ICETOOL                               */
/* . Requires the name of the RACF unload data base (IRRUDB00)       */
/*   offload utility output                                          */
/* . The name of the file tailoring skeleton is specified below as   */
/*   an operand of the CALL to the Build_JCL inline procedure.       */
/* . The format of the job statement used below is installation      */
/*   specific.  Modify as needed.                                    */
/*                                                                   */
/* The hard-coded name of the file tailoring skeleton is RACFREPT.   */
/*                                                                   */
/* ----------------------------------------------------------------- */

/* Trace ?I                                                          */

Address ISPEXEC
"CONTROL ERRORS CANCEL"

/* ----------------------------------------------------------------- */
/*                                                                   */
/* JOB Statement Construction                                        */
/*                                                                   */
/* ----------------------------------------------------------------- */

"VGET (ZACCTNUM, ZTEMPF) ASIS"

syspref = sysvar("syspref")
sysuid  = sysvar("sysuid")
acct    = zacctnum

If syspref = "" then
  syspref = sysuid

jobcard_jcl = "(jobcard1 jobcard2 jobcard3 jobcard4)"
"VGET" jobcard_jcl "SHARED"

If jobcard1 = "" then
  jobcard1 = ,
  "//"syspref"R JOB ("acct",32),'RACF ICETOOL Reports',CLASS=A,",

If jobcard2 = "" then
  jobcard2 = ,
  "//     MSGLEVEL=(1,1),MSGCLASS=X,NOTIFY="sysuid",REGION=0M"

If jobcard3 = "" then
  jobcard3 = ,
  "/*JOBPARM L=999999"

If jobcard4 = "" then
  jobcard4 = ,
  "//*"

"VPUT" jobcard_jcl "SHARED"

/* ----------------------- *
 | Generate the ISPF Table |
 * ----------------------- */
 racftab = racfrodt()

/* ----------------------------------------------------------------- */
/*                                                                   */
/* Display RACFROD - RACF Reports On-Demand Selection Panel          */
/*                                                                   */
/* ----------------------------------------------------------------- */

rtop = 1
do forever
'tbtop' racftbl
'tbskip' racftbl 'number(rtop)'
'tbdispl' racftbl 'panel(racfrodt)'
display_rc = RC
if display_rc = 0 then leave
If display_rc > 4 then
  Do
    rrdsm = "RACF ROD display ended"
    rrdlm = ,
    "RACF Reports On-Demand has ended due to a user requested termination."
    rrdalarm = "YES"
    rrdtyp = "NOTIFY"
    rrdwin = "NORESP"
    Address ISPEXEC
    "SETMSG MSG(RROD000Z)"
    'tbend' racftbl
    Exit display_rc
  End

"FTOPEN TEMP"
ftrc = RC
If ftrc > 0 then
  Do
    rrdsm = "File tailoring RC="ftrc
    rrdlm = ,
    "RACF Reports On-Demand has ended due to a file tailoring FTOPEN error."
    rrdalarm = "YES"
    rrdtyp = "NOTIFY"
    rrdwin = "NORESP"
    Address ISPEXEC
    "SETMSG MSG(RROD000Z)"
    'tbend' racftbl
    Exit ftrc
  End

Call BUILD_JCL "RACFREPT"

'tbend' racftbl
"FTCLOSE"

Address ISPEXEC
"VGET (ZTEMPF)"

/* ----------------------------------------------------------------- */
/*                                                                   */
/* Return codes greater than 4 for the EDIT service are critical.    */
/* Consult the ISPF Services Guide, SC19-3626, for detailed error    */
/* information.                                                      */
/*                                                                   */
/* ----------------------------------------------------------------- */

"EDIT DATASET('"ZTEMPF"') MACRO(RACFRODN)"
editrc = RC
If editrc > 4 then
  Do
    rrdsm = "Edit error RC="editrc
    rrdlm = ,
    "Critical EDIT failure.  Consult ISPF Services Guide for information."
    rrdalarm = "YES"
    rrdtyp = "CRITICAL"
    rrdwin = "RESP"
    Address ISPEXEC
    "SETMSG MSG(RROD000Z)"
    Exit editrc
  End

/* ----------------------------------------------------------------- */
/*                                                                   */
/* Submit the ICETOOL reports as selected from the PDF Edit display. */
/*                                                                   */
/* Specifying END, RETURN, SUBMIT, or EXIT will submit the JCL for   */
/* background JES2 execution.  CANCEL will terminate the PDF editor  */
/* and any submission to batch of the JCL that was generated by      */
/* ISPF file tailoring.                                              */
/*                                                                   */
/* ----------------------------------------------------------------- */

"VGET (ZVERB)"
If ,
(ZVERB = "END" | ZVERB = "RETURN" | ZVERB = "SUBMIT" | ZVERB = "EXIT"),
  then
   Do
     Address TSO
     "SUBMIT '"ZTEMPF"'"
     subrc = RC
     Address ISPEXEC
     If subrc = 0 then
       "SETMSG MSG(RROD003)"
     Else
       "SETMSG MSG(RROD005)"
   End
Else
  Do
    Address ISPEXEC
    "SETMSG MSG(RROD004)"
  End
Exit RC

/* ----------------------------------------------------------------- */
/*                                                                   */
/* Build_JCL:  A file tailoring subroutine to build JCL              */
/*                                                                   */
/* A return code greater than 0 indicates a critical error when      */
/* processing (tailoring) a file skeleton.  Consult the ISPF         */
/* Services Guide, SC19-3626, for detailed error information.        */
/*                                                                   */
/* ----------------------------------------------------------------- */

Build_JCL: Procedure

Parse upper arg skelname
"FTINCL" skelname
ftrc = RC
If ftrc > 0 then
  Do
    Address ISPEXEC
    "SETMSG MSG(RROD001)"
    Exit RC
  End
Return RC
