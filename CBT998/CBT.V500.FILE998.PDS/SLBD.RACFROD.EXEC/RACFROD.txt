/* Rexx Dialog to Submit a Suite of Generic RACF Reports             */
/* ----------------------------------------------------------------- */
/*                                                                   */
/* Updated, 20Mar20, Lionel B. Dyck                                  */
/* . Add non-ICETOOL reports (DSMON)                                 */
/* . Change from static table to dynamic                             */
/* . Allow multiple reports to be selected                           */
/* . Add ONLY to filter and Refresh to rebuild the table             */
/* . Call site customization exec RACFRODS                           */
/*                                                                   */
/* Updated, 22Jan20, William J. Smith, Trident Services, Inc.        */
/* . Corrected return code error display in two error messages.      */
/*                                                                   */
/* Created, 03Dec19, William J. Smith, Trident Services, Inc.        */
/* . An ISPF dialog that uses file tailoring to generate 13 generic  */
/*   RACF reports using DFSort ICETOOL                               */
/* . Requires the name of the RACF unload data base (IRRUDB00)       */
/*   offload utility output                                          */
/* . The name of the file tailoring skeleton is specified below as   */
/*   an operand of the CALL to the Build_JCL inline procedure.       */
/* . The format of the job statement used below is installation      */
/*   specific.  Modify as needed.                                    */
/*                                                                   */
/* The hard-coded name of the file tailoring skeleton is RACFREPT.   */
/*                                                                   */
/* ----------------------------------------------------------------- */

/* Trace ?I                                                          */

  Address ISPEXEC
  "CONTROL ERRORS CANCEL"

/* ----------------------------------------------------------------- */
/*                                                                   */
/* JOB Statement Construction                                        */
/*                                                                   */
/* ----------------------------------------------------------------- */

  "VGET (ZACCTNUM, ZTEMPF) ASIS"

/* --------------- *
 | Define defaults |
 * --------------- */
  parse value '' with null
  rrdalarm = "YES"
  rrdtyp = "NOTIFY"

  syspref = sysvar("syspref")
  sysuid  = sysvar("sysuid")
  acct    = zacctnum

  If syspref = null then
  syspref = sysuid

  jname = sysuid
  if length(jname) < 8 then jname = jname'R'

  'vget (rrjc1 rrjc2 rrjc3 rrjc4) profile'

  jc_update = 0

  if rrjc1 = null then do
    rrjc1 = ,
      "//"left(jname,8)" JOB ("acct"),'RACF ReportsOnDemand',CLASS=A,"
      jc_update = 1
  end

  if rrjc2 = null then
  rrjc2 = ,
    "//     MSGLEVEL=(1,1),NOTIFY=&SYSUID,REGION=0M"

  if rrjc3 = null then
  rrjc3 = ,
    "/*JOBPARM L=999999"

  if rrjc4 = null then
  rrjc4 = ,
    "//OUT    OUTPUT DEFAULT=YES,JESDS=ALL,OUTDISP=(HOLD,HOLD)"

  if jc_update = 1 then do
    zcmd = null
    jc1 =  rrjc1
    jc2 =  rrjc2
    jc3 =  rrjc3
    jc4 =  rrjc4
    'display panel(racfrods)'
    if rc = 0 then do
      rrjc1 = jc1
      rrjc2 = jc2
      rrjc3 = jc3
      rrjc4 = jc4
     'vput (rrjc1 rrjc2 rrjc3 rrjc4) profile'
    end
  end


/* --------------------------------- *
 | Create the RACF ISPF Report Table |
 * --------------------------------- */
  ratab = racfrodt()

/* ----------------------- *
 | Get site customizations |
 * ----------------------- */
  x = racfrods()
  'vget (rsub) profile'

/* ---------------------------------------- *
 | Selected flag indicates reports selected |
 * ---------------------------------------- */
  selected = 0

/* ----------------------------------------------------------------- */
/*                                                                   */
/* Display RACFROD - RACF Reports On-Demand Selection Panel          */
/*                                                                   */
/* ----------------------------------------------------------------- */

  rtop = 1

  do forever
    parse value '' with rsel doit
    if ztdsels > 1 then 'tbdispl' ratab
    else do
      'tbtop' ratab
      'tbskip' ratab 'number('rtop')'
      'tbdispl' ratab 'panel(racfrodt)'
    end
    display_rc = RC
    'vput (rsub) profile'
    If display_rc > 4 then if row = 0 then
    Do
      if selected > 0 then leave
      rrdsm = "RACF ROD display ended"
      rrdlm = ,
        "RACF Reports On-Demand has ended due to a user requested termination."
      Address ISPEXEC
      "SETMSG MSG(RROD000Z)"
      'tbend' ratab
      Exit display_rc
    End
    if zcmd /= null then do
      if abbrev('SETUP',word(zcmd,1)) = 1 then do
        zcmd = null
          'vget (rrjc1 rrjc2 rrjc3 rrjc4) profile'
        jc1 =  rrjc1
        jc2 =  rrjc2
        jc3 =  rrjc3
        jc4 =  rrjc4
        'display panel(racfrods)'
        if rc = 0 then do
          rrjc1 = jc1
          rrjc2 = jc2
          rrjc3 = jc3
          rrjc4 = jc4
          'vput (rrjc1 rrjc2 rrjc3 rrjc4) profile'
        end
      end
      if abbrev('ONLY',word(zcmd,1)) = 1 then
      if word(zcmd,2) /= null then do
        'tbtop' ratab
        'tbskip' ratab
        test = translate(word(zcmd,2))
        do forever
          if pos(test,translate(rec pgm name desc)) = 0 then do
            'tbdelete' ratab
          end
          'tbskip' ratab
          if rc > 0 then do
            rrdsm = 'Filtered.'
            rrdlm = 'Table of reports filtered by' test
            'setmsg msg(rrod000z)'
            leave
          end
        end
      end
      if abbrev('REFRESH',word(zcmd,1)) = 1 then do
        'tbend' ratab
        ratab = racfrodt()
        rrdsm = 'Refreshed.'
        rrdlm = 'Table of available reports refreshed.'
        'setmsg msg(rrod000z)'
      end
    end
    else do

      if row = null then if rsel = 'S' then
      if rec /= 'ALL' then
      do
        doit = 'S'
        'tbmod' ratab
        selected = selected + 1
      end
      if row > 0 then if rsel = 'S' then
      if rec /= 'ALL' then
      do
        'tbtop' ratab
        'tbskip' ratab 'number('row')'
        doit = 'S'
        'tbmod' ratab
        selected = selected + 1
      end
      if rsel = 'S' then
      if rec = 'ALL' then do
        if selected > 0 then do
          rrdsm = 'Error.'
          rrdlm = 'You may not select ALL after having selected' ,
            'individual reports. Perform a Refresh and then' ,
            'you may select ALL.'
          'setmsg msg(rrod000z)'
        end
        else do
          doit = 'S'
          'tbmod' ratab
          leave
        end
      end
    end
    zcmd = null
  end

  "FTOPEN TEMP"
  ftrc = RC
  If ftrc > 0 then
  Do
    rrdsm = "File tailoring RC="ftrc
    rrdlm = ,
      "RACF Reports On-Demand has ended due to a file tailoring FTOPEN error."
    Address ISPEXEC
    "SETMSG MSG(RROD000Z)"
    'tbend' ratab
    Exit ftrc
  End

  Call BUILD_JCL

  'tbend' ratab
  "FTCLOSE"

  "VGET (ZTEMPF)"

/* ----------------------------------------------------------------- */
/*                                                                   */
/* Return codes greater than 4 for the EDIT service are critical.    */
/* Consult the ISPF Services Guide, SC19-3626, for detailed error    */
/* information.                                                      */
/*                                                                   */
/* ----------------------------------------------------------------- */

  "EDIT DATASET('"ZTEMPF"') MACRO(RACFRODN)"
  editrc = RC
  If editrc > 4 then
  Do
    rrdsm = "Edit error RC="editrc
    rrdlm = ,
      "Critical EDIT failure.  Consult ISPF Services Guide for information."
    rrdtyp = "CRITICAL"
    Address ISPEXEC
    "SETMSG MSG(RROD000Z)"
    Exit editrc
  End

/* ----------------------------------------------------------------- */
/*                                                                   */
/* Submit the RACF Reports-On_Demand based upon RSUB = Y             */
/*                                                                   */
/* Specifying END, RETURN, SUBMIT, or EXIT will submit the JCL for   */
/* background JES execution.   CANCEL will terminate the ISPF editor */
/* and any submission to batch of the JCL that was generated by      */
/* ISPF file tailoring.                                              */
/*                                                                   */
/* ----------------------------------------------------------------- */

  if rsub = 'N' then exit 0

  "VGET (ZVERB)"

  If wordpos(zverb,'END RETURN SUBMIT EXIT') > 0
  then
  Do
    Address TSO
    "SUBMIT '"ZTEMPF"'"
    subrc = RC
    Address ISPEXEC
    If subrc = 0 then
    "SETMSG MSG(RROD003)"
    Else
    "SETMSG MSG(RROD005)"
  End
  Else
  Do
    Address ISPEXEC
    "SETMSG MSG(RROD004)"
  End
  Exit RC

/* ----------------------------------------------------------------- */
/*                                                                   */
/* Build_JCL:  A file tailoring subroutine to build JCL              */
/*                                                                   */
/* A return code greater than 0 indicates a critical error when      */
/* processing (tailoring) a file skeleton.  Consult the ISPF         */
/* Services Guide, SC19-3626, for detailed error information.        */
/*                                                                   */
/* ----------------------------------------------------------------- */

Build_JCL:

  "FTINCL RACFREPT"
  ftrc = RC
  If ftrc > 0 then
  Do
    Address ISPEXEC
    "SETMSG MSG(RROD001)"
    Exit ftrc
  End
  Return ftrc
