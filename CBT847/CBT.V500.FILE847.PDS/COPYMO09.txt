*          DATA SET CBT990     AT LEVEL 009 AS OF 03/21/00
   TITLE 'COPYMODS -- TAPE COPY PROGRAM'                                00001
**************************************************************          00002
**************************************************************          00003
**                                                          **          00004
**                                                          **          00005
**  MODULE: COPYMODS.   WRITTEN BY PAUL TOKHEIM, AFAFC.     **          00006
**                                                          **          00007
**                                                          **          00008
**  PURPOSE: COPY ALL FILES OF AN INPUT TAPE TO ONE TO 16   **          00009
**     OUTPUT TAPES.  THE ORIGINAL PURPOSE OF THIS PROGRAM  **          00010
**     IS FOR COPYING THE SHARE MVS MODS TAPE.              **          00011
**                                                          **          00012
**                                                          **          00013
**  METHOD: COPYMODS FIRST DETERMINES WHICH OUTPUT DD'S ARE **          00014
**    PRESENT. THE INPUT TAPE IS READ USING EXCP AND THE    **          00015
**    APPROPRIATE OUTPUT TAPES ARE WRITTEN USING EXCP.      **          00016
**    ANY LABELS ON THE INPUT WILL BE COPIED. ANY LABELS ON **          00017
**    THE OUTPUT TAPES WILL BE DESTROYED.  IF AN ERROR      **          00018
**    OCCURS ON THE INPUT TAPE, THE PROGRAM WILL ABEND WITH **          00019
**    A USER 69 ABEND.  IF AN ERROR OCCURS ON AN OUTPUT     **          00020
**    TAPE, THE PROGRAM WILL QUIT WRITING TO THE TAPE       **          00021
**    WHICH ENCOUNTERED THE ERROR.                          **          00022
**                                                          **          00023
**                                                          **          00023
**************************************************************          00025
**  CHANGE LOG:                                             **          00023
**                                                          **          00023
** LEVEL 003                                                **          00023
**  03/03/00 - ALLOW COPYING OF 64K BLOCKS        SBG 03/00 **          00023
**                                                          **          00023
** LEVEL 004                                                **          00023
**  03/03/00 - CHECK FOR DOUBLE TAPE MARK AFTER   SBG 03/00 **          00023
**             A HEADER LABEL.  THIS MEANS AN SL  SBG 03/00 **          00024
**             FILE WITHOUT ANY DATA IN IT.  AND  SBG 03/00 **          00024
**             IF THIS IS THE CASE, DON'T STOP    SBG 03/00 **          00024
**             COPYING FILES.                     SBG 03/00 **          00024
**                                                          **          00024
** LEVEL 005                                                **          00023
**  03/03/00 - GET AWAY FROM R13 AS BASE, USE     SBG 03/00 **          00023
**             R12,R11.                           SBG 03/00 **          00023
**                                                          **          00023
** LEVEL 006                                                **          00023
**  03/12/00 - ENLARGED THE PROGRAM TO ALLOW UP   SBG 03/00 **          00023
**             TO 16 OUTPUT TAPES FROM OUT1 TO    SBG 03/00 **          00023
**             OUT16.                             SBG 03/00 **          00023
**                                                          **          00023
** LEVEL 007 AND 008                                        **          00023
**  03/14/00 - CLEANED UP SPELLING ON ERROR       SBG 03/00 **          00023
**             MESSAGES AND FIXED DOCUMENTATION.  SBG 03/00 **          00023
**             ADDED EYECATCHERS COSMETICALLY.    SBG 03/00 **          00023
**                                                          **          00023
** LEVEL 009                                                **          00023
**  03/21/00 - STOP AFTER INITTED MVS TAPE        SBG 03/00 **          00023
**                                                          **          00023
**************************************************************          00025
**************************************************************          00026
         EJECT                                                          00027
         MACRO                                                          00028
&TAG     PRIME &REG,&AREA,&POOL,&LEN,&SAVE=,&R=,&RMK=                   00029
         GBLC  &CR0,&CR1,&CR12,&CR13,&CR14,&CR15                        00030
         LCLC  &C1,&C2                                                  00031
*******************   PROGRAM INITIATION ROUTINE   ******************** 00032
         AIF   ('&RMK' NE 'YES').PR10  JUMP IF REMARKS NOT DESIRED      00033
* THIS MACRO WILL CREATE A SAVEAREA, SAVE THE REGISTERS, AND PERFORM  * 00034
* THE NECESSARY ENTRY LINKAGE.  IT HAS THE FOLLOWING OPTIONS:         * 00035
*                                                                     * 00036
* (TAG)  PRIME (REG)(,AREA)(,POOL)(,LEN)(,SAVE=)(,R=)(,RMK=)          * 00037
*                                                                     * 00038
*    TAG = OPTIONAL - SYMBOLIC NAME IDENTIFIABLE TO 1ST GENERATED INST* 00039
*  PRIME = REQUIRED - OPERATION CODE.                                 * 00040
*    REG = OPTIONAL - INDICATE DESIRED BASE REGISTER OR REGISTERS.    * 00041
*          IF OMITTED REGISTER 13 ASSUMED.  IF MULTIPLE BASES DESIRED * 00042
*          SEPARATE BY COMMAS AND ENCLOSE IN PARENTHESIS (13,11,12).  * 00043
*          IF REG 13 IS SPECIFIED IT MUST BE FIRST.                   * 00044
*          DO NOT USE REG 13 AS A BASE IF RE-ENTERABLE CODING DESIRED.* 00045
*   AREA = OPTIONAL - SYMBOLIC NAME WHICH IF SPECIFIED WILL CAUSE A   * 00046
*          SAVEAREA TO BE GENERATED AND TAGGED WITH THIS NAME. IF REG * 00047
*          13 IS USED AS A BASE A SAVEAREA IS ALWAYS GENERATED AND    * 00048
*          WILL BE GIVEN THIS NAME. THIS AREA WILL NOT BE RELEASED IN * 00049
*          THE 'TERME' MACRO.  THIS IS DESIRABLE IF THE ROUTINE USING * 00050
*          THE 'PRIME & TERME' MACROS IS ENTERED SEVERAL TIMES. IF REG* 00051
*          13 IS NOT USED AS A BASE AND THIS OPTION IS NOT SPECIFIED  * 00052
*          PRIME WILL EXECUTE A 'GETMAIN' TO OBTAIN A SAVEAREA AND    * 00053
*          TERME WILL EXECUTE A 'FREEMAIN' TO RELEASE THE AREA.       * 00054
*          DO NOT SPECIFY THIS OPTION IF RE-ENTERABLE CODING DESIRED. * 00055
*   POOL = OPTIONAL - SPECIFIES THE SUBPOOL FROM WHICH THE SAVEAREA IS* 00056
*          TO BE OBTAINED. IF OMITTED SUBPOOL ZERO IS ASSUMED. THIS   * 00057
*          OPERAND IS IGNORED IF REG 13 IS USED AS A BASE OR IF THE   * 00058
*          AREA OR SAVE OPERANDS ARE SPECIFIED. IF THIS OPTION IS USE * 00059
*          THE SAME SUBPOOL MUST BE SPECIFIED IN THE 'TERME' MACRO.   * 00060
*    LEN = OPTIONAL - MAY BE USED TO SPECIFY THE AMOUNT OF STORAGE TO * 00061
*          BE OBTAINED FOR THE SAVEAREA.  THE OPERAND MUST BE NUMERIC * 00062
*          AND MUST NOT BE LESS THAN 72. THE FIRST 72 BYTES OF THE    * 00063
*          AREA ARE USED BY PRIME TO STORE THE REGISTERS. THE USER MAY* 00064
*          USE THE EXTRA AREA THAT WAS OBTAIN BEYOND 72 PASSED REG 13 * 00065
*          FOR A WORK AREA. THIS OPTION IS USEFUL FOR RE-ENTERABLE    * 00066
*          CODE AND IS IGNORED IF REG 13 IS USED AS A BASE, OR IF THE * 00067
*          AREA OR SAVE OPERANDS ARE SPECIFIED.                       * 00068
*   SAVE = OPTIONAL - SPECIFIES THE NAME OF AN 18 FULLWORD AREA WHERE * 00069
*          THE REGISTERS ARE TO BE SAVED.                             * 00070
*      R = OPTIONAL (KEY WORD) - IF (R=YES) IS SPECIFIED R0 THRU R15  * 00071
*          WILL BE EQUATED TO 0 THRU 15 TO SYMBOLICALLY IDENTIFY REGS.* 00072
*    RMK = OPTIONAL (KEYWORD) - IF (RMK=YES) IS SPECIFIED THIS REMARK * 00073
*          BLOCK WILL BE LISTED IN THE MACRO EXPANSION.               * 00074
*********************************************************************** 00075
.PR10    AIF   ('&CR0' NE '').PR40     JUMP IF REGS ALREADY EQUATED     00076
         AIF   ('&R' EQ '').PR20                                        00077
R0       EQU   0                                                        00078
R1       EQU   1                                                        00079
R2       EQU   2                                                        00080
R3       EQU   3                                                        00081
R4       EQU   4                                                        00082
R5       EQU   5                                                        00083
R6       EQU   6                                                        00084
R7       EQU   7                                                        00085
R8       EQU   8                                                        00086
R9       EQU   9                                                        00087
R10      EQU   10                                                       00088
R11      EQU   11                                                       00089
R12      EQU   12                                                       00090
R13      EQU   13                                                       00091
R14      EQU   14                                                       00092
R15      EQU   15                                                       00093
.PR20    ANOP                                                           00094
         AIF   ('&R' EQ '').PR30       JUMP IF NO EQUATE                00095
&CR0     SETC  'R0'                                                     00096
&CR1     SETC  'R1'                                                     00097
&CR12    SETC  'R12'                                                    00098
&CR13    SETC  'R13'                                                    00099
&CR14    SETC  'R14'                                                    00100
&CR15    SETC  'R15'                                                    00101
         AGO   .PR40                                                    00102
.PR30    ANOP                                                           00103
&CR0     SETC  '0'                                                      00104
&CR1     SETC  '1'                                                      00105
&CR12    SETC  '12'                                                     00106
&CR13    SETC  '13'                                                     00107
&CR14    SETC  '14'                                                     00108
&CR15    SETC  '15'                                                     00109
.PR40    DS    0F -------------------- VER.1 REL.6 -------------------- 00110
         AIF   ('&REG' NE '').PR50     JUMP IF REG PRESENT              00111
         USING *,&CR13                                                  00112
         AGO   .PR100                                                   00113
.PR50    AIF   (N'&REG GT 1).PR60           JUMP IF MORE THAN 1 REG     00114
         USING *,&REG(1)                                                00115
         AGO   .PR100                                                   00116
.PR60    AIF   (N'&REG GT 2).PR70           JUMP IF MORE THAN 2 REGS    00117
         USING *,&REG(1),&REG(2)                                        00118
         AGO   .PR100                                                   00119
.PR70    AIF   (N'&REG GT 3).PR80           JUMP IF MORE THAN 3 REGS    00120
         USING *,&REG(1),&REG(2),&REG(3)                                00121
         AGO   .PR100                                                   00122
.PR80    ANOP                                                           00123
         USING *,&REG(1),&REG(2),&REG(3),&REG(4)                        00124
         AIF   (N'&REG LT 5).PR100          JUMP IF LESS THAN 5 REGS    00125
      MNOTE 8,'** MAXIMUM NUMBER OF REGISTERS IS 4 - OTHER IGNORED **'  00126
.PR100   AIF   ('&REG' EQ '').PR110    JUMP IF REG NOT PRESENT          00127
         AIF   ('&REG(1)' EQ 'R13' OR '&REG(1)' EQ '13').PR110          00128
         AGO   .PR120                                                   00129
.PR110   AIF   ('&AREA' EQ '').PR115                                    00130
&AREA    DS    0F                                                       00131
.PR115   ANOP                                                           00132
&TAG     BAL   &CR15,76(&CR15)                                          00133
         DC    17F'0',H'4',H'4096'                                      00134
         SH    &CR15,68(&CR15)                                          00135
         STM   &CR14,&CR12,12(&CR13)                                    00136
         LR    &CR1,&CR15                                               00137
         AIF   (N'&REG LT 2).PR170                                      00138
         LR    &REG(2),&CR1                                             00139
         LA    &CR15,74(&CR1)                                           00140
         AGO   .PR150                                                   00141
.PR120   ANOP                                                           00142
&TAG     BALR  &CR15,0                                                  00143
         BCTR  &CR15,0                                                  00144
         BCTR  &CR15,0                                                  00145
         STM   &CR14,&CR12,12(&CR13)                                    00146
         LR    &REG(1),&CR15                                            00147
         AIF   ('&AREA' EQ '').PR130   JUMP IF AREA NOT DESIRED         00148
         BAL   &CR1,*+78                                                00149
&AREA    DC    18F'0',H'4096'                                           00150
         LA    &CR15,72(&CR1)                                           00151
         AGO   .PR140                                                   00152
.PR130   AIF   (N'&REG LT 2).PR160                                      00153
         BAL   &CR15,*+6                                                00154
         DC    H'4096'                                                  00155
.PR140   AIF   (N'&REG EQ 1).PR160     JUMP IF 1 REG                    00156
         LR    &REG(2),&REG(1)                                          00157
.PR150   AH    &REG(2),0(&CR15)                                         00158
         AIF   (N'&REG EQ 2).PR160     JUMP IF 2 REGS                   00159
         LR    &REG(3),&REG(2)                                          00160
         AH    &REG(3),0(&CR15)                                         00161
         AIF   (N'&REG EQ 3).PR160     JUMP IF 3 REGS                   00162
         LR    &REG(4),&REG(3)                                          00163
         AH    &REG(4),0(&CR15)                                         00164
.PR160   AIF   ('&REG(1)' EQ 'R13' OR '&REG(1)' EQ '13').PR170          00165
         AIF   ('&AREA' NE '').PR170   JUMP IF AREA                     00166
         AIF   ('&SAVE' EQ '').PR180   JUMP IF NO SAVE                  00167
         LA    &CR1,&SAVE                                               00168
.PR170   ST    &CR1,8(&CR13)                                            00169
         MVI   8(&CR13),X'A3'                                           00170
         AGO   .PR240                                                   00171
.PR180   AIF   ('&POOL' EQ '').PR190   JUMP IF NO POOL                  00172
&C1      SETC  '&POOL'                                                  00173
         AGO   .PR200                                                   00174
.PR190   ANOP                                                           00175
&C1      SETC  '0'                                                      00176
.PR200   AIF   ('&LEN' EQ '').PR220                                     00177
         AIF   ('&LEN' LT '72').PR210                                   00178
&C2      SETC  '&LEN'                                                   00179
         AGO   .PR230                                                   00180
.PR210   MNOTE 4,'SAVEAREA LENGTH SPECIFIED LESS THAN 72 - 72 ASSUMED'  00181
.PR220   ANOP                                                           00182
&C2      SETC  '72'                                                     00183
.PR230   GETMAIN R,LV=&C2,SP=&C1                                        00184
         XC    0(72,&CR1),0(&CR1)                                       00185
         ST    &CR1,8(&CR13)                                            00186
         MVI   8(&CR13),X'DC'                                           00187
.PR240   ST    &CR13,4(&CR1)                                            00188
         LM    &CR0,&CR1,20(&CR13)                                      00189
         L     &CR13,8(&CR13)                                           00190
         MEND                                                           00191
         MACRO                                                          00192
&TAG     TERME &OP,&POOL,&LEN,&RMK=                                     00193
         GBLC  &CR0,&CR1,&CR12,&CR13,&CR14,&CR15                        00194
         LCLA  &A                                                       00195
         LCLC  &C1,&C2                                                  00196
*******************   PROGRAM TERMINATION ROUTINE   ******************* 00197
         AIF   ('&RMK' NE 'YES').TR1   JUMP IF REMARKS NOT DESIRED      00198
* THIS MACRO WILL RESTORE THE REGISTERS, RELEASE THE SAVEAREA IF      * 00199
* OBTAIN BY 'PRIME' MACRO, AND RETURN TO NEXT HIGHER LEVEL.           * 00200
* UPON RETURN REGS. 0 & 1 WILL REMAIN THE SAME AS BEFORE THE TERME.   * 00201
*                                                                     * 00202
* (TAG)  TERME  (OP)(,POOL)(,LEN)(,RMK=NO)                            * 00203
*                                                                     * 00204
*    TAG = OPTIONAL - SYMBOLIC NAME IDENTIFIABLE TO 1ST GENERATED INST* 00205
*  TERME = REQUIRED - OPERATION CODE.                                 * 00206
*     OP = OPTIONAL - IF 'XCTL' IS SPECIFIED INSTRUCTIONS WILL BE     * 00207
*          GENERATED WHICH WILL ALLOW THE 'XCTL' MACRO TO BE ISSUED   * 00208
*          IMMEDIATLY FOLLOWING THE TERME MACRO. CONTROL WILL NOT BE  * 00209
*          GIVEN TO THE NEXT HIGHER LEVEL.                            * 00210
*   POOL = OPTIONAL - IT MUST BE PRESENT IF THE POOL OPTION IN 'PRIME'* 00211
*          WAS SPECIFIED, AND MUST INDICATE THE SAME SUBPOOL.         * 00212
*    LEN = OPTIONAL - IT MUST BE PRESENT IF THE LEN OPTION IN 'PRIME' * 00213
*          WAS SPECIFIED, AND MUST INDICATE THE SAME LENGTH.          * 00214
*    RMK = OPTIONAL (KEYWORD) - IF (RMK=YES) IS SPECIFIED THIS REMARK * 00215
*          BLOCK WILL BE LISTED IN THE MACRO EXPANSION.               * 00216
*********************************************************************** 00217
.TR1     ANOP                                                           00218
         AIF   ('&CR0' NE '').TR2      JUMP IF REG EQU IN PRIME         00219
&CR0     SETC  '0'                                                      00220
&CR1     SETC  '1'                                                      00221
&CR12    SETC  '12'                                                     00222
&CR13    SETC  '13'                                                     00223
&CR14    SETC  '14'                                                     00224
&CR15    SETC  '15'                                                     00225
.TR2     ANOP                                                           00226
&A       SETA  &SYSNDX                                                  00227
&TAG     L     &CR14,4(&CR13)                                           00228
         STM   &CR15,&CR1,16(&CR14)                                     00229
         XI    8(&CR14),X'DC'                                           00230
         BC    7,IXI5&A                                                 00231
         L     &CR1,8(&CR14)                                            00232
         AIF   ('&POOL' EQ '').TR10                                     00233
&C1      SETC  '&POOL'                                                  00234
         AGO   .TR20                                                    00235
.TR10    ANOP                                                           00236
&C1      SETC  '0'                                                      00237
.TR20    AIF   ('&LEN' EQ '').TR40                                      00238
         AIF   ('&LEN' LT '72').TR30                                    00239
&C2      SETC  '&LEN'                                                   00240
         AGO   .TR50                                                    00241
.TR30    MNOTE 4,'SAVEAREA LENGTH SPECIFIED LESS THAN 72 - ASSUME 72'   00242
.TR40    ANOP                                                           00243
&C2      SETC  '72'                                                     00244
.TR50    ANOP                                                           00245
         FREEMAIN R,LV=&C2,A=(1),SP=&C1                                 00246
IXI5&A   LR    &CR13,&CR14                                              00247
         L     &CR14,16(&CR13)                                          00248
         SPM   &CR14                                                    00249
         LM    &CR14,&CR12,12(&CR13)                                    00250
         XC    8(4,&CR13),8(&CR13) ----- VER.1 REL.6 -----              00251
         AIF   ('&OP' EQ 'XCTL').C                                      00252
         BR    &CR14                                                    00253
         MEXIT                                                          00254
.C       ANOP                                                           00255
         BALR  &CR15,0                                                  00256
         USING *,&CR15                                                  00257
         MEND                                                           00258
         EJECT                                                          00259
COPYMODS CSECT                                                          00260
         USING *,R15                                          SBG 03/00
         B     EYECATCH                                       SBG 03/00
         DC    C'--- '            MAKE EYECATCHER VISIBLE     SBG 03/00
         DC    C'COPYMODS - 16 OUTPUT TAPES VERSION - '       SBG 03/00
         DC    C'&SYSDATE',C'  ',C'&SYSTIME ',C'---'          SBG 03/00
EYECATCH DS    0D             ENSURE ALIGNMENT                SBG 03/00
         DROP  R15                                            SBG 03/00
         PRIME (R12,R11),R=YES,RMK=NO                         SBG 03/00 00261
         SR    R9,R9          ZERO FOR BLOCK COUNT                      00262
         LA    R8,1           INITIALIZE FOR FILE NUMBER                00263
         MVI   LBLFLAG,X'00'  INITIALIZE LABEL FLAG           SBG 03/00
         SPACE 3                                                        00264
*                                                                       00265
*  DETERMINE WHICH OUTPUT DD'S ARE PRESENT                              00266
*                                                                       00267
         L     R2,16          R2=CVT                                    00268
         L     R2,0(,R2)      R2=TCB WORDS                              00269
         L     R2,4(,R2)      R2=MY TCB                                 00270
         L     R2,12(,R2)     R2=TIOT                                   00271
         LA    R2,24(,R2)     R2=FIRST DD ENTRY                         00272
TIOTLOOP CLC   0(4,R2),=XL4'00' END OF TIOT?                            00273
         BE    OPENS          YES                                       00274
         CLC   4(8,R2),OUT1+40  OUTPUT FILE NUMBER 1?                   00275
         BNE   TIOT1          NO                                        00276
         OI    FLAG1,OP1      SHOW IT                                   00277
         B     TIOTNEXT       CONTINUE                                  00278
TIOT1    CLC   4(8,R2),OUT2+40  OUTPUT FILE NUMBER 2?                   00279
         BNE   TIOT2          NO                                        00280
         OI    FLAG1,OP2      SHOW IT                                   00281
         B     TIOTNEXT       CONTINUE                                  00282
TIOT2    CLC   4(8,R2),OUT3+40  OUTPUT FILE NUMBER 3?                   00283
         BNE   TIOT3          NO                                        00284
         OI    FLAG1,OP3      SHOW IT                                   00285
         B     TIOTNEXT       CONTINUE                                  00286
TIOT3    CLC   4(8,R2),OUT4+40  OUTPUT FILE NUMBER 4?                   00287
         BNE   TIOT4          NO                                        00288
         OI    FLAG1,OP4      SHOW IT                                   00289
         B     TIOTNEXT       CONTINUE                                  00290
TIOT4    CLC   4(8,R2),OUT5+40  OUTPUT FILE NUMBER 5?                   00291
         BNE   TIOT5          NO                                        00292
         OI    FLAG1,OP5      SHOW IT                                   00293
         B     TIOTNEXT       CONTINUE                                  00294
TIOT5    CLC   4(8,R2),OUT6+40  OUTPUT FILE NUMBER 6?                   00295
         BNE   TIOT6          NO                                        00296
         OI    FLAG1,OP6      SHOW IT                                   00297
         B     TIOTNEXT       CONTINUE                                  00298
TIOT6    CLC   4(8,R2),OUT7+40  OUTPUT FILE NUMBER 7?                   00299
         BNE   TIOT7          NO                                        00300
         OI    FLAG1,OP7      SHOW IT                                   00301
         B     TIOTNEXT       CONTINUE                                  00302
TIOT7    CLC   4(8,R2),OUT8+40  OUTPUT FILE NUMBER 8?                   00303
         BNE   TIOT8          NO                                        00304
         OI    FLAG1,OP8      SHOW IT                                   00305
         B     TIOTNEXT       CONTINUE                                  00306
TIOT8    CLC   4(8,R2),OUT9+40  OUTPUT FILE NUMBER 9?                   00307
         BNE   TIOT9          NO                                        00308
         OI    FLAG2,OP9      SHOW IT                                   00309
         B     TIOTNEXT       CONTINUE                                  00310
TIOT9    CLC   4(8,R2),OUT10+40  OUTPUT FILE NUMBER 10?                 00311
         BNE   TIOT10         NO                                        00312
         OI    FLAG2,OP10     SHOW IT                                   00313
         B     TIOTNEXT       CONTINUE                                  00314
TIOT10   CLC   4(8,R2),OUT11+40  OUTPUT FILE NUMBER 11?                 00311
         BNE   TIOT11         NO                                        00312
         OI    FLAG2,OP11     SHOW IT                                   00313
         B     TIOTNEXT       CONTINUE                                  00314
TIOT11   CLC   4(8,R2),OUT12+40  OUTPUT FILE NUMBER 12?                 00311
         BNE   TIOT12         NO                                        00312
         OI    FLAG2,OP12     SHOW IT                                   00313
         B     TIOTNEXT       CONTINUE                                  00314
TIOT12   CLC   4(8,R2),OUT13+40  OUTPUT FILE NUMBER 13?                 00311
         BNE   TIOT13         NO                                        00312
         OI    FLAG2,OP13     SHOW IT                                   00313
         B     TIOTNEXT       CONTINUE                                  00314
TIOT13   CLC   4(8,R2),OUT14+40  OUTPUT FILE NUMBER 14?                 00311
         BNE   TIOT14         NO                                        00312
         OI    FLAG2,OP14     SHOW IT                                   00313
         B     TIOTNEXT       CONTINUE                                  00314
TIOT14   CLC   4(8,R2),OUT15+40  OUTPUT FILE NUMBER 15?                 00311
         BNE   TIOT15         NO                                        00312
         OI    FLAG2,OP15     SHOW IT                                   00313
         B     TIOTNEXT       CONTINUE                                  00314
TIOT15   CLC   4(8,R2),OUT16+40  OUTPUT FILE NUMBER 16?                 00311
         BNE   TIOTNEXT       NO                                        00312
         OI    FLAG2,OP16     SHOW IT                                   00313
         B     TIOTNEXT       CONTINUE                                  00314
TIOTNEXT SR    R3,R3                                                    00315
         IC    R3,0(,R2)      R3=LENGTH OF DD ENTRY                     00316
         AR    R2,R3          R2=NEXT DD ENTRY                          00317
         B     TIOTLOOP       GO CHECK IT                               00318
         SPACE 3                                                        00319
*                                                                       00320
*  OPEN ALL FILES                                                       00321
*                                                                       00322
*  ---------------------------------------------------------  SBG 03/00
OPENS    OPEN  (IN,,SYSPRINT,OUTPUT)                                    00323
         MVC   FLAG3(2),FLAG1 SAVE INDICATION OF WHICH FILES ARE HERE   00324
*                             THIS ALSO MOVES FLAG2 TO FLAG4  SBG 03/00
*  ---------------------------------------------------------  SBG 03/00
         TM    FLAG1,OP1      OUTPUT 1 PRESENT?                         00325
         BZ    OPEN1          NO                                        00326
         OPEN  (OUT1,OUTPUT)                                            00327
OPEN1    TM    FLAG1,OP2      OUTPUT 2 PRESENT?                         00328
         BZ    OPEN2          NO                                        00329
         OPEN  (OUT2,OUTPUT)                                            00330
OPEN2    TM    FLAG1,OP3      OUTPUT 3 PRESENT?                         00331
         BZ    OPEN3          NO                                        00332
         OPEN  (OUT3,OUTPUT)                                            00333
OPEN3    TM    FLAG1,OP4      OUTPUT 4 PRESENT?                         00334
         BZ    OPEN4          NO                                        00335
         OPEN  (OUT4,OUTPUT)                                            00336
OPEN4    TM    FLAG1,OP5      OUTPUT 5 PRESENT?                         00337
         BZ    OPEN5          NO                                        00338
         OPEN  (OUT5,OUTPUT)                                            00339
OPEN5    TM    FLAG1,OP6      OUTPUT 6 PRESENT?                         00340
         BZ    OPEN6          NO                                        00341
         OPEN  (OUT6,OUTPUT)                                            00342
OPEN6    TM    FLAG1,OP7      OUTPUT 7 PRESENT?                         00343
         BZ    OPEN7          NO                                        00344
         OPEN  (OUT7,OUTPUT)                                            00345
OPEN7    TM    FLAG1,OP8      OUTPUT 8 PRESENT?                         00346
         BZ    OPEN8          NO                                        00347
         OPEN  (OUT8,OUTPUT)                                            00348
OPEN8    TM    FLAG2,OP9      OUTPUT 9 PRESENT?                         00349
         BZ    OPEN9          NO                                        00350
         OPEN  (OUT9,OUTPUT)                                            00351
OPEN9    TM    FLAG2,OP10     OUTPUT 10 PRESENT?                        00352
         BZ    OPEN10         NO                                        00353
         OPEN  (OUT10,OUTPUT)                                           00354
OPEN10   TM    FLAG2,OP11     OUTPUT 11 PRESENT?                        00352
         BZ    OPEN11         NO                                        00353
         OPEN  (OUT11,OUTPUT)                                           00354
OPEN11   TM    FLAG2,OP12     OUTPUT 12 PRESENT?                        00352
         BZ    OPEN12         NO                                        00353
         OPEN  (OUT12,OUTPUT)                                           00354
OPEN12   TM    FLAG2,OP13     OUTPUT 13 PRESENT?                        00352
         BZ    OPEN13         NO                                        00353
         OPEN  (OUT13,OUTPUT)                                           00354
OPEN13   TM    FLAG2,OP14     OUTPUT 14 PRESENT?                        00352
         BZ    OPEN14         NO                                        00353
         OPEN  (OUT14,OUTPUT)                                           00354
OPEN14   TM    FLAG2,OP15     OUTPUT 15 PRESENT?                        00352
         BZ    OPEN15         NO                                        00353
         OPEN  (OUT15,OUTPUT)                                           00354
OPEN15   TM    FLAG2,OP16     OUTPUT 16 PRESENT?                        00352
         BZ    READIN         NO                                        00353
         OPEN  (OUT16,OUTPUT)                                           00354
         SPACE 3                                                        00355
*                                                                       00356
*  READ INPUT                                                           00357
*                                                                       00358
READIN   MVI   INECB,X'00'    INITIALIZE ECB                            00359
         EXCP  INIOB          READ                                      00360
         WAIT  ECB=INECB      WAIT FOR READ                             00361
         TM    INCSW+4,X'01'  WAS TM READ?                              00362
         BO    READIN1        YES                                       00363
         CLI   INECB,X'7F'    DID READ COMPLETE NORMALLY?               00364
         BNE   INERR          NO -- ERROR                               00365
         MVI   OUTCCW,X'01'   SET OUTPUT CCW TO WRITE                   00366
         LH    R2,INCCW+6     R2=READ LENGTH                            00367
         LH    R3,INCSW+6     R3=RESIDUAL BYTE COUNT                    00368
         SR    R2,R3          R2=ACTUAL LENGTH READ                     00369
*  --- BELOW ---  CODE TO DETECT SL BEFORE 2 TAPE MARKS.      SBG 03/00
         C     R2,=F'80'      DID WE READ 80-BYTE BLOCK LAST? SBG 03/00
         BNE   READGO1        NO. SKIP THIS PROCESSING.       SBG 03/00
         CLC   INAREA(4),=C'VOL1'  LAST RECORD A VOL1 ?       SBG 03/00
         BNE   READJMPA       NO. TRY IF HDR1                 SBG 03/00
         OI    LBLFLAG,X'04'  YES. FLAG VOL1 SEEN.            SBG 03/00
         B     READGO1        GO ON WITH PROCESSING           SBG 03/00
READJMPA CLC   INAREA(4),=C'HDR1'  LAST RECORD A HDR1 ?       SBG 03/00
         BNE   READJMP0       NO. TRY IF HDR2                 SBG 03/00
         OI    LBLFLAG,X'01'  YES. FLAG HDR1 SEEN.            SBG 03/00
         CLC   INAREA+4(23),ZERO23   WAS TAPE JUST INITTED?   SBG 03/00
         BNE   READGO1        NO. THIS IS A HDR1 FOR DATASET  SBG 03/00
         OI    LBLFLAG,X'08'  YES. ZERO DSN AND VOLSER HERE   SBG 03/00
         B     READGO1        GO ON WITH PROCESSING           SBG 03/00
READJMP0 CLC   INAREA(4),=C'HDR2'  LAST RECORD A HDR2 ?       SBG 03/00
         BNE   READJMP1       NO. SEE IF EOF1 OR EOF2.        SBG 03/00
         OI    LBLFLAG,X'02'  YES. FLAG HDR2 SEEN.            SBG 03/00
         B     READGO1        GO ON WITH PROCESSING           SBG 03/00
READJMP1 CLC   INAREA(3),=C'EOF'   LAST RECORD EOF1 OR EOF2?  SBG 03/00
         BNE   READJMP2       NO. CHECK IF EOV.               SBG 03/00
         MVI   LBLFLAG,X'00'  EOF SEEN. CLEAR LABEL FLAGS,    SBG 03/00
*                       BECAUSE 2 TM'S ARE FINE AFTER EOF'S,  SBG 03/00
*                       AND THEY REALLY INDICATE AN END.      SBG 03/00
         B     READGO1        GO ON WITH PROCESSING           SBG 03/00
READJMP2 CLC   INAREA(3),=C'EOV'   LAST RECORD AN EOV ?       SBG 03/00
         BNE   READJMP3       NO. GO ON WITH PROCESSING       SBG 03/00
         MVI   LBLFLAG,X'00'  EOV SEEN. CLEAR LABEL FLAGS.    SBG 03/00
         B     READGO1        GO ON WITH PROCESSING           SBG 03/00
READJMP3 DS    0H                                             SBG 03/00
*  --- ABOVE ---  CODE TO DETECT SL BEFORE 2 TAPE MARKS.      SBG 03/00
READGO1  STH   R2,OUTCCW+6    STORE FOR WRITE                 SBG 03/00 00370
         LA    R9,1(,R9)      ADD 1 TO COUNT OF DATA BLOCKS             00371
         B     WRITE1         CONTINUE                                  00372
READIN1  CVD   R8,DWORD       CONVERT FILE NUMBER TO DECIMAL            00373
         OI    DWORD+7,X'0F'  MAKE SIGN READABLE                        00374
         UNPK  FILEDONE+6(5),DWORD+5(3)  UNPACK FILE NUMBER             00375
         CVD   R9,DWORD       CONVERT BLOCK COUNT TO DECIMAL            00376
         OI    DWORD+7,X'0F'  MAKE SIGN READABLE                        00377
         UNPK  FILEDONE+21(7),DWORD+4(4)  UNPACK BLOCK COUNT            00378
         PUT   SYSPRINT,FILEDONE  WRITE IT                              00379
         SR    R9,R9          ZERO BLOCK COUNT FOR NEW FILE             00380
         LA    R8,1(,R8)      ADD 1 TO FILE NUMBER                      00381
         CLI   OUTCCW,X'1F'   2ND TM IN A ROW?                          00382
         BE    TESTEND        YES. TEST IF RIGHT AFTER LABELS SBG 03/00 00383
NOLBL    MVI   OUTCCW,X'1F'   SET OUTPUT CCW TO WRITE TM      SBG 03/00 00384
         B     WRITE1         GO WRITE TM TO OUTPUTS                    00385
*  --- BELOW ---  CODE TO GO ON IF LABEL BEFORE 2 TAPE MARKS. SBG 03/00
TESTEND  TM    LBLFLAG,X'03'  TEST FOR PREV HDR1 OR HDR2      SBG 03/00
         BZ    READIN2        NOT EITHER. END AS USUAL.       SBG 03/00
*  AT THIS POINT, THERE'S A HDR1 OR A HDR2 THAT'S BEEN SEEN.  SBG 03/00
         TM    LBLFLAG,X'02'  TEST IF HDR2 HAS BEEN SEEN      SBG 03/00
         BZ    TESTVSE        NO, SEE IF INITTED              SBG 03/00
*  AT THIS POINT, THERE'S A HDR2, SO NOT INITTED AND NOT VSE. SBG 03/00
TESTGOON MVI   LBLFLAG,X'00'  CLEAR LABEL FLAGS FOR NEXT READ SBG 03/00
         B     NOLBL          GO ON WITHOUT SETTING EOV FLAG  SBG 03/00
*  AT THIS POINT, THERE'S A HDR1, SO WE TEST FOR INITTED.     SBG 03/00
TESTVSE  TM    LBLFLAG,X'0C'  VOL1 AND INITTED HDR1?          SBG 03/00
         BNO   TESTGOON       NOT INITTED, SO JUST KEEP GOING SBG 03/00
*  --- ABOVE ---  CODE TO GO ON IF LABEL BEFORE 2 TAPE MARKS. SBG 03/00
READIN2  OI    FLAG2A,EOV     INDICATE EOV ON INPUT                     00386
         SPACE 3                                                        00387
*                                                                       00388
*  WRITE TO OUTPUT TAPES                                                00389
*                                                                       00390
WRITE1   TM    FLAG1,OP1      IS THIS OUTPUT PRESENT?                   00391
         BZ    WRITE2         NO                                        00392
         LA    R2,OUT1        R2=ADDRESS OF CURRENT DCB                 00393
         ST    R2,OUTDCBPT    PUT IT IN IOB                             00394
         MVI   OUTECB,X'00'   INITIALIZE ECB                            00395
         EXCP  OUTIOB         WRITE IT                                  00396
         WAIT  ECB=OUTECB     WAIT FOR IT                               00397
         CLI   OUTECB,X'7F'   WRITE COMPLETE NORMALLY?                  00398
         BNE   OUTERR         NO - ERROR                                00399
WRITE2   TM    FLAG1,OP2      IS THIS OUTPUT PRESENT?                   00400
         BZ    WRITE3         NO                                        00401
         LA    R2,OUT2        R2=ADDRESS OF CURRENT DCB                 00402
         ST    R2,OUTDCBPT    PUT IT IN IOB                             00403
         MVI   OUTECB,X'00'   INITIALIZE ECB                            00404
         EXCP  OUTIOB         WRITE IT                                  00405
         WAIT  ECB=OUTECB     WAIT FOR IT                               00406
         CLI   OUTECB,X'7F'   WRITE COMPLETE NORMALLY?                  00407
         BNE   OUTERR         NO - ERROR                                00408
WRITE3   TM    FLAG1,OP3      IS THIS OUTPUT PRESENT?                   00409
         BZ    WRITE4         NO                                        00410
         LA    R2,OUT3        R2=ADDRESS OF CURRENT DCB                 00411
         ST    R2,OUTDCBPT    PUT IT IN IOB                             00412
         MVI   OUTECB,X'00'   INITIALIZE ECB                            00413
         EXCP  OUTIOB         WRITE IT                                  00414
         WAIT  ECB=OUTECB     WAIT FOR IT                               00415
         CLI   OUTECB,X'7F'   WRITE COMPLETE NORMALLY?                  00416
         BNE   OUTERR         NO - ERROR                                00417
WRITE4   TM    FLAG1,OP4      IS THIS OUTPUT PRESENT?                   00418
         BZ    WRITE5         NO                                        00419
         LA    R2,OUT4        R2=ADDRESS OF CURRENT DCB                 00420
         ST    R2,OUTDCBPT    PUT IT IN IOB                             00421
         MVI   OUTECB,X'00'   INITIALIZE ECB                            00422
         EXCP  OUTIOB         WRITE IT                                  00423
         WAIT  ECB=OUTECB     WAIT FOR IT                               00424
         CLI   OUTECB,X'7F'   WRITE COMPLETE NORMALLY?                  00425
         BNE   OUTERR         NO - ERROR                                00426
WRITE5   TM    FLAG1,OP5      IS THIS OUTPUT PRESENT?                   00427
         BZ    WRITE6         NO                                        00428
         LA    R2,OUT5        R2=ADDRESS OF CURRENT DCB                 00429
         ST    R2,OUTDCBPT    PUT IT IN IOB                             00430
         MVI   OUTECB,X'00'   INITIALIZE ECB                            00431
         EXCP  OUTIOB         WRITE IT                                  00432
         WAIT  ECB=OUTECB     WAIT FOR IT                               00433
         CLI   OUTECB,X'7F'   WRITE COMPLETE NORMALLY?                  00434
         BNE   OUTERR         NO - ERROR                                00435
WRITE6   TM    FLAG1,OP6      IS THIS OUTPUT PRESENT?                   00436
         BZ    WRITE7         NO                                        00437
         LA    R2,OUT6        R2=ADDRESS OF CURRENT DCB                 00438
         ST    R2,OUTDCBPT    PUT IT IN IOB                             00439
         MVI   OUTECB,X'00'   INITIALIZE ECB                            00440
         EXCP  OUTIOB         WRITE IT                                  00441
         WAIT  ECB=OUTECB     WAIT FOR IT                               00442
         CLI   OUTECB,X'7F'   WRITE COMPLETE NORMALLY?                  00443
         BNE   OUTERR         NO - ERROR                                00444
WRITE7   TM    FLAG1,OP7      IS THIS OUTPUT PRESENT?                   00445
         BZ    WRITE8         NO                                        00446
         LA    R2,OUT7        R2=ADDRESS OF CURRENT DCB                 00447
         ST    R2,OUTDCBPT    PUT IT IN IOB                             00448
         MVI   OUTECB,X'00'   INITIALIZE ECB                            00449
         EXCP  OUTIOB         WRITE IT                                  00450
         WAIT  ECB=OUTECB     WAIT FOR IT                               00451
         CLI   OUTECB,X'7F'   WRITE COMPLETE NORMALLY?                  00452
         BNE   OUTERR         NO - ERROR                                00453
WRITE8   TM    FLAG1,OP8      IS THIS OUTPUT PRESENT?                   00454
         BZ    WRITE9         NO                                        00455
         LA    R2,OUT8        R2=ADDRESS OF CURRENT DCB                 00456
         ST    R2,OUTDCBPT    PUT IT IN IOB                             00457
         MVI   OUTECB,X'00'   INITIALIZE ECB                            00458
         EXCP  OUTIOB         WRITE IT                                  00459
         WAIT  ECB=OUTECB     WAIT FOR IT                               00460
         CLI   OUTECB,X'7F'   WRITE COMPLETE NORMALLY?                  00461
         BNE   OUTERR         NO - ERROR                                00462
WRITE9   TM    FLAG2,OP9      IS THIS OUTPUT PRESENT?                   00463
         BZ    WRITE10        NO                                        00464
         LA    R2,OUT9        R2=ADDRESS OF CURRENT DCB                 00465
         ST    R2,OUTDCBPT    PUT IT IN IOB                             00466
         MVI   OUTECB,X'00'   INITIALIZE ECB                            00467
         EXCP  OUTIOB         WRITE IT                                  00468
         WAIT  ECB=OUTECB     WAIT FOR IT                               00469
         CLI   OUTECB,X'7F'   WRITE COMPLETE NORMALLY?                  00470
         BNE   OUTERR         NO - ERROR                                00471
WRITE10  TM    FLAG2,OP10     IS THIS OUTPUT PRESENT?                   00472
         BZ    WRITE11        NO                                        00473
         LA    R2,OUT10       R2=ADDRESS OF CURRENT DCB                 00474
         ST    R2,OUTDCBPT    PUT IT IN IOB                             00475
         MVI   OUTECB,X'00'   INITIALIZE ECB                            00476
         EXCP  OUTIOB         WRITE IT                                  00477
         WAIT  ECB=OUTECB     WAIT FOR IT                               00478
         CLI   OUTECB,X'7F'   WRITE COMPLETE NORMALLY?                  00479
         BNE   OUTERR         NO - ERROR                                00480
WRITE11  TM    FLAG2,OP11     IS THIS OUTPUT PRESENT?                   00472
         BZ    WRITE12        NO                                        00473
         LA    R2,OUT11       R2=ADDRESS OF CURRENT DCB                 00474
         ST    R2,OUTDCBPT    PUT IT IN IOB                             00475
         MVI   OUTECB,X'00'   INITIALIZE ECB                            00476
         EXCP  OUTIOB         WRITE IT                                  00477
         WAIT  ECB=OUTECB     WAIT FOR IT                               00478
         CLI   OUTECB,X'7F'   WRITE COMPLETE NORMALLY?                  00479
         BNE   OUTERR         NO - ERROR                                00480
WRITE12  TM    FLAG2,OP12     IS THIS OUTPUT PRESENT?                   00472
         BZ    WRITE13        NO                                        00473
         LA    R2,OUT12       R2=ADDRESS OF CURRENT DCB                 00474
         ST    R2,OUTDCBPT    PUT IT IN IOB                             00475
         MVI   OUTECB,X'00'   INITIALIZE ECB                            00476
         EXCP  OUTIOB         WRITE IT                                  00477
         WAIT  ECB=OUTECB     WAIT FOR IT                               00478
         CLI   OUTECB,X'7F'   WRITE COMPLETE NORMALLY?                  00479
         BNE   OUTERR         NO - ERROR                                00480
WRITE13  TM    FLAG2,OP13     IS THIS OUTPUT PRESENT?                   00472
         BZ    WRITE14        NO                                        00473
         LA    R2,OUT13       R2=ADDRESS OF CURRENT DCB                 00474
         ST    R2,OUTDCBPT    PUT IT IN IOB                             00475
         MVI   OUTECB,X'00'   INITIALIZE ECB                            00476
         EXCP  OUTIOB         WRITE IT                                  00477
         WAIT  ECB=OUTECB     WAIT FOR IT                               00478
         CLI   OUTECB,X'7F'   WRITE COMPLETE NORMALLY?                  00479
         BNE   OUTERR         NO - ERROR                                00480
WRITE14  TM    FLAG2,OP14     IS THIS OUTPUT PRESENT?                   00472
         BZ    WRITE15        NO                                        00473
         LA    R2,OUT14       R2=ADDRESS OF CURRENT DCB                 00474
         ST    R2,OUTDCBPT    PUT IT IN IOB                             00475
         MVI   OUTECB,X'00'   INITIALIZE ECB                            00476
         EXCP  OUTIOB         WRITE IT                                  00477
         WAIT  ECB=OUTECB     WAIT FOR IT                               00478
         CLI   OUTECB,X'7F'   WRITE COMPLETE NORMALLY?                  00479
         BNE   OUTERR         NO - ERROR                                00480
WRITE15  TM    FLAG2,OP15     IS THIS OUTPUT PRESENT?                   00472
         BZ    WRITE16        NO                                        00473
         LA    R2,OUT15       R2=ADDRESS OF CURRENT DCB                 00474
         ST    R2,OUTDCBPT    PUT IT IN IOB                             00475
         MVI   OUTECB,X'00'   INITIALIZE ECB                            00476
         EXCP  OUTIOB         WRITE IT                                  00477
         WAIT  ECB=OUTECB     WAIT FOR IT                               00478
         CLI   OUTECB,X'7F'   WRITE COMPLETE NORMALLY?                  00479
         BNE   OUTERR         NO - ERROR                                00480
WRITE16  TM    FLAG2,OP16     IS THIS OUTPUT PRESENT?                   00472
         BZ    WRITE17        NO                                        00473
         LA    R2,OUT16       R2=ADDRESS OF CURRENT DCB                 00474
         ST    R2,OUTDCBPT    PUT IT IN IOB                             00475
         MVI   OUTECB,X'00'   INITIALIZE ECB                            00476
         EXCP  OUTIOB         WRITE IT                                  00477
         WAIT  ECB=OUTECB     WAIT FOR IT                               00478
         CLI   OUTECB,X'7F'   WRITE COMPLETE NORMALLY?                  00479
         BNE   OUTERR         NO - ERROR                                00480
WRITE17  TM    FLAG2A,EOV     END OF INPUT?                             00481
         BO    ENDINPUT       YES - QUIT                                00482
         B     READIN         NO - LOOP                                 00483
         SPACE 3                                                        00484
*                                                                       00485
*  ERROR ON INPUT -- TELL OPERATOR AND ABEND                            00486
*                                                                       00487
INERR    TM    INCSW+4,X'02'  UNIT CHECK?                               00488
         BZ    INERR1         NO - ASSUME DATA CHECK                    00489
         MVC   LINE(133),INEQC  SET ERROR MESSAGE                       00490
         MVC   INERR3+16(25),INEQC+12  MOVE ERROR MESSAGE TO WTOR       00491
         B     INERR2         CONTINUE                                  00492
INERR1   MVC   LINE(133),INDCK  SET ERROR MESSAGE                       00493
         MVC   INERR3+16(20),INDCK+12  MOVE ERROR MESSAGE TO WTOR       00494
INERR2   PUT   SYSPRINT,LINE  WRITE ERROR MESSAGE                       00495
         MVI   INECB,X'00'    RESET ECB                                 00496
INERR3   WTOR  '                          -- ACKNOWLEDGE',REPLY,       X00497
               6,INECB,ROUTCDE=(3)                                      00498
         WAIT  ECB=INECB      WAIT FOR OPERATOR TO SEE THE MESSAGE      00499
*                             WHO CARES WHAT THE REPLY IS               00500
         ABEND 69             QUIT                                      00501
         SPACE 3                                                        00502
*                                                                       00503
*  ERROR ON OUTPUT -- TELL OPERATOR THEN TERMINATE THIS OUTPUT ONLY     00504
*                                                                       00505
OUTERR   TM    OUTCSW+4,X'02' UNIT CHECK?                               00506
         BZ    OUTERR1        NO - ASSUME DATA CHECK                    00507
         MVC   LINE(133),OUTEQC  SET ERROR MESSAGE                      00508
         B     OUTERR2        CONTINUE                                  00509
OUTERR1  MVC   LINE(133),OUTDCK  SET ERROR MESSAGE                      00510
OUTERR2  LH    R4,40(,R2)     R4=TIOT OFFSET                            00511
         L     R5,16          R5=CVT                                    00512
         L     R5,0(,R5)      R5=TCB WORDS                              00513
         L     R5,4(,R5)      R5=MY TCB                                 00514
         L     R5,12(,R5)     R5=TIOT                                   00515
         AR    R4,R5          R4=TIOT ENTRY FOR THIS OUTPUT             00516
         MVC   LINE+35(5),4(R4)  MOVE DDNAME TO MESSAGE                 00517
         L     R4,16(,R4)     R4=UCB FOR THIS OUTPUT                    00518
         MVC   LINE+47(3),13(R4)  MOVE UNIT ADDRESS TO MESSAGE          00519
         MVC   LINE+59(6),36(R4)  MOVE VOLSER TO MESSAGE                00520
         PUT   SYSPRINT,LINE  WRITE MESSAGE                             00521
         MVC   OUTERR4+16(53),LINE+12  MOVE ERROR MESSAGE TO WTOR       00522
OUTERR3  MVI   OUTECB,X'00'   RESET ECB                                 00523
OUTERR4  WTOR  '                                                       X00524
               -- REPLY GO TO CONTINUE',REPLY,2,OUTECB,ROUTCDE=(3)      00525
         WAIT  ECB=OUTECB     WAIT FOR OPERATOR TO REPLY                00526
         CLC   REPLY(2),=C'GO'  CORRECT REPLY?                          00527
         BNE   OUTERR3        NO - TRY AGAIN                            00528
         ST    R2,REWDCBPT    PUT DCB ADDRESS IN REWIND IOB             00529
         EXCP  REWIOB         REWIND TAPE WITH THE ERROR                00530
*                             DON'T BOTHER WAITING FOR REWIND           00531
         OI    FLAG2A,ERR     INDICATE THAT THERE WAS AN ERROR          00532
         LA    R3,OUT1        R3=DCB FOR OUTPUT 1                       00533
         CR    R2,R3          WAS THIS THE DCB WITH THE ERROR?          00534
         BNE   OUTERR5        NO                                        00535
         NI    FLAG1,FF-OP1   SHOW THIS OUTPUT NOT PRESENT ANY MORE     00536
         B     WRITE2         CONTINUE WITH OTHER OUTPUT                00537
OUTERR5  LA    R3,OUT2        R3=DCB FOR OUTPUT 2                       00538
         CR    R2,R3          WAS THIS THE DCB WITH THE ERROR?          00539
         BNE   OUTERR6        NO                                        00540
         NI    FLAG1,FF-OP2   SHOW THIS OUTPUT NOT PRESENT ANY MORE     00541
         B     WRITE3         CONTINUE WITH OTHER OUTPUT                00542
OUTERR6  LA    R3,OUT3        R3=DCB FOR OUTPUT 3                       00543
         CR    R2,R3          WAS THIS THE DCB WITH THE ERROR?          00544
         BNE   OUTERR7        NO                                        00545
         NI    FLAG1,FF-OP3   SHOW THIS OUTPUT NOT PRESENT ANY MORE     00546
         B     WRITE4         CONTINUE WITH OTHER OUTPUT                00547
OUTERR7  LA    R3,OUT4        R3=DCB FOR OUTPUT 4                       00548
         CR    R2,R3          WAS THIS THE DCB WITH THE ERROR?          00549
         BNE   OUTERR8        NO                                        00550
         NI    FLAG1,FF-OP4   SHOW THIS OUTPUT NOT PRESENT ANY MORE     00551
         B     WRITE5         CONTINUE WITH OTHER OUTPUT                00552
OUTERR8  LA    R3,OUT5        R3=DCB FOR OUTPUT 5                       00553
         CR    R2,R3          WAS THIS THE DCB WITH THE ERROR?          00554
         BNE   OUTERR9        NO                                        00555
         NI    FLAG1,FF-OP5   SHOW THIS OUTPUT NOT PRESENT ANY MORE     00556
         B     WRITE6         CONTINUE WITH OTHER OUTPUT                00557
OUTERR9  LA    R3,OUT6        R3=DCB FOR OUTPUT 6                       00558
         CR    R2,R3          WAS THIS THE DCB WITH THE ERROR?          00559
         BNE   OUTERR10       NO                                        00560
         NI    FLAG1,FF-OP6   SHOW THIS OUTPUT NOT PRESENT ANY MORE     00561
         B     WRITE7         CONTINUE WITH OTHER OUTPUT                00562
OUTERR10 LA    R3,OUT7        R3=DCB FOR OUTPUT 7                       00563
         CR    R2,R3          WAS THIS THE DCB WITH THE ERROR?          00564
         BNE   OUTERR11       NO                                        00565
         NI    FLAG1,FF-OP7   SHOW THIS OUTPUT NOT PRESENT ANY MORE     00566
         B     WRITE8         CONTINUE WITH OTHER OUTPUT                00567
OUTERR11 LA    R3,OUT8        R3=DCB FOR OUTPUT 8                       00568
         CR    R2,R3          WAS THIS THE DCB WITH THE ERROR?          00569
         BNE   OUTERR12       NO                                        00570
         NI    FLAG1,FF-OP8   SHOW THIS OUTPUT NOT PRESENT ANY MORE     00571
         B     WRITE9         CONTINUE WITH OTHER OUTPUT                00572
OUTERR12 LA    R3,OUT9        R3=DCB FOR OUTPUT 9                       00573
         CR    R2,R3          WAS THIS THE DCB WITH THE ERROR?          00574
         BNE   OUTERR13       NO                                        00575
         NI    FLAG2,FF-OP9   SHOW THIS OUTPUT NOT PRESENT ANY MORE     00576
         B     WRITE10        CONTINUE WITH OTHER OUTPUT                00577
OUTERR13 LA    R3,OUT10       R3=DCB FOR OUTPUT 10                      00573
         CR    R2,R3          WAS THIS THE DCB WITH THE ERROR?          00574
         BNE   OUTERR14       NO                                        00575
         NI    FLAG2,FF-OP10  SHOW THIS OUTPUT NOT PRESENT ANY MORE     00576
         B     WRITE11        CONTINUE WITH OTHER OUTPUT                00577
OUTERR14 LA    R3,OUT11       R3=DCB FOR OUTPUT 11                      00573
         CR    R2,R3          WAS THIS THE DCB WITH THE ERROR?          00574
         BNE   OUTERR15       NO                                        00575
         NI    FLAG2,FF-OP11  SHOW THIS OUTPUT NOT PRESENT ANY MORE     00576
         B     WRITE12        CONTINUE WITH OTHER OUTPUT                00577
OUTERR15 LA    R3,OUT12       R3=DCB FOR OUTPUT 12                      00573
         CR    R2,R3          WAS THIS THE DCB WITH THE ERROR?          00574
         BNE   OUTERR16       NO                                        00575
         NI    FLAG2,FF-OP12  SHOW THIS OUTPUT NOT PRESENT ANY MORE     00576
         B     WRITE13        CONTINUE WITH OTHER OUTPUT                00577
OUTERR16 LA    R3,OUT13       R3=DCB FOR OUTPUT 13                      00573
         CR    R2,R3          WAS THIS THE DCB WITH THE ERROR?          00574
         BNE   OUTERR17       NO                                        00575
         NI    FLAG2,FF-OP13  SHOW THIS OUTPUT NOT PRESENT ANY MORE     00576
         B     WRITE14        CONTINUE WITH OTHER OUTPUT                00577
OUTERR17 LA    R3,OUT14       R3=DCB FOR OUTPUT 14                      00573
         CR    R2,R3          WAS THIS THE DCB WITH THE ERROR?          00574
         BNE   OUTERR18       NO                                        00575
         NI    FLAG2,FF-OP14  SHOW THIS OUTPUT NOT PRESENT ANY MORE     00576
         B     WRITE15        CONTINUE WITH OTHER OUTPUT                00577
OUTERR18 LA    R3,OUT15       R3=DCB FOR OUTPUT 15                      00573
         CR    R2,R3          WAS THIS THE DCB WITH THE ERROR?          00574
         BNE   OUTERR19       NO                                        00575
         NI    FLAG2,FF-OP15  SHOW THIS OUTPUT NOT PRESENT ANY MORE     00576
         B     WRITE16        CONTINUE WITH OTHER OUTPUT                00577
*                             THEN IT MUST BE OUTPUT 16                 00578
OUTERR19 NI    FLAG2,FF-OP16  SHOW THIS OUTPUT NOT PRESENT ANY MORE     00579
         B     WRITE17        CONTINUE WITH OTHER OUTPUT                00580
         SPACE 3                                                        00581
*                                                                       00582
*  END OF INPUT -- FINISH UP                                            00583
*                                                                       00584
ENDINPUT MVC   LINE(133),SUCCESS  MOVE MESSAGE TO LINE                  00585
         TM    FLAG2,OP16     OUTPUT 16 COMPLETED?                      00586
         BO    ENDIN1         YES - CONTINUE                            00587
         MVC   LINE+126(5),LINE+131 NO - DON'T SHOW IT                  00588
ENDIN1   TM    FLAG2,OP15     OUTPUT 15 COMPLETED?                      00589
         BO    ENDIN2         YES - CONTINUE                            00590
         MVC   LINE+119(12),LINE+126  NO - DON'T SHOW IT                00591
ENDIN2   TM    FLAG2,OP14     OUTPUT 14 COMPLETED?                      00592
         BO    ENDIN3         YES - CONTINUE                            00593
         MVC   LINE+112(19),LINE+119  NO - DON'T SHOW IT                00594
ENDIN3   TM    FLAG2,OP13     OUTPUT 13 COMPLETED?                      00595
         BO    ENDIN4         YES - CONTINUE                            00596
         MVC   LINE+105(26),LINE+112  NO - DON'T SHOW IT                00597
ENDIN4   TM    FLAG2,OP12     OUTPUT 12 COMPLETED?                      00598
         BO    ENDIN5         YES - CONTINUE                            00599
         MVC   LINE+98(33),LINE+105  NO - DON'T SHOW IT                 00600
ENDIN5   TM    FLAG2,OP11     OUTPUT 11 COMPLETED?                      00601
         BO    ENDIN6         YES - CONTINUE                            00602
         MVC   LINE+91(40),LINE+98  NO - DON'T SHOW IT                  00603
ENDIN6   TM    FLAG2,OP10     OUTPUT 10 COMPLETED?                      00604
         BO    ENDIN7         YES - CONTINUE                            00605
         MVC   LINE+84(47),LINE+91  NO - DON'T SHOW IT                  00606
ENDIN7   TM    FLAG2,OP9      OUTPUT 9 COMPLETED?                       00607
         BO    ENDIN8         YES - CONTINUE                            00608
         MVC   LINE+78(53),LINE+84  NO - DON'T SHOW IT                  00609
ENDIN8   TM    FLAG1,OP8      OUTPUT 8 COMPLETED?                       00610
         BO    ENDIN9         YES - CONTINUE                            00611
         MVC   LINE+72(59),LINE+78  NO - DON'T SHOW IT                  00612
ENDIN9   TM    FLAG1,OP7      OUTPUT 7 COMPLETED?                       00613
         BO    ENDIN10        YES - CONTINUE                            00614
         MVC   LINE+66(65),LINE+72  NO - DON'T SHOW IT                  00615
ENDIN10  TM    FLAG1,OP6      OUTPUT 6 COMPLETED?                       00613
         BO    ENDIN11        YES - CONTINUE                            00614
         MVC   LINE+60(71),LINE+66  NO - DON'T SHOW IT                  00615
ENDIN11  TM    FLAG1,OP5      OUTPUT 5 COMPLETED?                       00613
         BO    ENDIN12        YES - CONTINUE                            00614
         MVC   LINE+54(77),LINE+60  NO - DON'T SHOW IT                  00615
ENDIN12  TM    FLAG1,OP4      OUTPUT 4 COMPLETED?                       00613
         BO    ENDIN13        YES - CONTINUE                            00614
         MVC   LINE+48(83),LINE+54  NO - DON'T SHOW IT                  00615
ENDIN13  TM    FLAG1,OP3      OUTPUT 3 COMPLETED?                       00613
         BO    ENDIN14        YES - CONTINUE                            00614
         MVC   LINE+42(89),LINE+48  NO - DON'T SHOW IT                  00615
ENDIN14  TM    FLAG1,OP2      OUTPUT 2 COMPLETED?                       00613
         BO    ENDIN15        YES - CONTINUE                            00614
         MVC   LINE+36(95),LINE+42  NO - DON'T SHOW IT                  00615
ENDIN15  TM    FLAG1,OP1      OUTPUT 1 COMPLETED?                       00613
         BO    ENDIN16        YES - CONTINUE                            00614
         MVC   LINE+30(101),LINE+36  NO - DON'T SHOW IT                 00615
ENDIN16  PUT   SYSPRINT,LINE  WRITE IT                                  00616
         SPACE 3                                                        00617
*                                                                       00618
*  CLOSE ALL FILES                                                      00619
*                                                                       00620
         CLOSE (IN,,SYSPRINT)                                           00621
         TM    FLAG3,OP1      OUTPUT 1 OPEN?                            00622
         BZ    CLOSE1         NO                                        00623
         CLOSE (OUT1)         CLOSE OUTPUT 1                            00624
CLOSE1   TM    FLAG3,OP2      OUTPUT 2 OPEN?                            00625
         BZ    CLOSE2         NO                                        00626
         CLOSE (OUT2)         CLOSE OUTPUT 2                            00627
CLOSE2   TM    FLAG3,OP3      OUTPUT 3 OPEN?                            00628
         BZ    CLOSE3         NO                                        00629
         CLOSE (OUT3)         CLOSE OUTPUT 3                            00630
CLOSE3   TM    FLAG3,OP4      OUTPUT 4 OPEN?                            00631
         BZ    CLOSE4         NO                                        00632
         CLOSE (OUT4)         CLOSE OUTPUT 4                            00633
CLOSE4   TM    FLAG3,OP5      OUTPUT 5 OPEN?                            00634
         BZ    CLOSE5         NO                                        00635
         CLOSE (OUT5)         CLOSE OUTPUT 5                            00636
CLOSE5   TM    FLAG3,OP6      OUTPUT 6 OPEN?                            00637
         BZ    CLOSE6         NO                                        00638
         CLOSE (OUT6)         CLOSE OUTPUT 6                            00639
CLOSE6   TM    FLAG3,OP7      OUTPUT 7 OPEN?                            00640
         BZ    CLOSE7         NO                                        00641
         CLOSE (OUT7)         CLOSE OUTPUT 7                            00642
CLOSE7   TM    FLAG3,OP8      OUTPUT 8 OPEN?                            00643
         BZ    CLOSE8         NO                                        00644
         CLOSE (OUT8)         CLOSE OUTPUT 8                            00645
CLOSE8   TM    FLAG4,OP9      OUTPUT 9 OPEN?                            00646
         BZ    CLOSE9         NO                                        00647
         CLOSE (OUT9)         CLOSE OUTPUT 9                            00648
CLOSE9   TM    FLAG4,OP10     OUTPUT 10 OPEN?                           00649
         BZ    CLOSE10        NO                                        00650
         CLOSE (OUT10)        CLOSE OUTPUT 10                           00651
CLOSE10  TM    FLAG4,OP11     OUTPUT 11 OPEN?                           00649
         BZ    CLOSE11        NO                                        00650
         CLOSE (OUT11)        CLOSE OUTPUT 11                           00651
CLOSE11  TM    FLAG4,OP12     OUTPUT 12 OPEN?                           00649
         BZ    CLOSE12        NO                                        00650
         CLOSE (OUT12)        CLOSE OUTPUT 12                           00651
CLOSE12  TM    FLAG4,OP13     OUTPUT 13 OPEN?                           00649
         BZ    CLOSE13        NO                                        00650
         CLOSE (OUT13)        CLOSE OUTPUT 13                           00651
CLOSE13  TM    FLAG4,OP14     OUTPUT 14 OPEN?                           00649
         BZ    CLOSE14        NO                                        00650
         CLOSE (OUT14)        CLOSE OUTPUT 14                           00651
CLOSE14  TM    FLAG4,OP15     OUTPUT 15 OPEN?                           00649
         BZ    CLOSE15        NO                                        00650
         CLOSE (OUT15)        CLOSE OUTPUT 15                           00651
CLOSE15  TM    FLAG4,OP16     OUTPUT 16 OPEN?                           00649
         BZ    THEEND         NO                                        00650
         CLOSE (OUT16)        CLOSE OUTPUT 16                           00651
         SPACE 3                                                        00652
*                                                                       00653
*  THE END                                                              00654
*                                                                       00655
THEEND   EQU   *                                                        00656
         MVC   WTOCBT+16(41),FILEDONE                                   00657
WTOCBT   WTO   'CBT990                                              '   00658
         SR    R15,R15        ZERO RETURN CODE                          00659
         TM    FLAG2A,ERR     WAS THERE AN ERROR?                       00660
         BZ    THEEND1        NO                                        00661
         LA    R15,16         SET RETURN CODE                           00662
THEEND1  TERME RMK=NO                                                   00663
         EJECT                                                          00664
DWORD    DC    D'0'                                                     00665
ZERO23   DC    23C'0'         TEST FOR IEHINITTED HDR1        SBG 03/00
         CNOP  0,8                                                      00666
INCCW    DC    X'02',AL3(INAREA),X'2000',X'FFFF'              SBG 03/00 00667
OUTCCW   DC    X'01',AL3(INAREA),X'2000',X'FFFF'              SBG 03/00 00668
REWCCW   DC    X'07',AL3(INAREA),X'2000',X'FFFF'              SBG 03/00 00669
INECB    DC    F'0'                                                     00670
INIOB    DC    X'02000000'                                              00671
         DC    A(INECB)                                                 00672
INCSW    DC    2F'0'                                                    00673
         DC    A(INCCW)                                                 00674
         DC    A(IN)                                                    00675
         DC    F'0'                                                     00676
         DC    X'00010000'                                              00677
OUTECB   DC    F'0'                                                     00678
OUTIOB   DC    X'02000000'                                              00679
         DC    A(OUTECB)                                                00680
OUTCSW   DC    2F'0'                                                    00681
         DC    A(OUTCCW)                                                00682
OUTDCBPT DC    A(OUT1)                                                  00683
         DC    F'0'                                                     00684
         DC    X'00010000'                                              00685
REWECB   DC    F'0'                                                     00686
REWIOB   DC    X'02000000'                                              00687
         DC    A(REWECB)                                                00688
REWCSW   DC    2F'0'                                                    00689
         DC    A(REWCCW)                                                00690
REWDCBPT DC    A(OUT1)                                                  00691
         DC    F'0'                                                     00692
         DC    X'00010000'                                              00693
         SPACE 3                                                        00694
FLAG1    DC    X'00'                                                    00695
OP1      EQU   X'80'                                                    00696
OP2      EQU   X'40'                                                    00697
OP3      EQU   X'20'                                                    00698
OP4      EQU   X'10'                                                    00699
OP5      EQU   X'08'                                                    00700
OP6      EQU   X'04'                                                    00701
OP7      EQU   X'02'                                                    00702
OP8      EQU   X'01'                                                    00703
FLAG2    DC    X'00'                                                    00704
OP9      EQU   X'80'                                                    00705
OP10     EQU   X'40'                                                    00706
OP11     EQU   X'20'                                                    00706
OP12     EQU   X'10'                                                    00706
OP13     EQU   X'08'                                                    00706
OP14     EQU   X'04'                                                    00706
OP15     EQU   X'02'                                                    00706
OP16     EQU   X'01'                                                    00706
FLAG2A   DC    X'00'                                          SBG 03/00 00704
ERR      EQU   X'02'                                                    00707
EOV      EQU   X'01'                                                    00708
FF       EQU   X'FF'                                                    00709
FLAG3    DC    X'00'                                                    00710
FLAG4    DC    X'00'                                                    00711
FLAG4A   DC    X'00'         COPY IMAGE FOR SAFETY'S SAKE     SBG 03/00 00704
LBLFLAG  DC    X'00'                                          SBG 03/00
*              X'01'    HDR1 HAS BEEN SEEN                    SBG 03/00
*              X'02'    HDR2 HAS BEEN SEEN                    SBG 03/00
*              X'04'    VOL1 HAS BEEN SEEN                    SBG 03/00
*              X'08'    HDR1 IS FROM AN INITTED TAPE          SBG 03/00
         SPACE 3                                                        00712
INEQC    DC    C' *********** EQUIPMENT CHECK ON INPUT -- COPY '        00713
         DC    C'TERMINATED '                                           00714
         DC    76C'*'                                                   00715
INDCK    DC    C' ********** DATA CHECK ON INPUT -- COPY TERMINATED '   00716
         DC    82C'*'                                                   00717
OUTEQC   DC    C' *********** EQUIPMENT CHECK ON DD XXXXX, UNIT '       00718
         DC    C'XXX, VOLUME XXXXXX -- THIS OUTPUT TERMINATED '         00719
         DC    41C'*'                                                   00720
OUTDCK   DC    C' **********       DATA CHECK ON DD XXXXX, UNIT '       00721
         DC    C'XXX, VOLUME XXXXXX -- THIS OUTPUT TERMINATED '         00722
         DC    41C'*'                                                   00723
FILEDONE DC    C' FILE 00000 CONTAINS 0000000 DATA BLOCKS'              00724
         DC    93C' '                                                   00725
SUCCESS  DC    C'0COPY CORRECTLY COMPLETED FOR OUT1, OUT2, OUT3, OU'    00726
         DC    C'T4, OUT5, OUT6, OUT7, OUT8, OUT9, OUT10, OUT11, OU'    00727
         DC    C'T12, OUT13, OUT14, OUT15, OUT16  '                     00728
         DC    C'       '          BLANKS FOR COVERING UP     SBG 03/00
LINE     DC    133C' '                                                  00729
         DC    C'       '          BLANKS FOR COVERING UP     SBG 03/00
REPLY    DC    C'      '                                                00730
         SPACE 3                                                        00731
         LTORG                                                          00732
         SPACE 3                                                        00733
IN       DCB   MACRF=(E),DDNAME=IN,DEVD=TA,DSORG=PS,RECFM=U             00734
OUT1     DCB   MACRF=(E),DDNAME=OUT1,DEVD=TA,DSORG=PS,RECFM=U           00735
OUT2     DCB   MACRF=(E),DDNAME=OUT2,DEVD=TA,DSORG=PS,RECFM=U           00736
OUT3     DCB   MACRF=(E),DDNAME=OUT3,DEVD=TA,DSORG=PS,RECFM=U           00737
OUT4     DCB   MACRF=(E),DDNAME=OUT4,DEVD=TA,DSORG=PS,RECFM=U           00738
OUT5     DCB   MACRF=(E),DDNAME=OUT5,DEVD=TA,DSORG=PS,RECFM=U           00739
OUT6     DCB   MACRF=(E),DDNAME=OUT6,DEVD=TA,DSORG=PS,RECFM=U           00740
OUT7     DCB   MACRF=(E),DDNAME=OUT7,DEVD=TA,DSORG=PS,RECFM=U           00741
OUT8     DCB   MACRF=(E),DDNAME=OUT8,DEVD=TA,DSORG=PS,RECFM=U           00742
OUT9     DCB   MACRF=(E),DDNAME=OUT9,DEVD=TA,DSORG=PS,RECFM=U           00743
OUT10    DCB   MACRF=(E),DDNAME=OUT10,DEVD=TA,DSORG=PS,RECFM=U          00744
OUT11    DCB   MACRF=(E),DDNAME=OUT11,DEVD=TA,DSORG=PS,RECFM=U          00744
OUT12    DCB   MACRF=(E),DDNAME=OUT12,DEVD=TA,DSORG=PS,RECFM=U          00744
OUT13    DCB   MACRF=(E),DDNAME=OUT13,DEVD=TA,DSORG=PS,RECFM=U          00744
OUT14    DCB   MACRF=(E),DDNAME=OUT14,DEVD=TA,DSORG=PS,RECFM=U          00744
OUT15    DCB   MACRF=(E),DDNAME=OUT15,DEVD=TA,DSORG=PS,RECFM=U          00744
OUT16    DCB   MACRF=(E),DDNAME=OUT16,DEVD=TA,DSORG=PS,RECFM=U          00744
SYSPRINT DCB   MACRF=PM,DDNAME=SYSPRINT,DSORG=PS,RECFM=FA,LRECL=133,   X00745
               BLKSIZE=133                                              00746
INAREA   DS    CL65535                                        SBG 03/00 00747
#PAN$AUD CSECT                                                          007474
$PAN#AUD DC    CL21'009CBT990    03/21/00'                    SBG 03/00 007475
         END                                                            00748
