HASX20B  TITLE 'JES2 USER EXIT 20 -- PROLOG (MODULE COMMENT BLOCK)'
***********************************************************************
*                                                                     *
* MODULE NAME = HASX20B  (HASX20B load module)                        *
*                                                                     *
* DESCRIPTIVE NAME = SAMPLE JES2 USER EXIT 20 (End of Input ROUTINE)  *
*                                                                     *
* STATUS = OS/VS2 - See $MODULE expansion below for FMID, version     *
*                                                                     *
* FUNCTION : This exit places the last two bytes of the output from   *
*            an STCK instruction, converted to printable hex in       *
*            bytes 3 through 6 of the JCTUSEID field. This field      *
*            is propogated into the SMF "user_id" records generated   *
*            by the job. This is done due to the fact that the        *
*            IRRADU00 output truncates the READER START TIME          *
*            to the nearest second. This could result in separate     *
*            jobs having the same "SMF JOBLOG" value. Also, IBM       *
*            has indicated that the JOBLOG is not guaranteed to       *
*            be unique. Use of the last two bytes of the STCK         *
*            output, along with the "JOBLOG" should result in a       *
*            unique indentifier, at least within a single sysplex.    *
*                                                                     *
*            The SMF "JOBLOG" consists of the concatenation of        *
*            the job name, reader start date, and reader start time.  *
*                                                                     *
*            Implementation of this JES2 exit is totally optional.    *
*            You may do it by compiling this program into a           *
*            dataset on the linklist and including the JES2           *
*            initialization statements (in z/OS 1.4):                 *
*                                                                     *
*            LOADMOD(HASX20B)                                         *
*            EXIT(20) ROUTINE=(EXIT20B),STATUS=ENABLED                *
*                                                                     *
* ENVIRONMENT : JES2 MAIN TASK                                        *
*                                                                     *
* NOTES                                                               *
*                                                                     *
*    DEPENDENCIES = JES2 $EXIT FACILITY, STANDARD JES2 SERVICES       *
*                                                                     *
*    RESTRICTIONS = This code is provided as an example of            *
*                   installation extensions to JES2.  This code       *
*                   is not considered TYPE 1 supported code of IBM.   *
*                   Any problems encountered in the use of this       *
*                   sample code is a user responsibility.  The        *
*                   IBM support center does not support user          *
*                   extensions of sample user exits.                  *
*                                                                     *
*    REGISTER CONVENTIONS = See entry point documentation             *
*                                                                     *
*    PATCH LABEL = NONE                                               *
*                                                                     *
* MODULE TYPE = Procedure ( CSECT type )                              *
*                                                                     *
*    PROCESSOR = IBM OS/Assembler H Version 2 or               @430P270
*                IBM High Level Assembler/MVS                  @430P270
*                                                                     *
*    MODULE SIZE = See $MODEND macro expansion at end of assembly     *
*                                                                     *
*    ATTRIBUTES = JES2 REENTRANT, RMODE ANY, AMODE 31                 *
*                                                                     *
* ENTRY POINTS : EXIT04                                               *
*                                                                     *
* REGISTER USAGE (ENTRY/EXIT) :                                       *
*                                                                     *
*    REG       VALUE ON ENTRY             VALUE ON EXIT               *
*                                                                     *
*    R0        A CODE PASSED TO THE       UNCHANGED                   *
*              ROUTINE BY JES2:                                       *
*                0  NORMAL END OF INPUT                               *
*                4  JOBS HAS A JES2 CONTROL STATEMENT ERROR.          *
*    R1-R9     N/A                        UNCHANGED                   *
*    R10       ADDRESS OF THE JCT OR ZERO UNCHANGED                   *
*    R11       ADDRESS OF THE HCT         UNCHANGED                   *
*    R12       N/A                        UNCHANGED                   *
*    R13       ADDRESS OF THE PCE         UNCHANGED                   *
*    R14       RETURN ADDRESS             UNCHANGED                   *
*    R15       ENTRY ADDRESS              RETURN CODE                 *
*                                                                     *
* RETURN CODES (R15 ON EXIT)                                          *
*                                                                     *
*       0  TELLS JES2 THAT IF THERE ARE ANY ADDITIONAL EXIT ROUTINES  *
*          ASSOCIATED WITH THIS EXIT, CALL THE NEXT CONSECUTIVE       *
*          EXIT ROUTINE.  IF THERE ARE NO OTHER EXIT ROUTINES ASSO-   *
*          CIATED WITH THIS EXIT, CONTINUE WITH NORMAL PROCESSING.    *
*                                                                     *
*       4  TELLS JES2 THAT EVEN IF THERE ARE ADDITIONAL EXIT ROUTINES *
*          ASSOCIATED WITH THIS EXIT, IGNORE THEM.  CONTINUE WITH     *
*          NORMAL PROCESSING.                                         *
*                                                                     *
*       8  TELLS JES2 TO TERMINATE NORMAL PROCESSING AND PRINT        *
*          THE OUTPUT.                                                *
*                                                                     *
*      12  TELLS JES2 TO TERMINATE NORMAL PROCESSING WITHOUT          *
*          PRINTING THE OUTPUT.                                       *
*                                                                     *
* MACROS = JES2 - $ENTRY, $MODEND, $MODULE, $MSG, $RETURN, $SAVE,     *
*                 $WTO                                                *
*                                                                     *
* MACROS = MVS  - NONE                                                *
*                                                                     *
* CHANGE ACTIVITY:                                                    *
*                                                                     *
*        @311    MVS/SP-JES2 VERSION 3 RELEASE 1 LEVEL 1              *
*                (SP3.1.1, HJE3311)                                   *
*                                                                     *
* $430P270=10X      HJE4430 920721 HGF:  $MODULE, ENVIRON, RMODE      *
*                                                                     *
* A000000-999999    CREATED FOR JES2 3.1.1                         @311
***********************************************************************
         TITLE 'JES2 USER EXIT 20 -- PROLOG ($HASPGBL)'
         COPY  $HASPGBL
         TITLE 'JES2 USER EXIT 20 -- PROLOG ($MODULE)'
HASX20B $MODULE ENVIRON=JES2,                                  @430P270C
               RMODE=ANY,                                      @430P270C
               TITLE='JES2 USER EXIT 20',                      @430P270C
               SPLEVEL=NOCHECK,                                        X
               $BUFFER,            GENERATE HASP I/O BUFFER DSECT      C
               $DCT,               GENERATE HASP DCT DSECT             C
               $HASPEQU,           GENERATE HASP EQUATES DSECT         C
               $HCT,               GENERATE HASP HCT DSECT             C
               $JCT,               GENERATE HASP JCT DSECT             C
               $MIT,               GENERATE HASP MIT DSECT             C
               $PCE,               GENERATE HASP PCE DSECT             C
               ASCB,               GENERATE MVS ASCB DSECT             C
               RPL                 GENERATE MVS RPL DSECT      @430P270
         TITLE 'JES2 USER EXIT 20 -- JCL/JECL SCAN'
***********************************************************************
*                                                                     *
*        EXIT20 - INSTALLATION EXIT 20 ROUTINE                        *
*                                                                     *
*  FUNCTION:                                                          *
*                                                                     *
***********************************************************************
         SPACE 1
EXIT20B $ENTRY  BASE=R12           PROVIDE EXIT ROUTINE ENTRY POINT
        $SAVE                      SAVE CALLER'S REGISTERS
         USING JCT,R10             ESTABLISH JCT ADDRESSABILITY
         SPACE 1
         LR    R12,R15             ESTABLISH BASE REGISTER
         LTR   R0,R0               TEST FOR NORMAL END OF INPUT
         BNZ   X20RC00
         LA    R2,JCTUSER1
         N     R2,=A(X'FFFFFFF8')
         LM    R3,R6,0(R2)
         STCK  0(R2)
         UNPK  8(5,R2),6(3,R2)
         MVC   JCTUSEID+2(4),8(R2)
         TR    JCTUSEID+2(4),TOHEX-C'0'
         STM   R3,R6,0(R2)
         B     X20RC00
         EJECT
         SPACE 1
***********************************************************************
*                                                                     *
*    EXIT20 COMMON RETURN ROUTINE                                     *
*                                                                     *
***********************************************************************
         SPACE 1
X20RC00  LA    R15,0               SET RC=0
         B     RRET                BRANCH TO RETURN WITH RC SET
         SPACE 1
X20RC04  LA    R15,4               SET RC=4
         B     RRET                BRANCH TO RETURN WITH RC SET
         SPACE 1
X20RC08  LA    R15,8               SET RC=8
         B     RRET                BRANCH TO RETURN WITH RC SET
         SPACE 1
X20RC12  LA    R15,12              SET RC=12
         SPACE 1
RRET    $RETURN RC=(R15)           SAVE RETURN CODE
         EJECT
         SPACE 2
         DS    CL90'UNUSED'
TOHEX    DC    C'0123456789ABCDEF'
         LTORG
         TITLE 'JES2 USER EXIT 20 -- EPILOG ($MODEND)'         @430P270
        $MODEND
APARNUM  DC    CL8'NONE'           APAR NUMBER
         END   ,                   END OF HASX20B
