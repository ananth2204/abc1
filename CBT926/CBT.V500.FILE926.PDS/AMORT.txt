       IDENTIFICATION DIVISION.
       PROGRAM-ID.     AMORTIZA.
      *
      * AUTHOR:  PHILIP POLCHINSKI
      *
      * THIS PROGRAM CREATES A SCHEDULE OF AMORTIZATION PAYMENTS
      *  WHEN INFORMATION IS SUPPLIED VIA A PARM FIELD AT THE TIME
      *   OF EXECUTION.  AFTER ALL VALUES HAVE BEEN VERIFIED,
      *    THE PROGRAM THEN PERFORMS 1 OF 3 DIFFERENT CALCULATIONS:
      *
      *  JOB TYPE-1 - CALCULATES THE AMOUNT OF PRINCIPAL TO BE BORROWED
      *    (WHEN MONTHLY PAYMENT AMOUNT AND # OF PAYMENTS IS PROVIDED)
      *
      *  JOB TYPE-2 - CALCULATES THE MONTHLY PAYMENT AMOUNT
      *    (WHEN PRINCIPAL AND # OF PAYMENTS IS PROVIDED)
      *
      *  JOB TYPE-3 - CALCULATES LENGTH OF LOAN IN NUMBER OF PAYMENTS
      *    (WHEN PRINCIPAL AND MONTHLY PAYMENT AMOUNT IS PROVIDED)
      *
      * VALID VALUES MUST ALWAYS APPEAR IN THE PARM AREA
      *
      *  1. THE JOB TYPE MUST ALWAYS BE A VALUE OF 1 OR 2 OR 3
      *      NO EXCEPTIONS
      *
      *  2. THE PRINCIPAL MUST ALWAYS BE A 9-DIGIT NUMBER
      *      IN DOLLARS ONLY    - BUT CAN BE ZERO ONLY WHEN JOB TYPE 1
      *
      *  3. THE MONTHLY PAYMENT AMOUNT MUST ALWAYS BE A 9-DIGIT NUMBER
      *      IN DOLLARS & CENTS - BUT CAN BE ZERO ONLY WHEN JOB TYPE 2
      *
      *  4. LENGTH OF LOAN IS TOTAL OF MONTHLY PAYMENTS
      *      (YEARS PLUS MONTHS)
      *      BOTH YY & MM MUST BE NUMERIC AND TOTAL CANNOT EXCEED 1200
      *       BUT CAN BE A TOTAL OF ZERO ONLY WHEN JOB TYPE 3
      *
      *  NOTE: LENGTH IS CALCULATED AS YEARS PLUS MONTHS (FOR EXAMPLE)
      *        2 YRS + 0 MOS = 1 YR + 12 MOS = 0 YRS + 24 MOS
      *        2 YRS + 6 MOS = 1 YR + 18 MOS = 0 YRS + 30 MOS
      *
      *  5. THE ANNUAL INTEREST RATE MUST ALWAYS BE > +0 AND
      *      MUST RANGE FROM .00001 TO .99999 FOR ALL JOB TYPES
      *
      * //JOBCARD AND JCL
      * //******PGM=PROGNAME,PARM='Z--PRINCIPAL-PAYMENTCC-YY-MM-INTER'
      * // EXEC PGM=PROGNAME,PARM='1--000000000-000060300-02-00-05999'
      * //******PGM=PROGNAME,PARM='2--000013600-000000000-00-24-05999'
      * //******PGM=PROGNAME,PARM='3--000013600-000060300-00-00-05999'
      * //******PGM=PROGNAME,PARM='Z--MMMTTTDDD-MTTTDDDcc-YY-MM-INTER'
      * //STEPLIB  DD DSN=NAME.OF.LOADLIB.LIBRARY,DISP=SHR
      * //SYSOUT   DD SYSOUT=*
      * //SCHEDULE DD SYSOUT=*
      *
      * SAMPLE OUTPUT:
      *
      * JOB TYPE-1 CALCULATES THE MAXIMUM AMOUNT OF PRINCIPAL
      * DISPLAY THE PARM FIELD ==> 1--000000000-000060300-02-00-05999
      * THE PRINCIPAL WILL BE ===>   +13,605.55
      *  PAYMENT       BALANCE     CURRENT     CURRENT    CURRENT
      *   NUMBER     REMAINING   PRINCIPAL    INTEREST    PAYMENT
      *     1       +13,070.57      534.98       68.02     603.00
      *
      * JOB TYPE-2 CALCULATES THE AMOUNT OF EACH MONTHLY PAYMENT
      * DISPLAY THE PARM FIELD ==> 2--000013600-000000000-00-24-05999
      * THE PAYMENTS WILL BE  ===>                 602.75
      *  PAYMENT       BALANCE     CURRENT     CURRENT    CURRENT
      *   NUMBER     REMAINING   PRINCIPAL    INTEREST    PAYMENT
      *     1       +13,065.24      534.76       67.99     602.75
      *
      * JOB TYPE-3 CALCULATES THE NUMBER OF PAYMENTS
      * DISPLAY THE PARM FIELD ==> 3--000013600-000060300-00-00-05999
      * >>>>> MAX DOWN TO FIND THE NUMBER OF PAYMENTS  >>>>>
      *  PAYMENT       BALANCE     CURRENT     CURRENT    CURRENT
      *   NUMBER     REMAINING   PRINCIPAL    INTEREST    PAYMENT
      *     1       +13,064.99      535.01       67.99     603.00
      *
       ENVIRONMENT DIVISION.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
           SELECT PAYMENT-SCHEDULE   ASSIGN TO UT-S-SCHEDULE.

       DATA DIVISION.
       FILE SECTION.
       FD  PAYMENT-SCHEDULE
           RECORDING MODE IS F
           BLOCK CONTAINS 0 RECORDS
           LABEL RECORDS ARE STANDARD.
       01  PRINT-RECORD.
           05 PRINT-CC            PIC  X(1).
           05 PRINT-LINE          PIC  X(132).

       WORKING-STORAGE SECTION.

       01  HEADER-LINE-1.
           05 FILLER           PIC  X(1)       VALUE SPACE.
           05 FILLER           PIC  X(7)       VALUE 'PAYMENT'.
           05 FILLER           PIC  X(10)      VALUE SPACE.
           05 FILLER           PIC  X(7)       VALUE 'BALANCE'.
           05 FILLER           PIC  X(11)      VALUE SPACE.
           05 FILLER           PIC  X(7)       VALUE 'CURRENT'.
           05 FILLER           PIC  X(9)       VALUE SPACE.
           05 FILLER           PIC  X(7)       VALUE 'CURRENT'.
           05 FILLER           PIC  X(9)       VALUE SPACE.
           05 FILLER           PIC  X(7)       VALUE 'CURRENT'.
           05 FILLER           PIC  X(4)       VALUE SPACE.
           05 FILLER           PIC  X(7)       VALUE SPACE.
           05 FILLER           PIC  X(10)      VALUE 'CUMULATIVE'.
           05 FILLER           PIC  X(7)       VALUE SPACE.
           05 FILLER           PIC  X(10)      VALUE 'CUMULATIVE'.
           05 FILLER           PIC  X(7)       VALUE SPACE.
           05 FILLER           PIC  X(10)      VALUE 'CUMULATIVE'.
           05 FILLER           PIC  X(2)       VALUE SPACE.

       01  HEADER-LINE-2.
           05 FILLER           PIC  X(2)       VALUE SPACE.
           05 FILLER           PIC  X(6)       VALUE 'NUMBER'.
           05 FILLER           PIC  X(8)       VALUE SPACE.
           05 FILLER           PIC  X(9)       VALUE 'REMAINING'.
           05 FILLER           PIC  X(9)       VALUE SPACE.
           05 FILLER           PIC  X(9)       VALUE 'PRINCIPAL'.
           05 FILLER           PIC  X(8)       VALUE SPACE.
           05 FILLER           PIC  X(8)       VALUE 'INTEREST'.
           05 FILLER           PIC  X(9)       VALUE SPACE.
           05 FILLER           PIC  X(7)       VALUE 'PAYMENT'.
           05 FILLER           PIC  X(4)       VALUE SPACE.
           05 FILLER           PIC  X(8)       VALUE SPACE.
           05 FILLER           PIC  X(9)       VALUE 'PRINCIPAL'.
           05 FILLER           PIC  X(9)       VALUE SPACE.
           05 FILLER           PIC  X(8)       VALUE 'INTEREST'.
           05 FILLER           PIC  X(10)      VALUE SPACE.
           05 FILLER           PIC  X(7)       VALUE 'PAYMENT'.
           05 FILLER           PIC  X(2)       VALUE SPACE.

       01  DETAIL-LINE.
           05 FILLER            PIC  X(1)       VALUE SPACE.
           05 PRINT-PAY-NUMBER  PIC  ZZZZ9.
           05 FILLER            PIC  X(2)       VALUE SPACE.
           05 FILLER            PIC  X(2)       VALUE SPACE.
           05 PRINT-REM-BAL     PIC  ++++,+++,++9.99.
           05 FILLER            PIC  X(4)       VALUE SPACE.
           05 PRINT-CUR-PRIN    PIC  ZZZ,ZZZ,ZZ9.99.
           05 FILLER            PIC  X(2)       VALUE SPACE.
           05 PRINT-CUR-INT     PIC  ZZZ,ZZZ,ZZ9.99.
           05 FILLER            PIC  X(2)       VALUE SPACE.
           05 PRINT-CUR-PAY     PIC  ZZZ,ZZZ,ZZ9.99.
           05 FILLER            PIC  X(4)       VALUE SPACE.
           05 FILLER            PIC  X(3)       VALUE SPACE.
           05 PRINT-CUM-PRIN    PIC  ZZZ,ZZZ,ZZ9.99.
           05 FILLER            PIC  X(3)       VALUE SPACE.
           05 PRINT-CUM-INT     PIC  ZZZ,ZZZ,ZZ9.99.
           05 FILLER            PIC  X(3)       VALUE SPACE.
           05 PRINT-CUM-PAY     PIC  ZZZ,ZZZ,ZZ9.99.
           05 FILLER            PIC  X(2)       VALUE SPACE.

       01  JOB-LINE-1.
           05 FILLER            PIC  X(28)      VALUE
             'THE PRINCIPAL WILL BE  ===> '.
           05 PRINT-CALC-PRIN   PIC  ++++,+++,++9.99.
           05 FILLER            PIC  X(89)      VALUE SPACE.

       01  JOB-LINE-2.
           05 FILLER            PIC  X(28)      VALUE
             'THE PAYMENTS WILL BE   ===> '.
           05 FILLER            PIC  X(11)      VALUE SPACE.
           05 PRINT-CALC-PAY    PIC  ZZZ,ZZZ,ZZ9.99.
           05 FILLER            PIC  X(79)      VALUE SPACE.

       01  JOB-LINE-3.
           05 FILLER            PIC  X(07)      VALUE
              '>>>>>  '.
           05 FILLER            PIC  X(46)      VALUE
              'MAX DOWN TO FIND THE NUMBER OF PAYMENTS  >>>>>'.
           05 FILLER            PIC  X(79)      VALUE SPACE.

       01  PARM-LINE.
           05 FILLER            PIC  X(31)      VALUE
              'DISPLAY THE PARM FIELD ===>    '.
           05 PRINT-PARM-FIELD  PIC  X(34)      VALUE SPACE.
           05 FILLER            PIC  X(67)      VALUE SPACE.

       01  WS-PARM-DATA.
           03 JOB-TYPE          PIC  9(1).
           03 FILLER            PIC  X(2).
           03 PARM-PRINCIPAL    PIC  9(9).
           03 FILLER            PIC  X(1).
           03 PARM-PAYMENTS     PIC  9(7)V9(2).
           03 FILLER            PIC  X(1).
           03 PARM-YEARS        PIC  9(2).
           03 FILLER            PIC  X(1).
           03 PARM-MONTHS       PIC  9(2).
           03 FILLER            PIC  X(1).
           03 PARM-RATE         PIC  9(5).

       01  WORK-AREA-1.
           03 LINE-CTR          PIC  9(3)       COMP-3 VALUE 48.
           03 TOTAL-MONTHS      PIC  9(5)       COMP-3 VALUE 0.
           03 PAYMENT-NUMBER    PIC  9(5)       COMP-3 VALUE 0.
           03 TEMP-CALC         PIC  9(2)V9(7)  COMP-3 VALUE 0.
           03 MONTHLY-RATE      PIC  9(2)V9(7)  COMP-3 VALUE 0.
           03 POINT-09          PIC  9(2)V9(5)  COMP-3 VALUE .09.
           03 POINT-00001       PIC  9(2)V9(5)  COMP-3 VALUE .00001.
           03 WS-REM-BAL        PIC S9(9)V9(2)  COMP-3 VALUE +0.
           03 WS-CUR-PRIN       PIC S9(9)V9(2)  COMP-3 VALUE 0.
           03 WS-CUR-INT        PIC S9(9)V9(2)  COMP-3 VALUE 0.
           03 WS-CUR-PAY        PIC S9(9)V9(2)  COMP-3 VALUE 0.
           03 WS-CUM-PRIN       PIC S9(9)V9(2)  COMP-3 VALUE 0.
           03 WS-CUM-INT        PIC S9(9)V9(2)  COMP-3 VALUE 0.
           03 WS-CUM-PAY        PIC S9(9)V9(2)  COMP-3 VALUE 0.
           03 LOW-BAL-AMOUNT    PIC S9(9)V9(2)  COMP-3 VALUE 0.

       LINKAGE SECTION.
       01  PARM.
           03 PARM-LENGTH         PIC S9(4)       COMP.
           03 PARM-DATA           PIC  X(34).

       PROCEDURE DIVISION USING PARM.

           PERFORM 0100-VERIFY-ALL-PARM-VALUES
           PERFORM 0200-CONFIRM-JOB-TYPE
           PERFORM 0300-PRINT-THE-PAY-SCHEDULE
            VARYING PAYMENT-NUMBER
             FROM 1 BY 1
              UNTIL WS-REM-BAL = +0
           PERFORM 0600-SUCCESSFUL.

       0100-VERIFY-ALL-PARM-VALUES.

           OPEN OUTPUT PAYMENT-SCHEDULE
           MOVE PARM-DATA TO WS-PARM-DATA PRINT-PARM-FIELD

           IF JOB-TYPE NUMERIC
              IF JOB-TYPE = 1 OR 2 OR 3
                 CONTINUE
              ELSE
                 PERFORM 0700-NOT-SUCCESSFUL-1
              END-IF
           ELSE
              PERFORM 0700-NOT-SUCCESSFUL-1
           END-IF

           IF PARM-PRINCIPAL NUMERIC
              CONTINUE
           ELSE
              PERFORM 0700-NOT-SUCCESSFUL-1
           END-IF

           IF PARM-PAYMENTS NUMERIC
              CONTINUE
           ELSE
              PERFORM 0700-NOT-SUCCESSFUL-1
           END-IF

           IF PARM-YEARS NUMERIC
              CONTINUE
           ELSE
              PERFORM 0700-NOT-SUCCESSFUL-1
           END-IF

           IF PARM-MONTHS NUMERIC
              CONTINUE
           ELSE
              PERFORM 0700-NOT-SUCCESSFUL-1
           END-IF

           IF PARM-RATE NUMERIC
              IF PARM-RATE > 0
                 CONTINUE
              ELSE
                 PERFORM 0700-NOT-SUCCESSFUL-1
              END-IF
           ELSE
              PERFORM 0700-NOT-SUCCESSFUL-1
           END-IF

           IF JOB-TYPE = 1
              IF PARM-PAYMENTS > 0
                 IF PARM-YEARS > 0 OR PARM-MONTHS > 0
                    CONTINUE
                 ELSE
                    PERFORM 0700-NOT-SUCCESSFUL-1
                 END-IF
              ELSE
                 PERFORM 0700-NOT-SUCCESSFUL-1
              END-IF
           END-IF

           IF JOB-TYPE = 2
              IF PARM-PRINCIPAL > 0
                 IF PARM-YEARS > 0 OR PARM-MONTHS > 0
                    CONTINUE
                 ELSE
                    PERFORM 0700-NOT-SUCCESSFUL-1
                 END-IF
              ELSE
                 PERFORM 0700-NOT-SUCCESSFUL-1
              END-IF
           END-IF

           IF JOB-TYPE = 3
              IF PARM-PRINCIPAL > 0
                 IF PARM-PAYMENTS > 0
                    CONTINUE
                 ELSE
                    PERFORM 0700-NOT-SUCCESSFUL-1
                 END-IF
              ELSE
                 PERFORM 0700-NOT-SUCCESSFUL-1
              END-IF
           END-IF.

       0200-CONFIRM-JOB-TYPE.

           MOVE SPACE TO PRINT-LINE
           MOVE '1' TO PRINT-CC
           WRITE PRINT-RECORD

           MOVE PARM-LINE TO PRINT-LINE
           MOVE '-' TO PRINT-CC
           WRITE PRINT-RECORD

           COMPUTE MONTHLY-RATE = (PARM-RATE * POINT-00001) / 12

           IF JOB-TYPE = 3
            MOVE PARM-PRINCIPAL TO WS-REM-BAL
            MOVE PARM-PAYMENTS TO WS-CUR-PAY
            MOVE JOB-LINE-3 TO PRINT-LINE
            MOVE ' ' TO PRINT-CC
            WRITE PRINT-RECORD
           ELSE
            COMPUTE TOTAL-MONTHS = (PARM-YEARS * 12) + PARM-MONTHS
            COMPUTE TEMP-CALC = (1 + MONTHLY-RATE) ** TOTAL-MONTHS
           END-IF

           IF JOB-TYPE = 2
            MOVE PARM-PRINCIPAL TO WS-REM-BAL
            COMPUTE WS-CUR-PAY ROUNDED =
             (WS-REM-BAL * MONTHLY-RATE * TEMP-CALC) / (TEMP-CALC - 1)
            MOVE WS-CUR-PAY TO PRINT-CALC-PAY
            MOVE JOB-LINE-2 TO PRINT-LINE
            MOVE ' ' TO PRINT-CC
            WRITE PRINT-RECORD
           END-IF

           IF JOB-TYPE = 1
            MOVE PARM-PAYMENTS TO WS-CUR-PAY
            COMPUTE WS-REM-BAL ROUNDED =
            (WS-CUR-PAY * (TEMP-CALC - 1)) / (MONTHLY-RATE * TEMP-CALC)
            MOVE WS-REM-BAL TO PRINT-CALC-PRIN
            MOVE JOB-LINE-1 TO PRINT-LINE
            MOVE ' ' TO PRINT-CC
            WRITE PRINT-RECORD
           END-IF

           COMPUTE LOW-BAL-AMOUNT = WS-CUR-PAY * POINT-09.

       0300-PRINT-THE-PAY-SCHEDULE.

           IF PAYMENT-NUMBER > 1200
            PERFORM 0800-NOT-SUCCESSFUL-2
           END-IF

           COMPUTE WS-CUR-INT ROUNDED =  WS-REM-BAL * MONTHLY-RATE

           IF WS-CUR-INT > WS-CUR-PAY
              PERFORM 0900-NOT-SUCCESSFUL-3
           END-IF

           COMPUTE WS-CUR-PRIN = WS-CUR-PAY - WS-CUR-INT

           IF WS-CUR-PRIN > WS-REM-BAL
              MOVE WS-REM-BAL TO WS-CUR-PRIN
              MOVE +0 TO WS-REM-BAL
              COMPUTE WS-CUR-PAY = WS-CUR-PRIN + WS-CUR-INT
           ELSE
              SUBTRACT WS-CUR-PRIN FROM WS-REM-BAL
              IF WS-REM-BAL > +0
                 IF WS-REM-BAL < LOW-BAL-AMOUNT
                    ADD WS-REM-BAL TO WS-CUR-PRIN
                    MOVE +0 TO WS-REM-BAL
                    COMPUTE WS-CUR-PAY = WS-CUR-PRIN + WS-CUR-INT
                 END-IF
              END-IF
           END-IF

           ADD WS-CUR-PRIN TO WS-CUM-PRIN
           ADD WS-CUR-INT  TO WS-CUM-INT
           ADD WS-CUR-PAY  TO WS-CUM-PAY

           MOVE PAYMENT-NUMBER TO PRINT-PAY-NUMBER
           MOVE WS-REM-BAL     TO PRINT-REM-BAL
           MOVE WS-CUR-PRIN    TO PRINT-CUR-PRIN
           MOVE WS-CUR-INT     TO PRINT-CUR-INT
           MOVE WS-CUR-PAY     TO PRINT-CUR-PAY
           MOVE WS-CUM-PRIN    TO PRINT-CUM-PRIN
           MOVE WS-CUM-INT     TO PRINT-CUM-INT
           MOVE WS-CUM-PAY     TO PRINT-CUM-PAY

           IF LINE-CTR = 48
              MOVE HEADER-LINE-1 TO PRINT-LINE
              MOVE '1' TO PRINT-CC
              WRITE PRINT-RECORD
              MOVE HEADER-LINE-2 TO PRINT-LINE
              MOVE ' ' TO PRINT-CC
              WRITE PRINT-RECORD
              MOVE SPACE TO PRINT-LINE
              MOVE ' ' TO PRINT-CC
              WRITE PRINT-RECORD
              MOVE 0 TO LINE-CTR
           END-IF

           MOVE DETAIL-LINE TO PRINT-LINE
           MOVE ' ' TO PRINT-CC
           WRITE PRINT-RECORD
           ADD 1 TO LINE-CTR.

       0600-SUCCESSFUL.
           DISPLAY '*********************************'
           DISPLAY '** THIS EXAMPLE WAS SUCCESSFUL **'
           DISPLAY '*********************************'
           CLOSE PAYMENT-SCHEDULE
           STOP RUN.

       0700-NOT-SUCCESSFUL-1.
           DISPLAY '************************************************'
           DISPLAY '** THIS EXAMPLE WAS NOT SUCCESSFUL BECAUSE'
           DISPLAY '**  INVALID VALUES EXIST IN THE PARM FIELD'
           DISPLAY WS-PARM-DATA
           DISPLAY '************************************************'
           CLOSE PAYMENT-SCHEDULE
           STOP RUN.

       0800-NOT-SUCCESSFUL-2.
           DISPLAY '************************************************'
           DISPLAY '** THIS EXAMPLE WAS NOT SUCCESSFUL BECAUSE'
           DISPLAY '**  TOTAL MONTHLY REPAYMENTS EXCEED 1200'
           DISPLAY '**   CURRENT PAYMENT NUMBER ==> ' PAYMENT-NUMBER
           DISPLAY '************************************************'
           CLOSE PAYMENT-SCHEDULE
           STOP RUN.

       0900-NOT-SUCCESSFUL-3.
           DISPLAY '************************************************'
           DISPLAY '** THIS EXAMPLE WAS NOT SUCCESSFUL BECAUSE'
           DISPLAY '**  CURRENT INTEREST EXCEEDS PAYMENT AMOUNT DUE'
           DISPLAY '**   INTEREST DUE =====> ' WS-CUR-INT
           DISPLAY '**   PAYMENT DUE ======> ' WS-CUR-PAY
           DISPLAY '************************************************'
           CLOSE PAYMENT-SCHEDULE
           STOP RUN.
