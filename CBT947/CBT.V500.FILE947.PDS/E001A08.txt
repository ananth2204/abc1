Quick display of devices

When maintaining numerous VSE systems, each with plenty of
ADD statements, occasionally the Physical Unit Block (PUB)
table fills up.  One must check the IPL procedure carefully
before adding a new device so as not to exceed the limit
imposed by VSE.  This can be extremely awkward if the
devices are specified in ranges such as 'ADD 468:46F,3277'.
Therefore, I decided to write this small program to go straight
into COMREG+X'40 and read through the PUB table before
coming to rest when it detects X'FF.  I have written this
program for CICS 1.7 running under VSE/SP 3.2.  I have hard-
coded the address of the PUB table, therefore VSE/SP 4 users
will have to add an extra few bytes.  I have called the
transaction SYS2, the program SYSPUBP and the mapset
SYSPUBM (the copybook for the map is SYSPUBA).  Note:
there is, included in the program, a useful routine for
converting hex into character; it is called CONVERT.  Figure 1
shows an example of the output as it appears on the screen.


SYSPUBM

* | MAP SYSPUBM  - PROGRAM SYSPUBP  displays VSE PUB table          | *
MAPSET   DFHMSD TYPE=&SYSPARM,                                         C
               MODE=INOUT,                                             C
               CTRL=(FREEKB,FRSET),                                    C
               TIOAPFX=YES,                                            C
               STORAGE=AUTO
         SPACE 2
* SET UP SCREEN                                 *
SYSPUBM  DFHMDI SIZE=(24,80)
         SPACE 2
* AND HEADING                                   *
LINE     DFHMDF POS=(1,1),                                             C
               LENGTH=7,                                               C
               INITIAL='SYSPUBM',                                      C
               ATTRB=(ASKIP,FSET)
         SPACE 2
         DFHMDF POS=(1,10),                                            C
               ATTRB=(ASKIP,BRT),                                      C
               LENGTH=52,                                              C
               INITIAL='               V S E    P U B    T A B L E     C
               '
         SPACE 2
* SHOW EACH ADDRESS IN PUB TABLE -LEFT TO RIGHT *
LINE1    DFHMDF POS=(1,63),LENGTH=16,                                  C
               ATTRB=(FSET,PROT)
         SPACE 2
* SHOW EACH ADDRESS IN PUB TABLE -LEFT TO RIGHT *
LINE3    DFHMDF POS=(3,3),                                             C
               LENGTH=75,                                              C
               ATTRB=(PROT,ASKIP),                                     C
               INITIAL=' '
         SPACE 2
LINE4    DFHMDF POS=(4,3),                                             C
               LENGTH=75,                                              C
               ATTRB=(PROT,ASKIP),                                     C
               INITIAL=' '
         SPACE 2
LINE5    DFHMDF POS=(5,3),                                             C
               LENGTH=75,                                              C
               ATTRB=(PROT,ASKIP),                                     C
               INITIAL=' '
         SPACE 2
LINE6    DFHMDF POS=(6,3),                                             C
               LENGTH=75,                                              C
               ATTRB=(PROT,ASKIP),                                     C
               INITIAL=' '
         SPACE 2
LINE7    DFHMDF POS=(7,3),                                             C
               LENGTH=75,                                              C
               ATTRB=(PROT,ASKIP),                                     C
               INITIAL=' '
         SPACE 2
LINE8    DFHMDF POS=(8,3),                                             C
               LENGTH=75,                                              C
               ATTRB=(PROT,ASKIP),                                     C
               INITIAL=' '
         SPACE 2
LINE9    DFHMDF POS=(9,3),                                             C
               LENGTH=75,                                              C
               ATTRB=(PROT,ASKIP),                                     C
               INITIAL=' '
         SPACE 2
LINE10   DFHMDF POS=(10,3),                                            C
               LENGTH=75,                                              C
               ATTRB=(PROT,ASKIP),                                     C
               INITIAL=' '
         SPACE 2
LINE11   DFHMDF POS=(11,3),                                            C
               LENGTH=75,                                              C
               ATTRB=(PROT,ASKIP),                                     C
               INITIAL=' '
         SPACE 2
LINE12   DFHMDF POS=(12,3),                                            C
               LENGTH=75,                                              C
               ATTRB=(PROT,ASKIP),                                     C
               INITIAL=' '
         SPACE 2
LINE13   DFHMDF POS=(13,3),                                            C
               LENGTH=75,                                              C
               ATTRB=(PROT,ASKIP),                                     C
               INITIAL=' '
         SPACE 2
LINE14   DFHMDF POS=(14,3),                                            C
               LENGTH=75,                                              C
               ATTRB=(PROT,ASKIP),                                     C
               INITIAL=' '
         SPACE 2
LINE15   DFHMDF POS=(15,3),                                            C
               LENGTH=75,                                              C
               ATTRB=(PROT,ASKIP),                                     C
               INITIAL=' '
         SPACE 2
LINE16   DFHMDF POS=(16,3),                                            C
               LENGTH=75,                                              C
               ATTRB=(PROT,ASKIP),                                     C
               INITIAL=' '
         SPACE 2
LINE17   DFHMDF POS=(17,3),                                            C
               LENGTH=75,                                              C
               ATTRB=(PROT,ASKIP),                                     C
               INITIAL=' '
         SPACE 2
LINE18   DFHMDF POS=(18,3),                                            C
               LENGTH=75,                                              C
               ATTRB=(PROT,ASKIP),                                     C
               INITIAL=' '
         SPACE 2
LINE19   DFHMDF POS=(19,3),                                            C
               LENGTH=75,                                              C
               ATTRB=(PROT,ASKIP),                                     C
               INITIAL=' '
         SPACE 2
LINE20   DFHMDF POS=(20,3),                                            C
               LENGTH=75,                                              C
               ATTRB=(PROT,ASKIP),                                     C
               INITIAL=' '
         SPACE 2
* SHOW NUMBER OF ENTRIES IN PUB TABLE           *
LINE22   DFHMDF POS=(22,9),                                            C
               LENGTH=33,                                              C
               ATTRB=(PROT,ASKIP),                                     C
               INITIAL=' NUMBER OF ENTRIES IN PUB TABLE :'
         SPACE 2
L22      DFHMDF POS=(22,44),                                           C
               LENGTH=3,                                               C
               ATTRB=(FSET,PROT,BRT)
         SPACE 2
* RETURN TO CICS INFORMATION                    *
LINE23   DFHMDF POS=(23,9),                                            C
               LENGTH=70,                                              C
               ATTRB=(PROT,ASKIP),                                     C
               INITIAL='        PRESS PF3,PF12 - TO EXIT'
         SPACE 2
* ERROR MESSAGE AREA                            *
ERRMSG   DFHMDF POS=(24,1),                                            C
               LENGTH=79,                                              C
               ATTRB=(PROT,BRT),                                       C
               INITIAL=' '
         SPACE 2
         DFHMSD TYPE=FINAL
         END


SYSPUBP

 * ASSEMBLER PROGRAM TO FIND NUMBER OF       *
 * DEVICES IN PUB TABLE AND DISPLAY WITHIN CICS                       *
 DFHEISTG DSECT
          COPY  SYSPUBA
          COPY  DFHBMSCA
          COPY  DFHCSADS
          EJECT
 PROGRAM  CSECT
          EXEC  CICS ADDRESS CSA(11) NOEDF
          USING DFHCSADS,11
          EXEC  CICS HANDLE AID PF12(CICS)PF3(CICS) ANYKEY(BUFFOON)     *
                 ENTER(MAINRTN)
          EXEC  CICS HANDLE CONDITION ERROR(ERRORS)
          EJECT
 FINDPUB  EQU   *
 R0       EQU   0
 R1       EQU   1
 R2       EQU   2
 R3       EQU   3
 R4       EQU   4
 R5       EQU   5
 R6       EQU   6
 R7       EQU   7
 R8       EQU   8
 R9       EQU   9
 R10      EQU   10
 R11      EQU   11
 R12      EQU   12
 R13      EQU   13
 R14      EQU   14
 R15      EQU   15
          EJECT
 * PLEASE NOTE - SP3 USE 538+40-SP4 USE 550+40                        *
          LH    R10,X'578'            ADDRESS OF PUB MOVED IN
          LA    R9,LINES              ADDRESS OF LINES
          ZAP   COUNT,=PL1'0'         RESET STORAGE FIRST TIME THROUGH
          SPACE 3
 LOOP     EQU   *
          CLI   0(R10),X'FF'          ARE WE AT THE END OF THE PUB
          BE    FINISH                YES - WE BETTER CALL IT A DAY THEN
          LA    R5,2                  NUMBER OF BYTES TO CONVERT
          LA    R6,CHARDEV            ADDRESS OF DISPLAY CHAR FIELD
          LR    R4,R10                ADDRESS OF FIELD IN REG 4
          XR    R2,R2                 CLEAR REGISTER 2
          BAL   R8,CONVERT            ITS IN HEX FORMAT SO LETS CONVERT
          MVC   0(4,R9),CHARDEV       MOVE IN CHARS TO APPROP. POSITION
          LA    R9,5(,R9)             ADVANCE SCREEN POSITION
          AP    COUNT,=PL1'1'         INCREMENT COUNTER
          LA    R10,8(,R10)           INDEX TO THE NEXT PUB ENTRY
          B     LOOP                  GO AND DO NEXT PUB ENTRY
          SPACE 3
 FINISH   EQU   *
          MVC   FOURBYTE,PATTRN       MOVE IN THE EDIT PATTERN
          ED    FOURBYTE,COUNT        AND EDIT COUNT
          SPACE 3
 MAINRTN  EQU   *
          MVC   LINE3O,DATA3          FIRST FIFTEEN DEVICES
          MVC   LINE4O,DATA4          AND NEXT FIFTEEN
          MVC   LINE5O,DATA5          AND SO ON....
          MVC   LINE6O,DATA6
          MVC   LINE7O,DATA7
          MVC   LINE8O,DATA8
          MVC   LINE9O,DATA9
          MVC   LINE10O,DATA10
          MVC   LINE11O,DATA11
          MVC   LINE12O,DATA12
          MVC   LINE13O,DATA13
          MVC   LINE14O,DATA14
          MVC   LINE15O,DATA15
          MVC   LINE16O,DATA16
          MVC   LINE17O,DATA17
          MVC   LINE18O,DATA18
          MVC   LINE19O,DATA19
          MVC   LINE20O,DATA20
          MVC   L22O,FOURBYTE+1       AND MOVE FIELD TO SCREEN
          B     SNDMAP
          SPACE 2
 ERRORS   EQU   *
          MVC   ERRMSGO,=CL79' ERRORS  CALL YOUR FRIENDLY SYSPROG'
          B     SNDMAP
          SPACE 2
 BUFFOON  EQU   *
          MVC   ERRMSGO,=CL79'     YOU HAVE HIT A WRONG KEY      '
          B     MAINRTN               SET UP MAP AGAIN
          SPACE 2
 SNDMAP   EQU   *
          EXEC  CICS SEND MAP('SYSPUBM') ERASE
          EXEC  CICS RECEIVE MAP('SYSPUBM')
          SPACE 2
 CICS     EQU   *
          MVC   MSGAREA,=CL19'NORMAL END OF TASK'
          EXEC  CICS SEND FROM(MSGAREA) ERASE
          EXEC  CICS RETURN
          SPACE 2
          EJECT
 * WE NEED THE FOLLOWING ROUTINE TO CONVERT HEX DEVICE INTO CHARACTER *
 * THIS ROUTINE REQUIRES R5 ... NUMBER OF BYTES TO CONVERT            *
 *                       R4 ... ADDRESS OF START OF CONVERSION        *
 *                       R6 ... ADDRESS OF PRINT FIELD                *
 *                       R2 ... CLEAR THIS REG BY XR                  *
 CONVERT  EQU   *
          IC    R2,0(R4)
          SRL   R2,4
          STC   R2,0(R6)
          TR    0(1,R6),TABLE
          IC    R2,0(R4)
          N     R2,NUM
          STC   R2,1(R6)
          TR    1(1,R6),TABLE
          LA    R4,1(R4)
          LA    R6,2(R6)
          BCT   R5,CONVERT
          BR    R8
          EJECT
 * ALL STORAGE FOLLOWS BELOW.....                                     *
          LTORG
          EJECT
          CNOP  0,4
 MSGAREA  DS    CL19
 TABLE    DS    0CL256
          DC    CL16'0123456789ABCDEF'
          DC    240CL1'?'
 FULLWORD DS    F
 NUM      DC    X'0000000F'
 CUUDEV   DC    XL2'00'               ADDRESS IN HEX
 CHARDEV  DC    XL4'F0'               ADDRESS IN CHARACTER
 COUNT    DC    PL2'0'                COUNT NUMBER OF DEVICES
 PATTRN   DC    X'40212020'           PACKED COUNT FIELD PATTERN
 FOURBYTE DS    CL4                   FOUR BYTE WORK FIELD
 LINES    DS    0CL1275
 DATA3    DC    CL75' '
 DATA4    DC    CL75' '
 DATA5    DC    CL75' '
 DATA6    DC    CL75' '
 DATA7    DC    CL75' '
 DATA8    DC    CL75' '
 DATA9    DC    CL75' '
 DATA10   DC    CL75' '
 DATA11   DC    CL75' '
 DATA12   DC    CL75' '
 DATA13   DC    CL75' '
 DATA14   DC    CL75' '
 DATA15   DC    CL75' '
 DATA16   DC    CL75' '
 DATA17   DC    CL75' '
 DATA18   DC    CL75' '
 DATA19   DC    CL75' '
 DATA20   DC    CL75' '
          END


Simon Connelly
Systems Programmer
E R Squibb (UK)
