A routine to intercept and analyse data exceptions

When data exceptions occur and a job is terminated, one
realizes that it would be very useful if more occurrences of data
exceptions could be found in a single test run. DIAGRTN will
allow up to 100 data exceptions to occur before terminating the
job. This can be extremely useful when doing conversions from
non-IBM to IBM systems.

The routine should be called as early as possible in the COBOL
program, so that trapping of data exceptions can occur.
DIAGRTN will produce a partition dump on the first
occurrence of a data exception, and an analysis of up to 100
data exceptions. If an error - other than a data exception -
occurs, the program is terminated with a dump and an
appropriate message. The dump may be suppressed by
including a //UPSI 01 card in the JCL for the job.

Note: the COBOL options STATE and FLOW must not be
specified if DIAGRTN is called.

The routine is called by including the following statement in
the COBOL program:

      CALL 'DIAGRTN'.

The source code for DIAGRTN follows. Assemble the source
code and catalogue the relocatable module into a relocatable
library. The relocatable library should be in the standard
relocatable library searchlist, so that the module may be
included in the calling program at link edit time.

         TITLE 'DIAGRTN - DIAGNOSTICS ROUTINE'
DIAGRTN  START
         USING DIAGRTN,15
         SAVE  (14,12)                 STORE REGISTERS
         STXIT PC,AAA,SAVE             ENTRY ADDRESS
         OPEN  LISTER
         RETURN (14,12)                RETURN
         DROP  15
         SPACE 2
*              DATA ERROR RECOVERY ROUTINE
         SPACE 2
AAA      BALR  10,0
         USING *,10
         LA    11,SAVE
         SPACE 2
*        R10   BASE REGISTER
*        R11   ADDRESS OF WEIGHT
         SPACE 2
         EXTRACT ID=BDY,AREA=PARTMAP,LEN=20
         CLI   3(11),X'07'             RECOVERABLE ERROR?
         BE    P110                    YES
P050     EQU   *
P100     PUT   LISTER,UNKNOWN          UNKNOWN ERROR
         B     P260
P110     EQU   *
         CP    COUNT,PZERO             FIRST ERROR
         BNE   P130                    NO
P120     PUT   LISTER,STARTD
         L     2,PBEGIN
         L     3,PENDLOG
         PDUMP (2),(3)                 TAKE A DUMP
P130     AP    COUNT,PONE              AUGMENT ERROR COUNT
         SLR   2,2
         IC    2,4(11)                 EXTRACT P-CNTR
         SRL   2,6                     GET INSTRUCTION LENGTH BITS
         SLL   2,1                     MULTIPLY BY 2
         ST    2,SAVEA
         L     2,4(11)                 EXTRACT P-CNTR
         S     2,SAVEA                 BACKDATE
         ST    2,SAVEA                 STORE P-CNTR
         USING NSTR,2
P151     EQU   *
         LA    3,3                     LENGTH OF PC
         LA    4,SAVEA                 ADDRESS OF P-CNTR
         BAL   12,CONVERT              CONVERT PC
         MVC   PC(6),UAREA+5           SET UP P-CNTR
         SLR   3,3                     LENGTH OF WEIGHT
         LA    4,3(11)                 A(WEIGHT)
         BAL   12,CONVERT              CONVERT WEIGHT
         MVC   WT(2),UAREA+9           SET UP WEIGHT
         LH    5,ADDR                  (A) ADDRESS
         BAL   12,RESOLVE              RESOLVE (A) ADDRESS
         ST    4,SAVEA
         LA    4,SAVEA                 ADDR OF (A) ADDRESS
         LA    3,3                     LENGTH OF ADDRESS
         BAL   12,CONVERT              CONVERT (A) ADDRESS
         MVC   AA(6),UAREA+5           SET UP AA
         SLR   4,4                     GET (A) LENGTH
         LA    3,240                   FO
         IC    4,LNGTH
         NR    3,4
         SRL   3,4                     (A) LNGTH IN R3
         LA    3,1(3)
         STH   3,LNGA
         BCTR  3,0
         L     4,SAVEA                 AD ADDRESS
         BAL   12,CONVERT              CONVERT AD TO DISPLAY
         MVC   AD(10),UAREA+1          SET UP ADATA
         LH    5,BDDR                  (B) ADDRESS
         BAL   12,RESOLVE              RESOLVE (B) ADDRESS
         ST    4,SAVEB
         LA    4,SAVEB                 ADDR OF (B) ADDRESS
         LA    3,3                     LENGTH OF ADDRESS
         BAL   12,CONVERT              CONVERT (B) ADDRESS
         MVC   BA(6),UAREA+5           SET UP BA
         SLR   4,4                     GET (B) LENGTH
         LA    3,15                    OF
         IC    4,LNGTH
         NR    3,4                     (B) LNGTH IN R3
         LA    3,1(3)
         STH   3,LNGB
         BCTR  3,0
         L     4,SAVEB                 BD ADDRESS
         BAL   12,CONVERT              CONVERT BD TO DISPLAY
         MVC   BD(10),UAREA+1          SET UP BDATA
         MVC   PSVE(6),PC              GET P-CNTR
         CLC   PSVE(6),LSTP            SAME AS LAST?
         BNE   P158                    NO
         AP    SCOUNT,PONE             AUGMENT SAME COUNT
         B     P165
P158     CP    SCOUNT,PZERO            ANY DUPLICATES?
         BE    P159                    NO
         UNPK  DISCNT(9),SCOUNT        DISPLAY DUPLICATE COUNT
         OI    DISCNT+8,240
         PUT   LISTER,OCCURS
         ZAP   SCOUNT,PZERO
P159     MVC   LSTP(6),PSVE            STORE THIS ERROR
P160     PUT   LISTER,DIAG
P165     EQU   *
P250     CP    COUNT,PHUN              MORE THAN 100 ERRORS?
         BNH   P270                    NO
         CP    SCOUNT,PZERO            ANY DUPLICATES?
         BE    P260
         UNPK  DISCNT(9),SCOUNT        DISPLAY DUPLICATE COUNT
         OI    DISCNT+8,240
         PUT   LISTER,OCCURS
P260     EQU   *
         COMRG REG=1                   GET COMREG ADDRESS
         TM    23(1),64                IS DUMP REQUIRED ?
         BO    P265                    NO
         L     2,PBEGIN
         L     3,PENDLOG
         PDUMP (2),(3)                 TAKE A DUMP
P265     EQU   *
         PUT   LISTER,ENDMSG
         EOJ
P270     EQU   *
         EXIT  PC
         SPACE 2
*              ROUTINE TO CONVERT TO DISPLAY
         SPACE 2
CONVERT  EX    3,MOVE                  MOVE DATA TO WAREA
         LA    3,1(3)                  AUGMENT LNGTH FOR UNPK
         EX    3,UNPK
         TR    UAREA(11),TABLE-240     TRANSLATE FOR DISPLAY
         BR    12                      RETURN
         SPACE 2
*              ROUTINE TO RESOLVE ADDRESS IN BDDD FORMAT
         SPACE 2
RESOLVE  LA    4,240                   FO
         SLL   4,8                     FOOO
         NR    4,5
         SRL   4,10                    BASE * 4
         L     4,SAVE+8(4)
         LA    6,4095                  0FFF
         NR    6,5                     DISPL IN R6
         AR    4,6                     FINAL ADDR IN R4
         BR    12
         SPACE 2
*              GENERAL WORKING STORAGE
         SPACE 2
COUNT    DC    PL5'0'                  ERROR COUNT
SCOUNT   DC    PL5'0'                  DUPLICATE COUNT
UNKNOWN  DC    CL80'0 ERROR - R11 = ADDRESS OF P-COUNTER'
STARTD   DC    CL80'0 DIAGRTN - DIAGNOSTICS'
ENDMSG   DC    CL80'0 TERMINATED BY DIAGRTN'
OCCURS   DC    CL27'0 THE ABOVE ERROR OCCURRED '
DISCNT   DC    CL9' '                  DISPLAY COUNT
         DC    CL44' TIMES'
PONE     DC    PL1'1'                  CONSTANT 1
PSVE     DC    CL6' '                  THIS P-CNTR
LSTP     DC    CL6' '                  LAST P-CNTR
WAREA    DC    CL16' '                 DATA STORAGE
UAREA    DC    CL12' '                 UNPACK AREA
TABLE    DC    C'0123456789ABCDEF'
DIAG     DS    0CL80
         DC    C'O ERROR = '
WT       DC    CL2' '                  INTERRUPT WEIGHT
         DC    C' - '
         DC    C'PC='
PC       DC    CL6' '
         DC    C','
         DC    C'AA='
AA       DC    CL6' '
         DC    C','
         DC    C'AD='
AD       DC    CL10' '                 A DATA
         DC    C','
         DC    C'BA='
BA       DC    CL6' '
         DC    C','
         DC    C'BD='
BD       DC    CL10' '                  B DATA
         DC    CL8' '
PZERO    DC    PL1'0'
PHUN     DC    PL2'100'
         SPACE 2
*              ALIGNED WORKING STORAGE
         SPACE 2
SAVE     DS    0D                      SAVE AREA
         DS    D                       PSW
         DS    16F                     REGISTER STORAGE
SAVEA    DS    F                       RESOLVED (A) ADDRESS
SAVEB    DS    F                       RESOLVED (B) ADDRESS
LNGA     DS    H                       (A) DATA LENGTH
LNGB     DS    H                       (B) DATA LENGTH
DWORK    DS    D                       DOUBLE WORK AREA
PARTMAP  MAPBDY DSECT=NO
         SPACE 2
NSTR     DSECT
OPCODE   DS    C
LNGTH    DS    C
ADDR     DS    CL2
BDDR     DS    CL2
DIAGRTN  CSECT
         SPACE 2
*              INSTRUCTION WORKING STORAGE
         SPACE 2
         USING AAA+2,10
MOVE     MVC   WAREA(1),0(4)
UNPK     UNPK  UAREA(12),WAREA(1)
         SPACE 2
LISTER   DTFPR DEVADDR=SYSLST,                                        X
               BLKSIZE=80,                                            X
               CTLCHR=ASA,                                            X
               DEVICE=1403,                                           X
               RECFORM=FIXUNB,                                        X
               IOAREA1=OUTAREA,                                       X
               MODNAME=IJDFAZZW
OUTAREA  DS    CL80
         SPACE 2
         LTORG
         END


M Nunan
Systems Programmer
Sage Computing (South Africa)             c Sage Computing 1992

