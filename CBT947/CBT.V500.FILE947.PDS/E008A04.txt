POWER queue rotation and management

This program illustrates the use of the CTLSPOOL interface
for issuing POWER/VS commands from a batch program. The
program can be used to alter and delete POWER LST, PUN,
and RDR queues. Put this program in JCL that is executed
every day, for example back-ups.  A queue management
routine can be put into effect that will keep the POWER data
file from becoming full with old reports. The following JCL
illustrates how to implement a one day rotation of POWER
queue class A.

// JOB DAILYBK EXECUTED ONCE EVERYDAY
.
.
// EXEC POWERCMD
L LST,D                 DELETE ALL ENTRIES IN CLASS D
A LST,A,CLASS=D         ALTER ALL CLASS A TO CLASS D
/*
.
.

Any valid POWER/VS RELEASE command will be accepted.
A message is issued on the console when the command is
passed to POWER/VS, and again when the command has been
executed.

Prerequisites:  the DOS/VS supervisor must specify support for
cross partition event control blocks, ie 'XECB=YES', and
POWER must specify support for the CTLSPOOL interface, ie
'SPOOL=YES'.

POWERCMD TITLE 'POWER COMMANDS / SYSIPT'
         PUNCH '   PHASE   POWERCMD,S,NOAUTO      '
         PUNCH '  INCLUDE  ,(POWERCMD)            '
         PUNCH '  INCLUDE  ,(IJSYSLOG)            '
         PUNCH '  INCLUDE  IJJFCIZD               '
         PRINT NOGEN
POWERCMD START X'78'
R5       EQU   5               BASE
R8       EQU   8               BASE FOR SPLMAP
R9       EQU   9               POWERCMD BASE REGISTER
R10      EQU   10
R7       EQU   7
R0       EQU   0               POINTER
R1       EQU   1               POINTER
 SPACE 2 TITLE 'POWERCMD - AUTOMATIC QUEUE MANAGEMENT FACILITY'
         BALR  R9,0
         BCTR  R9,0
         BCTR  R9,0
         USING POWERCMD,R9
         B     START
         DC    C'POWERCMD 1.0'
         EJECT ,
START    DS    0H
* DEFINE THE XECB FOR POWER/VS COMMUNICATION
         XECBTAB TYPE=DEFINE,XECB=SPMXECB,ACCESS=XWAIT
* REGISTER 15 WILL CONTAIN THE RETURN CODE - SHOULD BE ZERO
* REGISTER  1 WILL CONTAIN THE ADDRESS OF THE XECB
* REGISTER 14 WILL CONTAIN THE ADDRESS OF THE TABLE ENTRY
         LTR   15,15               DID XECB DEFINE OKAY ?
         BNZ   XECBERR             IF NOT, ISSUE ERROR MSG
*
         OPENR CARD,CONS        OPEN DTF
GETCARD  MVI   IOAREA1,C' '    CLEAR INPUT AREA
         MVC   IOAREA1+1(L'IOAREA1-1),IOAREA1
         GET   CARD                READ A CARD INTO IOAREA1
* REVIEW THE TESTS BELOW TO EDIT THE COMMANDS YOU WISH TO ACCEPT
         CLI   IOAREA1,C'L'             IS IT A DELETE COMMAND ?
         BE    OK                       BRANCH IF YES
         CLI   IOAREA1,C'R'             IS IT A RELEASE COMMAND ?
         BE    OK                       BRANCH IF YES
         CLI   IOAREA1,C'A'             IS IT AN 'ALTER' COMMAND ?
         BNE   INVCMD                   NO, REJECT COMMAND
OK       LA    R7,72                    SET LENGTH OF MESSAGE
         MVC   PWRMSG1(71),IOAREA1      MOVE CARD TO OUTPUT AREA
*=====>> PUT   CONS,PWRMSG              DISPLAY CARD READ ON CONSOLE
         LA    R5,SPL1                  LOAD ADDRESS OF SPL
         USING SPLMAP,R5
         XC    SPPW,SPPW                SET PASSWORD TO 'MASTER'
ISSUE    CTLSPOOL SPL=(5),PBUF=IOAREA1,REQ=COMMAND   ISSUE COMMAND
         CLI   SPMERROR,X'00'           TEST RETURN CODE FROM CTLSPOOL
         BE    CONSOLE                  ACCEPT 00 RETURN CODE
         CLI   SPMERROR,X'42'          TEST FOR 42 RETURN CODE AND
         BE    CONSOLE                 ACCEPT IT
         SPACE ,
         LA    R7,64                   NOT VALID RETURN CODE, SO
         MVC   PWRMSG1(71),IOAREA1      MOVE RESULT OF COMMAND
         MVC   PWRMSG1+35(L'CMDERR),CMDERR
         PUT   CONS,PWRMSG             SEND MESSAGE TO CONSOLE
         B     GETCARD                 GO TRY NEXT CARD
         SPACE ,
CONSOLE  LA    R7,64                   LOAD LENGTH OF MESSAGE
         MVC   PWRMSG1(71),IOAREA1      MOVE RESULT OF COMMAND
         PUT   CONS,PWRMSG             DISPLAY RESULT ON CONSOLE
         B     GETCARD                 GO READY ANOTHER COMMAND CARD
         SPACE ,
INVCMD   LA    R7,64                   ISSUE ERROR MESSAGE TO
         MVC   PWRMSG1(71),IOAREA1      MOVE RESULT OF COMMAND
         MVC   PWRMSG1+35(L'INVMSG),INVMSG
         PUT   CONS,PWRMSG             CONSOLE, AND GET THE NEXT
         B     GETCARD                 COMMAND CARD
         SPACE ,
XECBERR  DS    0H'0'                   XECB DID NOT DEFINE PROPERLY
         LA    R7,38                   SO ISSUE ERROR MESSAGE TO
         PUT   CONS,XERRMSG            CONSOLE AND EXIT PROGRAM
EOJ      EOJ   ,
         SPACE ,
SPL1     SPL   TYPE=DEFINE,REQ=COMMAND,PBUF=IOAREA1   DEFINE SPL
         PRINT NOGEN
CONS     DTFCN  TYPEFLE=INPUT,DEVADDR=SYSLOG,IOAREA1=CONSWORK,         C
               RECFORM=UNDEF,RECSIZE=(7),MODNAME=IJSYSLOG,             C
               BLKSIZE=80,WORKA=YES
CARD     DTFDI DEVADDR=SYSIPT,IOAREA1=IOAREA1,EOFADDR=EOJ,RECSIZE=80
PWRMSG   DC    CL09'POWERCMD-'
PWRMSG1  DC    CL71' '
CONSWORK DC    CL80' '    CONSOLE WORK AREA
IOAREA1  DC    CL80' '
         DC    CL08' '     POWER/VS BUFFER MUST BE 88 BYTES
XERRMSG  DC    CL38'POWERCMD FAILED DURING XECB DEFINITION'
CMDERR   DC    C'ERROR DURING CTLSPOOL'
INVMSG   DC    C'INVALID PARAMETER CARD FOUND'
SPMXECB  DC    F'0'       XECB
SPMERROR DC    XL1'00'    ERROR RETURN CODE
SPMSPLP  DC    AL3(SPL1)  POINTER TO SPL
LTORG    LTORG ,
POWERCMD CSECT ,
         PRINT GEN
SPLMAP   SPL   TYPE=MAP,SPMXECB=SPMXECB  SPL DSECT REQUIRED
         END   ,

Ray Lightcap
Lead Systems Programmer
CyCare Systems Inc (USA)                            c CyCare Systems Inc 1992

