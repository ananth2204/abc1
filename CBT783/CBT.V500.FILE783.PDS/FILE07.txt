//JOBF  JOB 111-1111,'SSP PL/1',MSGLEVEL=1
//MDSC EXEC PL1LFCLG,PARM.PL1L='NOLIST,NOATR,NOXREF,CHAR48'
//PL1L.SYSIN  DD  *
 MDSC..                                                                 MDSC  10
 /*********************************************************************/MDSC  20
 /*                                                                   */MDSC  30
 /*      TO READ THE PROBLEM PARAMETER CARD AND DATA FOR DISCRIMINANT */MDSC  40
 /*      ANALYSIS, CALL THE PROCEDURES TO CALCULATE VARIABLE MEANS IN */MDSC  50
 /*      EACH GROUP, POOLED DISPERSION MATRIX, COMMON COEFFICIENTS OF */MDSC  60
 /*      DISCRIMINANT FUNCTIONS AND PROBABILITY ASSOCIATED WITH LARG- */MDSC  70
 /*      EST DISCRIMINANT FUNCTION OF EACH CASE IN EACH GROUP, AND    */MDSC  80
 /*      PRINT THE RESULTS.                                           */MDSC  90
 /*                                                                   */MDSC 100
 /*********************************************************************/MDSC 110
    PROCEDURE OPTIONS (MAIN),.                                          MDSC 120
    DECLARE                                                             MDSC 130
         (I,J,K,L,M,N1,N2,NN)                                           MDSC 140
         FIXED BINARY,                                                  MDSC 150
         PR1 CHARACTER (6),                                             MDSC 160
         ERROR EXTERNAL CHARACTER (1),                                  MDSC 170
         (NCARD,NV) EXTERNAL,                                           MDSC 180
         CH  CHARACTER (80),.                                           MDSC 190
 /*                                                                   */MDSC 200
    ON ENDFILE (SYSIN) GO TO EXIT,.                                     MDSC 210
 S100..                                                                 MDSC 220
    GET EDIT (CH) (A(80)),.                                             MDSC 230
    GET STRING (CH) EDIT (PR1,K,M,NCARD) (A(6),3 F(2)),.                MDSC 240
 /*                                                                   */MDSC 250
 /*      PR1......PROBLEM NUMBER (MAY BE ALPHAMERIC)                  */MDSC 260
 /*      K........NUMBER OF GROUPS                                    */MDSC 270
 /*      M........NUMBER OF VARIABLES                                 */MDSC 280
 /*      NCARD....NUMBER OF CARDS PER OBSERVATION                     */MDSC 290
 /*                                                                   */MDSC 300
    NCARD=NCARD*80,.                                                    MDSC 310
    NV   =0,.                                                           MDSC 320
 ONE..                                                                  MDSC 330
    BEGIN,.                                                             MDSC 340
    DECLARE                                                             MDSC 350
         N(K)  FIXED BINARY,                                            MDSC 360
         (XBAR(M,K),C(M+1,K),D(M,M),CMEAN(M),DET,V,CON)                 MDSC 370
         BINARY FLOAT,.                /*SINGLE PRECISION VERSION  /*S*/MDSC 380
  /*     BINARY FLOAT (53),.           /*DOUBLE PRECISION VERSION  /*D*/MDSC 390
 /*                                                                   */MDSC 400
 /*      READ SAMPLE SIZE OF EACH GROUP                               */MDSC 410
 /*                                                                   */MDSC 420
    GET EDIT (CH) (A(80)),.                                             MDSC 430
    GET STRING (CH) EDIT ((N(I) DO I= 1 TO K))                          MDSC 440
         (25 F(3)),.                                                    MDSC 450
    NN   =SUM (N),.                                                     MDSC 460
 TWO..                                                                  MDSC 470
    BEGIN,.                                                             MDSC 480
    DECLARE                                                             MDSC 490
         LG(NN)  FIXED BINARY,                                          MDSC 500
         X(NN,M) FLOAT BINARY,                                          MDSC 510
         P(NN)                                                          MDSC 520
         BINARY FLOAT,.                /*SINGLE PRECISION VERSION  /*S*/MDSC 530
  /*     BINARY FLOAT (53),.           /*DOUBLE PRECISION VERSION  /*D*/MDSC 540
    PUT EDIT ('DISCRIMINANT ANALYSIS.....',PR1, '  NUMBER OF GROUPS',K, MDSC 550
         '  NUMBER OF VARIABLES',M,'  SAMPLE SIZES..','GROUP')          MDSC 560
         (PAGE,SKIP(4),COLUMN(10),A,A,SKIP(2),COLUMN(10),A,X(7),F(3),   MDSC 570
         SKIP(1),COLUMN(10),A,F(7),SKIP(1),COLUMN(10),A,SKIP(1),        MDSC 580
         COLUMN(22),A),.                                                MDSC 590
    PUT EDIT ((I,N(I) DO I= 1 TO K)) (SKIP(1),COLUMN(22),F(3),X(8),     MDSC 600
         F(4)),.                                                        MDSC 610
    PUT EDIT ('  ') (SKIP(2),A),.                                       MDSC 620
 /*                                                                   */MDSC 630
 /*      READ IN DATA. IN THE MANNER EQUIVALENT TO A 3-DIMENSIONAL    */MDSC 640
 /*      ARRAY X(1,1,1),X(2,1,1),X(3,1,1),ETC. THE FIRST SUBSCRIPT    */MDSC 650
 /*      IS THE CASE NUMBER,THE SECOND SUBSCRIPT IS THE VARIABLE      */MDSC 660
 /*      NUMBER AND THE THIRD SUBSCRIPT IS THE GROUP NUMBER           */MDSC 670
 /*                                                                   */MDSC 680
         DO I = 1 TO NN,.                                               MDSC 690
         CALL DAT2 (M,CMEAN),.                                          MDSC 700
              DO J = 1 TO M,.                                           MDSC 710
              X(I,J)=CMEAN(J),.                                         MDSC 720
              END,.                                                     MDSC 730
         END,.                                                          MDSC 740
    CALL DMTX (K,M,N,X,XBAR,D),.                                        MDSC 750
    IF ERROR NE '0'                                                     MDSC 760
    THEN DO,.                                                           MDSC 770
         PUT EDIT ('IN ROUTINE DMTX ERROR CODE = ',ERROR)               MDSC 780
              (SKIP(2),COLUMN(10),A,A(1)),.                             MDSC 790
         GO TO FIN,.                                                    MDSC 800
         END,.                                                          MDSC 810
 /*                                                                   */MDSC 820
 /*      PRINT MEANS AND POOLED DISPERSION MATRIX                     */MDSC 830
 /*                                                                   */MDSC 840
         DO I = 1 TO K,.                                                MDSC 850
         PUT EDIT ('GROUP',I,'MEANS') (SKIP(2),COLUMN(11),A,F(3),X(2),  MDSC 860
              A),.                                                      MDSC 870
         PUT EDIT ((XBAR(J,I) DO J= 1 TO M)) (SKIP(1),COLUMN(10),       MDSC 880
              (6)F(15,5)),.                                             MDSC 890
         END,.                                                          MDSC 900
    PUT EDIT ('POOLED DISPERSION MATRIX') (SKIP(3),COLUMN(10),A),.      MDSC 910
         DO I = 1 TO M,.                                                MDSC 920
         PUT EDIT ('ROW',I) (SKIP(2),COLUMN(10),A,F(3)),.               MDSC 930
         PUT EDIT ((D(I,J) DO J= 1 TO M)) (SKIP,COLUMN(10),6 F(15,5)),. MDSC 940
         END,.                                                          MDSC 950
    CON  =0,.                                                           MDSC 960
    CALL MINV (D,M,DET,CON),.                                           MDSC 970
    IF ERROR NE '0'                                                     MDSC 980
    THEN DO,.                                                           MDSC 990
         PUT EDIT ('IN ROUTINE MINV ERROR CODE = ',ERROR) (SKIP(2),     MDSC1000
              COLUMN(10),A,A(1)),.                                      MDSC1010
         GO TO CONT,.                                                   MDSC1020
         END,.                                                          MDSC1030
    CALL DSCR (K,M,N,X,XBAR,D,CMEAN,V,C,P,LG),.                         MDSC1040
    IF ERROR NE '0'                                                     MDSC1050
    THEN DO,.                                                           MDSC1060
         PUT EDIT ('IN ROUTINE DSCR ERROR CODE = ',ERROR)               MDSC1070
              (SKIP(2),COLUMN(10),A,A(1)),.                             MDSC1080
         GO TO S100,.                                                   MDSC1090
         END,.                                                          MDSC1100
 /*                                                                   */MDSC1110
 /*      PRINT THE COMMON MEANS.                                      */MDSC1120
 /*                                                                   */MDSC1130
    PUT EDIT ('COMMON MEANS') (SKIP(4),COLUMN(10),A),.                  MDSC1140
    PUT EDIT ((CMEAN(I) DO I= 1 TO M)) (SKIP,COLUMN(10),(6)F(15,5)),.   MDSC1150
 /*                                                                   */MDSC1160
 /*      PRINT GENERALIZED MAHALANOBIS D-SQUARE                       */MDSC1170
 /*                                                                   */MDSC1180
    PUT EDIT ('GENERALIZED MAHALANOBIS D-SQUARE',V)                     MDSC1190
         (SKIP(4),COLUMN(10),A,F(15,5),SKIP(2)),.                       MDSC1200
 /*                                                                   */MDSC1210
 /*      PRINT CONSTANTS AND COEFFICIENTS OF DISCRIMINANT FUNCTIONS   */MDSC1220
 /*                                                                   */MDSC1230
         DO I = 1 TO K,.                                                MDSC1240
         PUT EDIT ('DISCRIMINANT FUNCTION',I,'CONSTANT   *',            MDSC1250
              'COEFFICIENTS') (SKIP(2),COLUMN(10),A,F(3),SKIP(2),       MDSC1260
              COLUMN(16),A,X(3),A),.                                    MDSC1270
         PUT EDIT (C(1,I),'   *   ') (SKIP(2),COLUMN(10),F(14,5),A),.   MDSC1280
         PUT EDIT ((C(J,I) DO J= 2 TO M+1)) (SKIP,COLUMN(32),           MDSC1290
              (6)F(14,5)),.                                             MDSC1300
         END,.                                                          MDSC1310
 /*                                                                   */MDSC1320
 /*      PRINT EVALUATION OF CLASSIFICATION FUNCTIONS OF EACH         */MDSC1330
 /*      OBSERVATION.                                                 */MDSC1340
 /*                                                                   */MDSC1350
    PUT EDIT ('EVALUATION OF CLASSIFICATION FUNCTIONS FOR EACH',        MDSC1360
         ' OBSERVATION') (SKIP(4),COLUMN(10),A,A),.                     MDSC1370
    N1   =1,.                                                           MDSC1380
    N2   =N(1),.                                                        MDSC1390
         DO I = 1 TO K,.                                                MDSC1400
         PUT EDIT ('GROUP',I,'PROBABILITY ASSOCIATED WITH','LARGEST',   MDSC1410
              ' OBSERVATION','LARGEST DISCRIMINANT FUNCTION',           MDSC1420
              'FUNCTION NO.')                                           MDSC1430
              (SKIP(2),COLUMN(10),A,F(3),SKIP,COLUMN(28),A,X(11),A,     MDSC1440
              SKIP,COLUMN(10),A,X(5),A,X(8),A),.                        MDSC1450
         L    =0,.                                                      MDSC1460
              DO J = N1 TO N2,.                                         MDSC1470
              L    =L+1,.                                               MDSC1480
              PUT EDIT (L,P(J),LG(J)) (SKIP,COLUMN(10),F(6),X(20),F(8,5)MDSC1490
                   ,X(20),F(6)),.                                       MDSC1500
              END,.                                                     MDSC1510
         IF I = K                                                       MDSC1520
         THEN GO TO CONT,.                                              MDSC1530
         N1   =N1+N(I),.                                                MDSC1540
         N2   =N2+N(I+1),.                                              MDSC1550
         END,.                                                          MDSC1560
 CONT..                                                                 MDSC1570
    END,.                                                               MDSC1580
    END,.                                                               MDSC1590
    GO TO S100,.                                                        MDSC1600
 EXIT..                                                                 MDSC1610
    PUT FILE (SYSPRINT) EDIT ('END OF SAMPLE PROGRAM')                  MDSC1620
         (SKIP(5),COLUMN(10),A),.                                       MDSC1630
 FIN..                                                                  MDSC1640
    END,.                              /*END OF PROCEDURE MDSC        */MDSC1650
*PROCESS('CHAR48,NOLIST,NOATR,NOXREF');
 DMTX..                                                                 DMTX  10
 /*********************************************************************/DMTX  20
 /*                                                                   */DMTX  30
 /*      TO COMPUTE MEANS OF VARIABLES IN EACH GROUP AND A POOLED     */DMTX  40
 /*      DISPERSION MATRIX FOR ALL THE GROUPS.                        */DMTX  50
 /*                                                                   */DMTX  60
 /*********************************************************************/DMTX  70
    PROCEDURE (K,M,N,X,XBAR,D),.                                        DMTX  80
    DECLARE                                                             DMTX  90
         ERROR EXTERNAL CHARACTER (1),                                  DMTX 100
         (N(*),I,J,K,K1,K2,KK,L,M,NN)                                   DMTX 110
         FIXED BINARY,                                                  DMTX 120
         (X(*,*),FSUM)                                                  DMTX 130
         FLOAT BINARY,                                                  DMTX 140
         (XBAR(*,*),D(*,*),CMEAN(M))                                    DMTX 150
         BINARY FLOAT,.                /*SINGLE PRECISION VERSION  /*S*/DMTX 160
  /*     BINARY FLOAT (53),.           /*DOUBLE PRECISION VERSION  /*D*/DMTX 170
 /*                                                                   */DMTX 180
    ERROR='0',.                                                         DMTX 190
    IF M LE 1                          /* THE NUMBER OF VARIABLES IS  */DMTX 200
    THEN DO,.                          /* LESS THAN OR EQUAL TO ONE.  */DMTX 210
         ERROR='1',.                                                    DMTX 220
         GO TO FIN,.                                                    DMTX 230
         END,.                                                          DMTX 240
    IF K LE 1 OR K GT M                /* INVALID NUMBER OF GROUPS.   */DMTX 250
    THEN DO,.                                                           DMTX 260
         ERROR='2',.                                                    DMTX 270
         GO TO FIN,.                                                    DMTX 280
         END,.                                                          DMTX 290
         DO J = 1 TO K,.                                                DMTX 300
         IF N(J) LE 0                  /* NO OBSERVATIONS IN AT LEAST */DMTX 310
         THEN DO,.                     /* ONE OF THE GROUPS           */DMTX 320
              ERROR='3',.                                               DMTX 330
              GO TO FIN,.                                               DMTX 340
              END,.                                                     DMTX 350
         END,.                                                          DMTX 360
         DO I = 1 TO M,.                                                DMTX 370
              DO J = 1 TO K,.                                           DMTX 380
              XBAR(I,J)=0.0,.                                           DMTX 390
              END,.                                                     DMTX 400
         END,.                                                          DMTX 410
    L    =0,.                                                           DMTX 420
         DO I = 1 TO K,.                                                DMTX 430
         NN   =N(I),.                                                   DMTX 440
         FSUM =NN,.                                                     DMTX 450
              DO J = 1 TO NN,.                                          DMTX 460
              L    =L+1,.                                               DMTX 470
                   DO KK = 1 TO M,.                                     DMTX 480
                   XBAR(KK,I)=XBAR(KK,I)+X(L,KK),.                      DMTX 490
                   END,.                                                DMTX 500
              END,.                                                     DMTX 510
              DO KK = 1 TO M,.                                          DMTX 520
              XBAR(KK,I)=XBAR(KK,I)/FSUM,.                              DMTX 530
              END,.                                                     DMTX 540
         END,.                                                          DMTX 550
 /*                                                                   */DMTX 560
 /*      COMPUTE THE DISPERSION MATRIX                                */DMTX 570
 /*                                                                   */DMTX 580
         DO I = 1 TO M,.                                                DMTX 590
              DO J = 1 TO M,.                                           DMTX 600
              D(I,J)=0.0,.                                              DMTX 610
              END,.                                                     DMTX 620
         END,.                                                          DMTX 630
    L    =0,.                                                           DMTX 640
         DO I = 1 TO K,.                                                DMTX 650
         NN   =N(I),.                                                   DMTX 660
              DO J = 1 TO NN,.                                          DMTX 670
              L    =L+1,.                                               DMTX 680
                   DO KK = 1 TO M,.                                     DMTX 690
                   CMEAN(KK)=X(L,KK)-XBAR(KK,I),.                       DMTX 700
                   END,.                                                DMTX 710
                   DO K1 = 1 TO M,.                                     DMTX 720
                        DO K2 = K1 TO M,.                               DMTX 730
                        D(K1,K2)=D(K1,K2)+CMEAN(K1)*CMEAN(K2),.         DMTX 740
                        END,.                                           DMTX 750
                   END,.                                                DMTX 760
              END,.                                                     DMTX 770
         END,.                                                          DMTX 780
    L    =0,.                                                           DMTX 790
         DO KK = 1 TO K,.                                               DMTX 800
         L    =L+N(KK),.                                                DMTX 810
         END,.                                                          DMTX 820
    FSUM =L-K,.                                                         DMTX 830
         DO I = 1 TO M,.                                                DMTX 840
              DO J = I TO M,.                                           DMTX 850
              D(I,J)=D(I,J)/FSUM,.                                      DMTX 860
              D(J,I)=D(I,J),.                                           DMTX 870
              END,.                                                     DMTX 880
         END,.                                                          DMTX 890
 FIN..                                                                  DMTX 900
    RETURN,.                                                            DMTX 910
    END,.                              /*END OF PROCEDURE DMTX        */DMTX 920
*PROCESS('CHAR48,NOLIST,NOATR,NOXREF');
 MINV..                                                                 MINV  10
 /*********************************************************************/MINV  20
 /*                                                                   */MINV  30
 /*      TO INVERT A MATRIX                                           */MINV  40
 /*                                                                   */MINV  50
 /*********************************************************************/MINV  60
    PROCEDURE (A,N,D,CON),.                                             MINV  70
    DECLARE                                                             MINV  80
         ERROR EXTERNAL CHARACTER(1),                                   MINV  90
         (I,J,K,N,L(N),M(N))                                            MINV 100
         FIXED BINARY,                                                  MINV 110
         (A(*,*),BIGA,HOLD,D,CON,S)                                     MINV 120
         BINARY FLOAT,.                /*SINGLE PRECISION VERSION  /*S*/MINV 130
  /*     BINARY FLOAT (53),.           /*DOUBLE PRECISION VERSION  /*D*/MINV 140
 /*                                                                   */MINV 150
    ERROR='0',.                                                         MINV 160
    IF N LE 0                                                           MINV 170
    THEN DO,.                                                           MINV 180
         ERROR='1',.                   /* ORDER OF MATRIX = 0.        */MINV 190
         GO TO FIN,.                                                    MINV 200
         END,.                                                          MINV 210
    IF CON= 0                                                           MINV 220
    THEN S    =1.0E-5,.                /* SINGLE PRECISION VERSION /*S*/MINV 230
  /*THEN S    =1.0E-15,.               /* DOUBLE PRECISION VERSION /*D*/MINV 240
    ELSE S    =CON,.                                                    MINV 250
    IF N = 1                           /* INVERT A SCALAR             */MINV 260
    THEN DO,.                                                           MINV 270
         D    =A(1,1),.                                                 MINV 280
         IF ABS(D) LE S                                                 MINV 290
         THEN DO,.                                                      MINV 300
              ERROR='2',.                                               MINV 310
              END,.                                                     MINV 320
         ELSE A(1,1) = 1/D,.                                            MINV 330
         GO TO FIN,.                                                    MINV 340
         END,.                                                          MINV 350
    D    =1.0,.                        /* SEARCH FOR LARGEST ELEMENT  */MINV 360
         DO K = 1 TO N,.                                                MINV 370
         L(K) =K,.                                                      MINV 380
         M(K) =K,.                                                      MINV 390
         BIGA =A(K,K),.                                                 MINV 400
              DO I=K TO N,.                                             MINV 410
                   DO J=K TO N,.                                        MINV 420
                   IF ABS(BIGA) LT ABS(A(I,J))                          MINV 430
                   THEN DO,.                                            MINV 440
                        BIGA =A(I,J),.                                  MINV 450
                        L(K) =I,.                                       MINV 460
                        M(K) =J,.                                       MINV 470
                        END,.                                           MINV 480
                   END,.                                                MINV 490
              END,.                                                     MINV 500
         J    =L(K),.                  /* INTERCHANGE ROWS            */MINV 510
         IF L(K) GT K                                                   MINV 520
         THEN DO,.                                                      MINV 530
                   DO I = 1 TO N,.                                      MINV 540
                   HOLD =-A(K,I),.                                      MINV 550
                   A(K,I)=A(J,I),.                                      MINV 560
                   A(J,I)=HOLD,.                                        MINV 570
                   END,.                                                MINV 580
              END,.                                                     MINV 590
         I    =M(K),.                  /* INTERCHANGE COLUMNS         */MINV 600
         IF M(K) GT K                                                   MINV 610
         THEN DO,.                                                      MINV 620
                   DO J = 1 TO N,.                                      MINV 630
                   HOLD =-A(J,K),.                                      MINV 640
                   A(J,K)=A(J,I),.                                      MINV 650
                   A(J,I)=HOLD,.                                        MINV 660
                   END,.                                                MINV 670
              END,.                                                     MINV 680
         IF ABS(BIGA) LE S                                              MINV 690
         THEN DO,.                                                      MINV 700
              D    =0.0,.                                               MINV 710
              GO TO COMP,.                                              MINV 720
              END,.                                                     MINV 730
 /*                                                                   */MINV 740
 /*      DIVIDE COLUMNS BY MINUS PIVOT (VALUE OF PIVOT ELEMENT IS     */MINV 750
 /*      CONTAINED IN BIGA)                                           */MINV 760
              DO I = 1 TO N,.                                           MINV 770
              IF I NE K                                                 MINV 780
              THEN A(I,K)=A(I,K)/(-A(K,K)),.                            MINV 790
              END,.                                                     MINV 800
              DO I = 1 TO N,.          /* REDUCE MATRIX               */MINV 810
              IF I NE K                                                 MINV 820
              THEN DO,.                                                 MINV 830
                        DO J = 1 TO N,.                                 MINV 840
                        IF J NE K                                       MINV 850
                        THEN A(I,J)=A(I,K)*A(K,J)+A(I,J),.              MINV 860
                        END,.                                           MINV 870
                   END,.                                                MINV 880
              END,.                                                     MINV 890
              DO J = 1 TO N,.                                           MINV 900
              IF J NE K                /* DIVIDE BY ROW PIVOT         */MINV 910
              THEN A(K,J)=A(K,J)/A(K,K),.                               MINV 920
              END,.                                                     MINV 930
         D    =D*A(K,K),.              /* COMPUTE DETERMINANT         */MINV 940
 COMP..                                                                 MINV 950
         IF ABS(D) LE S                                                 MINV 960
         THEN DO,.                                                      MINV 970
              ERROR='2',.              /* DETERMINANT IS ZERO         */MINV 980
              GO TO FIN,.                                               MINV 990
              END,.                                                     MINV1000
         A(K,K)=1.0/A(K,K),.           /* REPLACE PIVOT BY RECIPROCAL */MINV1010
         END,.                                                          MINV1020
 /*                                                                   */MINV1030
 /*      FINAL ROW AND COLUMN INTERCHANGE                             */MINV1040
 /*                                                                   */MINV1050
    K    =N,.                                                           MINV1060
 LOOP..                                                                 MINV1070
    K    =K-1,.                                                         MINV1080
    IF K GT 0                                                           MINV1090
    THEN DO,.                                                           MINV1100
         I    =L(K),.                                                   MINV1110
         IF I GT K                                                      MINV1120
         THEN DO,.                                                      MINV1130
                   DO J = 1 TO N,.                                      MINV1140
                   HOLD =A(J,K),.                                       MINV1150
                   A(J,K)=-A(J,I),.                                     MINV1160
                   A(J,I)=HOLD,.                                        MINV1170
                   END,.                                                MINV1180
              END,.                                                     MINV1190
         J    =M(K),.                                                   MINV1200
         IF J GT K                                                      MINV1210
         THEN DO,.                                                      MINV1220
                   DO I = 1 TO N,.                                      MINV1230
                   HOLD =A(K,I),.                                       MINV1240
                   A(K,I)=-A(J,I),.                                     MINV1250
                   A(J,I)=HOLD,.                                        MINV1260
                   END,.                                                MINV1270
              END,.                                                     MINV1280
         GO TO LOOP,.                                                   MINV1290
         END,.                                                          MINV1300
 FIN..                                                                  MINV1310
    RETURN,.                                                            MINV1320
    END,.                              /*END OF PROCEDURE MINV        */MINV1330
*PROCESS('CHAR48,NOLIST,NOATR,NOXREF');
 DSCR..                                                                 DSCR  10
 /*********************************************************************/DSCR  20
 /*                                                                   */DSCR  30
 /*      TO COMPUTE A SET OF LINEAR FUNCTIONS WHICH SERVE AS INDICES  */DSCR  40
 /*      FOR CLASSIFYING AN INDIVIDUAL INTO ONE OF SEVERAL GROUPS.    */DSCR  50
 /*                                                                   */DSCR  60
 /*********************************************************************/DSCR  70
    PROCEDURE (K,M,N,X,XBAR,D,CMEAN,V,C,P,LG),.                         DSCR  80
    DECLARE                                                             DSCR  90
         (N(*),LG(*),I,J,K,K1,K2,L,LL,M,N1,NN)                          DSCR 100
         FIXED BINARY,                                                  DSCR 110
         ERROR EXTERNAL CHARACTER(1),                                   DSCR 120
         (X(*,*),FN(K))                                                 DSCR 130
         BINARY FLOAT,                                                  DSCR 140
         (XBAR(*,*),D(*,*),C(*,*),CMEAN(*),P(*),V,FSUM,PL)              DSCR 150
         BINARY FLOAT,.                /*SINGLE PRECISION VERSION  /*S*/DSCR 160
  /*     BINARY FLOAT (53),.           /*DOUBLE PRECISION VERSION  /*D*/DSCR 170
 /*                                                                   */DSCR 180
    L    =0,.                                                           DSCR 190
    ERROR='0',.                                                         DSCR 200
    IF M LE 1                          /* NUMBER OF VARIABLES LESS    */DSCR 210
    THEN DO,.                          /* THAN OR EQUAL TO ONE.       */DSCR 220
         ERROR='1',.                                                    DSCR 230
         GO TO FIN,.                                                    DSCR 240
         END,.                                                          DSCR 250
    IF K LE 1 OR K GT M                /* INVALID NUMBER OF GROUPS.   */DSCR 260
    THEN DO,.                                                           DSCR 270
         ERROR='2',.                                                    DSCR 280
         GO TO FIN,.                                                    DSCR 290
         END,.                                                          DSCR 300
         DO I = 1 TO K,.                                                DSCR 310
         IF N(I) LE 0                  /* NO OBSERVATIONS IN ONE OR   */DSCR 320
         THEN DO,.                     /* MORE GROUPS.                */DSCR 330
              ERROR='3',.                                               DSCR 340
              GO TO FIN,.                                               DSCR 350
              END,.                                                     DSCR 360
         END,.                                                          DSCR 370
         DO I = 1 TO K,.                                                DSCR 380
         L    =L+N(I),.                                                 DSCR 390
         END,.                                                          DSCR 400
    FSUM =L,.                                                           DSCR 410
         DO I = 1 TO M,.                                                DSCR 420
         V    =0.0,.                                                    DSCR 430
              DO J = 1 TO K,.                                           DSCR 440
              V    =V+N(J)*XBAR(I,J),.                                  DSCR 450
              END,.                                                     DSCR 460
         CMEAN(I)=V/FSUM,.                                              DSCR 470
         END,.                                                          DSCR 480
 /*                                                                   */DSCR 490
 /*      CALCULATE GENERALIZED MAHALANDBIS D SQUARE                   */DSCR 500
 /*                                                                   */DSCR 510
    V    =0,.                                                           DSCR 520
         DO I = 1 TO M,.                                                DSCR 530
              DO J = 1 TO M,.                                           DSCR 540
              FSUM =0.0,.                                               DSCR 550
                   DO KK = 1 TO K,.                                     DSCR 560
                   FSUM =FSUM+N(KK)*(XBAR(I,KK)-CMEAN(I))               DSCR 570
                        *(XBAR(J,KK)-CMEAN(J)),.                        DSCR 580
                   END,.                                                DSCR 590
              V    =V+D(I,J)*FSUM,.                                     DSCR 600
              END,.                                                     DSCR 610
         END,.                                                          DSCR 620
 /*                                                                   */DSCR 630
 /*      CALCULATE THE COEFFICIENTS OF DISCRIMINANT FUNCTIONS         */DSCR 640
 /*                                                                   */DSCR 650
         DO I = 1 TO K,.                                                DSCR 660
         FSUM =0,.                                                      DSCR 670
              DO J = 1 TO M,.                                           DSCR 680
                   DO KK = 1 TO M,.                                     DSCR 690
                   FSUM =FSUM+D(J,KK)*XBAR(J,I)*XBAR(KK,I),.            DSCR 700
                   END,.                                                DSCR 710
              END,.                                                     DSCR 720
         C(1,I)=-(FSUM/2),.                                             DSCR 730
              DO J = 1 TO M,.                                           DSCR 740
              C(J+1,I)=0.0,.                                            DSCR 750
                   DO KK = 1 TO M,.                                     DSCR 760
                   C(J+1,I)=C(J+1,I)+D(J,KK)*XBAR(KK,I),.               DSCR 770
                   END,.                                                DSCR 780
              END,.                                                     DSCR 790
         END,.                                                          DSCR 800
 /*                                                                   */DSCR 810
 /*      FOR EACH CASE IN EACH GROUP, CALCULATE.. DISCRIMINANT        */DSCR 820
 /*      FUNCTIONS.                                                   */DSCR 830
 /*                                                                   */DSCR 840
    N1   =0,.                                                           DSCR 850
    L    =0,.                                                           DSCR 860
         DO I = 1 TO K,.                                                DSCR 870
         NN   =N(I),.                                                   DSCR 880
              DO J = 1 TO NN,.                                          DSCR 890
              L    =L+1,.                                               DSCR 900
                   DO K1 = 1 TO K,.                                     DSCR 910
                   FN(K1)=C(1,K1),.                                     DSCR 920
                        DO K2 = 1 TO M,.                                DSCR 930
                        FN(K1)=FN(K1)+C(K2+1,K1)*X(L,K2),.              DSCR 940
                        END,.                                           DSCR 950
                   END,.                                                DSCR 960
 /*                                                                   */DSCR 970
 /*      THE LARGEST DISCRIMINANT FUNCTION                            */DSCR 980
 /*                                                                   */DSCR 990
              LL  =1,.                                                  DSCR1000
              FSUM =FN(1),.                                             DSCR1010
                   DO K1 = 2 TO K,.                                     DSCR1020
                   IF FSUM LT FN(K1)                                    DSCR1030
                   THEN DO,.                                            DSCR1040
                        LL   =K1,.                                      DSCR1050
                        FSUM =FN(K1),.                                  DSCR1060
                        END,.                                           DSCR1070
                   END,.                                                DSCR1080
 /*                                                                   */DSCR1090
 /*      PROBABILITY ASSOCIATED WITH THE LARGEST DISCRIMINANT         */DSCR1100
 /*      FUNCTION.                                                    */DSCR1110
 /*                                                                   */DSCR1120
              PL  =0,.                                                  DSCR1130
                   DO KK = 1 TO K,.                                     DSCR1140
                   PL   =PL+EXP(FN(KK)-FSUM),.                          DSCR1150
                   END,.                                                DSCR1160
              N1   =N1+1,.                                              DSCR1170
              LG(N1)=LL,.                                               DSCR1180
              P(N1)=1/PL,.                                              DSCR1190
              END,.                                                     DSCR1200
         END,.                                                          DSCR1210
 FIN..                                                                  DSCR1220
    RETURN,.                                                            DSCR1230
    END,.                              /*END OF PROCEDURE DSCR        */DSCR1240
*PROCESS('CHAR48,NOLIST,NOATR,NOXREF');
 DAT2..                                                                 DAT2  10
 /*********************************************************************/DAT2  20
 /*                                                                   */DAT2  30
 /*      TO READ FLOATING POINT DATA, ONE OBSERVATION AT A TIME.      */DAT2  40
 /*      DATA MAY BE SAVED ON A DATA SET.                             */DAT2  50
 /*                                                                   */DAT2  60
 /*********************************************************************/DAT2  70
    PROCEDURE (M,D),.                                                   DAT2  80
    DECLARE                                                             DAT2  90
         XDATA FILE STREAM ENVIRONMENT (CONSECUTIVE V(2000,200)),       DAT2 100
         (NCARD,NV) EXTERNAL,                                           DAT2 110
         CH CHARACTER(NCARD),                                           DAT2 120
         (I,M,MM) FIXED BINARY,                                         DAT2 130
         D(*) FLOAT BINARY,.                                            DAT2 140
 /*                                                                   */DAT2 150
    ON ENDFILE (SYSIN)                                                  DAT2 160
    GO TO EXIT,.                                                        DAT2 170
    GET EDIT (CH) (A(NCARD)),.                                          DAT2 180
    MM   =CEIL(M/12),.                                                  DAT2 190
    GET STRING (CH) EDIT ((D(I) DO I= 1 TO M))                          DAT2 200
         ((MM)((12)F(6,0),X(8))),.                                      DAT2 210
    IF NV= 1                                                            DAT2 220
    THEN PUT FILE (XDATA) EDIT ((D(I) DO I= 1 TO M)) ((M)F(6,0)),.      DAT2 230
    REVERT ENDFILE (SYSIN),.                                            DAT2 240
    RETURN,.                                                            DAT2 250
 EXIT..                                                                 DAT2 260
    PUT FILE (SYSPRINT) EDIT ('ERROR  INSUFFICIENT DATA')               DAT2 270
         (SKIP(1),COLUMN(10),A),.                                       DAT2 280
    STOP,.                                                              DAT2 290
    END,.                              /*END OF PROCEDURE DAT2        */DAT2 300
/*
//GO.SYSIN  DD  *
SAMPLE 4 6 1                                                                  10
  8  7  7  8                                                                  20
     3    10     9     8    24     8                                          30
     4    12     3     8    22     7                                          40
     9     3     2     8     9     8                                          50
    16     2     2     2     7     2                                          60
     5    10     5     8    23     9                                          70
    17     3     2     8     6     3                                          80
     2    10     9     8    29    16                                          90
     7    10     5     8    28    18                                         100
     9    10    27     8    28    16                                         110
    11     7     8     9     8    15                                         120
     8    10     2     8    27    16                                         130
     1     6     8    14    14    13                                         140
     7     8     9     6    18     2                                         150
     7     9     8     2    19     9                                         160
     7    10     5     8    27    17                                         170
     3    11     9    15    20    10                                         180
     9     4    10     7     9     9                                         190
     4    13    10     7    21    15                                         200
     8     5    16    16    16     7                                         210
     6     9    10     5    23    11                                         220
     8    10     5     8    27    16                                         230
    17     3     2     7     6     3                                         240
     3    10     8     8    23     8                                         250
     4    12     3     8    23     7                                         260
     9     3     2     8    21     7                                         270
    15     2     2     2     7     2                                         280
     9    10    26     8    27    16                                         290
     8     9     2     8    26    16                                         300
     7     8     6     9    18     2                                         310
     7    10     5     8    26    16                                         320
/*
