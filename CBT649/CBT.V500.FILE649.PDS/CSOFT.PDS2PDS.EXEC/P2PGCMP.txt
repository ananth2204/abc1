/* REXX */
/*===================================================================*\
|Program Name: P2PGCMP                                                |
|Date Written: May 31, 1999                                           |
|      Author: Michael A. Newell                                      |
|              Austin, TX                                             |
|              http://wb4huc.home.texas.net                           |
|              http://wb4huc.home.texas.net/pds2pds                   |
|                                                                     |
| Description: This exec processes the Compress command for the       |
|              PDS2PDS utility                                        |
|                                                                     |
|   Called By: P2PMBRL                                                |
|                                                                     |
|       Calls:                                                        |
|                                                                     |
\*===================================================================*/
ADDRESS ISPEXEC                             /*cmds default to ispf    */
ARG lr_sw,dsnl,dsnr,volidl,volidr           /*get args from caller    */
msg_stat = MSG('OFF')                       /*turn msg display off    */
dummy = OUTTRAP("xyz.")                     /*trap other messages     */
INTERPRET 'dsnc = dsn'lr_sw                 /*set dataset name        */
INTERPRET 'volid = volid'lr_sw              /*set source volser name  */
IF volid /= '' THEN                         /*if volid entered        */
   volid = 'VOLUME('volid')'                /*   use it               */
"ISPEXEC LMINIT"            ,               /*initialize dataset      */
        "DATAID("dsncomp")" ,               /*                        */
        "DATASET("dsnc")"   ,               /*                        */
        volid               ,               /*                        */
        "ENQ(EXCLU)"                        /*                        */
init_rc = rc                                /*set return code         */
IF init_rc \=0 THEN                         /*if bad init return code */
   DO                                       /*   do                   */
     CALL System_Error                      /*     put msgs in stack  */
     RETURN 4                               /*     return to caller   */
   END                                      /*   end                  */
sallocb = ''                                /*reset space alloc before*/
susedb = ''                                 /*reset space used before */
sextentb = ''                               /*reset nbr extents before*/
listdsi_rc = LISTDSI(dsnc DIRECTORY volid)  /*list dataset information*/
sallocb = sysalloc                          /*set space alloc before  */
susedb = sysused                            /*set space used before   */
sextentsb = sysextents                      /*set nbr extents before  */
"ISPEXEC LMCOMP"          ,                 /*compress dataset        */
        "DATAID("dsncomp")"                 /*                        */
compress_rc = rc                            /*set compress return code*/
IF compress_rc \= 0 THEN                    /*if bad return code      */
   DO                                       /*   do                   */
     CALL System_Error                      /*     put msgs in stack  */
     RETURN 4                               /*     return to caller   */
   END                                      /*   end                  */
salloca = ''                                /*reset space alloc after */
suseda = ''                                 /*reset space used after  */
sextenta = ''                               /*reset nbr extents after */
listdsi_rc = LISTDSI(dsnc DIRECTORY)        /*get dataset information */
salloca = sysalloc                          /*set space alloc after   */
suseda = sysused                            /*set space used after    */
sextentsa = sysextents                      /*set nbr extents after   */
sunitsb = sysunits                          /*get unit type before    */
sunitsa = sysunits                          /*get unit type after     */
IF sallocb > 1 THEN                         /*if more than 1 unit     */
   sunitsb = sunitsb || 'S'                 /*   make plural          */
IF salloca > 1 THEN                         /*if more than 1 unit     */
   sunitsa = sunitsa || 'S'                 /*   make plural          */
term_width = SYSVAR(SYSWTERM)               /*get terminal width      */
cmsg_const = LEFT(dsnc 'was compressed:', , /*set message line 1      */
                  term_width)               /*                        */
cmsg_before = 'BEFORE:' sallocb           , /*set before compress info*/
                        sunitsb           , /*                        */
                        susedb  'used'    , /*                        */
                        sextentsb         , /*                        */
                        'extents  '         /*                        */
cmsg_before = LEFT(cmsg_before,term_width)  /*set before message      */
cmsg_after = 'AFTER: ' salloca            , /*set after compress info */
                       sunitsa            , /*                        */
                       suseda  'used'     , /*                        */
                       sextentsa          , /*                        */
                       'extents'            /*                        */
cmsg_after  = LEFT(cmsg_after,term_width)   /*set after message       */
zerrsm  = 'Dataset Compressed'              /*set short message       */
zerrlm  = cmsg_const cmsg_before cmsg_after /*set long message        */
CALL System_Error                           /*just for passing msgs   */
RETURN 4                                    /*return to caller        */

System_Error:
   QUEUE zerrsm                             /*put short msg in stack  */
   QUEUE zerrlm                             /*put long msg in stack   */
   "ISPEXEC LMFREE DATAID("dsncomp")"       /*free dataset            */
RETURN
