/* REXX */
/*===================================================================*\
|Program Name: P2PLALL                                                |
|Date Written: May 31, 1999                                           |
|      Author: Michael A. Newell                                      |
|              Austin, TX                                             |
|              http://wb4huc.home.texas.net                           |
|              http://wb4huc.home.texas.net/pds2pds                   |
|                                                                     |
| Description: This exec processes the ALL command for the            |
|              PDS2PDS utility                                        |
|                                                                     |
|   Called By: P2PMBRL                                                |
|                                                                     |
|       Calls:                                                        |
|                                                                     |
\*===================================================================*/
ADDRESS ISPEXEC                             /*commands dflt to ispf   */
ARG lr_sw,tblname,dsnl,dsnr,p2pverno,    ,  /*get args from caller    */
    mbrl,mbrr,nbr_of_mbrs                   /*                        */
"TBTOP" tblname                             /*go to top of table      */
"TBSCAN" tblname                   ,        /*locate member name      */
         "ARGLIST(mbrl,mbrr)"      ,        /*                        */
         "CONDLIST(EQ)"            ,        /*                        */
         "PREVIOUS"                ,        /*                        */
         "POSITION(crpn)"          ,        /*                        */
         "ROWID(currow)"                    /*                        */
tbscan_rc = rc                              /*set return code         */
IF tbscan_rc = 8 THEN                       /*if mbr not found        */
   DO                                       /*   do                   */
     QUEUE 'MSG(PTPM007)'                   /*     set message id     */
     RETURN 8                               /*     return to caller   */
   END                                      /*   end                  */
IF tbscan_rc >8 THEN                        /*if other error          */
   DO                                       /*   do                   */
     CALL System_Error                      /*     put msgs in stack  */
     RETURN 4                               /*     return to caller   */
   END                                      /*   end                  */
"TBGET" tblname                             /*get tble row            */
IF lr_sw = 'L' THEN                         /*if we did left stats    */
   otherside = 'R'                          /*  swap to right stats   */
ELSE                                        /*else                    */
   otherside = 'L'                          /*  swap to left stats    */
CALL Explode_Stats lr_sw                    /*extract stats fields    */
CALL Explode_Stats otherside                /*extract stats fields    */
INTERPRET 'mbr = mbr'lr_sw                  /*set panel mbr name      */
IF mbr = '' THEN                            /*if mbr name blank       */
   INTERPRET 'mbr = mbr'otherside           /*   use other mbr name   */
"ADDPOP ROW(0) COLUMN(0)"                   /*show popup window       */
"DISPLAY PANEL(P2PLALL)"                    /*show panel              */
panel_rc = rc                               /*set panel rc            */
"REMPOP"                                    /*remove pop-up window    */
IF panel_rc = 8 THEN                        /*if pf3/pf4 pressed      */
   RETURN 0                                 /*   return to caller     */
IF panel_rc /= 0 THEN                       /*if bad panel rc         */
   DO                                       /*   do                   */
      CALL System_Error                     /*     put msgs in stack  */
      RETURN 4                              /*     return to caller   */
   END                                      /*   end                  */
RETURN 0

Explode_Stats:
   ARG mbr_flag                             /*get lr_switch           */
   INTERPRET ,                              /*extract stats for mbr   */
   'IF mbr'mbr_flag '/= " " & stat'mbr_flag '/= " " THEN;' ,
      'DO;'                                          ,
         'vvmm'mbr_flag '= LEFT(stat1'mbr_flag',5);' ,
         'cre'mbr_flag  '= LEFT(stat2'mbr_flag',8);'  ,
         'cha'mbr_flag  '= LEFT(stat3'mbr_flag',8);'  ,
         'time'mbr_flag '= LEFT(stat4'mbr_flag',8);' ,
         'size'mbr_flag '= LEFT(stat5'mbr_flag',5);' ,
         'init'mbr_flag '= LEFT(stat6'mbr_flag',5);' ,
         'mod'mbr_flag  '= LEFT(stat7'mbr_flag',5);'  ,
         'user'mbr_flag '= SUBSTR(stat7'mbr_flag',7,8);' ,
         'al'mbr_flag   '= SUBSTR(stat7'mbr_flag',16,3);' ,
      'END;'                                         ,
   'ELSE;'                                           ,
      'DO;'                                          ,
         'vvmm'mbr_flag '=  " ";'                    ,
         'cre'mbr_flag  '=  " ";'                     ,
         'cha'mbr_flag  '=  " ";'                     ,
         'time'mbr_flag '=  " ";'                    ,
         'size'mbr_flag '=  " ";'                    ,
         'init'mbr_flag '=  " ";'                    ,
         'mod'mbr_flag  '=  " ";'                     ,
         'user'mbr_flag '=  " ";'                    ,
         'al'mbr_flag   '=  " ";'                      ,
      'END;'
RETURN
