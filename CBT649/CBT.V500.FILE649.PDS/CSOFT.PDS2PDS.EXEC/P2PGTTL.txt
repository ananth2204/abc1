/* REXX */
/*===================================================================*\
|Program Name: P2PGTTL                                                |
|Date Written: May 31, 1999                                           |
|      Author: Michael A. Newell                                      |
|              Austin, TX                                             |
|              http://wb4huc.home.texas.net                           |
|              http://wb4huc.home.texas.net/pds2pds                   |
|                                                                     |
| Description: This exec processes the TOTAL LINES command for the    |
|              PDS2PDS utility                                        |
|                                                                     |
|   Called By: P2PMBRL                                                |
|                                                                     |
|       Calls:                                                        |
|                                                                     |
\*===================================================================*/
ADDRESS ISPEXEC                             /*commands dflt to ispf   */
ARG lr_sw,tblname,dsnl,dsnr,p2pverno        /*get args from caller    */
"TBTOP" tblname                             /*go to top of table      */
"TBQUERY" tblname "ROWNUM(TBLROWS)"         /*nbr of rows in table    */
sizelttl = 0                                /*init total size left    */
initlttl = 0                                /*init total init left    */
sizerttl = 0                                /*init total size right   */
initrttl = 0                                /*init total init right   */
mbrlcnt = 0                                 /*init nbr mbrs left      */
mbrrcnt = 0                                 /*init nbr mbrs right     */
DO tblrows                                  /*do once for each row    */
   "TBSKIP" tblname "NUMBER(1)"             /*   skip to next row     */
   "TBGET" tblname                          /*   get a row            */
   CALL Count_Lines                         /*   count total lines    */
END                                         /*end                     */
"ADDPOP ROW(0) COLUMN(0)"                   /*show popup window       */
"DISPLAY PANEL(P2PGTTL)"                    /*display panel           */
panel_rc = rc                               /*set panel rc            */
"REMPOP"                                    /*remove popup window     */
IF panel_rc = 8 THEN                        /*if pf3 or pf4 pressed   */
   RETURN 0                                 /*   return to caller     */
IF panel_rc /= 0 THEN                       /*if bad panel rc         */
   DO                                       /*   do                   */
     QUEUE zerrsm                           /*     queue short msg    */
     QUEUE zerrlm                           /*     queue long msg     */
     RETURN 4                               /*     return to caller   */
   END                                      /*   end                  */
RETURN 0

Count_Lines:
   IF mbrl /= ' ' & stat1l /= ' ' THEN      /*if mbr has stats        */
      DO                                    /*   do                   */
        mbrlcnt = mbrlcnt + 1               /*     add 1 to mbr count */
        sizel  = LEFT(stat5l,5)             /*     extract size fld   */
        initl  = LEFT(stat6l,5)             /*     extract init fld   */
        IF DATATYPE(sizel) = 'NUM' THEN     /*     if numeric         */
           sizelttl = sizelttl + sizel      /*        add to total    */
        IF DATATYPE(initl) = 'NUM' THEN     /*     if numeric         */
           initlttl = initlttl + initl      /*        add to total    */
      END                                   /*   end                  */
   IF mbrr /= ' ' & stat1r /= ' ' THEN      /*if mbr has stats        */
      DO                                    /*   do                   */
        mbrrcnt = mbrrcnt + 1               /*     add 1 to mbr count */
        sizer  = LEFT(stat5r,5)             /*     extract size fld   */
        initr  = LEFT(stat6r,5)             /*     extract init fld   */
        IF DATATYPE(sizer) = 'NUM' THEN     /*     if numeric         */
           sizerttl = sizerttl + sizer      /*        add to total    */
        IF DATATYPE(initr) = 'NUM' THEN     /*     if numeric         */
           initrttl = initrttl + initr      /*        add to total    */
      END                                   /*   end                  */
RETURN
