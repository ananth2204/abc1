/* REXX */
/*===================================================================*\
|Program Name: P2PGDEL                                                |
|Date Written: May 31, 1999                                           |
|      Author: Michael A. Newell                                      |
|              Austin, TX                                             |
|              http://wb4huc.home.texas.net                           |
|              http://wb4huc.home.texas.net/pds2pds                   |
|                                                                     |
| Description: This exec processes the Global Delete Command for the  |
|              PDS2PDS utility                                        |
|                                                                     |
|   Called By: P2PMBRL                                                |
|                                                                     |
|       Calls:                                                        |
|                                                                     |
\*===================================================================*/
ADDRESS ISPEXEC                             /*commands dflt to ispf   */
ARG lr_sw,tblname,dsnl,dsnr,volidl,volidr,, /*get args from caller  */
    p2pverno,memlo,memro,nbr_of_mbrs        /*                        */
INTERPRET 'pnldsn = dsn'lr_sw               /*set dsname for panel    */
INTERPRET 'volid = volid'lr_sw              /*set volser name         */
IF volid /= '' THEN                         /*if volid entered        */
   volid = 'VOLUME('volid')'                /*   use it               */
"LMINIT DATAID("ddvar")"     ,              /*get dataid for target   */
       "DATASET("pnldsn")"   ,              /*    dataset             */
       volid                 ,              /*                        */
       "ENQ(SHRW)"                          /*                        */
lminit_rc = rc                              /*set return code         */
IF lminit_rc \= 0 THEN                      /*if bad return code      */
   DO                                       /*   do                   */
     CALL System_Error                      /*     display message    */
     CALL Queue_Mbrs                        /*     put memlo/ro in stk*/
     RETURN 4                               /*     return             */
   END                                      /*   end                  */
"LMOPEN DATAID("ddvar")"     ,              /*open dataset            */
       "OPTION(OUTPUT)"                     /*                        */
lmopen_rc = rc                              /*set return code         */
IF lmopen_rc \= 0 THEN                      /*if bad return code      */
   DO                                       /*   do                   */
     CALL System_Error                      /*     display message    */
     CALL Queue_Mbrs                        /*     put mbrs in stack  */
     CALL Free_File                         /*     free file          */
     RETURN 4                               /*     return to caller   */
   END                                      /*   end                  */
"TBTOP" tblname                             /*go to top of table      */
"TBQUERY" tblname "ROWNUM(TBLROWS)"         /*nbr of rows in table    */
lmmdel_rc = 0                               /*init return code        */
cnfrmoff = ' '                              /*set cmd confirm sw      */
DO tblrows                                  /*do once for each row    */
   "TBSKIP" tblname "NUMBER(1)"             /*   skip to next row     */
   "TBGET" tblname                          /*   get a row            */
   INTERPRET 'mbro = mbr'lr_sw              /*   set mbr name         */
  IF mbro = ' ' THEN                        /*   if member name blank */
     ITERATE                                /*      get next member   */
  IF cnfrmoff = ' ' THEN                    /*   if cmd cnfrm off     */
     DO                                     /*      do                */
       "ADDPOP ROW(0) COLUMN(0)"            /*        show popup win  */
       "DISPLAY PANEL(P2PGDEL)"             /*        display panel   */
        panel_rc = rc                       /*        set panel rc    */
       "REMPOP"                             /*        remove popup win*/
        IF panel_rc = 8 THEN                /*        if pf3/pf4 presd*/
           ITERATE                          /*           go to nxt mbr*/
        IF panel_rc /= 0 THEN               /*        if bad rc       */
           DO                               /*           do           */
             CALL System_Error              /*             stack msgs */
             LEAVE                          /*             quit       */
           END                              /*           end          */
      END                                   /*      end               */
   IF cancelop \= ' ' THEN                  /*    if cmd not confirmed*/
      LEAVE                                 /*       get next member  */
  "LMMDEL DATAID("ddvar")"     ,            /*   delete member        */
         "MEMBER("mbro")"                   /*                        */
  lmmdel_rc = rc                            /*   set return code      */
  IF lmmdel_rc \= 0 THEN                    /*   if bad return code   */
     DO                                     /*      do                */
       CALL System_Error                    /*        put msgs in stak*/
       LEAVE                                /*        quit            */
     END                                    /*      end               */
   INTERPRET 'mem'lr_sw'o = mem'lr_sw'o - 1'/*   if necessary         */
   "TBDELETE" tblname                       /*   delete table row     */
   INTERPRET 'res'lr_sw '= "D"'             /*   set results field    */
   INTERPRET 'mbr'lr_sw '= " "'             /*   blank mbr name       */
   INTERPRET 'stat1'lr_sw '= " "'           /*   blank   stats line 1 */
   INTERPRET 'stat2'lr_sw '= " "'           /*   blank stats line 1   */
   INTERPRET 'stat3'lr_sw '= " "'           /*   blank stats line 1   */
   INTERPRET 'stat4'lr_sw '= " "'           /*   blank stats line 1   */
   INTERPRET 'stat5'lr_sw '= " "'           /*   blank stats line 1   */
   INTERPRET 'stat6'lr_sw '= " "'           /*   blank stats line 1   */
   INTERPRET 'stat7'lr_sw '= " "'           /*   blank stats line 1   */
   "TBADD" tblname "MULT("nbr_of_mbrs")"    /*   re-add row to table  */
   tbadd_rc = rc                            /*   set return code      */
   IF tbadd_rc > 8 THEN                     /*   if bad return code   */
      DO                                    /*      do                */
        CALL System_Error                   /*        put msgs in stak*/
        LEAVE                               /*        exit loop       */
      END                                   /*      end               */
END                                         /*end                     */

CALL Queue_Mbrs                             /*queue nbr mbrs to stack */
CALL Free_File                              /*close filee             */

EXIT lmdel_rc

Queue_Mbrs:
   QUEUE memlo                              /*nbr mbrs left side ds   */
   QUEUE memro                              /*nbr mbrs right side ds  */
RETURN

System_Error:
   QUEUE zerrsm                             /*put short msg in stack  */
   QUEUE zerrlm                             /*put long msg in stack   */
   lmdel_rc = 4                             /*set subroutine ret code */
RETURN

Free_File:
   "LMFREE DATAID("ddvar")"                 /*free daaset             */
RETURN
