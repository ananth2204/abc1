/* REXX */
/*===================================================================*\
|Program Name: P2PGALL                                                |
|Date Written: May 31, 1999                                           |
|      Author: Michael A. Newell                                      |
|              Austin, TX                                             |
|              http://wb4huc.home.texas.net                           |
|              http://wb4huc.home.texas.net/pds2pds                   |
|                                                                     |
| Description: This exec processes the ALL command for the            |
|              PDS2PDS utility                                        |
|                                                                     |
|   Called By: P2PMBRL                                                |
|                                                                     |
|       Calls:                                                        |
|                                                                     |
\*===================================================================*/
ADDRESS ISPEXEC                             /*commands dflt to ispf   */
ARG lr_sw,tblname,dsnl,dsnr,p2pverno        /*get args from caller    */
"TBTOP" tblname                             /*go to top of table      */
"TBQUERY" tblname "ROWNUM(TBLROWS)"         /*nbr of rows in table    */
IF lr_sw = 'L' THEN                         /*if we did left stats    */
   otherside = 'R'                          /*  swap to right stats   */
ELSE                                        /*else                    */
   otherside = 'L'                          /*  swap to left stats    */
all_rc = 0                                  /*set return code         */
DO tblrows                                  /*do once for each row    */
   "TBSKIP" tblname "NUMBER(1)"             /*   skip to next row     */
   "TBGET" tblname                          /*   get a row            */
   INTERPRET 'mbr = mbr'lr_sw               /*   set member name      */
   IF mbr = '' THEN                         /*   if mbr name blank    */
      INTERPRET 'mbr = mbr'otherside        /*      use other mbr name*/
   CALL Explode_Stats lr_sw                 /*   extract stats fields */
   CALL Explode_Stats otherside             /*   extract stats fields */
   "ADDPOP ROW(0) COLUMN(0)"                /*   pop-up window pos    */
   "DISPLAY PANEL(P2PGALL)"                 /*   displ rename panel   */
   panel_rc = rc                            /*   set panel return code*/
   "REMPOP"                                 /*   remove pop-up window */
   IF panel_rc = 8 THEN                     /*   if pf3/pf4 pressed   */
      LEAVE                                 /*      quit              */
   IF panel_rc /= 0 THEN                    /*   if rc not zero       */
      DO                                    /*      do                */
        CALL System_Error                   /*        put msgs in stak*/
        LEAVE                               /*        quit            */
      END                                   /*      end               */
   IF cancelop \= ' ' THEN                  /*   if command cancelled */
      LEAVE                                 /*      then leave        */
END                                         /*end                     */
RETURN all_rc                               /*return to caller        */

Explode_Stats:
   ARG mbr_flag                             /*get left/right indicator*/
   INTERPRET ,                              /*extract stats for mbr   */
   'IF mbr'mbr_flag '/= " " & stat'mbr_flag '/= " " THEN;'   ,
      'DO;'                                                  ,
         'vvmm'mbr_flag '= LEFT(stat1'mbr_flag',5);'         ,
         'cre'mbr_flag  '= LEFT(stat2'mbr_flag',8);'         ,
         'cha'mbr_flag  '= LEFT(stat3'mbr_flag',8);'         ,
         'time'mbr_flag '= LEFT(stat4'mbr_flag',8);'         ,
         'size'mbr_flag '= LEFT(stat5'mbr_flag',5);'         ,
         'init'mbr_flag '= LEFT(stat6'mbr_flag',5);'         ,
         'mod'mbr_flag  '= LEFT(stat7'mbr_flag',5);'         ,
         'user'mbr_flag '= SUBSTR(stat7'mbr_flag',7,8);'     ,
         'al'mbr_flag   '= SUBSTR(stat7'mbr_flag',16,3);'    ,
      'END;'                                                 ,
   'ELSE;'                                                   ,
      'DO;'                                                  ,
         'vvmm'mbr_flag '=  " ";'                            ,
         'cre'mbr_flag  '=  " ";'                            ,
         'cha'mbr_flag  '=  " ";'                            ,
         'time'mbr_flag '=  " ";'                            ,
         'size'mbr_flag '=  " ";'                            ,
         'init'mbr_flag '=  " ";'                            ,
         'mod'mbr_flag  '=  " ";'                            ,
         'user'mbr_flag '=  " ";'                            ,
         'al'mbr_flag   '=  " ";'                            ,
      'END;'
RETURN

System_Error:
   QUEUE zerrsm                             /*put short msg in stack  */
   QUEUE zerrlm                             /*put long msg in stack   */
   all_rc = 4                               /*set return code         */
RETURN
