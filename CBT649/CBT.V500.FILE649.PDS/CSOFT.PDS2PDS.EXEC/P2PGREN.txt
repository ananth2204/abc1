/* REXX */
/*===================================================================*\
|Program Name: P2PGREN                                                |
|Date Written: May 31, 1999                                           |
|      Author: Michael A. Newell                                      |
|              Austin, TX                                             |
|              http://wb4huc.home.texas.net                           |
|              http://wb4huc.home.texas.net/pds2pds                   |
|                                                                     |
| Description: This exec processes the Global Rename Command for the  |
|              PDS2PDS utility                                        |
|                                                                     |
|   Called By: P2PMBRL                                                |
|                                                                     |
|       Calls:                                                        |
|                                                                     |
\*===================================================================*/
ADDRESS ISPEXEC                             /*commands dflt to ispf   */
ARG lr_sw,tblname,dsnl,dsnr,volidl,volidr,, /*get args from caller  */
    p2pverno,memlo,memro,nbr_of_mbrs        /*                        */
INTERPRET 'pnldsn = dsn'lr_sw               /*set dsname for panel    */
INTERPRET 'volid = volid'lr_sw              /*set source volser name  */
IF volid /= '' THEN                         /*if volid entered        */
   volid = 'VOLUME('volid')'                /*   use it               */
"LMINIT DATAID("ddvar")"     ,              /*get dataid for target   */
       "DATASET("pnldsn")"   ,              /*   dataset              */
       volid                 ,              /*                        */
       "ENQ(SHRW)"                          /*                        */
lminit_rc = rc                              /*set return code         */
IF lminit_rc \= 0 THEN                      /*if bad return code      */
   DO                                       /*   do                   */
     CALL System_Error                      /*     display message    */
     RETURN 4                               /*     return             */
   END                                      /*   end                  */
"LMOPEN DATAID("ddvar")"     ,              /*open dataset            */
       "OPTION(OUTPUT)"                     /*                        */
lmopen_rc = rc                              /*set return code         */
IF lmopen_rc \= 0 THEN                      /*if bad return code      */
   DO                                       /*   do                   */
     CALL System_Error                      /*     display message    */
     CALL Free_File                         /*     free dataset       */
     RETURN 4                               /*     return             */
   END                                      /*   end                  */
"TBTOP" tblname                             /*go to top of table      */
"TBQUERY" tblname "ROWNUM(TBLROWS)"         /*nbr of rows in table    */
lmmren_rc = 0                               /*init return code        */
DO tblrows                                  /*do once for each row    */
   "TBSKIP" tblname "NUMBER(1)"             /*   skip to next row     */
   "TBGET" tblname                          /*   get a row            */
   INTERPRET 'mbro = mbr'lr_sw              /*   set member name      */
   IF mbro = '' THEN                        /*   if mbr name blank    */
      ITERATE                               /*      get next mbr      */
   mbr_err = 1                              /*   set switch           */
   DO while mbr_err = 1                     /*   do while mbrs not sam*/
      "ADDPOP ROW(0) COLUMN(0)"             /*      show popup win    */
      "DISPLAY PANEL(P2PGREN)"  ,           /*      show panel        */
                   "CURSOR(MBRN)"           /*                        */
      panel_rc = rc                         /*      set panel rc      */
      "REMPOP"                              /*      remove popup win  */
      mbr_err = 0                           /*      reset mbr err sw  */
      lmmren_rc = 0                         /*      reset return code */
      IF panel_rc = 8 THEN                  /*      if pf3/pf4 pressed*/
         LEAVE                              /*         quit           */
      IF panel_rc /= 0 THEN                 /*      if bad rc         */
         DO                                 /*         do             */
           CALL System_Error                /*           queue msgs   */
           Call Free_File                   /*           free file    */
           RETURN 4                         /*           quit         */
         END                                /*         end            */
      IF mbrn = mbro THEN                   /*      if mbr names same */
         DO                                 /*         do             */
           "SETMSG MSG(PTPM006)"            /*           dsp err msg  */
           mbr_err = 1                      /*           set err sw   */
           ITERATE                          /*           try again    */
         END                                /*         end            */
      IF cancelop \= ' ' THEN               /*   if cmd cancelled     */
         LEAVE                              /*      quit              */
      "LMMREN DATAID("ddvar")"     ,        /*   rename member        */
             "MEMBER("mbro")"      ,        /*                        */
             "NEWNAME("mbrn")"              /*                        */
      lmmren_rc = rc                        /*   set return code      */
      IF lmmren_rc \= 0 THEN                /*   if bad return code   */
         DO                                 /*      do                */
           "SETMSG MSG(ISRZ002)"            /*        display message */
           mbr_err = 1                      /*        set err sw      */
           ITERATE                          /*        try again       */
         END                                /*      end               */
      "TBDELETE" tblname                    /*   delete tbl row       */
      INTERPRET 'res'lr_sw '= R'            /*   set results field    */
      INTERPRET 'mbr'lr_sw '= mbrn'         /*   set member name      */
      "TBADD" tblname "MULT("nbr_of_mbrs")" /*   re-add table row     */
      tbadd_rc = rc                         /*   set return code      */
      IF tbadd_rc > 8 THEN                  /*   if bad return code   */
         DO                                 /*      do                */
           CALL System_Error                /*        put msgs in stak*/
           CALL Free_File                   /*        free files      */
           RETURN 4                         /*        return to caller*/
         END                                /*      end               */
      mbrn = ' '                            /*   blank mbr name       */
   END                                      /*   end                  */
   IF panel_rc = 8 THEN                     /*   if pf3/pf4 pressed   */
      ITERATE                               /*      get next mbr      */
   IF cancelop \= ' ' THEN                  /*   if cmd cancelled     */
      LEAVE                                 /*      leave             */
END                                         /*end                     */
                                            /*                        */
CALL Free_File                              /*free file               */

EXIT lmmren_rc

System_Error:
   QUEUE zerrsm                             /*put short msg in stack  */
   QUEUE zerrlm                             /*put long msg in stack   */
   lmmren_rc = 4                            /*set subroutine ret code */
RETURN

Free_File:
   "LMCLOSE DATAID("ddvar")"                /*close dataset           */
   "LMFREE  DATAID("ddvar")"                /*free dataset            */
RETURN
