/* REXX */
/*===================================================================*\
|Program Name: P2PGBRW                                                |
|Date Written: May 31, 1999                                           |
|      Author: Michael A. Newell                                      |
|              Austin, TX                                             |
|              http://wb4huc.home.texas.net                           |
|              http://wb4huc.home.texas.net/pds2pds                   |
|                                                                     |
| Description: This exec processes the Global Browse Command for the  |
|              PDS2PDS utility                                        |
|                                                                     |
|   Called By: P2PMBRL                                                |
|                                                                     |
|       Calls:                                                        |
|                                                                     |
\*===================================================================*/
ADDRESS ISPEXEC                             /*commands dflt to ispf   */
ARG lr_sw,gcmd,tblname,dsnl,dsnr,         , /*get args from caller  */
    volidl,volidr,p2pverno,nbr_of_mbrs      /*get args from caller  */
INTERPRET 'pnldsn = dsn'lr_sw               /*set panel dsname        */
INTERPRET 'volid = volid'lr_sw              /*set volser name         */
IF gcmd = 'B' THEN cmd = 'BROWSE'           /*do we browse? or..      */
IF gcmd = 'V' THEN cmd = 'VIEW'             /*do we view?             */
IF volid /= '' THEN                         /*if volid entered        */
   volid = 'VOLUME('volid')'                /*   use it               */
"TBTOP" tblname                             /*go to top of table      */
"TBQUERY" tblname "ROWNUM(TBLROWS)"         /*nbr of rows in table    */
browse_rc = 0                               /*init return code        */
cnfrmoff = ' '                              /*reset command cnfrm sw  */
DO i = 1 to tblrows                         /*do once for each row    */
   "TBSKIP" tblname "NUMBER(1)"             /*   skip to next row     */
   "TBGET" tblname                          /*   get a row            */
   INTERPRET 'mbr = mbr'lr_sw               /*   set mbr name         */
   IF mbr = '' THEN                         /*   if mbr name blank    */
      IF i = tblrows THEN                   /*      if last mbr       */
         LEAVE                              /*         quit           */
      ELSE                                  /*      else              */
         ITERATE                            /*         get next mbr   */
   IF cnfrmoff = ' ' THEN                   /*   if cmd cnfrm off     */
      DO                                    /*      do                */
        "ADDPOP ROW(0) COLUMN(0)"           /*        create popup win*/
        "DISPLAY PANEL(P2PGBRW)"            /*        display panel   */
        panel_rc = rc                       /*        set panel rc    */
        "REMPOP"                            /*        remove popup win*/
        IF panel_rc = 8 THEN                /*        if pf3/pf4      */
           ITERATE                          /*           get nxt mbr  */
        IF panel_rc /= 0 THEN               /*        if bad panel rc */
           DO                               /*           do           */
             CALL System_Error              /*             error      */
             LEAVE                          /*             leave      */
           END                              /*           end          */
      END                                   /*      end               */
   IF cancelop  \= ' ' THEN                 /*   if time to leave     */
      LEAVE                                 /*      then leave        */
   "CONTROL DISPLAY SAVE"                   /*   save current display */
   IF lr_sw = 'L' THEN                      /*   if browse/view left  */
      INTERPRET '"'cmd               ,      /*                        */
             "DATASET("            ||,      /*      browse/view left  */
              Build_Dsn(dsnl,mbrl) ||,      /*      member            */
              ")"                    ,      /*                        */
              volid'"'                      /*                        */
   ELSE                                     /*   else                 */
      INTERPRET '"'cmd               ,      /*                        */
             "DATASET("            ||,      /*      browse/view right */
              Build_Dsn(dsnr,mbrr) ||,      /*      member            */
              ")"                    ,      /*                        */
              volid'"'                      /*                        */
   browse_rc = rc                           /*   set return code      */
   "CONTROL DISPLAY RESTORE"                /*   restore current displ*/
   IF browse_rc > 0 THEN                    /*   if bad return code   */
      DO                                    /*      do                */
        CALL System_Error                   /*        put msgs in stak*/
        LEAVE                               /*        quit            */
      END                                   /*      end               */
   INTERPRET 'res'lr_sw '=' gcmd            /*   set results field    */
   "TBDELETE" tblname                       /*   delete table row     */
   tbdel_rc = rc                            /*   set return code      */
   IF tbdel_rc > 0 THEN                     /*   if bad return code   */
      DO                                    /*      do                */
        CALL System_Error                   /*        put msgs in stak*/
        LEAVE                               /*        quit            */
      END                                   /*      end               */
   "TBADD" tblname "MULT("nbr_of_mbrs")"    /*   re-add row to table  */
   tbadd_rc = rc                            /*   se return code       */
   IF tbadd_rc > 0 THEN                     /*   if bad return code   */
      DO                                    /*      do                */
        CALL System_Error                   /*        put msgs in stak*/
        LEAVE                               /*        quit            */
      END                                   /*      end               */
END                                         /*end                     */
EXIT browse_rc

System_Error:
   QUEUE zerrsm                             /*put short msg in stack  */
   QUEUE zerrlm                             /*put long msg in stack   */
   browse_rc = 4                            /*set return code         */
RETURN

Build_Dsn:
   ARG dsn,mbrname                          /*get dsn & mbrname       */
   mbrname = STRIP(mbrname)                 /*drop spaces from mbrname*/
   dsn_length = LENGTH(dsn) - 1             /*get dsn length          */
   dsn = LEFT(dsn,dsn_length)               /*drop right quote        */
   dsn = dsn || "(" || mbrname || ")'"      /*add mbrname/right paren */
RETURN dsn
