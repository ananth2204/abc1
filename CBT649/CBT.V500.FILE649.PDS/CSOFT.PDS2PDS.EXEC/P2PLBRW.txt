/* REXX */
/*===================================================================*\
|Program Name: P2PLBRW                                                |
|Date Written: May 31, 1999                                           |
|      Author: Michael A. Newell                                      |
|              Austin, TX                                             |
|              http://wb4huc.home.texas.net                           |
|              http://wb4huc.home.texas.net/pds2pds                   |
|                                                                     |
| Description: This exec processes the Edit Member Command for the    |
|              PDS2PDS utility                                        |
|                                                                     |
|   Called By: P2PMBRL                                                |
|                                                                     |
|       Calls:                                                        |
|                                                                     |
\*===================================================================*/
ADDRESS ISPEXEC                             /*commands dflt to ispf   */
ARG lr_sw,cmdl,cmdr,tblname,dsnl,dsnr,   ,  /*get args from caller    */
    p2pverno,mbrl,mbrr,volidl,volidr,    ,  /*get args from caller    */
    nbr_of_mbrs                             /*                        */
"TBTOP" tblname                             /*go to top of table      */
INTERPRET 'pnldsn = dsn'lr_sw               /*set dsname for panel    */
INTERPRET 'volid = volid'lr_sw              /*set volser name         */
INTERPRET 'mbrm = mbr'lr_sw                 /*set mbr name for panel  */
IF cmdl = 'V' THEN cmd = 'VIEW'             /*do we view? or...       */
IF cmdl = 'B' THEN cmd = 'BROWSE'           /*do we browse?           */
IF cmdr = 'V' THEN cmd = 'VIEW'             /*do we view? or...       */
IF cmdr = 'B' THEN cmd = 'BROWSE'           /*do we browse?           */
IF volid /= '' THEN                         /*if volid entered        */
   volid = 'VOLUME('volid')'                /*   use it               */
INTERPRET                           ,       /*find member             */
  '"TBSCAN"' tblname                ,       /*                        */
           '"ARGLIST(mbr'lr_sw')'   ,       /*                        */
           'CONDLIST(EQ)'           ,       /*                        */
           'PREVIOUS'               ,       /*                        */
           'POSITION(crpn)'         ,       /*                        */
           'ROWID(currow)"'                 /*                        */
tbscan_rc = rc                              /*set return code         */
IF tbscan_rc = 8 THEN                       /*if mbr not found        */
   DO                                       /*   do                   */
     QUEUE 'MSG(PTPM007)'                   /*     set short message  */
     RETURN 8                               /*     return to caller   */
   END                                      /*   end                  */
IF tbscan_rc >8 THEN                        /*if other error          */
   DO                                       /*   do                   */
     CALL System_Error                      /*     put msgs in stack  */
     RETURN 4                               /*     return to caller   */
   END                                      /*   end                  */
browse_rc = 0                               /*init return code        */
"TBSKIP" tblname "ROW("currow")"            /*skip to next row        */
"TBGET" tblname                             /*get a row               */
INTERPRET 'mbro = mbr'lr_sw                 /*set mbr name to edit    */
"CONTROL DISPLAY SAVE"                      /*save current display    */
IF lr_sw = 'L' THEN                         /*   if browse left       */
   INTERPRET '"'cmd               ,         /*                        */
          "DATASET("            ||,         /*      browse/view left  */
           Build_Dsn(dsnl,mbrl) ||,         /*      member            */
           ")"                    ,         /*                        */
           volid'"'                         /*                        */
ELSE                                        /*   else                 */
      INTERPRET '"'cmd               ,      /*                        */
             "DATASET("            ||,      /*      browse/view right */
              Build_Dsn(dsnr,mbrr) ||,      /*      member            */
              ")"                    ,      /*                        */
              volid'"'                      /*                        */
browse_rc = rc                              /*set return code         */
"CONTROL DISPLAY RESTORE"                   /*restore current displ   */
IF browse_rc \= 0 THEN                      /*if bad return code      */
   DO                                       /*   do                   */
     CALL System_Error                      /*     display message    */
     RETURN 4                               /*     leave              */
   END                                      /*   end                  */
INTERPRET 'res'lr_sw '=' LEFT(cmd,1)        /*set results field       */
"TBDELETE" tblname                          /*delete table row        */
tbdel_rc = rc                               /*set return code         */
IF tbdel_rc >8 THEN                         /*if other error          */
   DO                                       /*   do                   */
     CALL System_Error                      /*     put msgs in stack  */
     RETURN 4                               /*     return to caller   */
   END                                      /*   end                  */
"TBADD" tblname "MULT("nbr_of_mbrs")"       /*re-add row              */
tbadd_rc = rc                               /*set return code         */
IF tbadd_rc >8 THEN                         /*if other error          */
   DO                                       /*   do                   */
     CALL System_Error                      /*     put msgs in stack  */
     RETURN 4                               /*     return to caller   */
   END                                      /*   end                  */
EXIT 0

System_Error:
   QUEUE zerrsm                             /*put short msg in stack  */
   QUEUE zerrlm                             /*put long msg in stack   */
RETURN

Build_Dsn:
   ARG dsn,mbrname                          /*get dsn & mbrname       */
   mbrname = STRIP(mbrname)                 /*drop spaces from mbrname*/
   dsn_length = LENGTH(dsn) - 1             /*get dsn length          */
   dsn = LEFT(dsn,dsn_length)               /*drop right quote        */
   dsn = dsn || "(" || mbrname || ")'"      /*add mbrname/right paren */
RETURN dsn                                  /*                        */
