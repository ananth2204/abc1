        /* ---- REXX -------------------------------------- *
         * Name:     CONS                                   *
         *                                                  *
         * Function: Invoke a z/OS Console command and      *
         *           then display the results               *
         *                                                  *
         * Usage:    To execute a z/OS command and then     *
         *           display the results.                   *
         *                                                  *
         *           If the command is called under ISPF    *
         *           directly the resulting messages will   *
         *           be placed into ISPF Browse.            *
         *                                                  *
         *           If called by another commaand (such    *
         *           as TSOTRAP then the results are        *
         *           displayed using SAY and are            *
         *           trapable by OUTTRAP in REXX            *
         *                                                  *
         *           This command is best used if it is     *
         *           added to an ISPF Command Table         *
         *                                                  *
         *           This can be done by calling it the     *
         *           first time each ISPF session with      *
         *           a parameter of add:                    *
         *                                                  *
         *           e.g. TSO %CONS ADD                     *
         *                                                  *
         * Notes:    The GETMSG function has a default of   *
         *           30 seconds to wait for the response    *
         *           of the z/OS Command.  This is typical  *
         *           for START commands and perhaps others. *
         *           If the GETMSG time's out then no data  *
         *           will be displayed.                     *
         *                                                  *
         *           If the command responds before the     *
         *           wait time is exhausted then the        *
         *           results are captured and displayed     *
         *           without waiting for the full wait      *
         *           time to expire.                        *
         *                                                  *
         * History:                                         *
         *          03/08/2016 - Use STEMVIEW if installed  *
         *                       (see *custom*)             *
         *                     - get realistic lrecl based  *
         *                       on the captured messages   *
         *          03/07/2016 - Add new option to update   *
         *                       the ispcmds table          *
         *          03/01/2016 - cleanup and re-write       *
         *                                                  *
         * Author:  Lionel B. Dyck                          *
         * ------------------------------------------------ */

        /* ------------------------- *
         * Retrieve the z/OS command *
         * ------------------------- */
        arg cmd

        /* -------------------------------------- *
         * Test to see if a command was provided. *
         * Exit if not.                           *
         * -------------------------------------- */
        if length(cmd) = 0 then do
           say "No command passed..."
           exit 8
           end

        /* -------------------------------------------------- *
         * Test for single word command of ADD and if so then *
         * add CONS to the ISPCMDS ISPF Command Table.        *
         * -------------------------------------------------- */
         if strip(cmd) = "ADD" then do
            zctverb  = sysvar('sysicmd')
            zcttrunc = length(zctverb)
            zctact   = "SELECT CMD(%"SYSVAR('SYSICMD')" &ZPARM)"
            zctdesc = "Dynamically added" date() time()
            address ISPExec "TBAdd ISPCMDS"
            exit 0
            end

        /* --------------- *
         * Define Defaults *
         * *custom*        *
         * --------------- */
         stemview = 1   /* set to 0 if stemview is not installed */

        /* ------------------------------------- *
         * Setup the CONSOLE command environment *
         * ------------------------------------- */
        "CONSPROF SOLDISP(no) SOLNUM(400) UNSOLDISP(yes)"
        "CONSOLE ACTIVATE"

        /* --------------------------------- *
         * Execute the provided z/OS Command *
         * --------------------------------- */
        "CONSOLE SYSCMD("cmd") "

        /* --------------------------------------------------------------- *
         * Retrieve the results of the z/OS Command.                       *
         *                                                                 *
         * Note that the default wait time is 30 seconds. That value       *
         * can be changed to any time frame that you need. Be aware that   *
         * it is a maximum wait time and that if the command ends before   *
         * that limit is reached then the results will be returned without *
         * waiting for the wait time to expire.                            *
         * --------------------------------------------------------------- */
        rc = GETMSG('t.','sol',,,30)

        /* --------------------------------- *
         * Close out the CONSOLE environment *
         * --------------------------------- */
        "CONSOLE DEACTIVATE"

        /* -------------------------------------------------- *
         * Test the GETMSG returned message stem and if it    *
         * is not numeric then we know that the wait time was *
         * exhausted waiting for the results of the z/OS      *
         * command.                                           *
         * -------------------------------------------------- */
        if datatype(t.0) /= "NUM" then do
           say "Error - the command" cmd ,
               "did not respond within 30 seconds" ,
               "so no data was captured."
               exit 4
               end

        /* ----------------------------------------- *
         * Test to see if we were called directly or *
         * if another command called us.             *
         * ----------------------------------------- */
         sayit = 1
         if sysvar(sysispf) /= "ACTIVE" then sayit = 0
         if sysvar(sysnest) = "YES" then sayit = 0
         if sayit = 0 then
            do i = 1 to t.0
               say t.i
               end
         if sayit = 1 then do
            maxl = 0
            do i = 1 to t.0
               if maxl < length(t.i) then maxl = length(t.i)
               end
            if stemview = 1 then do
               CALL stemview 'Browse','T.',,,'TSO Console',,maxl
               end
            else do
               cmd_dd = "C"random()
               "Allocate File("cmd_dd") Reuse Unit(vio) Space(1 1)" ,
                        "CYL Dsorg(ps) Recfm(v b) Lrecl("maxl")"
               "Execio * Diskw" cmd_dd "(Stem t. Finis)"
               Address ISPExec
               "LMinit Dataid(dataid) DDname("cmd_dd")"
               "Browse Dataid("dataid")"
               "LMfree Dataid("dataid")"
               Address TSO
               "Free File("cmd_dd")"
               end
            end
