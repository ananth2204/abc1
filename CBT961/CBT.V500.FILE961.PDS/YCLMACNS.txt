/* Rexx by YCOS Yves Colliard Software - www.ycos.de                  */
/*                                                                    */
/*+------------------------------------------------------------------+*/
/*| YCLMACNS     (c)  YCOS Yves Colliard Software                    |*/
/*|                   2014-16                                        |*/
/*|                   Mail yves.colliardÂ§ycos.de                     |*/
/*|                   Use at your own RISK|                          |*/
/*+------------------------------------------------------------------+*/
/*| Support for Split Screen functions within YCLMACRX               |*/
/*| - E    : Edit - EZ Line Command                                  |*/
/*| - EU   : Edit Unix - EZ Line Command                             |*/
/*| - V    : View - VZ Line Command                                  |*/
/*| - VU   : View Unix - VZ Line Command                             |*/
/*| - DZ   : DSLIST - DZ# Line Command                               |*/
/*| - DZU  : DIrList Unix - DZ# Line Command                         |*/
/*+------------------------------------------------------------------+*/
/*| Vers 2.15  01.09.2014 Yves Colliard                              |*/
/*+------------------------------------------------------------------+*/

Address  ISPEXEC "CONTROL ERRORS RETURN"     /* Return ispf errors  */

Address ISPEXEC "VGET (ZOS390RL)"
                   /* "z/OS   01.13.00"   */
ed_parms=""
if ZOS390RL = "z/OS   01.13.00" |,
   left(ZOS390RL,10)="z/OS   02." then do
  Address ISPEXEC "VGET (ZLMAC)"
  if ZLMAC <> "" then do
    ed_parms=" LINECMDS("strip(ZLMAC)")"
  end
end

parse arg What Todo

select
when what="E" then do  /* Edit */
  parse var Todo mydsn myvol
  ZedSMsg = ""
  ZedLMsg = "Edit" MyDsn" in new Split Screen"
  Address ISPEXEC 'SETMSG MSG(ISRZ001)'
  Address ISPEXEC "EDIT DATASET('"MyDsn"')",
          "VOLUME("myvol")"ed_parms
  if RC>4 then do
    ZedSMsg = ""
    ZedLMsg = "Problem during Edit" MyDsn" Return Code:"RC
    Address ISPEXEC 'SETMSG MSG(ISRZ001)'
  end
end
when what="EU" then do  /* Edit USS */
  parse var Todo mydsn
  ZedSMsg = ""
  ZedLMsg = "Edit" MyDsn" in new Split Screen"
  Address ISPEXEC 'SETMSG MSG(ISRZ001)'
  Address ISPEXEC "EDIT FILE(MyDsn) "ed_parms
  if RC>4 then do
    ZedSMsg = ""
    ZedLMsg = "Problem during Edit" MyDsn" Return Code:"RC
    Address ISPEXEC 'SETMSG MSG(ISRZ001)'
  end
end
when what="V" then do  /* View */
  parse var Todo mydsn myvol
  ZedSMsg = ""
  ZedLMsg = "View" MyDsn" in new Split Screen"
  Address ISPEXEC 'SETMSG MSG(ISRZ001)'
  Address ISPEXEC "VIEW DATASET('"MyDsn"')",
          "VOLUME("myvol")"ed_parms
  if RC>4 then do
    ZedSMsg = ""
    ZedLMsg = "Problem during Edit" MyDsn" Return Code:"RC
    Address ISPEXEC 'SETMSG MSG(ISRZ001)'
  end
end
when what="VU" then do  /* View Unix */
  parse var Todo mydsn
  ZedSMsg = ""
  ZedLMsg = "View" MyDsn" in new Split Screen"
  Address ISPEXEC 'SETMSG MSG(ISRZ001)'
  Address ISPEXEC "VIEW FILE(MyDsn) "ed_parms
  if RC>4 then do
    ZedSMsg = ""
    ZedLMsg = "Problem during Edit" MyDsn" Return Code:"RC
    Address ISPEXEC 'SETMSG MSG(ISRZ001)'
  end
end
when what="DZ" then do  /* DSLIST */
  parse var Todo mydsn myvol
  ShowAct = "Dslist"                  /* 2.15 */
  ZedSMsg = ""
  ZedLMsg = "Dslist" MyDsn myvol" in new Split Screen"
  Address ISPEXEC 'SETMSG MSG(ISRZ001)'
  Address ISPEXEC "LMDINIT LISTID(ID) Level("MyDsn")",
          "VOLUME("MyVol")"
  Address ISPEXEC "LMDDISP LISTID("ID")"
  DispRC = RC
  Address ISPEXEC "LMDFREE LISTID("ID")"
  Address ISPEXEC "CONTROL ERRORS CANCEL"
  if DispRC > 0 then do
    ZedSMsg = "LMDDISP-RC="DispRC
                                 /* Eberle 120720 */
    if MyVol <> "" then do            /* 2.13 */
      Address ISPEXEC,
      "CONTROL ERRORS RETURN"
      Address ISPEXEC,
      "LMDINIT LISTID(ID) Level("MyDsn")"
      ZedSMsg  =  ""
      ZedLMsg  =  ShowAct "without Volume" MyVol "in new Split Screen"
      Address  ISPEXEC 'SETMSG MSG(ISRZ001)'
      Address ISPEXEC,
      "LMDDISP LISTID("ID")"
      Disp2RC = RC
      Address ISPEXEC,
      "LMDFREE LISTID("ID")"
      Address ISPEXEC,
      "CONTROL ERRORS CANCEL"
      if Disp2RC > 0 then do
        errvol = "VOL("strip(MyVol,"B")")"
        ZedSMsg = ""
        ZedLMsg = "Dslist" MyDsn errvol" in new Split Screen"
        Address ISPEXEC 'SETMSG MSG(ISRZ001)'
        ZedLMsg = "LMDDISP LEVEL("MyDsn")" errvol "Error"
        Address ISPEXEC 'SETMSG MSG(ISRZ001)'
      end
    end
    else do
      ZedLMsg = "LMDDISP LEVEL("MyDsn") Error"
      Address ISPEXEC 'SETMSG MSG(ISRZ001)'
    end
  end
end
when what="DZU" then do  /* Dir file Unix */
  parse var Todo mydsn
  ZedSMsg = ""
  ZedLMsg = "Directory List" MyDsn" in new Split Screen"
  Address ISPEXEC 'SETMSG MSG(ISRZ001)'
  Address ISPEXEC "DIRLIST PATH(mydsn)"
  select
  when rc=0 then do
    exit 0
    end
  when rc=8 then do
    if lastpos("/",mydsn)>0 then do
      mydsn=left(mydsn,lastpos("/",mydsn)-1)
      ZedSMsg = ""
      ZedLMsg = "Directory List" MyDsn" in new Split Screen"
      Address ISPEXEC 'SETMSG MSG(ISRZ001)'
      address ispexec,
      "DIRLIST PATH(mydsn)"
    end
  end
  otherwise
    ZedSMsg = ""
    ZedLMsg = "Problem during Dir List" MyDsn" Return Code:"RC
    Address ISPEXEC 'SETMSG MSG(ISRZ001)'
  end
end
otherwise
  ZedSMsg = ""
  ZedLMsg = "Unknown parameter" what
  Address ISPEXEC 'SETMSG MSG(ISRZ001)'
end
