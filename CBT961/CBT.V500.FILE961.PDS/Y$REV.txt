/* Rexx by YCOS Yves Colliard Software - www.ycos.de                  */
/*+------------------------------------------------------------------+*/
/*| YCLMACRX     (c)  YCOS Yves Colliard Software                    |*/
/*|                   2004-17                                        |*/
/*|                   Mail yves.colliard§ycos.de                     |*/
/*|                   Use at your own RISK|                          |*/
/*|                                                                  |*/
/*| Function Y$REV:   External line command: REV to call REVIEW      |*/
/*|                                                                  |*/
/*| Author:           Juergen Eberle                                 |*/
/*| Email:            Juergen.Eberle§huk-coburg.de                   |*/
/*+------------------------------------------------------------------+*/
/*| see http://www.prycroft6.com.au/REVIEW/index.html                |*/
/*+------------------------------------------------------------------+*/
/*| VERS 1.01  20.04.2016 Juergen Eberle                             |*/
/*| VERS 1.02  16.11.2016 Bruce Koss                                 |*/
/*|            Changed module name GETLMSG to YCLMACLM               |*/
/*|            Changed module name GETDSN  to YCLMACDS               |*/
/*| VERS 1.03  16.12.2016 Yves Colliard                              |*/
/*+------------------------------------------------------------------+*/

address "ISREDIT"
"MACRO (PARM) NOPROCESS"
"PROCESS RANGE "parm

"(CURLINE CURCOL) = CURSOR"

ShowAct = "Review"
"(LINE) = LINE "Curline
parse value YCLMACDS(Line) with retc mydsn '05'x myvol
if retc = 0 then do

      xx = outtrap('tsomsg.')
      if myvol <> "UNIXPATH" then do
         address "TSO" "ALLOC DUMMY DSN('"MyDsn"') SHR REU"
         retc = RC
         xx = outtrap('xx')
         if retc > 0 then do
            zedsmsg=''
            zedlmsg=tsomsg.1
            ADDRESS ISPEXEC "SETMSG MSG(ISRZ001)"
            "CURSOR = "CURLINE CURCOL
            return retc
         end
      end
      else do
         cc = syscalls("ON")
         address SYSCALL "stat "MyDsn" st."
         if st.0 = 0 then do
            zedsmsg=''
            zedlmsg="File '"MyDsn"' nicht vorhanden."
            ADDRESS ISPEXEC "SETMSG MSG(ISRZ001)"
            "CURSOR = "CURLINE CURCOL
            return 8
         end
      end

   if myvol = "" | myvol = "UNIXPATH" then do
      Address ISPEXEC "CONTROL ERRORS RETURN"
      Address ISPEXEC "SELECT CMD(REV '"MyDsn"') SUSPEND"
      if rc=20 then call not_found
      retc = rc
   end
   else do
      Address ISPEXEC "CONTROL ERRORS RETURN"
      Address ISPEXEC "SELECT CMD(REV '"MyDsn"' VOLUME("myvol")) SUSPEND"
      if rc=20 then call not_found
      retc = rc
   end

end
"CURSOR = "CURLINE CURCOL
return retc
not_found:
  zedsmsg = ""
  zedlmsg = "REVIEW command not found - must first be installed"
  Address ISPEXEC "SETMSG MSG(ISRZ001)"
return
