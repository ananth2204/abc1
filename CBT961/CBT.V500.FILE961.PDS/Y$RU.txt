/* Rexx by YCOS Yves Colliard Software - www.ycos.de                  */
/*                                                                    */
/*+------------------------------------------------------------------+*/
/*| Ruler - Display a Ruler as a note below the current cursor pos   |*/
/*|         define :RU or :RU1 as a PFK to simplify usage|           |*/
/*+------------------------------------------------------------------+*/
/*| RU will create a ruler as a note starting at cursor position     |*/
/*| Example:      X <== Cursor is here when RULER starts             |*/
/*|               ----+----1----+----2----+----3----+----4----+---...|*/
/*| RU# (any number from 1-...) will give the column number on 10th  |*/
/*|               ----+---10----+---20----+----30-...--150----+---...|*/
/*+------------------------------------------------------------------+*/

/* Based on - many thanks to Norbert Haas                             */
/* REXX ------------------------------------------------------------- */
/* Name:         RULER                                                */
/* Type:         Macro                                                */
/* Author:       Norbert Haas                                         */
/* Date:         21.12.2017                                           */
/* Syntax:       Linecommand RULER and cursor within data             */
/* Parameter:    none                                                 */
/* Description:  Shows a ruler from the cursor position               */
/* Example:      X <== Cursor is here when RULER starts               */
/*               ----+----1----+----2----+----3----+----4----+----5...*/
/* Comment:      Works perfect, if command RULER is on a PF-key       */
/*                                                                    */
/* Vers.  Date       Author    Reason                                 */
/* ------ ---------- --------- -------------------------------------- */
/* V03.01 02.03.2018 Y.Colliard                                       */
/* V02.01 21.12.2017 N.Haas    New Version (engl.)                    */
/* V01.01 29.02.2007 N.Haas    Creation (ยง Ford)                      */
/* ------------------------------------------------------------------ */


ADDRESS ISREDIT

"MACRO   (PARM) NOPROCESS"                   /* Get line command    */

parse var parm "RU" numb
select
when numb=""              then numb=0 /* line ruler        */
when datatype(numb)="NUM" then numb=1 /* with number       */
otherwise
  ZedSMsg = "Value not numeric"
  ZedLMsg = "This cannot happen|"
  Address ISPEXEC 'SETMSG MSG(ISRZ001)'
  exit
end
/* */
"(dmy, col) = CURSOR"
"(dw) = SAVE_LENGTH" dmy
if rc=6 then do /* not variable */
  "(dw) = LRECL"
end
if col = 0 then col = 1        /* added by LBD */
rl = dw - col + 1
if rl<1 then do
  ZedSMsg = "Cursor pos error"
  ZedLMsg = "Cursor position not within line"
  Address ISPEXEC 'SETMSG MSG(ISRZ001)'
  exit
end
/* setup */
ruler = COPIES(' ', col-1)
/* */
select
when numb=0 then do /* line ruler */
  /* */
  rul="----+----1----+----2----+----3----+----4----+----5"
  rul=rul"----+----6----+----7----+----8----+----9----+----0"
  /* first setting */
  ruler=ruler""left(rul,min(rl,100))
  rl=rl-min(rl,100)
  /* more than 100 */
  DO while rl>0
    ruler=ruler""left(rul,min(rl,100))
    rl=rl-min(rl,100)
  END
  /* insert note */
  "LINE_AFTER .ZCSR = INFOLINE (ruler)"
end
otherwise           /* line ruler with nmbers */
  /* */
  if rl>99999 then do
    ZedSMsg = "not supported"
    ZedLMsg = "more than 99999 cols not supported:" rl
    Address ISPEXEC 'SETMSG MSG(ISRZ001)'
    exit
  end
  /* */
  DO cnt=10 to rl by 10
    ruler = ruler"----+"RIGHT(cnt,5,"-")
  END
  ruler=left(ruler"-----+----",dw)
  /* insert notes */
  "LINE_AFTER .ZCSR = INFOLINE (ruler)"
end
/* set cursor */
"CURSOR = "dmy col
lcmd=""
return 0

