/***  REXX  **********************************************************/
/*  Funktion:  Simple sample to edit a dataset and activate          */
/*             other edit line commands                              */
/*             here a line command YVES will be added                */
/*  Copyright 2014-14 YCOS Yves Colliard Software GmbH V1.0          */
/*********************************************************************/
/* */
Address  ISPEXEC
"CONTROL ERRORS RETURN"     /* Return ispf errors  */

"VGET (ZOS390RL)"

YCLINECMDS=""
ed_parms=""
                   /* "z/OS   01.13.00"   */
if ZOS390RL = "z/OS   01.13.00" |,
   left(ZOS390RL,10)="z/OS   02." then do
  "VGET (ZLMAC)"
  if ZLMAC <> "" then do
    YCLINECMDS=ZLMAC
  end
end

/* */
if YCLINECMDS<>"" & YCLINECMDS<>"YCLINECMDS" then do
  'TBOPEN    'YCLINECMDS
  'TBTOP     'YCLINECMDS
  'TBSKIP    'YCLINECMDS
  do l1=1 until rc>0
    LCNAME.l1=ZELCNAME
    LCSLNM.l1=ZELCSLNM
    LCPGMF.l1=ZELCPGMF
    LCBLKA.l1=ZELCBLKA
    LCMLTA.l1=ZELCMLTA
    LCDSTA.l1=ZELCDSTA
    'TBSKIP    'YCLINECMDS
  end
  'TBCLOSE   'YCLINECMDS
end
/* */
'TBERASE   YCTMPLCM'
select
when rc=0 then do
  nop
end
when rc=8 then do
  nop
end
otherwise
  say 'Table Erase error - rc' rc 'YCTMPLCM'
  say 'Contact YCOS Support'
  exit
end
'TBCREATE YCTMPLCM NAMES(ZELCNAME, ZELCSLNM, ZELCPGMF,',
          'ZELCBLKA, ZELCMLTA, ZELCDSTA)'
/* */
if YCLINECMDS<>"" & YCLINECMDS<>"YCLINECMDS" then do
  do l2=1 to l1
    ZELCNAME=LCNAME.l2
    ZELCSLNM=LCSLNM.l2
    ZELCPGMF=LCPGMF.l2
    ZELCBLKA=LCBLKA.l2
    ZELCMLTA=LCMLTA.l2
    ZELCDSTA=LCDSTA.l2
    'TBADD YCTMPLCM'
  end
end

/*----- single line commands          ----------------------------*/
/* Macro   name of the Rexx or program to get control             */
ZELCSLNM="YLM02"       /* line cmd YVES */
/* Program Macro   N=Rexx (or Clist) Y=program                    */
ZELCPGMF="N"
/* Block Format   Y=block commands are supported (like: rr   rr)  */
ZELCBLKA="N"
/* Multi Line   Y=many lines as target (like: r9)                 */
ZELCMLTA="Y"
/* Dest Used   Y=a destination line is needed (like: c -> a)      */
ZELCDSTA="N"

/* User Command   name of the line command (1-6 chars)            */
ZELCNAME=left("YVES",6)
'TBADD YCTMPLCM'

/* */
'TBCLOSE YCTMPLCM'
/* */

ed_parms=" LINECMDS(YCTMPLCM)"

/* alloc output datasets */
call bpxwdyn "alloc space(15,15) tracks new delete lrecl(132)",
   "recfm(f,b) dsorg(ps) msg(ymsg2.) rtddn(ydd2) rtdsn(ydsn2)"
if result<>0 then do
  say "Alloc temp dataset error:" result
  do i=1 to ymsg2.0
    say ymsg2.i
  end
  exit 0
end
ot.1="Try to use YVES or line YVES# as a line command"
ot.2="Try to use YVES or line YVES# as a line command"
ot.3="Try to use YVES or line YVES# as a line command"
ot.4="Try to use YVES or line YVES# as a line command"
ot.5="Try to use YVES or line YVES# as a line command"
/* write the data */
address mvs "execio 5 diskw "ydd2" (stem ot. finis"
/* */
zedsmsg=""
zedlmsg="Edit line commands activated"
"SETMSG MSG(ISRZ001)"
/* show the result */
"lminit dataid(myou) ddname("ydd2")"
"edit dataid("myou")"ed_parms
"lmfree dataid("myou")"
address tso "free fi("ydd2")"
