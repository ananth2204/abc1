./  ADD  NAME=BOOLEAN,LEVEL=00,SOURCE=0,LIST=ALL
         MACRO
         BOOLEAN &B
.*
.* sample invocation:     label1  boolean  flag1,(d1fil1,d1fil2,f3,f4)
.*                        equated fields are filled from x'80' to x'01'
.*                        with names from first to last in parens
.*
.* *******************************************************************
.*                                                                   *
.*   THE MODS ON THIS TAPE HAVE BEEN USED SUCCESSFULLY AND TO THE    *
.* BEST OF OUR KNOWLEDGE THEY ARE OPERATIONAL, HOWEVER NO WARRANTY IS*
.* MADE TO THE ACCURACY OF THE MODS AND NO RESPONSIBILITY IS ASSUMED *
.* FOR ANY MODIFICATION DIRECTLY OR INDIRECTLY CAUSED BY THE USE OF  *
.* THE MODIFICATIONS.  IT IS THE USERS RESPONSIBILITY TO EVALUATE THE*
.* USEFULLNESS OF THE MATERIAL.                                      *
.*                                                                   *
.*   WE DO NOT GUARANTEE TO KEEP ANY MATERIAL PROVIDED UP TO DATE,   *
.* NOR DO WE GUARANTEE TO PROVIDE ANY CORRECTIONS OR EXTENSIONS MADE *
.* IN THE FUTURE.                                                    *
.*                                                                   *
.* *******************************************************************
         GBLC  &SWN(100)
         GBLC  &SWB(800)
         GBLA  &SWA(800)
         GBLA  &SW1
         GBLA  &SW2
         LCLC  &OP1
         LCLC  &OP2
         LCLC  &SWL
         LCLA  &NM
         LCLA  &NN
         LCLC  &TEMP
         AIF   (N'&SYSLIST NE 2).SW0010
&OP1     SETC  '&SYSLIST(1)'
&OP2     SETC  '&SYSLIST(2)'
         AGO   .SW0040
.SW0010  ANOP
         MNOTE 8,'BOOLEAN01 - TWO PARAMETERS MUST BE USED ON "BOOLEAN" *
               MACRO (THE FIRST MAY BE NULL)'
         MEXIT
.SW0040  ANOP
         AIF   (N'&SYSLIST(2) GT 8).SW0070
         AIF   (N'&SYSLIST(2) LT 1).SW0080
&NM      SETA  1
&SWL     SETC  '&OP1'
&SW1     SETA  &SW1+1
&SWN(&SW1) SETC '&SWL'
***** START OF SWITCH BYTE *****
         AIF   ('&SWL' EQ '').SW0050
&SWL     DS    X'00'
.SW0050  ANOP
         AIF   (K'&SYSLIST(2,&NM) LT 1).SW0090
&SWL     SETC  '&SYSLIST(2,&NM)'
         AIF   (&NM EQ 1).SW00580
         AIF   (&NM EQ 2).SW00540
         AIF   (&NM EQ 3).SW00520
         AIF   (&NM EQ 4).SW00510
         AIF   (&NM EQ 5).SW00508
         AIF   (&NM EQ 6).SW00504
         AIF   (&NM EQ 7).SW00502
&TEMP    SETC  'X''01'''
         AGO   .SW0050A
.SW00502 ANOP
&TEMP    SETC  'X''02'''
         AGO   .SW0050A
.SW00504 ANOP
&TEMP    SETC  'X''04'''
         AGO   .SW0050A
.SW00508 ANOP
&TEMP    SETC  'X''08'''
         AGO   .SW0050A
.SW00510 ANOP
&TEMP    SETC  'X''10'''
         AGO   .SW0050A
.SW00520 ANOP
&TEMP    SETC  'X''20'''
         AGO   .SW0050A
.SW00540 ANOP
&TEMP    SETC  'X''40'''
         AGO   .SW0050A
.SW00580 ANOP
&TEMP    SETC  'X''80'''
.SW0050A ANOP
&SWL     EQU   &TEMP
&NM      SETA  &NM+1
&SW2     SETA  &SW2+1
&SWB(&SW2) SETC '&SWL'
&SWA(&SW2) SETA &SW1
         AIF   (&NM LE N'&SYSLIST(2)).SW0050
*****  END OF SWITCH BYTE  *****
         MEXIT
.SW0070  ANOP
         MNOTE 8,'BOOLEAN02 - 8 IS THE MAXIMUM NUMBER OF BIT NAMES ALLO*
               WED'
         MEXIT
.SW0080  ANOP
         MNOTE 8,'BOOLEAN03 - AT LEAST 1 BIT NAME MUST BE SPECIFIED'
         MEXIT
.SW0090  ANOP
&TEMP    SETC  '&NM.TH'
         AIF   (&NM GT 3).SW0090C
         AIF   (&NM EQ 1).SW0090A
         AIF   (&NM EQ 2).SW0090B
&TEMP    SETC  '3RD'
         AGO   .SW0090C
.SW0090A ANOP
&TEMP    SETC  '1ST'
         AGO   .SW0090C
.SW0090B ANOP
&TEMP    SETC  '2ND'
.SW0090C ANOP
         MNOTE 8,'BOOLEAN04 - THE &TEMP PARAMETER OF THE BIT NAME LIST *
               IS INVALID'
         MEND
./  ADD  NAME=$STJCTX,LEVEL=00,SOURCE=0,LIST=ALL
         MACRO  - $STJCTX -- JCT EXTENTION
&LABEL   $STJCTX
.* *******************************************************************
.*                                                                   *
.*   THE MODS ON THIS TAPE HAVE BEEN USED SUCCESSFULLY AND TO THE    *
.* BEST OF OUR KNOWLEDGE THEY ARE OPERATIONAL, HOWEVER NO WARRANTY IS*
.* MADE TO THE ACCURACY OF THE MODS AND NO RESPONSIBILITY IS ASSUMED *
.* FOR ANY MODIFICATION DIRECTLY OR INDIRECTLY CAUSED BY THE USE OF  *
.* THE MODIFICATIONS.  IT IS THE USERS RESPONSIBILITY TO EVALUATE THE*
.* USEFULLNESS OF THE MATERIAL.                                      *
.*                                                                   *
.*   WE DO NOT GUARANTEE TO KEEP ANY MATERIAL PROVIDED UP TO DATE,   *
.* NOR DO WE GUARANTEE TO PROVIDE ANY CORRECTIONS OR EXTENSIONS MADE *
.* IN THE FUTURE.                                                    *
.*                                                                   *
.* *******************************************************************
.*********************************************************************
.*                                                                   *
.* FUNCTION: MAPPING MACRO FOR JCT EXTENTION.                        *
.*                                                                   *
.* REVISION HISTORY:                                                 *
.*                                                                   *
.*   DATE   NAME         DESCRIPTION OF CHANGE                       *
.* -------- ------------ ---------------------------------------------*
.* 04/20/00 S MCCOLLEY   ADDED THIS COMMENT BLOCK. *                  *
.* 11/13/03 S MCCOLLEY   ADDED CONDITIONAL EXPANSION OF $JCTX *       *
.* 08/18/06 S MCCOLLEY   ADDED VRM FIELD - TO AIDE OTHERS IN DEBUGGING*
.* 10/11/06 S MCCOLLEY   ADDED NEW JCX AND BERT FIELDS FOR UPGRADE    *
.* 10/11/06 S MCCOLLEY   ADDED COMMON DSECT FOR ECSA AREA - HOLDS THE *
.*                            VALUES FOR NEW PARMS -SMF#, WAITS ETC.  *
.* *                                                                  *
.**********************************************************************
         LCLC  &JCXOK
&JCXOK   SETC  T'JCXORG                     GET THE TYPE FOR JCXORG
         AIF   ('&JCXOK' NE 'U').STNXPND      ** IF NOT DEFINED ADD IT
         $JCTX
.STNXPND ANOP
STJCTX   DSECT
*
* STSC EXTENSION OF THE JCT TO BE USED FOR ALL EXITS
*
         DS    XL(JCXORG-JCTX)     DEFINE REQUIRED $JCTX PREFIX AREA
*
UJCXMFLG DS    XL1                 RESOURCE ROUTING FLAGS
UJCXAFT  EQU   B'00000001'           /*AFTER  SPECIFIED
UJCXBEF  EQU   B'00000010'           /*BEFORE SPECIFIED
UJCXCNTL EQU   B'00000100'           /*CNTL   SPECIFIED
UJCXWITF EQU   B'00001000'           /*WITH   SPECIFIED
*              B'11110000'           # OF /*CNTL NAMES PRESENT
*
*                                  i.e. what type cards are present
        BOOLEAN UJCXUFLG,(UJCXXVRM,UJCXXEYE)
*--UJCXXVRM  VRM FIELD IS PRESENT
*--UJCXXEYE  EYECATCHER IS PRESENT- it also indicates a new format rec.
UJCXDLYS DS    X                   JOB DELAY REASONS FROM OUR EXIT 49
*
UJCXWITH DS    CL8                 SAVE A WITH NAME HERE
UJCXAFTN DS    CL8                 SAVE AN AFTER NAME HERE
UJCXBFOR DS    CL8                 SAVE A BEFORE NAME HERE
UJCXDNAM DS    CL45                NAMES FROM CNTL CARDS AND THEIR     X
                                    SHARE TYPES - CL8=NAME, FOLLOWED BYX
                                    1 BYTE OF TYPE CL8RESNAME, CL1TYPE
*
         DS    0H                  DOUBLE WORD ALLIGN
*
*
*** NOTE ! ! ***  the old version had the 4 byte work field where we
*            ***  now keep the VRM field - that way we can safely test
*            ***  for a VRM value - old versions won't have a vrm, but
*            ***  there will be a data area so we won't get an abend
*            ***  when checking for one.
*            ***  This is just a safeguard, since we will also check
*            ***  to see if the VRM and EYECATCHER flags are on before
*            ***  trying to use any of the new fields.
*
UJCXVRM  DS    XL4                 VRM AREA X'00VVRRMM' USEFUL ID INFO
*
*
*  NEW fields from here on down... (ujcxwork and ujcxvrm were swapped)
*
*
UJCXWORK DS    CL4                 UNUSED, BUT KEPT FOR COMPATABILITY
*
UJCXID   DS    CL4                 EYECATCHER CL4'JCX' JCT EXTENSION
** *************************************************************** **
** *************************************************************** **
**                           **                                    **
**                           **                                    **
** SSM  SHARED SPOOL MODS - NEW FUNCTION ZOS 1.8 11/06 START THERE **
**                                                                 **
** *************************************************************** **
*
*
*/* FLAGS BYTE     - SUPPORT FOR WAITFOR AND WAITTILL
*
      BOOLEAN UJCWATF,(UJCWATT,UJCWATFR,UJCWATTS,UJCWATFS,UJCWALLS)
*
*-- UJCWATT      WAIT UNTIL TIME SPECIFIED
*-- UJCWATFR     WAIT FOR TIME SPECIFIED
*-- UJCWATTS     WAIT UNTIL IS SATISFIED
*-- UJCWATFS     WAIT FOR TIME IS SATISFIED
*-- UJCWALLS     ALL WAIT TIMES ARE NOW SATISFIED
*
       BOOLEAN UJCTAPF1,(UJCTPANY,UJCTAPNON)
*
*-- UJCTPANY     SET IF ANY TAPES ARE REQUIRED
*-- UJCTPNON     SET WHEN NO MORE TAPES ARE NEEDED
*
*/* FLAGS BYTE     - misc. support member routing - noauto and without
*
       BOOLEAN UJCMSCF1,(UJCMSWOJ,UJCMSNOT,UJCMSMEM,UJCMSNMM)
*
*-- UJCMSWOJ     WITHOUT JOBNAME SPECIFIED
*-- UJCMSNOT     NO AUTO RESOURCE ROUTING THIS JOB
*-- UJCMSMEM     OK MEMBER ROUTING PRESENT
*-- UJCMSNMM     NOT OK MEMBER ROUTING PRESENT
*
UJCOPNF  DS    XL1                 AN OPEN UNUSED FLAG BYTE
*
* FIELDS FOR /*HOLDTIL HH:MM:SS  CARDS
*
*  ALL TIME VALUES ARE BIN EQUIVELENT TIME VALUES
*
UJCHLDU  DS    XL4                 USER REQUESTED HOLD UNTIL TIME
*                                   x'hhmmss0f' is the format
UJCHLDUX DS    XL4                 TIME / DATE HOLD TILL IS SATISFIED
*                                   STCK TIME FORMAT OR STKE+1(4)
*
* SUPPORT /*HOLDFOR HH:MM:SS  CARDS
*
UJCHLDF  DS    XL4                 USER REQUESTED HOLD FOR TIME
*                                   x'hhmmss0f' is the format
UJCHLDFX DS    XL4                 TIME / DATE hoLDFOR IS SATISFIED
*                                   STCK TIME FORMAT OR STKE+1(4)
*
UJWIOUT  DS    CL8                 SUPPORT FOR /*WITHOUT JOBNAME
*
*    LIMIT JOB SELECTION BASED ON A MAS MEMBER NAME(S)
*
*
UJMMTQE  DS    XL1                 BIT MAPPED BYTE SHOWS WHICH MEMBERS
*                                  OF THE MAS HAD $STIMERS ISSUED FOR
*                                  THE CURRENT WAIT REASON -
*                                  MEMBER 1 =B'XXXX XXX1'
*                                  MEMBER 4 =B'XXXX 1XXX' ETC. ETC.
*
UJMMSTK  DS    8XL4                ROOM FOR 8 STCK VALUES FROM 8 MEMBER
*                                  MEMBER STARTUP TIMES, USED WITH
*                                  STTQEX AND $STIMER, AND UBRMMTQE.
UJRSVD   DS    XL31                RESERVED, CURRENTLY UNUSED SPACE.
*
UJCXSLN1 EQU   *-STJCTX            LENGTH OF SECTION
*
         MEND
./  ADD  NAME=$STQNAME,LEVEL=00,SOURCE=0,LIST=ALL
         MACRO -- $STQNAME -- SUNTRUSTS JQE EXTENSION
&LABEL   $STQNAME &DSECT=YES
.*
.* *******************************************************************
.*                                                                   *
.*   THE MODS ON THIS TAPE HAVE BEEN USED SUCCESSFULLY AND TO THE    *
.* BEST OF OUR KNOWLEDGE THEY ARE OPERATIONAL, HOWEVER NO WARRANTY IS*
.* MADE TO THE ACCURACY OF THE MODS AND NO RESPONSIBILITY IS ASSUMED *
.* FOR ANY MODIFICATION DIRECTLY OR INDIRECTLY CAUSED BY THE USE OF  *
.* THE MODIFICATIONS.  IT IS THE USERS RESPONSIBILITY TO EVALUATE THE*
.* USEFULLNESS OF THE MATERIAL.                                      *
.*                                                                   *
.*   WE DO NOT GUARANTEE TO KEEP ANY MATERIAL PROVIDED UP TO DATE,   *
.* NOR DO WE GUARANTEE TO PROVIDE ANY CORRECTIONS OR EXTENSIONS MADE *
.* IN THE FUTURE.                                                    *
.*                                                                   *
.* *******************************************************************
.**********************************************************************
.*
.* FUNCTION: DEFINE A BERT, AN EXTENSION TO THE JQE.
.*
.* NOTES:    THIS BERT ENTRY CONTAINS ALL THE FIELDS WE USED TO
.*           INCLUDE INTO THE $JQE MACRO PRIOR TO OS/390.
.*
.*           SOME OF THESE FIELD MUST BE IDENTICALLY DEFINED AND USED
.*           IN THE JCTX: UBRDNAME, UBRUFLAG, UBRMFLAG, UBRDLAYS.
.*
.*
.* REVISION HISTORY:
.*
.*   DATE   NAME         DESCRIPTION OF CHANGE
.* -------- ------------ ----------------------------------------------
.* 04/20/00 S MCCOLLEY   INITIAL VERSION -- FOR OS/390 2.6.
.*
.**********************************************************************
.*
STQNAME  DSECT ,
*
* UBRDNAME IS THE LIST OF AFTER/BEFORE JOBNAMES AND/OR CNTL RESOURCES
*          AND THE /*CNTL SHARE TYPE ("S" OR "E").  THESE SHARE TYPE
*          BYTES IMMEDIATELY FOLLOW THE RESOURCE NAME.
*
*   AFTER   (IF ANY) MUST BE FIRST  (SEE UBRAFT).
*   BEFORE  (IF ANY) MUST BE SECOND (SEE UBRBEF).
*   CNTL(S) (IF ANY) MUST BE LAST   (SEE UBRCNTL AND 4 HIGH ORDER BITS
*           OF UBRMFLAG.  THERE CAN BE UP TO 4 CNTL NAMES.)
*
UBRWITH  DS    CL8                 PLACE TO HOLD A /*WITH NAME
UBRAFTR  DS    CL8                 PLACE TO HOLD A /*AFTER NAME
UBRBFOR  DS    CL8                 PLACE TO HOLD A /*BEFORE NAME
UBRDNAME DS    CL45                NAMES FROM CNTL CARDS AND SHARE TYPES
                                   CL8(RESNAME)+CL1(TYPE) TYPE=S¦E¦P
UBRDNAML EQU   9                   LENGTH OF EACH NAME/SHARE TYPE FIELD
*
*                               UBRUFLAG WAS ** UNUSED ** BEFORE 1.7
        BOOLEAN UBRUFLAG,(UBRXXVRM,UBRXXEYE)
*-- UBRXXVRM  VRM FIELD IS PRESENT
*-- UBRXXEYE  EYECATCHER IS PRESENT
*
*
UBRMFLAG DS    BL1                 RESOURCE FLAGS
UBRAFT   EQU   B'00000001'           /*AFTER  SPECIFIED
UBRBEF   EQU   B'00000010'           /*BEFORE SPECIFIED
UBRCNTL  EQU   B'00000100'           /*CNTL   SPECIFIED
UBRWITHF EQU   B'00001000'           /*WITH   SPECIFIED
*              B'11110000'           # OF /*CNTL NAMES PRESENT
*
UBRNCTL  EQU   X'08'               MAX # OF NAMES INCLUDING
*                                    /*AFTER, /*BEFORE, AND  /*CNTL
*
*
UBRDLAYS DS    BL1                 DELAY REASONS FROM OUR EXIT 49.  .
*
*  THE - UBRXXVRM BIT IS NEW - IT WAS UNUSED BEFORE AND WILL BE TESTED
* BEFORE TRYING TO ACCESS ANY OF THE NEW EXPANDED FIELDS.  IT IS
* SIMPLY A BIT USED FOR COMPATIBILITY AS WE EXPAND THE EXTENSIONS FOR
* NEW FUTURE FUNCTION AND ADD VRM FIELDS AT THE JES2 1.7 LEVEL.
*
*
*
UBRDLWIT EQU   X'01'                /*WITH CONTENTION DELAY
UBRDLWIO EQU   X'02'                /*WITHOUT  CONTENTION (RESUSED)
UBRDLWFR EQU   X'03'                TIMING DELAY HOLDTIL/FOR (REUSD
UBRDLWTL EQU   X'04'                TIMING DELAY HOLDTIL/FOR (REUSD
UBRDLWTG EQU   X'05'                 GENERAL PURPOSE WAIT
UBRDLDUP EQU   X'06'                HOLD/DUP   CONTENTION
UBRDLAFT EQU   X'07'                /*AFTER    CONTENTION
UBRDLBEF EQU   X'08'                /*BEFORE   CONTENTION
UBRDLCTL EQU   X'09'                /*CNTL     CONTENTION
UBRDLCLS EQU   X'0A'                CLASS LIMIT REACHED
UBRDLJNM EQU   X'0B'                JOBNAME MASK LIMIT HAS BEEN REACHED
UBRDLSEL EQU   X'0C'                JOB HAS BEEN SELECTED - NO DELAY
UBRDLUID EQU   X'0D'                UID MASK LIMIT REACHED
*                                  SET TO MATCH THE UJCXWORK WORK AREA
UBRVRM   DS    XL4                X'00VVRRMM' (VRM) 1.7.0
*
UBRVRML  EQU   00010700            THE VRM VALUE
UBRXID   DS    CL4                EYECATCHER CL4'JQEX'  JQE BERT EXT.

       BOOLEAN UBRWATF,(UBRWATT,UBRWATFR,UBRWATTS,UBRWATFS,UBRWALLS)
*
*-- UBRWATT    WAIT UNTIL TIME SPECIFIED
*-- UBRWATFR   WAIT FOR TIME SPECIFIED
*-- UBRWATTS   WAIT UNTIL IS SATISFIED
*-- UBRWATFS   WAIT FOR TIME IS SATISFIED
*-- UBRWALLS   ALL WAIT TIMES ARE NOW SATISFIED
*
       BOOLEAN UBRTAPF1,(UBRTPANY,UBRTAPNON)
*
*-- UBRTPANY   SET IF ANY TAPES ARE REQUIRED
*-- UBRTPNON   SET WHEN NO MORE TAPES ARE NEEDED
*
*/* FLAGS BYTE     - SUPPORT FOR THIS AND THAT - LEFT OVERS
*
       BOOLEAN UBRMSCF1,(UBRMSWOJ,UBRMSNOT,UBRMSMEM,UBRMSNMM,          X
               UBRSABEF,UBRSAAFT)
*
*-- UBRMSWOJ   WITHOUT JOBNAME SPECIFIED
*-- UBRMSNOT   NO AUTO RESOURCE ROUTING THIS JOB
*-- UBRMSMEM   OK MEMBER ROUTING PRESENT
*-- UBRMSNMM   NOT OK MEMBER ROUTING PRESENT
*-- UBRSABEF   POSITIVE ACKNOLEDGEMENT OF BEFORE IS SATISFIED.
*-- UBRSAAFT   POSITIVE ACKNOLEDGEMENT OF AFTER IS SATISFIED.
*
*
UBROPNF1 DS    XL1                 AN OPEN FLAG - FOR ALIGNMENT
*
*
*
* FIELDS FOR /*HOLDTIL HH:MM:SS VALUES
*
*  ALL TIME VALUES ARE BIN EQUIVELENT TIME VALUES
*
UBRHLDU  DS    CL4                 USER REQUESTED HOLD UNTIL TIME
*                                   x'hhmmss0f' is the format
UBRHLDUX DS    XL4                 DATE/TIME HOLDTIL WILL BE SATISFIED
*                                   STCK TIME FORMAT OR STKE+1(4)
*
* SUPPORT /*HOLDFOR HH:MM:SS  CARDS
*
UBRHLDF  DS    CL4                 USER REQUESTED HOLD FOR TIME
*                                   x'hhmmss0f' is the format
UBRHLDFX DS    XL4                 DATE/TIME HOLDFOR WILL BE SATISFIED
*                                   STCK TIME FORMAT OR STKE+1(4)
*
UBRWIOUT DS    CL8                 SUPPORT FOR /*WITHOUT JOBNAME
*
*    LIMIT JOB SELECTION BASED ON A MAS MEMBER NAME(S)
*
*

UBRMMTQE DS    XL1                 BIT MAPPED BYTE SHOWS WHICH MEMBERS
*                                  OF THE MAS HAD $STIMERS ISSUED FOR
*                                  THE CURRENT WAIT REASON -
*                                  MEMBER 1 =B'XXXX XXX1'
*                                  MEMBER 4 =B'XXXX 1XXX' ETC. ETC.
*
UBRMMSTK DS    8XL4                ROOM FOR 8 STCK VALUES FROM 8 MEMBER
*                                  MEMBER STARTUP TIMES, USED WITH
*                                  STTQEX AND $STIMER, AND UBRMMTQE.
UBRRSVD  DS    XL31                RESERVED, CURRENTLY UNUSED SPACE.
*
*
STQNAMELN EQU  *-STQNAME           LENGTH OF THE BERTIE
*
         MEND
./  ADD  NAME=$SSMTB,LEVEL=00,SOURCE=0,LIST=ALL
         MACRO - $SSMTB - SunTrust Common ECSA area for SSM controls
.* *******************************************************************
.*                                                                   *
.*   THE MODS ON THIS TAPE HAVE BEEN USED SUCCESSFULLY AND TO THE    *
.* BEST OF OUR KNOWLEDGE THEY ARE OPERATIONAL, HOWEVER NO WARRANTY IS*
.* MADE TO THE ACCURACY OF THE MODS AND NO RESPONSIBILITY IS ASSUMED *
.* FOR ANY MODIFICATION DIRECTLY OR INDIRECTLY CAUSED BY THE USE OF  *
.* THE MODIFICATIONS.  IT IS THE USERS RESPONSIBILITY TO EVALUATE THE*
.* USEFULLNESS OF THE MATERIAL.                                      *
.*                                                                   *
.*   WE DO NOT GUARANTEE TO KEEP ANY MATERIAL PROVIDED UP TO DATE,   *
.* NOR DO WE GUARANTEE TO PROVIDE ANY CORRECTIONS OR EXTENSIONS MADE *
.* IN THE FUTURE.                                                    *
.*                                                                   *
.* *******************************************************************
.**********************************************************************
.*
.* $SSMTB - our SSM table area, referenced through a Name/Token pair.
.* The name used to get our token changes depending on whether JES2
.* has fully initialized:  at first, it is the literal SSMTEMP- plus
.* the JES2 ASID, but after initialization, it is SSM plus the local
.* XCFGRPNM.  The token is laid out as follows (16 bytes long):
.*
.*       AL4   A($SSMTB)
.*       CL4   =C'SSMT'
.*       AL4   A($SSMTB- BACKUP)
.*       AL4   OPEN FOR NOW - OR - C'SSMT'
.*
.**********************************************************************
&LABEL   $SSMTB
SSMTB    DSECT  ,
* SSMT Identification information
SSMLEN   DS     F                  length of table
SSMEYE   DS     CL4                eyecatcher  "SSMT"
SSMVRM   DS     XL4                version numeric X'00VVRRMM'
SSMUPCEC DS     F                  POINTER TO FIRST OF OUR TQEX PCE'S
SSMPCEDH EQU    (SSMUPCEC-SSMTB)   DISPLACEMENT TO USER PCE HEAD PTR
SSMUPCEK DS     F                  COUNTS FOR OUR TQEX PCES
*                                  1ST XL2 = #DEFINED, 2ND XL2 = #ACTIV
SSMPCEDK EQU    (SSMUPCEK-SSMTB)   DISPLACEMENT TO USER PCE COUNTS
*
SSMTQCB1 DS     F                  POINTER TO FIRST TQE CONTROL BLOCK
*
* Global SSM controls
*
SSMSMFNM DS     B                  SMF record number (0-255)
*
SSMSMFOP DS     CL1                SMF option:
SSMSMF_ALL      EQU  C'A'             All
SSMSMF_INPUT    EQU  C'I'             Input
SSMSMF_ACTION   EQU  C'X'             Action
SSMSMF_NONE     EQU  C' '             None
*
         DS     B                  open/reserved byte
*
SSMALLWS DS     B                  Allow $S commands (on/off)
SSMALLWS_ON     EQU  X'FF'            allow
SSMALLWS_OFF    EQU  X'00'            do not allow (default)
*
SSMCLSOP DS     B                  Limit by class (use/do not use)
SSMCLSOP_ON     EQU  X'FF'            use limit
SSMCLSOP_OFF    EQU  X'00'            use limit
*
SSMBEAFT DS     CL1                Before/After processing:
SSMBEAFT_POS    EQU  C'P'             Positive - future option only!
SSMBEAFT_DELAY  EQU  C'D'             Delay
SSMBEAFT_PREMOD EQU  C' '             Pre-modification behaviour
SSMBATIM DS     H                  Before/After time (for delay)
*
SSMUIDMX DS     H                  Userid Max (1-4096)
SSMUMASK DS     BL2                Userid Mask (bit representation)
         ORG    SSMUMASK
SSMUMHI  DS     B                     first four characters
SSMUMLO  DS     B                     last four characters
*
SSMJBNMX DS     H                  Jobname Max (1-4096)
SSMJMASK DS     BL2                Jobname Mask (bit representation)
         ORG    SSMJMASK
SSMJMHI  DS     B                     first four characters
SSMJMLO  DS     B                     last four characters
*
SSMUMSKC DS     CL8                Userid Mask (characters)
SSMJMSKC DS     CL8                Jobname Mask (characters)
*
SSMMSK1         EQU  C'U'             represent U as 1 bit
SSMMSK0         EQU  C'*'             represent * as 0 bit
*
*
SSMCLSLM DS     100Y               Class limit table
SSMCLSEN        EQU  SSMCLSLM,2,C'Y'  Class limit entry
SSMCLSLN        EQU  *-SSMCLSLM       length of limit table
SSMCLSNO        EQU  (SSMCLSLN/L'SSMCLSEN)  # of entries
*
SSMTBLEN EQU    ((((*-SSMTB)+7)/8)*8)    actual length (in doublewds)
         MEND
./  ADD  NAME=$STTQE,LEVEL=00,SOURCE=0,LIST=ALL
         MACRO
&LABEL   $STTQE  &DSECT=YES
         AIF   ('&DSECT' NE 'YES').NDS1
STTQE    DSECT
.NDS1    ANOP
.* *******************************************************************
.*                                                                   *
.*   THE MODS ON THIS TAPE HAVE BEEN USED SUCCESSFULLY AND TO THE    *
.* BEST OF OUR KNOWLEDGE THEY ARE OPERATIONAL, HOWEVER NO WARRANTY IS*
.* MADE TO THE ACCURACY OF THE MODS AND NO RESPONSIBILITY IS ASSUMED *
.* FOR ANY MODIFICATION DIRECTLY OR INDIRECTLY CAUSED BY THE USE OF  *
.* THE MODIFICATIONS.  IT IS THE USERS RESPONSIBILITY TO EVALUATE THE*
.* USEFULLNESS OF THE MATERIAL.                                      *
.*                                                                   *
.*   WE DO NOT GUARANTEE TO KEEP ANY MATERIAL PROVIDED UP TO DATE,   *
.* NOR DO WE GUARANTEE TO PROVIDE ANY CORRECTIONS OR EXTENSIONS MADE *
.* IN THE FUTURE.                                                    *
.*                                                                   *
.* *******************************************************************
.**********************************************************************
.* THIS MACRO EXPANSION MAPS A STTQEX - TQE EXTENSION - ONE IS
.*  OBTAINED ( SOMEHOW ) WHEN NEEDED BY EXIT49A - WHEN A JOB IS
.*  REJECTED FOR A TIME RELATED REASON (HOLDFOR, GENERAL BEAFT/ ETC)
.*   LOOKS LIKE ABOUT 24 BYTES WITH AT JES2 1.7
.**********************************************************************
STTQFP   DS    F                   FORWARD POINTER
STTQBK   DS    F                   BACKWARD POINTER
STTQTQE  DS    XL16                LENGTH OF THE JES2 TQE +4 J.I.Case
STTQLEN  EQU   *-STTQFP            LENGTH OF THE ENTIRE CONTROL BLOCK
         MEND
./  ADD  NAME=$STTQEXW,LEVEL=00,SOURCE=0,LIST=ALL
         MACRO
&LABEL   $STTQEXW &DSECT=YES
         AIF   ('&DSECT' EQ 'NO').NDS1
STTQEXW  DSECT
.NDS1    ANOP
.* *******************************************************************
.*                                                                   *
.*   THE MODS ON THIS TAPE HAVE BEEN USED SUCCESSFULLY AND TO THE    *
.* BEST OF OUR KNOWLEDGE THEY ARE OPERATIONAL, HOWEVER NO WARRANTY IS*
.* MADE TO THE ACCURACY OF THE MODS AND NO RESPONSIBILITY IS ASSUMED *
.* FOR ANY MODIFICATION DIRECTLY OR INDIRECTLY CAUSED BY THE USE OF  *
.* THE MODIFICATIONS.  IT IS THE USERS RESPONSIBILITY TO EVALUATE THE*
.* USEFULLNESS OF THE MATERIAL.                                      *
.*                                                                   *
.*   WE DO NOT GUARANTEE TO KEEP ANY MATERIAL PROVIDED UP TO DATE,   *
.* NOR DO WE GUARANTEE TO PROVIDE ANY CORRECTIONS OR EXTENSIONS MADE *
.* IN THE FUTURE.                                                    *
.*                                                                   *
.* *******************************************************************
.**********************************************************************
.* THIS MACRO EXPANSION MAPS A WORK AREA EXTENSION FOR THE STTQEPCE
.*  THE TQE (TIMER QUEUE) PCE.  THERE IS ONE FOR EACH PCE OF THIS TYPE.
.**********************************************************************
STTXNTN  DS    CL16                NAME/TOKEN PAIR - NAME VALUE
STTXNTT  DS    CL16                NAME/TOKEN PAIR - TOKEN VALUE
STTXDBL1 DS    D                   DOUBLE WORD TO SAVE $STCK VALUE IN
STTXLSAT DS    F                   WORK AREA - LOWEST SATISFIED TIME
STTXLSPT DS    F                   POINTER TO STTQE WITH LOWEST TIME
STTXLEN  EQU   *-STTXNTN           LENGTH OF THE ENTIRE PCE WORK AREA
$DRTQEX  EQU   63                  DISPATCHER STTQE RESOURCE
TQEX     EQU   255                 ID OF PCE
         MEND
./  ADD  NAME=STPCSMFD,LEVEL=00,SOURCE=0,LIST=ALL
         MACRO
&LAB1    STPCSMFD &DSECT=N,&JES2=N
         AIF   ('&DSECT' NE 'Y').NDS1
STPCSMFD DSECT
.NDS1    ANOP
         AIF   ('&LAB1' EQ '').LBL2
&LAB1    EQU   *
.LBL2    ANOP
.*
.*
         AIF   ('&JES2' EQ 'N').NJS1
         SPACE 2
SMFXXJES DS    CL8                 PREFIX FOR JES2 CHAINING AREA
.NJS1    ANOP
.*
.* *******************************************************************
.*                                                                   *
.*   THE MODS ON THIS TAPE HAVE BEEN USED SUCCESSFULLY AND TO THE    *
.* BEST OF OUR KNOWLEDGE THEY ARE OPERATIONAL, HOWEVER NO WARRANTY IS*
.* MADE TO THE ACCURACY OF THE MODS AND NO RESPONSIBILITY IS ASSUMED *
.* FOR ANY MODIFICATION DIRECTLY OR INDIRECTLY CAUSED BY THE USE OF  *
.* THE MODIFICATIONS.  IT IS THE USERS RESPONSIBILITY TO EVALUATE THE*
.* USEFULLNESS OF THE MATERIAL.                                      *
.*                                                                   *
.*   WE DO NOT GUARANTEE TO KEEP ANY MATERIAL PROVIDED UP TO DATE,   *
.* NOR DO WE GUARANTEE TO PROVIDE ANY CORRECTIONS OR EXTENSIONS MADE *
.* IN THE FUTURE.                                                    *
.*                                                                   *
.* *******************************************************************
.*
** * ------------------------------------------------------------- * **
** * -- THIS GROUP OF DS'S IS INTEDED TO BE USED AS PART OF AN  -- * **
** * -- EXISTING DSECT, TO DESCRIBE AN SMF BUFFER FOR RECORD    -- * **
** * -- TYPE 216 (D8) TECH SUBTYPED SMF RECORDS.                -- * **
** * ------------------------------------------------------------- * **
SMFBASE  EQU   *                   USE THIS LABEL FOR USING - NON JES2
SMFXLEN  DS    XL2                LRECL INCLUDING RDW
SMFXSEG  DS    XL2                SEGMENT - ALWAYS ZEROS
SMFXFLG  DS    XL1                B'0101 1110' INDICATES SUBTYPES
SMFXRTY  DS    XL1                SMF RECORD TYPE = 216 = X'D8'
SMFXTME  DS    XL4                TIME SINCE MIDNIGHT IN 1/100TH SEC.
*  TOD, USING FORMAT FROM TIME MACRO WITH BIN. INTVL
SMFXDTE  DS    PL4                X'01YYDDDF'
*  DATE IN PACKED DECIMAL FORM: 01YYDDDF
SMFXSID  DS    XL4                SYSID FROM ( SID )
SMFXSSI  DS    XL4                SUBSYS ID  (SSID = TECH) OR BLANKS
SMFXSTY  DS    XL2                RECORD SUBTYPE X'01'-X'FF'
*                 ****  PROGRAM EXECUTION TRACKING SUBTYPES  ****
*                         X'01' = TECH PGM EXECUTION
*                         X'02' = TECH PGM EXECUTION DUP LIB.
*                         X'03' = TECH PGM EXECUTION DUP LIB.
*                         X'04' = TECH PGM EXECUTION DUP LIB.
*                         X'05' = TECH PGM EXECUTION DUP LIB.
*                 ****  S.S.M. = SHARED SPOOL MODS SUBTYPES  ****
*                         X'40' = SSM REJECTION INFORMATION
*                         X'41' = SSM JOB PASSED SELECTION
*              FUTURE     X'42' = SSM OPERATOR ACTIONS ($T CMDS)
*                         X'43' = $SJ - ALLOWED OR REJECTED
*                         X'44' = SSM JECL CARD ACCEPTED
*              FUTURE     X'45' = SSM JECL CARD REJCTED JCL ERROR
*                         X'46' = SSM JOB SELECTION REDRIVEN
*              FUTURE     X'47' = JES2 SSM PARM ACCEPTED
*              FUTURE     X'48' = RESERVED FOR SSM
*              FUTURE     X'49' = RESERVED FOR SSM
SMFXNUMT DS    XL2                NUMBER OF TRIPLETS (SUBTYPES 1-5= 2)
*
SMFXRESV DS    XL2                LENGTH OF SELF-DEFINING SECTION
*** SELF-DEFINING SECTION ***
* - FIRST TRIPLET - PRODUCT SECTION
OFFPRD01 DS    XL4                OFFSET FROM RDW TO PROD. SECTION
LENPRD01 DS    XL2                LENGTH OF PRODUCT SECTION
NUMPRD01 DS    XL2                NUMBER OF PRODUCT SECTIONS
* - SECOND TRIPLET - SUBTYPED DATA SECTION
OFFTEC01 DS    XL4                OFFSET FROM RDW TO SUBTYPED DATA
LENTEC01 DS    XL2                LENGTH OF SUBTYPED SECTION
NUMTEC01 DS    XL2                NUMBER OF SUBTYPED SECTIONS
*
SDSEND   EQU   *                  END OF SELF DEFINING SECTION
SMFD8SSD EQU   SDSEND-OFFPRD01    EQU'D LEN OF SELF DEFINING ssm SECT.
SMFD8TSD EQU   SDSEND-OFFPRD01    EQU'D LEN OF SELF DEFINING tech SECT.
*
* THE PRODUCT SECTION FOR USE WITH TECH SUBSYSTEM GOES HERE
*
PRDOFT   EQU   *-SMFXLEN          OFFSET TO TECH PRODUCT SECTION
SMFD8TTY DS    XL2                SUBTYPE - 01 FOR PGM EXECUTION
SMFD8TVN DS    CL2                VERSION # C'01'
SMFD8TNM DS    XL16               C'TECH SUBSYSTEM  '
PRDLENT  EQU   *-SMFD8TTY         LENGTH OF THE PRODUCT SECTION
*
* THE SUBTYPED TECH SUBSYSTEM DATA GOES IN HERE
*
STDOFT   EQU   *-SMFXLEN          OFFSET TO SUBTYPED DATA
SMFD8TTP DS    XL2                SUBTYPE - X'01-05' = PGM EXECUTION
SMFD8TCF DS    CL8                RACFID OF EXECUTOR -
SMFD8TPN DS    CL8                RACFID GROUP NAME  -
SMFD8TGM DS    CL8                PROGRAM NAME
SMFD8TID DS    CL8                RACF TERMINAL ID
SMFD8TOB DS    CL8                THE JOB NAME
SMFD8TSI DS    CL4                SYSTEM ID
SMFD8TTE DS    PL4                SMF STYLE DATE
SMFD8TME DS    XL4                SMF STYLE TIME
STDLENT  EQU   *-SMFD8TTP         LENGTH OF SUBTYPED TECH TYPE DATA
SMFXLJ01 EQU   *-SMFXLEN          LENGTH OF THE ENTIRE RECORD
*
* THE PRODUCT SECTION(S) FOR JES2 SSM GOES HERE
*
         ORG   SDSEND             ORG TO END OF SELF DEFINING SECITON
*
PRDOFS   EQU   *-SMFXLEN          OFFSET TO PRODUCT SECTION
SMFD8STY DS    XL2                SUBTYPE - REPEATED - JUST IN CASE
SMFD8SVR DS    XL4                SAME AS UBRVRM AND UJCXVRM
SMFD8SID DS    XL16               C'SHARED SPOOL MOD'
PRDLENS  EQU   *-SMFD8STY         LENGTH OF THE SSM PRODUCT SECTION
*
PRD8JZZ  EQU   *                  END OF SSM PRODUCT SECTION
*
* THE SUBTYPED SHARED SPOOL MODS DATA GOES IN HERE
*
STDOFFS  EQU   *-SMFXLEN          OFFSET TO SUBTYPED SSM DATA
*
SMFD8S40 DS    XL2                SUBTYPE - X'0040' SSM REJECTION INFO
SMFD80JI DS    XL4                JOBID
SMFD80JN DS    CL8                JOBNAME
SMFD80SI DS    XL4                NODE ID REJECT TOOK PLACE ON
SMFD80TE DS    XL8                NODE NAME REJECT TOOK PLACE ON
SMFD80GN DS    XL8                NODE NAME REJECT TOOK PLACE ON
SMFD80ME DS    CL12               REJECTION REASON
SMFD80XT DS    CL8                STCK FORMAT DATE AND TIME
SMFLNS40 EQU   *-SMFD8S40         LENGTH OF SUBTYPED DATA
SMFXLS40 EQU   *-SMFXLEN          LENGTH OF THE ENTIRE RECORD
*
         ORG   PRD8JZZ            ORG TO END OF SSM PRODUCT SECTION
*
SMFD8S41 DS    XL2                SUBTYPE - X'0041' SSM JOB SELECTED
SMFD81IN DS    XL2                INPUT NODE ID       JQEINPND
SMFD81XN DS    XL2                EXECUTION NODE ID   JQEXEQND
SMFD81CD DS    CL1                JQE CREATION TIME    - JQXCRTME
SMFD81JC DS    CL1                JOB CLASS   JQEJCLAS
SMFD81JI DS    XL4                JOBID
SMFD81JN DS    CL8                JOBNAME JQEJNAME
SMFD81RI DS    CL8                USERID OF JOB OWNER    - JQEUSRID
SMFD81SL DS    CL8                SECURITY LABEL OF JOB   - JQESECLB
SMFD81XT DS    CL8                STCK FORMAT DATE AND TIME - THIS REC
SMFD81SE DS    CL16               SCHEDULING ENVIRONMENT NAME -JQASCHE
SMFD81TE DS    XL8                NODE NAME ACCEPTED ON
SMFD81GN DS    CL8                XCF GROUP NAME ACCEPTED ON
SMFLNS41 EQU   *-SMFD8S41         LENGTH OF SUBTYPED DATA
SMFXLS41 EQU   *-SMFXLEN          LENGTH OF THE ENTIRE RECORD
*
         ORG   PRD8JZZ            ORG TO END OF SSM PRODUCT SECTION
*
SMFD8S42 DS    XL2                SUBTYPE - X'0042' SSM OPER CMDS
SMFD82XT DS    CL8                STCK FORMAT DATE AND TIME - THIS REC
* THE ONLY THINGS AN OPERATOR CAN CHANGE ARE IN THE ECSA AREA -
* HERE IS A BEFORE AND AFTER COPY OF THE ECSA AREA
SMFD82NN DS    XL8                NODE NAME ACCEPTED ON
SMFD82NX DS    XL8                NODE ID COMMAND ENTERED ON
SMFD82CM DS    CL140              THE COMMAND ITSELF (IF WE CAN GET IT)
SMFD82CB DS    XL(SSMTBLEN)       THE ECSA AREA ITSELF (BEFORE)
SMFD82CA DS    XL(SSMTBLEN)       THE ECSA AREA ITSELF (AFTER)
SMFLNS42 EQU   *-SMFD8S42         LENGTH OF SUBTYPED DATA
SMFXLS42 EQU   *-SMFXLEN          LENGTH OF THE ENTIRE RECORD
*
         ORG   PRD8JZZ            ORG TO END OF SSM PRODUCT SECTION
*
SMFD8S43 DS    XL2                SUBTYPE - X'0043' $SJ ALLOWED OR NOT
SMFD83DT DS    CL8                STCK FORMAT DATE AND TIME - THIS REC
SMFD83NN DS    CL1                A=$SJ IS ALLOWED ;;;; X=$SJ REJECTED
SMFLNS43 EQU   *-SMFD8S43         LENGTH OF SUBTYPED DATA
SMFXLS43 EQU   *-SMFXLEN          LENGTH OF THE ENTIRE RECORD
*
         ORG   PRD8JZZ            ORG TO END OF SSM PRODUCT SECTION
*
SMFD8S44 DS    XL2                SUBTYPE - X'0044' JECL CARD ACCPETED
SMFD84IN DS    XL2                INPUT NODE ID       JQEINPND
SMFD84CD DS    CL4                JQE CREATION TIME    - JCTRDRON
SMFD84JC DS    CL1                JOB CLASS   JQEJCLAS
SMFD84JI DS    XL8                JOBID
SMFD84JN DS    CL8                JOBNAME JQEJNAME
SMFD84XT DS    CL8                STCK FORMAT DATE AND TIME - THIS REC
SMFD84MG DS    CL60               DETAILED INFO FOR SMF
SMFD84JA DS    CL(UJCXSLN1)       THE STQNAME (JCT EXTENSION) AFTER
*                                     PROCESSING CURRENT STATEMENT
SMFLNS44 EQU   *-SMFD8S44         LENGTH OF SUBTYPED DATA
SMFXLS44 EQU   *-SMFXLEN          LENGTH OF THE ENTIRE RECORD
*
         ORG   PRD8JZZ            ORG TO END OF SSM PRODUCT SECTION
*
SMFD8S45 DS    XL2                SUBTYPE - X'0045' JECL CARD REJECTED
SMFD85IN DS    XL2                INPUT NODE ID       JQEINPND
SMFD85CD DS    CL4                JQE CREATION TIME    - JQXCRTME
SMFD85JC DS    CL1                JOB CLASS   JQEJCLAS
SMFD85JI DS    XL8                JOBID
SMFD85JN DS    CL8                JOBNAME JQEJNAME
SMFD85XT DS    CL8                STCK FORMAT DATE AND TIME - THIS REC
SMFD85MG DS    CL140              JECL CARD IMAGE PROCESSED
SMFLNS45 EQU   *-SMFD8S45         LENGTH OF SUBTYPED DATA
SMFXLS45 EQU   *-SMFXLEN          LENGTH OF THE ENTIRE RECORD
*
         ORG   PRD8JZZ            ORG TO END OF SSM PRODUCT SECTION
*
SMFD8S46 DS    XL2                SUBTYPE - X'0046' QSEL IS REDRIVEN
SMFD86IN DS    XL2                NODEID SOMEWHERE IN $HCT OR $HCCT
SMFD86XT DS    CL8                STCK FORMAT DATE AND TIME - THIS REC
SMFLNS46 EQU   *-SMFD8S46         LENGTH OF SUBTYPED DATA
SMFXLS46 EQU   *-SMFXLEN          LENGTH OF THE ENTIRE RECORD
*
         ORG   PRD8JZZ            ORG TO END OF SSM PRODUCT SECTION
*
SMFD8S47 DS    XL2                SUBTYPE - X'0047' SSM PARM ACCEPTED
SMFD87IN DS    XL2                NODEID SOMEWHERE IN $HCT OR $HCCT
SMFD87XT DS    CL8                STCK FORMAT DATE AND TIME - THIS REC
SMFD87PM DS    CL256              PARM VALUE ACCEPTED
SMFD87EC DS    XL(SSMTBLEN)       THE ECSA AREA ITSELF
SMFLNS47 EQU   *-SMFD8S47         LENGTH OF SUBTYPED DATA
SMFXLS47 EQU   *-SMFXLEN          LENGTH OF THE ENTIRE RECORD
*
         ORG   PRD8JZZ            ORG TO END OF SSM PRODUCT SECTION
*
SMFD8S48 DS    XL2                SUBTYPE - X'0047' SSM PARM ACCEPTED
SMFD8801 DS    XL1                SOME DATA TO RECORD(UNUSED FOR NOW)
SMFD8802 DS    XL1                  MORE DATA TO RECORD
SMFLNS48 EQU   *-SMFD8S48         LENGTH OF SUBTYPED DATA
SMFXLS48 EQU   *-SMFXLEN          LENGTH OF THE ENTIRE RECORD
*
         ORG   PRD8JZZ            ORG TO END OF SSM PRODUCT SECTION
*
SMFD8S49 DS    XL2                SUBTYPE - X'0047' SSM PARM ACCEPTED
SMFD8901 DS    XL1                SOME DATA TO RECORD(UNUSED FOR NOW)
SMFD8902 DS    XL1                  MORE DATA TO RECORD
SMFLNS49 EQU   *-SMFD8S49         LENGTH OF SUBTYPED DATA
SMFXLS49 EQU   *-SMFXLEN          LENGTH OF THE ENTIRE RECORD
*
         ORG   ,
         DS    0F
* USE SMFD8LEN TO FIND BUFFER SIZE FOR *ANY* VERSION OF SMFD8...
SMFD8LEN EQU   *-STPCSMFD
         MEND
./  ADD  NAME=$USERCBS,LEVEL=00,SOURCE=0,LIST=ALL
         MACRO - $USERCBS - EXPAND ALL USER CONTROL BLOCKS
&LABEL   $USERCBS
.*
.* *******************************************************************
.*                                                                   *
.*   THE MODS ON THIS TAPE HAVE BEEN USED SUCCESSFULLY AND TO THE    *
.* BEST OF OUR KNOWLEDGE THEY ARE OPERATIONAL, HOWEVER NO WARRANTY IS*
.* MADE TO THE ACCURACY OF THE MODS AND NO RESPONSIBILITY IS ASSUMED *
.* FOR ANY MODIFICATION DIRECTLY OR INDIRECTLY CAUSED BY THE USE OF  *
.* THE MODIFICATIONS.  IT IS THE USERS RESPONSIBILITY TO EVALUATE THE*
.* USEFULLNESS OF THE MATERIAL.                                      *
.*                                                                   *
.*   WE DO NOT GUARANTEE TO KEEP ANY MATERIAL PROVIDED UP TO DATE,   *
.* NOR DO WE GUARANTEE TO PROVIDE ANY CORRECTIONS OR EXTENSIONS MADE *
.* IN THE FUTURE.                                                    *
.*                                                                   *
.* *******************************************************************
.**********************************************************************
.*
.* FUNCTION: GENERATE ALL SUNTRUST MAPPING MACROS FOR JES2.
.*
.* NOTES:    THIS MACRO IS REQUIRED FOR ALL SUNTRUST JES2 EXIT MODULES
.*           ALL USER MAPPING MACROS SHOULD BE EXPANDED HERE.
.*
.* REVISION HISTORY:
.*
.*   DATE   NAME         DESCRIPTION OF CHANGE
.* -------- ------------ ----------------------------------------------
.*
.**********************************************************************
.*
.*       TITLE '$STJCTX -- USER EXTENSION TO THE JCT
         $STJCTX  ,
.*
.*       TITLE '$STQNAME -- USER EXTENSION TO THE JQE
         $STQNAME ,
.*
.*       TITLE 'STPCSMFD -- USER SMF DSECTS FOR SHARED SPOOL MODS'
         STPCSMFD JES2=Y,DSECT=Y
.*
.*       TITLE '$SSMTB -- - COMMON ECSA AREA FOR JES2 SSM CONTROLS'
         $SSMTB ,
.*
.*       TITLE '$STTQEXW -- THE TIMER QUEUE PCE WORK AREA '
         $STTQEXW
.*
.*       TITLE '$STTQE   -- THE SSM TIMER QUEUE CONTROL BLOCK'
         $STTQE
.*
         MEND
./  ADD  NAME=#REGEQU,LEVEL=00,SOURCE=0,LIST=ALL
#REGEQU  MACRO
.* * ------------------------------------------------------------- * **
.* * -- GENERATE REGISTER EQUATES                               -- * **
.* * ------------------------------------------------------------- * **
&LABEL   #REGEQU &CONTROL=NO,&FP=NO,&GP=YES
         MNOTE *,' REG. EQUATES GENERATED WITH THE FOLLOWING CONTROLS'
         MNOTE *,' GENRAL PURPOSE = &GP'
         MNOTE *,' CONTROLS REGS  = &CONTROL'
         MNOTE *,' FLOATING POINT = &FP'
         PUSH  PRINT
         PRINT NOGEN
         LCLA  &RNUM
&RNUM    SETA  0
.LOOP15  ANOP
         AIF   (&RNUM GT 15).ENDR
         AIF   ('&GP' EQ 'NO').NOGPR
R&RNUM   EQU   &RNUM                   GPR REGISTER EQUATE
.NOGPR   ANOP
         AIF   ('&CONTROL' EQ 'NO').NOCRS
C&RNUM   EQU   &RNUM                 CONTROL REGISTER EQUATE
.NOCRS   ANOP
         AIF   ('&FP' EQ 'NO').NOFPRS
F&RNUM   EQU   &RNUM                  FLOATING POINT REGISTER
.NOFPRS  ANOP
&RNUM    SETA  &RNUM+1
         AGO   .LOOP15
.ENDR    POP   PRINT
         MEND
./   ENDUP
