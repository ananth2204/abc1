         TITLE 'IFMTNIB - VERIFY NO EMBEDED BLANKS IN INPUT FIELD'
* ******************************************************************* *
*                                                                     *
*    IIII   FFFFFFF M     M TTTTTTTT NN   NN  IIII   BBBBBB           *
*     II    FF      MM   MM    TT    NNN  NN   II    BB   BB          *
*     II    FF      MMM MMM    TT    NNN  NN   II    BB   BB          *
*     II    FFFFF   MMMMMMM    TT    NNNN NN   II    BBBBBB           *
*     II    FF      MM M MM    TT    NN NNNN   II    BB   BB          *
*     II    FF      MM   MM    TT    NN  NNN   II    BB   BB          *
*     II    FF      MM   MM    TT    NN  NNN   II    BB   BB          *
*    IIII   FF      MM   MM    TT    NN   NN  IIII   BBBBBB           *
*                                                                     *
* ******************************************************************* *
* THIS SUBROUTINE SCANS AN INPUT FIELD (DISPLAY FORMAT) TO VERIFY     *
* THAT THERE ARE NO EMBEDDED BLANKS. THERE CAN BE LEADING BLANKS AND  *
* TRAILING BLANKS, BUT NO BLANKS MAY OCCUR AFTER THE FIRST NON-BLANK  *
* CHARACTER IS ENCOUNTERED.                                           *
*                                                                     *
* NONE OF THE INITIALIZED VARIABLES ARE CHANGED DURING THE ROUTINE'S  *
* EXECUTION AND THERE ARE NO INTERNAL DATA FIELDS. ALL MANIPULATION   *
* IS DONE THROUGH REGISTERS. THE ROUTINE IS COMPLETELY REENTRANT.     *
*                                                                     *
* TWO FIELDS ARE PASSED AS PARAMETERS TO THE ROUTINE:                 *
*                                                                     *
*   1. A FIELD OF VARYING LENGTH CONTAINING THE INPUT DATA, AND       *
*   2. A 2 BYTE FIELD (IN BINARY FORMAT) CONTAINING THE LENGTH OF     *
*      THE FIRST FIELD PASSED.                                        *
*                                                                     *
* SUGGESTED CALLING SYNTAX FOR ALC CALLERS:                           *
*                                                                     *
*              CALL IFMTNIB,(INFIELD,INLENGTH)                        *
*                 . . .                                               *
*     INFIELD  DS   CL12'   128.25   '                                *
*     INLENGTH DS   H'12'                                             *
*                                                                     *
* SUGGESTED CALLING SYNTAX FOR COBOL CALLERS:                         *
*                                                                     *
*     01  INPUT-FIELD           PIC X(12) VALUE '   128.25   '.       *
*     01  INPUT-FIELD-LENGTH    PIC S9(4) COMP VALUE 12.              *
*                                                                     *
*     CALL 'IFMTNIB' USING INPUT FIELD, INPUT-FIELD-LENGTH.           *
*                                                                     *
* POSSIBLE RETURN CODE VALUES:                                        *
*     0 - DATA IN FIELD CONTAINS NO IMBEDDED BLANKS                   *
*     8 - FIELD CONTAINS IMBEDDED BLANKS                              *
* ******************************************************************* *
* ******************************************************************* *
         EJECT
         PRINT NOGEN
IFMTNIB  CSECT
         EYEC  'VERIFY NO EMBEDDED BLANKS IN INPUT FIELD'
BEGIN    SAVE  (14,12)                 SAVE CALLER'S REGISTERS
*
         BALR  BASE,0                  ESTABLISH BASE REGISTER
         USING *,BASE                  ASSUME BASE REGISTER
*
         LM    INREG,LGTHREG,0(R1)     LOAD ADDRESSES OF PARAMETERS
*
         SR    R15,R15                 ASSUME RETURN CODE OF ZERO
*
         LH    LGTHREG,0(,LGTHREG)     GET LENGTH OF INPUT FIELD
         LTR   LGTHREG,LGTHREG         IS LENGTH OF FIELD ZERO?
         BZ    RETURN                     IF YES, RETURN TO CALLER
*
*    FIND FIRST NON-BLANK CHARACTER IN FIELD
*
         LR    R7,INREG                BEGINNING OF FIELD
         LR    R8,LGTHREG              LENGTH OF FIELD
         BCTR  R8,0                    DECREASE BY 1 FOR EXECUTE
         EX    R8,FINDNB               FIND FIRST NON-BLANK
         BZ    RETURN                     IF ALL BLANKS, RETURN
*
*    FIND NEXT BLANK AFTER FIRST NON-BLANK
*
         LR    R7,R1                   FIRST NON-BLANK CHARACTER
         LR    R8,INREG                BEGINNING OF FIELD ADDRESS
         AR    R8,LGTHREG              ADD LENGTH OF FIELD
         SR    R8,R7                   SUBTRACT NON-BLANK CHAR ADDRESS
         BCTR  R8,0                    DECREASE BY 1 FOR EXECUTE
         EX    R8,FINDBL               FIND NEXT BLANK
         BZ    RETURN                     IF NO OTHER BLANKS, RETURN
*
*    MAKE SURE NO OTHER NON-BLANKS FOLLOW
*
         LR    R7,R1                   TRAILING BLANK CHARACTER
         LR    R8,INREG                BEGINNING OF FIELD ADDRESS
         AR    R8,LGTHREG              ADD LENGTH OF FIELD
         SR    R8,R7                   SUBTRACT TRAILING BLANK ADDRESS
         BCTR  R8,0                    DECREASE BY 1 FOR EXECUTE
         EX    R8,FINDNB               LOOK FOR NEXT NON-BLANK
         BZ    RETURN                     IF NONE FOUND, RETURN
*
*    RETURN CODE OF "8" INDICATES EMBEDDED BLANKS DETECTED
*
ERROR    LA    R15,8                    RETURN CODE OF 8
*
*    RETURN TO CALLER WITH RETURN CODE IN REGISTER 15
*
RETURN   RETURN (14,12),RC=(15)
*
*    TRT PERFORMED BY EXECUTE FROM ABOVE TO FIND NON-BLANKS
*
FINDNB   TRT   0(0,R7),TABLENB          SCAN FOR NON-BLANK CHARACTERS
*
*    TRT PERFORMED BY EXECUTE FROM ABOVE TO FIND BLANKS
*
FINDBL   TRT   0(0,R7),TABLEBL          SCAN FOR BLANK CHARACTER
         SPACE
* ******************************************************************* *
* *************************** CONSTANTS ***************************** *
* ******************************************************************* *
TABLENB  DC    64X'01'                 TABLE TO FIND NON-BLANKS
         DC    X'00'
         DC    191X'01'
TABLEBL  DC    64X'00'                 TABLE TO FIND BLANKS
         DC    X'01'
         DC    191X'00'
         LTORG
         EJECT
* ******************************************************************* *
* ***************************** EQUATES ***************************** *
* ******************************************************************* *
R0       EQU   0                       REGISTER 0
R1       EQU   1                       REGISTER 1
R2       EQU   2                       REGISTER 2
R3       EQU   3                       REGISTER 3
R4       EQU   4                       REGISTER 4
INREG    EQU   5                       INPUT FIELD
LGTHREG  EQU   6                       INPUT FIELD LENGTH
R7       EQU   7                       REGISTER 7
R8       EQU   8                       REGISTER 8
R9       EQU   9                       REGISTER 9
R10      EQU   10                      REGISTER 10
R11      EQU   11                      REGISTER 11
BASE     EQU   12                      BASE REGISTER
R13      EQU   13                      REGISTER 13
R14      EQU   14                      REGISTER 14
R15      EQU   15                      REGISTER 15
         END
