000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. FMTIVP.
000300 AUTHOR. JAY MOSELEY.
000400 DATE-WRITTEN. JULY, 2000.
000500 DATE-COMPILED.
000600
000700* ************************************************************* *
000800* THE PURPOSE OF THIS PROGRAM IS TO VERIFY THE FUNCTIONALITY    *
000900* OF THE 370 ASSEMBLER FIELD INPUT/OUTPUT FORMATTING ROUTINES   *
001000* AFTER THEIR INSTALLATION UNDER HERCULES/MVT.                  *
001100* ************************************************************* *
001200/
001300 ENVIRONMENT DIVISION.
001400 CONFIGURATION SECTION.
001500 SPECIAL-NAMES.
001600     C01 IS TO-FIRST-LINE.
001700 INPUT-OUTPUT SECTION.
001800 FILE-CONTROL.
001900     SELECT DATA-FILE
002000         ASSIGN TO UT-S-TESTDATA.
002100
002200     SELECT PRINT-FILE
002300         ASSIGN TO UR-S-REPORT.
002400 DATA DIVISION.
002500
002600 FILE SECTION.
002700 FD  DATA-FILE
002800     LABEL RECORDS ARE STANDARD
002850     BLOCK CONTAINS 0 RECORDS
002900     RECORD CONTAINS 80 CHARACTERS
003000     DATA RECORD IS DATA-RECD.
003100 01  DATA-RECORD                 PIC X(80).
003200
003300 FD  PRINT-FILE
003400     LABEL RECORDS ARE OMITTED
003500     RECORD CONTAINS 133 CHARACTERS
003600     DATA RECORD IS PRINT-RECD.
003700 01  PRINT-RECORD               PIC X(133).
003800/
003900 WORKING-STORAGE SECTION.
004000
004100* ************************************************************* *
004200* THESE ARE PROGRAM CONTROL SWITCHES AND MISCELLANEOUS FIELDS.  *
004300* ************************************************************* *
004400 01  PROGRAM-CONTROL-FIELDS.
004500     02  END-OF-DATA-SWITCH      PIC X(1) VALUE 'N'.
004600         88  END-OF-DATA         VALUE 'Y'.
004700     02  SYSTEM-TIME             PIC 9(8).
004800     02  FILLER                  REDEFINES SYSTEM-TIME.
004900         03  SYSTEM-TIME-SHORT   PIC 9(4).
005000         03  FILLER              PIC 9(4).
005100     02  PAGE-COUNT              PIC 999 VALUE ZERO.
005200     02  LINE-COUNT              PIC 99  VALUE 99.
005300     02  PAGE-SIZE               PIC 99  VALUE 42.
005400
005500     02  UNFORMATTED-GDATE       PIC X(8).
005600     02  FILLER                  REDEFINES UNFORMATTED-GDATE.
005700         03  UNFORMATTED-GMONTH  PIC 9(2).
005800         03  UNFORMATTED-GDAY    PIC 9(2).
005900         03  UNFORMATTED-GYEAR   PIC 9(4).
006000
006100     02  FORMATTED-GDATE         PIC X(10).
006200     02  FILLER                  REDEFINES FORMATTED-GDATE.
006300         03  FORMATTED-GMONTH    PIC Z9.
006400         03  FILLER              PIC X(1).
006500         03  FORMATTED-GDAY      PIC 9(2).
006600         03  FILLER              PIC X(1).
006700         03  FORMATTED-GYEAR     PIC 9(4).
006800
006900* ************************************************************* *
007000* FOLLOWING ARE SPECIFIC RECORD DESCRIPTIONS USED TO REDEFINE   *
007100* THE TEST DATA INPUT RECORD FOR EACH OF THE 12 ROUTINES.       *
007200* ************************************************************* *
007300 01  TEST-RECORD.
007400     02  TR-IDENTIFICATION       PIC X(4).
007500     02  FILLER                  PIC X(1).
007600     02  TR-DATA                 PIC X(75).
007700
007800     02  FILLER                  REDEFINES TR-DATA.
007900         03  TR-FILL-DATA        PIC X(12).
008000         03  FILLER              PIC X(1).
008100         03  TR-FILL-SIZE        PIC 9(2).
008200         03  FILLER              PIC X(60).
008300
008400     02  FILLER                  REDEFINES TR-DATA.
008500         03  TR-FLPT-DATA        PIC X(12).
008600         03  FILLER              PIC X(1).
008700         03  TR-FLPT-SIZE        PIC 9(2).
008800         03  FILLER              PIC X(60).
008900
009000     02  FILLER                  REDEFINES TR-DATA.
009100         03  TR-IHEX-DATA        PIC X(12).
009200         03  FILLER              PIC X(1).
009300         03  TR-IHEX-SIZE        PIC 9(2).
009400         03  FILLER              PIC X(60).
009500
009600     02  FILLER                  REDEFINES TR-DATA.
009700         03  TR-LJST-DATA        PIC X(12).
009800         03  FILLER              PIC X(1).
009900         03  TR-LJST-SIZE        PIC 9(2).
010000         03  FILLER              PIC X(60).
010100
010200     02  FILLER                  REDEFINES TR-DATA.
010300         03  TR-MASK-DATA        PIC X(16).
010400         03  FILLER              PIC X(1).
010500         03  TR-MASK-SIZE        PIC 9(2).
010600         03  FILLER              PIC X(1).
010700         03  TR-MASK-MASK        PIC X(16).
010800         03  FILLER              PIC X(39).
010900
011000     02  FILLER                  REDEFINES TR-DATA.
011100         03  TR-NIB-DATA         PIC X(12).
011200         03  FILLER              PIC X(1).
011300         03  TR-NIB-SIZE         PIC 9(2).
011400         03  FILLER              PIC X(60).
011500
011600     02  FILLER                  REDEFINES TR-DATA.
011700         03  TR-OHEX-DATA        PIC X(12).
011800         03  FILLER              PIC X(1).
011900         03  TR-OHEX-SIZE        PIC 9(2).
012000         03  FILLER              PIC X(60).
012100
012200     02  FILLER                  REDEFINES TR-DATA.
012300         03  TR-MONY-DATA        PIC S9(11)V9(2).
012400         03  FILLER              PIC X(62).
012500
012600* ************************************************************* *
012700* FOLLOWING ARE INTERMEDIATE FIELDS USING TO PREPARE AND        *
012800* INTERPRET DATA PASSED TO/FROM THE ROUTINES                    *
012900* ************************************************************* *
013000 01  INTER-PROGRAM-DATA.
013100     02  FIELD-SIZE              PIC S9(4)   COMP.
013200
013300     02  FLPT-OUTPUT             PIC S9(11)V9(7).
013400
013500     02  OUTPUT-FIELD            PIC X(160).
013600     02  FILLER                  REDEFINES OUTPUT-FIELD.
013700         03  OUTPUT-NUMEDIT      PIC 9(11)V9(7)-.
013800         03  FILLER              PIC X(141).
013900     02  FILLER                  REDEFINES OUTPUT-FIELD.
014000         03  OUTPUT-CHUNK1       PIC X(55).
014100         03  OUTPUT-CHUNK2       PIC X(55).
014200         03  OUTPUT-CHUNK3       PIC X(50).
014300
014400* ************************************************************* *
014500* FOLLOWING ARE THE REPORT HEADING AND DETAIL DESCRIPTIONS.     *
014600* ************************************************************* *
014700
014800 01  HEADING-1.
014900     02  FILLER  PIC X(1) VALUE ' '.
015000     02  FILLER  PIC X(6) VALUE 'DATE: '.
015100     02  H1-DATE PIC X(8).
015200     02  FILLER  PIC X(32) VALUE SPACES.
015300     02  FILLER  PIC X(36) VALUE
015400         'INPUT/OUTPUT FIELD EDIT SUBROUTINES '.
015500     02  FILLER  PIC X(35) VALUE SPACES.
015600     02  FILLER  PIC X(15) VALUE 'PROGRAM: FMTIVP'.
015700
015800 01  HEADING-2.
015900     02  FILLER  PIC X(1) VALUE ' '.
016000     02  FILLER  PIC X(6) VALUE 'TIME: '.
016100     02  H2-TIME PIC X(5).
016200     02  FILLER  PIC X(36) VALUE SPACES.
016300     02  FILLER  PIC X(33) VALUE
016400         'INSTALLATION VERIFICATION PROGRAM'.
016500     02  FILLER  PIC X(40) VALUE SPACES.
016600     02  FILLER  PIC X(9) VALUE 'PAGE:   '.
016700     02  H2-PAGE PIC ZZ9.
016800
016900 01  HEADING-3.
017000     02  FILLER  PIC X(1) VALUE ' '.
017100     02  FILLER  PIC X(22) VALUE
017200         'TEST DATA INPUT RECORD'.
017300     02  FILLER  PIC X(39) VALUE SPACES.
017400     02  FILLER  PIC X(7) VALUE 'ROUTINE'.
017500     02  FILLER  PIC X(2) VALUE SPACES.
017600     02  FILLER  PIC X(3) VALUE 'R/C'.
017700     02  FILLER  PIC X(59) VALUE SPACES.
017800
017900 01  DETAIL-LINE.
018000     02  FILLER  PIC X(1) VALUE ' '.
018100     02  DL-TEST-RECORD          PIC X(50).
018200     02  FILLER                  PIC X(11) VALUE SPACES.
018300     02  DL-ROUTINE-NAME         PIC X(7).
018400     02  FILLER                  PIC X(2) VALUE SPACES.
018500     02  DL-SLASH-1              PIC X.
018600     02  DL-RETURN-CODE          PIC 9.
018610     02  DL-RETURN-CODE-X        REDEFINES DL-RETURN-CODE
018620                                 PIC X.
018700     02  DL-SLASH-2              PIC X.
018800     02  FILLER                  PIC X(2) VALUE SPACES.
018900     02  DL-COMMON-FORMAT        PIC X(57).
019000
019100 01  DETAIL-FLPT.
019200     02  DCONV-DATEG             PIC X(10).
019300     02  FILLER                  PIC X(2) VALUE SPACES.
019400     02  DCONV-DATEJ             PIC X(8).
019500     02  FILLER                  PIC X(2) VALUE SPACES.
019600     02  DCONV-ANUM              PIC 9,999,999.
019700/
019800 PROCEDURE DIVISION.
019900
020000 0000-MAIN SECTION.
020100
020200 0050-SETUP.
020300     MOVE TIME-OF-DAY TO UNFORMATTED-GDATE.
020400     MOVE '  :  ' TO FORMATTED-GDATE.
020500     MOVE UNFORMATTED-GMONTH TO FORMATTED-GMONTH.
020600     MOVE UNFORMATTED-GDAY   TO FORMATTED-GDAY.
020700     MOVE FORMATTED-GDATE TO H2-TIME.
020800     MOVE CURRENT-DATE TO H1-DATE.
020900
021000 0100-CONTROL.
021100     OPEN INPUT DATA-FILE,
021200          OUTPUT PRINT-FILE.
021300
021400     PERFORM 0200-READ-DATA-FILE
021500        THRU 0299-READ-DATA-FILE-EXIT.
021600
021700     PERFORM 0300-PROCESS-DATA
021800        THRU 0399-PROCESS-DATA-EXIT
021900       UNTIL END-OF-DATA.
022000
022100     CLOSE DATA-FILE, PRINT-FILE.
022200
022300     STOP RUN.
022400* - - - - - - - - - - - - - - - - PROGRAM EXIT POINT
022500
022600 0200-READ-DATA-FILE.
022700     READ DATA-FILE
022800         AT END MOVE 'Y' TO END-OF-DATA-SWITCH.
022900
023000 0299-READ-DATA-FILE-EXIT.
023100     EXIT.
023200* - - - - - - - - - - - - - - - - PERFORM EXIT POINT
023300
023400 0300-PROCESS-DATA.
023500     MOVE SPACES TO DETAIL-LINE.
023600     MOVE DATA-RECORD TO TEST-RECORD, DL-TEST-RECORD.
023700
023800     IF TR-IDENTIFICATION EQUAL 'FILL'
023900         PERFORM 0400-CALL-FILL THRU 0400-CALL-FILL-EXIT
024000         GO TO 0300-CONTINUE.
024100
024200     IF TR-IDENTIFICATION EQUAL 'FLPT'
024300         PERFORM 0410-CALL-FLPT THRU 0410-CALL-FLPT-EXIT
024400         GO TO 0300-CONTINUE.
024500
024600     IF TR-IDENTIFICATION EQUAL 'IHEX'
024700         PERFORM 0415-CALL-IHEX THRU 0415-CALL-IHEX-EXIT
024800         GO TO 0300-CONTINUE.
024900
025000     IF TR-IDENTIFICATION EQUAL 'LJST'
025100         PERFORM 0420-CALL-LJST THRU 0420-CALL-LJST-EXIT
025200         GO TO 0300-CONTINUE.
025300
025400     IF TR-IDENTIFICATION EQUAL 'MASK'
025500         PERFORM 0425-CALL-MASK THRU 0425-CALL-MASK-EXIT
025600         GO TO 0300-CONTINUE.
025700
025800     IF TR-IDENTIFICATION EQUAL 'NIB '
025900         PERFORM 0430-CALL-NIB THRU 0430-CALL-NIB-EXIT
026000         GO TO 0300-CONTINUE.
026100
026200     IF TR-IDENTIFICATION EQUAL 'OHEX'
026300         PERFORM 0435-CALL-OHEX THRU 0435-CALL-OHEX-EXIT
026400         GO TO 0300-CONTINUE.
026500
026600     IF TR-IDENTIFICATION EQUAL 'MONY'
026700         PERFORM 0440-CALL-MONY THRU 0440-CALL-MONY-EXIT
026800         GO TO 0300-CONTINUE.
026900
027000     MOVE '-------' TO DL-ROUTINE-NAME
027100     MOVE '*' TO DL-SLASH-1, DL-SLASH-2, DL-RETURN-CODE-X
027200     MOVE 'INVALID ROUTINE SPECIFIED' TO DL-COMMON-FORMAT.
027300
027400  0300-CONTINUE.
027500     MOVE +0 TO RETURN-CODE.
027600
027700     IF DETAIL-LINE NOT EQUAL SPACES
027800         PERFORM 0500-PRINT-ROUTINE THRU 0500-EXIT.
027900
028000     PERFORM 0200-READ-DATA-FILE
028100        THRU 0299-READ-DATA-FILE-EXIT.
028200
028300 0399-PROCESS-DATA-EXIT.
028400     EXIT.
028500* - - - - - - - - - - - - - - - - PERFORM EXIT POINT
028600
028700 0400-CALL-FILL.
028800     MOVE 'I-FILL ' TO DL-ROUTINE-NAME.
028900     MOVE TR-FILL-SIZE TO FIELD-SIZE.
029000
029100     CALL 'IFMTFILL' USING TR-FILL-DATA, FIELD-SIZE.
029200
029300     MOVE RETURN-CODE TO DL-RETURN-CODE.
029400
029500     IF RETURN-CODE EQUAL 0
029600         GO TO 0400-CALL-FILL-EXIT.
029700
029800     MOVE '*' TO DL-SLASH-1, DL-SLASH-2.
029900     IF RETURN-CODE EQUAL 8
030000         MOVE 'FIELD WAS NOT FILLED'
030100           TO DL-COMMON-FORMAT
030200         GO TO 0400-CALL-FILL-EXIT.
030300
030400 0400-CALL-FILL-EXIT.
030500     EXIT.
030600* - - - - - - - - - - - - - - - - PERFORM EXIT POINT
030700
030800 0410-CALL-FLPT.
030900     MOVE 'I-FLPT ' TO DL-ROUTINE-NAME.
031000     MOVE TR-FLPT-SIZE TO FIELD-SIZE.
031100
031200     CALL 'IFMTFLPT' USING TR-FLPT-DATA, FIELD-SIZE,
031300                           FLPT-OUTPUT.
031400
031500     MOVE RETURN-CODE TO DL-RETURN-CODE.
031600
031700     IF RETURN-CODE EQUAL 0
031800         MOVE SPACES TO OUTPUT-FIELD
031900         MOVE FLPT-OUTPUT TO OUTPUT-NUMEDIT
032000         MOVE OUTPUT-FIELD TO DL-COMMON-FORMAT
032100         GO TO 0410-CALL-FLPT-EXIT.
032200
032300     MOVE '*' TO DL-SLASH-1, DL-SLASH-2.
032400     IF RETURN-CODE EQUAL 8
032500         MOVE 'INVALID FLOATING POINT FORMAT'
032600           TO DL-COMMON-FORMAT
032700         GO TO 0410-CALL-FLPT-EXIT.
032800
032900 0410-CALL-FLPT-EXIT.
033000     EXIT.
033100* - - - - - - - - - - - - - - - - PERFORM EXIT POINT
033200
033300 0415-CALL-IHEX.
033400     MOVE 'I-HEX  ' TO DL-ROUTINE-NAME.
033500     MOVE TR-IHEX-SIZE TO FIELD-SIZE.
033600     MOVE SPACES TO OUTPUT-FIELD.
033700
033800     CALL 'IFMTHEX' USING TR-IHEX-DATA, FIELD-SIZE,
033900                          OUTPUT-FIELD.
034000
034100     MOVE RETURN-CODE TO DL-RETURN-CODE.
034200
034300     IF RETURN-CODE EQUAL 0
034400         MOVE OUTPUT-FIELD TO DL-COMMON-FORMAT
034500         GO TO 0415-CALL-IHEX-EXIT.
034600
034700     MOVE '*' TO DL-SLASH-1, DL-SLASH-2.
034800     IF RETURN-CODE EQUAL 8
034900         MOVE 'INVALID HEX DIGIT IN INPUT'
035000           TO DL-COMMON-FORMAT
035100         GO TO 0415-CALL-IHEX-EXIT.
035200
035300 0415-CALL-IHEX-EXIT.
035400     EXIT.
035500* - - - - - - - - - - - - - - - - PERFORM EXIT POINT
035600
035700 0420-CALL-LJST.
035800     MOVE 'I-LJST ' TO DL-ROUTINE-NAME.
035900     MOVE TR-LJST-SIZE TO FIELD-SIZE.
036000     MOVE SPACES TO OUTPUT-FIELD.
036100
036200     CALL 'IFMTLJST' USING TR-LJST-DATA, FIELD-SIZE.
036400
036500     MOVE RETURN-CODE TO DL-RETURN-CODE.
036600
036700     IF RETURN-CODE EQUAL 0
036800         MOVE TR-LJST-DATA TO DL-COMMON-FORMAT
036900         GO TO 0420-CALL-LJST-EXIT.
037000
037100 0420-CALL-LJST-EXIT.
037200     EXIT.
037300* - - - - - - - - - - - - - - - - PERFORM EXIT POINT
037400
037500 0425-CALL-MASK.
037600     MOVE 'I-MASK ' TO DL-ROUTINE-NAME.
037700     MOVE TR-MASK-SIZE TO FIELD-SIZE.
037800     MOVE SPACES TO OUTPUT-FIELD.
037900
038000     CALL 'IFMTMASK' USING TR-MASK-DATA, FIELD-SIZE,
038100                           TR-MASK-MASK, OUTPUT-FIELD.
038200
038300     MOVE RETURN-CODE TO DL-RETURN-CODE.
038400
038500     IF RETURN-CODE EQUAL 0
038600         MOVE OUTPUT-FIELD TO DL-COMMON-FORMAT
038700         GO TO 0425-CALL-MASK-EXIT.
038800
038900     MOVE '*' TO DL-SLASH-1, DL-SLASH-2.
039000     IF RETURN-CODE EQUAL 8
039100         MOVE 'INPUT DOES NOT CONFORM TO MASK'
039200           TO DL-COMMON-FORMAT
039300         GO TO 0425-CALL-MASK-EXIT.
039400
039500 0425-CALL-MASK-EXIT.
039600     EXIT.
039700* - - - - - - - - - - - - - - - - PERFORM EXIT POINT
039800
039900 0430-CALL-NIB.
040000     MOVE 'I-NIB  ' TO DL-ROUTINE-NAME.
040100     MOVE TR-NIB-SIZE TO FIELD-SIZE.
040200
040300     CALL 'IFMTNIB' USING TR-NIB-DATA, FIELD-SIZE.
040400
040500     MOVE RETURN-CODE TO DL-RETURN-CODE.
040600
040700     IF RETURN-CODE EQUAL 0
040800         GO TO 0430-CALL-NIB-EXIT.
040900
041000     MOVE '*' TO DL-SLASH-1, DL-SLASH-2.
041100     IF RETURN-CODE EQUAL 8
041200         MOVE 'FIELD CONTAINS EMBEDDED BLANKS'
041300           TO DL-COMMON-FORMAT
041400         GO TO 0430-CALL-NIB-EXIT.
041500
041600 0430-CALL-NIB-EXIT.
041700     EXIT.
041800* - - - - - - - - - - - - - - - - PERFORM EXIT POINT
041900
042000 0435-CALL-OHEX.
042100     MOVE 'O-HEX  ' TO DL-ROUTINE-NAME.
042200     MOVE TR-OHEX-SIZE TO FIELD-SIZE.
042300     MOVE SPACES TO OUTPUT-FIELD.
042400
042500     CALL 'OFMTHEX' USING TR-OHEX-DATA, FIELD-SIZE,
042600                          OUTPUT-FIELD.
042700
042800     MOVE RETURN-CODE TO DL-RETURN-CODE.
042900
043000     IF RETURN-CODE EQUAL 0
043100         MOVE OUTPUT-FIELD TO DL-COMMON-FORMAT
043200         GO TO 0435-CALL-OHEX-EXIT.
043300
043400 0435-CALL-OHEX-EXIT.
043500     EXIT.
043600* - - - - - - - - - - - - - - - - PERFORM EXIT POINT
043700
043800 0440-CALL-MONY.
043900     MOVE 'O-MONY ' TO DL-ROUTINE-NAME.
044000     MOVE SPACES TO OUTPUT-FIELD.
044100
044200     CALL 'OFMTMONY' USING TR-MONY-DATA, OUTPUT-FIELD.
044300
044400     MOVE RETURN-CODE TO DL-RETURN-CODE.
044500
044600     IF RETURN-CODE EQUAL 0
044700         GO TO 0440-CALL-MONY-OK.
044800
044900     MOVE '*' TO DL-SLASH-1, DL-SLASH-2.
045000     IF RETURN-CODE EQUAL 4
045100         MOVE 'NEGATIVE INPUT VALUE'
045200           TO DL-COMMON-FORMAT
045300         GO TO 0440-CALL-MONY-EXIT.
045400
045500     IF RETURN-CODE EQUAL 8
045600         MOVE 'NON-NUMERIC CHARACTER IN INPUT'
045700           TO DL-COMMON-FORMAT
045800         GO TO 0440-CALL-MONY-EXIT.
045900
046000 0440-CALL-MONY-OK.
046100     MOVE OUTPUT-CHUNK1 TO DL-COMMON-FORMAT.
046200     PERFORM 0500-PRINT-ROUTINE THRU 0500-EXIT.
046300     MOVE SPACES TO DETAIL-LINE.
046400
046500     IF OUTPUT-CHUNK2 EQUAL SPACES
046600         GO TO 0440-CALL-MONY-EXIT.
046700
046800     MOVE OUTPUT-CHUNK2 TO DL-COMMON-FORMAT.
046900     PERFORM 0500-PRINT-ROUTINE THRU 0500-EXIT.
047000     MOVE SPACES TO DETAIL-LINE.
047100
047200     IF OUTPUT-CHUNK3 EQUAL SPACES
047300         GO TO 0440-CALL-MONY-EXIT.
047400
047500     MOVE OUTPUT-CHUNK3 TO DL-COMMON-FORMAT.
047600     PERFORM 0500-PRINT-ROUTINE THRU 0500-EXIT.
047700     MOVE SPACES TO DETAIL-LINE.
047800
047900 0440-CALL-MONY-EXIT.
048000     EXIT.
048100* - - - - - - - - - - - - - - - - PERFORM EXIT POINT
048200
048300 0500-PRINT-ROUTINE.
048400     IF LINE-COUNT LESS THAN PAGE-SIZE
048500         GO TO 0500-DETAIL.
048600
048700     IF LINE-COUNT LESS THAN 99
048800         GO TO 0500-PAGEHEAD.
048900
049000     MOVE SPACES TO PRINT-RECORD.
049100     WRITE PRINT-RECORD AFTER ADVANCING TO-FIRST-LINE.
049200
049300  0500-PAGEHEAD.
049400     ADD 1 TO PAGE-COUNT
049500     MOVE PAGE-COUNT TO H2-PAGE
049600     WRITE PRINT-RECORD FROM HEADING-1 BEFORE ADVANCING 1
049700     WRITE PRINT-RECORD FROM HEADING-2 BEFORE ADVANCING 2
049800     WRITE PRINT-RECORD FROM HEADING-3 BEFORE ADVANCING 1
049900     MOVE 4 TO LINE-COUNT.
050000
050100 0500-DETAIL.
050200     WRITE PRINT-RECORD FROM DETAIL-LINE BEFORE ADVANCING 1.
050300     ADD 1 TO LINE-COUNT.
050400
050500 0500-EXIT.
050600     EXIT.
050700* - - - - - - - - - - - - - - - - PERFORM EXIT POINT
050800
