*---------------------------------------------------------------------*
*  Copyright 2011 by Clemson University Research Foundation           *
*                                                                     *
*  Licensed under the Apache License, Version 2.0 (the "License");    *
*  you may not use this file except in compliance with the License.   *
*  You may obtain a copy of the License in this library as member     *
*  $LICENSE, or from                                                  *
*                                                                     *
*      http://www.apache.org/licenses/LICENSE-2.0                     *
*                                                                     *
*  Unless required by applicable law or agreed to in writing,         *
*  software distributed under the License is distributed on an "AS    *
*  IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either    *
*  express or implied.  See the License for the specific language     *
*  governing permissions and limitations under the License.           *
*---------------------------------------------------------------------*
         MACRO
         SRVMOD &NAME=,&DATES='1990, 1995'
         GBLC  &SRV_NAME
         GBLC  &SRV_MODNAME
         GBLC  &SRV_COMPID
         GBLC  &SRV_FMID
         GBLC  &SRV_PTF
         GBLC  &SRV_DATES
         GBLC  &SRV_STDMNT
         GBLB  &SRV_SMP
         GBLB  &SRV_CLEMSON
         LCLA  &L
&SRV_NAME    SETC '&NAME'
&SRV_DATES   SETC '&DATES'
&SRV_MODNAME SETC '&SRV_NAME'
         COPY  SRVLEVEL
         AIF   ('&SRV_STDMNT' NE '').PAST_SET_STDMNT
&SRV_STDMNT SETC 'SRVMNMVS'
.PAST_SET_STDMNT ANOP
         AIF   (K'&NAME LE 3).ERR_NAME
         AIF   ('&SRV_COMPID' EQ '').DFLT_COMPID
         AIF   (K'&SRV_COMPID NE 3).ERR_COMPID
         AGO   .SET_MODNAME
.DFLT_COMPID ANOP
&SRV_COMPID SETC 'SRV'
.SET_MODNAME ANOP
.* MODNAME IS COMPID CONCATINATED WITH 4TH AND REMAINING CHARS OF NAME.
&L       SETA  K'&NAME-3
&SRV_MODNAME SETC '&SRV_COMPID'.'&NAME'(4,&L)
         AIF   (NOT &SRV_SMP).NO_SMP
         PUNCH '++ MOD (&SRV_MODNAME) DISTLIB(SYSPUNCH) .'
.NO_SMP  ANOP
         MACRO
         ##MODPFX &NAME,                NAME OF BLOCK                  +
               &CPYRGHT                 COPYRIGHT NOTICE
         GBLC  &SRV_FMID
         GBLC  &SRV_PTF
         GBLC  &SRV_DATES
         GBLC  &SRV_NAME
         GBLC  &SRV_MODNAME
         AIF   ('&NAME' EQ '&SRV_MODNAME').NAME_OK
         MNOTE 8,'MODULE NAME AND PROGRAM BLOCK MACRO MUST MATCH.'
.NAME_OK ANOP
##MODPFX__START_&SYSNDX EQU *
         DC    AL1(##MODPFX__SIZE_&SYSNDX)
         DC    CL8'&SRV_MODNAME'        MODULE NAME
         DC    CL10' &SYSDATE '         ASSEMBLE DATE
         DC    CL6'&SYSTIME'            ASSEMBLE TIME
         DC    CL8'&SRV_FMID'           FUNCTION ID
         DC    CL8'&SRV_PTF'            PTF ID
         DC    C'(C) COPYRIGHT CLEMSON UNIVERSITY RESEARCH FOUNDATION, +
               '
         DC    C&SRV_DATES
         DC    C'.'
         DS    0H
##MODPFX__SIZE_&SYSNDX EQU *-##MODPFX__START_&SYSNDX
.BLD0010 ANOP
         MEND
         MEXIT
.ERR_NAME MNOTE 8,'NAME=&NAME IS INVALID.  NAME MUST BE AT LEAST 3 CHAR+
               ACTERS LONG.'
         MEXIT
.ERR_COMPID MNOTE 8,'COMPID &SRV_COMPID IS INVALID.  COMPID MUST BE 3 C+
               HARACTERS LONG.'
         MEND
