/*REXX****************************************************************/
/*                                                                   */
/* Name: SQL -- ISPF Edit Macro                                      */
/*                                                                   */
/* Function: Run BSPUFI program in interactive mode.                 */
/*           When you edit/view a member with SQL statements         */
/*           type SQL ÝSSID¨ in the command line to execute them,    */
/*           output of the program is displayed by Browse at the     */
/*           end of execution.                                       */
/*                                                                   */
/* Note. Excluded lines by X/XX prefix command(s) are ignored.       */
/*********************************************************************/
/*                                                                   */
/* ---------  Adjust the following default assignments ------------- */
/*                                                                   */
 SSID = "DB2E"                                 /* default SSID       */
 BSPUFI="RUN PROGRAM(BSPUFI) PLAN(BSPUFI)",
        "LIB('MDCT.TEST.LOADLIB') ",           /* specify your lib   */
        "PARMS('INSQL(DD:INSQL) PRINT(OFF) LOG(DD:LOG)' )"
/*                                                                   */
/************************************* Vladimir Mestovski 2012 *******/
trace 'O'

sql  = Userid()||'.$TEMP$.SQL'
rpt  = Userid()||'.$TEMP$.LOG'

"ISREDIT MACRO (OPT1) PROCESS"
"ISPEXEC CONTROL ERRORS RETURN"
"ISREDIT (user) = USER_STATE"
"ISREDIT (hrow,hcol) = CURSOR"
"ISREDIT (scan) = SCAN"
"ISREDIT SCAN OFF"
"ISREDIT (LAST) = LINENUM .ZLAST"
"ISREDIT (LRECL) = LRECL"

if opt1<>'' then parse upper var opt1 SSID .

if datatype(LAST)='CHAR' then do;
 say "*ERROR* YOU should remove pending prefix commands first.."
 exit 8;
end;

k=0
do i = 1 to LAST
 "ISREDIT" '(statusx) = XSTATUS' i
 if statusx = 'X' then iterate
 "ISREDIT" '(DATA) = LINE' i
 k=k+1
 lines.k = DATA
end
lines.0=k

x = Outtrap('o.')
  Address TSO "FREE FILE(INSQL)"
  Address TSO "FREE FILE(LOG)"
  Address TSO "DELETE '"sql"'"
  Address TSO "DELETE '"rpt"'"
x=outtrap('OFF');

  Address TSO "ALLOCATE FI(INSQL) DSNAME('"sql"')",
              ' NEW DSORG(PS) RECFM(F,B) LRECL(80) ',
              ' SPACE(1,1) CYLINDERS RELEASE UNIT(SYSDA) CATALOG REUSE'

  Address TSO 'EXECIO * DISKW INSQL (FINIS STEM lines.'
  If rc>1   then exit (restoreState(rc))

  Address TSO "ALLOCATE FILE(LOG) DSNAME('"rpt"')",
              " NEW DSORG(PS) RECFM(V,B) ",
              ' SPACE(10,10) TRACKS RELEASE UNIT(SYSDA) CATALOG REUSE'
  Queue BSPUFI
  Queue "END"

  Address TSO "DSN SYSTEM ("SSID")"

  Address MVS "DELSTACK"
  "ISPEXEC BROWSE DATASET('"rpt"')"

  Return (restoreState(0))

restoreState: parse arg src
  'ISREDIT USER_STATE = (user)'
  'ISREDIT SCAN = 'scan
  'ISREDIT MEND'
  Return src
