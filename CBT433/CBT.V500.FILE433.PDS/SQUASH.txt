/* REXX    SQUASH     Submit a job to compress a dataset.
   |**-***-***-***-***-***-***-***-***-***-***-***-***-***-***-***-**|
   |                                                                 |
   |          WARNING: EMBEDDED COMPONENTS.                          |
   |                      See text following TOOLKIT_INIT            |
   |                                                                 |
   |**-***-***-***-***-***-***-***-***-***-***-***-***-***-***-***-**|

           Written by Chris Lewis 19960708

     Impact Analysis
.    SYSPROC   TRAPOUT
.    ISPSLIB   SQUASH

     Modification History
     19980505 fxc RXSKLY2K; upgrade from v.960702 to v.19980225;
                  DECOMM;
     19990712 fxc adapted for PMU
     19991129 fxc upgrade from v.19980225 to v.19991109; new DEIMBED;
     20010716 fxc added INSTALL option; structured;
     20010718 fxc block PFSHOW

*/ arg argline
address ISPEXEC                        /* REXXSKEL ver.19991109      */
arg parms "((" opts

signal on syntax
signal on novalue

call TOOLKIT_INIT                      /* conventional start-up     -*/
rc     = trace(tv)
info   = parms                         /* to enable parsing          */

call A_INIT                            /*                           -*/
call B_PROLOG                          /*                           -*/

   if sw.0install then do
      call ZZ_LOAD_CMDTBL              /*                           -*/
      if sw.0load_err then nop
      else do
         zerrsm = "Shortcut created"
         zerrlm = "A command has been written to your personal command",
               "table as specified. When activated, you may invoke",
               "this routine from Edit, Browse, or View."
         "SETMSG MSG(ISRZ002)"
         end
      return                           /* no processing              */
      end                              /*                            */

call C_JOBCARD                         /*                           -*/
call J_BUILD_JCL                       /*                           -*/
call L_EPILOG                          /*                           -*/
call R_REVIEW_SUBMIT                   /*                           -*/

exit                                   /*@ SQUASH                    */
/*
.  ----------------------------------------------------------------- */
A_INIT:                                /*@                           */
   if branch then call BRANCH
   address TSO

   parse value reverse(info) with dsn . /* take parm over ZDSN       */
   dsn = reverse(dsn)

   if dsn = "" | dsn = "''" then do
      helpmsg = "Dataset Name Required"
      call HELP
      end

   if left(dsn,1) = "'" then           /* dataset should be in TSO   */
      dsn = strip(dsn,,"'")            /* format; must be fully      */
   else                                /* qualified and unquoted for */
      dsn = userid()"."dsn             /* the JCL.                   */

return                                 /*@ A_INIT                    */
/*
.  ----------------------------------------------------------------- */
B_PROLOG:                              /*@                           */
   if branch then call BRANCH
   address ISPEXEC

   call DEIMBED                        /* extract SLIB(SQUASH)      -*/

   dd = ""
   do Words(ddnlist)                   /* each LIBDEF DD             */
      parse value ddnlist dd  with  dd ddnlist
      $ddn   = $ddn.dd                 /* PLIB322 <- PLIB            */
      "LIBDEF  ISP"dd "LIBRARY  ID("$ddn") STACK"
   end
   ddnlist = ddnlist dd

return                                 /*@ B_PROLOG                  */
/*
.  ----------------------------------------------------------------- */
C_JOBCARD:                             /*@                           */
   if branch then call BRANCH
   address ISPEXEC

   "VGET JOB1S ASIS"                   /* jobcard                    */
   if job1s = "" then do
      "VGET JOB1 ASIS"                 /* get standard jobcard       */
      if rc > 0 then do                /* not found?                 */
         address TSO "JOBCARDS"        /* setup initial set          */
         "VGET JOB1 ASIS"
         end                           /* JOB1 not found             */
      parse var job1 w1 rest           /* //jobname ...              */
      job1s = "//"Userid()"SQ" rest    /* reconstruct                */
      "VPUT JOB1S PROFILE"             /* save it                    */
      end                              /* JOB1H not found            */

return                                 /*@ C_JOBCARD                 */
/*
.  ----------------------------------------------------------------- */
J_BUILD_JCL:                           /*@                           */
   if branch then call BRANCH
   address ISPEXEC

   "FTOPEN TEMP"
   "FTINCL SQUASH"
   "FTCLOSE"

return                                 /*@ J_BUILD_JCL               */
/*
.  ----------------------------------------------------------------- */
L_EPILOG:                              /*@                           */
   if branch then call BRANCH
   address ISPEXEC

   dd = ""
   do Words(ddnlist)                   /* each LIBDEF DD             */
      parse value ddnlist dd  with  dd ddnlist
      $ddn   = $ddn.dd                 /* PLIB322 <- PLIB            */
      "LIBDEF  ISP"dd
      address TSO "FREE  FI("$ddn")"
   end
   ddnlist = ddnlist dd

return                                 /*@ L_EPILOG                  */
/*
.  ----------------------------------------------------------------- */
R_REVIEW_SUBMIT:                       /*@                           */
   if branch then call BRANCH
   address ISPEXEC

   "VGET (ZTEMPN ZTEMPF)"

   if monitor then do
      "LMINIT DATAID(DDNID) DDNAME("ztempn")"
      "EDIT DATAID("DDNID")"
      end

   if noupdt then nop
   else
      address TSO "submit '"ztempf"'"

return                                 /*@ R_REVIEW_SUBMIT           */
/*
.  ----------------------------------------------------------------- */
LOCAL_PREINIT:                         /*@ customize opts            */
   if branch then call BRANCH
   address TSO

   sw.0install  = SWITCH("INSTALL")

return                                 /*@ LOCAL_PREINIT             */
/*
   Install a shortcut command on the caller's command table.
.  ----------------------------------------------------------------- */
ZZ_LOAD_CMDTBL:                        /*@                           */
   if branch then call BRANCH
   address ISPEXEC

   "VGET ZPFCTL"; save_zpf = zpfctl    /* save current setting       */
      zpfctl = "OFF"; "VPUT ZPFCTL"    /* PFSHOW OFF                 */
   "ADDPOP ROW(8) COLUMN(5)"
   zwinttl = "Create Shortcut"
   "DISPLAY PANEL(INSTALL)"
   disp_rc = rc
   "REMPOP ALL"
      zpfctl = save_zpf; "VPUT ZPFCTL" /* restore                    */

   if tlibds <> "" then do
      if Sysdsn(tlibds) <> "OK" then do
         zerrsm = "Library?"
         zerrlm = "The datasetname specified is not available"
         "SETMSG MSG(ISRZ002)"
         sw.0load_err = "1"
         return
         end
      if cmdtbl <> "" then do
         "LIBDEF ISPTLIB DATASET ID("tlibds") STACK"
         "TBOPEN" cmdtbl"CMDS WRITE"
         if rc = 8 then do             /* new table                  */
            "TBCREATE" cmdtbl"CMDS WRITE",
                     "NAMES(ZCTVERB ZCTTRUNC ZCTACT ZCTDESC)"
            end
         if rc > 4 then do
            zerrsm = "Oops"
            zerrlm = "TBOPEN/TBCREATE failed for "cmdtbl"CMDS"
            "SETMSG MSG(ISRZ002)"
            sw.0load_err = "1"
            return
            end
         "LIBDEF ISPTLIB"

         zctverb  = "SQUASH"
         zcttrunc = 2
         zctact   = "SELECT CMD(%SQUASH '&ZDSN' &ZPARM)"
         zctdesc  = "Submit a job to compress a dataset"
         "TBADD"  cmdtbl"CMDS"

         "LIBDEF ISPTABL DATASET ID("tlibds") STACK"
         "TBCLOSE" cmdtbl"CMDS"
         "LIBDEF ISPTABL"
         end
      end
   else sw.0load_err = "1"

return                                 /*@ ZZ_LOAD_CMDTBL            */
/*
   Parse out the embedded components at the back of the source code.
.  ----------------------------------------------------------------- */
DEIMBED: Procedure expose,             /*@                           */
   (tk_globalvars)  ddnlist  $ddn.  daid.

   address TSO

   fb80po.0  = "NEW UNIT(VIO) SPACE(5 5) TRACKS DIR(40)",
                   "RECFM(F B) LRECL(80) BLKSIZE(0)"
   parse value ""   with  ddnlist $ddn.  daid.

   lastln   = sourceline()
   currln   = lastln                   /*                            */
   if Left(sourceline(currln),2) <> "*/" then return

   currln = currln - 1                 /* previous line              */
   "NEWSTACK"
   address ISPEXEC
   do while sourceline(currln) <> "/*"
      text = sourceline(currln)        /* save with a short name !   */
      if Left(text,3) = ")))" then do  /* package the queue          */
         parse var text ")))" ddn mbr .   /* PLIB PANL001  maybe     */
         if Pos(ddn,ddnlist) = 0 then do  /* doesn't exist           */
            ddnlist = ddnlist ddn      /* keep track                 */
            $ddn = ddn || Random(999)
            $ddn.ddn = $ddn
            address TSO "ALLOC FI("$ddn")" fb80po.0
            "LMINIT DATAID(DAID) DDNAME("$ddn")"
            daid.ddn = daid
            end
         daid = daid.ddn
         "LMOPEN DATAID("daid") OPTION(OUTPUT)"
         do queued()
            parse pull line
            "LMPUT DATAID("daid") MODE(INVAR) DATALOC(LINE) DATALEN(80)"
         end
         "LMMADD DATAID("daid") MEMBER("mbr")"
         "LMCLOSE DATAID("daid")"
         end                           /* package the queue          */
      else push text                   /* onto the top of the stack  */
      currln = currln - 1              /* previous line              */
   end                                 /* while                      */
   address TSO "DELSTACK"

return                                 /*@ DEIMBED                   */
/*
.  ----------------------------------------------------------------- */
HELP:                                  /*@                           */
address TSO;"CLEAR"
if helpmsg <> "" then do ; say helpmsg; say ""; end

say "  SQUASH        Submit a job to compress a dataset.                       "
say "                                                                          "
say "  Syntax:   SQUASH    <dsn>              -  TSO Format                    "
say "                   (( <INSTALL>                                           "
say "                                                                          "
say "            To squeeze the dataset you are currently in, add the following"
say "            to your command table:                                        "
say "                                                                          "
say "            VERB     T  ACTION                                            "
say "                           DESCRIPTION                                    "
say "            -------- -  ----------------------------------------          "
say "            SQUASH   2  SELECT CMD(%SQUASH '&ZDSN' &ZPARM)                "
say "                           Submit a JOB to compress a dataset             "
say "                                                                          "
say "            Specifying '(( INSTALL' will result in the proper command     "
say "            being inserted to your command table (which must already      "
say "            exist).                                                       "
say "                                                                          "
say "            NOTE:  Any parm is taken in preference to ZDSN.  The program  "
say "                   will take the last parm as the dsn to compress.        "
say "            NOTE:  Option MONITOR will display the JCL prior to           "
say "                   submission.                                            "
say "                   Option NOUPDT will stop automatic submission of the    "
say "                   JCL.                                                   "
say "                                                                          "
"NEWSTACK" ; pull ; "CLEAR" ; "DELSTACK"
say "   Debugging tools provided include:                                      "
say "                                                                          "
say "        MONITOR:  displays key information throughout processing.         "
say "                  Displays most paragraph names upon entry.               "
say "                                                                          "
say "        NOUPDT:   by-pass all update logic.                               "
say "                                                                          "
say "        BRANCH:   show all paragraph entries.                             "
say "                                                                          "
say "        TRACE tv: will use value following TRACE to place the execution in"
say "                  REXX TRACE Mode.                                        "
say "                                                                          "
say "                                                                          "
say "   Debugging tools can be accessed in the following manner:               "
say "                                                                          "
say "        TSO" exec_name"  parameters  ((  debug-options                    "
say "                                                                          "
say "   For example:                                                           "
say "                                                                          "
say "        TSO" exec_name " (( MONITOR TRACE ?R                              "
address ISPEXEC "CONTROL DISPLAY REFRESH"
exit                                   /*@ HELP                      */
/*
.  ----------------------------------------------------------------- */
BRANCH: Procedure expose,              /*@                           */
        sigl exec_name
   rc = trace("O")                     /* we do not want to see this */
   arg brparm .

   origin = sigl                       /* where was I called from ?  */
   do currln = origin to 1 by -1       /* inch backward to label     */
      if Right(Word(Sourceline(currln),1),1) = ":" then do
         parse value sourceline(currln) with pgfname ":" .  /* Label */
         leave ; end                   /*                name        */
   end                                 /* currln                     */

   select
      when brparm = "NAME" then return(pgfname) /* Return full name  */
      when brparm = "ID"      then do           /* wants the prefix  */
         parse var pgfname pgfpref "_" .        /* get the prefix    */
         return(pgfpref)
         end                           /* brparm = "ID"              */
      otherwise
         say left(sigl,6) left(pgfname,40) exec_name "Time:" time("L")
   end                                 /* select                     */

return                                 /*@ BRANCH                    */
/*
.  ----------------------------------------------------------------- */
DUMP_QUEUE:                            /*@ Take whatever is in stack */
   rc = trace("O")                     /*  and write to the screen   */
   address TSO

   "QSTACK"                            /* how many stacks?           */
   stk2dump    = rc - tk_init_stacks   /* remaining stacks           */
   if stk2dump = 0 & queued() = 0 then return
   say "Total Stacks" rc ,             /* rc = #of stacks            */
       "Begin Stacks" tk_init_stacks , /* Stacks present at start    */
       "Stacks to DUMP" stk2dump

   do dd = rc to tk_init_stacks by -1  /* empty each one.            */
      say "Processing Stack #" dd "Total Lines:" queued()
      do queued();pull line;say line;end /* pump to the screen       */
      "DELSTACK"                       /* remove stack               */
   end                                 /* dd = 1 to rc               */

return                                 /*@ DUMP_QUEUE                */
/*
.  ----------------------------------------------------------------- */
KEYWD: Procedure expose info           /*@ hide all vars, except info*/
   arg kw
   kw_pos = wordpos(kw,info)           /* find where it is, maybe    */
   if kw_pos = 0 then return ""        /* send back a null, not found*/
   kw_val = word(info,kw_pos+1)        /* get the next word          */
   info   = Delword(info,kw_pos,2)     /* remove both                */
return kw_val                          /*@ KEYWD                     */
/*
.  ----------------------------------------------------------------- */
KEYPHRS: Procedure expose,             /*@                           */
         info helpmsg exec_name        /*  except these three        */
   arg kp
   wp    = wordpos(kp,info)            /* where is it?               */
   if wp = 0 then return ""            /* not found                  */
   front = subword(info,1,wp-1)        /* everything before kp       */
   back  = subword(info,wp+1)          /* everything after kp        */
   parse var back dlm back             /* 1st token must be 2 bytes  */
   if length(dlm) <> 2 then            /* Must be two bytes          */
      helpmsg = helpmsg "Invalid length for delimiter("dlm") with KEYPHRS("kp")"
   if wordpos(dlm,back) = 0 then       /* search for ending delimiter*/
      helpmsg = helpmsg "No matching second delimiter("dlm") with KEYPHRS("kp")"
   if helpmsg <> "" then call HELP     /* Something is wrong         */
   parse var back kpval (dlm) back     /* get everything b/w delim   */
   info =  front back                  /* restore remainder          */
return Strip(kpval)                    /*@ KEYPHRS                   */
/*
.  ----------------------------------------------------------------- */
NOVALUE:                               /*@                           */
   say exec_name "raised NOVALUE at line" sigl
   say " "
   say "The referenced variable is" condition("D")
   say " "
   zsigl = sigl
   signal SHOW_SOURCE                  /*@ NOVALUE                   */
/*
.  ----------------------------------------------------------------- */
SHOW_SOURCE:                           /*@                           */
   call DUMP_QUEUE                     /* Spill contents of stacks  -*/
   if sourceline() <> "0" then         /* to screen                  */
      say sourceline(zsigl)
   rc =  trace("?R")
   nop
   exit                                /*@ SHOW_SOURCE               */
/*
.  ----------------------------------------------------------------- */
SS: Procedure                          /*@ Show Source               */
   arg  ssbeg  ssend  .
   if ssend = "" then ssend = 10
   if \datatype(ssbeg,"W") | \datatype(ssend,"W") then return
   ssend = ssbeg + ssend
   do ssii = ssbeg to ssend ; say sourceline(ssii) ; end
return                                 /*@ SS                        */
/*
.  ----------------------------------------------------------------- */
SWITCH: Procedure expose info          /*@                           */
   arg kw
   sw_val = Wordpos(kw,info) > 0       /* exists = 1; not found = 0  */
   if sw_val then                      /* exists                     */
      info = Delword(info,Wordpos(kw,info),1) /* remove it           */
return sw_val                          /*@ SWITCH                    */
/*
.  ----------------------------------------------------------------- */
SYNTAX:                                /*@                           */
   errormsg = exec_name "encountered REXX error" rc "in line" sigl":",
                        errortext(rc)
   say errormsg
   zsigl = sigl
   signal SHOW_SOURCE                  /*@ SYNTAX                    */
/*
   Can call TRAPOUT.
.  ----------------------------------------------------------------- */
TOOLKIT_INIT:                          /*@                           */
   address TSO
   info = Strip(opts,"T",")")          /* clip trailing paren        */

   parse source  sys_id  how_invokt  exec_name  DD_nm  DS_nm,
                     as_invokt  cmd_env  addr_spc  usr_tokn

   parse value "" with  tv  helpmsg  .
   parse value 0   "ISR00000  YES"     "Error-Press PF1"    with,
               sw.  zerrhm    zerralrm  zerrsm

   if SWITCH("TRAPOUT") then do
      "TRAPOUT" exec_name parms "(( TRACE R" info
      exit
      end                              /* trapout                    */

   if Word(parms,1) = "?" then call HELP /* I won't be back          */

   "QSTACK" ; tk_init_stacks = rc      /* How many stacks?           */

   parse value SWITCH("BRANCH") SWITCH("MONITOR") SWITCH("NOUPDT") with,
               branch           monitor           noupdt    .

   parse value mvsvar("SYSNAME") sysvar("SYSNODE") with,
               #tk_cpu           node          .

   sw.nested    = sysvar("SYSNEST") = "YES"
   sw.batch     = sysvar("SYSENV")  = "BACK"
   sw.inispf    = sysvar("SYSISPF") = "ACTIVE"

   parse value KEYWD("TRACE")  "O"    with   tv  .
   tk_globalvars = "tk_globalvars exec_name tv helpmsg sw. zerrhm",
                   "zerralrm zerrsm zerrlm tk_init_stacks branch",
                   "monitor noupdt"

   call LOCAL_PREINIT                  /* for more opts             -*/

return                                 /*@ TOOLKIT_INIT              */
/*
)))SLIB SQUASH
&JOB1S
&JOB2
&JOB3
&JOB4
//* -------------------------------------- ISPSLIB(SQUASH) */
//SQUASH   EXEC PGM=IEBCOPY
//SYSPRINT  DD SYSOUT=*
//SYSUT3    DD UNIT=SYSDA,SPACE=(80,(60,45))
//SYSUT4    DD UNIT=SYSDA,SPACE=(256,(15,1)),DCB=KEYLEN=8
//SYSIN     DD *
     COPY  OUTDD=INOUT,INDD=INOUT
//INOUT     DD DISP=SHR,DSN=&DSN
)))PLIB INSTALL
)ATTR
  % TYPE(TEXT)   INTENS(HIGH) SKIP(ON)
  + TYPE(TEXT)   INTENS(LOW)  SKIP(ON)
  _ TYPE(INPUT)  INTENS(HIGH) CAPS(ON) JUST(LEFT) PAD('_')
  @ TYPE(OUTPUT) INTENS(HIGH) CAPS(ON) JUST(LEFT)
  $ TYPE(INPUT)  INTENS(HIGH) CAPS(ON) JUST(LEFT)
)BODY WINDOW(68,5)
+
+ ISPTLIB DSN%==>$tlibds                                        +
+     ...CMDS%==>$z   +
+
)INIT
  .ZVARS   = '(CMDTBL)'
  .HELP    = INSTALH
  .CURSOR  = TLIBDS
)PROC
  VER (&TLIBDS,DSNAME)
  VER (&CMDTBL,NAME)
  VPUT (CMDTBL,TLIBDS) PROFILE
)END
)))PLIB INSTALH
)ATTR
  % TYPE(TEXT)   INTENS(HIGH)  SKIP(ON)
  + TYPE(TEXT)   INTENS(LOW)   SKIP(ON)
  _ TYPE(INPUT)  INTENS(HIGH)
  ! TYPE(OUTPUT) INTENS(HIGH)  SKIP(ON)
  @ TYPE(OUTPUT) INTENS(LOW)   SKIP(ON)
)BODY EXPAND(¦¦)
%TUTORIAL ¦-¦ COMPILE -- Install Shortcut ¦-¦ TUTORIAL %Next Selection
===>_ZCMD

+
     Enter a Library datasetname and membername to identify your personal
     command table.  A shortcut will be generated at that location.

     If you do not have a personal command table, leave this information blank
     and the installation step will be skipped.

     It is%HIGHLY RECOMMENDED+that you have a personal command table which can
     be activated as by (e.g.) ADDCMDS.
)PROC
)END
*/
