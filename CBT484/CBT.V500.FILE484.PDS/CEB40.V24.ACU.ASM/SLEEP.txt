         TITLE '  REXX EXTENSION -- ISSUE STIMER WAIT'
SLEEP   CSECT                         *
SLEEP   AMODE 31
SLEEP   RMODE ANY
*---------------------------------------------------------------------
* (C) COPYRIGHT AMDAHL CORPORATION  1995. ALL RIGHTS RESERVED
* WRITTEN BY:  CHUCK BAUMANN ------------------MAY 1993     ----------
*
*      AN EXTERNAL REXX SUBROUTINE       THIS CODE WILL ISSUE AN MVS
*      STIMER MACRO USING THE ARGUMENT AS THE INTERVAL (IN SECONDS) TO
*      WAIT. THIS SIMULATES THE VM SLEEP COMMAND.
*
* INVOKED:    CALL SLEEP XXXX      (XXXX = NUMBER OF SECONDS TO SLEEP)
* REFERNC:    TSO EXTENSIONS VERSION 2
*             PROCEDURES LANGUAGE MVS/REXX REFERENCE  SC28-1883
*
* REGISTER USAGE: (ON ENTRY)
*   R0     -  ADDRESS OF ENVIRONMENT BLOCK
*   R1     -  ADDRESS OF THE EXTERNAL FUNCTION PARAMETER LIST
*   R2-12  -  UNPREDICTABLE
*   R13    -  REGISTER SAVE AREA
*   R14    -  RETURN ADDRESS
*   R15    -  ENTRY POINT ADDRESS
* REGISTER USAGE:
*   R2     -  ADDRESS OF ARGUMENT
*   R3     -  LENGTH  OF ARGUMENT
*   R11    -  ADDRESSABILITY FOR EFPL MAPPING MACRO (IRXEFPL)
*   R12    -  BASE
*---------------------------------------------------------------------
         YREGS
         SAVE  (14,12)                 STANDARD LINKAGE
         LR    R12,R15
         USING SLEEP,R12
         ST    R13,SAVE+4
         LA    R14,SAVE
         ST    R14,8(R13)
         LR    R13,R14
         EJECT
*----------------------------------------------------------------------
*     THE FORMAT OF THE PARSED ARGUMENT LIST THIS FUNCTION RECEIVES   *
*     IS ADDRESS/LENGTH PAIRS UNTIL TERMINATED BY A DOUBLEWORD OF     *
*     HIGH VALUES.                                                    *
*     DS   A      ADDRESS OF ARG N                                    *
*     DS   F      LENGTH  OF ARG N                                    *
*     DC   X'FFFFFFFFFFFFFFFF'                                        *
*----------------------------------------------------------------------
         LR    R11,R1                  ADDRESS OF EXT FUNCT PLIST
         USING EFPL,R11
         L     R10,EFPLARG             ADDRESS OF PARSED ARG LIST
         L     R9,EFPLEVAL             ADDRESS OF FULLWORD PTR
         L     R9,0(,R9)               ADDRESS OF EVALBLOCK
         L     R2,0(,R10)              ADDRESS OF ARG(1)
         L     R3,4(,R10)              LENGTH  OF ARG(1)
         LA    R4,5                    MAX LENGTH OF ARGUMENT 99999      00002A
         SR    R4,R3                   OFFSET AT WHICH TO MOVE ARG       00002E
         LA    R5,INTERVAL             ADDRESS OF WORK AREA              000030
         AR    R5,R4                   OFFSET IN INTERVAL FOR MOVE       000034
         MVC   INTERVAL,=C'00000'      INIT WORKAREA                     000036
         BCTR  R3,R0                   DECREMENT LENGTH BY 1             00003C
         EX    R3,MOVEIT               MOVE ARGUMENT TO WORKAREA         00003E
         ZAP   PACKAREA,=P'0'          INIT THE DECIMAL WORKAREA         000042
         PACK  PACKAREA+5(3),INTERVAL(5)   PACK THE ARGUMENT             000048
         SRP   PACKAREA,2,0            * 100 CONVERT TO 100THS OF SEC    00004E
         CVB   R4,PACKAREA             CONVERT TO BINARY FOR STIMER      000054
         N     R4,UNSIGN               REMOVE SIGN                       000058
         ST    R4,BINSLP               SAVE TIME TO SLEEP                00005C
         STIMER WAIT,BINTVL=BINSLP     START WAIT                        000060
*----------------------------------------------------------------------
*  FILL IN REQUIRED INFO IN THE EVALBLOCK BEFORE RETURNING
*  IF LENGTH OF INPUT DATA IS GREATER THAN 125 YOU CAN CALL THE IRXRLT
*  ROUTINE TO OBTAIN A LARGER EVALUATION BLOCK.
*----------------------------------------------------------------------
         L     R3,NORESULT             FIX UP RETURN PARAMETERS          000070
         ST    R3,8(,R9)               SAVE IN EVALLEN                   000074
         SR    R15,R15                 SET RETURN CODE = 0
EXIT     EQU   *
         L     R13,SAVE+4
         RETURN (14,12),RC=(15)
MOVEIT   MVC   0(0,R5),0(R2)           MOVE ARG TO WORKAREA              00008C
         EJECT
SAVE     DC    18F'0'
NORESULT DC    X'80000000'             INDICATES NO RESULT RETURNED
UNSIGN   DC    X'EFFFFFFF'             MASK TO TURN OFF BIT
INTERVAL DS    CL5
PACKAREA DS    D
BINSLP   DS    F
*
         EJECT
         LTORG
         IRXEFPL
         END   SLEEP
