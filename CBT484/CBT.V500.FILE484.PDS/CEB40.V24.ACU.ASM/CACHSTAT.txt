           TITLE 'CACHEMAN DASD SCAN MODULE'
CACHSTAT CSECT *
*---------------------------------------------------------------------
* This program will scan online ucbs to construct a table that is
* used in the CACHEMN1 exec.
* REQUIREMENTS:
*   Macros -  all dialog services macros from my macro library
* (C) COPYRIGHT AMDAHL CORPORATION  1995. All Rights Reserved
*---------------------------------------------------------------------
         YREGS
         SAVE  (14,12)                 STANDARD LINKAGE
         LR    R11,R15
         LA    R12,4095(R11)
         LA    R12,1(R12)
         USING CACHSTAT,R11,R12
         ST    R13,SAVE+4
         LA    R14,SAVE
         ST    R14,8(R13)
         LR    R13,R14
*---------------------------------------------------------------------
* Load a copy of ISPLINK for use by the Dialog Manager macros
*---------------------------------------------------------------------
         LOAD    EP=ISPLINK
         ST      R0,SAVEISP
*
*-- Test which version of MVS is running. If prior to MVS 4 then
*-- use the older techniqe to access UCB scan services
*
         L     R2,CVTPTR               address of CVT
         USING CVTMAP,R2
         LR    R3,R2
         LA    R4,CVTMAP-CVTFIX        address of CVT prefix area
         SR    R3,R4
         USING CVTFIX,R3               addressability for prefix area
PRODN    MVC   MVSVERS(1),CVTPRODN+2   save version of prod number
         DROP  R3
*---------------------------------------------------------------------
* VDEFINE the storage areas so ISPF can associate variables with them
* all areas with same size and type can be done with one vdefine
*---------------------------------------------------------------------
         VDEFINE (TCHAN),HEX,2
         VDEFINE (TVOL),CHAR,6
         VDEFINE (TDEVICE),CHAR,4
*
* Create the table to hold the CACHSTAT entries
*
CRTTAB   TBCREATE CSTTABLE,,(TCHAN,TVOL,TDEVICE),WR,REPLACE
UCBCNT   SR    R9,R9                   CLEAR R9 FOR UCB COUNTER
         MVI   DEV,X'20'               set device class to DASD
         LA    R1,WORKX                work area for old format scan
         ST    R1,P1                   save address of work area
         LA    R1,DEV
         ST    R1,P2                   save address of device class
         LA    R1,UCBA
         ST    R1,P3                   save address of ucb area
         XC    UCB48,UCB48
         XC    WORKX,WORKX
*
UCBSCAN  EQU   *
         CLI   MVSVERS,C'4'            is product MVS SP4 ESA 4 or >
         BL    PRESP4                  no;  go to pre-SP4 logic
*
* -- This section of code invokes the extended UCB scan services via
* -- the UCBSCAN macro.  This handles dynamic UCBs and 4 digit addr.
* -- This macro was new with MVS 4.1? (really a DFP version macro)
*
         UCBSCAN COPY,WORKAREA=WORKX,UCBAREA=UCB48,                    X
               DEVCLASS=DASD,DYNAMIC=YES,RANGE=ALL
         LTR   R15,R15                 WAS A DASD UCB FOUND
         BNZ   EXIT                    NO DASD UCB FOUND FOR VOL
         LA    R8,UCB48                ELSE GET ADDRESS PASSED BACK
         B     UCBRETND                branch around code for old svcs
*
* -- This section of code is to invoke the UCB scan services for
* -- MVS systems prior to Version 4.  The UCBSCAN macro was new with
* -- DFP 3.?  and will abend with a 0D6 abend if executed on pre 3.2?
*
PRESP4   L     R14,CVTPTR              ADDR OF CVT
         L     R15,CVTUCBSC-CVTMAP(,R14) address of ucb scan services
         LA    R1,P1                   LOAD ADDRESS OF PARMLIST
         OI    P3,X'80'                SET END OF PARMLIST
         BALR  R14,R15                 CALL UCB LOOKUP ROUTINE
         LTR   R15,R15                 WAS A DASD UCB FOUND
         BNZ   EXIT                    NO DASD UCB FOUND FOR VOL
         L     R8,UCBA                 ELSE GET ADDRESS PASSED BACK
UCBRETND EQU   *
         USING UCBOB,R8                DSECT
         TM    UCBSTAT,UCBONLI         DEVICE ONLINE???
         BNO   UCBSCAN                 TRY AGAIN
         TM    UCBSTAT,UCBCHGS         DEVICE GOING OFFLINE???
         BO    UCBSCAN                 TRY TRY AGAIN
GOTONE   EQU   *
         SR    R2,R2
         LH    R2,UCBCHAN
         SRL   R2,4                    shift off last nibble
         SR    R3,R3
         LH    R3,TCHAN
         SRL   R3,4                    shift off last nibble
         CR    R2,R3                   is address from same range?
         BE    UCBSCAN                 yes - don't add it to the table
         MVC   TCHAN,UCBCHAN           move to table variable
         MVC   TVOL,UCBVOLI            volser in ucb
         SR    R1,R1
         IC    R1,UCBUNTYP         LOAD UNIT TYPE DEVICE CODE FIELD
         SLL   R1,2                MULTIPLY BY 4 FOR TABLE ENTRIES
         LA    R3,DEVNM            GET START OF DEVICE NAME TABLE
         AR    R1,R3               AND ADD TO CORRECT OFFSET
         MVC   TDEVICE(4),0(R1)    LOAD DEVICE TYPE FROM TABLE
         TBADD CSTTABLE                ADD A ROW TO SPACE TABLE
         B     UCBSCAN                 LOOP UNTIL ALL UCB'S FOUND
         CNOP  0,4
TBCLPRM  DC    A(TBCLOSE)
         DC    A(CSTTABLE+X'80000000')
EXIT     EQU   *
         LA    R1,TBCLPRM
         L     R15,SAVEISP
         BALR  R14,R15
QWIKEXIT EQU   *
         L     R13,SAVE+4
         RETURN (14,12),RC=(15)
         EJECT
         COPY  DMFUNCT
WORKADR  DC    PL3'0'
FROMX    DS    XL2
TOX      DS    XL2
*
DEVNM    DC    CL4'....'               00 (00) - UNASSIGNED
         DC    CL4'2311'               01 (01) - 2311 DISK
         DC    CL4'2301'               02 (02) - 2301 DRUM
         DC    CL4'2303'               03 (03) - 2303 DRUM
         DC    CL4'2302'               04 (04) - 2302 DISK FILE
         DC    CL4'2321'               05 (05) - 2321 DATA CELL
         DC    CL4'2305'               06 (06) - 2305-1
         DC    CL4'2305'               07 (07) - 2305-2
         DC    CL4'2314'               08 (08) - 2314 DISK
         DC    CL4'3330'               09 (09) - 3330 DISK
         DC    CL4'3340'               10 (0A) - 3340
         DC    CL4'3350'               11 (0B) - 3350 DISK
         DC    CL4'3375'               12 (0C) - 3375
         DC    CL4'3330'               13 (0D) - 3330-11 DISK
         DC    CL4'3380'               14 (0E) - 3380
         DC    CL4'3390'               15 (0F) - 3390
         SPACE 2
*------------------------------------------------------------------
*-- The following work areas are used by the UCBSCAN macro.
*
         DS    0F
WORKX    DC    100X'00'
UCB48    DS    CL48
*------------------------------------------------------------------
*-- The following work areas are used by the 'old' style invocation
*-- of the UCB scan service routines.
*
         DS    0F
UCBA     DS    A
DEV      DS    XL1
         DS    0F
P1       DS    A
P2       DS    A
P3       DS    A
*------------------------------------------------------------------
MVSVERS  DC    C'0'
*
SAVE     DC    18F'0'
ZERO     DC    H'0'
         LTORG
         DSECT
         IEFUCBOB DEVCLAS=DA
         CVT   DSECT=YES,PREFIX=YES
         END   CACHSTAT
