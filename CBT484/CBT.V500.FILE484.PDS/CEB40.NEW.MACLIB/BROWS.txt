         MACRO
&NAME    BROWS   &DSN=,&VOLSER=,&DATAID=,&MBR=
.*DIALOG MANAGER MACRO -- DIALOG MANAGEMENT SERVICES (SC34-4021)
.*------------------------------------------------------------------
.* DIALOG MANAGER SERVICE: BROWSE      BROWSE A DATASET
.*
.* (REQUIRED)  DSN= OR DATAID=
.*------------------------------------------------------------------
.*
         LCLC  &BRANCH
         LCLC  &BROPRM
         LCLC  &VOLLBL
         LCLC  &MEMLBL
&BRANCH  SETC  'BRO'.'&SYSNDX'
&BROPRM  SETC  'BROP'.'&SYSNDX'
&NAME    CNOP  0,4
         B     &BRANCH
&BROPRM  DC    A(BROWSE)
         AIF   ('&VOLSER' EQ '').MBR
&VOLLBL  SETC  'VOL'.'&SYSNDX'
&VOLLBL  DC    CL6'&VOLSER'
.MBR     AIF   ('&MBR' EQ '').DSN
&MEMLBL  SETC  'MEM'.'&SYSNDX'
&MEMLBL  DC    CL8'&MBR'
.DSN     AIF   ('&DSN' NE '').DATASET
         AIF   ('&DATAID' NE '').DATAID
         AGO   .ERRPRM1
.DATASET ANOP
         AIF   ('&DATAID' NE '').ERRPRM2
         AIF   ('&VOLLBL' NE '').CALL2
         DC    A(&DSN+X'80000000')
         AGO   .FINIS
.CALL2   DC    A(&DSN)
         DC    A(&VOLLBL+X'80000000')
         AGO   .FINIS
.DATAID  ANOP
         AIF   ('&MEMLBL' NE '').CALL3
         DC    A(&DATAID+X'80000000')
         AGO   .FINIS
.CALL3   DC    A(&DATAID)
         DC    A(&MEMLBL+X'80000000')
.FINIS   ANOP
&BRANCH  EQU   *
         LA    R1,&BROPRM
         L     R15,SAVEISP
         BALR  R14,R15
         MEXIT
.ERRPRM1 MNOTE 12,'DSN OR DATAID IS A REQUIRED PARM'
         MEXIT
.ERRPRM2 MNOTE 12,'BOTH DSN AND DATAID CANNOT BE SPECIFIED'
         MEXIT
         MEND
