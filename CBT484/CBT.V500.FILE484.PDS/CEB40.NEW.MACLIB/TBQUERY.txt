         MACRO
&NAME TBQUERY &TABNM,&KEYS=,&NAMES=,&ROWNUM=,&KEYNUM=,&NAMENUM=,&POS=
.*DIALOG MANAGER MACRO -- DIALOG MANAGEMENT SERVICES (SC34-4021)
         LCLC  &BRANCH
         LCLC  &TBSKPRM
         LCLC  &KEYSV
         LCLC  &NAMESV
         LCLC  &ROWNUMV
         LCLC  &KEYNUMV
         LCLC  &NAMENUMV
&KEYSV  SETC  'A(0)'
&NAMESV  SETC  'A(0)'
&ROWNUMV SETC  'A(0)'
&KEYNUMV SETC  'A(0)'
&NAMENUMV SETC 'A(0)'
&BRANCH  SETC  'TBQU'.'&SYSNDX'
&TBSKPRM SETC  'TBQE'.'&SYSNDX'
         AIF   ('&TABNM' EQ '').ERR
&NAME    B     &BRANCH
         AIF   ('&KEYS' EQ '').SAVEL
&KEYSV  SETC  'A(&KEYS)'
.SAVEL   AIF   ('&NAMES' EQ '').ROWNUML
&NAMESV  SETC  'A(&NAMES)'
.ROWNUML AIF   ('&ROWNUM' EQ '').KEYNUML
&ROWNUMV SETC  'A(&ROWNUM)'
.KEYNUML AIF   ('&KEYNUM' EQ '').NAMENUML
&KEYNUMV SETC  'A(&KEYNUM)'
.NAMENUML AIF  ('&NAMENUM' EQ '').DONE
&NAMENUMV SETC 'A(&NAMENUM)'
.DONE    CNOP  0,4
&TBSKPRM DC    A(TBQUERY)
         AIF   ('&POS' EQ '').NAMENUM
         DC    A(&TABNM)
         DC    &KEYSV
         DC    &NAMESV
         DC    &ROWNUMV
         DC    &KEYNUMV
         DC    &NAMENUMV
         DC    A(&POS+X'80000000')
         AGO   .FINIS
.NAMENUM AIF   ('&NAMENUM' EQ '').KEYNUM
         DC    A(&TABNM)
         DC    &KEYSV
         DC    &NAMESV
         DC    &ROWNUMV
         DC    &KEYNUMV
         DC    A(&NAMENUM+X'80000000')
         AGO   .FINIS
.KEYNUM  AIF   ('&ROW' EQ '').ROWNUM
         DC    A(&TABNM)
         DC    &KEYSV
         DC    &NAMESV
         DC    &ROWNUMV
         DC    A(&KEYNUM+X'80000000')
         AGO   .FINIS
.ROWNUM  AIF   ('&ROWNUM' EQ '').NAMES
         DC    A(&TABNM)
         DC    &KEYSV
         DC    &NAMESV
         DC    A(&ROWNUM+X'80000000')
         AGO   .FINIS
.NAMES   AIF   ('&NAMES' EQ '').NUM
         DC    A(&TABNM)
         DC    &KEYSV
         DC    A(&NAMES+X'80000000')
         AGO   .FINIS
.NUM     AIF   ('&KEYS' EQ '').TABNM
         DC    A(&TABNM)
         DC    A(&KEYS+X'80000000')
         AGO   .FINIS
.TABNM   ANOP
         DC    A(&TABNM+X'80000000')
.FINIS   ANOP
&BRANCH  EQU   *
         LA    R1,&TBSKPRM
         L     R15,SAVEISP
         BALR  R14,R15
         MEXIT
.ERR     MNOTE 12,'TABLENAME IS A REQUIRED PARM'
         MEXIT
         MEND
