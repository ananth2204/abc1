         MACRO
&NAME    VDELETE  &NMLST
.*DIALOG MANAGER MACRO -- DIALOG MANAGEMENT SERVICES (SC34-4021)
.*------------------------------------------------------------------
.* DIALOG MANAGER SERVICE: VDELETE        REMOVE VARIABLE NAMES
.*   FORMAT:   VDELETE NAMELIST
.*
.* (REQUIRED)  NAMELIST - LIST OF VARIABLE NAMES TO DELETE.
.*                        MUST BE ENCLOSED IN () UNLESS * IS CODED.
.*                        IF * IS CODED ALL DIALOG VARIABLES DEFINED
.*                        BY THIS PROGRAM ARE REMOVED.
.*------------------------------------------------------------------
         LCLC  &VDELPRM
&VDELPRM SETC  'VDLP'.'&SYSNDX'
         LCLC  &VDELLST
         LCLC  &BRANCH                 BRANCH LABEL
&BRANCH  SETC  'VDLB'.'&SYSNDX'        VARIABLE NAME FOR NAMELIST
&VDELLST SETC  'VDEL'.'&SYSNDX'        VARIABLE NAME FOR NAMELIST
.*
         AIF   ('&NMLST' EQ '').ERR
         AIF   ('&NMLST'(1,1) NE '(').ASTRSK
         AIF   ('&NMLST'(2,1) NE '*').SETNAM
&DNAMLST SETC  '*'
         AGO   .CODE
.ASTRSK  AIF   ('&NMLST'(1,1) NE '*').ERRAST
&DNAMLST SETC  '*'
         AGO   .CODE
.SETNAM  ANOP
&DNAMLST SETC  '&NMLST'
.CODE    ANOP
&NAME    B     &BRANCH
&VDELLST DC    C'&DNAMLST'             LIST OF VARIABLES TO DEFINE
         CNOP  0,4
&VDELPRM DC    A(VDELETE)
         DC    A(&VDELLST+X'80000000')
&BRANCH  EQU   *
         LA    R1,&VDELPRM
         L     R15,SAVEISP
         BALR  R14,R15
         MEXIT
.ERR     MNOTE 12,'NAMELIST IS A REQUIRED PARM'
         MEXIT
.ERRAST  MNOTE 12,'NAMELIST MUST BE ENCLOSED IN ()'
         MEND
