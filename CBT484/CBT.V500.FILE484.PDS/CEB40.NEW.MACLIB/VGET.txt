         MACRO
&NAME    VGET     &NMLST,&POOL
.*DIALOG MANAGER MACRO -- DIALOG MANAGEMENT SERVICES (SC34-4021)
.*------------------------------------------------------------------
.* DIALOG MANAGER SERVICE: VGET        COPY VAR FROM SHRD OR PROF
.*   FORMAT:   VGET NAMELIST,POOL              TO FUNCTION POOL
.*
.* (REQUIRED)  NAMELIST - LIST OF VARIABLE NAMES TO COPY.
.* (OPTIONAL)  POOL     - ASIS (DEFAULT), SHARED OR PROFILE
.*------------------------------------------------------------------
         LCLC  &VGETLST
         LCLC  &VPOOL
         LCLC  &VGETPRM
         LCLC  &BRANCH                 BRANCH LABEL
&BRANCH  SETC  'VGTB'.'&SYSNDX'        VARIABLE NAME FOR NAMELIST
&VGETLST SETC  'VGET'.'&SYSNDX'        VARIABLE NAME FOR NAMELIST
&VGETPRM SETC  'VGTP'.'&SYSNDX'        VARIABLE NAME FOR PARMLIST
&VPOOL   SETC  'ASIS'
.*
         AIF   ('&NMLST' EQ '').ERR
         AIF   (K'&POOL EQ 0).SKPPOOL
&VPOOL   SETC  '&POOL'
.SKPPOOL ANOP
&NAME    B     &BRANCH
&VGETLST DC    C'&NMLST'             LIST OF VARIABLES TO COPY
         CNOP  0,4
&VGETPRM DC    A(VGET)
         DC    A(&VGETLST)
         DC    A(&VPOOL+X'80000000')
&BRANCH  EQU   *
         LA    R1,&VGETPRM
         L     R15,SAVEISP
         BALR  R14,R15
         MEXIT
.ERR     MNOTE 12,'NAMELIST IS A REQUIRED PARM'
         MEXIT
         MEND
