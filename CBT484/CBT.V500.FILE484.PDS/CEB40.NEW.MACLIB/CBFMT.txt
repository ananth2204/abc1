         MACRO
&NAME    CBFMT  &FLD=,&LEN=,&TYPE=,&OFFSET=,&DESC=
         LCLA  &XSET                   OFFSET IN FIELD
         LCLA  &XLEN                   OFFSET IN FIELD
         LCLA  &TLEN                   OFFSET IN FIELD
         LCLA  &SLEN                   OFFSET IN FIELD
         LCLA  &BOFFSET                OFFSET IN FIELD
&TLEN    SETA  &LEN*2+1                TARGET LEN FOR UNPACK
&BOFFSET SETA  &LEN*2+20               OFFSET FOR EXTRA BLANK
&SLEN    SETA  &LEN+1                  SOURCE LEN FOR UNPACK
&XLEN    SETA  K'&OFFSET               LENGTH OF OFFSET ARG
&DESCLEN SETA  K'&DESC-2               LENGTH OF DESC FLD - 2 FOR ''
         AIF   ('&LEN' GT '8').ERRLEN
         AIF   ('&FLD' EQ '').ERRMISS
         AIF   (K'&DESC  GT 95).ERRDESC
         AIF   (K'&OFFSET EQ 4).LEN4
         AIF   (K'&OFFSET EQ 3).LEN3
         AIF   (K'&OFFSET EQ 2).LEN2
&XSET    SETA  4
         AGO   .CODE
.LEN2    ANOP
&XSET    SETA  3
         AGO   .CODE
.LEN3    ANOP
&XSET    SETA  2
         AGO   .CODE
.LEN4    ANOP
&XSET    SETA  1
.*
.CODE    ANOP
         MVI   0(2),C' '             CLEAR PRINT LINE
         MVC   1(132,2),0(2)
         MVC   &XSET.(&XLEN,2),=C'&OFFSET'    OFFSET
         MVC   7(1,2),=C'&LEN'                LENGTH
         MVC   10(8,2),=CL8'&FLD'             FLD NAME
         MVC   37(&DESCLEN,2),=C&DESC         DESCRIPTION
         AIF   ('&TYPE' EQ 'CHARACTER').CHAR
         AIF   ('&LEN' EQ '8').TWOPRT
         UNPK  20(&TLEN,2),&FLD.(&SLEN)
         TR    20(&TLEN,2),HEXTAB-240         VALUE
         MVI   &BOFFSET.(2),C' '
         AGO   .NEXT
.TWOPRT  UNPK  20(9,2),&FLD.(5)
         TR    20(9,2),HEXTAB-240
         UNPK  28(9,2),&FLD.+4(5)
         TR    28(9,2),HEXTAB-240
         MVI   36(2),C' '
         AGO   .NEXT
.CHAR    MVC   20(&LEN,2),&FLD       EBCDIC SO NO CONVERSION
.NEXT    LA    2,133(,2)             INCREMENT TO NEXT SLOT IN TABLE
         LA    3,1(,3)               ADD 1 TO LINE CNTR
         MEXIT
.ERRLEN  MNOTE 12,'LEN CANNOT BE GREATER THAN 8'
         MEXIT
.ERRMISS MNOTE 12,'FLD NAME IS MISSING'
         MEXIT
.ERRDESC MNOTE 12,'DESCRIPTION IS TOO LONG. MAX 95'
         MEXIT
         MEND
