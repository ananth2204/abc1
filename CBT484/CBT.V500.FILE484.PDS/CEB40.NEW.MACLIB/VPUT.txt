         MACRO
&NAME    VPUT     &NMLST,&POOL
.*DIALOG MANAGER MACRO -- DIALOG MANAGEMENT SERVICES (SC34-4021)
.*------------------------------------------------------------------
.* DIALOG MANAGER SERVICE: VPUT        COPY VAR FROM FUNCTION POOL
.*   FORMAT:   VPUT NAMELIST,POOL              TO SHARED OR PROFILE
.*
.* (REQUIRED)  NAMELIST - LIST OF VARIABLE NAMES TO COPY.
.* (OPTIONAL)  POOL     - ASIS (DEFAULT), SHARED OR PROFILE
.*------------------------------------------------------------------
         LCLC  &VPUTLST
         LCLC  &VPOOL
         LCLC  &BRANCH                 BRANCH LABEL
         LCLC  &VPUTPRM
&VPUTPRM SETC  'VGTP'.'&SYSNDX'        VARIABLE NAME FOR PARMLIST
&BRANCH  SETC  'VGTB'.'&SYSNDX'        VARIABLE NAME FOR NAMELIST
&VPUTLST SETC  'VPUT'.'&SYSNDX'        VARIABLE NAME FOR NAMELIST
&VPOOL   SETC  'ASIS'
.*
         AIF   ('&NMLST' EQ '').ERR
         AIF   (K'&POOL EQ 0).SKPPOOL
&VPOOL   SETC  '&POOL'
.SKPPOOL ANOP
&NAME    B     &BRANCH
&VPUTLST DC    C'&NMLST'             LIST OF VARIABLES TO COPY
         CNOP  0,4
&VPUTPRM DC    A(VPUT)
         DC    A(&VPUTLST)
         DC    A(&VPOOL+X'80000000')
&BRANCH  EQU   *
         LA    R1,&VPUTPRM
         L     R15,SAVEISP
         BALR  R14,R15
         MEXIT
.ERR     MNOTE 12,'NAMELIST IS A REQUIRED PARM'
         MEXIT
         MEND
