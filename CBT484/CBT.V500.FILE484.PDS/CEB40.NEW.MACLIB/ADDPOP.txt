         MACRO
&NAME    ADDPOP &FLDNAM,&ROW,&COLUMN
.*DIALOG MANAGER MACRO -- DIALOG MANAGEMENT SERVICES (SC34-4021)
.*------------------------------------------------------------------
.* DIALOG MANAGER SERVICE: ADDPOP   START POPUP WINDOW MODE
.*
.* (OPTIONAL)  FLDNAM   - POSITION POPUP WINDOW RELATIVE TO FLDNAM
.* (OPTIONAL)  ROW      - ADJUST FLD SPECIFIC ROW BY SPECIFIED AMOUNT
.* (OPTIONAL)  COLUMN   - ADJUST FLD SPECIFIC COL BY SPECIFIED AMOUNT
.*--------------------------------------------------------------------
         LCLC  &ADDV
         LCLC  &FLDV
         LCLC  &ROWV
         LCLC  &COLV
         LCLC  &BRANCH
         LCLC  &ADDPPRM
&BRANCH  SETC  'ADP'.'&SYSNDX'
&ADDPPRM SETC  'ADDP'.'&SYSNDX'
&ADDV    SETC  'A(ADDPOP)'
&FLDV    SETC  'A(0)'
&ROWV    SETC  'A(0)'
&COLV    SETC  'A(0)'
         AIF   ('&FLDNAM' EQ '').ROWL
&FLDV    SETC  'A(&FLDNAM)'
.ROWL    AIF   ('&ROW' EQ '').COLL
&ROWV    SETC  'A(&ROW)'
.COLL    AIF   ('&COLUMN' EQ '').DONE
&COLV    SETC  'A(&COLUMN)'
.DONE    ANOP
&NAME    B     &BRANCH
         AIF   ('&COLUMN' EQ '').ROW
&COLV    SETC  'A(&COLUMN+X''80000000'')'
         AGO   .FINIS
.ROW     AIF   ('&ROW' EQ '').FLD
&ROWV    SETC  'A(&ROW+X''80000000'')'
         AGO   .FINIS
.FLD     AIF   ('&FLDNAM' EQ '').ADDPOP
&FLDV    SETC  'A(&FLDNAM+X''80000000'')'
         AGO   .FINIS
.ADDPOP  ANOP
&ADDV    SETC  'A(ADDPOP+X''80000000'')'
.FINIS   ANOP
         CNOP  0,4
&ADDPPRM DC    &ADDV
         DC    &FLDV
         DC    &ROWV
         DC    &COLV
&BRANCH  EQU   *
         LA    R1,&ADDPPRM
         L     R15,SAVEISP
         BALR  R14,R15
         MEXIT
         MEND
