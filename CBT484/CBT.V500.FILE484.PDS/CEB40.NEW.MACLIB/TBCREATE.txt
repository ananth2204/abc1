         MACRO
&NAME    TBCREATE &TABNM,&KEYLST,&NAMELST,&WRITE,&REPLACE,&LIB,&SHARE
.*DIALOG MANAGER MACRO -- DIALOG MANAGEMENT SERVICES (SC34-4021)
.*------------------------------------------------------------------
.* DIALOG MANAGER SERVICE: TBCREATE      CREATE A NEW TABLE
.*   FORMAT:   TBCREATE   TABLENAME,KEYS,NAMES,WRITE,REPL,LIB,SHARE
.*
.* (REQUIRED)  TABLENAME- NAME OF TABLE TO BE CREATED.
.* (OPTIONAL)  KEYS     - LIST OF VARIABLES TO BE USED AS KEYS TO
.*                        ACCESS THE TABLE
.* (OPTIONAL)  NAMES    - LIST OF NON-KEY VARIABLES IN EACH ROW OF
.*                        THE TABLE
.* (OPTIONAL)  WRITE    - WRITE SPECIFIES THAT THE TABLE IS PERMANENT
.*                        NOWRITE SPECIFIES THE TABLE IS TEMPORARY
.* (OPTIONAL)  REPL     - REPLACE SPECIFIES THAT AN EXISTING TABLE IS
.*                        TO BE REPLACED IN STORAGE.
.* (OPTIONAL)  LIB      - SPECIFIES AN OPTIONAL INPUT LIBRARY OTHER
.*                        THAN ISPTLIB
.* (OPTIONAL)  SHARE    - SPECIFIES THE CREATED TABLE CAN BE SHARED
.*                        BETWEEN ALL LOGICAL SCREENS DURING SPLTSCRN
.*--------------------------------------------------------------------
         LCLC  &TBKYLST
         LCLC  &TBNMLST
         LCLC  &TBLIB
         LCLC  &TBCRPRM
         LCLC  &BRANCH
         LCLC  &KEYL
         LCLC  &NAMEL
         LCLC  &WRITEV
         LCLC  &REPLV
         LCLC  &LIBV
&TBCRPRM SETC  'TBCR'.'&SYSNDX'        VARIABLE NAME FOR PARMLIST
&BRANCH  SETC  'TBCB'.'&SYSNDX'        VARIABLE NAME FOR BRANCH
&KEYL    SETC  'A(0)'
&NAMEL   SETC  'A(0)'
&WRITEV  SETC  'A(0)'
&REPLV   SETC  'A(0)'
&LIBV    SETC  'A(0)'
&TBKYLST SETC  'TBKY'.'&SYSNDX'        VARIABLE NAME FOR KEYLST
&TBNMLST SETC  'TBNK'.'&SYSNDX'        VARIABLE NAME FOR NAMELST
&TBLIB   SETC  'TBLB'.'&SYSNDX'        VARIABLE NAME FOR LIBRARY
         AIF   ('&TABNM' EQ '').ERR
&NAME    B     &BRANCH
&TABNM   DC    CL8'&TABNM'              TABLE NAME
         AIF   ('&KEYLST' EQ '').NAMEL
&KEYL    SETC  'A(&TBKYLST)'
&TBKYLST DC    C'&KEYLST'               LIST OF KEY VARIABLES
.NAMEL   AIF   ('&NAMELST' EQ '').LIBA
&NAMEL   SETC  'A(&TBNMLST)'
&TBNMLST DC    C'&NAMELST'              LIST OF NON-KEY VARIABLES
.LIBA    AIF   ('&LIB' EQ '').WRITEA
&LIBV    SETC  'A(&TBLIB)'
&TBLIB   DC    CL8'&LIB'                LIBRARY OTHER THAN ISPTLIB
.WRITEA  AIF   ('&WRITE' EQ '').REPL
&WRITEV  SETC  'A(&WRITE)'
.REPL    AIF   ('&WRITE' EQ '').DONE
&REPLV   SETC  'A(&REPLACE)'
.DONE    CNOP  0,4
&TBCRPRM DC    A(TBCREATE)
         DC    A(&TABNM)
.SHARE   AIF   ('&SHARE' EQ '').LIB
         DC    &KEYL
         DC    &NAMEL
         DC    &WRITEV
         DC    &REPLV
         DC    &LIBV
         DC    A(&SHARE+X'80000000')
         AGO   .FINIS
.LIB     AIF   ('&LIB' EQ '').REPLACE
         DC    &KEYL
         DC    &NAMEL
         DC    &WRITEV
         DC    &REPLV
         DC    A(&TBLIB+X'80000000')
         AGO   .FINIS
.REPLACE AIF   ('&REPLACE' EQ '').WRITE
         DC    &KEYL
         DC    &NAMEL
         DC    &WRITEV
         DC    A(&REPLACE+X'80000000')
         AGO   .FINIS
.WRITE   AIF   ('&WRITE' EQ '').NAMELST
         DC    &KEYL
         DC    &NAMEL
         DC    A(&WRITE+X'80000000')
         AGO   .FINIS
.NAMELST AIF   ('&NAMELST' EQ '').KEYLST
         DC    &KEYL
         DC    A(&TBNMLST+X'80000000')
         AGO   .FINIS
.KEYLST  AIF   ('&KEYLST' EQ '').FINIS
         DC    A(&TBKYLST+X'80000000')
.FINIS   ANOP
&BRANCH  EQU   *
         LA    R1,&TBCRPRM
         AIF   (D'SAVEISP).DEFINED
         L     R15,=V(SAVEISP)         ADDRESS OF ISPLINK
         AGO   .CALL
.DEFINED ANOP
         L     R15,SAVEISP             ADDRESS OF ISPLINK
.CALL    ANOP
         BALR  R14,R15
         MEXIT
.ERR     MNOTE 12,'TABLENAME IS A REQUIRED PARM'
         MEXIT
         MEND
