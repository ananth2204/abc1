         MACRO
&NAME    VCOPY    &NMLST,&LENGTH,&VARLST,&MODE,&DEFINE=,&LABEL=
.*DIALOG MANAGER MACRO -- DIALOG MANAGEMENT SERVICES (SC34-4021)
.*------------------------------------------------------------------
.* DIALOG MANAGER SERVICE: VCOPY
.*   FORMAT:   VCOPY  NAMELIST,LENGTH,VARIABLE,MODE
.*
.* (REQUIRED)  NAMELIST - LIST OF VARIABLE NAMES TO COPY.
.* (REQUIRED)  LENGTH   - LENGTH OF VARIABLE TO COPY.
.* (OPTIONAL)  VARIABLE - AREA OF STORAGE TO COPY INTO IF DIFFERENT
.*                        FROM THE NAME IN NAMELIST
.* (OPTIONAL)  MODE     - MOVE MODE OR LOCATE MODE (LOCATE IS DFLT)
.* (KEYWORD )  DEFINE   - DEFINE THE VARIABLE (Y OR N)  DEFAULT=N
.* (KEYWORD )  LABEL    - LABEL TO USE FOR LEN OF COPIED VARIABLE
.*  EXAMPLE    VCOPY  (ZTDSELS),4,,MOVE
.*             WILL MOVE A COPY OF VARIABLE ZTDSELS TO PROGRAM
.*             AREA OF NAME ZTDSELS AND LENGTH 4
.*--------------------------------------------------------------------
         LCLA  &NMLEN                  NMLST  LENGTH
         LCLC  &VCPYLEN
         LCLC  &VCPYPRM
         LCLC  &VARNME
         LCLC  &MODETYP
         LCLC  &BRANCH
         LCLC  &DEFVAR
&VCPYPRM SETC  'VCPR'.'&SYSNDX'        VARIABLE NAME FOR LENGTH
&VCPYLEN SETC  'VCLN'.'&SYSNDX'        VARIABLE NAME FOR LENGTH
         AIF   (K'&LABEL EQ 0).NOLBL
&VCPYLEN SETC  '&LABEL'
.NOLBL   ANOP
&VCPYLST SETC  'VCNM'.'&SYSNDX'        VARIABLE NAME FOR NAMLST
&BRANCH  SETC  'VCBR'.'&SYSNDX'        VARIABLE NAME FOR BRANCH
&VARNME  SETC  '&VARLST'               INITIALIZE THE VARIABLE NAME
         AIF   ('&NMLST' EQ '').ERR1
         AIF   ('&LENGTH' EQ '').ERR2
&DEFVAR  SETC  'YES'
         AIF   (K'&DEFINE GT 0).GOVAR
&DEFVAR  SETC  'NO'                    DEFAULT FOR DEFINE THE VAR
.GOVAR   AIF   ('&VARLST' NE '').MODE  IF VARLST NOT SUPPLIED
&VARNME  SETC  '&NMLST'                THEN SET IT TO NMLST
.MODE    AIF   ('&MODE' EQ 'MOVE').MOVE
&MODETYP SETC  'LOCATE'
         AGO   .CODE
.MOVE    ANOP
&MODETYP SETC  'MOVE'
.*
.CODE    ANOP
&NAME    CNOP  0,4
         MVC   &VCPYLEN,=F'&LENGTH'
         B     &BRANCH
&VCPYLST DC    C'(&NMLST)'
&VCPYLEN DC    F'&LENGTH'               LENGTH OF VARIABLE
         AIF   ('&DEFVAR'(1,1) NE 'Y').NODEF
&NMLST   DS    CL&LENGTH
.NODEF   CNOP  0,4
&VCPYPRM DC    A(VCOPY)
         DC    A(&VCPYLST)
         DC    A(&VCPYLEN)
         DC    A(&VARNME)
         DC    A(&MODETYP+X'80000000')
&BRANCH  EQU   *
         LA    R1,&VCPYPRM
         L     R15,SAVEISP
         BALR  R14,R15
         MEXIT
.ERR1    MNOTE 12,'NAMELIST  IS A REQUIRED PARM'
         MEXIT
.ERR2    MNOTE 12,'LENGTH IS A REQUIRED PARM'
         MEXIT
         MEND
