         MACRO
&NAME    TBADD &TBNAM,&EXTVARS,&ORDER
.*DIALOG MANAGER MACRO -- DIALOG MANAGEMENT SERVICES (SC34-4021)
.*------------------------------------------------------------------
.* DIALOG MANAGER SERVICE: TBADD         ADD A ROW TO TABLE
.*   FORMAT:   TBADD      TABLENAME,NAMES,ORDER
.*
.* (REQUIRED)  TABLENAME- NAME OF TABLE TO BE CREATED.
.* (OPTIONAL)  NAMES    - LIST OF EXTENSION VARIABLES TO BE SAVED IN
.*                        ADDITION TO TABLE VARIABLES
.* (OPTIONAL)  ORDER    - NEW ROW TO BE ADDED TO THE TABLE IN ORDER
.*                        SPECIFIED IN SORT INFO RECORD. A TBSORT MUST
.*                        HAVE BEEN PERFORMED PRIOR TO USING THIS
.*                        KEYWORD
.*--------------------------------------------------------------------
         LCLC  &TBEXLST
         LCLC  &TBADPRM
         LCLC  &BRANCH
&TBEXLST SETC  'TBEX'.'&SYSNDX'        VARIABLE NAME FOR EXTVARS
&TBADPRM SETC  'TBAP'.'&SYSNDX'        VARIABLE NAME FOR PARMLIST
&BRANCH  SETC  'TBAD'.'&SYSNDX'        VARIABLE NAME FOR BRANCH
         AIF   ('&TBNAM' EQ '').ERR
&NAME    CNOP  0,4
         B     &BRANCH
.SKIP1   AIF   ('&EXTVARS' EQ '').SKIP2
&TBEXLST DC    C'&EXTVARS'              LIST OF NON-KEY VARIABLES
.SKIP2   CNOP  0,4
&TBADPRM DC    A(TBADD)
         AIF   ('&ORDER' EQ '').EXT
         DC    A(&TBNAM)
         DC    A(&EXTVARS)
         DC    A(&ORDER+X'80000000')
         AGO   .FINIS
.EXT     AIF   ('&EXTVARS' EQ '').CCALL
         DC    A(&TBNAM)
         DC    A(&EXTVARS+X'80000000')
         AGO   .FINIS
.CCALL   ANOP
         DC    A(&TBNAM+X'80000000')
.FINIS   ANOP
&BRANCH  EQU   *
         LA    R1,&TBADPRM
         AIF   (D'SAVEISP).DEFINED
         L     R15,=V(SAVEISP)         ADDRESS OF ISPLINK
         AGO   .CALL
.DEFINED ANOP
         L     R15,SAVEISP             ADDRESS OF ISPLINK
.CALL    ANOP
         BALR  R14,R15
         MEXIT
.ERR     MNOTE 12,'TABLENAME IS A REQUIRED PARM'
         MEXIT
         MEND
