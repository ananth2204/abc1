         MACRO
&NAME    SETMSG   &MSGID
.*DIALOG MANAGER MACRO -- DIALOG MANAGEMENT SERVICES (SC34-4021)
.*------------------------------------------------------------------
.* DIALOG MANAGER SERVICE: SETMSG       DISPLAY MSG ON NEXT PANEL
.*   FORMAT:   SETMSG MSGID
.*
.* (REQUIRED)  MSGID    - ID OF A MESSAGE IN THE ISPMLIB CONCATN.
.*------------------------------------------------------------------
         LCLC  &SETMPRM                BRANCH LABEL
         LCLC  &BRANCH                 BRANCH LABEL
         LCLC  &MSGLAB
&SETMPRM SETC  'STM'.'&SYSNDX'        VARIABLE NAME FOR PARMLIST
&MSGLAB  SETC  'MSG'.'&SYSNDX'        VARIABLE NAME FOR MSGID
&BRANCH  SETC  'MSGB'.'&SYSNDX'       VARIABLE NAME FOR BRANCH
.*
         AIF   ('&MSGID' EQ '').ERR
&NAME    CNOP  0,4
         AIF   ('&MSGID' NE 'ZERRMSG').SET1
         MVC   &MSGLAB,ZERRMSG
.SET1    B     &BRANCH
&MSGLAB  DC    CL8'&MSGID'             LENGTH OF VARIABLE
         CNOP  0,4
&SETMPRM DC    A(SETMSG)
         DC    A(&MSGLAB+X'80000000')
&BRANCH  EQU   *
         LA    R1,&SETMPRM
         AIF   (D'SAVEISP).DEFINED
         L     R15,=V(SAVEISP)         ADDRESS OF ISPLINK
         AGO   .CALL
.DEFINED ANOP
         L     R15,SAVEISP             ADDRESS OF ISPLINK
.CALL    ANOP
         BALR  R14,R15
         MEXIT
.ERR     MNOTE 12,'MESSAGEID IS A REQUIRED PARM'
         MEND
