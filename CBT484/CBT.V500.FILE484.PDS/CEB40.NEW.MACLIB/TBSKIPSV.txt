         MACRO
&NAME    TBSKIP &TABNM,&NUM=,&SVE=,&ROWID=,&ROW=,&READ=,&POS=
.*DIALOG MANAGER MACRO -- DIALOG MANAGEMENT SERVICES (SC34-4021)
.*------------------------------------------------------------------
.* DIALOG MANAGER SERVICE: TBSKIP   MOVE CRP FORWARD/BACKWARD & RTRVE
.*
.* (REQUIRED)  TABLENAME- NAME OF TABLE TO BE CREATED.
.* (OPTIONAL)  NUMBER   - DIRECTION AND NUMBER OF ROWS TO MOVE + OR -
.* (OPTIONAL)  SVENAME  - NAME OF VARIABLE IN WHICH EXT VARS ARE SAVED
.* (OPTIONAL)  ROWID    - NAME OF VAR IN WHICH THE NUMBER OF ROW BEING
.*                        ACCESSED IS TO BE STORED.
.* (OPTIONAL)  ROW      - NUMERIC VALUE THAT IDENTIFIES ROW TO BE ACCSD
.* (OPTIONAL)  NOREAD   - DON'T READ VARIABLES IN ROW TO VARIABLE POOL
.* (OPTIONAL)  POSITION - NAME OF VARIABLE IN WHICH THE ROW NUMBER
.*                        POINTED TO BY THE CRP IS TO BE STORED.
.*--------------------------------------------------------------------
         LCLC  &BRANCH
         LCLC  &TBSKPRM
         LCLC  &TBSKROW
         LCLC  &NUMV
         LCLC  &SVEV
         LCLC  &ROWIDV
         LCLC  &ROWV
         LCLC  &READV
         LCLC  &TSKNUM
&NUMV    SETC  'A(0)'
&SVEV    SETC  'A(0)'
&ROWIDV  SETC  'A(0)'
&ROWV    SETC  'A(0)'
&CRPV    SETC  'F(0)'
&READV   SETC  'A(0)'
&BRANCH  SETC  'TBSK'.'&SYSNDX'
&TBSKPRM SETC  'TBKI'.'&SYSNDX'
&TBSKROW SETC  'TBKR'.'&SYSNDX'
&TSKNUM  SETC  'NUM'.'&SYSNDX'
         AIF   ('&TABNM' EQ '').ERR
         AIF   ('&NUM' EQ '').NONUM
         MVC   &TSKNUM,&NUM+2
         OI    &TSKNUM,X'80'
.NONUM   AIF   ('&ROW' EQ '').NOROW
         MVC   &TBSKROW,&ROW+2
         OI    &TBSKROW,X'80'
.NOROW   CNOP  0,4
&NAME    B     &BRANCH
.SAVEL   AIF   ('&SVE' EQ '').ROWIDL
&SVEV    SETC  'A(&SVE)'
.ROWIDL  AIF   ('&ROWID' EQ '').ROWL
&ROWIDV  SETC  'A(&ROWID)'
.ROWL    AIF   ('&ROW' EQ '').READL
&ROWV    SETC  'A(&ROW)'
.READL   AIF   ('&READ' EQ '').DONE
&READV   SETC  'A(&READ)'
.DONE    CNOP  0,4
&TBSKPRM DC    A(TBSKIP)
         AIF   ('&POS' EQ '').READ
         DC    A(&TABNM)
&TSKNUM  DC    X'00000000'
         DC    &SVEV
         DC    &ROWIDV
         DC    &ROWV
         DC    &READV
         DC    A(&POS+X'80000000')
         AGO   .FINIS
.READ    AIF   ('&READ' EQ '').ROW
         DC    A(&TABNM)
&TSKNUM  DC    X'00000000'
         DC    &SVEV
         DC    &ROWIDV
         DC    &ROWV
         DC    A(&READ+X'80000000')
         AGO   .FINIS
.ROW     AIF   ('&ROW' EQ '').ROWID
         DC    A(&TABNM)
&TSKNUM  DC    X'00000000'
         DC    &SVEV
         DC    &ROWIDV
&TBSKROW DC    A(&ROW+X'80000000')
         AGO   .FINIS
.ROWID   AIF   ('&ROWID' EQ '').SVE
         DC    A(&TABNM)
&TSKNUM  DC    X'00000000'
         DC    &SVEV
         DC    A(&ROWID+X'80000000')
         AGO   .FINIS
.SVE     AIF   ('&SVE' EQ '').NUM
         DC    A(&TABNM)
&TSKNUM  DC    X'00000000'
         DC    A(&SVE+X'80000000')
         AGO   .FINIS
.NUM     AIF   ('&NUM' EQ '').TABNM
         DC    A(&TABNM)
&TSKNUM  DC    X'80000000'
         AGO   .FINIS
.TABNM   ANOP
         DC    A(&TABNM+X'80000000')
.FINIS   ANOP
&BRANCH  EQU   *
         LA    R1,&TBSKPRM
         L     R15,SAVEISP
         BALR  R14,R15
         MEXIT
.ERR     MNOTE 12,'TABLENAME IS A REQUIRED PARM'
         MEXIT
         MEND
