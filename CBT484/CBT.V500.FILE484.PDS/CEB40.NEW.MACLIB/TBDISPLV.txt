         MACRO
&NAME   TBDISPLV &TABNM,&PANEL,&MESSAGE,&FIELD,&TBROWNM,&CUR,&AUTOSEL, X
               &CRPNAM,&ROWID
.*DIALOG MANAGER MACRO -- DIALOG MANAGEMENT SERVICES (SC34-4021)
.*------------------------------------------------------------------
.* DIALOG MANAGER SERVICE: TBDISPL       DISPLAY TABLE INFORMATION
.*
.* (REQUIRED)  TABLENAME- NAME OF TABLE TO BE CREATED.
.* (OPTIONAL)  PANEL    - NAME OF PANEL TO BE DISPLAYED
.* (OPTIONAL)  MESSAGEID- ID OF MESSAGE TO DISPLAY ON PANEL
.* (OPTIONAL)  FIELD    - NAME OF FIELD WHERE CURSOR IS TO BE PLACED
.* (OPTIONAL)  TBROWNM  - TABLE ROW NUMBER WHERE CURSOR IS TO BE PLACED
.* (OPTIONAL)  CUR      - CURSOR POSITION WITHIN A FIELD
.* (OPTIONAL)  AUTOSEL  - SEE MANUAL
.* (OPTIONAL)  CRPNAM   - NAME OF VARIABLE IN WHICH ROW NUM IS STORED
.* (OPTIONAL)  ROWID    - NAME OF VARIABLE IN WHICH A NUMBER THAT
.*                        UNIQUELY IDENTIFIES  THE ROW BEING ACCESSED
.*                        IS TO BE STORED.
.*--------------------------------------------------------------------
         LCLC  &PANV
         LCLC  &MSGV
         LCLC  &FLDV
         LCLC  &ROWNV
         LCLC  &CURV
         LCLC  &AUTOV
         LCLC  &CRPV
         LCLC  &BRANCH
         LCLC  &TBDSPRM
         LCLC  &PNL
&PANV    SETC  'A(0)'
&MSGV    SETC  'A(0)'
&FLDV    SETC  'A(0)'
&ROWNV   SETC  'A(0)'
&CURV    SETC  'A(0)'
&AUTOV   SETC  'A(0)'
&CRPV    SETC  'A(0)'
&BRANCH  SETC  'TBDS'.'&SYSNDX'
&TBDSPRM SETC  'TBDP'.'&SYSNDX'
&PNL     SETC  'TDSP'.'&SYSNDX'
         AIF   ('&TABNM' EQ '').ERR
         AIF   ('&PANEL' EQ '').MSGL
&PANV    SETC  'A(&PNL)'
.MSGL    AIF   ('&MESSAGE' EQ '').FLDL
&MSGV    SETC  'A(&MESSAGE)'
.FLDL    AIF   ('&FIELD' EQ '').ROWNL
&FLDV    SETC  'A(&FIELD)'
.ROWNL   AIF   ('&TBROWNM' EQ '').CURL
&ROWNV   SETC  'A(&TBROWNM)'
.CURL    AIF   ('&CUR' EQ '').AUTOL
&CURV    SETC  'A(&CUR)'
.AUTOL   AIF   ('&AUTOSEL' EQ '').CRPL
&AUTOV   SETC  'A(&AUTOSEL)'
.CRPL    AIF   ('&CRPNAM' EQ '').DONE
&CRPV    SETC  'A(&CRPNAM)'
.DONE    ANOP
&NAME    B     &BRANCH
         AIF   ('&PANEL' EQ '').NOPAN
&PNL     DC    CL8'&PANEL'
.NOPAN   CNOP  0,4
&TBDSPRM DC    A(TBDISPL)
.ROWID   AIF   ('&ROWID' EQ '').CRP
         DC    A(&TABNM)
         DC    A(&PANEL)
         DC    &MSGV
         DC    &FLDV
         DC    &ROWNV
         DC    &CURV
         DC    &AUTOV
         DC    &CRPV
         DC    A(&ROWID+X'80000000')
         AGO   .FINIS
.CRP     AIF   ('&CRPNAM' EQ '').AUTOSEL
         DC    A(&TABNM)
         DC    A(&PANEL)
         DC    &MSGV
         DC    &FLDV
         DC    &ROWNV
         DC    &CURV
         DC    &AUTOV
         DC    A(&CRPNAM+X'80000000')
         AGO   .FINIS
.AUTOSEL AIF   ('&AUTOSEL' EQ '').CUR
         DC    A(&TABNM)
         DC    A(&PANEL)
         DC    &MSGV
         DC    &FLDV
         DC    &ROWNV
         DC    &CURV
         DC    A(&AUTOSEL+X'80000000')
         AGO   .FINIS
.CUR     AIF   ('&CUR' EQ '').TBROW
         DC    A(&TABNM)
         DC    A(&PANEL)
         DC    &MSGV
         DC    &FLDV
         DC    &ROWNV
         DC    A(&CUR+X'80000000')
         AGO   .FINIS
.TBROW   AIF   ('&TBROWNM' EQ '').FIELD
         DC    A(&TABNM)
         DC    A(&PANEL)
         DC    &MSGV
         DC    &FLDV
         DC    A(&TBROWNM+X'80000000')
         AGO   .FINIS
.FIELD   AIF   ('&FIELD' EQ '').MSG
         DC    A(&TABNM)
         DC    A(&PANEL)
         DC    &MSGV
         DC    A(&FIELD+X'80000000')
         AGO   .FINIS
.MSG     AIF   ('&MESSAGE' EQ '').PNL
         DC    A(&TABNM)
         DC    A(&PANEL)
         DC    A(&MESSAGE+X'80000000')
         AGO   .FINIS
.PNL     AIF   ('&PANEL' EQ '').CCALL
         DC    A(&TABNM)
         DC    A(&PANEL+X'80000000')
         AGO   .FINIS
.CCALL   ANOP
         DC    A(&TABNM+X'80000000')
.FINIS   ANOP
&BRANCH  EQU   *
         LA    R1,&TBDSPRM
         L     R15,SAVEISP
         BALR  R14,R15
         MEXIT
.ERR     MNOTE 12,'TABLENAME IS A REQUIRED PARM'
         MEXIT
         MEND
