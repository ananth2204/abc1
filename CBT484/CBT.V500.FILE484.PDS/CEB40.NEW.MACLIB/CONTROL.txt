         MACRO
&NAME    CONTROL &OPRND1,&OPRND2,&OPRND3
.*DIALOG MANAGER MACRO -- DIALOG MANAGEMENT SERVICES (SC34-4021)
.*------------------------------------------------------------------
.* DIALOG MANAGER SERVICE: CONTROL       DEFINES PROCESSING OPTIONS
.*   FORMAT:   CONTROL    OPERAND1,OPERAND2,OPERAND3
.*
.* (REQUIRED)  OPERAND1 - PROCESS TO DEFINE.
.* (OPTIONAL)  OPERAND2 - PARM OF OPERAND 1.
.* (OPTIONAL)  OPERAND3 - PARM OF OPERAND 1.
.*--------------------------------------------------------------------
         LCLC  &BRANCH
         LCLC  &CONPRM
&BRANCH  SETC  'CON'.'&SYSNDX'
&CONPRM  SETC  'CONP'.'&SYSNDX'
         AIF   ('&OPRND1' EQ '').ERR
&NAME    CNOP  0,4
         B     &BRANCH
&CONPRM  DC    A(CONTROL)
         AIF   ('&OPRND3' EQ '').OP2
         DC    A(&OPRND1)
         DC    A(&OPRND2)
         DC    A(&OPRND3+X'80000000')
         AGO   .FINIS
.OP2     AIF   ('&OPRND2' EQ '').OP1
         DC    A(&OPRND1)
         DC    A(&OPRND2+X'80000000')
         AGO   .FINIS
.OP1     DC    A(&OPRND1+X'80000000')
.FINIS   ANOP
&BRANCH  EQU   *
         LA    R1,&CONPRM
         L     R15,SAVEISP
         BALR  R14,R15
         MEXIT
.ERR     IHBERMAC 24
         MEXIT
         MEND
