         MACRO
&NAME    TBPUT &TABNM,&SVENAM,&ORDER
.*DIALOG MANAGER MACRO -- DIALOG MANAGEMENT SERVICES (SC34-4021)
.*------------------------------------------------------------------
.* DIALOG MANAGER SERVICE: TBPUT    CONDITIONAL UPDATE OF A ROW
.*
.* (REQUIRED)  TABLENAME- NAME OF TABLE TO BE UPDATED.
.* (OPTIONAL)  SVENAME  - LIST OF EXTENSION VARS TO BE SAVED IN ROW
.* (OPTIONAL)  ORDER    - UPDATED ROW TO BE PLACED AT POINT THAT
.*                        PRESERVES INFO IN THE SORT INFO RECORD.
.*                        A TBSORT MUST HAVE BEEN PERFORMED FOR THIS
.*                        TABLE PRIOR TO THE USE OF THIS KEYWORD.
.*--------------------------------------------------------------------
         LCLC  &SVEV
         LCLC  &BRANCH
         LCLC  &TBPTPRM
&SVEV    SETC  'A(0)'
&BRANCH  SETC  'TBPT'.'&SYSNDX'
&TBPTPRM SETC  'TBPP'.'&SYSNDX'
         AIF   ('&TABNM' EQ '').ERR
         AIF   ('&SVENAM' EQ '').DONE
&SVEV    SETC  'A(&SVENAM)'
.DONE    ANOP
&NAME    B     &BRANCH
         CNOP  0,4
&TBPTPRM DC    A(TBPUT)
         AIF   ('&ORDER' EQ '').SVENAM
         DC    A(&TABNM)
         DC    &SVEV
         DC    A(&ORDER+X'80000000')
         AGO   .FINIS
.SVENAM  AIF   ('&SVENAM' EQ '').CCALL
         DC    A(&TABNM)
         DC    A(&SVENAM+X'80000000')
         AGO   .FINIS
.CCALL   ANOP
         DC    A(&TABNM+X'80000000')
.FINIS   ANOP
&BRANCH  EQU   *
         LA    R1,&TBPTPRM
         L     R15,SAVEISP
         BALR  R14,R15
         MEXIT
.ERR     MNOTE 12,'TABLENAME IS A REQUIRED PARM'
         MEXIT
         MEND
