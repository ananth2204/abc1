         MACRO
&NAME    TBGET &TABNM,&SVENAM,&ROWID,&NOREAD,&POSITION
.*DIALOG MANAGER MACRO -- DIALOG MANAGEMENT SERVICES (SC34-4021)
.*------------------------------------------------------------------
.* DIALOG MANAGER SERVICE: TBGET    ACCESS A ROW IN THE TABLE
.*
.* (REQUIRED)  TABLENAME- NAME OF TABLE TO BE CREATED.
.* (OPTIONAL)  SVENAME  - NAME OF VARIABLE IN WHICH EXT VARS ARE SAVED
.* (OPTIONAL)  ROWID    - NAME OF VAR IN WHICH THE NUMBER OF ROW BEING
.*                        ACCESSED IS TO BE STORED.
.* (OPTIONAL)  NOREAD   - DON'T READ VARIABLES IN ROW TO VARIABLE POOL
.* (OPTIONAL)  POSITION - NAME OF VARIABLE IN WHICH THE ROW NUMBER
.*                        POINTED TO BY THE CRP IS TO BE STORED.
.*--------------------------------------------------------------------
         LCLC  &SVEV
         LCLC  &ROWV
         LCLC  &NORV
         LCLC  &BRANCH
         LCLC  &TBGTPRM
&SVEV    SETC  'A(0)'
&ROWV    SETC  'A(0)'
&NORV    SETC  'A(0)'
&BRANCH  SETC  'TBGT'.'&SYSNDX'
&TBGTPRM SETC  'TBGP'.'&SYSNDX'
         AIF   ('&TABNM' EQ '').ERR
         AIF   ('&SVENAM' EQ '').ROWL
&SVEV    SETC  'A(&SVENAM)'
.ROWL    AIF   ('&ROWID' EQ '').NORL
&ROWV    SETC  'A(&ROWID)'
.NORL    AIF   ('&NOREAD' EQ '').DONE
&NORV    SETC  'A(&NOREAD)'
.DONE    ANOP
&NAME    B     &BRANCH
         CNOP  0,4
&TBGTPRM DC    A(TBGET)
         AIF   ('&POSITION' EQ '').NORD
         DC    A(&TABNM)
         DC    &SVEV
         DC    &ROWV
         DC    &NORV
         DC    A(&POSITION+X'80000000')
         AGO   .FINIS
.NORD    AIF   ('&NOREAD' EQ '').ROWID
         DC    A(&TABNM)
         DC    &SVEV
         DC    &ROWV
         DC    A(&NOREAD+X'80000000')
         AGO   .FINIS
.ROWID   AIF   ('&ROWID' EQ '').SVENAM
         DC    A(&TABNM)
         DC    &SVEV
         DC    A(&ROWID+X'80000000')
         AGO   .FINIS
.SVENAM  AIF   ('&SVENAM' EQ '').CCALL
         DC    A(&TABNM)
         DC    A(&SVENAM+X'80000000')
         AGO   .FINIS
.CCALL   ANOP
         DC    A(&TABNM+X'80000000')
.FINIS   ANOP
&BRANCH  EQU   *
         LA    R1,&TBGTPRM
         L     R15,SAVEISP
         BALR  R14,R15
         MEXIT
.ERR     MNOTE 12,'TABLENAME IS A REQUIRED PARM'
         MEXIT
         MEND
