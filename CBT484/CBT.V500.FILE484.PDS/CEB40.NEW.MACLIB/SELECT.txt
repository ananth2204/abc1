         MACRO
&NAME    SELECT   &OP1,&OP2,&OP3,&OP4,&OP5,&OP6,&OP7,&OP8
.*DIALOG MANAGER MACRO -- DIALOG MANAGEMENT SERVICES (SC34-4021)
.*------------------------------------------------------------------
.* DIALOG MANAGER SERVICE: SELECT
.*   FORMAT:   SELECT PANEL(XXX)
.*
.* (REQUIRED)  AT LEAST ONE PARM. (MAX OF EIGHT).
.*--------------------------------------------------------------------
         LCLA  &NUMOPS                 NUMBER OF PARMS SUPPLIED
         LCLA  &C                      LOOP COUNTER
         LCLA  &LENGTH                 LENGTH REQUIRED FOR BUFFER
         LCLC  &BRANCH
         LCLC  &SELPRM
         LCLC  &BUFFER
         LCLC  &LBUFFER
         LCLC  &LBUFLEN
&C       SETA  1
&NUMOPS  SETA  N'&SYSLIST
&BRANCH  SETC  'SEL'.'&SYSNDX'        VARIABLE NAME FOR BRANCH
&SELPRM  SETC  'SELP'.'&SYSNDX'       VARIABLE NAME FOR BRANCH
&LBUFFER SETC  'BUFR'.'&SYSNDX'        VARIABLE NAME FOR BUFFER
&LBUFLEN SETC  'BUFL'.'&SYSNDX'       VARIABLE NAME FOR BUFFER
&BUFFER  SETC  '&SYSLIST(1)'          FIRST STRING IN BUFFER
&LENGTH  SETA  K'&SYSLIST(1)+1        LENGTH OF FIRST STR IN BUFFER
&C       SETA  &C+1
.*
.LOOP    AIF   ('&C' GT '&NUMOPS').FINIS
&LENGTH  SETA  &LENGTH+K'&SYSLIST(&C)+1    ADD 1 FOR SPACE
&BUFFER  SETC  '&BUFFER &SYSLIST(&C)'    CONCATENATE STRINGS
&C       SETA  &C+1
         AGO   .LOOP
.FINIS   ANOP
&LENGTH  SETA  &LENGTH-1              DECREMENT ONE FOR LAST PARM
&NAME    CNOP  0,4
         B     &BRANCH
&LBUFLEN DC    F'&LENGTH'             LENGTH OF VARIABLE
&LBUFFER DC    C'&BUFFER'
         CNOP  0,4
&SELPRM  DC    A(SELECT)
         DC    A(&LBUFLEN)
         DC    A(&LBUFFER+X'80000000')
&BRANCH  EQU   *
         LA    R1,&SELPRM
         L     R15,SAVEISP
         BALR  R14,R15
         MEXIT
         MEND
