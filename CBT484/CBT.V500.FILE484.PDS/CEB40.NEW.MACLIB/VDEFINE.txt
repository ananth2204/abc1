         MACRO
&NAME    VDEFINE  &NMLST,&FORMAT,&LEN,&OPTLST,&UDATA
.*DIALOG MANAGER MACRO -- DIALOG MANAGEMENT SERVICES (SC34-4021)
.*------------------------------------------------------------------
.* DIALOG MANAGER SERVICE: VDEFINE
.*   FORMAT:   VDEFINE NAMELIST,FORMAT,LENGTH,OPTIONS,USERDATA
.*
.* (REQUIRED)  NAMELIST - LIST OF VARIABLE NAMES TO DEFINE.
.*                        MUST BE ENCLOSED IN () AND SEPARATED BY ','
.*                        IF MULTIPLE VARIABLES ARE DEFINED THEY
.*                        MUST ALL BE THE SAME FORMAT AND LENGTH.
.* (REQUIRED)  FORMAT   - VALID FMTS ARE: CHAR, BIT, FIXED, HEX, PACK
.* (REQUIRED)  LENGTH   - LENGTH OF VARIABLE
.* (OPTIONAL)  OPTIONS  - SPECIFIES INITIALZTN OF DEFINED STORAGE
.*                        AND/OR RETENTION OF TRAILING BLANKS IN
.*                        VARIABLE STORAGE
.* (OPTIONAL)  USERDATA - NOT CURRENTLY HANDLED
.*------------------------------------------------------------------
         LCLA  &C                      COUNTER FOR ITEMS IN SUBLIST
         LCLA  &MAX                    # OF ITEMS + 1 (LOOP PRCSSNG)
         LCLC  &BRANCH                 BRANCH LABEL
         LCLC  &T                      DS TYPE
         LCLC  &VDEFPRM
         LCLC  &VARNAME
         LCLC  &VDEFFMT
         LCLC  &VDEFLEN
         LCLC  &VDEFLST
&VDEFPRM SETC  'VDF'.'&SYSNDX'         VARIABLE NAME FOR PARMLIST
&VDEFFMT SETC  'VFM'.'&SYSNDX'         VARIABLE NAME FOR FORMAT
&VDEFLEN SETC  'VDLN'.'&SYSNDX'        VARIABLE NAME FOR LENGTH
&VDEFLST SETC  'VDNM'.'&SYSNDX'        VARIABLE NAME FOR NAMELIST
&BRANCH  SETC  'VDBR'.'&SYSNDX'        VARIABLE NAME FOR BRANCH LABEL
.*
&C       SETA  1
&MAX     SETA  N'&SYSLIST(1)+1         # OF ENTRIES IN SUBLIST + 1
         AIF   ('&NMLST' EQ '').ERR1
         AIF   ('&FORMAT' EQ '').ERR2
         AIF   ('&LEN' EQ '').ERR3
.TYPEP   AIF   ('&FORMAT'(1,4) NE 'PACK').TYPEC
&T       SETC  'PL&LEN'
         AGO   .CODE
.TYPEC   AIF   ('&FORMAT' NE 'CHAR').TYPEH
&T       SETC  'CL&LEN'
         AGO   .CODE
.TYPEH   AIF   ('&FORMAT' NE 'HEX').TYPEFL
&T       SETC  'XL&LEN'
         AGO   .CODE
.TYPEFL  AIF   ('&FORMAT' NE 'FLOAT').TYPEF
&T       SETC  'XL&LEN'
         AGO   .CODE
.TYPEF   AIF   ('&FORMAT' NE 'FIXED').TYPEB
&T       SETC  'XL&LEN'
         AGO   .CODE
.TYPEB   AIF   ('&FORMAT' NE 'BIT').ERRTYPE
&T       SETC  'BL&LEN'
.CODE    ANOP
&NAME    B     &BRANCH
&VDEFLST DC    C'&NMLST'               LIST OF VARIABLES TO DEFINE
&VDEFLEN DC    F'&LEN'                 LENGTH OF VARIABLE
&VDEFFMT DC    CL8'&FORMAT'            FORMATTING TYPE
.DCLOOP  ANOP
&VARNAME SETC  '&NMLST(&C)'            VARIABLE NAME
&VARNAME DS    &T                      VARIABLE STORAGE
&C       SETA  &C+1
         AIF   ('&MAX' GT '&C').DCLOOP
         CNOP  0,4
&VDEFPRM DC    A(VDEFINE)
         DC    A(&VDEFLST)
         DC    A(&NMLST(1))
         DC    A(&VDEFFMT)
         DC    A(&VDEFLEN+X'80000000')
&BRANCH  EQU   *
         LA    R1,&VDEFPRM             ADDRESS OF PARM LIST
         AIF   (D'SAVEISP).DEFINED
         L     R15,=V(SAVEISP)         ADDRESS OF ISPLINK
         AGO   .CALL
.DEFINED ANOP
         L     R15,SAVEISP             ADDRESS OF ISPLINK
.CALL    ANOP
         BALR  R14,R15
         MEXIT
.ERRTYPE MNOTE 12,'FORMAT MUST BE ''CHAR HEX BIT OR FIXED'''
         MEXIT
.ERR1    MNOTE 12,'NAMELIST IS A REQUIRED PARM'
         MEXIT
.ERR2    MNOTE 12,'FORMAT IS A REQUIRED PARM'
         MEXIT
.ERR3    MNOTE 12,'LENGTH IS A REQUIRED PARM'
         MEXIT
         MEND
