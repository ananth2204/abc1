         MACRO
&NAME    VREPLACE &NMLST,&LENGTHS,&VALUES
.*DIALOG MANAGER MACRO -- DIALOG MANAGEMENT SERVICES (SC34-4021)
.*------------------------------------------------------------------
.* DIALOG MANAGER SERVICE: VREPLACE
.*   FORMAT:   VREPLACE NAMELIST,LENGTH,VALUE
.*
.* (REQUIRED)  NAMELIST - LIST OF VARIABLE NAMES TO DEFINE.
.*                        MUST BE ENCLOSED IN () AND SEPARATED BY ,
.* (REQUIRED)  LENGTH   - LENGTH OF VARIABLE IN () SEPARATED BY ,
.* (REQUIRED)  VALUES   - DATA IN () SEPARATED BY ,
.*------------------------------------------------------------------
         LCLA  &C                      COUNTER FOR ITEMS IN SUBLIST
         LCLA  &MAX                    # OF ITEMS + 1 (LOOP PRCSSNG)
         LCLC  &BRANCH                 BRANCH LABEL
         LCLC  &VREPLEN
         LCLC  &VREPLST
         LCLC  &VREPPRM
&VREPPRM SETC  'VRPM'.'&SYSNDX'        VARIABLE NAME FOR PARMLIST
&VREPLEN SETC  'VRLN'.'&SYSNDX'        VARIABLE NAME FOR LENGTH
&VREPLST SETC  'VRNM'.'&SYSNDX'        VARIABLE NAME FOR NAMELIST
&BRANCH  SETC  'VRBR'.'&SYSNDX'        VARIABLE NAME FOR BRANCH
.*
&C       SETA  1
&MAX     SETA  N'&SYSLIST(1)+1         # OF ENTRIES IN SUBLIST + 1
         AIF   ('&NMLST' EQ '').ERR1
         AIF   ('&LENGTHS' EQ '').ERR2
         AIF   ('&VALUES' EQ '').ERR3
&NAME    B     &BRANCH
&VREPLST DC    C'&NMLST'               LIST OF VARIABLES TO DEFINE
&VREPLEN DC    F'&LENGTHS(1)'          LENGTH OF VARIABLE
         AGO   .INC1
.DCLEN   ANOP
         DC    F'&LENGTHS(&C)'         LENGTH OF VARIABLE
.INC1    ANOP
&C       SETA  &C+1
         AIF   ('&MAX' GT '&C').DCLEN
&C       SETA  1
         CNOP  0,4
&VREPPRM DC    A(VREPLACE)
         DC    A(&VREPLST)
         DC    A(&VREPLEN)
         DC    A(&VALUES+X'80000000')
&BRANCH  EQU   *
         LA    R1,&VREPPRM
         L     R15,SAVEISP
         BALR  R14,R15
         MEXIT
.ERR1    MNOTE 12,'NAMELIST IS A REQUIRED PARM'
         MEXIT
.ERR2    MNOTE 12,'LENGTH IS A REQUIRED PARM'
         MEXIT
.ERR3    MNOTE 12,'VALUE  IS A REQUIRED PARM'
         MEXIT
         MEND
