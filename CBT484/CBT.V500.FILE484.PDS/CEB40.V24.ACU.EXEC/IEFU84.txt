//CEB40U84 JOB (X),'CHUCK.B',MSGCLASS=Y,CLASS=A
//ASM   EXEC ASMHCL,PARM.C='RENT',
//            PARM.L='LIST,MAP,LET,RENT,REFR'
//C.SYSLIB DD DSN=SYS1.MACLIB,DISP=SHR
//          DD DSN=SYS1.MODGEN,DISP=SHR
//C.SYSIN DD *
         TITLE 'IEFU84 BRANCH ENTERED SMF EXIT'
IEFU84   CSECT
IEFU84  AMODE 31
IEFU84  RMODE ANY
*--------------------------------------------------------------------
*  (C) COPYRIGHT AMDAHL CORPORATION  1995. All Rights Reserved
*    This SMF exit routine only handles type 22(8) records
*    and issues a WTO (branch entered) describing 3990 state changes.
*    All other record types are ignored.
* Entry Points -
*         IEFU84
* Locks -
*  Local lock (enabled for I/O and EXT interrupts)
* Input
*    Register 1 contains the address of a fullword that points to
*    the record that SMF is going to write.
*
* Output
*    Register 15 must contain one of the following return codes:
*    0 - Write the SMF record (keep)
*    4 - Don't write the SMF record (delete)
*
* Installation
*    Assemble and link into SYS1.LPALIB.  Re-IPL with CLPA option.
*    Edit the SYS1.PARMLIB(SMFPRMxx) member and add IEFU84 to the
*    EXITS for SYS and make sure SMF type 22 is being recorded.
* Written by:  Chuck Baumann,   Amdahl   January 1995
*              optional part of Cacheman utility
*--------------------------------------------------------------------
         EJECT
         YREGS
TYPE22   EQU   X'16'               SMF type 22 record
S3990CHG EQU   X'08'               3990 state change indicator
CACHSTAT EQU   B'11100000'         cache status changes
DEVSTAT  EQU   B'11000000'         device cache status bits
PINSTAT  EQU   B'11000000'         pinned data status bits
DFWSTAT  EQU   B'00110000'         DFW cache status bits
DUPSTAT  EQU   B'00000011'         Duplex cache status bits
ACTIVE   EQU   B'00000000'         active
PENDACT  EQU   B'00100000'         pending active
DEACTSUB EQU   B'01000000'         deactivated subsystem
DEACTHST EQU   B'10000000'         deactivated host
PENDING  EQU   B'11000000'         pending
PENDFAIL EQU   B'11100000'         pending off failed
SSDISABL EQU   B'00010000'         subsystem storage disabled maint
IMLDEV   EQU   B'00000010'         IML device is not available
CFWDISAB EQU   B'00000001'         Cache Fast Write is disabled
DEV00    EQU   B'00000000'         active
DEV40    EQU   B'01000000'         not used
DEV80    EQU   B'10000000'         deactivation pending
DEVC0    EQU   B'11000000'         deactivated
DUP00    EQU   B'00000000'         available
DUP01    EQU   B'00000001'         pending
DUP02    EQU   B'00000010'         failed duplex,
DUP03    EQU   B'00000011'         failed duplex,
DUP04    EQU   B'00000100'         secondary of duplex pair
DUP08    EQU   B'00001000'         primary of duplex pair
DFW00    EQU   B'00000000'         active
DFW10    EQU   B'00010000'         not used
DFW20    EQU   B'00100000'         deactivation pending
DFW30    EQU   B'00110000'         deactivated
PIN00    EQU   B'00000000'         no pinned data exists
PIN40    EQU   B'01000000'         pinned data exists for suspended
PIN80    EQU   B'10000000'         reserved bit
PINC0    EQU   B'11000000'         pinned data exists for suspended
NVS00    EQU   B'00000000'         NVS is active
NVS40    EQU   B'01000000'         NVS deactivated subsystem
NVS80    EQU   B'10000000'         NVS deactivated host
NVSC0    EQU   B'11000000'         NVS is pending
NVSSTAT  EQU   B'11000000'         NVS status changes
NVSDISMT EQU   B'00010000'         NVS disabled for Maintenance
NVSPEND  EQU   B'00001000'         NVS pending due to error
CCAMASK  EQU   B'11000000'         N mask for CCA reconstruction
*
         EJECT
         USING *,R15               Temporary base for
         B     HERE                branching around
         DC    CL8'IEFU84  '       module name
         DC    CL8'&SYSDATE'
         DC    CL8' @ &SYSTIME'
HERE     STM   R14,R12,12(R13)     save regs
         DROP  R15                 drop temp base
         USING IEFU84,R12          base
         LR    R12,R15
         LR    R11,R1              save ptr to parms
         LR    R10,R13             save temporary until getmain is done
         L     R2,0(R11)           get smf record address
         CLI   5(R2),TYPE22        is it type 22?
         BNE   QUIKEXIT            if not then go to exit
         CLI   19(R2),S3990CHG     was it created by 3990 state chg?
         BNE   QUIKEXIT            if not then go to exit
*
*  Obtain the Local lock unconditionally
*
GETLOCK  EQU   *
         SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,REGS=USE
SAVERC   LR    R9,R15              save return code from setlock
*
         L     R5,CVTPTR           R5 = addr of cvt
         USING CVT,R5
         L     R6,CVTTCBP          R6 = ptr to addr of cur tcb
         L     R7,12(R6)           R7 = addr of current ascb
         DROP  R5
         SR    R0,R0               clear reg 0  (mem really is SP252
*                                  because we are key 0 supervisor st)
         SR    R4,R4               indicate current TCB
         LA    R0,WORKLEN          set length for getmain from sp 0
*                                  R4 and R7 are used by branch GETMAIN
         GETMAIN R,LV=(R0),BRANCH=YES     unconditional request
*                                  will abend this task if not rc=0
         LR    R13,R10             restore so we can chain saves
         ST    R1,8(R13)           chain
         ST    R13,4(R1)           save
         LR    R13,R1              areas
         USING WORK,R13            base
         MVC   WTOMSG(WTOLEN),WTO  move message to gotten
*
         L     R2,0(R11)           get smf record address
         USING SMFRCD22,R2         map symbolic names to type 22 rec
*
         CLC   SMF22SCS,SMF22PCS   compare current to previous
         BE    CHKNVS              no change.  go check for nvs change
         MVC   RESULTXC,SMF22SCS   prepare for XC
         XC    RESULTXC,SMF22PCS   resultxc tells which flds chgd
         SR    R3,R3
         IC    R3,SMF22SCS         move one byte to register
         EJECT
         TM    RESULTXC,CACHSTAT   test for changes with prev field
         BZ    CHKDISAB
         LA    R6,MSGTAB           point to first entry in table
         LA    R7,NMSGS            r7 = number of entries in table
         LR    R1,R3
         SRL   R1,5                shift off bits 3-7
CHECKMSG EQU   *
         CLM   R1,1,0(R6)          do the bit patterns match?
         BE    MSGFND              yes   go build wto message
         LA    R6,LENMSG(R6)       no    point to next entry in table
         BCT   R7,CHECKMSG         try until match or end of table
         LA    R6,NOMSG            debug message
         BAL   R10,WTORTN          go issue WTO
         B     ENDCHK              not good. this should not happen.
MSGFND   EQU   *
         BAL   R10,WTORTN          go issue WTO
CHKDISAB EQU   *
         TM    RESULTXC,SSDISABL   did subsys storage dis maint chg?
         BNO   CHKIML              not on.   go chk next flag bit
         TM    SMF22SCS,SSDISABL   is subsys storage dis maint new?
         BNO   LSTGENA             not on.   go chk next flag bit
         LA    R6,STGDIS           r6 = address of message to issue
         BAL   R10,WTORTN          go issue WTO
         B     CHKIML
LSTGENA  EQU   *                   now on.   go chk next flag bit
         LA    R6,STGENA           r6 = address of message to issue
         BAL   R10,WTORTN          go issue WTO
CHKIML   EQU   *
         TM    RESULTXC,IMLDEV     is IML device unavailable new?
         BNO   CHKCFW              not on.   go chk next flag bit
         TM    SMF22SCS,IMLDEV     is IML device unavailable new?
         BNO   LIMLENA             now on.   go chk next flag bit
         LA    R6,IMLDIS           r6 = address of message to issue
         BAL   R10,WTORTN          go issue WTO
         B     CHKCFW
LIMLENA  EQU   *                   now on.   go chk next flag bit
         LA    R6,IMLENA           r6 = address of message to issue
         BAL   R10,WTORTN          go issue WTO
CHKCFW   EQU   *
         TM    RESULTXC,CFWDISAB   is cache fast write disabled new?
         BNO   ENDCHK              not on.  all flags have been tested
         TM    SMF22SCS,CFWDISAB   is cache fast write disabled new?
         BNO   LCFWENA             not on.  all flags have been tested
         LA    R6,CFWDIS           r6 = address of message to issue
         BAL   R10,WTORTN          go issue WTO
         B     ENDCHK
LCFWENA  EQU   *                   now on.  all flags have been tested
         LA    R6,CFWENA           r6 = address of message to issue
         BAL   R10,WTORTN          go issue WTO
         B     ENDCHK
*
         EJECT
CHKNVS   EQU   *                   SMF22SNV compared to SMF22PNV
         CLC   SMF22SNV,SMF22PNV   compare current to previous
         BE    CHKDEV              no change.  go check for device chg
         MVC   RESULTXC,SMF22SNV   prepare for exclusive or character
         XC    RESULTXC,SMF22PNV   resultxc indicates which flds chgd
         TM    RESULTXC,NVSSTAT    test for changes with prev field
         BZ    CHKNVSDM            no  NVS status bits did not change
CHKNC0   TM    SMF22SNV,NVSC0      was NVS changed to active or pendin?
         BZ    LNVSACT             both bits are off so NVS is active
         BNO   CHKN10              both bits are not on. check next ptn
         LA    R6,NVSPND           both bits are on.  NVS is pending
         BAL   R10,WTORTN          go issue WTO
         B     CHKNVSDM            go check next flag.
LNVSACT  LA    R6,NVSACT           get address of message
         BAL   R10,WTORTN          go issue WTO
         B     CHKNVSDM            go check next flag.
CHKN10   EQU   *
         TM    SMF22SNV,NVS80      was NVS changed deactivated host?
         BNO   CHKN01              no change.  go check next pattern
         LA    R6,NVSDHS           get address of message
         BAL   R10,WTORTN          go issue WTO
         B     CHKNVSDM            go check next flag.
CHKN01   EQU   *
         TM    SMF22SNV,NVS40      was NVS changed deactivated subsys?
         BNO   CHKNVSDM            no change.  go check next pattern
         LA    R6,NVSDSS           get address of message
         BAL   R10,WTORTN          go issue WTO
CHKNVSDM EQU   *
         TM    RESULTXC,NVSDISMT   test for changes with prev field
         BZ    CHKNPND             no  NVS status bits did not change
         TM    SMF22SNV,NVSDISMT   did NVS disabled maint status chg?
         BNO   LNVSENA             not on.   go chk next flag bit
         LA    R6,NVSDIM           r6 = address of message to issue
         BAL   R10,WTORTN          go issue WTO
         B     CHKNPND
LNVSENA  EQU   *                   now on.   go chk next flag bit
         LA    R6,NVSENA           r6 = address of message to issue
         BAL   R10,WTORTN          go issue WTO
CHKNPND  EQU   *
         TM    SMF22SNV,NVSPEND    did NVS pending due err stat  chg?
         BNO   ENDCHK              not on.   all checks are complete
         LA    R6,NVSPER           r6 = address of message to issue
         BAL   R10,WTORTN          go issue WTO
         B     ENDCHK
         EJECT
CHKDEV   EQU   *
         CLC   SMF22SDS,SMF22PDS   compare current to previous
         BE    ENDCHK              no changes.
         MVC   RESULTXC,SMF22SDS   prepare for XC
         XC    RESULTXC,SMF22PDS   resultxc says which flds chgd
         TM    RESULTXC,DEVSTAT    test for changes with prev field
         BZ    CHKDFWST            no  device cache status didnt change
CHKDEVC0 EQU   *
         TM    SMF22SDS,DEVC0      was device cache chgd to deactivate?
         BZ    LDEVACT             both bits are off so dev cache act
         BNO   CHKDEV80            both bits are not on.
         LA    R6,DEVDEA           both bits are on.  dev cache deact
         BAL   R10,WTORTN          go issue WTO
         B     CHKDFWST            go check next flag.
LDEVACT  EQU   *
         LA    R6,DEVACT           get address of message
         BAL   R10,WTORTN          go issue WTO
         B     CHKDFWST            go check next flag.
CHKDEV80 EQU   *
         TM    SMF22SDS,DEV80      was device cache chgd to deact pend?
         BNO   CHKDEV40            no change.  go check next pattern
         LA    R6,DEVDPN           get address of message
         BAL   R10,WTORTN          go issue WTO
         B     CHKDFWST            go check next flag.
CHKDEV40 EQU   *
         TM    SMF22SDS,DEV40      was device cache chgd to deact pend?
         BNO   CHKDFWST            no change.  go check next pattern
         LA    R6,BADDEV           get address of message
         BAL   R10,WTORTN          go issue WTO
CHKDFWST EQU   *
         TM    RESULTXC,DFWSTAT    test for changes with prev field
         BZ    CHKDUPST            no  DFW status bits did not change
CHKDFW30 EQU   *
         TM    SMF22SDS,DFW30      was device cache chgd to deact pend?
         BZ    LDFWACT             both bits are off. dfw status active
         BNO   CHKDFW20            both bits are not on.
         LA    R6,DFWDEA           both bits are on.  dfw is deactivatd
         BAL   R10,WTORTN          go issue WTO
         B     CHKDUPST            go check next flag.
LDFWACT  EQU   *
         LA    R6,DFWACT           get address of message
         BAL   R10,WTORTN          go issue WTO
         B     CHKDUPST            go check next flag.
CHKDFW20 EQU   *
         TM    SMF22SDS,DFW20      was device cache chgd to deact pend?
         BNO   CHKDFW10            no change.  go check next pattern
         LA    R6,DFWDPN           get address of message
         BAL   R10,WTORTN          go issue WTO
         B     CHKDUPST            go check next flag.
CHKDFW10 EQU   *
         TM    SMF22SDS,DFW10      was device cache chgd to deact pend?
         BNO   CHKDUPST            no change.  go check next pattern
         LA    R6,BADDFW           get address of message
         BAL   R10,WTORTN          go issue WTO
         EJECT
CHKDUPST EQU   *
         TM    RESULTXC,DUPSTAT    test for changes with prev field
         BZ    CHKDUPPR            no  duplex       status didnt change
         MVC   CCAORIG,SMF22CCA    get CCA of originating device
         NI    CCAORIG,CCAMASK     save only bits 0-1
         MVC   CCAWORK,SMF22SDS+1  get other cca in duplex
         NI    CCAWORK,X'3F'       only low order 6 bits are used
         OC    CCAWORK(1),CCAORIG  restore bits 0-1 to get full CCA
         UNPK  CCA(3),CCAWORK(2)    prepare for translate instr
         TR    CCA(2),HEXTABLE-240  translate to displayable
         MVI   CCA+2,C' '          eliminate extra char from unpack
CHKDUP03 EQU   *
         TM    SMF22SDS,DUP03      was duplex changed to failed?
         BZ    LDUPAVL             both bits are off. duplex available
         BNO   CHKDUP02            both bits are not on.
         LA    R6,DUPFLB           both bits are on.
         BAL   R10,WTORTN          go issue WTO
         B     CHKDUPPR            go check next flag.
LDUPAVL  EQU   *
         TM    SMF22SDS,DUP08      is the primary of duplex bit on?
         BZ    DAVAIL              no so issue the available message
         LA    R6,DUPEST           yes so issue the duplex est msg
         BAL   R10,WTORTN          go issue WTO
         B     CHKDUPPR            go check next flag.
DAVAIL   LA    R6,DUPAVL           get address of message
         BAL   R10,WTORTN          go issue WTO
         B     CHKDUPPR            go check next flag.
CHKDUP02 EQU   *
         TM    SMF22SDS,DUP02      was duplex changed to failed?
         BNO   CHKDUP01            no change.  go check next pattern
         LA    R6,DUPFLA           get address of message
         BAL   R10,WTORTN          go issue WTO
         B     CHKDUPPR            go check next flag.
CHKDUP01 EQU   *
         TM    SMF22SDS,DUP01      was duplex changed to pending?
         BNO   CHKDUPPR            no change.  go check next pattern
         LA    R6,DUPPEN           get address of message
         BAL   R10,WTORTN          go issue WTO
CHKDUPPR EQU   *
         TM    RESULTXC,DUP08      was primary of duplex changed?
         BNO   CHKDUPSE            no change.  go check next pattern
         MVC   CCAORIG,SMF22CCA    get CCA of originating device
         NI    CCAORIG,CCAMASK     save only bits 0-1
         MVC   CCAWORK,SMF22SDS+1  get other cca in duplex
         NI    CCAWORK,X'3F'       only low order 6 bits are used
         OC    CCAWORK(1),CCAORIG  restore bits 0-1 to get full CCA
         UNPK  CCA(3),CCAWORK(2)    prepare for translate instr
         TR    CCA(2),HEXTABLE-240  translate to displayable
         MVI   CCA+2,C' '          eliminate extra char from unpack
         TM    SMF22SDS,DUP08      was primary of duplex changed?
         BNO   LDUPSIM             changed to off. back to simplex
         LA    R6,DUPPRI           get address of message
         BAL   R10,WTORTN          go issue WTO
         TM    SMF22SDS,DUP01      is duplex pending flag on? SLOPPY
         BO    CHKDUPSE            no so skip duplex est message
         LA    R6,DUPEST           get address of message
         BAL   R10,WTORTN          go issue WTO
         B     CHKDUPSE
LDUPSIM  EQU   *
         MVC   CCA,=C'   '
         LA    R6,DUPSIM
         BAL   R10,WTORTN          go issue WTO
CHKDUPSE EQU   *
         TM    SMF22SDS,DUP04      is  secondary of duplex on?
         BNO   CHKPINST            no change.  go check next pattern
         MVC   CCAORIG,SMF22CCA    get CCA of originating device
         NI    CCAORIG,CCAMASK     save only bits 0-1
         MVC   CCAWORK,SMF22SDS+1  get other cca in duplex
         NI    CCAWORK,X'3F'       only low order 6 bits are used
         OC    CCAWORK(1),CCAORIG  restore bits 0-1 to get full CCA
         UNPK  CCA(3),CCAWORK(2)    prepare for translate instr
         TR    CCA(2),HEXTABLE-240  translate to displayable
         MVI   CCA+2,C' '          eliminate extra char from unpack
         LA    R6,DUPSEC           get address of message
         BAL   R10,WTORTN          go issue WTO
CHKPINST EQU   *
         MVC   RESULTXC,SMF22SDS+1 prepare for XC
         XC    RESULTXC,SMF22PDS+1 resultxc says which flds chgd
         TM    RESULTXC,PINSTAT    test for changes with prev field
         BZ    ENDCHK              no changes so its miller time.
CHKPINC0 TM    SMF22SDS+1,PINC0    no pinned exists?
         BZ    LPINNOX             both bits are off. pinned no exist
         BNO   CHKPIN80            both bits are not on.
         LA    R6,PINEXS           both bits are on. pinned exists
         BAL   R10,WTORTN          go issue WTO
         B     ENDCHK
LPINNOX  LA    R6,PINNOX           get address of message
         BAL   R10,WTORTN          go issue WTO
         B     ENDCHK
CHKPIN80 TM    SMF22SDS+1,PIN80    unused bit. if on issue bad msg
         BNO   CHKPIN40            no change.  go check next pattern
         LA    R6,BADPIN           get address of message
         BAL   R10,WTORTN          go issue WTO
         B     ENDCHK
CHKPIN40 TM    SMF22SDS+1,PIN40    no pinned exists?
         BNO   ENDCHK              no change.  go check next pattern
         LA    R6,PINEXI           get address of message
         BAL   R10,WTORTN          go issue WTO
         B     ENDCHK
         EJECT
*--------------------------------------------------------------------*
*   Construct the message to be issued. Move message text, descriptor*
*   code and the volser of device in SMF22VOL.  Exec the WTO & return*
*--------------------------------------------------------------------*
WTORTN   EQU   *
         SR    R0,R0               clear reg 0
         LA    R4,WTOMSG           point to beginning of parm list
         AH    R4,0(R4)            add length of text
         MVC   0(2,R4),1(R6)       move descriptor code to parm list
         MVC   SSSTAT,3(R6)        move message text to parm list
         UNPK  DEVID(5),SMF22CUA(3)   prepare for translate instr
         TR    DEVID(4),HEXTABLE-240  translate to displayable
         MVI   DEVID+4,C','        eliminate extra char from unpack
         WTO   MF=(E,WTOMSG),LINKAGE=BRANCH
         BR    R10                 go back to work
*
ENDCHK   EQU   *
         SR    R0,R0               clear reg 0
         LA    R0,WORKLEN          free gotten area
         L     R5,CVTPTR           R5 = addr of cvt
         USING CVT,R5
         L     R6,CVTTCBP          R6 = ptr to addr of cur tcb
         DROP  R5
         L     R7,12(R6)           R7 = addr of current ascb
         SR    R4,R4               indicate current TCB
         LR    R1,R13              address of getmained area
         L     R13,4(R13)          get to callers save area
         FREEMAIN R,LV=(R0),A=(R1),BRANCH=YES
         LTR   R9,R9               test return code from Setlock OBTAIN
         BNZ   HADLOCK             rc for obtain = 4 (caller had lock)
         SETLOCK RELEASE,TYPE=LOCAL,REGS=USE
HADLOCK  EQU   *
QUIKEXIT LM    R14,R12,12(R13)     restore regs
         SR    R15,R15             set rc = 0
*        BSM   0,14                return
         BR    R14                 return
WTO  WTO 'PCC10I XXXX,MESSAGE TEXT OF CONDITION************   ',       X
               ROUTCDE=(1),DESC=(2),MCSFLAG=(BRDCST),LINKAGE=,MF=L
WTOLEN   EQU   *-WTO
         EJECT
*   Descriptor Code
*    1   System Failure
*    2   Immediate Action required
*    3   Eventual Action required
*    4   System Status
*    5   Immediate Command response
*    6   Job status
*    7   Retain action message for life of task
*    8   Out-of-line message
*    9   Operator request
*   10   Dynamic status displays
*   11   Critical eventual action requested
*   12   Important information messages
*   13   Message previously automated
*
MSGTAB   DS    0H
*               SMF    descriptor flags
*              flag    1234567890123456       message text
         DC    X'00',B'0010000000000000'
         DC    CL40'SUBSYSTEM CACHE NOW ACTIVE'
LENMSG   EQU   *-MSGTAB
         DC    X'01',B'0010000000000000'
         DC    CL40'SUBSYSTEM CACHE IS PENDING ACTIVE'
         DC    X'02',B'0010000000000000'
         DC    CL40'SUBSYSTEM CACHE DEACTIVATED'
         DC    X'04',B'0010000000000000'
         DC    CL40'SUBSYSTEM CACHE DEACTIVATED BY HOST'
         DC    X'06',B'0010000000000000'
         DC    CL40'SUBSYSTEM CACHE PENDING OFF'
         DC    X'07',B'0100000000000000'
         DC    CL40'SUBSYSTEM CACHE PENDING OFF FAILED'
NMSGS    EQU   (*-MSGTAB)/LENMSG
*                 descriptor flags
*                 1234567890123456       message text
DEVACT   DC    X'00',B'0010000000000000'
         DC    CL40'DEVICE CACHE IS ACTIVE'
BADDEV   DC    X'00',B'0100000000000000'
         DC    CL40'INVALID DEVICE CACHE STATUS'
DEVDPN   DC    X'00',B'0010000000000000'
         DC    CL40'DEVICE CACHE DEACTIVATE PENDING'
DEVDEA   DC    X'00',B'0010000000000000'
         DC    CL40'DEVICE CACHE HAS BEEN DEACTIVATED'
*
DFWACT   DC    X'00',B'0010000000000000'
         DC    CL40'DEVICE DFW IS ACTIVE'
BADDFW   DC    X'00',B'0100000000000000'
         DC    CL40'INVALID DEVICE DFW STATUS'
DFWDPN   DC    X'00',B'0010000000000000'
         DC    CL40'DEVICE DFW DEACTIVATION PENDING'
DFWDEA   DC    X'00',B'0010000000000000'
         DC    CL40'DEVICE DFW HAS BEEN DEACTIVATED'
*
DUPEST   DC    X'00',B'0010000000000000'
         DC    CL40'DUPLEX IS ESTABLISHED      OTHER CCA='
DUPAVL   DC    X'00',B'0010000000000000'
         DC    CL40'DUPLEX IS AVAILABLE        OTHER CCA='
DUPSIM   DC    X'00',B'0010000000000000'
         DC    CL40'DUPLEX PAIR RESET TO SIMPLEX         '
DUPPEN   DC    X'00',B'0010000000000000'
         DC    CL40'PENDING DUPLEX             OTHER CCA='
DUPFLA   DC    X'00',B'0010000000000000'
         DC    CL40'SECONDARY DEV SUSPENDED    OTHER CCA='
DUPFLB   DC    X'00',B'0010000000000000'
         DC    CL40'PRIMARY DEVICE SUSPENDED   OTHER CCA='
DUPPRI   DC    X'00',B'0010000000000000'
         DC    CL40'PRIMARY OF DUPLEX PAIR     OTHER CCA='
DUPSEC   DC    X'00',B'0010000000000000'
         DC    CL40'SECONDRY OF DUPLEX PAIR    OTHER CCA='
*
NVSACT   DC    X'00',B'0010000000000000'
         DC    CL40'SUBSYSTEM NVS IS ACTIVE'                (X'00')
NVSDSS   DC    X'00',B'0100000000000000'
         DC    CL40'SUBSYSTEM NVS FAILED'                   (X'01')
NVSDHS   DC    X'00',B'0010000000000000'
         DC    CL40'SUBSYSTEM NVS HAS BEEN DEACTIVATED'     (X'02')
NVSPND   DC    X'00',B'0010000000000000'
         DC    CL40'SUBSYSTEM NVS IS PENDING OFF'           (X'04')
*
NVSDIM   DC    X'00',B'0010000000000000'
         DC    CL40'SUBSYSTEM NVS DISABLED FOR MAINTENANCE'
NVSENA   DC    X'00',B'0010000000000000'
         DC    CL40'SUBSYSTEM NVS ENABLED FROM MAINTENANCE'
NVSPER   DC    X'00',B'0100000000000000'
         DC    CL40'SUBSYSTEM NVS PENDING OFF DUE TO ERROR'
*
PINNOX   DC    X'00',B'0010000000000000'
         DC    CL40'NO PINNED DATA EXISTS FOR DEVICE'           (X'00')
PINEXI   DC    X'00',B'0100000000000000'
         DC    CL40'DEVICE HAS PINNED DATA, DFW ACTIVE'         (X'01')
PINEXS   DC    X'00',B'0100000000000000'
         DC    CL40'DEVICE HAS PINNED DATA, DFW SUSPENDED'      (X'03')
BADPIN   DC    X'00',B'0100000000000000'
         DC    CL40'INVALID PINNED DATA STATUS'                 (X'04')
*
STGDIS   DC    X'00',B'0100000000000000'
         DC    CL40'SUBSYSTEM STORAGE DISABLED MAINT'
STGENA   DC    X'00',B'0010000000000000'
         DC    CL40'SUBSYSTEM STORAGE NOW ENABLED'
IMLDIS   DC    X'00',B'0100000000000000'
         DC    CL40'IML DEVICE UNAVAILABLE'
CFWDIS   DC    X'00',B'0010000000000000'
         DC    CL40'CACHE FASTWRITE IS DISABLED'
IMLENA   DC    X'00',B'0100000000000000'
         DC    CL40'IML DEVICE IS NOW AVAILABLE'
CFWENA   DC    X'00',B'0010000000000000'
         DC    CL40'CACHE FASTWRITE IS ACTIVE'
NOMSG    DC    X'00',B'0100000000000000'
         DC    CL40'INVALID SUBSYSTEM CACHING STATUS'
*
HEXTABLE DC    C'0123456789ABCDEF' used by translate instruction
         LTORG
WORK     DSECT
SAVEAREA DS    18F
RESULTXC DS    CL1
CCAORIG  DS    CL1                 CCA of originating device
CCAWORK  DS    CL2                 CCA of other device in pair (2-7)
         DS    0F
WTOMSG   DS    256X                work
         ORG   WTOMSG+4
         DS    CL7    'PCC10I '
DEVID    DS    CL4    'XXXX'
         DS    CL1    ','
SSSTAT   DS    CL38   'MESSAGE TEXT'
CCA      DS    CL3
         ORG
WORKLEN  EQU   *-WORK
         EJECT
         IFASMFR 22                mapping macro for type 22 record
         IHAPSA
         CVT   DSECT=YES
         END
//L.SYSLMOD DD DSN=SYS1.LPALIB,DISP=SHR,
//      UNIT=SYSDA,VOL=SER=SM51D0
//SYSIN  DD *
 ENTRY IEFU84
 NAME IEFU84(R)
/*
