/*  rexx exec called by Amdahl Cache Utility to check for exceptions */
/*  (C) COPYRIGHT AMDAHL CORPORATION  1995. All Rights Reserved      */
/*  written:  February 1995                                          */
/*  author :  Chuck Baumann                                          */
/*  Panels :  CSHMAN7                                                */
/*  Help   :  CSHHMAN7                                               */
/*  Changes:  V1.0 -- Initial functions                              */
/*  95/02/02  V2.3 -- Check for exceptional conditions               */
/*-------------------------------------------------------------------*/
address 'ISPEXEC' 'TBCREATE EXCEPT ,
  NAMES(TBMDL,TBSSID,TBSSSTG,TBSSAVL,TBNVSAVL,TBNVSSTG,TBU,TBREASON) ,
   NOWRITE REPLACE'

address 'ISPEXEC' 'TBQUERY SSGTAB ROWNUM(ROWS) POSITION(CROW)'
address 'ISPEXEC' 'TBTOP SSGTAB'
do rows
  x = 'N'                    /* x = flag to indicate exception */
  address 'ISPEXEC' 'TBSKIP SSGTAB'
  address 'ISPEXEC' 'TBGET  SSGTAB'
  if TBOFF ¬= 'NO'           /* some cache memory is offline */
  then do
         x = 'Y'
         tbreason = 'Cache or NVS memory is offline'
       end
  if TBSSST ¬= 'ON'          /* subsystem cache is not on */
  then do
         x = 'Y'
         tbreason = 'Subsystem Cache is not on'
       end
  if substr(TBNVST,1,1) ¬= 'O' ,  /* NVS in error condition */
  &  tbmdl ¬= '3880'
  then do
         x = 'Y'
         tbreason = 'NVS is in an error state'
       end
  if TBPIN  ¬= 'NO' ,        /* pinned data exists       */
  then do
         x = 'Y'
         tbreason = 'Pinned data exists'
       end
  if x = 'Y'
  then do
         address 'ISPEXEC' 'TBADD EXCEPT'
       end
end

address 'ISPEXEC' 'TBQUERY EXCEPT ROWNUM(ROWS)'
if rows > 0
then do
       address 'ISPEXEC' 'TBTOP EXCEPT'
       do while rc < 8
         address 'ISPEXEC' 'TBDISPL EXCEPT PANEL(CSHMAN7)'
       end
     end
else address 'ISPEXEC' 'SETMSG MSG(CSHM035) COND'

address 'ISPEXEC' 'TBCLOSE EXCEPT'
address 'ISPEXEC' 'TBTOP SSGTAB'
address 'ISPEXEC' 'TBSKIP SSGTAB ROW('CROW')'
return
