/* Example of StartUp Procedure */

 x = cbwto('CBTIPL: Startup procedure in progress...')

/* Step 1 =================================================

     Bring up:
           - JES2
           - DLF
           - VMCF (EZAZSSI)
           - FFST
           - CA90 (CAS9)
---------------------------------------------------------- */

     x  = cbcmd('START JES2,P=JES2PAA1')
     x  = cbcmd('START DLF,SUB=MSTR')
     x  = cbcmd('START EZAZSSI,P=&SYSNAME')
     x  = cbcmd('START CAS9,SUB=MSTR')
     x  = cbcmd('START EPWFFST.FFST,SUB=MSTR')


/* Step 2 =================================================

     Ask zCBT to collect:
        - JES2 message $HASP492
        - JES2 message $HASP454
        - VTAM message IST020I

---------------------------------------------------------- */

     x  = cbset('MSG','$HASP492')
     x  = cbset('MSG','$HASP454')
     x  = cbset('MSG','IST020I')


/* Step 3 =================================================

     Wait collected events
      + When msg $HASP492 occurs, indicates JES2 up,
        then bring up:
              - VTAM
              - SDSF
              - ACF2

      + When msg $HASP454 occurs, JES2 needs to confirm
        due to check point problem.  Then reply Y

      + When msg IST020I occurs, indicates VTAM up,
        then bring up:
              - TCPIP
              - TSO

---------------------------------------------------------- */

 jes2  = 0
 jrpy  = 0
 vtam  = 0
 all   = 0
 Do forever
     event  = cbevent()
     evtype = strip(word(event,1))
     If evtype = 'MSG' then do
         info = strip(word(event,2))
         Select
             When info = '$HASP492' then,
                  Do
                      jes2 = 1
                      x  = cbcmd('START VTAM')
                      x  = cbcmd('START SDSF')
                  End
             When info = 'IST020I' then,
                  Do
                      vtam = 1
                      x  = cbcmd('START TSO')
                      x  = cbcmd('START TCPIP')
                      x  = cbcmd('START HTTPD1')
                  End
             Otherwise,
                  If strip(word(event,3)) = '$HASP454' then,
                      Do
                         Parse var info "(REPLYID=" rpyid ")" .
                         rpytxt = "REPLY " || rpyid || ",Y"
                         x  = cbcmd(rpytxt)
                         jrpy = 1
                      End
         End
         all = jes2 * vtam
         if all = 1 then LEAVE
     End
 End
 x = cbwto('CBTIPL: Startup procedure complete')
 Exit
