*
*
*                     +----------+
*     Module Name  -  | CBTINSVA |
*                     +----------+
*
*
*     Function -
*               Initialize SVA
*
*     Syntax -
*
*
*     Operation -
*
*
*     Author -
*          Name     :  Deru Sudibyo
*                      Software Architect
*          Copyright:  (C)2006 Deru Sudibyo
*
*
*     Modification Log -
*     +--------+------------+--------+---------------------------+
*     | Date   | Engineer   | V.R.L. | Description               |
*     +--------+------------+--------+---------------------------+
*     |06/09/24| Deru S.    | 1.0.0. | First development         |
*     |        |            |        |                           |
*     |        |            |        |                           |
*     |        |            |        |                           |
*     |        |            |        |                           |
*     |        |            |        |                           |
*     |        |            |        |                           |
*     +--------+------------+--------+---------------------------+
*
         MACRO
&LABEL   CBTINSVA &base=
         COPY  CBTGBLVR
         lclc  &lbl,&baselbl
         aif   (&DERMFINIT).done
&lbl     setc  'DER&sysndx'
         aif   (t'&base eq 'O').defbase
&baselbl setc  '&base'
         ago   .begin
.defbase anop
&baselbl setc  '&DERMEP'
.begin   anop
         ds    0h
&DERSVLOC equ  *
         stm   r0,r15,SVAregs       save reg 0-15
         la    r12,&DERSVLOC-&baselbl offset from the module base
         slr   r15,r12              base address
         lr    r12,r15              establish base addressability
         la    r0,SVAuserarea       locate start of user area
         la    r1,SVAuserareasize   length of user area
&lbl.a   xr    r2,r2                clear reg 2
         xr    r3,r3                clear reg 3
         mvcl  r0,r2                zap MF area
         ltr   r1,r1                was it complete?
         bnz   &lbl.a               no, continue if interrupted
*        bc    #cc3,&lbl.a          continue if interrupted
         la    r0,&DERMFLOC         locate MF area
         la    r1,&DERMFLOC._len    length of MF area
         la    r2,SVA_macforms      target MF area in SVA
         la    r3,SVA_macforms_len  length of target (should the same)
&lbl.b   mvcl  r2,r0                initialize MF area
         bc    #cc3,&lbl.b          continue if interrupted
         lm    r0,r15,SVAregs       restore reg 0-15
         br    r14                  return
&lbl.c   dc    a(&DERSVLOC-&baselbl)
&DERMFLOC CBTMFORM prefix=&DERCID
&DERMFINIT setb 1
.done    anop
         mend
