.*
.*
.*                    +----------+
.*    Macro Name   -  | @EXIT    |
.*                    +----------+
.*
.*
.*    Function -
.*
.*
.*    Syntax -
.*
.*
.*    Operation -
.*
.*
.*    Author -
.*         Name     :  Deru Sudibyo
.*                     Technical Consultant
.*                     Computer Associates
.*                     Indonesia Office
.*                     Wisma BNI-46 34th floor, BNI-46 City
.*                     Jl. Jend. Sudirman, Kav 1, Jakarta Pusat
.*         Copyright:  (C)1998 Deru Sudibyo
.*
.*    Disclaimer -
.*        This macro is developed as a personal support, and is
.*        not a part of CA support matterials.  The purpose is as
.*        a temporary solution or even just a sample.  Therefor,
.*        further modifications and maintenance are client's
.*        responsibilities.
.*
.*    Modification Log -
.*    +--------+------------+--------+---------------------------+
.*    | Date   | Engineer   | V.R.L. | Description               |
.*    +--------+------------+--------+---------------------------+
.*    |98/02/23| Deru S.    | 1.0.0. | First development         |
.*    |        |            |        |                           |
.*    |        |            |        |                           |
.*    |        |            |        |                           |
.*    |        |            |        |                           |
.*    |        |            |        |                           |
.*    |        |            |        |                           |
.*    +--------+------------+--------+---------------------------+
.*
         MACRO
&NAME    @EXIT &KEEPRC=YES,&SVAINLOC=NO,&SVAINBASE=
         COPY  CBTGBLVR
         gblb  &DIB390,&DIBESA
         LCLB  &KRC
&NAME    DS    0H
         aif   (not &CBTDOMSV).ckkrc
         stm   r14,r1,&DERSVGR     save reg 0-1 and 14-15
         CBTDOM PURGE,loc=SVA
         lm    r14,r1,&DERSVGR     restore reg 0-1 and 14-15
&CBTDOMSV setb 0
.ckkrc   AIF   ('&KEEPRC' NE 'YES').SKIP
&KRC     SETB  1
.*       ST    R15,&DERWORD        Save RC
.SKIP    ANOP
         AIF   (NOT &DIB390).ESA
         AIF   (NOT &DERSTOR).S390X
         icm   r2,b'1111',&DERSACSV get saved ASC code
         sac   0(r2)               restore ASC code
         l     r2,&DERPSWSV        get saved PSW key
         LR    R1,R13
         AIF   (not &KRC).S390rel
         AIF   (not &DERSVA).S390rel
         lr    r3,r15              keep RC here
.S390rel anop
         STORAGE RELEASE,ADDR=(R1),LENGTH=&DERSVSZ
         ltr   r2,r2               was key set?
         bz    *+8                 no, skip below
         spka  0(r2)               yes, restore it
.S390X   AIF   (&KRC).S390RC
         LA    R15,0               Set RC=0
         PR                        Unstack states and return to MVS
         ago   .epilog
.S390RC  AIF   (&DERSVA).S390rc1
         L     R15,&DERWORD        restore RC
         PR                        Unstack states and return to MVS
         ago   .epilog
.S390rc1 anop
         Lr    R15,r3              restore RC
         PR                        Unstack states and return to MVS
         ago   .epilog
.ESA     AIF   (NOT &DIBESA).S370
         icm   r2,b'1111',&DERSACSV get saved ASC code
         sac   0(r2)               restore ASC code
         l     r2,&DERPSWSV        get saved PSW key
         LR    R1,R13
         L     R13,4(R13)          OLD SA PTR
         AIF   (NOT &DERSTOR).ESAX
         AIF   (not &KRC).ESArel
         AIF   (not &DERSVA).ESArel
         lr    r3,r15              keep RC here
.ESArel  STORAGE RELEASE,ADDR=(R1),LENGTH=&DERSVSZ
         ltr   r2,r2               was key set?
         bz    *+8                 no, skip below
         spka  0(r2)               yes, restore it
.ESAX    AIF   (&KRC).ESArc
         xr    R15,R15             set RC = 0
         ago   .ESAZ
.ESArc   Lr    R15,r3              restore RC
.ESAZ    LM    R0,R12,20(R13)      Restore REGs
         L     R14,12(R13)
         BSM   0,R14
         ago   .epilog
.S370    ANOP
         LR    R2,R13
         L     R13,4(R13)          OLD SA PTR
         AIF   (NOT &DERSTOR).S370X
         AIF   (not &KRC).S370rel
         lr    r3,r15              keep RC here
.S370rel FREEMAIN R,A=(R2),LV=&DERSVSZ
.S370X   AIF   (&KRC).S370RC
         RETURN (14,12),RC=0       BACK TO MVS
         ago   .epilog
.S370RC  ANOP
         Lr    R15,r3
         RETURN (14,12),RC=(15)   BACK TO MVS
.epilog  aif   (t'&SVAINLOC ne 'YES').quit
         CBTINSVA base=&SVAINBASE
.quit    anop
         MEND
