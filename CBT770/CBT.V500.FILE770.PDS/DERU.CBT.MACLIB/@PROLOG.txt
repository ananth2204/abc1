.*
.*
.*                    +----------+
.*    Macro Name   -  | @PROLOG  |
.*                    +----------+
.*
.*
.*    Function -
.*
.*
.*    Syntax -
.*
.*
.*    Operation -
.*
.*
.*    Author -
.*         Name     :  Deru Sudibyo
.*                     Technical Consultant
.*                     Computer Associates
.*                     Indonesia Office
.*                     Wisma BNI-46 34th floor, BNI-46 City
.*                     Jl. Jend. Sudirman, Kav 1, Jakarta Pusat
.*         Copyright:  (C)1998 Deru Sudibyo
.*
.*    Disclaimer -
.*        This macro is developed as a personal support, and is
.*        not a part of CA support matterials.  The purpose is as
.*        a temporary solution or even just a sample.  Therefor,
.*        further modifications and maintenance are client's
.*        responsibilities.
.*
.*    Modification Log -
.*    +--------+------------+--------+---------------------------+
.*    | Date   | Engineer   | V.R.L. | Description               |
.*    +--------+------------+--------+---------------------------+
.*    |98/02/23| Deru S.    | 1.0.0. | First development         |
.*    |        |            |        |                           |
.*    |        |            |        |                           |
.*    |        |            |        |                           |
.*    |        |            |        |                           |
.*    |        |            |        |                           |
.*    |        |            |        |                           |
.*    +--------+------------+--------+---------------------------+
.*
         MACRO
&NAME    @PROLOG &ESA=370,         390, 370 or NO (means S/370)        ++
               &AMODE=ANY,                                             ++
               &RMODE=ANY,                                             ++
               &RENT=YES,                                              ++
               &RSECT=NO,                                              ++
               &SPLEVEL=6,                                             ++
               &ARMODE=NO,                                             ++
               &ARCTRL=NO,                                             ++
               &EXECUTE=YES,                                           ++
               &DOMSAVE=YES,                                           ++
               &DOMEXCH=NO,                                            ++
               &EP=,                                                   ++
               &KEY=ASIS,                                              ++
               &REGEQU=YES,                                            ++
               &SVA=YES,                                               ++
               &SVALOC=ANY,                                            ++
               &SVAINIT=YES,                                           ++
               &RUNTYPE=MAINTASK,                                      ++
               &JES2MAP=NO,                                            ++
               &version=0,                                             ++
               &entry=YES,                                             ++
               &dsid=ZCBTDSPC,                                         ++
               &dsshrid=SCHEDRPT,                                      ++
               &divid=zCBT-DIV,                                        ++
               &maxjob=10000,        max for 10,000 jobs               ++
               &maxtrig=100000,      max for 100,000 triggers          ++
               &maxstep=500000,      max for 500,000 jobsteps          ++
               &shareid=DERU@CBT,                                      ++
               &svrcomm=ZCBTCOMM,    GEN or a name e.g COMMAREA        ++
               &agtcomm=ZCBTACOM,    GEN or a name e.g ZCOMAREA        ++
               &BREG=R12
         COPY  CBTGBLVR
         LCLA  &NBR,&I,&SVASIZE,&j,&N
         LCLC  &BR1,&BR2,&BR3,&BRLINK,&IX,&OS,&CID,&macname,&jescb
&DERMFLOC setc 'DER&sysndx.MF'
&DERSVLOC setc 'DER&sysndx.SVA'
&DERSVMFI setc 'DER&sysndx.SVMFINIT'
&DERDSID  setc '&dsid'
&DERDSSID setc '&dsshrid'
&DERDIVID setc '&divid'
&DERSHRID setc '&shareid'
&DERMAXJB setc '&maxjob'
&DERMAXTR setc '&maxtrig'
&DERMAXST setc '&maxstep'
         aif   ('&svrcomm' eq 'GEN').gencomx
&DERCOMMX setc '&svrcomm'
         ago   .ckcomz
.gencomx anop
&DERCOMMX setc 'CX'.'&SYSDATC'(3,6)
.ckcomz  aif   ('&agtcomm' eq 'GEN').gencomz
&DERCOMMZ setc '&agtcomm'
         ago   .okcomm
.gencomz anop
&DERCOMMZ setc 'CZ'.'&SYSDATC'(3,6)
.okcomm  anop
         print NOGEN
&derzos  setb  1
         mnote 0,'Generated on &SYSTEM_ID'
         aif   ('&SYSTEM_ID'(1,4) eq 'z/OS').zos
         aif   ('&SYSTEM_ID'(1,6) eq 'OS/390').os390
&derosver setc '00.00.00'
         ago   .prodver
.zos     anop
&derzos  setb  1
&derosvrm setc '&SYSTEM_ID'(6,8)
         ago   .prodver
.os390   anop
&derzos  setb  0
&derosvrm setc '&SYSTEM_ID'(8,8)
.prodver anop
&derosver setc '&derosvrm'(1,2)
&derosrel setc '&derosvrm'(4,2)
&derosmod setc '&derosvrm'(7,2)
&prodver seta  &version
&DERSVLC setc  '&SVALOC'
         aif   ('&JES2MAP' eq 'NO').start
         mnote 0,'DERPRO001I Generating JES2 data areas of &jes2map'
&DERJES2 setb  1
&N       seta  n'&jes2map
&j       seta  1
&jescb   setc  '&jes2map(1)'
.jesloop anop
&j       seta  &j+1
&jescb   setc  '&jescb,&jes2map(&j)'
         aif   (&j lt &N).jesloop
         mnote 0,'DERPRO002I Generated JES2 &N data areas: &jescb'
.*       copy  $HASPGBL
        $module &jes2map
.start   anop
&IX      SETC  '&SYSNDX'
&DERDSN   SETC  'DER&IX._DSN'
&DERDSNC  SETC  'DER&IX._DSN_COUNTER'
&DERDATE  SETC  'DER&IX._DATE'
&DERTIME  SETC  'DER&IX._TIME'
&DERYDAT  SETC  'DER&IX._YDATE'
&DERYTIM  SETC  'DER&IX._YTIME'
&DERSVGR  SETC  'DER&IX._SVGR'
&DERSVAR  SETC  'DER&IX._SVAR'
&DERWTOL  SETC  'DER&IX._WTO'
&DEROPNL  SETC  'DER&IX._OPEN'
&DERCLSL  SETC  'DER&IX._CLOSE'
&DERMSGL  SETC  'DER&IX._MSG'
&DERTMEL  SETC  'DER&IX._TIMEL'
&DEREDTL  SETC  'DER&IX._EDIT'
&DERWORD  SETC  'DER&IX._WORD'
&DERDBWD  SETC  'DER&IX._DWORD'
&DERWORK  SETC  'DER&IX._WORK'
&DERPSWSV SETC  'DER&IX._pswsave'
&DERSACSV SETC  'DER&IX._sacsave'
&DERWGRSV SETC  'DER&IX._wgrsave'
&DERWARSV SETC  'DER&IX._warsave'
&DERWTOBUF SETC  'DER&IX._wtobuf'
         AIF   ('&regequ' eq 'NO').okrgequ
#maxSCT  equ   &DERMAXJB
#maxTRG  equ   &DERMAXTR
#maxEOT  equ   &DERMAXST
&I       SETA  0
.LOOPEQU ANOP
A&I      EQU   &I
C&I      EQU   &I
         aif   ((&DERJES2) or ('&regequ' eq 'JES2')).nextequ
R&I      EQU   &I
.nextEQU ANOP
&I       SETA  &I+1
         AIF   (&I LE 15).LOOPEQU
F0       EQU   0
F2       EQU   2
F4       EQU   4
F6       EQU   6
.okrgequ anop
#cc0     equ   8      cc = 0
#cc1     equ   4      cc = 1
#cc2     equ   2      cc = 2
#cc3     equ   1      cc = 3
#ccnot0  equ   7      cc not 0  (b'0111')
#ccnot1  equ   11     cc not 1  (b'1011')
#ccnot2  equ   13     cc not 2  (b'1101')
#ccnot3  equ   14     cc not 3  (b'1110')
&DEREQU  setb  1
         AIF   (('&RENT' NE 'Y') AND ('&RENT' NE 'YES')).ckesa
&DERRENT SETB  1
         aif   ('&rsect' ne 'YES').okrent
&DERRSECT SETB  1
.okrent  anop
&SVASIZE seta  90
         aif   ('&SVA' ne 'YES').ckesa
&DERSVA  setb  1
&SVASIZE seta  0
.ckesa    AIF   ('&ESA' NE '390').ESA370
&DER390   SETB  1
&DERESA   SETB  1
.ESA370   AIF   ('&ESA' NE '370').splvl
&DERESA   SETB  1
.splvl   splevel set=&SPLEVEL
&macname setc  '@ENTRY'
         aif   ('&entry' eq 'YES').entry
&macname setc  '@INIT'
.entry   anop
         print GEN
&NAME    AMODE &AMODE
&NAME    RMODE &RMODE
&NAME    &macname EP=&EP,                                              +
               ARMODE=&ARMODE,                                         +
               ARCTRL=&ARCTRL,                                         +
               EXECUTE=&EXECUTE,                                       +
               SVASIZE=&SVASIZE,                                       +
               SVALOC=&SVALOC,                                         +
               SVAINIT=&SVAINIT,                                       +
               RUNTYPE=&RUNTYPE,                                       +
               DOMSAVE=&DOMSAVE,                                       +
               DOMEXCH=&DOMEXCH,                                       +
               KEY=&KEY,                                               +
               CSECT=YES,                                              +
               BREG=&BREG
.done     anop
          MEND
