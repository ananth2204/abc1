         TITLE  'SMP REXX INTERFACE'
***********************************************************************
*        PROGRAM  : RXGIMAPI                                          *
*        FUNCTION : A FRONT END TO GIMAPI (API) FROM SMP/E            *
*        AUTHOR   : GAETA (DFGAETA@BR.IBM.COM)                        *
***********************************************************************
         MACRO
         RXVAR  &FUNC='FETCH'
         MVC    RXFUNC,=CL8'&FUNC'
         LA     1,RXPLIST
         L      15,ALXEXCOM
         BALR   14,15
         MEND
*
BALAPI   CSECT
         STM    14,12,12(13)
         LR     12,15
@PSTART  EQU    BALAPI
         USING  @PSTART,12
         ST     13,SAVE+4
         LA     14,SAVE
         ST     14,8(13)
         LR     13,14
BALAPI   AMODE 31
*        RETRIEVE CSI NAME AND SET GIM CONTROL BLOCK
         XC    QUERY_PARMS,QUERY_PARMS
         RXVAR FUNC=FETCH
         LA    3,VDDATA1
         ST    3,PCSI
         LH    3,VDLEN1
         ST    3,CSILEN
*        RETRIEVE ZONE NAME AND SET GIM CONTROL BLOCK
         LA    3,VDDATA2
         ST    3,PZONE
         LH    3,VDLEN2
         ST    3,ZONELEN
*        RETRIEVE ENTRY NAME AND SET GIM CONTROL BLOCK
         LA    3,VDDATA3
         ST    3,PENTRY
         LH    3,VDLEN3
         ST    3,ENTRYLEN
*        RETRIEVE SUBENTRY NAME AND SET GIM CONTROL BLOCK
         LA    3,VDDATA4
         ST    3,PSUBENTRY
         LH    3,VDLEN4
         ST    3,SUBENTRYLEN
*        RETRIEVE FILTER NAME AND SET GIM CONTROL BLOCK
         LA    3,VDDATA5
         ST    3,PFILTER
         LH    3,VDLEN5
         ST    3,FILTERLEN
*
* NOW LOAD THE API
*
         LA    2,APIPGM
         LOAD EPLOC=(2),LOADPT=PGMADR
*
* NOW DO THE QUERY
*
         L     15,PGMADR
         CALL (15),(QUERYCMD,QUERY_PARMS@,CMDOUT,APILANG,RC,CC,MSG@)
*
*
* NOW SEE WHAT WAS RETURNED
*
         L     3,RC
         LTR   3,3
         BNZ   ERRPRINT
*
* ESTABLISH ADDRESSABILITY
*
         OPEN (SYSPRINT,OUTPUT)
         L     3,CMDOUT
         USING ENTRY_LIST,3
         L      4,ENTRIES
         USING CSIENTRY,4
         L      5,SUBENTRIES
         USING SUBENTRY,5
         L      6,SUBENTDATA
         USING ITEM_LIST,6
         L      7,DATA
         USING RETDATA,7
*
* PRINT ENTRIES
*
PRTENT   LTR    4,4
         BZ     CLOSEOUT
         MVI    BUFFER,C' '
         MVC    BUFFER+1(119),BUFFER
         MVC    BUFFER(8),ENTRYNAME
         MVC    BUFFER+10(7),ZONENAME
         LA     2,ANSICHAR
         PUT    SYSPRINT,(2)
         B      PRTSUB
MOREENT  L      4,CSINEXT
         B      PRTENT
*
* PRINT SUBENTRIES
*
PRTSUB   LTR    5,5
         BZ     MOREENT
         MVI    BUFFER,C' '
         MVC    BUFFER+1(119),BUFFER
         MVC    BUFFER(11),SUBTYPE
         PUT    SYSPRINT,(2)
         B      PRTDATA
MORESUB  L      5,SUBNEXT
         B      PRTSUB
*
* PRINT DATA
*
PRTDATA  LTR    7,7
         BZ     MORESUB
         L      8,DATALEN
CHECKLEN C      8,OUTRECLN
         BNH    SETLEN
         L      8,OUTRECLN
SETLEN   LA    15,1
         SLR    8,15
         MVI   BUFFER,C' '
         MVC   BUFFER+1(119),BUFFER
         EX     8,@MOVDATA
         LA   2,ANSICHAR
         PUT  SYSPRINT,(2)
         L      8,DATALEN
         C      8,OUTRECLN
         BNH    MORESUB
         S      8,OUTRECLN
         ST     8,DATALEN
         A      7,OUTRECLN
         B      CHECKLEN
*
* PRINT ERROR MESSAGES
*
ERRPRINT OPEN   (SYSPRINT,OUTPUT)
         L      6,MSG@
         L      7,DATA
         L      8,DATALEN
CHKMSGLN C      8,OUTRECLN
         BNH    SETMSGLN
         L      8,OUTRECLN
SETMSGLN LA     15,1
         SLR    8,15
         MVI    BUFFER,C' '
         MVC    BUFFER+1(119),BUFFER
         EX     8,@MOVDATA
         LA     2,ANSICHAR
         PUT    SYSPRINT,(2)
         L      8,DATALEN
         C      8,OUTRECLN
         BNH    CLOSEOUT
         S      8,OUTRECLN
         ST     8,DATALEN
         A      4,OUTRECLN
         B      CHKMSGLN
*
CLOSEOUT CLOSE  SYSPRINT
*
* NOW FREE THE STORAGE OBTAINED DURING THE QUERY
*
CLEANUP  L     15,PGMADR
         CALL (15),(FREECMD,0,CMDOUT,APILANG,RC,CC,MSG@)
*
* NOW DELETE GIMAPI
*
         DELETE EPLOC=APIPGM
*
* EXIT
*
EXIT     LA     15,0
         L      13,4(,13)
         L      14,12(,13)
         LM     00,12,20(13)
         BR     14
*
*
@MOVDATA MVC    BUFFER(0),RETDATA
SYSPRINT DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=PM,LRECL=121,RECFM=FBA
ANSICHAR DC     CL1' '
BUFFER   DS     CL120
*MYCSI    DC     CL26'WWP115.GSD2007B.GLOBAL.CSI'
*MYZONE   DC     CL7'DB2810T'
*MYENTRY  DC     CL6'SYSMOD'
*MYFILTER DC     CL18'SMODTYPE=''USERMOD'''
*MYSUBNTY DC     CL3'*'
APIPGM   DC     CL8'GIMAPI  '
QUERYCMD DC     CL8'QUERY   '
FREECMD  DC     CL8'FREE    '
APILANG  DC     CL3'ENU'
         DS     0F
QUERY_PARMS@ DC AL4(QUERY_PARMS)
PGMADR   DC     AL4(0)
CMDOUT   DC     AL4(0)
MSG@     DC     AL4(0)
OUTRECLN DC     F'120'
RC       DS     F'0'
CC       DS     F'0'
SAVE     DC     18F'0'
ALXEXCOM               DC    V(ALXEXCOM)
*
         DS    0D
LTSTORE  DC    CL8'STORE   '
LTFETCH  DC    CL8'FETCH   '
RXPLIST  DC    A(RXFUNC)
RXFUNC   DC    CL8' '
RXVCOUNT DC    F'5'
RXRC     DC    F'0'
VNLEN1   DC    AL4(6)
VNNAME1  DC    CL6'GIMCSI'
VDLEN1   DC    AL4(80)
VDDATA1  DC    CL80' '
VNLEN2   DC    AL4(7)
VNNAME2  DC    CL7'GIMZONE'
VDLEN2   DC    AL4(80)
VDDATA2  DC    CL80' '
VNLEN3   DC    AL4(8)
VNNAME3  DC    CL8'GIMENTRY'
VDLEN3   DC    AL4(80)
VDDATA3  DC    CL80' '
VNLEN4   DC    AL4(9)
VNNAME4  DC    CL9'GIMSENTRY'
VDLEN4   DC    AL4(80)
VDDATA4  DC    CL80' '
VNLEN5   DC    AL4(9)
VNNAME5  DC    CL9'GIMFILTER'
VDLEN5   DC    AL4(80)
VDDATA5  DC    CL80' '
         GIMMASM
RETDATA  DSECT  CL0
         END
