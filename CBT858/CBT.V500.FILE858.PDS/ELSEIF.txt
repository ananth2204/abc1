.*--------------------------------------------------------------------*
.*  Copyright 2011 by Clemson University Research Foundation          *
.*                                                                    *
.*  Licensed under the Apache License, Version 2.0 (the "License");   *
.*  you may not use this file except in compliance with the License.  *
.*  You may obtain a copy of the License in this library as member    *
.*  $LICENSE, or from                                                 *
.*                                                                    *
.*      http://www.apache.org/licenses/LICENSE-2.0                    *
.*                                                                    *
.*  Unless required by applicable law or agreed to in writing,        *
.*  software distributed under the License is distributed on an "AS   *
.*  IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either   *
.*  express or implied.  See the License for the specific language    *
.*  governing permissions and limitations under the License.          *
.*--------------------------------------------------------------------*
         MACRO
         ELSEIF  &IF=
.*--------------------------------------------------------------------*
.*
.*  (C) COPYRIGHT 1983.  CLEMSON UNIVERSITY COMPUTER CENTER.
.*
.*  MACRO      ELSEIF
.*
.*  TITLE      CLEMSON UNIVERSITY COMPUTER CENTER STRUCTURED
.*             PROGRAMMING MACROS - ELSEIF
.*
.*  PURPOSE    STRUCTURED MACRO USED TO START ELSEIF CODE.
.*
.*  NOTE       SEE THE FOLLOWING DOCUMENT FOR MORE INFORMATION ON
.*             THIS AND OTHER STRUCTURED PROGRAMMING MACROS:
.*
.*             . CLEMSON UNIVERSITY COMPUTER CENTER STRUCTURED
.*               PROGRAMMING MACROS USER'S GUIDE AND REFERENCE.
.*
.*  ACTIVITY   11/01/83 - NEW  MGG
.*              9/15/89 MGG - ADDED T=A TO ##COND MACRO CALL.       @C1
.*             08/02/05 MGG - Changed to use ##B macro for branching@C2
.*
.*--------------------------------------------------------------------*
         GBLA  &#MNDX                   LAST TAG INDEX USED
         GBLA  &#MLVL                   STRUCTURAL LEVEL
         GBLA  &#IMLVL(20)              STRUCTURAL LEVEL OF IF
         GBLA  &#ILVL                   IF NEXTING LEVEL
         GBLA  &#INDX(20)               IF TAG INDEX
         GBLA  &#INDX2(20)              IF TAG INDEX 2
         GBLB  &#IELSE(20)              ELSE ENCOUNTERED
         GBLC  &#INAME(20)              IF NAMES
         LCLA  &CTR                     WORK COUNT
         LCLC  &FTAG,&STAG,&TAG         FAIL TAG AND SUCCESS TAG
&#MNDX   SETA  &#MNDX+1                 ADD ONE TO TAG INDEX
         AIF   (&#ILVL LT 1).ERR0001    ERROR IF NO IF ACTIVE
         AIF   (&#IELSE(&#ILVL)).ERR0002 ERROR IF ELSE ENCOUNTERED
         AIF   ('&IF' EQ '').PRC0010    ERROR IF IF= DOES NOT MATCH
         AIF   ('&IF' NE '&#INAME(&#ILVL)').ERR0003
.PRC0010 AIF   (&#IMLVL(&#ILVL) NE &#MLVL).ERR0004
         ##B U,#IE&#INDX(&#ILVL)        GO TO ENDIF                 @C2
&TAG     SETC  '#IF&#INDX2(&#ILVL)'     SET TAG TO PREVIOUS FAIL TAG
&#INDX2(&#ILVL) SETA  &#MNDX            SET TAG INDEX 2
&FTAG    SETC  '#IF&#MNDX'              FAIL TAG NAME
&STAG    SETC  '#IS&#MNDX'              SUCCESS TAG NAME
&CTR     SETA  N'&SYSLIST               GET NUMBER OF POSITIONAL PARMS
&TAG     ##COND &SYSLIST(1),&SYSLIST(2),                               $
               &SYSLIST(3),&SYSLIST(4),&SYSLIST(5),&SYSLIST(6),        $
               &SYSLIST(7),&SYSLIST(8),&SYSLIST(9),&SYSLIST(10),       $
               &SYSLIST(11),&SYSLIST(12),&SYSLIST(13),&SYSLIST(14),    $
               &SYSLIST(15),&SYSLIST(16),&SYSLIST(17),&SYSLIST(18),    $
               &SYSLIST(19),&SYSLIST(20),&SYSLIST(21),&SYSLIST(22),    $
               &SYSLIST(23),&SYSLIST(24),&SYSLIST(25),&SYSLIST(26),    $
               &SYSLIST(27),&SYSLIST(28),&SYSLIST(29),&SYSLIST(30),    $
               &SYSLIST(31),&SYSLIST(32),&SYSLIST(33),                 $
               &SYSLIST(34),&SYSLIST(35),&SYSLIST(36),                 $
               &SYSLIST(37),&SYSLIST(38),&SYSLIST(39),                 $
               &SYSLIST(40),&SYSLIST(41),&SYSLIST(42),                 $
               &SYSLIST(43),&SYSLIST(44),&SYSLIST(45),                 $
               &SYSLIST(46),&SYSLIST(47),&SYSLIST(48),                 $
               &SYSLIST(49),&SYSLIST(50),&SYSLIST(51),                 $
               F=&FTAG,S=&STAG,E=&CTR,B=1,T=A                       @C1
&STAG    DS    0H                       START OF SUCCESSFUL ELSEIF CODE
         AGO   .EXIT
.ERR0001 MNOTE 8,'ELSEIF must be contained in IF structure'
         AGO   .EXIT
.ERR0002 MNOTE 8,'ELSE for this IF precedes ELSEIF'
         AGO   .EXIT
.ERR0003 MNOTE 8,'IF= does not match corresponding IF'
         AGO   .EXIT
.ERR0004 MNOTE 8,'ELSEIF structural level is not the same as correspond$
               ing IF'
.EXIT    MEND
