.*--------------------------------------------------------------------*
.*  Copyright 2011 by Clemson University Research Foundation          *
.*                                                                    *
.*  Licensed under the Apache License, Version 2.0 (the "License");   *
.*  you may not use this file except in compliance with the License.  *
.*  You may obtain a copy of the License in this library as member    *
.*  $LICENSE, or from                                                 *
.*                                                                    *
.*      http://www.apache.org/licenses/LICENSE-2.0                    *
.*                                                                    *
.*  Unless required by applicable law or agreed to in writing,        *
.*  software distributed under the License is distributed on an "AS   *
.*  IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either   *
.*  express or implied.  See the License for the specific language    *
.*  governing permissions and limitations under the License.          *
.*--------------------------------------------------------------------*
*---------------------------------------------------------------------*
*
*   (C) COPYRIGHT 1988.  CLEMSON UNIVERSITY COMPUTER CENTER.
*
*   MEMBER     SMSYMS
*
*   TITLE      CLEMSON UNIVERSITY COMPUTER CENTER STRUCTURED
*              PROGRAMMING MACROS - SMSYMS
*
*   PURPOSE    THIS COPY MEMBER CONTAINS CODE THAT NEEDS TO BE
*              COPIED INTO PROGRAMS THAT USE THE STRUCTURED
*              MACROS.
*
*   NOTE       SEE THE FOLLOWING DOCUMENT FOR MORE INFORMATION ON
*              THIS AND OTHER STRUCTURED PROGRAMMING MACROS:
*
*              . CLEMSON UNIVERSITY COMPUTER CENTER STRUCTURED
*                PROGRAMMING MACROS USER'S GUIDE AND REFERENCE.
*
*   ACTIVITY    9/1/88 - NEW  MGG
*               3/14/89 MGG - ADDED &P SYMBOL
*               3/23/89 MGG - ADDED &PWA AND &PSD SYMBOLS.
*               5/02/89 MGG - ADDED &SWA, &SSD, &SPL AND &PPL SYMBOLS.
*              11/03/98 MGG - ADDED STANDARD UCOND AND UDT.
*               8/10/05 MGG - Removed experimental definitions.
*                             Added SM_ macros.
*
*---------------------------------------------------------------------*
         GBLC  &PWA                PROGRAM WORK AREA ID
         GBLC  &PSD                PROGRAM STATIC DATA ID
         GBLC  &PPL                PROGRAM PARAMETER LIST
         GBLC  &SWA                SUBROUTINE WORK AREA ID
         GBLC  &W                  SHORTER NAME
         GBLC  &SSD                SUBROUTINE STATIC DATA ID
         GBLC  &S                  SHORTER NAME
         GBLC  &SPL                SUBROUTINE PARAMETER LIST
         GBLC  &P                  SHORTER NAME
.*--------------------------------------------------------------------*
.* Standard user conditions.
.*--------------------------------------------------------------------*
         DEFUCOND NAME=EXI,MACRO=##UCEXI
         DEFUCOND NAME=TRT,MACRO=##UCTRT
         DEFUCOND NAME=L16,MACRO=##UCL16
         DEFUCOND NAME=L32,MACRO=##UCL32
.*====================================================================*
.* SM_ macros.
.*====================================================================*
.* Move Long
         MACRO
&NAME    SM_MOVEL &TOREG,&FROMREG,&LENREG
         LCLA   &N
&N       SETA   N'&SYSLIST
&NAME    ##MOVEL &TOREG,&FROMREG,&LENREG,N=&N,MACNAME=SM_MOVEL
         MEND
.* Add halfword immediate.
         MACRO
&NAME    SM_AHI &REG,&VALUE
&NAME    ##AHI  &REG,&VALUE
         MEND
.* Load halfword immediate.
         MACRO
&NAME    SM_LHI &REG,&VALUE
&NAME    ##LHI  &REG,&VALUE
         MEND
.* Load address long.
         MACRO
&NAME    SM_LAL &REG,&TAG,&LPBASE=YES
&NAME    ##LAL &REG,&TAG,LPBASE=&LPBASE
         MEND
.* Increment fullword
         MACRO
&NAME    SM_INC &FULLWORD,&WREG=R14,&INC=1
&NAME    L     &WREG,&FULLWORD
         ##AHI &WREG,&INC
         ST    &WREG,&FULLWORD
         MEND
.* Increment halfword
         MACRO
&NAME    SM_INCH &HALFWORD,&WREG=R14,&INC=1
&NAME    LH    &WREG,&HALFWORD
         ##AHI &WREG,&INC
         STH   &WREG,&HALFWORD
         MEND
.* Increment register
         MACRO
&NAME    SM_INCR &REG,&INC=1
&NAME    ##AHI &REG,&INC
         MEND
.* Decrement fullword
         MACRO
&NAME    SM_DEC &FULLWORD,&WREG=R14,&DEC=1
&NAME    L     &WREG,&FULLWORD
         ##AHI &WREG,-(&DEC)
         ST    &WREG,&FULLWORD
         MEND
.* Decrement halfword
         MACRO
&NAME    SM_DECH &HALFWORD,&WREG=R14,&DEC=1
&NAME    LH    &WREG,&HALFWORD
         ##AHI &WREG,-(&DEC)
         STH   &WREG,&HALFWORD
         MEND
.* Decrement register
         MACRO
&NAME    SM_DECR &REG,&DEC=1
&NAME    ##AHI &REG,-(&DEC)
         MEND
.* Add fullword storage
         MACRO
&NAME    SM_AS &TO,&FROM,&WREG=R14
&NAME    L     &WREG,&FROM
         A     &WREG,&TO
         ST    &WREG,&TO
         MEND
.* Add halfword storage
         MACRO
&NAME    SM_ASH &TO,&FROM,&WREG=R14
&NAME    LH    &WREG,&FROM
         AH    &WREG,&TO
         STH   &WREG,&TO
         MEND
.* Subtract fullword storage
         MACRO
&NAME    SM_SS &TO,&FROM,&WREG=R14
&NAME    L     &WREG,&TO
         S     &WREG,&FROM
         ST    &WREG,&TO
         MEND
.* Subtract halfword storage
         MACRO
&NAME    SM_SSH &TO,&FROM,&WREG=R14
&NAME    LH    &WREG,&TO
         SH    &WREG,&FROM
         STH   &WREG,&TO
         MEND
*-- END OF SMSYMS ----------------------------------------------------*
