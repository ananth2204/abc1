.*--------------------------------------------------------------------*
.*  Copyright 2011 by Clemson University Research Foundation          *
.*                                                                    *
.*  Licensed under the Apache License, Version 2.0 (the "License");   *
.*  you may not use this file except in compliance with the License.  *
.*  You may obtain a copy of the License in this library as member    *
.*  $LICENSE, or from                                                 *
.*                                                                    *
.*      http://www.apache.org/licenses/LICENSE-2.0                    *
.*                                                                    *
.*  Unless required by applicable law or agreed to in writing,        *
.*  software distributed under the License is distributed on an "AS   *
.*  IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either   *
.*  express or implied.  See the License for the specific language    *
.*  governing permissions and limitations under the License.          *
.*--------------------------------------------------------------------*
         MACRO
         ##LA  &REG,                    REGISTER TO LOAD               $
               &FIELD                   FIELD TO LOAD
.*--------------------------------------------------------------------*
.*
.*  (C) COPYRIGHT 1985.  CLEMSON UNIVERSITY COMPUTER CENTER.
.*
.*  MACRO      ##LA
.*
.*  TITLE      CLEMSON UNIVERSITY COMPUTER CENTER STRUCTURED
.*             PROGRAMMING MACROS - ##LA
.*
.*  PURPOSE    INTERNAL MACRO USED BY PLIST AND CALLX MACRO.
.*
.*  NOTE       SEE THE FOLLOWING DOCUMENT FOR MORE INFORMATION ON
.*             THIS AND OTHER STRUCTURED PROGRAMMING MACROS:
.*
.*             . CLEMSON UNIVERSITY COMPUTER CENTER STRUCTURED
.*               PROGRAMMING MACROS USER'S GUIDE AND REFERENCE.
.*
.*  ACTIVITY    6/20/85 MGG - NEW
.*              8/31/88 MGG - ADDED *ADDR SUPPORT                   @C1
.*             11/03/98 MGG - ADDED UDT SUPPORT                     @C2
.*
.*--------------------------------------------------------------------*
         GBLC  &##DT__VALUE                                         @C2
         LCLC  &TAG                     WORK TAG
         AIF   ('&FIELD' NE '').X100
         SLR   &REG,&REG                NO PARAMETER SPECIFIED
         MEXIT
.X100    AIF   (T'&FIELD NE 'N').X200
         AIF   ('&FIELD'(1,1) LT '0' OR '&FIELD'(1,1) GT '9').ERR1
         LA    &REG,=F'&FIELD'          LOAD ADDR OF NUMERIC CONSTANT
         MEXIT
.X200    AIF   ('&FIELD'(1,1) NE '''').X300
         LA    &REG,=C&FIELD            LOAD ADDR OF CHARACTER STRING
         MEXIT
.X300    AIF   ('&FIELD'(1,1) NE '*').X400                          @C1
&TAG     SETC  '&FIELD'(2,K'&FIELD-1)                               @C1
         L     &REG,&TAG                                            @C1
         MEXIT                                                      @C1
.X400    AIF   ('&FIELD'(1,1) NE '%').X500                          @C2
         ##DTXTR &FIELD                                             @C2
         LA    &REG,=&##DT__VALUE                                   @C2
         MEXIT                                                      @C2
.X500    LA    &REG,&FIELD              LOAD ADDR OF PARAMETER      @C2
         MEXIT
.ERR1    MNOTE 8,'Decimal self-defining terms are the only type of self$
               -defining terms allowed'
         MEND
