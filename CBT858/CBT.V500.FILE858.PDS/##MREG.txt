.*--------------------------------------------------------------------*
.*  Copyright 2011 by Clemson University Research Foundation          *
.*                                                                    *
.*  Licensed under the Apache License, Version 2.0 (the "License");   *
.*  you may not use this file except in compliance with the License.  *
.*  You may obtain a copy of the License in this library as member    *
.*  $LICENSE, or from                                                 *
.*                                                                    *
.*      http://www.apache.org/licenses/LICENSE-2.0                    *
.*                                                                    *
.*  Unless required by applicable law or agreed to in writing,        *
.*  software distributed under the License is distributed on an "AS   *
.*  IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either   *
.*  express or implied.  See the License for the specific language    *
.*  governing permissions and limitations under the License.          *
.*--------------------------------------------------------------------*
         MACRO
&LABEL   ##MREG &TYPE,&REG
.*--------------------------------------------------------------------*
.*
.*  (C) COPYRIGHT 1986.  CLEMSON UNIVERSITY COMPUTER CENTER.
.*
.*  MACRO      ##MREG
.*
.*  TITLE      CLEMSON UNIVERSITY COMPUTER CENTER STRUCTURED
.*             PROGRAMMING MACROS - ##MREG
.*
.*  PURPOSE    INTERNAL MACRO USED TO KEEP UP WITH WHICH REGS ARE USED
.*
.*  NOTE       SEE THE FOLLOWING DOCUMENT FOR MORE INFORMATION ON
.*             THIS AND OTHER STRUCTURED PROGRAMMING MACROS:
.*
.*             . CLEMSON UNIVERSITY COMPUTER CENTER STRUCTURED
.*               PROGRAMMING MACROS USER'S GUIDE AND REFERENCE.
.*
.*  ACTIVITY    2/14/86 - NEW  MGG
.*              8/10/05 - MGG Added TYPE=CHECK.
.*
.*--------------------------------------------------------------------*
         GBLA  &#MCVREG
         GBLB  &#REG(16)
.*--------------------------------------------------------------------*
.*  IF TYPE IS RESET,  INDICATE ALL BUT R13-R1 AS FREE.
.*--------------------------------------------------------------------*
         AIF   ('&TYPE' NE 'RESET').PRC1000
&#REG(0+1)  SETB 1
&#REG(1+1)  SETB 1
&#REG(2+1)  SETB 0
&#REG(3+1)  SETB 0
&#REG(4+1)  SETB 0
&#REG(5+1)  SETB 0
&#REG(6+1)  SETB 0
&#REG(7+1)  SETB 0
&#REG(8+1)  SETB 0
&#REG(9+1)  SETB 0
&#REG(10+1) SETB 0
&#REG(11+1) SETB 0
&#REG(12+1) SETB 0
&#REG(13+1) SETB 1
&#REG(14+1) SETB 1
&#REG(15+1) SETB 1
            MEXIT
.*--------------------------------------------------------------------*
.*  IF TYPE IS USE, INDICATE REGISTER USED IF VALID AND NOT ALREADY
.*  USED.
.*--------------------------------------------------------------------*
.PRC1000 AIF   ('&TYPE' NE 'USE').PRC2000
         ##CVREG &REG
         AIF   (&#MCVREG LE 15).PRC1020
         MNOTE 8,'Register specification &REG is invalid'
         MEXIT
.PRC1020 AIF   (NOT &#REG(&#MCVREG+1)).PRC1040
         MNOTE 8,'Register &REG is a temporary register or reserved for+
                another purpose.'
.PRC1040 ANOP
&#REG(&#MCVREG+1) SETB 1                INDICATE REGISTER USED
         MEXIT
.*--------------------------------------------------------------------*
.*  IF TYPE IS RELEASE,  INDICATE REGISTER IS NOT USED IF VALID.
.*--------------------------------------------------------------------*
.PRC2000 AIF   ('&TYPE' NE 'RELEASE').PRC3000
         ##CVREG &REG
         AIF   (&#MCVREG LE 15).PRC2020
         MNOTE 8,'Register specification &REG is invalid'
         MEXIT
.PRC2020 AIF   (&#REG(&#MCVREG+1)).PRC2040
         MNOTE 8,'Register &REG was not used - internal error'
.PRC2040 ANOP
&#REG(&#MCVREG+1) SETB 0                INDICATE REGISTER USED
         MEXIT
.*--------------------------------------------------------------------*
.*  IF TYPE IS CHECK, ISSUE MNOTE IF REG ALREADY USED.  THIS IS USEFUL
.*  IF NEEDING TO CHECK REGISTER FOR TEMPORARY USE.
.*--------------------------------------------------------------------*
.PRC3000 AIF   ('&TYPE' NE 'CHECK').PRC4000
         ##CVREG &REG
         AIF   (&#MCVREG LE 15).PRC3020
         MNOTE 8,'Register specification &REG is invalid'
         MEXIT
.PRC3020 AIF   (NOT &#REG(&#MCVREG+1)).PRC3040
         MNOTE 8,'Register &REG is a temporary register or reserved for+
                another purpose.'
.PRC3040 ANOP
         MEXIT
.*--------------------------------------------------------------------*
.*  IF TYPE IS NOT ONE OF THE ABOVE, ISSUE ERROR MESSAGE.
.*--------------------------------------------------------------------*
.PRC4000 MNOTE 8,'Invalid type of &TYPE on ##MREG macro - internal erro$
               r'
         MEND
