.*--------------------------------------------------------------------*
.*  Copyright 2011 by Clemson University Research Foundation          *
.*                                                                    *
.*  Licensed under the Apache License, Version 2.0 (the "License");   *
.*  you may not use this file except in compliance with the License.  *
.*  You may obtain a copy of the License in this library as member    *
.*  $LICENSE, or from                                                 *
.*                                                                    *
.*      http://www.apache.org/licenses/LICENSE-2.0                    *
.*                                                                    *
.*  Unless required by applicable law or agreed to in writing,        *
.*  software distributed under the License is distributed on an "AS   *
.*  IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either   *
.*  express or implied.  See the License for the specific language    *
.*  governing permissions and limitations under the License.          *
.*--------------------------------------------------------------------*
         MACRO
         ##WPSC &TYPE
.*--------------------------------------------------------------------*
.*
.*  (C) COPYRIGHT 1998.  CLEMSON UNIVERSITY COMPUTER CENTER.
.*
.*  MACRO      ##WPSC
.*
.*  TITLE      CLEMSON UNIVERSITY COMPUTER CENTER STRUCTURED
.*             PROGRAMMING MACROS - ##WPSC
.*
.*  PURPOSE    INTERNAL MACRO USED TO GENERATE PROLOG FOR A SAS/C
.*             PWA.
.*
.*  NOTE       SEE THE FOLLOWING DOCUMENT FOR MORE INFORMATION ON
.*             THIS AND OTHER STRUCTURED PROGRAMMING MACROS:
.*
.*             . CLEMSON UNIVERSITY COMPUTER CENTER STRUCTURED
.*               PROGRAMMING MACROS USER'S GUIDE AND REFERENCE.
.*
.*  ACTIVITY    8/06/98 MGG - NEW
.*
.*--------------------------------------------------------------------*
         GBLA  &#PB#C                   PROGRAM BLOCK NUMBER
         GBLA  &#PBPLMX                 MAX PARMS IN PROGRAM
         GBLB  &#PBPPWA                 PARTIAL PWA ENCOUNTERED
         GBLB  &#PBPWA                  PROGRAM BLOCK PWA OPTION
         GBLC  &PWA                     PWA DSECT TAG
         GBLA  &#PBWAMX                 MAX WORDS OF SMWA NEEDED
         GBLB  &#SBSWA                  SWA OPTION ON SUBROUTINE BLOCK
         LCLA  &N
         LCLC  &TAG
         LCLC  &D
&D       SETC  '&PWA'
.*--------------------------------------------------------------------*
.*  IF TYPE=LAST, SUPPLY SWA FOR PREVIOUS SUBROUTINE IF MISSING.
.*--------------------------------------------------------------------*
         AIF   ('&TYPE' NE 'LAST').PRC0000
         AIF   (NOT &#SBSWA).PRC0000
         SWA   TYPE=LAST                FINISH LAST SWA
         ENDSWA ,
.PRC0000 ANOP
.*--------------------------------------------------------------------*
.*  REQUIRE THAT PWA OPTION BE SPECIFIED FOR PROGRAM.
.*--------------------------------------------------------------------*
         AIF   (&#PBPWA).PRC0010
         MNOTE 8,'PWA option should be specified on the program block m$
               acro'
.PRC0010 ANOP
.*--------------------------------------------------------------------*
.*  GENERATE PWA DSECT CARD.
.*--------------------------------------------------------------------*
&N       SETA  &#PB#C
&PWA DSECT                              PWA DSECT
.*--------------------------------------------------------------------*
.*  IF THIS IS THE FIRST CALL FOR THIS PWA, GENERATE DS'S FOR FIELDS
.*  THAT GO AT THE START OF THE PWA.
.*--------------------------------------------------------------------*
.PRC0100 AIF   (&#PBPPWA).PRC0200
&#PBPPWA SETB  1
*=====================================================================*
*  SAS/C DYNAMIC STORAGE AREA                                         *
*                                                                     *
*  MORE INFORMATION ABOUT THIS CONTROL BLOCK IS CONTAINED IN THE      *
*  SAS/C COMPILER AND LIBRARY USER'S REFERENCE.                       *
*                                                                     *
*=====================================================================*
         SPACE
         COPY   DSA
         SPACE
*=== END OF COPY =====================================================*
.PRC0200 ANOP
.*--------------------------------------------------------------------*
.*  IF THIS IS TYPE=PARTIAL, ALL THAT NEEDS TO BE DONE HAS BEEN DONE.
.*--------------------------------------------------------------------*
         AIF   ('&TYPE' EQ 'PARTIAL').PRC0900
.PRC0300 ANOP
.*--------------------------------------------------------------------*
.*  IF THIS IS TYPE=LAST, GENERATE FIELDS FOR SMWA AND PARAMETER LIST
.*  STORAGE.
.*--------------------------------------------------------------------*
         AIF   ('&TYPE' NE 'LAST').PRC0400
&#PBPPWA SETB  0
#PBW&N   DS    &#PBWAMX.A               SMWA
#PBP&N   DS    &#PBPLMX.A               PARAMETER LIST STORAGE
#PBU&N   DS    0X                       START OF USER STORAGE
         AGO   .PRC0900
.PRC0400 ANOP
.*--------------------------------------------------------------------*
.*  IF TYPE IS NOT ONE OF THE ABOVE THEN IT IS INVALID.
.*--------------------------------------------------------------------*
         MNOTE 8,'Invalid type &TYPE specified on PWA macro'
.PRC0900 ANOP
         MEND
