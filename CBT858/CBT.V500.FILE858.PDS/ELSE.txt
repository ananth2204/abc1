.*--------------------------------------------------------------------*
.*  Copyright 2011 by Clemson University Research Foundation          *
.*                                                                    *
.*  Licensed under the Apache License, Version 2.0 (the "License");   *
.*  you may not use this file except in compliance with the License.  *
.*  You may obtain a copy of the License in this library as member    *
.*  $LICENSE, or from                                                 *
.*                                                                    *
.*      http://www.apache.org/licenses/LICENSE-2.0                    *
.*                                                                    *
.*  Unless required by applicable law or agreed to in writing,        *
.*  software distributed under the License is distributed on an "AS   *
.*  IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either   *
.*  express or implied.  See the License for the specific language    *
.*  governing permissions and limitations under the License.          *
.*--------------------------------------------------------------------*
         MACRO
         ELSE  &IF=
.*--------------------------------------------------------------------*
.*
.*  (C) COPYRIGHT 1983.  CLEMSON UNIVERSITY COMPUTER CENTER.
.*
.*  MACRO      ELSE
.*
.*  TITLE      CLEMSON UNIVERSITY COMPUTER CENTER STRUCTURED
.*             PROGRAMMING MACROS - ELSE
.*
.*  PURPOSE    STRUCTURED MACRO USED TO START ELSE CODE.
.*
.*  NOTE       SEE THE FOLLOWING DOCUMENT FOR MORE INFORMATION ON
.*             THIS AND OTHER STRUCTURED PROGRAMMING MACROS:
.*
.*             . CLEMSON UNIVERSITY COMPUTER CENTER STRUCTURED
.*               PROGRAMMING MACROS USER'S GUIDE AND REFERENCE.
.*
.*  ACTIVITY   11/01/83 - NEW  MGG
.*             08/02/05 MGG - Changed to use ##B macro for branching@C1
.*
.*--------------------------------------------------------------------*
         GBLA  &#MLVL                   STRUCTURAL LEVEL
         GBLA  &#IMLVL(20)              STRUCTURAL LEVEL OF IF
         GBLA  &#ILVL                   IF NEXTING LEVEL
         GBLA  &#INDX(20)               IF TAG INDEX
         GBLA  &#INDX2(20)              IF TAG INDEX 2
         GBLB  &#IELSE(20)              ELSE ENCOUNTERED
         GBLC  &#INAME(20)              IF NAMES
         LCLC  &TAG                     WORK TAG
         AIF   (&#ILVL LT 1).ERR0001
         AIF   (&#IELSE(&#ILVL)).ERR0002
         AIF   ('&IF' EQ '').PRC0010
         AIF   ('&IF' NE '&#INAME(&#ILVL)').ERR0003
.PRC0010 AIF   (&#IMLVL(&#ILVL) NE &#MLVL).ERR0004
         ##B U,#IE&#INDX(&#ILVL)        GO TO ENDIF                 @C1
&TAG     SETC  '#IF&#INDX2(&#ILVL)'     SET TAG TO PREVIOUS FAIL TAG
&TAG     DS    0H                       START OF ELSE CODE
&#IELSE(&#ILVL) SETB 1
         AGO   .EXIT
.ERR0001 MNOTE 8,'IF nexting level is zero'
         AGO   .EXIT
.ERR0002 MNOTE 8,'ELSE already encountered for this IF'
         AGO   .EXIT
.ERR0003 MNOTE 8,'NAME= does not match corresponding IF'
         AGO   .EXIT
.ERR0004 MNOTE 8,'ELSE structural level is not the same as the correspo$
               nding IF'
.EXIT    MEND
