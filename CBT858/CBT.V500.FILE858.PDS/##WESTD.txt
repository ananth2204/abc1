.*--------------------------------------------------------------------*
.*  Copyright 2011 by Clemson University Research Foundation          *
.*                                                                    *
.*  Licensed under the Apache License, Version 2.0 (the "License");   *
.*  you may not use this file except in compliance with the License.  *
.*  You may obtain a copy of the License in this library as member    *
.*  $LICENSE, or from                                                 *
.*                                                                    *
.*      http://www.apache.org/licenses/LICENSE-2.0                    *
.*                                                                    *
.*  Unless required by applicable law or agreed to in writing,        *
.*  software distributed under the License is distributed on an "AS   *
.*  IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either   *
.*  express or implied.  See the License for the specific language    *
.*  governing permissions and limitations under the License.          *
.*--------------------------------------------------------------------*
         MACRO
         ##WESTD
.*--------------------------------------------------------------------*
.*
.*  (C) COPYRIGHT 1986.  CLEMSON UNIVERSITY COMPUTER CENTER.
.*
.*  MACRO      ##WESTD
.*
.*  TITLE      CLEMSON UNIVERSITY COMPUTER CENTER STRUCTURED
.*             PROGRAMMING MACROS - ##WESTD
.*
.*  PURPOSE    INTERNAL MACRO USED TO GENERATE EPILOG FOR A STANDARD
.*             PWA.
.*
.*  NOTE       SEE THE FOLLOWING DOCUMENT FOR MORE INFORMATION ON
.*             THIS AND OTHER STRUCTURED PROGRAMMING MACROS:
.*
.*             . CLEMSON UNIVERSITY COMPUTER CENTER STRUCTURED
.*               PROGRAMMING MACROS USER'S GUIDE AND REFERENCE.
.*
.*  ACTIVITY    1/06/87 MGG - NEW
.*              5/08/89 MGG - CHANGED &#PBWAID TO &PWA              @C1
.*              7/25/97 JCB - Added support for ##SECT              @C2
.*              8/07/05 MGG - Changed to generate LTORG if this is  @C3
.*                            not a partial PWA END.  The LTORG is  @C3
.*                            placed in the PSD if LOCTRs are being @C3
.*                            used.  This was put in to support     @C3
.*                            NOCODEBASE since the default LTORG    @C3
.*                            would be in code and not data.        @C3
.*
.*--------------------------------------------------------------------*
         GBLA  &#CSF(500)               CALLSUB FROM
         GBLA  &#CSFF(99)               FIRST FROM INDEX
         GBLA  &#CSFT#N                 CALLSUB FROM/TO NEXT NUMBER
         GBLA  &#CST(500)               CALLSUB TO
         GBLA  &#PB#C                   PROGRAM BLOCK NUMBER
         GBLB  &#PBDSWA                 ENDPWA SHOULD DO DS
         GBLB  &#PBDYN                  PROGRAM BLOCK DYN OPTION
         GBLC  &#PBDYNS                 DYNSIZE KEYWORD
         GBLB  &#PBLOCR                 ENDPWA SHOULD DO DS
         GBLC  &#PBNAME                 CSECT NAME OF PROGRAM BLOCK
         GBLB  &#PBPPWA                 PARTIAL PWA ENCOUNTERED
         GBLC  &PWA                     PWA ID                      @C1
         GBLA  &#SB#CPB                 'SUB #' OF CURRENT PROG BLK
         GBLA  &#SB#M                   MAX SUBROUTINE NUMBER
         GBLC  &#SBNM(99)               SUBROUTINE NAMES
         LCLA  &CTR
         LCLA  &N
         LCLC  &TAG
&N       SETA  &#PB#C
.*--------------------------------------------------------------------*
.*  IF THIS IS A PARTIAL PWA, JUST SWITCH BACK TO CSECT.
.*--------------------------------------------------------------------*
.PRC0100 AIF   (NOT &#PBPPWA).PRC0200                               @C3
&#PBNAME ##SECT ,                                                   @C3
         MEXIT                                                      @C3
.*--------------------------------------------------------------------*
.*  IF DYN OPTION IN EFFECT, RESERVE ROOM FOR SUBROUTINE WORK AREAS.
.*--------------------------------------------------------------------*
.PRC0200 AIF   (NOT &#PBDYN).PRC0800                                @C3
#PBX&N   DS    0D                   EXTRA STORAGE FOR SWA'S AND PWA'S
         AIF   ('&#PBDYNS' EQ '').PRC0300
         AIF   ('&#PBDYNS'(1,1) EQ '-').PRC0300
         AIF   ('&#PBDYNS'(1,1) EQ '+').PRC0300
         DS    (&#PBDYNS)X
         AGO   .PRC0800
.PRC0300 ANOP
&CTR     SETA  &#SB#CPB
.PRC0310 AIF   (&CTR GT &#SB#M).PRC0315
         MNOTE *,'&#SBNM(&CTR)(&CTR) first from indes is &#CSFF(&CTR)'
&CTR     SETA  &CTR+1
         AGO   .PRC0310
.PRC0315 ANOP
&CTR     SETA  &#SB#CPB
.PRC0320 AIF   (&CTR GE &#CSFT#N).PRC0330
         MNOTE *,'&CTR CALLSUB from &#SBNM(&#CSF(&CTR))(&#CSF(&CTR)) to$
                &#SBNM(&#CST(&CTR))(&#CST(&CTR))'
&CTR     SETA  &CTR+1
         AGO   .PRC0320
.PRC0330 ##ORGS &#SB#CPB,&#SB#CPB       DO ORGS
         ORG   ,
         AIF   ('&#PBDYNS' EQ '').PRC0700
         ORG   *&#PBDYNS
.PRC0700 DS    16F                      64 EXTRA BYTES
.*--------------------------------------------------------------------*
.*  GENERATE LENGTH TAG.
.*--------------------------------------------------------------------*
.PRC0800 DS    0D                       INSURE DOUBLEWORD ALIGNMENT
&TAG     SETC  '&PWA.PLLEN'                                         @C1
&TAG     EQU   *-&PWA                   LENGTH OF PWA               @C1
.*------------------------------------------------------------------@C3
.*  SWITCH BACK TO CSECT/RSECT.  MAKE SURE THAT THE LITERAL POOL    @C3
.*  WILL BE WITH DATA AND NOT CODE.  GENERATE DS FOR PWA IF NEEDED. @C3
.*------------------------------------------------------------------@C3
&#PBNAME ##SECT ,                                                   @C3
         AIF   (NOT &#PBLOCR).PRC0810                               @C3
         PSD                                                        @C3
.PRC0810 AIF   (NOT &#PBDSWA).PRC0820                               @C3
#PBJ&N   DS    0D                       ALIGN ON DOUBLE WORD        @C3
         DS    (&PWA.PLLEN)X            PWA STORAGE AREA            @C3
.PRC0820 LTORG ,                                                    @C3
         AIF   (NOT &#PBLOCR).PRC0830                               @C3
         ENDPSD                                                     @C3
.PRC0830 ANOP                                                       @C3
.*------------------------------------------------------------------@C3
         MEND
