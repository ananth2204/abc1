.*--------------------------------------------------------------------*
.*  Copyright 2011 by Clemson University Research Foundation          *
.*                                                                    *
.*  Licensed under the Apache License, Version 2.0 (the "License");   *
.*  you may not use this file except in compliance with the License.  *
.*  You may obtain a copy of the License in this library as member    *
.*  $LICENSE, or from                                                 *
.*                                                                    *
.*      http://www.apache.org/licenses/LICENSE-2.0                    *
.*                                                                    *
.*  Unless required by applicable law or agreed to in writing,        *
.*  software distributed under the License is distributed on an "AS   *
.*  IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either   *
.*  express or implied.  See the License for the specific language    *
.*  governing permissions and limitations under the License.          *
.*--------------------------------------------------------------------*
         MACRO
         CASEBLK &REG=,&MULT=1,&WREG=,&NAME=
.*--------------------------------------------------------------------*
.*
.*  (C) COPYRIGHT 1983.  CLEMSON UNIVERSITY COMPUTER CENTER.
.*
.*  MACRO      CASEBLK
.*
.*  TITLE      CLEMSON UNIVERSITY COMPUTER CENTER STRUCTURED
.*             PROGRAMMING MACROS - CASEBLK
.*
.*  PURPOSE    STRUCTURED MACRO USED TO START A CASE BLOCK.
.*
.*  NOTE       SEE THE FOLLOWING DOCUMENT FOR MORE INFORMATION ON
.*             THIS AND OTHER STRUCTURED PROGRAMMING MACROS:
.*
.*             . CLEMSON UNIVERSITY COMPUTER CENTER STRUCTURED
.*               PROGRAMMING MACROS USER'S GUIDE AND REFERENCE.
.*
.*  ACTIVITY   11/01/83 - mgg New
.*              9/23/93 - mgg Allow case number up to 501*mult.
.*             08/02/05 MGG - Changed to use ##B macro for branching@C1
.*                            Changed to use ADCON table in PSD if  @C1
.*                            &SYSALVL is >= 1.                     @C1
.*
.*--------------------------------------------------------------------*
         GBLA  &SYSALVL                 ARCH LEVEL (SYSSTATE MACRO) @C1
         GBLA  &#MNDX                   LAST TAG INDEX USED
         GBLA  &#MLVL                   STRUCTURAL LEVEL
         GBLA  &#CMLVL(20)              STRUCTURAL LEVEL
         GBLA  &#CLVL                   CASEBLK LEVEL
         GBLA  &#CNDX(20)               CASEBLK BLOCK TAG INDEX
         GBLB  &#COTH(20)               CASEBLK OTHER ENCOUNTERED
         GBLB  &#CCAS(20)               CASEBLK ENCOUNTERED
         GBLC  &#CNAME(20)              CASEBLK NAMES
         GBLA  &#CMULT(20)              CASEBLK MULT FACTOR
         GBLA  &#CMAXC(20)              MAX CASE NUM IN THIS CASEBLK
         GBLA  &#CTAGS(10020)  20*501   CASE TAGS
         LCLA  &CTR                     WORK COUNT
         LCLC  &TAG                     WORK TAG
         LCLC  &R                       REGISTER USED
&#MNDX   SETA  &#MNDX+1                 ADD ONE TO TAG INDEX
         AIF   (&#CLVL GE 20).ERR0001
&#CLVL   SETA  &#CLVL+1                 ADD ONE TO CASEBLK LEVEL
&#MLVL   SETA  &#MLVL+1                 ADD ONE TO STRUCTURAL LEVEL
&#CMLVL(&#CLVL) SETA &#MLVL             SAVE LEVEL FOR THIS CASEBLK
&#CNDX(&#CLVL)  SETA &#MNDX             SET BLOCK TAG INDEX
&#COTH(&#CLVL) SETB  0                  CLEAR CASE OTHER FLAG
&#CCAS(&#CLVL) SETB  0                  CLEAR CASE FLAG
&#CNAME(&#CLVL) SETC '&NAME'            SAVE NAME
&#CMULT(&#CLVL) SETA &MULT              SET MULT FACTOR
&#CMAXC(&#CLVL) SETA 0                  SET MAX CASE TO 0
&CTR     SETA  500                      CLEAR
.PRC0010 ANOP                             CASE
&#CTAGS((&#CLVL-1)*501+1+&CTR) SETA 0            TAGS
&CTR     SETA  &CTR-1
         AIF   (&CTR GE 0).PRC0010
.PRC0050 ANOP
&R       SETC  '&WREG'                  SET REGISTER
         AIF   ('&R' NE '').PRC0060
&R       SETC  '&REG'                   DEFAULT TO REG=
         AIF   ('&R' EQ '').ERR0002
         AGO   .PRC0070
.PRC0060 LR    &R,&REG                  LOAD WORK REGISTER
.PRC0070 ANOP
.PRC0100 AIF   (&SYSALVL GE 1).PRC0300                              @C1
.*- Use branch offset table.                                        @C1
         AIF   ('&MULT' NE '1').PRC0110                             @C1
         SLA   &R,1                     MULTIPLY CASE NUMBER BY 2
         AGO   .PRC0200
.PRC0110 AIF   ('&MULT' NE '2').PRC0120
.*       NO SCALEING FOR MULT=2
         AGO   .PRC0200
.PRC0120 AIF   ('&MULT' NE '4').PRC0130
         SRA   &R,1                     DIVIDE CASE BY 2
         AGO   .PRC0200
.PRC0130 AIF   ('&MULT' NE '8').PRC0140
         SRA   &R,2                     DIVIDE CASE BY 4
         AGO   .PRC0200
.PRC0140 ANOP
         MNOTE 8,'Invalid MULT= specified. Must be 1, 2, 4, or 8'
.PRC0200 ANOP                                                       @C1
         CH    &R,#CM&#CNDX(&#CLVL)      CHECK FOR MAX
         BH    #CO&#CNDX(&#CLVL)         GO TO CASE OTHER IF HIGH   @C1
         LH    &R,#CT&#CNDX(&#CLVL).(&R) LOAD DISPLACEMENT TO CASE
&TAG     SETC  '#CS&#CNDX(&#CLVL)'
&TAG     B     *(&R)                    BRANCH TO CASE
         AGO   .EXIT
.*- Use address table.                                              @C1
.PRC0300 ANOP                                                       @C1
         TREG  &R                        CHECK CASE                 @C1
         ##B M,#CO&#CNDX(&#CLVL)         GO TO CASE OTHER IF MINUS  @C1
         CHI   &R,#CM&#CNDX(&#CLVL)      CHECK FOR MAX              @C1
         ##B H,#CO&#CNDX(&#CLVL)         GO TO CASE OTHER IF HIGH   @C1
         AIF   ('&MULT' NE '1').PRC0310                             @C1
         SLA   &R,2                      MULTIPLY CASE NUMBER BY 4  @C1
         AGO   .PRC0400                                             @C1
.PRC0310 AIF   ('&MULT' NE '2').PRC0320                             @C1
         SLA   &R,1                      MULTIPLY CASE NUMBER BY 2  @C1
         AGO   .PRC0400                                             @C1
.PRC0320 AIF   ('&MULT' NE '4').PRC0330                             @C1
.*       NO SCALEING FOR MULT=4                                     @C1
         AGO   .PRC0400                                             @C1
.PRC0330 AIF   ('&MULT' NE '8').PRC0340                             @C1
         SRA   &R,1                      DIVIDE CASE BY 2           @C1
         AGO   .PRC0400                                             @C1
.PRC0340 ANOP                                                       @C1
         MNOTE 8,'Invalid MULT= specified. Must be 1, 2, 4, or 8'   @C1
.PRC0400 ANOP                                                       @C1
         AL    &R,=A(#CT&#CNDX(&#CLVL).) LOAD ADDRESS OF CASE THIS  @C1
         L     &R,0(,&R)                 WAY BECAUSE THE VECTOR OF  @C1
*                                        CASE ADDRESSES IS AT BOTTOM@C1
*                                        OF PSD.                    @C1
         BR    &R                        BRANCH TO CASE.            @C1
         AGO   .EXIT                                                @C1
.ERR0001 MNOTE 8,'CASEBLK can not be nested more than 20 deep'
         AGO   .EXIT
.ERR0002 MNOTE 8,'REG= must be specified'
         AGO   .EXIT
.EXIT    MEND
