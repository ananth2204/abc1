         MACRO
&sLabel  LOG   &LEVEL=NONE,&WRITER=Say
.**********************************************************************
.*                                                                   **
.* FUNCTION - Generate and conditionally execute code to log a       **
.*            message.                                               **
.*                                                                   **
.* ON ENTRY - &LEVEL   = Message level between 0 and 255.  The       **
.*                       message is not logged if the current level  **
.*                       as defined by cLogLevel is lower than this  **
.*                       value.                                      **
.*                                                                   **
.**********************************************************************

         AIF   ('&sLabel' EQ '').NOLABEL
&sLabel  DS    0H
.NOLABEL ANOP

         AIF   ('&LEVEL' EQ 'NONE').NONE

         CLI   cLogLevel,&LEVEL   Check current logging level
         JL    L&SYSNDX           Jump if low

.NONE    ANOP
         AIF   (N'&SYSLIST EQ 0).S0,                                   X
               (N'&SYSLIST EQ 1).S1,                                   X
               (N'&SYSLIST EQ 2).S2,                                   X
               (N'&SYSLIST EQ 3).S3,                                   X
               (N'&SYSLIST EQ 4).S4
         AGO   .S4

.S0      ANOP
         SAY   &sLabel,WRITER=&WRITER
         AGO   .EXIT

.S1      ANOP
         SAY   &SYSLIST(1),WRITER=&WRITER
         AGO   .EXIT

.S2      ANOP
         SAY   &SYSLIST(1),&SYSLIST(2),WRITER=&WRITER
         AGO   .EXIT

.S3      ANOP
         SAY   &SYSLIST(1),&SYSLIST(2),&SYSLIST(3),WRITER=&WRITER
         AGO   .EXIT

.S4      ANOP
         SAY   &SYSLIST(1),&SYSLIST(2),&SYSLIST(3),&SYSLIST(4),WRITER=&X
               WRITER
         AGO   .EXIT

.EXIT    ANOP
         AIF   ('&LEVEL' EQ 'NONE').NONE2
L&SYSNDX DS    0H
.NONE2   ANOP
         MEND
