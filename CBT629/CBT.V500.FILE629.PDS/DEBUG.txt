         MACRO
&sLabel  DEBUG &sText
.**********************************************************************
.*                                                                   **
.* FUNCTION - Conditionally ASSEMBLE code to log a debug message.    **
.*                                                                   **
.*                                                                   **
.* ON ENTRY - &sText   = Message to be logged                        **
.*                    or ON if code generation is to be enabled      **
.*                    or OFF if code generation is to be disabled    **
.*                                                                   **
.*                                                                   **
.**********************************************************************

         GBLB  &bDebug
         AIF   ('&sText' EQ 'ON').ON
         AIF   ('&sText' EQ 'OFF').OFF
         AIF   (NOT &bDebug).EXIT

         AIF   (N'&SYSLIST EQ 0).S0,                                   X
               (N'&SYSLIST EQ 1).S1,                                   X
               (N'&SYSLIST EQ 2).S2,                                   X
               (N'&SYSLIST EQ 3).S3,                                   X
               (N'&SYSLIST EQ 4).S4
         AGO   .S4

.S0      ANOP
&sLabel  SAY   &sLabel
         MEXIT

.S1      ANOP
&sLabel  SAY   &SYSLIST(1)
         MEXIT

.S2      ANOP
&sLabel  SAY   &SYSLIST(1),&SYSLIST(2)
         MEXIT

.S3      ANOP
&sLabel  SAY   &SYSLIST(1),&SYSLIST(2),&SYSLIST(3)
         MEXIT

.S4      ANOP
&sLabel  SAY   &SYSLIST(1),&SYSLIST(2),&SYSLIST(3),&SYSLIST(4)
         MEXIT

.ON      ANOP
&bDebug  SETB  1
         MEXIT

.OFF     ANOP
&bDebug  SETB  0
         MEXIT

.EXIT    ANOP
         AIF   ('&sLabel' EQ '').MEND
&sLabel  DS    0H
.MEND    ANOP
         MEND
