         MACRO
&sLabel  C2X   &sTarget,&sSource,&n,&z
.**********************************************************************
.*                                                                   **
.* FUNCTION - Converts binary to displayable hexadecimal. It is      **
.*            named after the REXX c2x funciton and has that         **
.*            function's semantics.  The length of the source field  **
.*            determines the length of the target field.  The source **
.*            field must be no longer than 7 bytes so as to avoid    **
.*            the author having to think too hard.                   **
.*                                                                   **
.* ON ENTRY - &sTarget = Label of storage to receive result          **
.*            &sSource = Label of source storage to be converted     **
.*            &n       = Number of source bytes                      **
.*            &z       = NOTRAIL if a trailing blank is to be omitted**
.*                                                                   **
.* EXAMPLES - sSource DC X'0123'         (Binary string)             **
.*            sTarget DC CL4             (Displayable hex)           **
.*            C2X sTarget,sSource        Converts the data at sSource**
.*                                       to C'0123' and stores it at **
.*                                       sTarget.                    **
.*                                                                   **
.**********************************************************************

         AIF   ('&sLabel' EQ '').NOLABEL
&sLabel  DS    0H
.NOLABEL ANOP

         UNPK  &sTarget.(2*&n+1),&sSource.(&n+1)
         TR    &sTarget.(2*&n),=C'0123456789ABCDEF'-C'0'

         AIF   ('&z' EQ 'NOTRAIL').NOTRAIL
         MVI   &sTarget+2*&n,C' '
.NOTRAIL ANOP

         MEND
