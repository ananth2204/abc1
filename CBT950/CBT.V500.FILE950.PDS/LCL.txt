/* REXX ***************************************************************/
/*%COPYRIGHT (C) Norbert Haas, U.N.P. - Software GmbH                 */
/* -------------------------------------------------------------------*/
/* Name:       LCL Long CommandLine                                   */
/* Short:      gives you a long command line                          */
/* Type:       REXX-Macro                                             */
/* Author:     Norbert Haas, noaDatex.de                              */
/* Syntax:     LCL                                                    */
/* Parameter:  none                                                   */
/*                                                                    */
/* Version Autor         Datum      Anlass                            */
/* ------- ------------- ---------- --------------------------------- */
/* V02.03  N.Haas        02.01.2017 englich version                   */
/* V02.02  N.Haas        11.01.2008 Zweite Eingabezeile               */
/* V02.01  N.Haas        24.02.2003 Panel ganz nach unten             */
/* V02.00  N.Haas        10.09.2002 zuz}glich 10 "alte" Zeilen        */
/* V01.00  N.Haas        04.07.2002 Erstellung                        */
/**********************************************************************/
"ISREDIT MACRO"
"ISPEXEC VGET (lcl01, lcl02, lcl03, lcl04, lcl05) PROFILE"
"ISPEXEC VGET (lcl06, lcl07, lcl08, lcl09, lcl10) PROFILE"
"ISPEXEC VGET (zscreend,zscreenw)"

"ISPEXEC ADDPOP ROW("zscreend-24") COLUMN(0)"
Show_Panel:
"ISPEXEC DISPLAY PANEL(LCLP)"

IF rc = 0 THEN DO
   IF cursor = 'RETRIEVE' THEN SIGNAL Show_Panel

   IF cmd2 /= "" THEN DO
      cmd = cmd || cmd2
      cmd2 = ""
   END

   IF TRANSLATE(LEFT(cmd,1)) = "F" &,
      LEFT(WORD(cmd, 2),1) /= "'" &,
      LEFT(WORD(cmd, 2),1) /= '"' THEN cmd = "F '"SUBSTR(cmd, 3)"'"

   "ISREDIT" cmd
   DO i = 10 TO 2 BY -1
      ii = TRANSLATE(FORMAT(i,  2),'0',' ')
      jj = TRANSLATE(FORMAT(i-1,2),'0',' ')
      INTERPRET "lcl"ii" = lcl"jj
   END
   lcl01 = cmd
   "ISPEXEC VPUT (lcl01, lcl02, lcl03, lcl04, lcl05) PROFILE"
   "ISPEXEC VPUT (lcl06, lcl07, lcl08, lcl09, lcl10) PROFILE"
END
"ISPEXEC REMPOP"
