DATE: PROCEDURE
   /* --------------------------------------------------------------- */
   /* Function:  DATE (must be imported into any other REXX-Exec, so  */
   /*            that it becomes an internal function!)               */
   /* Syntax:    x = DATE(format Ý,length¨)                           */
   /* Parameter: format = 'G' (German) or 'DSN' or any other format   */
   /*            of the builtin function DATE                         */
   /*          - If G, then length = 8 or 10                          */
   /*            ==>   8 = dd.mm.yy   (8 is default)                  */
   /*            ==>  10 = dd.mm.yyyy                                 */
   /*          - IF DSN, length is obsolete                           */
   /*            ==>     = #jjmmtt                                    */
   /* Examples:  SAY DATE()                                           */
   /*            SAY DATE('S')                                        */
   /*            SAY DATE('U')                                        */
   /*            SAY DATE('G')                                        */
   /*            SAY DATE('G', 8)                                     */
   /*            SAY DATE('G',10)                                     */
   /*            SAY USERID() || ".ABC." || DATE('DSN')               */
   /* Return:    4 Jan 2017                                           */
   /*            20170104                                             */
   /*            01/04/17                                             */
   /*            04.01.17                                             */
   /*            04.01.17                                             */
   /*            04.01.2017                                           */
   /*            NHAAS.ABC.#170104                                    */
   /*                                                                 */
   /* Vers.  Datum      Autor     Grund                               */
   /* ------ ---------- --------- ----------------------------------- */
   /* V01.02 03.01.2017 N.Haas    English Version                     */
   /* V01.01 29.02.2015 N.Haas    Creation, noaDatex.de               */
   /* --------------------------------------------------------------- */
   PARSE UPPER ARG a, l

   SELECT
      WHEN a = 'G' THEN DO
         tmp = "DATE"('E')                              /* tt/mm/jj   */
         tmp = TRANSLATE(tmp,'.','/')                   /* tt.mm.jj   */
         IF l = '' | l = 8 THEN NOP
         ELSE DO
            IF l = 10 THEN DO                           /* 03.01.2017 */
               tmp = LEFT(tmp,6) || RIGHT("DATE"(),4)
            END
            ELSE SAY "ERROR bei Aufruf DATE('G',len)"
         END
      END
      WHEN a = 'DSN' THEN DO
         tmp = "DATE"('S')                              /* jjjjmmtt   */
         tmp = "#" || SUBSTR(tmp, 3)                    /* jjmmtt     */
      END
      OTHERWISE DO
         IF a = "" THEN tmp = "DATE"()
                   ELSE tmp = "DATE"(a)
      END
   END
RETURN tmp
