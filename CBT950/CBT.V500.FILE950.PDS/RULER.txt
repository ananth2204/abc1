/* REXX ------------------------------------------------------------- */
/* Name:         RULER                                                */
/* Type:         Macro                                                */
/* Author:       Norbert Haas                                         */
/* Date:         21.12.2017                                           */
/* Syntax:       Linecommand RULER and cursor within data             */
/* Parameter:    none                                                 */
/* Description:  Shows a ruler from the cursor position               */
/* Example:      X <== Cursor is here when RULER starts               */
/*               ----+----1----+----2----+----3----+----4----+----5...*/
/* Comment:      Works perfect, if command RULER is on a PF-key       */
/*                                                                    */
/* Vers.  Date       Author    Reason                                 */
/* ------ ---------- --------- -------------------------------------- */
/* V02.01 21.12.2017 N.Haas    New Version (engl.)                    */
/* V01.01 29.02.2007 N.Haas    Creation (@ Ford)                      */
/* ------------------------------------------------------------------ */
"ISREDIT MACRO (parm) NOPROCESS"
"ISREDIT (dw) = DATA_WIDTH"
"ISREDIT (dmy, col) = CURSOR"
if col = 0 then col = 1        /* added by LBD */
ruler = COPIES(' ', col-1)
cnt = 0
rl = dw - col
DO i = col TO col + rl
   cnt = cnt + 1
   SELECT
      WHEN cnt // 10 = 0 THEN ruler = ruler || RIGHT(cnt / 10, 1)
      WHEN cnt //  5 = 0 THEN ruler = ruler || '+'
      OTHERWISE               ruler = ruler || '-'
   END
END
"ISREDIT LINE_AFTER .ZCSR = INFOLINE (ruler)"
EXIT
