/* REXX ------------------------------------------------------------- */
/* Name:         FLAGCHGS                                             */
/* Type:         Macro                                                */
/* Author:       Norbert Haas, noaDatex.de                            */
/* Date:         08.02.2011                                           */
/* Syntax:       FLAGCHGS Ý-/+¨ ÝX/NX¨                                */
/*   also:       FLAGCHGS ÝSHOW/DO¨ ÝX/NX¨                            */
/* Parameter:    - / SHOW = Show all then changes (default)           */
/*               + / DO   = Mark all the changes                      */
/* Description:  Show / mark all the changes in cobol programms in    */
/*               column 1 - 6.                                        */
/*                                                                    */
/* Vers.  Datum      Autor     Grund                                  */
/* ------ ---------- --------- -------------------------------------- */
/* V01.08 03.01.2017 N.Haas    English version                        */
/* V01.07 06.06.2012 N.Haas    LOCATE verbessert                      */
/* V01.06 12.04.2012 N.Haas    LOCATE verbessert                      */
/* V01.05 03.01.2012 N.Haas    LOCATE verbessert                      */
/* V01.04 09.11.2011 N.Haas    X/NX eingeführt                        */
/* V01.03 01.06.2011 N.Haas    RES LAB raus                           */
/* V01.02 25.02.2011 N.Haas    Parm Default = '-'                     */
/* V01.01 08.02.2011 N.Haas    Erstellung                             */
/* ------------------------------------------------------------------ */
"ISREDIT MACRO (parm) NOPROCESS"
"ISREDIT (fl,ll) = DISPLAY_LINES"

/* Inits */
TRUE  = 1
FALSE = 0
not_x = FALSE
loc   = FALSE
set   = FALSE

/* Parameter */
parm = TRANSLATE(parm)

IF parm = '' THEN DO
   set   = FALSE
   not_x = FALSE
END
ELSE DO
   p = 0
   DO i = 1 TO WORDS(parm)
      SELECT
         WHEN WORD(parm,i) = '+' | ,
              WORD(parm,i) = 'DO' THEN DO
            p = p + 1
            set = TRUE
         END

         WHEN WORD(parm,i) = '-' | ,
              WORD(parm,i) = 'SHOW' THEN DO
            p = p + 1
            set = FALSE
         END

         WHEN WORD(parm,i) = 'X' THEN DO
            p = p + 1
            not_x = FALSE
            loc   = FALSE
         END

         WHEN WORD(parm,i) = 'NX' THEN DO
            p = p + 1
            not_x = TRUE
            loc   = FALSE
         END

         WHEN WORD(parm,i) = 'L'  |,
              WORD(parm,i) = 'GO' THEN DO
            p = p + 1
            not_x = TRUE
            loc   = TRUE
         END

         OTHERWISE DO
            zedsmsg = 'Wrong parm'
            zedlmsg = 'Macro FLAGCHGS allows the following parms:',
                      '"+", "-" equal to "DO", "SHOW" and "X" or "NX".'
            "ISPEXEC SETMSG MSG(ISRZ001)"
            EXIT -1
         END
      END
   END
END

IF p > 2 THEN DO
   zedsmsg = 'Wrong number of parms'
   zedlmsg = 'Macro FLAGCHGS allows only two parms, which can be:',
             '"+", "-" equal to "DO", "SHOW" and "X" or "NX"',
             'or "L" (also "GO").'
   "ISPEXEC SETMSG MSG(ISRZ001)"
   EXIT -1
END

/* Date */
PARSE VALUE DATE('E') WITH t '/' m '/' j
cf = "CHG==>"

/* All lines */
first = 1
"ISREDIT (last) = LINENUM .ZL"
"ISREDIT RES X"

/* More inits */
changes = FALSE
"ISREDIT (df,dl) = DISPLAY_LINES"

/* Collect information */
DO i = first TO last
   "ISREDIT (info) = LINE_STATUS (i)"
   IF SUBSTR(info,1,1) = 0 |,                    /* Not original line */
      SUBSTR(info,8,1) = 1 THEN DO               /* Line was changed  */
      "ISREDIT XSTATUS (i) = X"
      changes = TRUE
   END
END
"ISREDIT HIDE X"

/* Message */
IF changes THEN DO
   "ISREDIT FLIP"
   IF set THEN DO
      "ISREDIT (a,b) = NUMBER"
      "ISREDIT NUMBER OFF"
      "ISREDIT C ALL p'======' 1 '"cf"' .ZF .ZL NX"
      "ISREDIT NUMBER" a b
      "ISREDIT RES C"
      "ISREDIT RES X"
      CALL DispMsg '',,
                   "All changed lines have been flagged with '"cf"'.",,
                   FALSE
      "ISREDIT FIND FIRST '"cf"' 1"
      IF not_x THEN "ISREDIT RES X"
      "ISREDIT LOCATE" df
      IF df + 0 = 1 THEN "ISREDIT UP 1"
      EXIT 0
   END
   ELSE DO
      CALL DispMsg '',,
                   "The current visible lines have been changed and",
                   "can be flagged with '"cf"' using 'FLAGCHGS +'.",,
                   FALSE
      "ISREDIT LOCATE 0"
      EXIT 1
   END
   "ISREDIT LOCATE" fl
END
ELSE DO
  "ISREDIT RES X"
   CALL DispMsg '',,
                'There are no changes'.,,
                FALSE
END

IF loc THEN "ISREDIT LOCATE" fl

EXIT 1
