/* REXX OK ************************************************************/
/* -------------------------------------------------------------------*/
/* Name:     AUTOMAT                                                  */
/* Syntax:   ÝTSO¨ AUTOMAT ÝB|E|V|R¨                                  */
/* Parms:    B = Browse Profile member                                */
/*           E = Edit        "    "                                   */
/*           V = View        "    "                                   */
/*           R = Review      "    "                                   */
/* Purpose:  Automatically Opening Several Split Screens              */
/* Type:     REXX-Exec                                                */
/* Author:   Norbert Haas, noaDatex.de                                */
/* Date:     10.04.1999                                               */
/*                                                                    */
/* Change:   Assume that your ISPF PROFILE dataset is called          */
/*           userid.ISPF.ISPPROF  and not  userid.TSOPROF        SBG  */
/*           (See member AUTOMAT1 for original exec.)                 */
/*                                                                    */
/* Description:                                                       */
/* ------------                                                       */
/* The purpose of this exec is to automatically open multiple ISPF    */
/* split screens, under the control of a profile dataset, which       */
/* determines how many and what screens are open, and in what order.  */
/*                                                                    */
/* The profile member, whose member name is AUTOMAT, is created in    */
/* the ISPF profile library, and the applications to be called, are   */
/* listed there.  This member is created during the first call,       */
/* brought into the editor and can then be changed by the user.       */
/* The AUTOMAT profile is newly created only during the first call.   */
/*                                                                    */
/* The ISPF profile name should be changed in this exec, to your own  */
/* ISPF profile name, or name pattern.  See line 112.                 */
/*                                                                    */
/* Applications listed in the AUTOMAT profile are each called up in   */
/* a separate split screen, but executing AUTOMAT itself, needs       */
/* an initial ISPF screen, because AUTOMAT won't run unless ISPF is   */
/* already up.  The first application started by AUTOMAT then comes   */
/* up in the second ISPF screen, the second application in the 3rd    */
/* screen, and so forth.                                              */
/*                                                                    */
/* If AUTOMAT is called with parameters B(rowse), V(iew) or E(dit),   */
/* then the AUTOMAT profile member in the ISPF profile library (as    */
/* hard-coded in this exec) is Browsed, Viewed, or Edited, so you     */
/* can see, or change, how you want AUTOMAT to operate.               */
/*                                                                    */
/* I have added an option to call AUTOMAT with a parameter "R" for    */
/* the REVIEW TSO command.  REVIEW (found on CBT Tape Files 134       */
/* (source) and 135 (load modules)) can browse or edit datasets in    */
/* TSO READY mode, or it can use ISPF if it is available.  REVIEW     */
/* has an editing option, if you "UPDATE" the dataset being reviewed. */
/* So you can use REVIEW to fix the profile dataset when ISPF is      */
/* not up yet.  REVIEW is a very convenient recovery tool. (SBG)      */
/*                                                                    */
/* Remark:                                                            */
/* ------                                                             */
/* It might be useful to put the following command into the ISPF      */
/* Command Table as specified below.  In this way, you can avoid      */
/* having to press TSO before entering the command, AUTOMAT.          */
/* You can check the results of executing AUTOMAT using the SWAP      */
/* LIST ISPF command.                                                 */
/*                                                                    */
/*    Variable   Value                                                */
/*    ---------- -------                                              */
/*    ZCTVERB    AUTOMAT                                              */
/*    ZCTTRUNC   4                                                    */
/*    ZCTACT     SELECT CMD(%AUTOMAT &ZPARM) SUSPEND SCRNAME(AUTOMAT) */
/*    ZCTDESC    Call the split-screen automation                     */
/*                                                                    */
/* If the automation is to be started from the Primary Option Menu as */
/* a menu item, this can be done as follows (in the example, 'T' is   */
/* the corresponding menu character):                                 */
/*                                                                    */
/* Part of the PROC section of the Primary Option Menu panel:         */
/*                                                                    */
/*    &ZSEL = TRANS ( TRUNC (&ZCMD,'.')                               */
/*      0,'PGM(ISPISM) SCRNAME(SETTINGS)'                             */
/*      1,'PGM(ISRBRO) PARM(ISRBRO01) SCRNAME(VIEW)'                  */
/*      2,'PGM(ISREDIT) PARM(P,ISREDM01) SCRNAME(EDIT)'               */
/*      3,'PANEL(ISRUTIL) SCRNAME(UTIL)'                              */
/*      ...  (more lines)                                             */
/*      T,'CMD(automat)'                                              */
/*      X,EXIT                                                        */
/*     ' ',' '                                                        */
/*       *,'?' )                                                      */
/*                                                                    */
/* Adjustments                                                        */
/* -----------                                                        */
/* It is necessary to adjust the maximum number of split screens on   */
/* line 110 and the DSN of the profile library on line 111.           */
/*                                                                    */
/* -------------------------------------------------------------------*/
/* Version   Author      Date        Modification                     */
/* --------- ----------- ----------- -------------------------------- */
/* V01.02    N.Haas      19.12.2016  Translated into English          */
/*                                   (powered by Google Translator)   */
/*           S.Golob     26.12.2016  English fixed better.            */
/*                                   Add support for REVIEW command   */
/* V01.01    N.Haas      31.05.1999  Added SPLIT-Screen test          */
/* V01.00    N.Haas      10.04.1999  Creation                         */
/* ********************************************************************/
/*                                                                    */
/* Initialization --------------------------------------------------- */
/* --------------                                                     */

/* Get parameter (empty, B, V, or E, or R)  ------------------------- */
PARSE UPPER ARG parm
parm = LEFT(parm,1)

"ISPEXEC CONTROL ERRORS RETURN"
TRUE = 1                                                      /* True */
FALSE = 0                                                    /* False */
userid = USERID()
cmd_counter = 0
cmd_lines. = ''

max_split = 8                 /* not greater than 8 */    /*  adjust  */
automat_dsn_mem = "'"userid".ISPF.ISPPROF(AUTOMAT)'"      /*  adjust  */

automat_exists = SYSDSN(automat_dsn_mem)

/* Does the AUTOMAT member exist in the ISPF profile library ? ------ */
IF automat_exists /= 'OK' THEN DO
   /* Create it, but if not.....   ---------------------------------- */
   CALL Init_Default_Automat

   /* Message ------------------------------------------------------- */
   CALL Display_Message "",,
       "A sample profile member has been created. Please adapt",
       "this member to your needs now. After customizing the member",
       "and exiting the Edit, you can then restart AUTOMAT.",,
      "ISRZ000"

   /* if you set a parm of "E", then you will call the Editor  ------ */
   parm = 'E'
END

/* Will a parameter be entered? ------------------------------------- */
IF parm /= "" THEN DO        /* With Parameter ==> Browse, View, Edit */
   SELECT
      WHEN parm = "B" THEN DO                               /* BROWSE */
         "ISPEXEC BROWSE DATASET("automat_dsn_mem")"
      END

      WHEN parm = "V" THEN DO                                 /* VIEW */
         "ISPEXEC VIEW DATASET("automat_dsn_mem")"
      END

      WHEN parm = "E" THEN DO                                 /* EDIT */
         "ISPEXEC EDIT DATASET("automat_dsn_mem")"
      END

      WHEN parm = "R" THEN DO                               /* REVIEW */
         "REVIEW "automat_dsn_mem" "
      /*  The REVIEW TSO command is on the CBT Tape File 134.   */
      /*  The REVIEW load modules are on the CBT Tape File 135. */
      /*  REVIEW operates with or without ISPF, so you can use  */
      /*  REVIEW's UPDATE parameter to edit the profile without */
      /*  needing ISPF to be up.  AUTOMAT R works without ISPF. */
      /*        CBT Tape address is:  www.cbttape.org           */
      END

      OTHERWISE DO
         CALL Display_Message "",,
             "Bad Parameter -",
             "AUTOMAT must start with parameter B(rowse), V(iew) or",
             "E(dit), or R(eview), or without a parameter.",,
            "ISRZ001"
      END
   END
   /* Cancel -------------------------------------------------------- */
   SIGNAL Ende
END
ELSE DO            /* Wihout a parameter, AUTOMAT creates the splits. */
   /* Are two SPLIT-Screens already active?-------------------------- */
   "ISPEXEC CONTROL SPLIT DISABLE"
   split_rc = rc
   IF split_rc /= 0 THEN DO
      CALL Display_Message "",,
          "Message - Split screens are already opened.",
          "You cannot call AUTOMAT now if more than",
          "one ISPF screen is open.",,
         "ISRZ001"
       SIGNAL Ende
   END
   ELSE "ISPEXEC CONTROL SPLIT ENABLE"
END

/* The member exists => Read and process all lines of it. ----------- */
"ALLOC FI(dd) DA("automat_dsn_mem") SHR REUSE"
"EXECIO * DISKR dd (FINIS STEM line.)"
"FREE FI(dd)"

/* Put all the commands together. ----------------------------------- */
DO i = 1 TO line.0
   temp_pos = INDEX(line.i,'/*')                    /* Comment line ? */

   IF temp_pos > 0 THEN DO                 /* Is the line a comment ? */
      IF temp_pos = 1 THEN DO      /* Yes the whole line is a comment */
         temp_cmd = ''
      END
      ELSE DO                  /* No. Just chop the comment at the end */
         temp_cmd = STRIP(LEFT(line.i,temp_pos - 1))
      END
   END
   ELSE DO                                       /* No comment there. */
      temp_cmd = STRIP(line.i)
   END

   IF temp_cmd /= '' THEN DO               /* Then save that command. */
      cmd_counter = cmd_counter + 1
      cmd_lines.cmd_counter = temp_cmd
   END
END

/* More than max_splits - 1 ? --------------------------------------- */
IF cmd_counter > max_split - 1 THEN DO
      CALL Display_Message "",,
         "Too many commands specified in profile.",
         "A maximum of" max_split "screens is possible.",
         "AUTOMAT requires one initial ISPF screen",
         "before starting up.",
         "So please reduce the number of commands",
         "in the profile, to a maximum of",
          max_split - 1".",,
         "ISRZ001"

   /* Call Edit to edit the profile dataset) ------------------------ */
   "ISPEXEC EDIT DATASET("automat_dsn_mem")"

   /* Cancel -------------------------------------------------------- */
   SIGNAL Ende
END

/* Call the commands now  ------------------------------------------- */
DO i = 1 TO cmd_counter
   /* Get each command ... ------------------------------------------ */
   tempcmd = cmd_lines.i

   /* ... and process  ---------------------------------------------- */
   CALL Aktivate_Command
END

/* The first (current) screen will get the screen name TMP ------- */
tempcmd = "SCRNAME TMP PERM"
"ISPEXEC DISPLAY COMMAND(tempcmd)"

/* Afterwards display a SWAP LIST ----------------------------------- */
tempcmd = "SWAP LIST"
"ISPEXEC DISPLAY COMMAND(tempcmd)"

/* Ende ------------------------------------------------------------- */
Ende:
EXIT

/* ================================================================== */
/* ========================  Subroutines  =========================== */
/* ================================================================== */

Aktivate_Command:
   /* Add SWAP at the end and process ------------------------------- */
   tempcmd = tempcmd";SWAP"
   "ISPEXEC SELECT PGM(ISPSTRT) PARM("tempcmd")"
Return

Init_Default_Automat:
   line.0 = 17

   line.1 = "/* --------------------------------------------------- */"
   line.2 = "/* AUTOMAT PROFILE - Applications to be started...     */"
   line.3 = "/* --------------------------------------------------- */"
   line.4 = "/* Please note that AUTOMAT itself requires one ISPF   */"
   line.5 = "/* screen to be opened first. This means that the next */"
   line.6 = "/* application which will be opened by AUTOMAT, will   */"
   line.7 = "/* start next.  That is, the first screen specified    */"
   line.8 = "/* below will be the second split screen, and the next */"
   line.9 = "/* one after that will be the 3rd split screen, etc.   */"
   line.10= "/*                                                     */"
   line.11= "/* Sample entries:                                     */"
   line.12= "/* --------------------------------------------------- */"
   line.13= "1; SCRNAME BRS                            /* 1.View    */"
   line.14= "2; SCRNAME EDT                            /* 2.Editor  */"
   line.15= "3.4                                       /* 3.DSLIST  */"
   line.16= "TSO UPSEDIT N#C015.X1480.SRC; SCRNAME SRC /* 4.TSO-CMD */"
   line.17= "SDSF; PREFIX <your userid>; OWNER *       /* 5.SDSF    */"

   "ALLOC FI(dd) DA("automat_dsn_mem") SHR REUSE"
   "EXECIO" line.0 "DISKW dd (FINIS STEM line.)"
   "FREE FI(dd)"
RETURN

Display_Message:
   PARSE ARG zedsmsg, zedlmsg, msgtype

   "ISPEXEC SETMSG MSG("msgtype")"
RETURN
