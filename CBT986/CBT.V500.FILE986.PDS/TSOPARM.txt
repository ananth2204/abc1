         Macro
.*   r1% -> command buffer
.*   Command buffer layout:
.*    al2(length of buffer incl prefix)
.*    al2(offset = lmodname +1 -> text from call)
.*    clx lmodname
.*    cl1 blank
.*    clx text from call
&mlbl    TsoParm &a,&l
         lclc  &lbl
&lbl     setc  'tp&sysndx'
&mlbl    sr    r15,r15
         icm   r1,15,0(r1)             -> cmdbfr
         jz    &lbl.x
         lh    r15,0(,r1)              buffer length
         clfi  r15,5
         jl    &lbl.x
         sh    r15,2(r1)               minus offset to text
         ahi   r15,-4                  minus prefix length
         ah    r1,2(r1)                lmod length
         aif   (k'&l eq 0).b           don't check length
         clfi  r15,&l                  check length
         jnh   *+8
         lgfi  r15,&l
.b       bctr  r15,0                   mch length
         mvc   &a.(*-*),4(r1)          copy text
         ex    r15,*-6
         la    r15,1(,r15)             reinstate text length
&lbl.x   equ   *
         Mend
