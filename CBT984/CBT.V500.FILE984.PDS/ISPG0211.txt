/* We first need rexx in comment if compiled or read from SYSPROC. */
REXX_ISPG0211:
  rexxpgm_version = "01.00"          /* Set with 'ver 1' and 'lev 0'. */

  SAY "EDIT-edit a data set"
  SAY "===================="

  ADDRESS ISPEXEC "CONTROL ERRORS RETURN"
Example_0:
  SAY "EDREC QUERY is usually used in a loop."
  done = 0
  DO WHILE done = 0
    ADDRESS ISPEXEC "EDREC QUERY"
    IF (RC = 4) THEN DO
      SAY "ZEDBDSN:" ZEDBDSN
      SAY "ZEDMODE:" ZEDMODE
      SAY "ZEDROW :" ZEDROW
      SAY "ZEDTDSN:" ZEDTDSN
      SAY "ZEDTMEM:" ZEDTMEM
      SAY "ZEDTVOL:" ZEDTVOL
      SAY "ZEDUSER:" ZEDUSER
      ADDRESS ISPEXEC "EDREC DEFER"
    END
    ELSE DO
      done = 1
    END
  END

Example_1:
  SAY "Invokes the EDREC service for INIT"
  ADDRESS ISPEXEC "EDREC INIT"
  edrec_rc = RC
  SELECT
    WHEN (edrec_rc = 0) THEN DO
      /* Normal return.

         -> INIT: Edit recovery table was successfully created.
         -> QUERY: Recovery is not pending.
         -> PROCESS: Recovery was completed and the data was saved. */
      NOP
    END
    WHEN (edrec_rc = 4) THEN DO
      /* Normal return.

         -> INIT: Edit recovery table already exists for current
            application.
         -> QUERY: Entry found in edit recovery table; recovery is
            pending.
         -> PROCESS: Recovery was completed, but user did not save
            data. */
      SAY "INIT: Edit recovery table already exists."
    END
    WHEN (edrec_rc = 20) THEN DO
      /* Severe error; unable to continue. */
      SAY "Ask your programmer to fix error."
      SAY ZERRSM
      SAY ZERRLM
    END
    OTHERWISE DO
      SAY "Something very unexpected happened, RC("edrec_rc")."
    END
  END

Example_2:
  SAY "EDIT DATAID(&DDBROW) MEMBER(ISPG020*)"
/* REXX exec to use edit recovery prior to edit */

  trace ?irs
  ADDRESS ISPEXEC "LMINIT DATAID(DATA1) DATASET(DEMO.REXX)"
  IF (RC = 0) THEN DO
    ADDRESS ISPEXEC "EDREC INIT" /* CREATE RECOVERY TABLE */
    edrc = 0
    edcon = 1
    DO UNTIL ((edrc <> 4) | (edcon = 0))
      ADDRESS ISPEXEC "EDREC QUERY" /* CHECK FOR RECOVERY DS */
      edrc = RC
      IF (edrc = 4) THEN DO
        z1 = ZEDTDSN /* SET UP PANEL VARIABLE */
                     /* AND SHOW RECOVERY PANEL */
        ADDRESS ISPEXEC "DISPLAY PANEL(ISREDM02) CURSOR(ZCMD)"
        IF ((RC = 0) & (SUBSTR(ZEDCMD,1,1) = " ")) THEN DO
          ADDRESS ISPEXEC "EDREC PROCESS" /* PROCESS RECOVERY */
          edcon = 0 /* AND END LOOP */
        END
        ELSE DO
          IF ((RC = 0) & (SUBSTR(ZEDCMD,1,1) = "C")) THEN DO
            ADDRESS ISPEXEC "EDREC CANCEL"
          END
        END
      END
      ELSE DO
        ADDRESS ISPEXEC "EDREC DEFER"
      END
    END
    ADDRESS ISPEXEC "EDIT DATAID(&DATA1) MEMBER(ISPG0211)"
    ADDRESS ISPEXEC "LMFREE DATAID(&DATA1)"
  END

  RETURN 0
