/* We first need rexx in comment if compiled or read from SYSPROC. */
rexx_sortbyhx:
  ADDRESS IsrEdit "MACRO (args)"
  arg.0 =  Words(args)
  Do wx = 1 to arg.0
    arg.wx = Word(args,wx)
  End
  If ( DataType(arg.1) <> "NUM" ) Then begin_col = 1
  Else begin_col = arg.1
  If ( DataType(arg.2) <> "NUM" ) Then span = 4
  Else span = arg.2
  ADDRESS ISREDIT "(mydsn) = DATASET"
  TSO_RC = LISTDSI("'"mydsn"'")
  If ( TSO_RC > 4 ) Then Do
    Say "LISTDSI of" mydsn "failed, rc="tso_rc
    Exit tso_rc
  End
  ADDRESS ISREDIT "(mymbr) = MEMBER"
  ADDRESS ISREDIT "(lastln) = LINENUM .ZLAST"
  If ( lastln > 1 ) Then Do
    Call mysort
  End
  Exit 1

mysort:
  outtrap_orig = OUTTRAP("ON")
  ADDRESS TSO "FREE FI(SORTIN,SORTOUT,SORTMSG,SYSIN)"
  ADDRESS TSO "FREE DDNAME(SYSIN,SORTIN,SORTOUT,SORTWK01)"
  If ( DataType(sysused) <> "NUM") Then Do
    sysused = sysalloc
  End
  ADDRESS TSO "ALLOC FILE(SORTWK01)" ,
    "UNIT(SYSALLDA) SPACE("sysused",10) cylinders"
  outtrap_now = OUTTRAP(outtrap_orig)
  recfml = length(sysrecfm)
  myrecfm = ""
  Do rx = 1 to recfml
    one = substr(sysrecfm,rx,1)
    myrecfm = myrecfm||one" "
  End
  If ( Left(myrecfm,1) = "V" ) Then Do
    begin_col = begin_col + 4
  End
  If ( sysunits = "BLOCK" ) Then Do
    sysunits = "TRACK"
  End
  ADDRESS TSO "ALLOC FI(SORTIN) NEW REUSE" ,
    "UNIT(SYSALLDA) SPACE("sysused")" sysunits"S" ,
    "RECFM("myrecfm") BLKSIZE(0) LRECL("syslrecl")"

  NewStack

  cx = 1
  Do While ( cx <= lastln )
    "ISREDIT (sortin) = LINE" cx
     line_rc = RC
     cx = cx + 1
     If ( line_rc = 0 ) Then Queue sortin
  End
  Queue ""

  ADDRESS MVS "ExecIO * DiskW sortin (FINIS"

  DelStack
  NewStack

  ADDRESS TSO "ALLOC DD(sysin) NEW REUSE" ,
    "UNIT(SYSALLDA) TRACKS SPACE(1)" ,
    "RECFM(F B) BLKSIZE(0) LRECL(80)"

  Queue ' SORT FIELDS=('begin_col','span||,
    ',AC,A),DYNALLOC=(SWORK,3)'
  Queue " END "
  Queue ""
  ADDRESS MVS "ExecIO * DiskW sysin (FINIS"

  DelStack

  "ALLOC FI(SORTOUT) NEW REUSE" ,
    "UNIT(SYSALLDA) SPACE("sysused")" sysunits"S" ,
    "RECFM("myrecfm") BLKSIZE(0) LRECL("syslrecl")"
  "ALLOC FI(SORTMSG) sysout(1)"
  outtrap_orig = Outtrap("alcmsgs.")
  "ALLOC FI(SYSOUT) sysout(1)"
  outtrap_now = Outtrap(outtrap_orig)

  "CALL *(SORT)"
  sort_rc = rc
  If ( sort_rc <> 0 ) Then Do
    say "SORT failed with RC("sort_rc")."
    ADDRESS TSO "FREE DDNAME(SYSIN,SORTIN,SORTOUT,SORTWK01,SYSOUT)"
    Return sort_rc
  End

  ADDRESS ISREDIT "DELETE 1 .ZLAST"   /* CLEAN THE EDIT SLATE */
  ADDRESS MVS "ExecIO * DiskR SORTOUT (Stem srt. FINIS"

   cx = 1
   Do While cx <> srt.0 + 1
     SORTOUT = srt.cx
     Lcx = RIGHT(cx-1,6,0)
     ADDRESS ISREDIT "LINE_AFTER "Lcx" = (SORTOUT)"
     cx = cx + 1
   End

  ADDRESS TSO "FREE DDNAME(SYSIN,SORTIN,SORTOUT,SORTWK01,SYSOUT)"
  Return
