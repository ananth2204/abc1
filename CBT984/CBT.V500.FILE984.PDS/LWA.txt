/*------------------------------ REXX --------------------------------*/
/*                                                                    */
/* LIB: DSN=XSYS.KTOMIAK.REXX(PSCB)                                   */
/* GDE: Kenneth E. Tomiak                                             */
/* DOC: Follow control blocks to peek at command passed from logon    */
/*      panel.                                                        */
/*                                                                    */
/*--------------------------------------------------------------------*/
lwa:
    parse arg gotcha
    call display_who_i_am
    call retrieve_logon_work_area
    exit 0

display_who_i_am:
    PARSE VERSION rexx_version rexx_date
    PARSE SOURCE host_env call_type uc_exec_name,
      HOSTDD HOSTDSN,
      nf_exec_name initial_cmd_env asid_name,
      char8_user_token HOSTOTHER
    say ""
    say Copies("=",45)
    say left("=" uc_exec_name "executing under" host_env,
             "as a" call_type,43) "="
    say left("=  on" date() "at" time()".",43) "="
    say Copies("=",45)
    Return

retrieve_logon_work_area:
    PSAAOLD  = STORAGE(224,4)                     /* POINTER TO ASCB */
    ASCBASXB = STORAGE(D2X(C2D(PSAAOLD)+108),4)   /* POINTER TO ASXB */
    ASXBLWA  = STORAGE(D2X(C2D(ASCBASXB)+20),4)   /* POINTER TO LWA  */
    LWALGCMD = STORAGE(D2X(C2D(ASXBLWA)+186),80)  /* POINTER to  cmd */
    say left(lwalgcmd,79)
    say Copies("=",45)
    Return
