/* We first need rexx in comment if compiled or read from SYSPROC. */
rexx_demo:
  rexxpgm_version = "01.07"          /* Set with 'ver 1' and 'lev 2'. */

  PARSE ARG all_my_arguments
  sysvar_sysispf = SYSVAR("SYSISPF")
  IF (sysvar_sysispf <> "ACTIVE") THEN DO
    Say "You first need to start ISPF and then you can execute" ,
      "this DEMO tool."
    RETURN 16
  END

  ADDRESS ISPEXEC "CONTROL ERRORS RETURN"
  Numeric Digits 12
  return_code = 0

/* <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
=======================================================================
 This code drives Initialization, Process, and Termination routines.
=======================================================================
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> */
Main_Routine:
  return_code = Initialization_routine()
  IF (return_code = 0) THEN DO
    return_code = Process_routine()
  END
  return_code = Termination_routine()
  Return return_code

/* <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<

  Dataset:  TSO.REXX(Demo)
  Contact: "Kenneth E. Tomiak"<CBT_Ken@KTomiak.BIZ>
  Overview: This is an ISPF dialog driver program. It will display a
            panel and process requests until the user exits.

            A demonstration sample of using PDC and PNT in a panel.
            This REXX program supports displaying a primary panel and
            handling the various ways a user can choose a feature to
            be used.
  Syntax:   %demo

=======================================================================

  History of Modifications
  ------------------------
vv.mm When     Who  /  What you did
----- -------- --------------------------------------------------------
01.07 20180417 "Kenneth E. Tomiak"<CBT_Ken@KTomiak.BIZ>
               1) Removed unnecessary VPUT of ZERR variables.
----- -------- --------------------------------------------------------
01.06 20180412 "Kenneth E. Tomiak"<CBT_Ken@KTomiak.BIZ>
               1) Added panel option KT for the GENIE sub-panel.
----- -------- --------------------------------------------------------
01.05 20170808 "Kenneth E. Tomiak"<CBT_Ken@KTomiak.BIZ>
               1) Applied my new normal first comment that states why
                  it even exists. TSO/E does not need REXX on the first
                  record when found under the //SYSEXEC or ALTLIB EXEC
                  methods.
               2) Removed LIBDEF ISPPLIB.
----- -------- --------------------------------------------------------
01.04 20170807 "Kenneth E. Tomiak"<CBT_Ken@KTomiak.BIZ>
               1) Shifted the logic to ensure ISPF is active as the
                  first check.
               2) Removed makebuf/dropbuf.
               3) Moved *_letters to routines.
               4) Modified zerrlm for as yet unsupported menu options.
----- -------- --------------------------------------------------------
01.03 20170806 "Kenneth E. Tomiak"<CBT_Ken@KTomiak.BIZ>
               1) 15 will run %demodeep.
----- -------- --------------------------------------------------------
01.02 20170323 "Kenneth E. Tomiak"<CBT_Ken@KTomiak.BIZ>
               1) Added handling a few more choices.
                 a) ZORK is an 'Easter egg'.
                 b) 10 will run the BIORYTHM code.
               2) DEMOVER and DEMOAUTH set for the Menu bar Help About
                  panel.
----- -------- --------------------------------------------------------
01.01 20170320 "Kenneth E. Tomiak"<CBT_Ken@KTomiak.BIZ>
               1) Changed panel name.
               2) Moved code to handle the outcome of displaying the
                  panel to the Process_routine routine.
----- -------- --------------------------------------------------------
01.00 20170315 "Kenneth E. Tomiak"<CBT_Ken@KTomiak.BIZ>
               1) Original code.
----- -------- --------------------------------------------------------

=======================================================================

>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> */


/* <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
=======================================================================
 This routine initializes constants and variables.
=======================================================================
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> */
Initialization_routine:
  PARSE VERSION,
    version_rexx_name,
    version_rexx_version,
    version_rexx_date
  PARSE SOURCE,
    source_host_env,
    source_call_type,
    source_uc_exec_name,
    source_hostdd,
    source_hostdsn,
    source_nf_exec_name,
    source_initial_cmd_env,
    source_asid_name,
    source_char8_user_token,
    source_hostother

  sysvar_sysuid = SYSVAR("SYSUID")
  sysvar_sysenv = SYSVAR("SYSENV")
  sysvar_syspref = SYSVAR("SYSPREF")
  function_rc = rexx_Jobspecs()
  function_rc = rexx_SAF()
  function_rc = rexx_Hardware()
  function_rc = rexx_Server_Info()
  function_rc = rexx_Termid()

  ZERRHM = "DEMOTUTR"
  ZERRALRM = "Yes"
  ZERRSM = ""
  ZERRLM = ""

  Return return_code

/* <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
=======================================================================
 This routine does the processing.
 Display a PANEL and act upon the user's choices.
=======================================================================
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> */
Process_routine:
  DEMOVER = rexxpgm_version
  DEMOAUTH = "Kenneth E. Tomiak"
  display_rc = 0
  Do While (display_rc = 0)
    msg_rc = 0
    Address ISPEXEC "DISPLAY PANEL(DEMOPRIM)"
    display_rc = RC
    IF (display_rc = 8) THEN DO
      ZERRSM = "Okay."
      ZERRLM = "You pressed END (PF3) to leave."
      ADDRESS ISPEXEC "SETMSG MSG(ISRZ002)"
      Leave
    END
    Parse VAR ZCMD my_zcmd "." ZTRAIL
    Address ISPEXEC "VPUT (ZTRAIL)"
    Select
      WHEN (display_rc = 4) THEN DO
        Say "return_code = 4"
        ZERRSM = "RC=4"
        ZERRLM = "A minor issue displaying DEMOPRIM."
        ADDRESS ISPEXEC "SETMSG MSG(ISRZ002)"
      END
      WHEN (display_rc > 0) THEN DO
        Say "return_code > 0"
        Say "DEBUGGING NEEDED ON DEMOPRIM)"
        return_code = 32
      END
      WHEN (my_ZCMD = 0) THEN DO
        Address ISPEXEC ,
          "SELECT PGM(ISPISM) SCRNAME(SETTINGS)"
        function_rc = RC
        IF (function_rc <> 0) THEN DO
          ZERRSM = "ISPEXEC FAILURE"
          ZERRLM = "Invoking PGM(ISPISM) failed, RC("function_rc")"
          ADDRESS ISPEXEC "SETMSG MSG(ISRZ002)"
        END
      END
      WHEN (my_ZCMD = 1) THEN DO
        Address ISPEXEC ,
          "SELECT PGM(ISRBRO) PARM(ISRBRO01) SCRNAME(VIEW)"
        function_rc = RC
        IF (function_rc <> 0) THEN DO
          ZERRSM = "ISPEXEC FAILURE"
          ZERRLM = "Invoking PGM(ISPISM) failed, RC("function_rc")"
          ADDRESS ISPEXEC "SETMSG MSG(ISRZ002)"
        END
      END
      WHEN (my_ZCMD = 2) THEN DO
        Address ISPEXEC ,
          "SELECT PGM(ISREDIT) PARM(P,ISREDM01) SCRNAME(EDIT)"
        function_rc = RC
        IF (function_rc <> 0) THEN DO
          ZERRSM = "ISPEXEC FAILURE"
          ZERRLM = "Invoking PGM(ISREDIT) failed, RC("function_rc")"
          ADDRESS ISPEXEC "SETMSG MSG(ISRZ002)"
        END
      END
      WHEN (my_ZCMD = 3) THEN DO
        Address ISPEXEC ,
          "SELECT PANEL(ISRUTIL) SCRNAME(UTIL) OPT("ZTRAIL")"
        function_rc = RC
        IF (function_rc <> 0) THEN DO
          ZERRSM = "ISPEXEC FAILURE"
          ZERRLM = "Invoking PANEL(ISRUTIL) failed, RC("function_rc")"
          ADDRESS ISPEXEC "SETMSG MSG(ISRZ002)"
        END
      END
      WHEN (my_ZCMD = 4) THEN DO
        Address ISPEXEC ,
          "SELECT PANEL(ISRFPA) SCRNAME(FOREGRND) OPT("ZTRAIL")"
        function_rc = RC
        IF (function_rc <> 0) THEN DO
          ZERRSM = "ISPEXEC FAILURE"
          ZERRLM = "Invoking PANEL(ISRFPA) failed, RC("function_rc")"
          ADDRESS ISPEXEC "SETMSG MSG(ISRZ002)"
        END
      END
      WHEN (my_ZCMD = 5) THEN DO
        Address ISPEXEC ,
          "SELECT PGM(ISRJB1) PARM(ISRJPA) SCRNAME(BATCH) NOCHECK" ,
          "OPT("ZTRAIL")"
        function_rc = RC
        IF (function_rc <> 0) THEN DO
          ZERRSM = "ISPEXEC FAILURE"
          ZERRLM = "Invoking PGM(ISRJB1) failed, RC("function_rc")"
          ADDRESS ISPEXEC "SETMSG MSG(ISRZ002)"
        END
      END
      WHEN (my_ZCMD = 6) THEN DO
        Address ISPEXEC ,
          "SELECT PGM(ISRPTC) SCRNAME(CMD)"
        function_rc = RC
        IF (function_rc <> 0) THEN DO
          ZERRSM = "ISPEXEC FAILURE"
          ZERRLM = "Invoking PGM(ISRPTC) failed, RC("function_rc")"
          ADDRESS ISPEXEC "SETMSG MSG(ISRZ002)"
        END
      END
      WHEN (my_ZCMD = 7) THEN DO
        Address ISPEXEC ,
          "SELECT PGM(ISPYXDR) PARM(&ZTAPPLID) SCRNAME(DTEST) NOCHECK" ,
          "OPT("ZTRAIL")"
        function_rc = RC
        IF (function_rc <> 0) THEN DO
          ZERRSM = "ISPEXEC FAILURE"
          ZERRLM = "Invoking PGM(ISPYXDR) failed, RC("function_rc")"
          ADDRESS ISPEXEC "SETMSG MSG(ISRZ002)"
        END
      END
      WHEN (my_ZCMD = 8) THEN DO
        Address ISPEXEC ,
          "SELECT PANEL(ISRLPRIM) SCRNAME(LMF)"
        function_rc = RC
        IF (function_rc <> 0) THEN DO
          ZERRSM = "ISPEXEC FAILURE"
          ZERRLM = "Invoking PANEL(ISRLPRIM) failed, RC("function_rc")"
          ADDRESS ISPEXEC "SETMSG MSG(ISRZ002)"
        END
      END
      WHEN (my_ZCMD = 9) THEN DO
        Address ISPEXEC ,
          "SELECT PANEL(ISRDIIS) ADDPOP OPT("ZTRAIL")"
        function_rc = RC
        IF (function_rc <> 0) THEN DO
          ZERRSM = "ISPEXEC FAILURE"
          ZERRLM = "Invoking PANEL(ISRDIIS) failed, RC("function_rc")"
          ADDRESS ISPEXEC "SETMSG MSG(ISRZ002)"
        END
      END
      WHEN (my_ZCMD = 10) THEN DO
        Address ISPEXEC ,
          "SELECT PGM(ISRSCLM) SCRNAME(SCLM) NOCHECK OPT("ZTRAIL")"
        function_rc = RC
        IF (function_rc <> 0) THEN DO
          ZERRSM = "ISPEXEC FAILURE"
          ZERRLM = "Invoking PGM(ISRSCLM) failed, RC("function_rc")"
          ADDRESS ISPEXEC "SETMSG MSG(ISRZ002)"
        END
      END
      WHEN (my_ZCMD = 11) THEN DO
        Address ISPEXEC ,
          "SELECT PGM(ISRUDA) PARM(ISRWORK) SCRNAME(WORK)"
        function_rc = RC
        IF (function_rc <> 0) THEN DO
          ZERRSM = "ISPEXEC FAILURE"
          ZERRLM = "Invoking PGM(ISRUDA) failed, RC("function_rc")"
          ADDRESS ISPEXEC "SETMSG MSG(ISRZ002)"
        END
      END
      WHEN (Translate(my_ZCMD) = "BI") THEN DO
        function_rc = BIORYTHM()  /* An external REXX routine */
      END
      WHEN (Translate(my_ZCMD) = "CF") THEN DO
        say "CF recognized."
        Address TSO "ISPCCONF"
      END
      WHEN (Translate(my_ZCMD) = "DC") THEN DO
        function_rc = DEMOCNSL()  /* An external REXX routine */
      END
      WHEN (TRANSLATE(Word(my_ZCMD,1)) = "DEMOPDCR") THEN DO
        choice = Word(my_ZCMD,2)
        function_rc = DEMOPDCR_choice_selector()
      END
      WHEN (Translate(my_ZCMD) = "DD") THEN DO
        function_rc = DEMODEEP()  /* An external REXX routine */
      END
      WHEN (Translate(my_ZCMD) = "DT") THEN DO
        function_rc = DEMOTBL1()  /* An external REXX routine */
      END
      WHEN (Translate(my_ZCMD) = "KT") THEN DO
        ADDRESS ISPEXEC "SELECT PANEL(KTGENIE)"
      END
      WHEN (Translate(my_ZCMD) = "R") THEN DO
        Address ISPEXEC
          "SELECT PANEL(ICHP00) OPT("ZTRAIL")"
        function_rc = RC
        IF (function_rc <> 0) THEN DO
          ZERRSM = "ISPEXEC FAILURE"
          ZERRLM = "Invoking PANEL(ICHP00) failed, RC("function_rc")"
          ADDRESS ISPEXEC "SETMSG MSG(ISRZ002)"
        END
      END
      WHEN (Translate(my_ZCMD) = "SD") THEN DO
        Address ISPEXEC
          "SELECT PGM(ISFISP) NOCHECK NEWAPPL(ISF) SCRNAME(SDSF)"
        function_rc = RC
        IF (function_rc <> 0) THEN DO
          ZERRSM = "ISPEXEC FAILURE"
          ZERRLM = "Invoking PGM(ISFISP) failed, RC("function_rc")"
          ADDRESS ISPEXEC "SETMSG MSG(ISRZ002)"
        END
      END
      WHEN (my_ZCMD = "") THEN DO
        ZERRSM = "Huh?"
        ZERRLM = "You pressed enter without selecting an option."
        ADDRESS ISPEXEC "SETMSG MSG(ISRZ002)"
      END
      WHEN (To_Uppercase(my_ZCMD) = "X") THEN DO
        ZERRSM = "Okay"
        ZERRLM = "You used X to leave."
        ADDRESS ISPEXEC "SETMSG MSG(ISRZ002)"
        return_code = -1
      END
      WHEN (To_Uppercase(my_ZCMD) = "ZORK") THEN DO
        Say "zcmd=ZORK"
        function_rc = ZORK_easter_egg()
      END
      OTHERWISE DO
        ZERRSM = "Try again"
        ZERRLM = my_zcmd ,
          "is not yet something DEMO handles at this time."
        ADDRESS ISPEXEC "SETMSG MSG(ISRZ002)"
      END  /* END of OTHERWISE */
    END  /* END of SELECT  */
    ZCMD = ""
  END
  Return return_code

/* <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
=======================================================================
 This routine does cleanup before exiting.
=======================================================================
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> */
Termination_routine:
  IF (return_code > 0) THEN DO
    IF (msg_rc = 1) THEN DO
      IF (ZERRSM = "") THEN DO
        ZERRSM = "An unhappy ending"
        ZERRLM = "Some unhappy turn of events is sending back",
          "a RETURN code higher than 0 with no message."
      END
      ADDRESS ISPEXEC "SETMSG MSG(ISRZ002)"
    END
  END
  Return return_code

/* <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
 Follow MVS control blocks to discover my task names.
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> */
rexx_Jobspecs:
  PSATOLD  = STORAGE(21C,4)                        /* Pointer to TCB */
  PTRTIOT  = STORAGE(D2X(C2D(PSATOLD)+12),4)      /* Pointer to TIOT */
  jobspecs_JOBNAME  = STORAGE(D2X(C2D(PTRTIOT)),8)
  jobspecs_STEPNAME = STORAGE(D2X(C2D(PTRTIOT)+8),8)
  jobspecs_PROCNAME = STORAGE(D2X(C2D(PTRTIOT)+16),8)
  Tcbjscbb = Storage(D2x(C2d(Psatold)+181),3)                /* jscb */
  jscbssib = STORAGE(D2X(C2D(tcbjscbb)+316),4)        /* Ptr to SSIB */
  jobspecs_ssibjbid = STORAGE(D2X(C2D(jscbssib)+12),8)
    jobspecs_jobtype  = Substr(jobspecs_ssibjbid,1,3)
    jobspecs_tasknum  = Substr(jobspecs_ssibjbid,4,5)
  Return 0
  Say "ssibjbid="jobspecs_ssibjbid

/* <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
 Follow MVS control blocks to discover what RACF groups they are in.
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> */
rexx_SAF:
  psaold   = Storage(D2x(548),4)             /* Pointer to curr ASCB */
  ascbasxb = Storage(D2x(C2d(psaold)+108),4)     /* Get ASXB address */
  asxbsenv = Storage(D2x(C2d(ascbasxb)+200),4)   /* Get ACEE address */
  aceefcgp = Storage(D2x(C2d(asxbsenv)+116),4)     /* get group list */
  cgrplen  = C2d(Storage(D2x(C2d(aceefcgp)+5),3))     /* list length */
  cgrp     = Storage(D2x(C2d(aceefcgp)+32),(cgrplen-32))   /* groups */
  saf_groups   = ""
  Do loop = 1 to cgrplen by 24                   /* loop thru groups */
    saf_groups = saf_groups";"Strip(Substr(cgrp,loop,8))   /* append */
  END                                        /* END loop thru groups */
  Return 0
  Say "List-of-Groups="saf_groups

/* <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
 Follow MVS control blocks to discover what machine they are on.
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> */
rexx_Hardware:
  PTR2PSA  = 0                                     /* Pointer to PSA */
  CVTPTR   = STORAGE(D2X(PTR2PSA + 16),4)          /* Pointer to CVT */
  CVT      = BitAnd(CVTPTR,"7FFFFFFF"X)       /* Flip high order bit */
  CSDPTR   = STORAGE(D2X(C2D(CVT)+X2D("294")),4)   /* Pointer to CSD */
  CSD      = BitAnd(CSDPTR,"7FFFFFFF"X)       /* Flip high order bit */
  hardware_CPUENG = C2D(STORAGE(D2X(C2D(CSD)+10),2))
  PCCAAVT  = STORAGE(D2X(C2D(CVT)+X2D("2FC")),4)
  PCCAAVT  = BitAnd(PCCAAVT,"7FFFFFFF"X)
  Do I = 0 TO 60 BY 4
    PCCA = STORAGE(D2X(C2D(PCCAAVT)+I),4)
    PCCA = BitAnd(PCCA,"7FFFFFFF"X)
    If PCCA == "00000000"X Then Iterate I
    INFO = STORAGE(C2X(PCCA),32)
    hardware_CPUID   = SubStr(INFO,7,1)
    hardware_CPUSN   = SubStr(INFO,8,5)
    hardware_CPUTYPE = SubStr(INFO,13,4)
  END
  Return 0
  Say "Hardware="hardware_CPUENG
  Say "        ="hardware_CPUID
  Say "        ="hardware_CPUSN
  Say "        ="hardware_CPUTYPE

/* <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
 Follow MVS control blocks to discover what server (host) they are on.
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> */
rexx_Server_Info:
  PTR2PSA  = 0                                     /* Pointer to PSA */
  CVTPTR   = STORAGE(D2X(PTR2PSA + 16),4)          /* Pointer to CVT */
  ECVTPTR  = STORAGE(D2X(C2D(CVTPTR)+140),4)      /* Pointer to ECVT */
  server_ECVTSPLX = STORAGE(D2X(C2D(ECVTPTR)+8),8)    /* SYSPLEXNAME */
  CVTSMCA  = STORAGE(D2X(C2D(CVTPTR)+197),3)      /* Pointer to SMCA */
  server_SMFID = STORAGE(D2X(C2D(CVTSMCA)+16),4)   /* SMF Identifier */
  mvsvar_sysname = MvsVar("SYSNAME")
  IF (server_smfid <> mvsvar_sysname) THEN DO
    Say "Tricky Having Your System Name Different Than The SMF ID."
  END
  Return 0
  Say "Server="server_ECVTSPLX
  Say "      ="server_SMFID
  Say "      ="mvsvar_sysname

/* <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
 Follow MVS control blocks to discover what terminal they are on.
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> */
rexx_Termid:
  ascbptr  = STORAGE(224,4)
  asxbptr  = STORAGE(D2X(C2D(ascbptr)+x2d('6c')),4)
  termptr  = STORAGE(D2X(C2D(asxbptr)+x2d('c8')),4)
  asxb_TERMID = STORAGE(D2X(C2D(termptr)+40),8)
  the_TERMID = C2X(asxb_TERMID)
  Return 0

/* <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
 DEMOPDCR is used to handle the PDC menu bar choices by parm.
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> */
DEMOPDCR_choice_selector:
  Select
    WHEN (choice = "OPEN") THEN DO
      demopdcr_rc = DEMOPDC1_choice_processor()
    END
    WHEN (choice = "PRINT") THEN DO
      demopdcr_rc = DEMOPDC2_choice_processor()
    END
    WHEN (choice = "OPTIONS") THEN DO
      demopdcr_rc = DEMOPDC3_choice_processor()
    END
    WHEN (choice = "SUPPORT") THEN DO
      demopdcr_rc = DEMOPDC4_choice_processor()
    END
    OTHERWISE DO
      ZERRSM = "HOW?"
      ZERRLM = "Did you reach here?"
      ADDRESS ISPEXEC "SETMSG MSG(ISRZ002)"
    END
  END
  Return 0

/* <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
 DEMOPDC1 is used to handle the PDC menu bar choice: File-Open.
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> */
DEMOPDC1_choice_processor:
  Address ISPEXEC "DISPLAY PANEL(DEMOPDC1)"
  popup_rc = RC
  IF (ABBREV("CANCEL",ZCMD,1) = 1) THEN DO
    Return 0
  END
  IF (Left(PDC1NAME,1) <> "'") THEN DO
    IF (sysvar_syspref <> "") THEN DO
      viewname = "'"sysvar_syspref"."PDC1NAME"'"
    END
    ELSE DO
      viewname = "'"PDC1NAME"'"
    END
  END
  sysdsn_rc = SysDsn(viewname)
  IF (sysdsn_rc = "OK") THEN DO
    Address ISPEXEC "View Dataset("viewname")"
  END
  Return 0

/* <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
 DEMOPDC2 is used to handle the PDC menu bar choice: File-Print.
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> */
DEMOPDC2_choice_processor:
  Address ISPEXEC "DISPLAY PANEL(DEMOPDC2)"
  popup_rc = RC
  IF (ABBREV("CANCEL",ZCMD,1) = 1) THEN DO
    Return 0
  END
  ZERRSM = "Sorry."
  ZERRLM = Left("There is no print support at this time.",79)
  ZERRLM = zerrlm || Left("You will have to find another" ,
    "way to print:",79)
  ZERRLM = zerrlm || Left(PDC2NAME,79)
  ADDRESS ISPEXEC "SETMSG MSG(ISRZ002)"
  Return 0

/* <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
 DEMOPDC3 is used to handle the PDC menu bar choice: Tools-Options.
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> */
DEMOPDC3_choice_processor:
  v1 = sysvar_syspref
  v2 = "Purple"
  v3 = "FORD"
  v4 = 4
  v5 = 1
  v6 = 0
  v7 = "09:00-17:00"
  v8 = "Lumber"
  v9 = "midway"
  v10 = "$275"
  v11 = " 11 "
  v12 = "No"
  v13 = "On"
  v14 = "?"
  v15 = "Some"
  v16 = "Never"
  v17 = "Nope"
  V18 = "This could never fit on a simple 80 column wide terminal" ,
    "display since a single attribute byte reduces it to 79 characters"
  V19 = "This could never fit on a simple 80 column wide terminal" ,
    "display since a single attribute byte reduces it to 79 characters"
  Do vx = 1 to 19
    Interpret "ov"vx "= v"vx
  END
  Address ISPEXEC "DISPLAY PANEL(DEMOINPT)"
  Do vx = 1 to 19
    if_1 = "If (ov"vx "<> v"vx") Then Say 'Changed v'"vx ,
      "'from' ov"vx "'to' v"vx"'.'"
    interpret if_1
  END
  Return 0

/* <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
 DEMOPDC4 is used to handle the PDC menu bar choice: Help-Support.
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> */
DEMOPDC4_choice_processor:
  Address ISPEXEC "DISPLAY PANEL(DEMOWEBV)"
  Return 0

/* <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
 This is an 'Easter Egg', something not shown so it is a HIDDEN choice.
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> */
ZORK_easter_egg:
  Say ""
  Say "Clear your screen and press ENTER for an important message."
  Say ""
  Pull nothing_wanted
  Say ""
  Say "Zork is set in 'the ruins of an ancient empire lying far"
  Say "underground'. The player is a nameless adventurer 'who is"
  Say "venturing into this dangerous land in search of wealth"
  Say "and adventure'. The goal is to return from exploring the"
  Say "'Great Underground Empire' (GUE, for short) alive and with"
  Say "all treasures needed to complete each adventure,"
  Say "ultimately inheriting the title of Dungeon Master. The"
  Say "dungeons are stocked with many novel creatures, objects,"
  Say "and locations, among them the ferocious but light-fearing"
  Say "grues, zorkmids (the GUE's currency), and Flood Control"
  Say "Dam #3."
  Say ""
  Say ""
  Say "This Easter Egg was brought to you by Kenneth Tomiak."
  Say "To learn more about the ZORK games visit:"
  Say "  https://en.wikipedia.org/wiki/Zork"
  Say ""
  Say ""
  Say "An Easter egg is an intentional inside joke, a hidden"
  Say "message, or a secret feature of an interactive work"
  Say "(often, a computer program, video game or other work)."
  Say ""
  Say ""
  Return 0

/* <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
 Convert string to lowercase.
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> */
To_Lowercase: Parse arg myarg
  IF (lowercase_letters = "LOWERCASE_LETTERS") THEN DO
    lowercase_letters = "abcdefghijklmnopqrstuvwxyz"
  END
  IF (uppercase_letters = "UPPERCASE_LETTERS") THEN DO
    uppercase_letters = "ABCDEFGHIJKLMNOPQRSTUVWXYZ"
  END
  Return Translate(myarg,lowercase_letters,uppercase_letters)

/* <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
 Convert string to uppercase.
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> */
To_Uppercase: Parse arg myarg
  IF (lowercase_letters = "LOWERCASE_LETTERS") THEN DO
    lowercase_letters = "abcdefghijklmnopqrstuvwxyz"
  END
  IF (uppercase_letters = "UPPERCASE_LETTERS") THEN DO
    uppercase_letters = "ABCDEFGHIJKLMNOPQRSTUVWXYZ"
  END
  Return Translate(myarg,uppercase_letters,lowercase_letters)

/* <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
=======================================================================
End of code.
=======================================================================
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> */
