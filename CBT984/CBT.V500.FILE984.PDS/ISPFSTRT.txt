rexx_ISPFSTRT: /* We still need rexx in comment if read from SYSPROC */
  rexxpgm_version = "01.05"
/* >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>

  Dataset: ...REXX(ISPFSTRT)
  Author:  KTomiak
  Contact: "Kenneth Tomiak"<KenTomiak@KTomiak.org>
  Syntax:  ISPFSTRT {arguments ...}

=======================================================================

=======================================================================

  History of Modifications
  ------------------------
vv.mm When     Who      What you did
----- -------- -------- -----------------------------------------------
01.05 20100929 KTomiak  "Kenneth E Tomiak"<KenTomiak@KTomiak.org>
                        1) Altered REXX_TERMINATION.
----- -------- -------- -----------------------------------------------
01.04 20090503 KTomiak  "Kenneth E Tomiak"<KenTomiak@KTomiak.org>
                        1) Fixed final return to use return_code.
----- -------- -------- -----------------------------------------------
01.03 20090115 KTomiak  "Kenneth E Tomiak"<KenTomiak@KTomiak.org>
                        1) Fixed starter block.
----- -------- -------- -----------------------------------------------
01.02 20070329 KTomiak  "Kenneth E Tomiak"<KenTomiak@KTomiak.org>
                        1) Formatted
----- -------- -------- -----------------------------------------------
01.01 20070329 KTomiak  "Kenneth E Tomiak"<KenTomiak@KTomiak.org>
                        1) Original code
----- -------- -------- -----------------------------------------------

=======================================================================

<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<< */

  Parse Arg all_my_arguments
  Numeric Digits 12

rexx_Main_ISPFSTRT:
  return_code = rexx_Initialize()
  If (return_code = 0) then,
    Do
      return_code = rexx_Process()
    End
   return_code = rexx_Termination()
   Return return_code

/* >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
=======================================================================
  In this routine we open any files and intialize any
  constants we might need.
=======================================================================
<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<< */
rexx_Initialize:
  PARSE VERSION,
    version_rexx_name,
    version_rexx_version,
    version_rexx_date
  PARSE SOURCE,
    source_host_env,
    source_call_type,
    source_uc_exec_name,
    source_hostdd,
    source_hostdsn,
    source_nf_exec_name,
    source_initial_cmd_env,
    source_asid_name,
    source_char8_user_token,
    source_hostother
  If (source_host_env = "TSO") Then do
    MAKEBUF
    job_specs = rexx_jobspecs()
    sysvar_sysuid = SYSVAR("SYSUID")
    sysvar_sysenv = SYSVAR("SYSENV")
    sysvar_sysispf = SYSVAR("SYSISPF")
  End
  Else do
    job_specs = rexx_no_jobspecs()
    sysvar_sysuid = "Nobody"
    sysvar_sysenv = source_host_env
    sysvar_sysispf = "INACTIVE"
  End
  say ""
  say Copies("=",55)
  say left("=" job_specs,53) "="
  say left("=" source_uc_exec_name rexxpgm_version,53) "="
  say left("= executing under" source_host_env,53) "="
  say left("= as a" source_call_type,53) "="
  say left("= on" date() "at" time()".",53) "="
  say Copies("=",55)
  say ""
  return_code = 0

  If (source_host_env >< "TSO") Then,
    Do
      Say "Wrong host environment" source_host_env
      return_code = 16
    End
  Return return_code

/* >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
=======================================================================
  In this routine we do our processing.
=======================================================================
<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<< */
rexx_Process:
  say "Attempting to execute command=" all_my_arguments
  Say ""
  say copies(" =",39)
  Signal OFF Error
  x=OutTrap("sysoutline.",1000)
  "ispstart cmd("all_my_arguments") BDISPMAX(500)"
  arc = RC
  x=OutTrap("OFF")
  return_code = arc
  If (return_code > 4096) then,
    Do
      old_code = return_code
      return_code = 4096
      say "Fixing return code("old_code") to rc("return_code")"
    End
  Return Return_code

/* >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
=======================================================================
  In this routine we close files and finish up.
=======================================================================
<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<< */
rexx_Termination:
  If (sysoutline.0 > 0) Then,
    Do
      say ""
      say "Any output between lines is from called command."
      say ""
      say copies(" -",39)
      Do i = 1 to sysoutline.0
        say sysoutline.i
      End
      say copies(" -",39)
      say ""
    End
  If (return_code > 4096) then do
    old_code = return_code
    return_code = 4096
    say "Fixing return code("old_code") to rc("return_code")"
  End
  If (Sysvar(SYSISPF)="ACTIVE") Then do
    ZISPFRC = return_code
    Address ISPEXEC "VPUT (ZISPFRC) SHARED"
  End
  Say Copies("*",55)
  Say left("* FINAL RC=" return_code,53) "*"
  Say Copies("*",55)
  Return Return_code

/* >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
=======================================================================
 Follow MVS control blocks to discover my name.
=======================================================================
<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<< */
rexx_jobspecs:
  /*---------------------------------------------------------------*/
  /* ADDRESSABILITY TO CVT FROM PSA                                */
  /*---------------------------------------------------------------*/
  PTR2PSA = 0
  CVTPTR = STORAGE(D2X(PTR2PSA + 16),4)
  /*---------------------------------------------------------------*/
  /* ADDRESSABILITY TO SMCA, THEN GET SMF SYSTEM ID                */
  /*---------------------------------------------------------------*/
  CVTSMCA = STORAGE(D2X(C2D(CVTPTR)+197),3)
  SMFID = STORAGE(D2X(C2D(CVTSMCA)+16),4)
  /*---------------------------------------------------------------*/
  /* GET JOBNAME, STEP, AND PROCEDURE                              */
  /*---------------------------------------------------------------*/
  PSATOLD  = STORAGE(21C,4)                    /* POINTER TO TCB   */
  PTRTIOT  = STORAGE(D2X(C2D(PSATOLD)+12),4)   /* POINTER TO TIOT  */
  JOBNAME  = STORAGE(D2X(C2D(PTRTIOT)),8)
  STEPNAME = STORAGE(D2X(C2D(PTRTIOT)+8),8)
  PROCNAME = STORAGE(D2X(C2D(PTRTIOT)+16),8)
  Tcbjscbb = Storage(D2x(C2d(Psatold)+181),3)              /* jscb */
  jscbssib = STORAGE(D2X(C2D(tcbjscbb)+316),4)      /* Ptr to SSIB */
  ssibjbid = STORAGE(D2X(C2D(jscbssib)+12),8)    /* job identifier */
  Return JOBNAME SSIBJBID STEPNAME PROCNAME

/* >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
=======================================================================
 Pretend to know who I am on non-MVS systems.
=======================================================================
<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<< */
rexx_no_jobspecs:
  if (lastpos("\",source_uc_exec_name) > 0) then do
    jobname = ,
      substr(source_uc_exec_name, ,
      lastpos("\",source_uc_exec_name)+1,12)
  end
  else do
    jobname = "none"
  end
  STEPNAME = "none"
  PROCNAME = "none"
  ssibjbid = "REXX####"
  Return JOBNAME SSIBJBID STEPNAME PROCNAME

/* >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
=======================================================================
 Place any of your subroutines and procedures below this.
 And describe your routines here.
=======================================================================
<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<< */

/* >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
=======================================================================
__DATA__
=======================================================================
<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<< */
