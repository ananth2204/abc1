/* rexx */
Rexx_AGE:
  rexxpgm_version = "01.01"           /* ver lev */

  ADDRESS ISREDIT "MACRO (newmbr, replace)"
  isredit_rc = RC
  IF (isredit_rc > 0) THEN DO
    SAY ""
    SAY "AGE is not a TSO command, it is an Edit macro."
    SAY "Edit macros can be invoked from EDIT and VIEW."
    SAY "Do not preface it with TSO."
    SAY ""
    return_code = 665
    RETURN return_code
  END
  ADDRESS ISPEXEC "CONTROL ERRORS RETURN"

  UPPER newmbr
  UPPER replace

/* <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
=======================================================================
 This code drives Initialization, Process, and Termination routines.
=======================================================================
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> */
Main_Routine:
  return_code = Initialization_Routine()
  IF (return_code = 0) THEN DO
    return_code = Process_Routine()
  END
  return_code = Termination_Routine()
  ADDRESS ISREDIT "MEND"
  RETURN return_code

/* <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
=======================================================================

 Dataset:  DEMO.REXX(AGE)
 Contact:  "Kenneth Tomiak"<CBT_Ken@KTomiak.BIZ>
 Overview: LMCOPY the as yet unsaved member as a backup.
 Syntax:   age newmbr |replace | yes|
           newmbr: How you go about dreaming up a new member name
                   to be a backup is entirely up to you.
           replace | yes: Replace the newmbr name if it exists.

-----------------------------------------------------------------------

 History of Modifications
 ------------------------
vv.mm When     Who / What you did ... (Newest change at the top.)
----- -------- --------------------------------------------------------
01.01 20180505 KTOMIAK "Kenneth Tomiak"<CBT_Ken@KTomiak.BIZ>
         -     1) Stylize using Edit Macro @putktm:
      20180426    Programming Using The Kenneth Tomiak Method.
               2) Stylize using template $PUTKTM:
                  Programming Using The Kenneth Tomiak Method.
               3) Stylize using Edit Macro @putktm:
                  Programming Using The Kenneth Tomiak Method.
               4) Stylize using:
                  Programming Using The Kenneth Tomiak Method.
               5) Handle setting one instance of MSG(ISRZ0002).
               6) Added validation of new member name.
                  a) LENGTH(newmbr) < 9
                  b) Standard MVS naming convention check.
                  c) If it exists and REPLACE not used then SETMSG.
               7) Append ZERRLM messages when practical.
----- -------- --------------------------------------------------------
01.00 ???????? KTOMIAK "Kenneth Tomiak"<CBT_Ken@KTomiak.BIZ>
               1) Original base code.
----- -------- --------------------------------------------------------

-----------------------------------------------------------------------

STANDARD GLOBAL DISCLAIMER
--------------------------
The author explicitly disavows any claim whatsoever about the
correctness or functionality of this program, and disclaims liability
for anything and everything bad that might happen in connection with,
before, during, or after using it. I have tried to make it work right,
and I am personally pretty confident that it does, but everybody makes
mistakes, so if you use it, you do so at your own risk.

=======================================================================
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> */

/* <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
=======================================================================
 This routine initializes constants and variables.
=======================================================================
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> */
Initialization_Routine:
  return_code = 0
  good_chars = "@#$QWERTYUIOPASDFGHJKLZXCVBNM1234567890"
  lminit_in_set = 0
  lminit_ot_set = 0

  msg_set = 0
  ZERRALRM = "YES"
  ZERRHM = "*"
  ZERRSM = "Press F1 for HELP"
  ZERRLM = ""

  ADDRESS ISREDIT "(mydsn,yourdsn,libnum) = DATASET"
  ADDRESS ISREDIT "(myvol1,myvolq) = VOLUME"
  ADDRESS ISREDIT "(member) = MEMBER"

  IF (newmbr = member) THEN DO
    msg_set = 1
    ZERRLM = ZERRLM || ,
      LEFT("New name is not allowed to match current name.",76)
    ZERRLM = ZERRLM || ,
      LEFT("It makes no sense to save the underlying as yet",76)
    ZERRLM = ZERRLM || ,
      LEFT("unmodified version over itself. Choose a new name.",76)
    return_code = 1
    RETURN return_code
  END
  IF (LENGTH(newmbr) > 8) THEN DO
    msg_set = 1
    ZERRLM = ZERRLM || ,
      LEFT("New name is too long. It can be up to 8 characters.",76)
    ZERRLM = ZERRLM || ,
      LEFT("The name can start with any of" LEFT(good_chars,29)".",76)
    ZERRLM = ZERRLM || ,
      LEFT("The remaining (up to 7 more chars) can be any of",76)
    ZERRLM = ZERRLM || ,
      LEFT("the above plus: 1234567890.",76)
    return_code = 1
    RETURN return_code
  END
  good_newmbr = 1
  error_line = ""
  IF (POS(LEFT(newmbr,1),LEFT(good_chars,29)) = 0) THEN DO
    good_newmbr = 0
    error_line = "?"
  END
  DO nx = 2 TO (LENGTH(newmbr))
    IF (POS(SUBSTR(newmbr,nx,1),good_chars) = 0) THEN DO
      good_newmbr = 0
      error_line = error_line || "?"
    END
    ELSE DO
      error_line = error_line || "."
    END
  END
  IF (good_newmbr = 0) THEN DO
    msg_set = 1
    ZERRLM = ZERRLM || ,
      LEFT(newmbr "does not conform to naming standards.",76)
    ZERRLM = ZERRLM || ,
      LEFT(error_line,76)
    ZERRLM = ZERRLM || ,
      LEFT("The name can start with any of:" ,
      LEFT(good_chars,29)".",76)
    ZERRLM = ZERRLM || ,
      LEFT("The remaining (up to 7 more) can be any of",76)
    ZERRLM = ZERRLM || ,
      LEFT("the above plus: 1234567890.",76)
    return_code = 1
    RETURN return_code
  END

  IF ((ABBREV("REPLACE",replace,1) = 1) | ,
      (ABBREV("YES",replace,1) = 1)) THEN DO
    replacey = "REPLACE"
  END
  ELSE DO
    replacey = ""
  END
  IF (replace = "") THEN DO
    IF ((ABBREV("REPLACE",newmbr,1) = 1) | ,
        (ABBREV("YES",newmbr,1) = 1)) ,
    THEN DO
      msg_set = 1
      ZERRLM = ZERRLM || ,
        LEFT(newmbr ,
          "requires using some form of the REPLACE option.",76)
      return_code = 1
      RETURN return_code
    END
    sysdsn_rc = SYSDSN("'"mydsn"("newmbr")'")
    IF (sysdsn_rc = "OK") THEN DO
      msg_set = 1
      ZERRLM = ZERRLM || ,
        LEFT(newmbr "already exists and REPLACE was not specified.",76)
      return_code = 1
      RETURN return_code
    END
  END

  ADDRESS ISPEXEC,
    "LMINIT DATAID(dataidin) DATASET('"mydsn"') ENQ(SHRW)",
    "VOLUME("myvol1")"
  lminit_rc = RC
  IF (lminit_rc <> 0) THEN DO /* RETURN CODES                      */
                                /*  8 - Data set or file not        */
                                /*      allocated                   */
                                /*    - DDname not found            */
                                /*    - Data set or file            */
                                /*      organization not supported  */
                                /* 12 - Invalid parameter VALUE     */
                                /* 16 - Truncation or translation   */
                                /*      ERROR in accessing dialog   */
                                /*      variables                   */
                                /* 20 - Severe ERROR                */
    msg_set = 1
    ZERRSM = "LMINIT 1 ERROR ("lminit_rc")"
    ZERRLM = ZERRLM || ,
      LEFT("AN ISPF ERROR PREVENTED LMINIT#1 from working.",76)
    return_code = lminit_rc
    RETURN return_code
  END
  lminit_in_set = 1
  ADDRESS ISPEXEC,
    "LMINIT DATAID(dataidot) DATASET('"mydsn"') ENQ(SHRW)",
    "VOLUME("myvol1")"
  lminit_rc = RC
  IF (lminit_rc <> 0) THEN DO /* RETURN CODES                      */
    msg_set = 1
    ZERRSM = "LMINIT 1 ERROR ("lminit_rc")"
    ZERRLM = ZERRLM || ,
      LEFT("AN ISPF ERROR PREVENTED LMINIT#2 from working.",76)
    return_code = lminit_rc
    RETURN return_code
  END
  lminit_ot_set = 1
  RETURN return_code

/* <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
=======================================================================
 This routine does the processing.
=======================================================================
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> */
Process_Routine:
  ADDRESS ISPEXEC,
    "LMCOPY FROMID("dataidin") FROMMEM("member")",
         "TODATAID("dataidot")  TOMEM("newmbr")" replacey
  lmcopy_rc = RC
  IF (lmcopy_rc <> 0) THEN DO /* RETURN CODES                      */
                          /*  4 - Member not available: in an LMF  */
                          /*      controlled ISPF library and      */
                          /*      currently owned by another user  */
                          /*    - Member not available: in an LMF  */
                          /*      controlled ISPF library and user */
                          /*      is not a valid user as specified */
                          /*      by member access id              */
                          /*    - "From" data set is empty         */
                          /*    - No members match pattern         */
                          /*    - Tried to lock a member in an     */
                          /*      SCLM-controlled library          */
                          /*  8 - "From" member not found          */
                          /* 10 - No data set associated with the  */
                          /*      given data-id                    */
                          /* 12 - Invalid data set organization    */
                          /*    - Data set attribute invalid for   */
                          /*      packed data                      */
                          /*    - Member already exists in "to"    */
                          /*      data set and "REPLACE" was       */
                          /*      not specified                    */
                          /*    - OPEN ERROR                       */
                          /* 16 - Truncation ERROR                 */
                          /* 20 - Severe ERROR                     */
/*  ZERRSM = "LMCOPY ERROR ("lmcopy_rc")"
    ZERRLM = "AN ISPF ERROR PREVENTED LMCOPY from working." */
    msg_set = 1
    ADDRESS ISPEXEC "SETMSG MSG(ISRZ002)"
  END
  return_code = 1
  RETURN return_code

/* <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
=======================================================================
 This routine does cleanup before exiting.
=======================================================================
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> */
Termination_Routine:
  IF (lminit_in_set = 1) THEN DO
    ADDRESS ISPEXEC,
      "LMFREE DATAID("dataidin")"
    lmfree_rc = RC
    IF (lmfree_rc <> 0) THEN DO /* RETURN CODES                     */
                                /*   8 - Free data set or file     */
                                /*       failed                    */
                                /*  10 - No data set or file       */
                                /*       associated with data-id   */
                                /*  20 - Severe ERROR              */
      msg_set = 1
      ZERRLM = "AN ISPF ERROR PREVENTED LMFREE from working."
    END
  END
  IF (lminit_ot_set = 1) THEN DO
    ADDRESS ISPEXEC,
      "LMFREE DATAID("dataidot")"
    lmfree_rc = RC
    IF (lmfree_rc <> 0) THEN DO /* RETURN CODES                     */
                                /*   8 - Free data set or file     */
                                /*       failed                    */
                                /*  10 - No data set or file       */
                                /*       associated with data-id   */
                                /*  20 - Severe ERROR              */
      msg_set = 1
      ZERRLM = "AN ISPF ERROR PREVENTED LMFREE from working."
    END
  END

  IF (msg_set = 0) THEN DO
    ZERRSM = "AGED to" newmbr"!"
    ZERRLM = "It worked. AGE copied" member "to" newmbr"."
  END
  ADDRESS ISPEXEC "SETMSG MSG(ISRZ002)"
  RETURN return_code

