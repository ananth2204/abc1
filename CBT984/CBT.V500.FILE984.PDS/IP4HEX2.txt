/* REXX -

 +-----------------------------------------------------------------+

   Name:       IP2HEX
   Type:       REXX EXEC
   Purpose:    Convert four part dotted decimal ip address to hex.
   Release:    1.0.0
   Programmer: Kenneth E. Tomiak
   Date:       02000-05-01
   Expectations: regular ip address is passed in.
   Syntax:     TSO %IP2HEX ipaddr
               Replace ipaddr with your four part dotted decimal ip
               Address. Sample: 9.153.12.102
               Example: TSO %IP2HEX 9.153.12.102
               Output: IP address 9.153.12.102 converts to 09 99 0C 66.

 +------------------------------------------------------------------+ */
    PARSE UPPER ARG IPaddr
    IF (IPaddr = "") then,
      Do
        Say "IPaddr is required."
        Exit 16
      End
    IPaddr = Strip(IPaddr,"B"," ")
    ip_as_hex = ""
    PARSE VAR ipaddr part.1 '.' part.2 '.' part.3 '.' part.4
    Do ix = 1 to 4
      ip_part = Strip(part.ix,"B"," ")
      If (ip_part = "") then,
        Do
          Say "IPaddr is a four part dotted decimal number like",
              "9.153.12.102"
          Exit 16
        End
      If (DataType(ip_part) <> "NUM") then,
        Do
          Say "IPaddr is a four part dotted decimal number like",
              "9.153.12.102"
          Say "fix" ip_part
          Exit 16
        End
      part = Right("00"d2x(part.ix),2)
      ip_as_hex = ip_as_hex  part
    End
    Say "IP address" IPaddr "converts to" ip_as_hex"."
    Exit 0
