/* We first need rexx in comment if compiled or read from SYSPROC. */
pgm_DEMODEEP:
  rexxpgm_version = "02.00"

/* <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
=======================================================================

 Dataset:  REXX(DEMODEEP)
 Contact:  "Kenneth E. Tomiak"<CBT_Ken@KTomiak.BIZ>
 Syntax:   %demodeep
 Overview: Demonstrate a faux scrollable panel with more lines (Depth)
           than the current display session allows. The REXX code
           extracts a subset of the 'dyndata' for the 'dynarea'.

-----------------------------------------------------------------------

 History of Modifications
 ------------------------
vv.mm When     Who / What you did ... (Newest change at the top.)
----- -------- --------------------------------------------------------
02.00 20170806 "Kenneth E. Tomiak"<CBT_Ken@KTomiak.BIZ>
               1) Some major modifications to support INPUT fields and
                  wider screens.
                  a) Overlay the dyndata from the dynarea before
                     doing any scrolling and before handling the END
                     condition.
                  b) Widths use ZSCREENW.
                  c) Only non-blank input fields are shown after END.
               2) Removed the errant 'CONTROL DISPLAY SAVE' that broke
                  the underlying panel in progress. It was not needed
                  so deleting it was a better option than adding lots
                  of code to ensure a 'CONTROL DISPLAY RESTORE' was
                  done.
               3) Changed some clunky variable names.
               4) Fixed some comments.
               5) Added my own TOP/BOTTOM of dynamic lines markers.
               6) Added lots of mising RC validation logic.
               7) Tailored default attributes to be useful:
                  a) _ Input is Low intensity and underscored so that
                     the end-user has a visual on how long the field
                     is.
                  b) + Text uses Skip(On) to jump to the next field.
----- -------- --------------------------------------------------------
01.00 20110504 prino
               http://ibmmainframes.com/about54372.html#ixzz4ojBTS0Cb
               "code comes from another forum that is currently defunct,
               so I cannot properly attribute it, sorry."
----- -------- --------------------------------------------------------

-----------------------------------------------------------------------

STANDARD GLOBAL DISCLAIMER
--------------------------
The author explicitly disavows any claim whatsoever about the
correctness or functionality of this program, and disclaims liability
for anything and everything bad that might happen in connection with,
before, during, or after using it. I have tried to make it work right,
and I am personally pretty confident that it does, but everybody makes
mistakes, so if you use it, you do so at your own risk.

=======================================================================
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> */

  Address IspExec "CONTROL ERRORS RETURN"
  load_rc = Load_temporary_PDS()
  If (load_rc <> 0) Then Do
    Say "Aborting"
    Return 16
  End

  red     = '01'x                            /* Assign colors to  */
  blue    = '02'x                            /*  Attribute bytes */
  green   = '03'x                            /*   found in the data */
  white   = '04'x
  fldbgn  = '05'x
  fldend  = '06'x
  maxlines = 85                              /* max number of lines */
  Address ISPEXEC "VGet (ZSCREENW)"
  dyndata = ""                               /* initialize data */
  dyndata = dyndata || Left(blue || ,
    Center(green || "Dynamic (pseudo-scrollable area follows." || ,
    blue, zscreenw, "-"), zscreenw)

  Do ax = 1 to maxlines                       /* Create panel lines */
    fldlen = 7 + (ax // 8)
    dyndata = dyndata || Left(white ||,
              left("This is" || red || "red" || white || ax, 20) ,
              left("This is" || blue || "blue" || white , 20) ,
              left("This is" || green || "green" || white , 20) ,
              "==>"fldbgn || Copies(" ",fldlen) || fldend, zscreenw)
  End
  dyndata  = dyndata || ,
    Left(blue || ,
    Center(green || "Dynamic (pseudo-scrollable area precedes." || ,
    blue, zscreenw, "-"), zscreenw)
  dyndata  = dyndata || ,
    Copies(" ",zscreenw) || ,
    Left(green || ,
    Center(white || "Bottom of panel." || ,
    green, zscreenw, "="), zscreenw)

  curline = 1
  display_rc = 0
  Do until display_rc > 0
    dynarea = substr(dyndata,1+(curline-1)*zscreenw)
    Select
      When (curline = 1) Then ,
        MORELESS = "More (+) Showing line 1 of" maxlines
      When ((curline + zscrolln) >= maxlines) Then
        MORELESS = "Less (-) Showing line" (curline - 1) "of" maxlines
      Otherwise
        MORELESS = "More/Less (+/-) Showing line" (curline - 1) "of" ,
          maxlines
    End
    Address ISPEXEC "display panel(DEMODEEP)"
    display_rc = rc                          /* save Return code */
    dyndata = Overlay(dynarea,dyndata,1+(curline-1)*zscreenw)
    If (display_rc = 8) Then Leave
    If (display_rc > 8) Then Do
      Say "Error in displaying panel(DEMODEEP), RC("display_rc")."
      Leave
    End
    Address ISPEXEC ,
      "VGet (ZVERB, ZSCROLLA, ZSCROLLN)" /* get scroll values */
    select                                   /* Process scrolling */
      When zverb = "UP" Then Do              /* Scroll up */
        If (zscrolla = "MAX") Then Do        /*  If scroll was max */
          curline = 1                         /*    scroll to top */
        End
        Else Do                             /*  Else a number is known*/
          curline = max(1, curline - zscrolln) /* (maximum is top) */
        End
      End
      When zverb = "DOWN" Then Do            /* Scroll down */
        If (zscrolla = "MAX") Then Do        /*  If scroll was max */
          curline = maxlines                  /*    scroll to bottom */
        End
        Else Do                              /* Else a number is known*/
          curline = min(maxlines, curline + zscrolln) /* max is bottom */
        End
      End
      When zverb = "LEFT" Then Do            /* Scroll left */
      End
      When zverb = "RIGHT" Then Do            /* Scroll right */
      End
      Otherwise Do
        Nop
      End
    End
  End                                        /* End of display loop */

  If (display_rc < 9) Then Do
    any_input = 0
    fixed_input = 0
    dynamic_input = 0
    If (somevar <> "") Then Do
      any_input = 1
      fixed_input = 1
    End
    inpdata = dyndata
    fields. = ""
    fldctr = 0
    Do While (Pos(fldbgn,inpdata) > 0)
      fldctr = fldctr + 1
      fldstart = Pos(fldbgn,inpdata) + 1
      fldlen = Pos(fldend,inpdata) - fldstart
      parse var inpdata . =(fldstart) fields.fldctr +(fldlen) ,
       . +1 inpdata
      If (fields.fldctr <> "") Then Do
        any_input = 1
        dynamic_input = 1
      End
    End

    If (any_input = 1) Then Do
      Say ""
      Say "Process the input fields:"
      If (fixed_input = 1) Then Do
        Say "Process the fixed input fields:"
        Say "somevar =" somevar
      End
      If (dynamic_input = 1) Then Do
        Say "Process the dynamic DataIn fields:"
        Say "  You will have to keep track of how the input"
        Say "  fields align with the variables you use."
        Do fx = 1 to fldctr
          If (fields.fx <> "") Then Do
            Say Right(fx,length(fldctr),"0") fields.fx
          End
        End
      Say ""
      End
    End
  End

  Address ISPEXEC "Libdef ISPPLIB"
  Address TSO "Free File("tmp_dataset")"
  Return 0

/*
   Load_temporary_PDS:

   This procedure allocates a PDS that can be loaded with any type
   of member you like. It currently only does a LibDef for ISPPLIB.
   So, if you want to include anything else then keep in mind, member
   names must be unique and you have to ALTLIB/LIBDEF as appropriate.

*/
Load_temporary_PDS:
  tmp_dataset = "dyn"random(99999)
  alloc = "alloc fi("tmp_dataset") rtdsn(sysdsname)" ,
                     "New Delete Reuse" ,
                     "Space(1,1) Dir(1)" ,
                     "Blksize(0) Lrecl(80)"
  bpx_rc = bpxwdyn(alloc)
  If (bpx_rc) > 0 Then Do
    Say "Something very bad happened in Load_temporary_PDS," ,
      "rc("bpx_rc")"
    Return bpx_rc
  End
  Address TSO "Newstack"
  member = "DEMODEEP"
  queue ")ATTR DEFAULT(%+_)"
  queue "/* Defaults */"
  queue "/*  %  TYPE(TEXT) INTENS(HIGH) */"
  queue "/*  +  TYPE(TEXT) INTENS(LOW) SKIP(OFF) */"
  queue " +  TYPE(TEXT) INTENS(LOW) SKIP(ON)"
  queue "/*  _  TYPE(INPUT) INTENS(HIGH) CAPS(OFF) JUST(LEFT) */"
  queue " _  TYPE(INPUT) INTENS(Low)" ,
            "CAPS(OFF) JUST(LEFT) Hilite(Uscore)"
  queue " @  Area(Dynamic) Scroll(On) Extend(On)"
  queue " 01 Type(Dataout) Color(Red)"
  queue " 02 Type(Dataout) Color(Blue)"
  queue " 03 Type(Dataout) Color(Green)"
  queue " 04 Type(Dataout) Color(White)"
  queue " 05 Type(Datain) Color(Pink) Hilite(UScore)"
  queue " 06 Type(Dataout) Color(Green) Skip(On)"
  queue " $  Type(Text)    Color(Yellow)"
  queue ")Body Expand(||) Width(&ZSCREENW)"
  queue "%|-| Faux scrollable display of &maxlines dynamically" ,
    "built lines.|-|"
  queue "%Command ===>_zcmd | | %Scroll ===>_ZSCR+"
  queue "%"
  queue "+ This is a fixed area to show something that does not scroll."
  queue "+ This is an input field%===>_somevar +"
  queue "+| |&MORELESS| |"
  queue "@dynarea| |@"
  queue ")Init"
  queue "If (&DYNAREA = &Z)"
  queue "  &dynarea = ' OOPS!'"
  queue ")End"

  DEMOPLIB = "DEMO"Random(9999)
  Address TSO "Alloc File("DEMOPLIB")" ,
    "Dataset('"sysdsname"("member")') SHR REUSE"
  alloc_rc = rc
  If (alloc_rc <> 0) Then Do
    Say "ALLOC failed, rc("alloc_rc")."
    return 12
  End
  Address MVS "Execio" queued() "DiskW "DEMOPLIB" (Finis)"
  execio_rc = rc
  If (execio_rc <> 0) Then Do
    Say "EXECIO failed, rc("execio_rc")."
    return 12
  End
  Address TSO "Free File("DEMOPLIB")"
  Address TSO "Delstack"
  Address ISPEXEC "Libdef ispplib library id("tmp_dataset") stack"
  LIBDEF_rc = rc
  If (LIBDEF_rc <> 0) Then Do
    Say "LIBDEF failed, rc("LIBDEF_rc")."
    return 12
  End
  Return 0
