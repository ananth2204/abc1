/* We first need rexx in comment if compiled or read from SYSPROC. */
REXX_ISPG0205:
  rexxpgm_version = "01.00"          /* Set with 'ver 1' and 'lev 0'. */

  ADDRESS ISPEXEC "CONTROL ERRORS RETURN"

  SAY "DIRLIST-directory list service"
  SAY "=============================="

  syscalls_rc = SYSCALLS("ON")

  ADDRESS SYSCALL "getcwd cwd"
  syscalls_rc = RC
  getcwd_retval = RETVAL
  getcwd_errno = ERRNO
  IF ((syscalls_rc >= 0) & (getcwd_retval = -1)) THEN DO
    getcwd_errnojr = ERRNOJR
  END
  ELSE DO
    getcwd_errnojr = ""
  END
  SELECT
    WHEN (syscalls_rc = 0) THEN DO
      /* The command finished successfully. If there is an
         error code for the requested function, it is returned
         in RETVAL and ERRNO. */
      NOP
    END
    WHEN (syscalls_rc > 0) THEN DO
      /* The command finished successfully, but a
         function-specific warning is indicated. */
      ZERRALRM = "Yes"
      ZERRHM = "*"
      ZERRSM = "getcwd failure"
      ZERRLM = LEFT("What do retval, errno, and errnojr tell us?",76)
      ZERRLM = ZERRLM || "RETVAL("getcwd_retval")" ,
        "ERRNO("getcwd_errno") ERRNOJR("getcwd_errnojr")."
      ADDRESS ISPEXEC "SETMSG MSG(ISRZ002)"
    END
    WHEN (syscalls_rc = -3) THEN DO
      /* The command environment has not been called.
         Probably the SYSCALLS('ON') function did not end
         successfully, or the current address environment is
         not SYSCALL. */
      ZERRALRM = "Yes"
      ZERRHM = "*"
      ZERRSM = "SYSCALLS failure"
      ZERRLM = LEFT("What do retval, errno, and errnojr tell us?",76)
      ZERRLM = ZERRLM || "RETVAL("getcwd_retval")" ,
        "ERRNO("getcwd_errno") ERRNOJR("getcwd_errnojr")."
      ADDRESS ISPEXEC "SETMSG MSG(ISRZ002)"
      return_code = 12
      RETURN return_code
    END
    WHEN (syscalls_rc = -20) THEN DO
      /* The command was not recognized or there was an
         improper number of parameters specified on the
         command. */
      ZERRALRM = "Yes"
      ZERRHM = "*"
      ZERRSM = "Command or Parameter"
      ZERRLM = LEFT("The command was not recognized or there was an",76)
      ZERRLM = ZERRLM || ,
        LEFT("improper number of parameters specified on the command.",76)
      ZERRLM = ZERRLM || "RETVAL("getcwd_retval")" ,
        "ERRNO("getcwd_errno") ERRNOJR("getcwd_errnojr")."
      ADDRESS ISPEXEC "SETMSG MSG(ISRZ002)"
      return_code = 12
      RETURN return_code
    END
    WHEN ((syscalls_rc < -20) & (syscalls_rc > -30)) THEN DO
      /* The first, second, ... parameter is in error. (The
         parameter is indicated by the second digit.) */
      parm_in_error = syscalls_rc + 20
      ZERRALRM = "Yes"
      ZERRHM = "*"
      ZERRSM = "SYSCALLS failure"
      ZERRLM = LEFT("What do retval, errno, and errnojr tell us?",76)
      ZERRLM = ZERRLM || "RETVAL("getcwd_retval")" ,
        "ERRNO("getcwd_errno") ERRNOJR("getcwd_errnojr")."
      ADDRESS ISPEXEC "SETMSG MSG(ISRZ002)"
      return_code = 12
      RETURN return_code
    END
    WHEN (syscalls_rc < 0) THEN DO
      /* Other negative values might be returned by the
         REXX language processor. A negative value means
         that the command did not finish successfully. */
      ZERRALRM = "Yes"
      ZERRHM = "*"
      ZERRSM = "Command failure"
      ZERRLM = LEFT("The command did not finish successfully.",76)
      ZERRLM = ZERRLM || "RETVAL("getcwd_retval")" ,
        "ERRNO("getcwd_errno") ERRNOJR("getcwd_errnojr")."
      ADDRESS ISPEXEC "SETMSG MSG(ISRZ002)"
      return_code = 12
      RETURN return_code
    END
    OTHERWISE DO
      SAY "getcwd rc("syscalls_rc")" ,
        "ERRNO("getcwd_errno") ERRNOJR("getcwd_errnojr")."
    END
  END

Example_1:
  SAY "DIRLIST - Displays a modified directory list for CWD."
  fromname = ".sh_history"
  ADDRESS ISPEXEC "DIRLIST" ,
    "PATH(cwd)"      /* Must be a non-ampersand variable name. */ ,
    "COLS(PE,10,TY,4,MO,10)" ,
    "FROM(fromname)" /* Must be a non-ampersand variable name. */ ,
    "LCMDS(ISPGPROC,LL,UPD)"
  dirlist_rc = RC
  SELECT
    WHEN (dirlist_rc = 0) THEN DO
      /* Normal completion. */
      NOP
    END
    WHEN (dirlist_rc = 8) THEN DO
      /* Error building the directory list. The error condition is
         described in the ISPF system dialog variables. */
      SAY "Ask your programmer to validate the dialog variables."
      SAY ZERRSM
      SAY ZERRLM
    END
    WHEN (dirlist_rc = 12) THEN DO
      /* A keyword value is incorrect. */
      SAY "Ask your programmer to change the keyword value."
      SAY ZERRSM
      SAY ZERRLM
    END
    WHEN (dirlist_rc = 20) THEN DO
      /* A severe error occurred while processing the directory
         list. */
      SAY "Ask your programmer to fix the logic."
      SAY ZERRSM
      SAY ZERRLM
    END
    OTHERWISE DO
      SAY "Something very unexpected happened, RC("dirlist_rc")."
    END
  END

Example_2:
  SAY "ISPF Option 3.17 via SELECT PGM(ISRDSLST)"
  ADDRESS SYSCALL "getcwd cwd"
  ADDRESS ISPEXEC "SELECT" ,
    "PGM(ISRDSLST)" ,
    "PARM(UDL &cwd)" /* Can be a ampersand variable name. */
  isrdslst_rc = RC

Example_3:
  SAY "DIRLIST - User provided code adds line commands LL and UPD."
  ADDRESS SYSCALL "getcwd cwd"
  fromname = ".."
  ADDRESS ISPEXEC "DIRLIST" ,
    "PATH(cwd)"      /* Must be a non-ampersand variable name. */ ,
    "COLS(PE,10,TY,4,MO,10)" ,
    "FROM(fromname)" /* Must be a non-ampersand variable name. */ ,
    "LCMDS(ISPGPROC,LL,UPD)"
  dirlist_rc = RC

  RETURN 0
