/* We first need rexx in comment if compiled or read from SYSPROC. */
pgm_DEMOCNSL:
  rexxpgm_version = "01.00"          /* Set with 'ver 1' and 'lev 2'. */

  SAY ""
  SAY "DEMOCNSL: Demonstrate using CONSOLE to get Disk sizes."
  SAY "======================================================"
  PARSE ARG my_range and_other_junk
  numeric digits 20
  Say Left(date() date("W"),25) time()
  call fetch_volsers
/* sample output from devserv command:
z/OS 2.3
DS QD,TYPE=ALL,DEFINED
----+----1----+----2----+----3----+----4----+----5----+----6----+----7---+----8
 IEE459I 12.43.28 DEVSERV QDASD 482
  UNIT VOLSER SCUTYPE DEVTYPE       CYL  SSID SCU-SERIAL DEV-SERIAL EFC
 00220 VTW90A 2107951 2107900      6000* D380 0175-XD721 0175-XD721 BYP
 00221 VDW90A 2107951 2107900      5000* D380 0175-XD721 0175-XD721 BYP
 00222 VPW90A 2107961 2107900     10017* 3580 0175-BTM91 0175-BTM91 BYP
 0022D VTCTGJ 2107961 2107900      1200* 3480 0175-BTM91 0175-BTM91 BYP
 0022E VPCTGJ 2107961 2107900       100* 3400 0175-BTM91 0175-BTM91 BYP
 0038A VTDFSA 2107961 2107900      5000* 3380 0175-BTM91 0175-BTM91 BYP
 ...
 003DF VPMVSF 2107961 2107900       600* 3400 0175-BTM91 0175-BTM91 BYP
 **** UNLISTED DEVICE(S) AND REASON CODES :
  00223(02) 00224(02) 00225(02) 00226(02) 00227(02) 00228(02) 00229(02)
  0022A(02) 0022B(02) 0022C(02) 0022F(02) 00230(02) 00231(02) 00232(02)
  00B16(02) 00B17(02)
 **** (02) - UCB NOT CONNECTED
  UNIT VOLSER SCUTYPE DEVTYPE       CYL  SSID SCU-SERIAL DEV-SERIAL EFC
 00B1C VPI12A 2107961 2107900      1000* 3580 0175-BTM91 0175-BTM91 BYP
 00B1D VPI12B 2107961 2107900      1000* 3580 0175-BTM91 0175-BTM91 BYP
 **** (02) - UCB NOT CONNECTED
 ****    162 DEVICE(S) MET THE SELECTION CRITERIA
 ****      0 DEVICE(S) FAILED EXTENDED FUNCTION CHECKING

*/
  fmt = Pos(" SCU-SERIAL",rec.2)
  IF (fmt <> 46) Then DO
    say "Display output has changed, update REXX code."
    say "blank-S-C-U---S-E-R-I-A-L found at" fmt"."
    say "----+----1----+----2----+----3----+----4----+----5----+----6"
    say rec.2
  End
  Else Do
    do i = 3 to rec.0
      parse var rec.i . +2,
                ucb     " " . ,
           =8   volser  " " . ,
           =15  scutype " " . ,
           =23  devtype " " . ,
           =31  cyl     +10 . ,
           =42  ssid    " " . ,
           =47  scuserial " " . ,
           =58  devserial " " . ,
           =69  efchk   " " .
    if (ucb = "****") then do
      iterate
    end
    if (left(ucb,2) = "  ") then do
      iterate
    end
    say volser cyl ucb
  end
  EXIT 0

/* <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
-----------------------------------------------------------------------
Retrieve the message output and process it.
-----------------------------------------------------------------------
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> */
fetch_volsers:
  devserv_cmd="DS QD,TYPE=ALL,DEFINED"
/*********************************************************************/
/*  DEVSERV subroutine: issue DEVSERV, trap and parse response       */
/*********************************************************************/
  wait_time = 60                   /*  seconds to wait for reply */
  myid = sysvar("SYSUID")
  "CONSOLE ACTIVATE NAME("myid"2)"
  lastrc = rc
  if lastrc >< 0 then
   do
      Say ""
      Say "*** Unable to activate TSO CONSOLE services!"
      Say "*** The return code from 'console activate' was:" lastrc
      Say "*** Attempting to recover..."
      "CONSOLE DEACT"
      lastrc = rc
      Say "*** CONSOLE DEACT return code was:" lastrc
      "CONSOLE ACTIVATE"
      lastrc = rc
      if lastrc = 0 then,
        Say "*** Recovery successful!"
      else
        do
          Say "*** Recovery attempt failed (I issued CONSOLE DEACT)",
            "return code was:" lastrc;
          Say "*** Perhaps you don't have TSO CONSOLE authority?"
          how_many = Queued()
          "EXECIO" how_many " DISKW EMAIL (FINIS"
          exit 16
        end
   end
  "CONSPROF SOLDISPLAY(NO)  SOLNUM(1000)"
  cart="DS" || time('M')
  Address "TSO"
  "CONSOLE SYSCMD("devserv_cmd") CART('"cart"')"
  getcode = getmsg("rec.","SOL",cart,,wait_time)
  if getcode ><0 then
    do
      Queue "*** GETMSG return code was:" lastrc
      "CONSPROF SOLDISPLAY(YES) SOLNUM(1000)"
      "CONSOLE DEACTIVATE"
      exit 12
    end
  Address "TSO"
  "CONSPROF SOLDISPLAY(YES) SOLNUM(1000)"
  "CONSOLE DEACTIVATE"
  Say "devserv lines("rec.0")"

  RETURN
