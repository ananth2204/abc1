/* REXX */
/*********************************************************************/
/*                                                                   */
/*   This exec can optionally be used with IBM Fault Analyzer for    */
/*   OS/390 to automatically receive and import fault entries sent   */
/*   from another system to a local history file.                    */
/*                                                                   */
/*********************************************************************/
"profile msgid"
histfile = 'C547KTO.IDI.HIST.NEW' /* <--- verify/change */
temphist = 'C547KTO.HIST'        /* <--- verify/change */
userid   = 'IDIROBOT'            /* <--- verify/change */
seconds  = '60'                  /* <--- verify/change */
address tso
x = prompt('on')
x = outtrap('var.',10,'noconcat')
do forever
  /* Obtain information about transmitted data on the JES output queue */
  if queued() = 0 then queue 'end'
  'receive userid('||userid||')'
  input = 'Y'

  /* Examine the output from the 'dummy' receive command.
     The following variables are initialized:
       dsn     - the 'sending' history file name
       fromid  - the user ID performing the TSO XMIT
       node    - the JES node from which the fault entry was sent
       faultid - the fault ID (member name) */
  do i = 1 to var.0
    say i '=' var.i
    parse var var.i msgno t1 t2 t3 t4 t5 t6
    if msgno = 'INMR901I' then do
      dsn = t2
      fromid = t4
      node = t6
    end
    else if msgno = 'INMR902I' then
      faultid = t2
    else if msgno = 'INMR003I' then
      /* This message indicates no data to receive */
      input = 'N'
  end

  /* Perform actual receive to the staging history file followed by an
     IDIUTIL batch utility import if there is data available */
  if input = 'Y' then do
    /* The target history file in the 'histfile' variable could be
       determined here based on any of the initialized variables above.
       This sample EXEC uses a single history file only. */

    say 'Receiving' dsn||'('||faultid||') from' node||'.'||fromid
    queue "da('"||temphist||"')"
    queue 'end'
    'receive userid('||userid||')'

    /* Perform IDIUTIL batch utility import */
    parm = "'import("||histfile||","||temphist||"("||faultid||"))'"
    address tso "call *(idiutil)" parm
    say 'Import rc =' RC
  end
  else do
    say 'Taking a short nap at' Date() Time()'.'
    /* Sleep for 60 seconds before attempting to receive again */
    address tso "call *(idisleep) '"||seconds||"'"
  end
end
