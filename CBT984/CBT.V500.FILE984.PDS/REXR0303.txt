/* We first need rexx in comment if compiled or read from SYSPROC. */
pgm_REXR0303:
  rexxpgm_version = "01.00"          /* Set with 'ver 1' and 'lev 2'. */

  SAY ""
  SAY "REXR0303: CALL"
  SAY "=============="

  IF ( Arg(1) = "" ) THEN DO
    SAY "Try using:"
    SAY "tso %rexr0303 4"
    SAY ""
    RETURN 16
  END

  SAY "Example: Recursive subroutine execution using CALL"
  SAY "=================================================="
/* Recursive subroutine execution... */
  ARG z
  CALL Factorial z
  SAY z "! =" result

trapping:
  SAY ""
  SAY "Example: Trapping conditions CALL"
  SAY "=================================="
  return_code = 0

  CALL_type = ""
  CALL ON ERROR
  CALL ON FAILURE
  CALL ON HALT

  signal_type = ""
  come_back = "000"
  SAY "Cause a negative RC:"
  SAY "--------------------"
  Address MVS "EXECIO 1 DISKr Apple (STEM pits. FINIS)"
noerror_000:
  IF ( signal_type <> "" ) THEN DO
    SAY "I came back to the Process_routine after an" signal_type"."
  END
  CALL OFF ERROR
  CALL OFF FAILURE
  CALL OFF HALT

  function_rc = localized_return()

  SAY "Done experimenting."
  RETURN return_code

/* <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
=======================================================================
 Calculate Factorial by recursive invocation.
=======================================================================
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> */
Factorial: Procedure
  ARG n
  IF ( n = 0 ) THEN RETURN 1
  CALL Factorial n-1
  RETURN result * n

/* <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
=======================================================================
 This sub-routine handles several conditions using a named routine.
=======================================================================
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> */
localized_return:
  SAY ""
  SAY "Setting local CALL ON conditions:"
  SAY "================================="
  CALL   ON ERROR    NAME Error_Handler_Routine
  CALL   ON FAILURE  NAME Error_Handler_Routine
  CALL   ON HALT     NAME Error_Handler_Routine

  signal_type = ""
  SAY "Cause an error:"
  SAY "---------------"
  ADDRESS TSO "ALLOC FILE(Apple) DA(TSO.REXX(ERRHNDLR)) SHR REUSE"
  ADDRESS MVS "EXECIO * DISKR Apple (STEM pits. )"
  SAY "Read" pits.0 "record(s)"
  SAY "An attempt to read from APPLE will fail."
  ADDRESS MVS "EXECIO 1 DISKR Apple (STEM pits. FINIS)"
  SAY "I came back to the localized_return routine."

  ADDRESS TSO "FREE FILE(Apple)"

  CALL   OFF ERROR
  CALL   OFF FAILURE
  CALL   OFF HALT

  RETURN return_code

/* <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
=======================================================================
 This routine handles several conditions and returns after the fault.
=======================================================================
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> */
Error_Handler_Routine:
  err_sigl = Sigl
  err_rc   = RC
  SAY "  I came to the Error_Handler_Routine."
  signal_type = To_UpperCase(CONDITION("C"))
  signal_desc = CONDITION("D")
  signal_inst = CONDITION("I")
  signal_stat = CONDITION("S")
  SAY "EHR c:" signal_type "at" err_sigl
  SAY "    d:" signal_desc
  SAY "    i:" signal_inst
  SAY "    s:" signal_stat", RC("err_rc")"
  error_code = -1
  SELECT
    WHEN ( signal_type = "ERROR" ) THEN DO
      error_code = 8
      signal_type = "Error"
    END
    WHEN ( signal_type = "Error" ) THEN DO
      error_code = 8
      signal_type = "FAILURE"
    END
    WHEN ( signal_type = "FAILURE" ) THEN DO
      error_code = 8
      signal_type = "HALT"
    END
    OTHERWISE DO
      error_code = 32
      signal_type = "Unknown"
    END
  END
  result = -1
  SAY "  RETURN to" signal_type"_"come_back
  RETURN error_code

/* <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
=======================================================================
 This routine does cleanup before exiting.
=======================================================================
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> */
ERROR:
  CALL   OFF ERROR
  SAY "    I came to the ERROR ROUTINE."
  err_sigl = Sigl
  signal_type = "Error"
  SAY signal_type "at" err_sigl
  return_code = 5
  RETURN return_code

FAILURE:
  CALL OFF FAILURE
  SAY "    I came to the FAILURE ROUTINE."
  err_sigl = Sigl
  signal_type = "FAILURE"
  SAY signal_type "at" err_sigl
  return_code = 6
  RETURN return_code

HALT:
  CALL OFF HALT
  SAY "    I came to the HALT ROUTINE."
  err_sigl = Sigl
  signal_type = "HALT"
  SAY signal_type "at" err_sigl
  return_code = 7
  RETURN return_code

/* <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
Convert string to uppercase.
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> */
To_Uppercase:
  PARSE ARG myarg
  IF ( lowercase_letters = "LOWERCASE_LETTERS" ) THEN DO
    lowercase_letters = "abcdefghijklmnopqrstuvwxyz"
  END
  IF ( uppercase_letters = "UPPERCASE_LETTERS" ) THEN DO
    uppercase_letters = "ABCDEFGHIJKLMNOPQRSTUVWXYZ"
  END
  RETURN TRANSLATE(myarg,uppercase_letters,lowercase_letters)

/* <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
=======================================================================

 Dataset:  REXX(REXR0303)
 Author:   Kenneth Tomiak
 Contact:  "Kenneth Tomiak"<CBT_Ken@KTomiak.BIZ>
 Manual:   ikj4a3a0.pdf
           SA22-7790-10 z/OS TSO/E REXX Reference
           Version 1 Release 13
           Chapter 3. Keyword instructions
           Instructions
           CALL
 Overview: Execute a set of instructions and return.

-----------------------------------------------------------------------

 History of Modifications
 ------------------------
vv.mm WHEN     Who / What you did ... (Newest change at the top.)
----- -------- --------------------------------------------------------
01.00 20171006 KTOMIAK "Kenneth Tomiak"<CBT_Ken@KTomiak.BIZ>
               1) Original base code.
----- -------- --------------------------------------------------------

-----------------------------------------------------------------------

STANDARD GLOBAL DISCLAIMER
--------------------------
The author explicitly disavows any claim whatsoever about the
correctness or functionality of this program, and disclaims liability
for anything and everything bad that might happen in connection with,
before, during, or after using it. I have tried to make it work right,
and I am personally pretty confident that it does, but everybody makes
mistakes, so if you use it, you do so at your own risk.

=======================================================================
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> */

