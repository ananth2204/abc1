rexx_HLQSPACE: /* We still need rexx in comment if read from SYSPROC */
  arg hlq vol options
  if (pos("TRACE",hlq vol options) > 0) then
    trace ?ilrs
  used_debug = ""
  if (pos("DEBUG",hlq vol options) > 0) then used_debug = "SOME"
  used_report = ""
  if (pos("REPORT",hlq vol options) > 0) then used_report = "YES"
  hlqverb = ""
  volverb = ""
  if (hlq >< "") then hlqverb = "LEVEL("hlq")"
  if (hlq = "*") then hlqverb = ""
  if (vol >< "") then volverb = "VOLUME("vol")"
  if (vol = "*") then volverb = ""
  if (hlqverb volverb = "") then
    do
      hlq = space(sysvar("SYSUID"))
      if (hlq >< "") then hlqverb = "LEVEL("hlq")"
    end

  say Date() Date("W") TIME() "HLQSPACE"
  say "TOTAL TRACKS FOR:" hlqverb volverb

  max_alc = 0
  max_use = 0
  max_dsn = 0

  Address ISPEXEC "CONTROL ERRORS RETURN"
  Address ISPEXEC,
    "LMDINIT LISTID(DSNLIST)",
    hlqverb volverb
                              /* Return codes                     */
                              /*  8 - Listid not created          */
                              /* 12 - Invalid parameter value     */
                              /* 16 - Truncation or translation   */
                              /*      error in accessing dialog   */
                              /*      variables                   */
                              /* 20 - Severe error                */
  RCSAVE = RC
  IF (RCSAVE >< 0) THEN,
    do
      say "Does" hlq "exist?"
      exit 4
    end
  DSNVAR = ''
  rclist = 0
  if (used_report = "YES") then,
    do
      say left("Dataset",44) left("SPACE UNIT",10),
        left("Tracks",6) left("USE",3)"%" left("Blksz",5)
      say copies("-",44) copies("-",10),
        copies("-",6) copies("-",4) copies("-",5)
    end
  DO while rclist = 0
    Address ISPEXEC,
      "LMDLIST LISTID("DSNLIST")",
                    "OPTION(LIST)",
                    "DATASET(DSNVAR)",
                    "STATS(YES)"
    RCLIST = RC
                 /* Return codes                           */
                 /*  4 - No data sets matched specified    */
                 /*      search criteria (the values for   */
                 /*      keywords LEVEL and VOLUME on the  */
                 /*      LMDINIT service).                 */
                 /*  8 - No more data sets names in list   */
                 /*      (option LIST)                     */
                 /*    - No data set list exists for this  */
                 /*      dslist-id (option FREE)           */
                 /*    - Data set lists already exists and */
                 /*      must be freed before using SAVE   */
                 /*      option (option SAVE)              */
                 /* 10 - Data set listid not created by    */
                 /*      lmdinit                           */
                 /* 12 - Invalid parameter value           */
                 /* 16 - Truncation or translation error   */
                 /*      in accessing dialog variables     */
                 /* 20 - Severe error                      */
    if (RCLIST > 0) then leave
    max_dsn = max_dsn + 1
    if (used_debug = "SOME") then,
      do
        say "DSNVAR C(44) Val("DSNVAR")"
          /* Data set name.             */
          /******************************/
        say "  ZDLBLKSZ C(5) Val("ZDLBLKSZ")"
          /* Block size.                */
          /******************************/
        say "  ZDLCDATE C(10) Val("ZDLCDATE")"
          /* Creation date.             */
          /******************************/
        say "  ZDLDEV C(8) Val("ZDLDEV")"
          /* Device type.               */
          /******************************/
        say "  ZDLDSNTP C(8) Val("ZDLDSNTP")"
          /* DS name type ('PDS',       */
          /* 'LIBRARY', or ' ').        */
          /******************************/
        say "  ZDLDSORG C(4) Val("ZDLDSORG")"
          /* Data set organization.     */
          /******************************/
        say "  ZDLEDATE C(10) Val("ZDLEDATE")"
          /* Expiration date.           */
          /******************************/
        say "  ZDLEXT C(3) Val("ZDLEXT")"
          /* Number of extents used.    */
          /******************************/
        say "  ZDLLRECL C(5) Val("ZDLLRECL")"
          /* Number of extents used.    */
          /******************************/
        say "  ZDLMIGR C(3) Val("ZDLMIGR")"
          /* Whether the data set is    */
          /* migrated ('YES' or 'NO').  */
          /******************************/
        say "  ZDLRDATE C(10) Val("ZDLRDATE")"
          /* Date last referenced.      */
          /******************************/
        say "  ZDLRECFM C(5) Val("ZDLRECFM")"
          /* Record format.             */
          /******************************/
        say "  ZDLSIZE C(6) Val("ZDLSIZE")"
          /* Data  set size in tracks.  */
          /******************************/
        say "  ZDLSPACU C(10) Val("ZDLSPACU")"
          /******************************/
          /* Space units, one of the    */
          /* following: CYLINDERS,      */
          /* MEGABYTES, KILOBYTES,      */
          /* BYTES, BLOCKS or TRACKS.   */
        say "  ZDLUSED C(3) Val("ZDLUSED")"
          /* Percentage of used tracks  */
          /* or 4K pages (PDSE).        */
          /******************************/
        say "  ZDLVOL C(6) Val("ZDLVOL")"
          /* Volume serial.             */
          /******************************/
        say ""
      end
    if (datatype(zdlsize) = "NUM") then
      do
        if (used_report = "YES") then,
          say left(DSNVAR,44) left(zdlspacu,10),
            left(zdlsize,6) left(zdlused,3)"%" left(ZDLBLKSZ,5)
        max_alc = max_alc + zdlsize
        if (datatype(zdlused) = "NUM") then
          max_use = max_use + (zdlsize * zdlused / 100)
        else,
          max_use = max_use + zdlsize
      end
    else
      do
        if (used_report = "YES") then,
          say left(DSNVAR,44) left(zdlspacu,10)
      end
  end
  select
    when (RCLIST = 4) then,
      do
        say "Problem listing the HLQ("hlq")"
      end
    when (RCLIST = 8) then,
      do
        nop
      end
    otherwise
      nop
  end
  Address ISPEXEC,
     "LMDLIST LISTID("DSNLIST") OPTION(FREE)"
  Address ISPEXEC,
    "LMDFREE DATAID("DSNLIST")"
  if (used_report = "YES") then,
    say copies("-",44) copies("-",10),
      copies("-",6) copies("-",4) copies("-",5)
  say "TOTAL ENTRIES LISTED  :" max_dsn
  say "TOTAL ALLOCATED TRACKS:" max_alc
  say "TOTAL    USED   TRACKS:" max_use
  exit
