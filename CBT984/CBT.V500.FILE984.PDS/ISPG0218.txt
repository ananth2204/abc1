/* We first need rexx in comment if compiled or read from SYSPROC. */
REXX_ISPG0218:
  rexxpgm_version = "01.00"          /* Set with 'ver 1' and 'lev 0'. */

  SAY "GETMSG-Get a message"
  SAY "===================="

  ADDRESS ISPEXEC "CONTROL ERRORS RETURN"

Example_1:
  SAY "For the message named ISPG218I."
  ADDRESS ISPEXEC "GETMSG MSG(ISPG218I) LONGMSG(errlmsg) HELP(hpanel)"
  getmsg_rc = RC
  SELECT
    WHEN (getmsg_rc = 0) THEN DO
      /* Normal completion. */
      SAY "The HELP panel is" hpanel"."
      SAY "The LONGMSG text is:"
      SAY ERRLMSG
      ADDRESS ISPEXEC "SETMSG MSG(ISPG218I)"
    END
    WHEN (getmsg_rc = 12) THEN DO
      /* The specified message could not be found.    */
      /*                OR                            */
      /* You need to initialize one of its variables. */
      SAY "Ask your programmer to LIBDEF ISPMLIB with the message DSN."
      SAY ZERRSM
      SAY ZERRLM
      mlib_found = 0
      msgmbr = ""
      dsnlist = ""
      ADDRESS ISPEXEC "QLIBDEF ISPMLIB ID(dsnlist)"
      pfx = "QLIBDEF :"
      tmplist = dsnlist
      DO WHILE (tmplist <> "")
        PARSE VAR tmplist onelist "," tmplist
        msg_dsnmbr = "'"STRIP(onelist,"B","'") || "(ISPG21)'"
        sysdsn_result = SYSDSN(msg_dsnmbr)
        IF (sysdsn_result = "OK") THEN DO
          IF (msgmbr = "") THEN DO
            mlib_found = 1
            msgmbr = msg_dsnmbr
          END
          sysdsn_result = "<=="
        END
        ELSE DO
          sysdsn_result = ""
        END
        SAY pfx LEFT(onelist,46) sysdsn_result
        pfx = "         "
      END
      IF (mlib_found = 0) THEN DO
        dsnlist = ""
        ADDRESS ISPEXEC "QBASELIB ISPMLIB ID(dsnlist)"
        pfx = "QBASELIB:"
        tmplist = dsnlist
        DO WHILE (tmplist <> "")
          PARSE VAR tmplist onelist "," tmplist
          msg_dsnmbr = "'"STRIP(onelist,"B","'") || "(ISPG21)'"
          sysdsn_result = SYSDSN(msg_dsnmbr)
          IF (sysdsn_result = "OK") THEN DO
            IF (msgmbr = "") THEN DO
              mlib_found = 1
              msgmbr = msg_dsnmbr
            END
            sysdsn_result = "<=="
          END
          ELSE DO
            sysdsn_result = ""
          END
          SAY pfx LEFT(onelist,46) sysdsn_result
          pfx = "         "
        END
      END
      IF (mlib_found = 1) THEN DO
        SAY "View the data set found containing ISPG21."
        say msgmbr
        ADDRESS ISPEXEC "VIEW DATASET("msgmbr")"
        view_rc = RC
        say "View:" view_rc
      END
    END
    WHEN (getmsg_rc = 20) THEN DO
      /* Severe error. */
      SAY "Ask your programmer to fix the severe error."
      SAY ZERRSM
      SAY ZERRLM
    END
    OTHERWISE DO
      SAY "Something very unexpected happened, RC("getmsg_rc")."
    END
  END

  RETURN 0
