/* We first need rexx in comment if compiled or read from SYSPROC. */
pgm_REXR0456:
  rexxpgm_version = "01.00"          /* Set with 'ver 1' and 'lev 2'. */

  SAY ""
  SAY "REXR0456: VERIFY"
  SAY "================"

  Say "0 for a complete match, >0 is where it fails."
  original_value = "(800) 555-2424"
  verify_rc = VERIFY(original_value,"- ()0123456789")
  Say original_value "verify_rc("verify_rc")"
  original_value = "(800)-(555)-(2424)"
  verify_rc = VERIFY(original_value,"- ()0123456789")
  Say original_value "verify_rc("verify_rc")"
  original_value = "(800) KEN-HOME"
  verify_rc = VERIFY(original_value," ()0123456789")
  Say original_value "verify_rc("verify_rc")"
  verify_rc = VERIFY(original_value," ()0123456789","Nomatch")
  Say original_value "verify_rc("verify_rc")"
  verify_rc = VERIFY(original_value," ()08KENHOME-","Nomatch")
  Say original_value "verify_rc("verify_rc")"
  verify_rc = VERIFY(original_value," ()0123456789","Match")
  Say original_value "verify_rc("verify_rc") should be 1."

  SAY "DSNAME verification"
  SAY "-------------------"
  first_char = "@#$QWERTYUIOPASDFGHJKLZXCVBNM"
  other_chars = "1234567890QWERTYUIOPASDFGHJKLZXCVBNM@#$"
  good_dsname = "KTOMIAK.$TEST.DSNAME1"
  isitgood_rc = VERIFY_dsname(good_dsname)
  IF (isitgoOd_rc <> 0) THEN DO
    SAY "Errors found at" isitgood_rc
  END
  bad_dsname = "1TOMIAK.$TES%.DSN ME1"
  isitgood_rc = VERIFY_dsname(bad_dsname)
  IF (isitgoOd_rc <> 0) THEN DO
    SAY "Errors found at" isitgood_rc
  END
  isitgood_rc = VERIFY_dsname(good_dsname, bad_dsname)
  IF (isitgoOd_rc <> 0) THEN DO
    SAY "Errors found at" isitgood_rc
  END
  return 0

VERIFY_dsname: PROCEDURE ,
  EXPOSE first_char other_chars
  num_args = ARG()
  test_dsname = ARG(1)
  SAY test_dsname
  isitgood_rc = 0
  badpos = ""
  level_ctr = 0
  DO WHILE test_dsname <> ""
    PARSE VAR test_dsname one_level "." test_dsname
    level_ctr = level_ctr + 1
    isitgood_rc = VERIFY(SUBSTR(one_level,1,1),first_char,"NoMatch")
    IF (isitgood_rc > 0) THEN DO
      badpos = badpos isitgood_rc
      SAY "level:" level_ctr isitgood_rc
    END
    start_pos = 2
    isitgood_rc = VERIFY(one_level,other_chars,"N",start_pos)
    DO WHILE isitgood_rc > 0
      IF (isitgood_rc > 0) THEN DO
        badpos = badpos isitgood_rc
        SAY "level:" level_ctr "char:" isitgood_rc
      END
      start_pos = isitgood_rc + 1
      isitgood_rc = VERIFY(one_level,other_chars,"N",start_pos)
    END
  END

  IF (num_args > 1) THEN DO
    SAY "VERIFY_dsname only checks the first DSNAME passed in."
    DO AX = 2 TO num_args
      SAY ARG(ax) "ignored."
    END
  END
  IF (badpos = "") THEN badpos = 0
  return badpos

/* <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
=======================================================================

 Dataset:  REXX(REXR0456)
 Author:   Kenneth Tomiak
 Contact:  "Kenneth Tomiak"<CBT_Ken@KTomiak.BIZ>
 Manual:   ikj4a3a0.pdf
           SA22-7790-10 z/OS TSO/E REXX Reference
           Version 1 Release 13
           Chapter 4. Functions
           Built-in functions
           VERIFY
 Overview: returns a number that, by default, indicates whether string
           is composed only of characters from reference;
           returns 0 if all characters in string are in reference,
           or returns the position of the first character in string not
           in reference.

-----------------------------------------------------------------------

 History of Modifications
 ------------------------
vv.mm When     Who / What you did ... (Newest change at the top.)
----- -------- --------------------------------------------------------
01.00 20171222 KTOMIAK "Kenneth Tomiak"<CBT_Ken@KTomiak.BIZ>
               1) Original base code.
----- -------- --------------------------------------------------------

-----------------------------------------------------------------------

STANDARD GLOBAL DdSCLAdMER
--------------------------
The author explicitly disavows any claim whatsoever about the
correctness or functionality of this program, and disclaims liability
for anything and everything bad that might happen in connection with,
before, during, or after using it. d have tried to make it work right,
and d am personally pretty confident that it does, but everybody makes
mistakes, so if you use it, you do so at your own risk.

=======================================================================
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> */

