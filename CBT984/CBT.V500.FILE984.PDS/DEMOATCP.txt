/* We first need rexx in comment if compiled or read from SYSPROC. */
rexx_DEMOATCP:
  rexxpgm_version = "01.00"          /* Set with 'ver 1' and 'lev 2'. */

  Say ""
  Say "ATTCHPGM DSNTIAR"
  Say "================="
  SQLCODE = -913
  SQLC = D2X(SQLCODE,8)
  SQLC = X2C(SQLC)
  Say "SQLC("sqlc")" ,
    "converted from D2X and then X2C yields:" c2x(sqlc) ,
    "in hex."

  SQLCA = "SQLCA   "
  SQLCA = SQLCA ||  X2C(00000088)
  SQLCA = SQLCA ||  SQLC
  SQLCA = SQLCA ||  X2C(0000)
  SQLCA = SQLCA ||  COPIES(" ",78)
  SQLCA = SQLCA ||  COPIES(X2C(00),24)
  SQLCA = SQLCA ||  COPIES(" ",16)
  TIAR_MSG = X2C(0190)COPIES(" ",400)
  TEXT_LEN = X2C(00000050) ;
  ADDRESS ATTCHPGM "DSNTIAR SQLCA TIAR_MSG TEXT_LEN"
  Say "Message text follows:"
  Do msgx = 1 to 400 by 80
    If (Strip(SUBSTR(TIAR_MSG,msgx,80),"T"," ") <> "") Then Do
      SAY SUBSTR(TIAR_MSG,msgx,80)
    End
  End
  return_code = RC
  Say "ATTCHPGM DSNTIAR RC("return_code")."

  Say ""
  Say "ATTCHPGM DEMOATCP"
  Say "================="
  Say ""
  msg1 = Left("If you mimic JCL's PARM= then the limit is" ,
    "100 characters",100)
  msg2 = "Unlike JCL, more than one parm can be passed."
  msg3 = "I use 1 to see how that looks."
  parms = "msg1 msg2 msg3"
  Say 'ADDRESS ATTCHPGM "DEMOATCP"' parms
  ADDRESS ATTCHPGM "DEMOATCP" parms
  Say "       " msg1
  return_code = RC
  Say "ATTCHPGM DEMOATCP RC("return_code")."

  Return return_code

/* <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<

  Dataset:  TSO.REXX(Demo)
  Contact: "Kenneth E. Tomiak"<CBT_Ken@KTomiak.BIZ>
  Overview: This is a REXX program to demonstrate using the REXX
            ADDRESS ATTACHPGM environment.
            ATTACHPGM unauthorized programs on a different task level.
            R1 P1 points to the length and parameter string.
            Internally the parameter string contains pairs of length
            and value strings. There is a firm dependency on passing
            the maximum variable length (pad with spaces if you want).

  Syntax:   %DEMOATCP

=======================================================================

  History of Modifications
  ------------------------
vv.mm When     Who  /  What you did
----- -------- --------------------------------------------------------
01.04 20171001 "Kenneth E Tomiak"<KenTomiak@KTomiak.org>
               1) Used SNAP to confirm the manual is correct. Fixed
                  my code to use R1 and the parameter list of
                  parameter length value setup
----- -------- --------------------------------------------------------
01.03 20170929 "Kenneth E Tomiak"<KenTomiak@KTomiak.org>
               1) Lots of experimentation fails to get more than one
                  parameter string. Thus I built my own method of
                  what would be a multi-parameter list of length and
                  value pairs.
----- -------- --------------------------------------------------------
01.02 20170929 "Kenneth E Tomiak"<KenTomiak@KTomiak.org>
               1) Add IBM's example for IEBGENER using alternate DD.
----- -------- --------------------------------------------------------
01.01 20170929 "Kenneth E Tomiak"<KenTomiak@KTomiak.org>
               1) Corrected spelling of variable.
----- -------- --------------------------------------------------------
01.00 20170928 "Kenneth E Tomiak"<KenTomiak@KTomiak.org>
               1) Original code.
----- -------- --------------------------------------------------------

=======================================================================

>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> */

