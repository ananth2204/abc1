/* REXX exec header
**
** EXEC:   BR2JMP
** USEAGE: Change assembler Branch instructions to their equivalent JUMPs
** PARMS:  X or NX to change only excluded or non-excluded lines
**
**
** WRITTEN BY:   Richard L. Hobt
**               DHL Worldwide Express
**               7501 E Hartford Drive
**               Scottsdale, AZ  85255
**
** CHANGE LOG
**
** RFC      DATE        BY    DESCRIPTION
** 0000000  2007/06/27  TRLH  Original exec
*/
address ISREDIT "MACRO (PARM1)"

parse upper var PARM1

if      PARM1 = "X"  then excl = "X"
else if PARM1 = "NX" then excl = "NX"
else                      excl = ""

/*
** Get number of rows.
*/
address ISREDIT "(LASTROW) = LINENUM .ZLAST"

/*
** Loop through each line.
*/

changed = 0
do current_row = 1 to LASTROW
   /* Only look at the line if the exclusion status matches */
   if EXCL Â¬= "" then address ISREDIT "(XSTAT) = XSTATUS "current_row
                 else XSTAT = ""
   if EXCL = XSTAT then do
      address ISREDIT "(THISLINE) = LINE "current_row
      label = ""
      if left(THISLINE,1) <> " " then
         parse var THISLINE label instr rest
      else
         parse var THISLINE instr rest
      /* Ignore BCTRs and comments */
      if label = "*" ,
      |  instr = "BCTR" then iterate current_row
      here = pos(instr,THISLINE)
      newline = THISLINE
      select
         when instr = "B" then ,
            newline = overlay("J",THISLINE,here)
         when instr = "BAL" then ,
            newline = overlay("JAS",THISLINE,here)
         when instr = "BAS" then ,
            newline = overlay("JAS",THISLINE,here)
         when instr = "BC" then ,
            newline = overlay("BRC",THISLINE,here)
         when instr = "BCT" then ,
            newline = overlay("JCT",THISLINE,here)
         when instr = "BE" then ,
            newline = overlay("JE",THISLINE,here)
         when instr = "BH" then ,
            newline = overlay("JH",THISLINE,here)
         when instr = "BL" then ,
            newline = overlay("JL",THISLINE,here)
         when instr = "BM" then ,
            newline = overlay("JM",THISLINE,here)
         when instr = "BNE" then ,
            newline = overlay("JNE",THISLINE,here)
         when instr = "BNH" then ,
            newline = overlay("JNH",THISLINE,here)
         when instr = "BNL" then ,
            newline = overlay("JNL",THISLINE,here)
         when instr = "BNM" then ,
            newline = overlay("JNM",THISLINE,here)
         when instr = "BNO" then ,
            newline = overlay("JNO",THISLINE,here)
         when instr = "BNP" then ,
            newline = overlay("JNP",THISLINE,here)
         when instr = "BNZ" then ,
            newline = overlay("JNZ",THISLINE,here)
         when instr = "BO" then ,
            newline = overlay("JO",THISLINE,here)
         when instr = "BP" then ,
            newline = overlay("JP",THISLINE,here)
         when instr = "BZ" then ,
            newline = overlay("JZ",THISLINE,here)
         when instr = "BXLE" then ,
            newline = overlay("BRXLE",THISLINE,here)
         when instr = "BXH" then ,
            newline = overlay("BRXH",THISLINE,here)
         otherwise iterate current_row
      end /* select */
      /* If we got this far, make the change */
      address ISREDIT "LINE" current_row "= (newline)"
      changed = changed + 1
   end
end
ZEDSMSG = changed "CHANGES"
ZEDLMSG = "There were" changed "relative addressing changes made"
address ISPEXEC "SETMSG MSG(ISRZ000)"

exit 0
