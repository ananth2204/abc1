         MACRO
&LABEL   OUTR  &RETCODE=
.**********************************************************************
.*
.*  THIS MACRO GENERATES STANDARD EXIT CODE FOR BATCH PROGRAMS USING
.*  THE 'INR' MACRO.
.*
.*  RETCODE MAY BE:
.*    'YES' INDICATING THAT R15 IS ALREADY LOADED WITH A RETURN CODE
.*    (R)   INDICATING THAT THE RETURN CODE IS IN REGISTER R
.*    XXX   SET RETURN CODE 'XXX'
.*  '0' IS THE RETURN CODE IF NOT OTHERWISE SET
.*
.**********************************************************************
         GBLC  &LGETLEN
         LCLC  &RETC,&RETREG
         LCLA  &RETCLEN
&RETC    SETC  '0'                DEFAULT RETURN CODE
         AIF   ('&RETCODE' EQ '').SETCON
         AIF   ('&RETCODE'(1,1) NE '(').SETCODE
&RETCLEN SETA  K'&RETCODE-2
&RETREG  SETC  '&RETCODE'(2,&RETCLEN)
         AGO   .SETCON
.SETCODE ANOP
&RETC    SETC  '&RETCODE'
.SETCON  ANOP
         PUSH  PRINT
         PRINT GEN
&LABEL   DS    0H
         LR    1,13               GET ADDR OF OUR SAVE AREA
         L     13,4(,13)          RESTORE CALLER'S SAVE AREA
         AIF   ('&RETCODE' EQ 'YES').SETCON3
         AIF   ('&RETREG' EQ '').SETCON2
         LR    15,&RETREG         SET RETURN CODE
         AGO   .SETCON3
.SETCON2 ANOP
         LA    15,&RETC           SET RETURN CODE
.SETCON3 ANOP
         ST    15,16(,13)         SAVE RETURN CODE ACROSS LM
         STORAGE RELEASE,LENGTH=&LGETLEN,ADDR=(1) FREE OUR SAVE AREA
         LM    14,12,12(13)       RESTORE REGS 14-12
         BR    14                 RETURN TO CALLER
         POP   PRINT
         MEND
