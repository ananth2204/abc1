/* REXX                                                            */
/* Function  : unterse a dataset with dynamic space allocation     */
/*             this version can be used in ISPF 3.4 screen         */
/*             the output untersed will have a suffix .UNTERSE     */
/* Parameter : UNPACK 'infile' -'outfile'-                         */
/* Author    : Daniel Gaeta (dfgaeta@br.ibm.com)                   */

  arg pdsni pdsno
  parse value pdsni with "'"ilib"'"
  parse value pdsno with "'"olib"'"
  suf=0
  zifile=ilib
  if index(ilib,'.TERSE')>0 then do
     ilib=substr(ilib,1,index(ilib,'.TERSE')-1)
     suf=1
  end
  address tso
  if pdsno='' then
     zofile=ilib||'.UNTERSE'
  else zofile=olib
  zrc=listdsi("'"||zifile||"'")
  newpri=sysprimary*3
  "alloc f(infile) da('"||zifile||"') shr reuse"
  "execio 1 diskr infile (stem ldata."
  "alloc f(sysprint) da(*)"
  bytect=substr(ldata.1,1,1)
  bytell=x2d(c2x(substr(ldata.1,3,2)))
  byteds=substr(ldata.1,2,1)
  if byteds='01'x then do
     bytell=bytell+4
  end
  if bytect='07'x then do
     "alloc f(outfile) da('"||zofile||"') new catalog unit(sysda)",
     "dsorg(po) lrecl("bytell") ",
     "dir(30) space("newpri")"
  end
  if bytect='02'x then do
     "alloc f(outfile) da('"||zofile||"') new catalog unit(sysda) ",
     "dsorg(ps) lrecl("bytell")"
  end
  "call 'sys1.local.linklib(trsmain)' 'UNPACK'"
  "free f(infile)"
  "free f(outfile)"

exit
