/* Rexx */
/* Aidsc: Universelle SCROLL-Routine für dynamische Areas           */
/* Unterstützte Modi:  UP + DOWN                                    */
/* Das Scrolling funktioniert analog ISPF Standards.                */
/* Mögliche Scroll-Values: PAGE, HALF, CSR, DATA (1. wird letzte)   */
/* und MAX (='M' + PF7 / PF8)                                       */
/* AUFRUF: CALL AIDSC 'P=panel A=area F=UP]DOWN S=scroll L=laenge_  */
/*                     inputstring T=topleftcorner                  */
/*                                                                  */
/*                                                                  */
/* Returnvalue:      Top_left_corner_Value: Wert der neuerrechneten */
/*                   "oberen linken Ecke"                           */
/*                                                                  */
/* P=panel            Panelname, welches gescrolled werden soll     */
/* A=area             Name der dynamischen Area                     */
/* F=UP]DOWN          scroll UP oder DOWN                           */
/* S=scrollamount     PAGE, HALF, DATA, MAX oder CSR                */
/* L=length_inputstr  Laenge des durch die dynamische Area repräsen-*/
/*                    tierten Strings                               */
/* T=top_left_corner  Obere linke Ecke (bei 1. Aufruf immer 1)      */
/* V=dsc_vdepth       &LVLINE auf Panel (Last visible Line im SPLIT)*/
/*                                                                  */
/* Ist der Benutzer im SPLIT-Screen mode, so wird dies bei den      */
/* Berechnungen berücksichtigt.                                     */
/*                                                                  */
/*                                            15.07.96 $$TRA        */
/*                                                                  */
Arg parmstr
Parse Var parmstr . 'P='dsc_panlname .    /* Panelname from caller  */
Parse Var parmstr . 'A='dsc_areaname .    /* Areaname from caller   */
Parse Var parmstr . 'F='dsc_scrollfc .    /* Scroll-fc from caller  */
Parse Var parmstr . 'S='dsc_scrollam .    /* Scr-Amount from caller */
Parse Var parmstr . 'L='dsc_stringlg .    /* Length of String       */
Parse Var parmstr . 'T='dsc_topleftc .    /* Top left Corner        */
Parse Var parmstr . 'V='dsc_vdepth .      /* Last visible line      */
/********************************************************************/
/* Query the Panelcharacteristics + save the values we got          */
/********************************************************************/
'ISPEXEC PQUERY  PANEL('dsc_panlname') AREANAME('dsc_areaname'),
        AREATYPE(DSCATYP) WIDTH(DSCAWID),
        ROW(DSCAROW) COLUMN(DSCACOL)'
/********************************************************************/
/* First, Check if DOWN-Key was pressed                             */
/********************************************************************/
If dsc_scrollfc = 'DOWN' Then              /* Yes, it's Down, get   */
  Do                                       /* the SCROLL-Amt.       */
    Select
      When Datatype(dsc_scrollam) = 'NUM' Then
        new_topleftc = dsc_topleftc + (dscawid * dsc_scrollam)
/********************************************************************/
/* nnnn: numeric Value: add Value * width to top-left-corner        */
/********************************************************************/
      When dsc_scrollam = 'PAGE' Then      /* Page, it is simple |  */
        new_topleftc = dsc_topleftc + (dscawid * dsc_vdepth)
/********************************************************************/
/* PAGE: 1 Page = width of dynamic Area (dscawid) *                 */
/*                Nr. of visible lines (dsc_vdepth)                 */
/********************************************************************/
      When dsc_scrollam = 'HALF' Then
        new_topleftc = dsc_topleftc + (dscawid * (dsc_vdepth % 2))
/********************************************************************/
/* HALF: 1 Page Modulo 2                                            */
/********************************************************************/
      When dsc_scrollam = 'DATA' Then
        new_topleftc = dsc_topleftc + (dscawid * (dsc_vdepth - 1))
/********************************************************************/
/* DATA: 1 Page - 1 Line                                            */
/********************************************************************/
      When dsc_scrollam = 'MAX' Then
        Do
          new_topleftc = dsc_stringlg - (dscawid * dsc_vdepth) + 1
          If new_topleftc < 1 Then
            new_topleftc = 1
        End
/********************************************************************/
/* MAX: Go to the bottom (dsc_stringlg) and subtract 1 Page,        */
/*      and then add 1 to point to the 1st pos. of the last Page    */
/********************************************************************/
      When dsc_scrollam = 'CSR' Then
        Do
/********************************************************************/
/* Cursor, its getting complicated |                                */
/* First, get the Areaname (AIDSC010) and the relative CursorPos.   */
/*        in this Area (AIDSC020) (rel.Pos = #of bytes from the top */
/*        left corner of the Area)                                  */
/********************************************************************/
          'ISPEXEC VGET (AIDSC010 AIDSC020) SHARED'
          If aidsc010 = dsc_areaname Then   /* Is this really "our" */
            Do                              /* area? - Yes, compute|*/
              dsc_currow = aidsc020
/********************************************************************/
/* dsc_curline is the line, on which the cursor is positionned:     */
/* rel. cursorposition-1 modulo areawidth = line-1, + 1 = line      */
/* e.g: curpos=188, width=20                                        */
/* 188-1 = 187 % 20 = 9 + 1 = 10  => cursor is on line 10           */
/* or curpos = 1, width = 20                                        */
/* 1-1=0 % 20 = 0 + 1 = 1    => cursor is on line 1                 */
/********************************************************************/
              dsc_curline = (aidsc020 - 1) % dscawid + 1
/********************************************************************/
/* new_topleftc is the new top left corner:                         */
/* oldtopleftcorner + (cursorline-1) * width                        */
/********************************************************************/
              new_topleftc = dsc_topleftc + ((dsc_curline-1) * dscawid)
            End
          Else
            new_topleftc = dsc_topleftc + (dscawid * dsc_vdepth)
/********************************************************************/
/* cursor is not on the activ area, so scrolling is PAGE            */
/********************************************************************/
        End
      Otherwise
        Nop
    End
/********************************************************************/
/* If Scrolling exeeds bottom of data, show the last line on top    */
/********************************************************************/
    If new_topleftc >= dsc_stringlg Then
      new_topleftc = dsc_stringlg - dscawid + 1
  End
Else If dsc_scrollfc = 'UP' Then
  Do
    Select
      When Datatype(dsc_scrollam) = 'NUM' Then
        new_topleftc = dsc_topleftc - (dscawid * dsc_scrollam)
/********************************************************************/
/* nnnn: numeric Value: add Value * width to top-left-corner        */
/********************************************************************/
      When dsc_scrollam = 'PAGE' Then
        new_topleftc = dsc_topleftc - (dscawid * dsc_vdepth)
/********************************************************************/
/* HALF: 1 Page = % 2                                               */
/********************************************************************/
      When dsc_scrollam = 'HALF' Then
        new_topleftc = dsc_topleftc - (dscawid * (dsc_vdepth % 2))
/********************************************************************/
/* DATA: 1 Page - 1 Line                                            */
/********************************************************************/
      When dsc_scrollam = 'DATA' Then
        new_topleftc = dsc_topleftc - (dscawid * (dsc_vdepth - 1))
/********************************************************************/
/* MAX: always 1                                                    */
/********************************************************************/
      When dsc_scrollam = 'MAX' Then
        new_topleftc = 1
      When dsc_scrollam = 'CSR' Then
        Do
          'ISPEXEC VGET (AIDSC010 AIDSC020) SHARED'
          If aidsc010 = dsc_areaname Then
            Do
              dsc_currow = aidsc020
/********************************************************************/
/* dsc_curline is the same as described in the 'DOWN' section.      */
/* new_topleftc is the new top left corner:                         */
/* oldtopleftcorner - (cursorline  * width)                         */
/********************************************************************/
              dsc_curline = (aidsc020 - 1) % dscawid + 1
              new_topleftc = dsc_topleftc -,
                  ((dsc_vdepth * dscawid) - ((dsc_curline) * dscawid))
            End
          Else
            new_topleftc = dsc_topleftc - (dscawid * dsc_vdepth)
        End
      Otherwise
        Nop
    End
    If new_topleftc <= 1 Then
      new_topleftc = 1
  End
Else
  Nop
Return new_topleftc
