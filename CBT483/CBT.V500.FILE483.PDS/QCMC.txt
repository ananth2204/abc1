/* Rexx */
/* QCMD Console-Interface                                           */
/* This procedure establishes the Console-interaction between QCMD  */
/* and the user.                                                    */
/* The Command-Syntax is the same as invoking QCMD directly.        */
/* Exceptions: Messagedelivery MUST be 'V'                          */
/*             QCMD must not be specified in front of the Command.  */
/* Please remember that the assigned Console-Authority is MASTER.   */
/* CLASS OPERCMDS does not affect QCMD.                             */
/*                                                                  */
/* The QCMD-Syntax is as follows:                                   */
/*                                                                  */
/* 1. with a native MVS-Command:  TSO QCMD command                  */
/*    command may be any valid MVS-Command:                         */
/*                                                                  */
/*    TSO QCMD CF CHP(xx),ONLINE or TSO QCMD D GRS,C                */
/*                                                                  */
/* 2. With additional Parameters, other than Command                */
/*                                                                  */
/*    TSO QCMD CMD=(command),T=x,M=y                                */
/*                                                                  */
/*      - command: see above                                        */
/*      - T=x:     the time in seconds to wait for the message.     */
/*                 (1-999)                                          */
/*      - M=P:     Message-delivery: P=PUTLINE (default)            */
/*          V:                       V=into Rexx-Variables          */
/*          B:                       B=Both                         */
/*                                                                  */
/*    Assume that 'TSO QCMD F LLA,REFRESH' returns no message.      */
/*    So specifiy 'TSO QCMD CMD=(F LLA,REFRESH),T=10 and you may    */
/*    get it into 10 seconds.                                       */
/*    With the Console-Interface, M=V is recommended.               */
/*                                                                  */
/* The following Rexx-variables are created by QCMD M=V or BOTH:    */
/*                                                                  */
/* "Normal"-MVS-Command             Variable contents:              */
/* --------------------             ----------------------------    */
/* QCMD.MSG_COLOR.0                 Number of Color-elements        */
/* QCMD.MSG_COLOR.1-n               Color-attribute of Message      */
/* QCMD.MSG_HILITE.0                Number of hilite-Attributes     */
/* QCMD.MSG_HILITE.1-n              Hilite-attribute of Message     */
/* QCMD.MSG_TEXT.0                  Number of Messages returned     */
/* QCMD.MSG_TEXT.0-n                Message Text 1-n                */
/* QCMD_RC                          Function Return-Code            */
/*                                  0 - Execution Successfull       */
/*                                  8 - No Message Returned         */
/*                                                                  */
/* Specialcase GET_REPLIES          Variable contents:              */
/* -----------------------          ----------------------------    */
/* QCMD.REPLY_ID.0                  Number of Outstanding WTORs     */
/* QCMD.REPLY_ID.1-n                Reply-Ids 1-n                   */
/* QCMD.REPLY_ID.0                  Number of Outstanding WTORs     */
/* QCMD.REPLY_ID.1-n                Reply-Ids 1-n                   */
/* QCMD.ISSUER.0                    Number of Owners of WTORs       */
/* QCMD.ISSUER.1-n                  Owners list                     */
/* QCMD.JOB_ID.0                    Number of Jobname-Elements      */
/* QCMD.JOB_ID.1-n                  Tasknames of WTORS              */
/* QCMD.JOB_NAME.0                  Number of taskname-Elements     */
/* QCMD.JOB_NAME.1-n                tasknames of Wtors              */
/* QCMD.REPLY_TEXT.0                Number of Outstanding WTORs     */
/* QCMD.REPLY_TEXT.1-n              Text-Ids                        */
/* QCMD_RC                          Function Return-Code            */
/*                                  0 - Execution Successfull       */
/*                                  4 - No Replies outstanding      */
/*                                  8 - No Message Returned         */
/*                                                                  */
/* QCMD Runs as a TSO-Command in Foreground- or in Batchmode.       */
/*                                                                  */
/*                                  6.1.97 $$TRA                    */
/*                                                                  */
/********************************************************************/
Address TSO
Arg mvscmd
flin = 0
tlin = 0
llin = 0
If mvscmd = '' Then                /* No Command passed, show panel */
  Do While mvscmd = ''
    'ISPEXEC DISPLAY PANEL(QCMP010) CURSOR(MVSCMD)'
    If Rc > 0 Then
      Exit
  End
spos = Pos('CMD=(',mvscmd)
If spos > 0 Then                   /* Parameters other than command?*/
  Do
    cut_pos = Substr(mvscmd,Lastpos(')',mvscmd))
    Parse Var cut_pos . 'M='dlv .
    If dlv = '' Then
      mvscmd = mvscmd',M=V'
    Else If Pos('V',dlv) ¬= 1 Then
      Do
        Say 'Invalid messagedelivery specified. Must be V w. Console.'
        Exit
      End
    Else
      Nop
  'QCMD 'mvscmd                    /* Yes, Issue ASIS               */
  End
Else
  'QCMD CMD=('mvscmd'),M=V'        /* No, Set delivery to rexx-vars */
dyndisp = ''
datastr = ''
newfirst = 1
disp_Rc = 0
'ISPEXEC PQUERY PANEL(QCMP010) AREANAME(DYNDISP) WIDTH(DYNWIDTH),
         DEPTH(DYNDEPTH)'
pagelen = dynwidth * dyndepth      /* visible page */
j = 0
If Qcmd_Rc = 0 Then                /* something returned ? */
  Do
    Upper mvscmd
    If Pos('GET_REPLIES',mvscmd) > 0 Then
      Do
        qcmdc1 = 'RED'
        qcmdh1 = 'USCORE'
        qcmdc2 = 'BLUE'
        qcmdc3 = 'YELLOW'
        qcmdc4 = 'TURQ'
        line = ('04'x) || (' Replies Outstanding: ') || ('04'x) ||,
                 (qcmd.reply_id.0) || ('02'x)
        datastr = datastr''Substr(line,1,dynwidth)
        Do i = 1 to qcmd.reply_id.0
          datastr = datastr''Substr(' ',1,dynwidth,' ')
          line = ('02'x) || (' Reply-Id:     ') || ('01'x) ||,
                 (qcmd.reply_id.i) || ('02'x)
          datastr = datastr''Substr(line,1,dynwidth)
          line = ('02'x) || (' Reply-Issuer: ') || ('03'x) ||,
                 (qcmd.issuer.i) || ('02'x)
          datastr = datastr''Substr(line,1,dynwidth)
          line = ('02'x) || (' Taskname:     ') || ('03'x) ||,
                 (qcmd.job_id.i) || ('02'x)
          datastr = datastr''Substr(line,1,dynwidth)
          line = ('02'x) || (' Tasknumber:   ') || ('03'x) ||,
                 (qcmd.job_number.i) || ('02'x)
          datastr = datastr''Substr(line,1,dynwidth)
          line = ('02'x) || (' Reply-Text:   ') || ('03'x) ||,
                 (qcmd.reply_text.i) || ('02'x)
          If Length(line) > dynwidth Then
            Do
              lastwrd = Lastpos(' ',line)
              contpos = Pos(qcmd.reply_text.i,line)
              line1 = Substr(line,1,lastwrd)
              line1 = Substr(line1,1,dynwidth,' ')
              line2 = Substr(line,lastwrd+1)
              line2 = Copies(' ',contpos-1)''Substr(line,lastwrd+1)
              line2 = Substr(line2,1,dynwidth,' ')
              line = line1''line2
              datastr = datastr''line
            End
          Else
            datastr = datastr''Substr(line,1,dynwidth)
        End
      End
    Else Do i = 1 to qcmd.msg_color.0
      j = j + 1
      attr_char = D2c(j)
      Interpret 'qcmdc'j' = 'qcmd.msg_color.i
      If qcmd.msg_hilite.i ¬= 'DEFAULT' Then
        Interpret 'qcmdh'j' = 'qcmd.msg_hilite.i
      line = Substr(attr_char''qcmd.msg_text.i,1,dynwidth)
      datastr = datastr''line
      If j / dyndepth = 1 Then
        j = 0
    End
    curfld = 'ZCMD'
    mvscmd = ''
  End
Else                          /* Nothing returned, show errormsg */
  Do
    qcmdc1 = 'TURQ'
    qcmdh1 = 'REVERSE'
    pad = Substr(' ',1,4*dynwidth,' ')
    pad1 = ('01'x) || (Substr(' ',1,dynwidth-2,' ')) || ('02'x)
    li.1 = ' '
    If qcmd_Rc = 8 Then
      Do
        li.2 = 'Error: Command 'mvscmd' Returns no message.'
        li.3 = 'Enter YES to re-execute the Command with a wait',
                'time of 10 seconds.'
      End
    If qcmd_Rc = 4 Then
      Do
        li.2 = '***** No outstanding replies found. *****'
        li.3 = 'Request GET_REPLIES ended with an RC=4.'
        mvscmd = ''
      End
    li.4 = ' '
    li.0 = 4
    Do i = 1 to li.0
      li.i = ('01'x) || (' ') || ('02'x) || (Centre(li.i,dynwidth-6))
      li.i = (li.i) || ('01'x) || (' ') || ('02'x)
      datastr = (datastr) || (li.i)
    End
    curfld = 'MVSCMD'
    savecmd = mvscmd
    datastr = (pad) || (pad1) || (datastr) || (pad1)
  End
Do While disp_Rc ¬= 8
  dyndisp = Substr(datastr,newfirst,pagelen,' ')
  If qcmd_Rc = 8 Then
    mvscmd = 'YES'
  Else If qcmd_rc = 4 Then
    Nop
  Else                                   /* line-counters */
    Do
      flin = newfirst % dynwidth + 1
      tlin = length(datastr) % dynwidth
      llin = flin + dyndepth - 1
      If llin > tlin Then
        llin = tlin
    End
  'ISPEXEC DISPLAY PANEL(QCMP010) CURSOR('CURFLD')'
  disp_Rc = Rc
  If disp_Rc = 8 Then
    Exit
  If mvscmd ¬= '' Then
    Do
      If qcmd_Rc = 8 Then
        Do
          If mvscmd = 'YES' Then
            mvscmd = 'CMD=('savecmd'),M=V,T=10'
        End
      Call QCMC mvscmd
      Exit
    End
  'ISPEXEC VGET (ZVERB ZSCROLLA ZSCROLLN)'
  If Wordpos(zverb,'UP DOWN') > 0 Then
    Do                            /* Yes, so do it.                 */
      Call QCMSCRL 'P=QCMP010 A=DYNDISP F='zverb' V='dyndepth ,
                 'S='zscrolla' L='Length(datastr)' T='newfirst
      newfirst = Result
    End
End
Exit
