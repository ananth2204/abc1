*/********************************************************************/
*/*                                                                  */
*/*  ADDMSG MACRO:                                                   */
*/*  APPENDS A MESSAGE INTO AN USER-GETMAINED AREA.                  */
*/*                                                                  */
*/*  PARAMETERS:                                                     */
*/*  -----------                                                     */
*/*  3, ALL  POSITIONAL : FIRST FULLWORD OR REG. CONTAINS THE LENGTH */
*/*                       OF THE MESSAGE TO BE STORED.               */
*/*                       2ND FULLWORD OR REG. POINTS AT THE START   */
*/*                       OF THE MESSAGE TO BE STORED.               */
*/*                       3RD FULLWORD OR REG. POINTS AT THE START   */
*/*                       OF THE AREA WHERE THE MESSAGE SHOULD BE    */
*/*                       STORED                                     */
*/*                                                                  */
*/*  $$NOTE$$ THESE INPUT-CONVENTIONS MATCH THE OUTPUT OF "PRINTREG" */
*/*                                                                  */
*/*  FUNCTIONALITY:       THE MACRO PICKS THE LENGTH, SUBTRACTS 1    */
*/*                       AND THE EXECUTES THE MESSAGE. THE MESSAGE  */
*/*                       IS THEN STORED INTO A GETMAINED AREA PRE-  */
*/*                       CEEDED BY A HALFWORD CONTAINING THE LENGTH */
*/*                       OF THE MESSAGE.                            */
*/*                                                                  */
*/*  REGISTER-CONTENTS AT RETURN:   REG. 15 CONTAINS 0, REGISTER 0   */
*/*                       THE LENGTH OF THE MESSAGE EXCLUDING THE    */
*/*                       PRECEEDING HALFWORD,  R1 POINTS AT THE     */
*/*                       END OF THE JUST STORED MESSAGE.            */
*/*                                                                  */
*/*  THE MACRO ADDMSG   IS REENTRANT.                                */
*/*                                                                  */
*/********************************************************************/
         MACRO
&NAME    ADDMSG &MSGLEN,&MSGADDR,&OUTADDR
.*********************************************************************
.* SET LOCAL VARIABLES
.*********************************************************************
&A       SETA  &SYSNDX             LABEL/FIELD NAME INDEX
.*
.******************************************************************
.* IF THE &PRINTV PARAMETER IS A REGISTER BRANCH TO ISAREG  TO
.* PROCESS IT.
.******************************************************************
.*
         AIF   (('&MSGLEN' NE '') AND ('&MSGADDR' NE '' ) AND          X
               ('&OUTADDR' NE '')).CHKMLN
         MNOTE 8,'ONE OR MORE REQUIRED PARAMETER(S) MISSING'
         MEXIT
.*
.CHKMLN  AIF   ('&MSGLEN'(1,1) NE '(').MLNOREG     NOT REGISTERFORM
         LR    0,&MSGLEN
         AGO   .CHKMAD
.MLNOREG L     0,&MSGLEN
.*
.CHKMAD  AIF   ('&MSGADDR'(1,1) NE '(').MANOREG    NOT REGISTERFORM
         LR    1,&MSGADDR
         AGO   .CHKOAD
.MANOREG L     1,&MSGADDR
.*
.CHKOAD  AIF   ('&OUTADDR'(1,1) NE '(').OANOREG    NOT REGISTERFORM
         LR    15,&OUTADDR
         AGO   .GOTVALS
.OANOREG L     15,&OUTADDR
.*
.GOTVALS ANOP
         STH   0,0(15)                   STORE PASSED LENGTH OF MSG
         BCTR  0,0                       SUBTRACT 1 AND ...
         LR    14,0                      USE R14 FOR EXECUTE (R0 FAILS)
         EX    14,$ADDMSG_&A._L0010      DO THE EXECUTE
$ADDMSG_&A._L0010 MVC   2(0,15),0(1)     MOVE BEHIND LENGTH INTO WA
         LA    14,3(0,14)                ADD 3 (EX. + HALFWORD LENGTH)
         LR    0,14                      RELOAD R0
         LR    1,15                      POINT AT BEGIN OF MESSAGE
         AR    1,0                       AND ADD LENGTH TO POINT AT END
         XR    15,15                     CLEAR WORKREG
         MEND
