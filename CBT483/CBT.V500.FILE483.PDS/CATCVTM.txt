/* Rexx */
/* CATCVTM uses a member of the pds created by MCNVTCAT (Mastercata- */
/* log conversion Utility) as input and rebuilds IDCAMS-commandblocks*/
/* after an X aLL;F ALL xxxx command.                                */
/* e.g: You enter in the SYS1-member "X ALL;F ALL '******' to find   */
/*      all entries on the SYSRES. Then, just type CATCVTM and the   */
/*      macro will reshow you the whole IDCAMS-Command of the entries*/
/*      so that you can easily build new members and/or DEL ALL X aso*/
Address 'ISREDIT'
'MACRO'
Trace Off
'(toline) = LINENUM .zlast'
If toline <= 1 Then
  Exit 12
Do i = 1 to toline
  '(excstat) = XSTATUS 'i
  If excstat = 'NX' Then
    Do
      curri = i
      topdone = 'NO'
      Do While topdone = 'NO' & curri > 0
        '(currlin) = LINE 'curri
        If Pos('- ',currlin) > 0 Then
          Do
            'RESET EXCLUDED 'curri curri
            curri = curri - 1
          End
        Else
          topdone = 'YES'
      End
      curri = i
      botdone = 'NO'
      Do While botdone = 'NO' & curri <= toline
        '(currlin) = LINE 'curri
        If Pos('- ',currlin) > 0 Then
          Do
            'RESET EXCLUDED 'curri curri
            curri = curri + 1
          End
        Else
          Do
            'RESET EXCLUDED 'curri curri
            botdone = 'YES'
          End
      End
      i = curri
    End
End
Exit
