*/********************************************************************/
*/*                                                                  */
*/*  PRINTREG MACRO:                                                 */
*/*  MAKE A FULLWORD OR REGISTER CONTENT PRINTABLE                   */
*/*  PARAMETERS:                                                     */
*/*  -----------                                                     */
*/*  FIRST IS POSITIONAL: FULLWORD OR REGISTER TO BE PRINTED.        */
*/*  DEFAULT:             NONE                                       */
*/*                                                                  */
*/*  PADCHAR=           : PADDING CHARACTER TO BE USED INSTEAD OF    */
*/*                       LEADING ZEROES. CAN BE ANY PRINTABLE       */
*/*                       CHARACTER. SPECIAL CASES: 'BLANK' USES     */
*/*                       X'40', 'NULLSTRING' RETURNS RESULT IN      */
*/*                       "EXECUTED" FORM, WITHOUT LEADING PADS.     */
*/*  DEFAULT            : X'F0'                                      */
*/*                                                                  */
*/*  RESULT=            : SPECIFIES WHICH NUMBER MODE TO USE AS      */
*/*                       OUTPUT: MUST BE EITHER 'DECIMAL' OR        */
*/*                       'HEX', WHICH IS THE DEFAULT                */
*/*                                                                  */
*/*  REGISTER-CONTENTS AT RETURN:   REG. 15 CONTAINS 0, REGISTER 0   */
*/*                       THE LENGTH OF THE RESULT (INCLUDING PAD-   */
*/*                       DING CHARACTERS), AND R1 POINTS TO THE     */
*/*                       ANSWER AREA.                               */
*/*                       IF RESULT IS DECIMAL, THERE ARE 16 BYTES   */
*/*                       RETURNED.                                  */
*/*                                                                  */
*/*  THE MACRO PRINTREG IS REENTRANT.                                */
*/*                                                                  */
*/********************************************************************/
         MACRO
&NAME    PRINTREG &PRINTV,&PADCHAR=,&RESULT=
.*********************************************************************
.* SET LOCAL VARIABLES
.*********************************************************************
&A       SETA  &SYSNDX             LABEL/FIELD NAME INDEX
         LCLB  &EXFLAG,&BLKFLAG,&DEFPAD,&TRFLAG,&EDFLAG
         LCLC  &PCHAR
.*********************************************************************
.* BUMP OVER PSEUDO-DSECT WHICH DETERMINES THE AMOUNT OF STORAGE
.* NEEDED WHITIN THE MACRO
.*********************************************************************
         B     CHK&A
.*
         DS    0F                  ALIGN STORAGE
WORKDW&A DS    D                   USED FOR CVD
WKMASK&A DS    CL16                USED FOR EDIT
         DS    CL1                 WORKBYTE FOR UNPACK
LWRKDS&A EQU   *-WORKDW&A
.*************
.* CONSTANTS
.*************
EDMASK&A DC    XL16'F0202020202020202020202020202020'   EDIT-MASK
TRTAB&A  DC    XL240'00'
         DC    C'0123456789ABCDEF'     TRANSLATION-TABLE
.*
CHK&A    DS    0H
.*
.*********************************************************************
.* START REGULAR ASSEMBLER PROCESSING HEREAFTER
.*********************************************************************
.*
         STORAGE OBTAIN,LENGTH=LWRKDS&A,LOC=ANY  STAY REENTRANT
.*
&PCHAR   SETC  '0'                 THE PADDING CHARACTER
&EXFLAG  SETB  ('&PADCHAR' EQ 'NULLSTRING')  SET EXECUTE-FORM FLAG
&BLKFLAG SETB  ('&PADCHAR' EQ 'BLANK')       PADCHAR IS BLANK
&DEFPAD  SETB  (('&PADCHAR' EQ '') OR ('&PADCHAR' EQ '0') OR           X
               (&EXFLAG))          IS IT THE DEFAULT-PADDING ?
         AIF   ((&EXFLAG) OR (&BLKFLAG) OR (&DEFPAD)).PADOK
         AIF   (K'&PADCHAR EQ 1).MOVEIT      IF PADCHAR GIVEN, REFILL
         MNOTE 8,'PADDING CHARACTER '&PADCHAR' IS INVALID'
         MEXIT
.MOVEIT  ANOP
&PCHAR   SETC  '&PADCHAR'
.PADOK   ANOP
.*
.*********************************************************************
.* IF THE RESULT SHOULD BE HEXADECIMAL, USE TRANSLATE
.*********************************************************************
.*
&TRFLAG  SETB  (('&RESULT' EQ '') OR ('&RESULT' EQ 'HEX'))
&EDFLAG  SETB  ('&RESULT' EQ 'DECIMAL')    RESULT WILL BE DECIMAL
         AIF   ((&TRFLAG) OR (&EDFLAG)).RESLTOK   IS RESULT= VALID?
         MNOTE 8,'RESULT MUST EITHER BE "HEX" OR "DECIMAL"'
         MEXIT
.RESLTOK ANOP
.*
.******************************************************************
.* IF THE &PRINTV PARAMETER IS A REGISTER BRANCH TO ISAREG  TO
.* PROCESS IT.
.******************************************************************
.*
         AIF   ('&PRINTV'(1,1) NE '(').ISNOREG     NOT REGISTERFORM
         LR    15,&PRINTV               LOAD REGISTER TO BE PRINTED
         AGO   .GOTVAL
.ISNOREG L     15,&PRINTV               FULLWORD PASSED, USE LOAD
.GOTVAL  ANOP
.********************************************************************
.*  DECIDE WHETHER TO EDIT OR TO UNPACK/TRANSLATE THE INPUT
.********************************************************************
         AIF   (&TRFLAG).DOTRANS        DO I HAVE TO TRANSLATE ?
         CVD   15,0(1)                  CONVERT TO DECIMAL
         MVC   8(16,1),EDMASK&A         MOVE THE EDITMASK THEREAFTER
.*********************************************************************
.* IF NO PADCHAR HAS BEEN PASSED, THEN JUMP AT END OF PADCHAR CHECKS
.*********************************************************************
.*
         AIF   (&DEFPAD).DOEDIT         DEFAULT PADDING REQ'ED ?
         AIF   (&BLKFLAG).MOVEBL        BLANK REQUIRED ?
         MVI   8(1),C'&PCHAR'           MOVE THE PADCHAR INTO EDITMASK
         AGO   .DOEDIT
.MOVEBL  MVI   8(1),C' '                MOVE IN A BLANK AT POSTION 1
&PCHAR   SETC  ' '
.DOEDIT  ED    8(16,1),0(1)             EDIT THE DW
         AGO   .STORREL                 BUMP OVER TRANSLATE-PROCESSING
.DOTRANS ST    15,0(1)                  STORE R15 INTO GETMAINED AREA
         UNPK  8(9,1),0(5,1)            UNPACK IT INTO RESULT-AREA
         TR    8(8,1),TRTAB&A           TRANSLATE THE STUFF
         MVI   16(1),C' '               CLEAR WORKBYTE AT THE END
         AIF   ((&EXFLAG) OR (&DEFPAD)).STORREL    DEF. PAD, CONTINUE
         LR    15,1                     POINT AT BEGIN OF RESULT
         LA    0,7                      MAX. 7 PADS, ELSE ZERO
TRLUP&A  CLI   8(15),C'0'               IS IT A '0' ?
         BNE   ENDPAD&A                 (BNE=NO, NO FURTHER PADDING)
         MVI   8(15),C'&PCHAR'          MOVE THE PADCHAR IN THERE
         LA    15,1(,15)                POINT AT NEXT CHARACTER
         BCT   0,TRLUP&A                RETRY
ENDPAD&A DS    0H                       END OF PADDING ACTIONS
.STORREL STORAGE RELEASE,LENGTH=LWRKDS&A,ADDR=(1) STAY REENTRANT
         LA    1,8(0,1)                 POINT THE EDITED/TRLTED AREA
         LA    0,8                      ASSUME TRANSLATE, SO L=8
         AIF   (&TRFLAG).GOTLEN         IS IT REALLY TRLATED ?
         LA    0,16                     NO, LENGTH IS 16
.GOTLEN  AIF   (&EXFLAG EQ 0).BYEBYE    NO EXECUTE TO MAKE, GO AHEAD
CMP&A    CLI   0(1),C'0'                IS IT NO DATA ?
         BNE   ENDLUP&A                 (BNE=NO, START OF DATA, EXIT)
         LA    1,1(,1)                  POINT TO NEXT BYTE
         BCT   0,CMP&A                  RE-DO TEST
         BCTR  1,0                      IF ALL ZEROES, JUST GO 1 BACK
         LA    0,1                      LOAD LENGTH OF 1
ENDLUP&A DS    0H                       END OF "EXECUTING"
.BYEBYE  XR    15,15                    CLEAR WORKREG
         MEND
