       PROCESS NOADV
       IDENTIFICATION DIVISION.
       PROGRAM-ID. JXC310JX.
      *****************************************************************
      *                                                               *
      * (C) 2005 RENAISSANCE DATA SYSTEMS INC.                        *
      *          CHANGEWIZ STAT REL 1.0 VER 1.0                       *
      *                                                               *
      * THESE PROGRAMS ARE DISTRIBUTED ON THE CBT TAPE WITH THE       *
      * PROVISO THAT THEY MAY BE FREELY DISTRIBUTED TO ANY OTHER      *
      * PARTY ON CONDITION THAT NO INDUCEMENT BEYOND REASONABLE       *
      * HANDLING COSTS IS OFFERED OR ACCEPTED BY EITHER SIDE FOR      *
      * SUCH DISTRIBUTION OR YOUR NORMAL CONSULTING COSTS FOR       . *
      * INSTALLATION AND SUPPORT.                                     *
      *                                                               *
      *                                                               *
      *****************************************************************
      *JX* MERGE DAILY CHANGE RECORDS WITH HISTORY FILE.
      *
      *    1. MATCH DAILY TO HISTORY BY ELEMENT NAME AND LIBRARY.
      *    2. SORT BOTH FILES BY ELEMENT NAME, LIBRARY,
      *       LEVEL, PROMOTE DATE/TIME
      *    3. DO NOT ADD DUPLICATES.
      *    4. SET LEVEL FOR ADDED RECORDS
      *    5. MATCH ON PROMOTE-DATE FOR SCLM AND EDIT-DATE FOR PDS
      *    6. IF A DELETED ELEMENT IS BEING RESTORED,
      *         GIVE IT A NEW LEVEL.
      *
      * A0000-  INITIALIZATION AND TERMINATION ROUTINES
      * C0000-  MAIN PROCESSING
      * D0000-  FORMAT OUTPUT RECORDS
      * X0000-  PROGRAM READ, WRITE AND OTHER I-O ROUTINES
      * Y0000-  DEBUG ROUTINES
      * Z0000-  ABEND ROUTINES
      *
      *
      ****************************************************************

       ENVIRONMENT DIVISION.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.

      * INPUT FILES
           SELECT CHANGE-HIST-FILE
               STATUS IS CHANGE-HST-STATUS ASSIGN CHGHIST.
           SELECT CHANGE-DLY-FILE
               STATUS IS CHANGE-DLY-STATUS ASSIGN CHGDLY.
           SELECT RUN-TYPE
               STATUS IS RUN-TYPE-STATUS   ASSIGN RUNTYPE.

      * OUTPUT FILES
           SELECT NEW-CHANGE-FILE
               STATUS IS NEW-CHANGE-STATUS ASSIGN NEWCHG.

           SELECT DEBUG-REPORT             ASSIGN RDBUG.

       DATA DIVISION.
       FILE SECTION.

       FD  CHANGE-HIST-FILE
           RECORDING MODE IS F
           BLOCK CONTAINS 0 RECORDS.
       01  CHANGE-HIST-RECORD          PIC X(200).

       FD  CHANGE-DLY-FILE
           RECORDING MODE IS F
           BLOCK CONTAINS 0 RECORDS.
       01  CHANGE-DLY-FILE-RECORD      PIC X(200).

       FD  RUN-TYPE
           RECORDING MODE IS F
           BLOCK CONTAINS 0 RECORDS.
       01  RUN-TYPE-RECORD             PIC X(80).

       FD  NEW-CHANGE-FILE
           RECORDING MODE IS F
           BLOCK CONTAINS 0 RECORDS.
       01  NEW-CHANGE-RECORD           PIC X(200).


       FD  DEBUG-REPORT
           RECORDING MODE IS F
           BLOCK CONTAINS 0 RECORDS.
       01  DEBUG-RPT-LINE              PIC X(133).


       WORKING-STORAGE SECTION.

       01  WS-PROGRAM                  PIC X(08) VALUE 'JXC310JX'.

      *  VENDOR INFO
           COPY JXCPW001.

      *  REFWIZ PRODUCT INFO
           COPY JXCPW002.


       01  WS-RUN-TYPE.
           05  WS-RUN-TYPE-LIB         PIC X(08).
               88  WS-VALID-RUN-TYPE              VALUE 'SCLM    '
                                                        'PDS     '.
               88  WS-PDS-RUN-TYPE                VALUE 'PDS     '.
               88  WS-SCLM-RUN-TYPE               VALUE 'SCLM    '.
           05  FILLER                  PIC X(72).

           EJECT
      *  CHANGE HISTORY RECORD LAYOUT
           COPY JXCPR300.

      *  CHANGE HISTORY RECORD LAYOUT 2
           COPY JXCPR305.

           EJECT
       01  WS-CURR-KEY.
           05  WS-CURR-ELEMENT         PIC X(08).
           05  WS-CURR-LIBRARY         PIC X(08).

       01  WS-DLY-KEY.
           05  WS-DLY-ELEMENT          PIC X(08).
           05  WS-DLY-LIBRARY          PIC X(08).


       01  WORK-IO-FIELDS.
           05  CHANGE-HIST-CNT         PIC 9(09) VALUE ZERO.
           05  CHANGE-DLY-CNT          PIC 9(09) VALUE ZERO.
           05  DLY-DUMMY-CNT           PIC 9(09) VALUE ZERO.
           05  DLY-WRITTEN-CNT         PIC 9(09) VALUE ZERO.
           05  HIST-WRITTEN-CNT        PIC 9(09) VALUE ZERO.
           05  TOTAL-WRITTEN-CNT       PIC 9(09) VALUE ZERO.
           05  DLY-DUPLICATE-CNT       PIC 9(09) VALUE ZERO.
           05  SAVE-HISTORY-RECORD     PIC X(300) VALUE SPACE.
           05  SAVE-HIST-LEVEL         PIC 9(05) VALUE ZERO.
           05  SAVE-HIST-PROMOTE-DATE  PIC X(10) VALUE SPACE.
           05  SAVE-HIST-PROMOTE-TIME  PIC X(08) VALUE SPACE.
           05  SAVE-HIST-EDIT-DATE     PIC X(10) VALUE SPACE.
           05  SAVE-HIST-EDIT-TIME     PIC X(08) VALUE SPACE.
           05  SAVE-HIST-CHANGE-CODE   PIC X(08) VALUE SPACE.
           05  SAVE-CAPTURE-DATE       PIC X(10) VALUE SPACE.
           05  SAVE-CAPTURE-JULDATE    PIC X(08) VALUE SPACE.
           05  SAVE-CAPTURE-TIME       PIC X(08) VALUE SPACE.
           05  SAVE-SRC-STATUS         PIC X(06) VALUE SPACE.
           05  DLY-CHANGE-LEVEL        PIC 9(05) VALUE ZERO.
           05  DISPLAY-CNT             PIC ZZZ,ZZZ,ZZ9.

           05  DEBUG-RPT-LINE-CNT      PIC S9(3) COMP-3 VALUE +80.
           05  DEBUG-PAGE-CNT          PIC S9(7) COMP-3 VALUE ZERO.
           05  CHANGE-HST-STATUS       PIC X(2) VALUE SPACE.
           05  NEW-CHANGE-STATUS       PIC X(2) VALUE SPACE.
           05  RUN-TYPE-STATUS         PIC X(2) VALUE SPACE.
           05  CHANGE-DLY-STATUS       PIC X(2) VALUE SPACE.
           05  END-OF-HIST-CHANGE-SW   PIC X    VALUE SPACE.
               88  END-OF-HIST-CHANGE           VALUE 'Y'.
           05  END-OF-DLY-CHANGE-SW    PIC X    VALUE SPACE.
               88  END-OF-DLY-CHANGE            VALUE 'Y'.

           05  HOLD-PRINT-LINE         PIC X(133) VALUE SPACE.

           05  LIB-COUNT-MAX           PIC 9(09) COMP-3 VALUE 1000.
           05  LIB-SUB                 PIC 9(09) COMP   VALUE ZERO.
           05  WS-COUNT                PIC 9(09) COMP-3 VALUE ZERO.

       01  LIBRARY-COUNT-TABLE.
           05  LIB-COUNTS OCCURS 1000 TIMES.
               10  LCNT-ELEMENT-LIB    PIC X(08).
               10  LCNT-HIST-CNT       PIC 9(09) COMP-3.
               10  LCNT-DAILY-IN-CNT   PIC 9(09) COMP-3.
               10  LCNT-DAILY-OUT-CNT  PIC 9(09) COMP-3.


           EJECT
       01  DEBUG-HEADING-1.
           05  FILLER                  PIC X(01) VALUE SPACES.
           05  DBH1-IO-TYPE            PIC X(04) VALUE SPACES.
           05  FILLER                  PIC X(01) VALUE SPACES.
           05  DBH1-ELEMENT            PIC X(08) VALUE 'ELEMENT'.
           05  FILLER                  PIC X(01) VALUE SPACES.
           05  DBH1-PROMOTE-DATE       PIC X(10) VALUE '  PROMOTE'.
           05  FILLER                  PIC X(01) VALUE SPACES.
           05  DBH1-PROMOTE-TIME       PIC X(08) VALUE 'PROMOTE'.
           05  FILLER                  PIC X(01) VALUE SPACE.
           05  DBH1-EDIT-DATE          PIC X(10) VALUE SPACE.
           05  FILLER                  PIC X(02) VALUE SPACES.
           05  DBH1-OUTPUT-1           PIC X(41) VALUE
               'LIBRARY  LEVEL LANGUAGE  #LINES EDIT BY  '.
           05  DBH1-OUTPUT-1           PIC X(24) VALUE
               '         STATUS         '.

       01  DEBUG-HEADING-2.
           05  FILLER                  PIC X(01) VALUE SPACES.
           05  DBH2-IO-TYPE            PIC X(04) VALUE ' IO'.
           05  FILLER                  PIC X(01) VALUE SPACES.
           05  DBH2-ELEMENT            PIC X(08) VALUE '  NAME'.
           05  FILLER                  PIC X(01) VALUE SPACES.
           05  DBH2-PROMOTE-DATE       PIC X(10) VALUE '   DATE '.
           05  FILLER                  PIC X(01) VALUE SPACES.
           05  DBH2-PROMOTE-TIME       PIC X(08) VALUE '  TIME'.
           05  FILLER                  PIC X(01) VALUE SPACE.
           05  DBH2-EDIT-DATE          PIC X(10) VALUE 'EDIT DATE'.
           05  FILLER                  PIC X(02) VALUE SPACES.
           05  DBH2-OUTPUT             PIC X(65) VALUE SPACES.

       01  DEBUG-DETAIL-LINE-IN.
           05  DEBUG-CC                PIC X  VALUE SPACE.
           05  DEBUG-DATA-IN.
               10  DBI-IO-TYPE         PIC X(04).
               10  FILLER              PIC X(01).
               10  DBI-ELEMENT         PIC X(08).
               10  FILLER              PIC X(01).
               10  DBI-PROMOTE-DATE    PIC X(10).
               10  FILLER              PIC X(01).
               10  DBI-PROMOTE-TIME    PIC X(08).
               10  FILLER              PIC X(01).
               10  DBI-EDIT-DATE       PIC X(10).
               10  FILLER              PIC X(02).
               10  DBI-DATA-REC        PIC X(65).

       01  DISPLAY-TOTAL-LINE.
           05  FILLER                  PIC X(02).
           05  DT-DESC                 PIC X(30).
           05  FILLER                  PIC X(02).
           05  DT-COUNT                PIC ZZZ,ZZZ,ZZ9.
           05  FILLER                  PIC X(05).

       01  DISPLAY-LIBCNT-HEADER-1.
           05  FILLER                  PIC X(02)  VALUE SPACE.
           05  DLHA-ELEMENT-LIB        PIC X(08)  VALUE 'PDS LIB'.
           05  FILLER                  PIC X(01)  VALUE SPACE.
           05  DLHA-HIST-CNT           PIC X(12)  VALUE
               ' HISTORY  IN'.
           05  FILLER                  PIC X(01)  VALUE SPACE.
           05  DLHA-DAILY-IN-CNT       PIC X(12)  VALUE
               '    DAILY IN'.
           05  FILLER                  PIC X(01)  VALUE SPACE.
           05  DLHA-DAILY-OUT-CNT      PIC X(12)  VALUE
               '   DAILY OUT'.
           05  FILLER                  PIC X(01)  VALUE SPACE.
           05  DLHA-TOT-OUT-CNT        PIC X(12)  VALUE
               'TOTAL OUTPUT'.

       01  DISPLAY-LIBCNT-HEADER-2.
           05  FILLER                  PIC X(02)  VALUE SPACE.
           05  DLHB-ELEMENT-LIB        PIC X(08)  VALUE ALL '-'.
           05  FILLER                  PIC X(01)  VALUE SPACE.
           05  DLHB-HIST-CNT           PIC X(12)  VALUE ALL '-'.
           05  FILLER                  PIC X(01)  VALUE SPACE.
           05  DLHB-DAILY-IN-CNT       PIC X(12)  VALUE ALL '-'.
           05  FILLER                  PIC X(01)  VALUE SPACE.
           05  DLHB-DAILY-OUT-CNT      PIC X(12)  VALUE ALL '-'.
           05  FILLER                  PIC X(01)  VALUE SPACE.
           05  DLHB-TOT-OUT-CNT        PIC X(12)  VALUE ALL '-'.

       01  DISPLAY-LIBCNT-LINE.
           05  FILLER                  PIC X(02).
           05  DL-ELEMENT-LIB          PIC X(08).
           05  FILLER                  PIC X(02).
           05  DL-HIST-CNT             PIC ZZZ,ZZZ,ZZ9.
           05  FILLER                  PIC X(02).
           05  DL-DAILY-IN-CNT         PIC ZZZ,ZZZ,ZZ9.
           05  FILLER                  PIC X(02).
           05  DL-DAILY-OUT-CNT        PIC ZZZ,ZZZ,ZZ9.
           05  FILLER                  PIC X(02).
           05  DL-TOT-OUT-CNT          PIC ZZZ,ZZZ,ZZ9.

           EJECT
      *  COMMON REPORT HEADINGS AND FOOTER
      *  DATE-TIME WORK AREAS
      *  COMMON DEBUG WORK AREAS
      *  COMMON ABEND WORK AREA
           COPY JXCPW020.


           EJECT
      *  DATE CONVERSION WORK AREAS
      *    COPY JXCPW025.

           EJECT
      *+ INSTALLATION SPECIFIC CONSTANTS
           COPY JXINW005.
           EJECT
       LINKAGE SECTION.
      *  COMMON PROGRAM RUN PARAMETERS
           COPY JXCPL005.

           EJECT
       PROCEDURE DIVISION USING JXL-PARM-DATA.

           PERFORM A1000-INITIALIZATION THRU A1000-EXIT.

           PERFORM C1000-PROCESS-CHANGE-HIST
              THRU C1000-EXIT
              UNTIL END-OF-HIST-CHANGE AND
                    END-OF-DLY-CHANGE.

           PERFORM A9000-TERMINATION THRU A9000-EXIT.
           GOBACK.

       A1000-INITIALIZATION.

           DISPLAY '**************************************************'
                  '**************************************************'.
           DISPLAY ' '.
           DISPLAY 'PROGRAM JXC310JX - MERGE DAILY CHANGES WITH '
                   'HISTORY FILE'
           MOVE ' DEBUG REPORT - MERGE DAILY CHANGES WITH HISTORY'
                                                 TO JX-DEBUG-TITLE.

           MOVE RULER-LINE(11:65)  TO DBH2-OUTPUT.
           MOVE WS-PROGRAM         TO JXRD-PROGRAM
                                      JXCD-PROGRAM
                                      HD2-PROGRAM.

           INITIALIZE LIBRARY-COUNT-TABLE.
           MOVE 'TOTALS'           TO LCNT-ELEMENT-LIB(1).

           MOVE 'CHANGEWIZ  HIST   REL 1.0 VER 1.0' TO FT1-PRODUCT.
           MOVE '(C) 2005 RENAISSANCE DATA SYSTEMS INC.'
                                                    TO FT1-VENDOR.


      **  DISPLAY COMPILE DATE AND TIME
           COPY JXCPP010.

      **  PROCESS COMMON INPUT PARAMETER
           COPY JXCPP015.

      *+  INSTALLATION SPECIFIC INITIALIZATION ROUTINES
           COPY JXINP005.


           OPEN INPUT CHANGE-HIST-FILE
                       CHANGE-DLY-FILE.
           OPEN OUTPUT NEW-CHANGE-FILE.

           IF JX-PRINT-DEBUG
               OPEN OUTPUT DEBUG-REPORT.

           IF CHANGE-HST-STATUS NOT = '00' AND '97'
               DISPLAY '   BAD OPEN ON CHANGE HIST FILE. STATUS = '
                       CHANGE-HST-STATUS
               MOVE 'Y' TO END-OF-HIST-CHANGE-SW
               PERFORM Z9000-ABEND-PROGRAM THRU Z9000-EXIT
           ELSE
               PERFORM X1000-READ-CHANGE-HIST THRU X1000-EXIT.

           IF END-OF-HIST-CHANGE
               DISPLAY 'NO RECORDS IN CHANGE HISTORY FILE'
           END-IF.

           IF CHANGE-DLY-STATUS NOT = '00' AND '97'
               DISPLAY '   BAD OPEN ON DAILY CHANGE FILE. STATUS = '
                       CHANGE-DLY-STATUS
               PERFORM Z9000-ABEND-PROGRAM THRU Z9000-EXIT
           ELSE
               PERFORM X2000-READ-CHANGE-DLY THRU X2000-EXIT
           END-IF.


      *  SAVE DATES FROM DUMMY RECORD. THESE ARE USED FOR CREATING
      *  DELETE RECORDS

           IF END-OF-DLY-CHANGE
               DISPLAY 'NO RECORDS IN CHANGE DAILY FILE'
           ELSE
              MOVE SH2-CAPTURE-DATE-CYMD  TO SAVE-CAPTURE-DATE
              MOVE SH2-CAPTURE-DATE-JUL   TO SAVE-CAPTURE-JULDATE
              MOVE SH2-CAPTURE-TIME       TO SAVE-CAPTURE-TIME
              IF SH2-ELEMENT-NAME = SPACE
                 ADD 1 TO DLY-DUMMY-CNT
                 PERFORM X2000-READ-CHANGE-DLY THRU X2000-EXIT
              END-IF
           END-IF.

           IF NEW-CHANGE-STATUS NOT = '00' AND '97'
               DISPLAY '   BAD OPEN ON CHANGE HIST OUT FILE. STATUS = '
                       NEW-CHANGE-STATUS
               PERFORM Z9000-ABEND-PROGRAM THRU Z9000-EXIT
           END-IF.

      *+  INSTALLATION-SPECIFIC CODE TO DETERMINE DATE CUTOFF
           COPY JXINP010.

           PERFORM A2000-PROCESS-RUN-TYPE THRU A2000-EXIT.

       A1000-EXIT.
           EXIT.

      ***********************************************************
      *   READ AND EDIT THE RUN-TYPE CARD
      *   VALID VALUES ARE 'SCLM' AND 'PDS'
      ***********************************************************
       A2000-PROCESS-RUN-TYPE.

           OPEN INPUT RUN-TYPE.
           IF RUN-TYPE-STATUS NOT = '00' AND '97'
               DISPLAY '   BAD OPEN ON RUN-TYPE FILE. STATUS = '
                       RUN-TYPE-STATUS
               PERFORM Z9000-ABEND-PROGRAM THRU Z9000-EXIT
           END-IF.

           READ RUN-TYPE INTO WS-RUN-TYPE.
           IF RUN-TYPE-STATUS = '10'
              DISPLAY 'NO RECORD IN RUN-TYPE FILE.  CHANGE THE '
                      'JCL AND RESUBMIT'
              DISPLAY 'VALID VALUES ARE "SCLM" OR "PDS" '
                      'STARTING IN POSITION 1'
              PERFORM Z9000-ABEND-PROGRAM THRU Z9000-EXIT
           END-IF.

           IF RUN-TYPE-STATUS NOT = '00'
               DISPLAY '  BAD READ ON RUN-TYPE FILE. '
                   ' STATUS = '
                   RUN-TYPE-STATUS
               PERFORM Z9000-ABEND-PROGRAM THRU Z9000-EXIT
           END-IF.

           DISPLAY 'CARD INPUT IS: ' WS-RUN-TYPE-LIB
           IF NOT WS-VALID-RUN-TYPE
              DISPLAY 'RUN-TYPE CARD NOT VALID. '
              DISPLAY 'VALID VALUES ARE "SCLM" OR "PDS" '
                      'STARTING IN POSITION 1'
           END-IF.

           CLOSE RUN-TYPE.

       A2000-EXIT.
           EXIT.

      ***********************************************************
      *   END OF JOB PROCESSING
      ***********************************************************
       A9000-TERMINATION.

           CLOSE CHANGE-HIST-FILE
                 CHANGE-DLY-FILE
                 NEW-CHANGE-FILE.

      *  ENABLE TOTALS TO PRINT ON DEBUG REPORT
           IF JX-PRINT-DEBUG OR DEBUG-PAGE-CNT > 0
              ADD 40 TO JX-PRINT-DEBUG-MAX
              IF NOT JX-PRINT-DEBUG
                 MOVE 'Y' TO JX-DEBUG-SW
              END-IF
              ADD 40 TO DEBUG-RPT-LINE-CNT
              IF DEBUG-RPT-LINE-CNT > INST-LINES-PER-PAGE
                 SUBTRACT 40 FROM DEBUG-RPT-LINE-CNT
                 PERFORM Y1025-DEBUG-HEADING THRU Y1025-EXIT
              ELSE
                 SUBTRACT 40 FROM DEBUG-RPT-LINE-CNT
                 MOVE SPACES        TO DEBUG-RPT-LINE
                 PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
                 PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
              END-IF
           END-IF.

           DISPLAY ' '.
           DISPLAY ' '.
           DISPLAY '****************************************'
                   '****************************************'
           DISPLAY ' '.
           DISPLAY 'END OF PROGRAM JXC310JX.'.
           DISPLAY ' '.


           IF JX-PRINT-DEBUG
               MOVE ALL '*'       TO DEBUG-RPT-LINE(2:125)
               PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
               MOVE SPACES        TO DEBUG-RPT-LINE
               PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
               MOVE ' END-OF-PROGRAM JXC310JX RECORD COUNTS' TO
                                     DEBUG-RPT-LINE
               PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
               MOVE SPACES        TO DEBUG-RPT-LINE
               PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
           END-IF.

           MOVE SPACE TO DISPLAY-TOTAL-LINE.

           MOVE 'CHANGE HISTORY RECORDS READ' TO DT-DESC
           MOVE CHANGE-HIST-CNT               TO DT-COUNT.
           DISPLAY DISPLAY-TOTAL-LINE
           IF JX-PRINT-DEBUG
              MOVE DISPLAY-TOTAL-LINE TO DEBUG-RPT-LINE
              PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
           END-IF.

           MOVE 'CHANGE DAILY   RECORDS READ' TO DT-DESC
           MOVE CHANGE-DLY-CNT                TO DT-COUNT.
           DISPLAY DISPLAY-TOTAL-LINE
           DISPLAY '  '
           IF JX-PRINT-DEBUG
              MOVE DISPLAY-TOTAL-LINE TO DEBUG-RPT-LINE
              PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
              MOVE SPACE              TO DEBUG-RPT-LINE
              PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
           END-IF.

           MOVE 'CHANGE RECORDS WRITTEN        ' TO DT-DESC
           MOVE TOTAL-WRITTEN-CNT      TO DT-COUNT.
           DISPLAY DISPLAY-TOTAL-LINE
           IF JX-PRINT-DEBUG
              MOVE DISPLAY-TOTAL-LINE TO DEBUG-RPT-LINE
              PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
           END-IF.

           MOVE '    FROM HISTORY           ' TO DT-DESC
           MOVE HIST-WRITTEN-CNT              TO DT-COUNT.
           DISPLAY DISPLAY-TOTAL-LINE
           IF JX-PRINT-DEBUG
              MOVE DISPLAY-TOTAL-LINE TO DEBUG-RPT-LINE
              PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
           END-IF.

           MOVE '    FROM DAILY             ' TO DT-DESC
           MOVE DLY-WRITTEN-CNT               TO DT-COUNT.
           DISPLAY DISPLAY-TOTAL-LINE
           IF JX-PRINT-DEBUG
              MOVE DISPLAY-TOTAL-LINE TO DEBUG-RPT-LINE
              PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
           END-IF.

           MOVE '    DUPLICATE IN DAILY     ' TO DT-DESC
           MOVE DLY-DUPLICATE-CNT             TO DT-COUNT.
           DISPLAY DISPLAY-TOTAL-LINE
           IF JX-PRINT-DEBUG
              MOVE DISPLAY-TOTAL-LINE TO DEBUG-RPT-LINE
              PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
           END-IF.


           MOVE '    DUMMY DAILY RECS   '     TO DT-DESC
           MOVE DLY-DUMMY-CNT                 TO DT-COUNT.
           DISPLAY DISPLAY-TOTAL-LINE
           DISPLAY '   '
           DISPLAY '   '
           IF JX-PRINT-DEBUG
              MOVE DISPLAY-TOTAL-LINE TO DEBUG-RPT-LINE
              PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
              MOVE SPACE              TO DEBUG-RPT-LINE
              PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
              PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
           END-IF.

      *  PRINT HEADER FOR LIBRARY COUNTS
           DISPLAY DISPLAY-LIBCNT-HEADER-1.
           DISPLAY DISPLAY-LIBCNT-HEADER-2.
           IF JX-PRINT-DEBUG
              MOVE DISPLAY-LIBCNT-HEADER-1 TO DEBUG-RPT-LINE
              PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
              MOVE DISPLAY-LIBCNT-HEADER-2 TO DEBUG-RPT-LINE
              PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
           END-IF.

      *  PRINT COUNTS FOR EACH LIBRARY
      *  THE FIRST OCCURRENCE IS FOR THE TOTAL LINE

           PERFORM VARYING LIB-SUB FROM 2 BY 1
                   UNTIL (LIB-SUB > LIB-COUNT-MAX) OR
                         (LCNT-ELEMENT-LIB(LIB-SUB) = SPACE)
              MOVE SPACE TO DISPLAY-LIBCNT-LINE
              MOVE LCNT-ELEMENT-LIB(LIB-SUB)   TO DL-ELEMENT-LIB
              MOVE LCNT-HIST-CNT(LIB-SUB)      TO DL-HIST-CNT
              MOVE LCNT-DAILY-IN-CNT(LIB-SUB)  TO DL-DAILY-IN-CNT
              MOVE LCNT-DAILY-OUT-CNT(LIB-SUB) TO DL-DAILY-OUT-CNT
              COMPUTE WS-COUNT = LCNT-HIST-CNT(LIB-SUB) +
                                 LCNT-DAILY-OUT-CNT(LIB-SUB)
              MOVE WS-COUNT                    TO DL-TOT-OUT-CNT
              DISPLAY DISPLAY-LIBCNT-LINE
              IF JX-PRINT-DEBUG
                 MOVE DISPLAY-LIBCNT-LINE TO DEBUG-RPT-LINE
                 PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
              END-IF
              ADD LCNT-HIST-CNT(LIB-SUB)      TO LCNT-HIST-CNT(1)
              ADD LCNT-DAILY-IN-CNT(LIB-SUB)  TO LCNT-DAILY-IN-CNT(1)
              ADD LCNT-DAILY-OUT-CNT(LIB-SUB) TO LCNT-DAILY-OUT-CNT(1)
           END-PERFORM.

           DISPLAY DISPLAY-LIBCNT-HEADER-2.
           IF JX-PRINT-DEBUG
              MOVE DISPLAY-LIBCNT-HEADER-2 TO DEBUG-RPT-LINE
              PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
           END-IF.

      *  PRINT TOTALS LINE
           MOVE 1 TO LIB-SUB.
           MOVE SPACE TO DISPLAY-LIBCNT-LINE
           MOVE LCNT-ELEMENT-LIB(LIB-SUB)   TO DL-ELEMENT-LIB
           MOVE LCNT-HIST-CNT(LIB-SUB)      TO DL-HIST-CNT
           MOVE LCNT-DAILY-IN-CNT(LIB-SUB)  TO DL-DAILY-IN-CNT
           MOVE LCNT-DAILY-OUT-CNT(LIB-SUB) TO DL-DAILY-OUT-CNT
           COMPUTE WS-COUNT = LCNT-HIST-CNT(LIB-SUB) +
                              LCNT-DAILY-OUT-CNT(LIB-SUB)
           MOVE WS-COUNT                    TO DL-TOT-OUT-CNT
           DISPLAY DISPLAY-LIBCNT-LINE
           IF JX-PRINT-DEBUG
              MOVE DISPLAY-LIBCNT-LINE TO DEBUG-RPT-LINE
              PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
           END-IF.

           DISPLAY ' '.
           DISPLAY '****************************************'
                   '****************************************'
           DISPLAY ' '.

           IF JX-PRINT-DEBUG
               MOVE SPACES        TO DEBUG-RPT-LINE
               PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
               MOVE ALL '*'       TO DEBUG-RPT-LINE(2:125)
               PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
               MOVE SPACES        TO DEBUG-RPT-LINE
               PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
           END-IF.


      **  PRINT LAST CHANGE FILE DATA
           IF DEBUG-PAGE-CNT > 0
               PERFORM Y1050-DEBUG-FOOTER THRU Y1050-EXIT.

           IF JX-PRINT-DEBUG OR DEBUG-PAGE-CNT > 0
               CLOSE DEBUG-REPORT.


       A9000-EXIT.
           EXIT.
           EJECT
      **************************************************************
      *  1. WRITE OUT ALL HISTORY AND DAILY CHANGE RECORDS THAT DO
      *     NOT MATCH ON ELEMENT NAME AND LIBRARY
      *  2. IF THEY DO MATCH, CHECK TO SEE IF DAILY CHANGE IS NEW
      **************************************************************
       C1000-PROCESS-CHANGE-HIST.

           IF END-OF-DLY-CHANGE
              IF JX-PRINT-DEBUG
                 MOVE SPACES                TO DEBUG-DATA-IN
                 MOVE 'END OF DAILY FILE, WRITING ALL HISTORY'
                                                  TO DBI-DATA-REC
                 MOVE DEBUG-DETAIL-LINE-IN  TO DEBUG-RPT-LINE
                 PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
              END-IF
              PERFORM UNTIL END-OF-HIST-CHANGE
                 PERFORM D1000-WRITE-HISTORY    THRU D1000-EXIT
                 PERFORM X1000-READ-CHANGE-HIST THRU X1000-EXIT
              END-PERFORM
              GO TO C1000-EXIT
           END-IF.

           IF END-OF-HIST-CHANGE
              IF JX-PRINT-DEBUG
                 MOVE SPACES                TO DEBUG-DATA-IN
                 MOVE 'END OF HISTORY FILE, WRITING ALL DAILY'
                                                  TO DBI-DATA-REC
                 MOVE DEBUG-DETAIL-LINE-IN  TO DEBUG-RPT-LINE
                 PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
                 MOVE SPACES                 TO DEBUG-DATA-IN
                 MOVE 'IHST'                 TO DBI-IO-TYPE
                 MOVE SAVE-HISTORY-RECORD(1:8) TO DBI-ELEMENT
                 MOVE SAVE-HIST-PROMOTE-TIME TO DBI-PROMOTE-TIME
                 MOVE SAVE-HIST-PROMOTE-DATE TO DBI-PROMOTE-DATE
                 MOVE SAVE-HIST-EDIT-DATE    TO DBI-EDIT-DATE
                 MOVE SAVE-HISTORY-RECORD(10:65) TO DBI-DATA-REC
                 MOVE DEBUG-DETAIL-LINE-IN   TO DEBUG-RPT-LINE
                 PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
              END-IF
              PERFORM UNTIL END-OF-DLY-CHANGE
                 MOVE DLY-CHANGE-LEVEL TO SH2-ELEMENT-LEVEL
                 PERFORM D2000-WRITE-DAILY     THRU D2000-EXIT
                 PERFORM X2000-READ-CHANGE-DLY THRU X2000-EXIT
              END-PERFORM
              GO TO C1000-EXIT
           END-IF.

      *  READ AND WRITE ALL HISTORY FOR AN ELEMENT
      *  SAVE THE HIGHEST LEVEL NUMBER
           MOVE SH-ELEMENT-NAME     TO WS-CURR-ELEMENT
           MOVE SH-ELEMENT-LIB      TO WS-CURR-LIBRARY

           PERFORM UNTIL END-OF-HIST-CHANGE OR
                         (WS-CURR-ELEMENT NOT = SH-ELEMENT-NAME) OR
                         (WS-CURR-ELEMENT = SH-ELEMENT-NAME AND
                         (WS-CURR-LIBRARY NOT = SH-ELEMENT-LIB))
              MOVE SH-ELEMENT-LEVEL      TO SAVE-HIST-LEVEL
              MOVE SH-PROMOTE-DATE-CYMD  TO SAVE-HIST-PROMOTE-DATE
              MOVE SH-PROMOTE-TIME       TO SAVE-HIST-PROMOTE-TIME
              MOVE SH-EDIT-DATE-CYMD     TO SAVE-HIST-EDIT-DATE
              MOVE SH-EDIT-TIME          TO SAVE-HIST-EDIT-TIME
              MOVE SH-CHANGE-CODE        TO SAVE-HIST-CHANGE-CODE
              MOVE SH-PAN-STATUS         TO SAVE-SRC-STATUS
              MOVE SOURCE-HISTORY-RECORD TO SAVE-HISTORY-RECORD
              PERFORM D1000-WRITE-HISTORY    THRU D1000-EXIT
              PERFORM X1000-READ-CHANGE-HIST THRU X1000-EXIT
           END-PERFORM.

      *  IF DAILY KEY IS LESS, THIS IS A NEW ELEMENT

           IF WS-CURR-KEY > WS-DLY-KEY
              IF JX-PRINT-DEBUG
                 MOVE SPACES                TO DEBUG-DATA-IN
                 MOVE 'NO MATCH, NEW ELEMENT IN DAILY  '
                                                  TO DBI-DATA-REC
                 MOVE DEBUG-DETAIL-LINE-IN  TO DEBUG-RPT-LINE
                 PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
                 MOVE SPACES                 TO DEBUG-DATA-IN
                 MOVE 'IHST'                 TO DBI-IO-TYPE
                 MOVE WS-CURR-KEY            TO DBI-ELEMENT
                 MOVE SAVE-HIST-PROMOTE-TIME TO DBI-PROMOTE-TIME
                 MOVE SAVE-HIST-PROMOTE-DATE TO DBI-PROMOTE-DATE
                 MOVE SAVE-HIST-EDIT-DATE    TO DBI-EDIT-DATE
                 MOVE SAVE-HISTORY-RECORD(10:65) TO DBI-DATA-REC
                 MOVE DEBUG-DETAIL-LINE-IN   TO DEBUG-RPT-LINE
                 PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
              END-IF
              PERFORM UNTIL END-OF-DLY-CHANGE OR
                            (WS-CURR-KEY NOT > WS-DLY-KEY)
                 MOVE DLY-CHANGE-LEVEL TO SH2-ELEMENT-LEVEL
                 PERFORM D2000-WRITE-DAILY     THRU D2000-EXIT
                 PERFORM X2000-READ-CHANGE-DLY THRU X2000-EXIT
              END-PERFORM
           END-IF.

      *  IF DAILY KEY IS GREATER, PROCESS THE NEXT HISTORY ELEMENT NAME

           IF WS-CURR-KEY < WS-DLY-KEY
              IF NOT END-OF-HIST-CHANGE
                 MOVE SH-ELEMENT-LEVEL      TO SAVE-HIST-LEVEL
                 MOVE SH-PROMOTE-DATE-CYMD  TO SAVE-HIST-PROMOTE-DATE
                 MOVE SH-PROMOTE-TIME       TO SAVE-HIST-PROMOTE-TIME
                 MOVE SH-EDIT-DATE-CYMD     TO SAVE-HIST-EDIT-DATE
                 MOVE SH-EDIT-TIME          TO SAVE-HIST-EDIT-TIME
                 MOVE SH-CHANGE-CODE        TO SAVE-HIST-CHANGE-CODE
                 MOVE SH-PAN-STATUS         TO SAVE-SRC-STATUS
                 MOVE SOURCE-HISTORY-RECORD TO SAVE-HISTORY-RECORD
              END-IF
              GO TO C1000-EXIT
           END-IF.

           IF WS-CURR-KEY NOT = WS-DLY-KEY
              DISPLAY 'LOGIC ERROR C1000-   CURR-KEY = ' WS-CURR-KEY
                      '  DLY-KEY = '  WS-DLY-KEY
              GO TO C1000-EXIT
           END-IF.

      * THE KEYS MATCH - SEE IF DAILY CHANGE IS NEW
      * THE DAILY CHANGES CAN BE PULLED MULTIPLE TIMES DEPENDING ON
      * THE PULL DATE RANGE.
      * ONLY ONE RECORD IS PULLED DAILY FOR EACH ELEMENT/LIBRARY

           IF JX-PRINT-DEBUG
              MOVE SPACES                TO DEBUG-DATA-IN
              MOVE 'KEYS MATCH - CHECK DATE/TIME '
                                               TO DBI-DATA-REC
              MOVE DEBUG-DETAIL-LINE-IN  TO DEBUG-RPT-LINE
              PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
           END-IF.

           PERFORM D3000-PROCESS-MATCH   THRU D3000-EXIT.

           PERFORM X2000-READ-CHANGE-DLY  THRU X2000-EXIT.

       C1000-EXIT.
           EXIT.
           EJECT
      ***************************************************************
      *  WRITE HISTORY TO NEW HISTORY FILE
      ***************************************************************
       D1000-WRITE-HISTORY.
      *    DISPLAY 'D1000-WRITE-HISTORY      '.

           WRITE NEW-CHANGE-RECORD FROM SOURCE-HISTORY-RECORD.
           ADD 1 TO HIST-WRITTEN-CNT
                    TOTAL-WRITTEN-CNT.


       D1000-EXIT.
           EXIT.

      ***************************************************************
      *  WRITE DAILY CHANGE TO NEW HISTORY FILE
      ***************************************************************
       D2000-WRITE-DAILY.
      *    DISPLAY 'D2000-WRITE-DAILY        '.

           IF JX-PRINT-DEBUG
               MOVE SPACES                  TO DEBUG-DATA-IN
               MOVE 'ODLY'                  TO DBI-IO-TYPE
               MOVE SH2-ELEMENT-NAME        TO DBI-ELEMENT
               MOVE SH2-PROMOTE-TIME        TO DBI-PROMOTE-TIME
               MOVE SH2-PROMOTE-DATE-CYMD   TO DBI-PROMOTE-DATE
               MOVE SH2-EDIT-DATE-CYMD      TO DBI-EDIT-DATE
               MOVE SOURCE-HISTORY-RECORD-2(10:65) TO DBI-DATA-REC
               MOVE DEBUG-DETAIL-LINE-IN    TO DEBUG-RPT-LINE
               PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
           END-IF.

           WRITE NEW-CHANGE-RECORD FROM SOURCE-HISTORY-RECORD-2.
           ADD 1 TO DLY-WRITTEN-CNT
                    TOTAL-WRITTEN-CNT.

           IF (LIB-SUB > LIB-COUNT-MAX) OR
              (LIB-SUB = ZERO)
              MOVE 1 TO LIB-SUB
           END-IF.

           IF SH2-ELEMENT-LIB NOT = LCNT-ELEMENT-LIB(LIB-SUB)
              PERFORM VARYING LIB-SUB FROM 1 BY 1
                      UNTIL (LIB-SUB > LIB-COUNT-MAX) OR
                            (LCNT-ELEMENT-LIB(LIB-SUB) = SPACE) OR
                            (LCNT-ELEMENT-LIB(LIB-SUB) =
                                                  SH2-ELEMENT-LIB)
              END-PERFORM
           END-IF.

           IF LIB-SUB > LIB-COUNT-MAX
              DISPLAY ' MORE THAN ' LIB-COUNT-MAX
                      ' LIBRARY NAMES IN CHANGE HISTORY FILE.'
                      '  INCREASE SIZE OF LIBRARY-COUNT-TABLE.'
              MOVE LIB-COUNT-MAX TO LIB-SUB
              GO TO D2000-EXIT
           ELSE
           IF LCNT-ELEMENT-LIB(LIB-SUB) = SPACE
              MOVE SH2-ELEMENT-LIB   TO LCNT-ELEMENT-LIB(LIB-SUB)
              MOVE 1                 TO LCNT-DAILY-OUT-CNT(LIB-SUB)
           ELSE
              ADD  1                 TO LCNT-DAILY-OUT-CNT(LIB-SUB)
           END-IF.

       D2000-EXIT.
           EXIT.

      ***************************************************************
      *  THE KEYS MATCH - SEE IF DAILY CHANGE IS A NEW CHANGE
      *  ONLY THE LATEST CHANGE IS PULLED IN THE DAILY FILE
      *  FOR EACH ELEMENT.
      ***************************************************************
       D3000-PROCESS-MATCH.

      ***************************************************************
      *   DAILY IS A COMPLETE DUPLICATE OF THE HISTORY RECORD
      *   SCLM MATCHES ON PROMOTE-DATE/TIME
      ***************************************************************
           IF WS-SCLM-RUN-TYPE
           IF SH2-PROMOTE-DATE-CYMD   = SAVE-HIST-PROMOTE-DATE AND
              SH2-PROMOTE-TIME        = SAVE-HIST-PROMOTE-TIME
              ADD 1 TO DLY-DUPLICATE-CNT
              IF JX-PRINT-DEBUG
                 MOVE SPACES                TO DEBUG-DATA-IN
                 MOVE 'SCLM DLY IS DUPLICATE ' TO DBI-DATA-REC
                 MOVE DEBUG-DETAIL-LINE-IN  TO DEBUG-RPT-LINE
                 PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
                 MOVE SPACES                 TO DEBUG-DATA-IN
                 MOVE 'IHST'                 TO DBI-IO-TYPE
                 MOVE WS-CURR-ELEMENT        TO DBI-ELEMENT
                 MOVE SAVE-HIST-PROMOTE-TIME TO DBI-PROMOTE-TIME
                 MOVE SAVE-HIST-PROMOTE-DATE TO DBI-PROMOTE-DATE
                 MOVE SAVE-HIST-EDIT-DATE    TO DBI-EDIT-DATE
                 MOVE SAVE-HISTORY-RECORD(10:65) TO DBI-DATA-REC
                 MOVE DEBUG-DETAIL-LINE-IN   TO DEBUG-RPT-LINE
                 PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
              END-IF
              GO TO D3000-EXIT
           END-IF
           END-IF.

      ***************************************************************
      *   DAILY IS A COMPLETE DUPLICATE OF THE HISTORY RECORD
      *   PDS  MATCHES ON EDIT-DATE
      *   ALSO CHECK STATUS. IF HISTORY WAS DELETED AND THEN
      *   RESTORED, WRITE THE DAILY OUT.
      ***************************************************************
           IF WS-PDS-RUN-TYPE
           IF SH2-EDIT-DATE-CYMD      = SAVE-HIST-EDIT-DATE AND
              SH2-EDIT-TIME           = SAVE-HIST-EDIT-TIME AND
              SH2-PAN-STATUS          = SAVE-SRC-STATUS
              ADD 1 TO DLY-DUPLICATE-CNT
              IF JX-PRINT-DEBUG
                 MOVE SPACES                TO DEBUG-DATA-IN
                 MOVE 'PDS DLY IS DUPLICATE ' TO DBI-DATA-REC
                 MOVE DEBUG-DETAIL-LINE-IN  TO DEBUG-RPT-LINE
                 PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
                 MOVE SPACES                 TO DEBUG-DATA-IN
                 MOVE 'IHST'                 TO DBI-IO-TYPE
                 MOVE WS-CURR-ELEMENT        TO DBI-ELEMENT
                 MOVE SAVE-HIST-PROMOTE-TIME TO DBI-PROMOTE-TIME
                 MOVE SAVE-HIST-PROMOTE-DATE TO DBI-PROMOTE-DATE
                 MOVE SAVE-HIST-EDIT-DATE    TO DBI-EDIT-DATE
                 MOVE SAVE-HISTORY-RECORD(10:65)  TO DBI-DATA-REC
                 MOVE DEBUG-DETAIL-LINE-IN   TO DEBUG-RPT-LINE
                 PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
              END-IF
              GO TO D3000-EXIT
           END-IF
           END-IF.


      * NOT A COMPLETE MATCH, WRITE DAILY
           COMPUTE SH2-ELEMENT-LEVEL = SAVE-HIST-LEVEL +
                                       DLY-CHANGE-LEVEL

           IF JX-PRINT-DEBUG
               MOVE SPACES                 TO DEBUG-DATA-IN
               MOVE 'IHST'                 TO DBI-IO-TYPE
               MOVE WS-CURR-ELEMENT        TO DBI-ELEMENT
               MOVE SAVE-HIST-PROMOTE-TIME TO DBI-PROMOTE-TIME
               MOVE SAVE-HIST-PROMOTE-DATE TO DBI-PROMOTE-DATE
               MOVE SAVE-HIST-EDIT-DATE    TO DBI-EDIT-DATE
               MOVE SAVE-HISTORY-RECORD(10:65)  TO DBI-DATA-REC
               MOVE DEBUG-DETAIL-LINE-IN   TO DEBUG-RPT-LINE
               PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
           END-IF.

           PERFORM D2000-WRITE-DAILY     THRU D2000-EXIT.

       D3000-EXIT.
           EXIT.


           EJECT
      ***************************************************************
      *  READ CHANGE HISTORY FILE
      ***************************************************************
       X1000-READ-CHANGE-HIST.
           READ CHANGE-HIST-FILE INTO SOURCE-HISTORY-RECORD.

           IF CHANGE-HST-STATUS = '10'
               MOVE 'Y' TO END-OF-HIST-CHANGE-SW
               MOVE HIGH-VALUES TO SH-ELEMENT-NAME
               GO TO X1000-EXIT.

           ADD 1           TO CHANGE-HIST-CNT.
           IF CHANGE-HST-STATUS NOT = '00'
               DISPLAY '  BAD READ ON CHANGE HISTORY FILE. '
                   ' STATUS = '
                   CHANGE-HST-STATUS ' ON RECORD NBR '
                   CHANGE-HIST-CNT
               GO TO X1000-EXIT.

           IF JX-PRINT-DEBUG AND CHANGE-HIST-CNT = 1
               MOVE SPACES                 TO DEBUG-DATA-IN
               MOVE 'IHST'                 TO DBI-IO-TYPE
               MOVE SH-ELEMENT-NAME        TO DBI-ELEMENT
               MOVE SH-PROMOTE-TIME        TO DBI-PROMOTE-TIME
               MOVE SH-PROMOTE-DATE-CYMD   TO DBI-PROMOTE-DATE
               MOVE SH-EDIT-DATE-CYMD      TO DBI-EDIT-DATE
               MOVE SOURCE-HISTORY-RECORD(10:65) TO DBI-DATA-REC
               MOVE DEBUG-DETAIL-LINE-IN   TO DEBUG-RPT-LINE
               PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
           END-IF.

           IF (LIB-SUB > LIB-COUNT-MAX) OR
              (LIB-SUB = ZERO)
              MOVE 1 TO LIB-SUB
           END-IF.

           IF SH-ELEMENT-LIB NOT = LCNT-ELEMENT-LIB(LIB-SUB)
              PERFORM VARYING LIB-SUB FROM 1 BY 1
                      UNTIL (LIB-SUB > LIB-COUNT-MAX) OR
                            (LCNT-ELEMENT-LIB(LIB-SUB) = SPACE) OR
                            (LCNT-ELEMENT-LIB(LIB-SUB) =
                                                  SH-ELEMENT-LIB)
              END-PERFORM
           END-IF.

           IF LIB-SUB > LIB-COUNT-MAX
              DISPLAY ' MORE THAN ' LIB-COUNT-MAX
                      ' LIBRARY NAMES IN CHANGE HISTORY FILE.'
                      '  INCREASE SIZE OF LIBRARY-COUNT-TABLE.'
              MOVE LIB-COUNT-MAX TO LIB-SUB
              GO TO X1000-EXIT
           ELSE
           IF LCNT-ELEMENT-LIB(LIB-SUB) = SPACE
              MOVE SH-ELEMENT-LIB    TO LCNT-ELEMENT-LIB(LIB-SUB)
              MOVE 1                 TO LCNT-HIST-CNT(LIB-SUB)
           ELSE
              ADD  1                 TO LCNT-HIST-CNT(LIB-SUB)
           END-IF.

       X1000-EXIT.
           EXIT.

      ***************************************************************
      *  READ CHANGE DAILY FILE
      ***************************************************************
       X2000-READ-CHANGE-DLY.
           READ CHANGE-DLY-FILE INTO SOURCE-HISTORY-RECORD-2.

           IF CHANGE-DLY-STATUS = '10'
               MOVE 'Y' TO END-OF-DLY-CHANGE-SW
               MOVE HIGH-VALUES TO SH2-ELEMENT-NAME
               GO TO X2000-EXIT.

           ADD 1           TO CHANGE-DLY-CNT.
           IF CHANGE-DLY-STATUS NOT = '00'
               DISPLAY '  BAD READ ON CHANGE DAILY FILE. '
                   ' STATUS = '
                   CHANGE-DLY-STATUS ' ON RECORD NBR '
                   CHANGE-DLY-CNT
               GO TO X2000-EXIT.

           IF SH2-ELEMENT-NAME = WS-DLY-ELEMENT AND
              SH2-ELEMENT-LIB  = WS-DLY-LIBRARY
              ADD 1                   TO DLY-CHANGE-LEVEL
           ELSE
              MOVE 1                  TO DLY-CHANGE-LEVEL
              MOVE SH2-ELEMENT-NAME   TO WS-DLY-ELEMENT
              MOVE SH2-ELEMENT-LIB    TO WS-DLY-LIBRARY
           END-IF.

           IF JX-PRINT-DEBUG
               MOVE ALL '-'                 TO DEBUG-DATA-IN
               MOVE DEBUG-DETAIL-LINE-IN    TO DEBUG-RPT-LINE
               PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
               MOVE SPACES                  TO DEBUG-DATA-IN
               MOVE 'IDLY'                  TO DBI-IO-TYPE
               MOVE SH2-ELEMENT-NAME        TO DBI-ELEMENT
               MOVE SH2-PROMOTE-TIME        TO DBI-PROMOTE-TIME
               MOVE SH2-PROMOTE-DATE-CYMD   TO DBI-PROMOTE-DATE
               MOVE SH2-EDIT-DATE-CYMD      TO DBI-EDIT-DATE
               MOVE SOURCE-HISTORY-RECORD-2(10:65) TO DBI-DATA-REC
               MOVE DEBUG-DETAIL-LINE-IN    TO DEBUG-RPT-LINE
               PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
           END-IF.

           IF (LIB-SUB > LIB-COUNT-MAX) OR
              (LIB-SUB = ZERO)
              MOVE 1 TO LIB-SUB
           END-IF.

           IF SH2-ELEMENT-LIB NOT = LCNT-ELEMENT-LIB(LIB-SUB)
              PERFORM VARYING LIB-SUB FROM 1 BY 1
                      UNTIL (LIB-SUB > LIB-COUNT-MAX) OR
                            (LCNT-ELEMENT-LIB(LIB-SUB) = SPACE) OR
                            (LCNT-ELEMENT-LIB(LIB-SUB) =
                                                  SH2-ELEMENT-LIB)
              END-PERFORM
           END-IF.

           IF LIB-SUB > LIB-COUNT-MAX
              DISPLAY ' MORE THAN ' LIB-COUNT-MAX
                      ' LIBRARY NAMES IN CHANGE HISTORY FILE.'
                      '  INCREASE SIZE OF LIBRARY-COUNT-TABLE.'
              MOVE LIB-COUNT-MAX TO LIB-SUB
              GO TO X2000-EXIT
           ELSE
           IF LCNT-ELEMENT-LIB(LIB-SUB) = SPACE
              MOVE SH2-ELEMENT-LIB   TO LCNT-ELEMENT-LIB(LIB-SUB)
              MOVE 1                 TO LCNT-DAILY-IN-CNT(LIB-SUB)
           ELSE
              ADD  1                 TO LCNT-DAILY-IN-CNT(LIB-SUB)
           END-IF.


       X2000-EXIT.
           EXIT.


           EJECT
      ***************************************************************
      ***  PRINT ROUTINES
      ***************************************************************

       Y1000-PRINT-DEBUG.
           ADD 1 TO JX-PRINT-DEBUG-CNT.
           IF JX-PRINT-DEBUG-CNT > JX-PRINT-DEBUG-SKIP
               IF DEBUG-RPT-LINE-CNT > INST-LINES-PER-PAGE
                   MOVE DEBUG-RPT-LINE TO HOLD-PRINT-LINE
                   PERFORM Y1025-DEBUG-HEADING THRU Y1025-EXIT
                   MOVE HOLD-PRINT-LINE TO DEBUG-RPT-LINE.

           IF JX-PRINT-DEBUG-CNT > JX-PRINT-DEBUG-SKIP
               WRITE DEBUG-RPT-LINE
                   AFTER ADVANCING 1 LINE
               ADD 1 TO DEBUG-RPT-LINE-CNT.

           IF JX-PRINT-DEBUG-CNT > JX-PRINT-DEBUG-MAX
               MOVE 'N' TO JX-DEBUG-SW.
           MOVE SPACES TO DEBUG-RPT-LINE.
       Y1000-EXIT.
           EXIT.

       Y1025-DEBUG-HEADING.
           ADD 1                   TO DEBUG-PAGE-CNT.
           MOVE DEBUG-PAGE-CNT     TO HD1-PAGE.
           MOVE JX-DEBUG-TITLE     TO HD2-TITLE.
           MOVE 'DB'               TO HD2-REPORT.
           MOVE '            CHANGEWIZ DEBUGGING REPORT           '
                                   TO HD1-COMPANY-NAME


           IF DEBUG-PAGE-CNT > 1
               PERFORM Y1050-DEBUG-FOOTER THRU Y1050-EXIT.

           WRITE DEBUG-RPT-LINE FROM HEADING-1
               AFTER ADVANCING PAGE.
           WRITE DEBUG-RPT-LINE FROM HEADING-2
               AFTER ADVANCING 1 LINE.
           WRITE DEBUG-RPT-LINE FROM DEBUG-HEADING-1
               AFTER ADVANCING 2 LINE.
           WRITE DEBUG-RPT-LINE FROM DEBUG-HEADING-2
               AFTER ADVANCING 1 LINE.
           MOVE SPACES TO DEBUG-RPT-LINE
           WRITE DEBUG-RPT-LINE
               AFTER ADVANCING 1 LINE.
           MOVE 6 TO DEBUG-RPT-LINE-CNT.
       Y1025-EXIT.
           EXIT.

       Y1050-DEBUG-FOOTER.
           MOVE SPACES TO DEBUG-RPT-LINE.

           PERFORM UNTIL DEBUG-RPT-LINE-CNT > INST-LINES-PER-PAGE
               WRITE DEBUG-RPT-LINE
                   AFTER ADVANCING 1 LINE
               ADD 1 TO DEBUG-RPT-LINE-CNT
           END-PERFORM.

           WRITE DEBUG-RPT-LINE
               AFTER ADVANCING 2 LINE.
           WRITE DEBUG-RPT-LINE FROM FOOTER-1
               AFTER ADVANCING 1 LINE.

       Y1050-EXIT.
           EXIT.

           EJECT
      *  COMMON ABEND ROUTINE
           COPY JXCPP099.


