 PARM LINK (JXE300PV R)
*******************************************************************
*      PROGRAM JXE300PV
*JX*  CREATE AN EXTRACT FROM A PANVALET DIRECTORY REPORT
*            (C) 2005 RENAISSANCE DATA SYSTEMS INC.
*                 REFWIZ  JXREF VER 5.0
*******************************************************************
*  INPUT IS A PAN#2 PANVALET DIRECTORY REPORT  1-UP
 FILE PANRPT
  PV-REC              2 120 A
  PV-NAME             2   8 A
  PV-COMMENT-IND      2   1 A
  PV-STUFF           11  70 A
  PV-VERSION-LIT     62   8 A
  PV-VERSION-DATE    73   8 A
  PV-VERSION-MM      73   2 A
  PV-VERSION-DD      76   2 A
  PV-VERSION-YY      79   2 A
  PV-TYPE            27  12 A
  PV-STAT-1          39   1 A
  PV-STAT-2          44   1 A
  PV-STAT-3          49   1 A
  PV-MAINT-DATE      55   8 A
  PV-MAINT-MM        55   2 A
  PV-MAINT-DD        58   2 A
  PV-MAINT-YY        61   2 A
  PV-ACCESS-DATE     64   8 A
  PV-ACCESS-MM       64   2 A
  PV-ACCESS-DD       67   2 A
  PV-ACCESS-YY       70   2 A

 FILE PANLST      FB (133 27930)
  PO-REC              1 133 A
  PO-NAME             1   8 A
  PO-TYPE            26  12 A
  PO-STATUS          38  15 A
  PO-STAT-1          38   1 A
  PO-STAT-2          40   1 A
  PO-STAT-3          42   1 A
  PO-MAINT-DATE      52  10 A
  PO-MAINT-CC        52   2 A
  PO-MAINT-YY        54   2 A
  PO-MAINT-SL1       56   1 A
  PO-MAINT-MM        57   2 A
  PO-MAINT-SL2       59   1 A
  PO-MAINT-DD        60   2 A
  PO-DATE           122  10 A

 WS-VERSION-DATE     W   8 A
 WS-DATE     W  10 A
  WS-CC   WS-DATE     2 A
  WS-YY   WS-DATE +2  2 A
  WS-SL1  WS-DATE +4  1 A
  WS-MM   WS-DATE +5  2 A
  WS-SL2  WS-DATE +7  1 A
  WS-DD   WS-DATE +8  2 A

 REC-CNT     W   7 N

*****************************************************************
*  DETERMINE REPORT DATE AND CREATE AN EXTRACT FILE
*****************************************************************
JOB INPUT(PANRPT)
 REC-CNT = REC-CNT + 1

*  DETERMINE REPORT DATE
 IF REC-CNT = 1
     WS-DATE = '  '
     IF PV-VERSION-LIT = 'VERSION '
         WS-VERSION-DATE = PV-VERSION-DATE
         WS-MM =  PV-VERSION-MM
         WS-DD =  PV-VERSION-DD
         WS-YY =  PV-VERSION-YY
         WS-SL1 = '/'
         WS-SL2 = '/'
         IF WS-YY < '65'
             WS-CC = '20'
         ELSE
             WS-CC = '19'
         END-IF
         GO TO JOB
     END-IF
 END-IF

*  OMIT HEADING AND FOOTER LINES
 IF PV-NAME = 'XXXXX.IN'  'SYS2.PAN'  'NAME    ' '        ' +
              '++PRINT '  'P A N V '  'ALLOCATE' 'IN USE  ' +
              'PERCENT '  'AVAILABL'  'OPENED  ' 'CLOSED  ' +
              '***** LA'  'FILE VER'
     GO TO JOB
 END-IF

*  OMIT COMMENT LINES
 IF PV-COMMENT-IND = '*' ' '
     GO TO JOB
 END-IF

 PO-REC = PV-REC
 PO-DATE  = WS-DATE

*  REFORMAT STATUS TO SINGLE CHARACTERS
 PO-STATUS  = '  '
 PO-STAT-1  =  PV-STAT-1
 PO-STAT-2  =  PV-STAT-2
 PO-STAT-3  =  PV-STAT-3

* IF DATE IS SPACES, USE ACCESS DATE
 IF PV-MAINT-DATE = '   '
    PV-MAINT-DATE = PV-ACCESS-DATE
 END-IF

*  REFORMAT DATE AND ADD CENTURY
 PO-MAINT-DATE = '          '
 PO-MAINT-DATE = '    /  /  '
 PO-MAINT-MM   = PV-MAINT-MM
 PO-MAINT-DD   = PV-MAINT-DD
 PO-MAINT-YY   = PV-MAINT-YY
 IF PO-MAINT-YY < '65'
     PO-MAINT-CC = '20'
 END-IF
 IF PO-MAINT-YY GE '65'
     PO-MAINT-CC = '19'
 END-IF

 PUT PANLST
 GO TO JOB

