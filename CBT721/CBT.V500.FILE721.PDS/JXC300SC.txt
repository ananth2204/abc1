       PROCESS NOADV
       IDENTIFICATION DIVISION.
       PROGRAM-ID. JXC300SC.
      *****************************************************************
      *                                                               *
      * (C) 2005 RENAISSANCE DATA SYSTEMS INC.                        *
      *         CHANGEWIZ STAT REL 1.0 VER 1.0                        *
      *                                                               *
      * THESE PROGRAMS ARE DISTRIBUTED ON THE CBT TAPE WITH THE       *
      * PROVISO THAT THEY MAY BE FREELY DISTRIBUTED TO ANY OTHER      *
      * PARTY ON CONDITION THAT NO INDUCEMENT BEYOND REASONABLE       *
      * HANDLING COSTS IS OFFERED OR ACCEPTED BY EITHER SIDE FOR      *
      * SUCH DISTRIBUTION OR YOUR NORMAL CONSULTING COSTS FOR         *
      * INSTALLATION AND SUPPORT.                                     *
      *                                                               *
      *****************************************************************
      ****************************************************************
      *JX* CREATE AN ELEMENT EXTRACT FILE FROM SCLM ACCOUNT FILE.
      *
      * A0000-  INITIALIZATION AND TERMINATION ROUTINES
      * C0000-  MAIN PROCESSING
      * D0000-  FORMAT OUTPUT RECORDS
      * E0000-  CHANGE CODE PROCESSING
      * W0000-  DATE CONVERSION ROUTINES
      * X0000-  PROGRAM READ, WRITE AND OTHER I-O ROUTINES
      * Y0000-  DEBUG ROUTINES
      * Z0000-  ABEND ROUTINES
      *
      *
      ****************************************************************
      *  MODIFICATIONS
      *
      *  12/05/2003 SH  GET CREATE-DATE FOR HISTORY RECORD.
      ****************************************************************

       ENVIRONMENT DIVISION.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.

           SELECT SCLM-ACCOUNT-FILE
               STATUS IS SCLM-FILE-STATUS   ASSIGN SCACCT.
           SELECT RUN-TYPE-FILE
               STATUS IS RUN-TYPE-STATUS    ASSIGN RUNTYPE.

           SELECT ELEMENT-XTRCT-OUT
               STATUS IS ELEM-XTRCT-STATUS  ASSIGN SRCXTRT.
           SELECT ELEMENT-LIST-OUT
               STATUS IS ELEM-LIST-STATUS   ASSIGN SRCLIST.

           SELECT DEBUG-REPORT              ASSIGN RDBUG.

       DATA DIVISION.
       FILE SECTION.

       FD  SCLM-ACCOUNT-FILE
           RECORDING MODE IS V
           BLOCK CONTAINS 0 RECORDS.
      *  INPUT SCLM ACCOUNT FILE
       01  SCLM-ACCOUNT-REC-MIN        PIC X(80).
       01  SCLM-ACCOUNT-REC-MAX        PIC X(32000).

       FD  RUN-TYPE-FILE
           RECORDING MODE IS F
           BLOCK CONTAINS 0 RECORDS.
       01  RUN-TYPE-REC.
           05  RT-RUN-TYPE             PIC X(08).
           05  RT-CHANGED-DAYS         PIC 9(05).
           05  FILLER                  PIC X(67).

       FD  ELEMENT-XTRCT-OUT
           RECORDING MODE IS F
           BLOCK CONTAINS 0 RECORDS.
       01  ELEMENT-XTRCT-REC           PIC X(200).

       FD  ELEMENT-LIST-OUT
           RECORDING MODE IS F
           BLOCK CONTAINS 0 RECORDS.
       01  ELEMENT-LIST-REC.
           05  LST-ELEMENT-NAME        PIC X(08).
           05  FILLER                  PIC X(01).
           05  LST-ELEMENT-LIB         PIC X(08).
           05  FILLER                  PIC X(03).


       FD  DEBUG-REPORT
           RECORDING MODE IS F
           BLOCK CONTAINS 0 RECORDS.
       01  DEBUG-RPT-LINE              PIC X(133).

      ************************************************************
       WORKING-STORAGE SECTION.

       01  WS-PROGRAM                  PIC X(08) VALUE 'JXC300SC'.

      *  VENDOR INFO
           COPY JXCPW001.

      *  REFWIZ PRODUCT INFO
           COPY JXCPW002.

           EJECT
       01  WS-RUN-TYPE-RECORD.
           05  WS-RUN-TYPE             PIC X(08).
               88  WS-VALID-RUN-TYPE   VALUE 'ALL   '  'CHANGED'.
               88  WS-EXTRACT-ALL      VALUE 'ALL   '.
               88  WS-EXTRACT-CHANGED  VALUE 'CHANGED'.
           05  WS-CHANGED-DAYS         PIC 9(05).
           05  FILLER                  PIC X(67).

       01  SCLM-ACCOUNT-RECORD.
           05  SA-DATA-FIXED.
               10  SA-AB-REC-TYPE         PIC X(01).
               10  SA-AB-REC-NBR          PIC X(01).
               10  SA-ENVIR-LEVEL         PIC X(08).
      *                              FLMTYP  ACCOUNTING TYPE
               10  SA-LIBRARY-TYPE        PIC X(08).
      *                              FLMMBR  ACCOUNTING MEMBER
               10  SA-ELEMENT-NAME        PIC X(08).
               10  FILLER                 PIC X(03).
      *                      FLMCD4 AND FLMCTM  CHANGE DATE/TIME
               10  SA-CHANGE-DATE-TIME    PIC X(12).
      *                              FLMCLV    CHANGE GROUP
               10  FILLER                 PIC X(08).
      *                              FLMCUS    CHANGE USER ID
               10  SA-EDIT-BY             PIC X(08).
               10  FILLER                 PIC X(03).
      *                              FLMMVR    MEMBER VERSION
               10  SA-NUMBER-UPDATES      PIC S9(09) COMP.
      *                              FLMLAN    LANGUAGE
               10  SA-LANGUAGE            PIC X(08).
               10  FILLER                 PIC X(32).
      *                              FLMID4    CREATION DATE
               10  SA-CREATE-DATE-TIME    PIC X(12).
      *                      FLMMD4 AND FLMMTM  BUILD MAP DATE/TIME
               10  SA-BUILD-DATE-TIME     PIC X(12).
               10  FILLER                 PIC X(12).
      *                      FLMPD4 AND FLMPTM  PROMOTE DATE/TIME
               10  SA-PROMOTE-DATE-TIME   PIC X(12).
      *                              FLMPUS    PROMOTE USER ID
               10  SA-PROMOTE-BY          PIC X(08).
               10  FILLER                 PIC X(40).
      *                              FLMTLL    TOTAL LINES
               10  SA-TOTAL-LINES         PIC S9(09) COMP.
      *                              FLMCML    COMMENT LINES
               10  SA-COMMENT-LINE-CNT    PIC S9(09) COMP.
      *                              FLMNCL   NONCOMMENT LINE COUNT
               10  SA-NONCOMMENT-LINE-CNT PIC S9(09) COMP.
      *                              FLMBLL   BLANK LINES
               10  SA-BLANK-LINE-CNT      PIC S9(09) COMP.
      *                              FLMPRL  PROLOG LINES
               10  SA-COUNT-5             PIC S9(09) COMP.
      *                              FLMTLS    TOTAL STATEMENTS
               10  SA-TOTAL-STMTS         PIC S9(09) COMP.
      *                              FLMCMS    COMMENT STATEMENTS
               10  SA-COUNT-7             PIC S9(09) COMP.
      *                              FLM???
               10  SA-COUNT-8             PIC S9(09) COMP.
      *                              FLM???
               10  SA-COUNT-9             PIC S9(09) COMP.
      *                              FLMNCS    NONCOMMENT STATEMENTS
               10  SA-NONCOMMENT-STMTS    PIC S9(09) COMP.
      *                              FLM???
               10  SA-COUNT-11            PIC S9(09) COMP.
      *                              FLMNIN  TOTAL MODS
               10  SA-TOTAL-MODS          PIC S9(09) COMP.
      *                              FLM???
               10  SA-MOD-TYPES           PIC S9(09) COMP.
      *                              FLMNCC  NBR OF CHANGE CODES
               10  SA-NBR-CHANGE-CODES    PIC S9(09) COMP.
      *                              FLM???
               10  SA-COUNT-15            PIC S9(09) COMP.

           05  SA-DATA-VARIABLE           PIC X(31740).


      *  SOURCE HISTORY RECORD LAYOUT
           COPY JXCPR300.


       01  WORK-IBM-DATE-TIME.
           05  WK-IBM-DATE.
               10  WK-IBM-DATE-CCYY   PIC S9(9) COMP.
               10  WK-IBM-DATE-MM1    PIC X(01).
               10  WK-IBM-DATE-DD1    PIC X(01).
           05  WK-IBM-DATE-2     REDEFINES WK-IBM-DATE.
               10  WK-IBM-DATE-YMD    PIC 9(06).
           05  WK-IBM-DATE-3     REDEFINES WK-IBM-DATE.
               10  WK-IBM-DATE-YY     PIC 9(02).
               10  WK-IBM-DATE-MM     PIC 9(02).
               10  WK-IBM-DATE-DD     PIC 9(02).
           05  WK-IBM-DATE-4     REDEFINES WK-IBM-DATE.
               10  WK-IBM-DATE-HV     PIC X(02).
               10  WK-IBM-DATE-YY-BIN PIC S9(04) COMP.
               10  FILLER             PIC X(02).
           05  WK-IBM-TIME.
               10  WK-IBM-TIME-HH     PIC 9(02).
               10  WK-IBM-TIME-MIN    PIC 9(02).
               10  WK-IBM-TIME-SEC    PIC 9(02).
           05  WK-IBM-TIME-2     REDEFINES WK-IBM-TIME.
               10  WK-IBM-TIME-HMS    PIC 9(06).

       01  WORK-IO-FIELDS.
           05  READ-ACCOUNT-CNT        PIC S9(9) COMP-3 VALUE ZERO.
           05  WRITE-EXTRACT-CNT       PIC S9(9) COMP-3 VALUE ZERO.
           05  WRITE-ELEMENT-CNT       PIC S9(9) COMP-3 VALUE ZERO.
           05  DEBUG-RPT-LINE-CNT      PIC S9(3) COMP-3 VALUE +80.
           05  DEBUG-PAGE-CNT          PIC S9(7) COMP-3 VALUE ZERO.
           05  DISPLAY-COUNT           PIC ZZZ,ZZZ,ZZZ.
           05  SCLM-FILE-STATUS        PIC X(02) VALUE SPACE.
           05  RUN-TYPE-STATUS         PIC X(02) VALUE SPACE.
           05  ELEM-XTRCT-STATUS       PIC X(02) VALUE SPACE.
           05  ELEM-LIST-STATUS        PIC X(02) VALUE SPACE.
           05  END-OF-ACCOUNT-LINES-SW     PIC X VALUE SPACE.
               88  END-OF-ACCOUNT-LINES          VALUE 'Y'.

       01  HOLD-FIELDS.
           05  HOLD-PRINT-LINE         PIC X(133) VALUE SPACE.
           05  SAVE-CAPTURE-DATE       PIC X(10) VALUE SPACE.
           05  SAVE-CAPTURE-JULDATE    PIC X(08) VALUE SPACE.
           05  SAVE-CAPTURE-TIME       PIC X(08) VALUE SPACE.
           05  SAVE-CHANGED-JULDATE    PIC X(08) VALUE SPACE.


       01  WK-CONVERSION-FIELDS.
           05  WK-POS                      PIC S9(9) COMP VALUE ZERO.
           05  WK-ACCOUNT-DATE.
               10  WK-ACCOUNT-CCYY         PIC 9(04) VALUE ZERO.
               10  WK-ACCOUNT-CCYY-X REDEFINES WK-ACCOUNT-CCYY.
                   15  WK-ACCOUNT-CC       PIC 9(02).
                   15  WK-ACCOUNT-YY       PIC 9(02).
               10  FILLER                  PIC X(01) VALUE '-'.
               10  WK-ACCOUNT-MM           PIC 9(02) VALUE ZERO.
               10  FILLER                  PIC X(01) VALUE '-'.
               10  WK-ACCOUNT-DD           PIC 9(02) VALUE ZERO.

           05  WK-ACCOUNT-JULDATE.
               10  WK-ACCT-JUL-CCYY         PIC 9(04) VALUE ZERO.
               10  WK-ACCT-JUL-CCYY-X REDEFINES WK-ACCT-JUL-CCYY.
                   15  WK-ACCT-JUL-CC       PIC 9(02).
                   15  WK-ACCT-JUL-YY       PIC 9(02).
               10  FILLER                  PIC X(01) VALUE '-'.
               10  WK-ACCT-JUL-DDD         PIC 9(03) VALUE ZERO.

           05  WK-ACCOUNT-TIME.
               10  WK-ACCOUNT-HH           PIC 9(02) VALUE ZERO.
               10  FILLER                  PIC X(01) VALUE ':'.
               10  WK-ACCOUNT-MIN          PIC 9(02) VALUE ZERO.
               10  FILLER                  PIC X(01) VALUE ':'.
               10  WK-ACCOUNT-SS           PIC 9(02) VALUE ZERO.

           05  WK-CONV-BYTE            PIC S9(4) COMP.
           05  WK-CONV-BYTE-X REDEFINES WK-CONV-BYTE.
               10  WK-HALFWORD-1       PIC X.
               10  WK-HALFWORD-2       PIC X.

           05  WK-CONV-COUNT           PIC S9(9) COMP.
           05  WK-CONV-COUNT-X REDEFINES WK-CONV-COUNT  PIC X(04).

           05  WK-HOLD-DDD             PIC 9(05) VALUE ZERO.

           05  WK-YEAR-CCYY            PIC 9(04) VALUE ZERO.
           05  WK-YEAR-CCYY-X REDEFINES WK-YEAR-CCYY.
               10  WK-YEAR-CC          PIC 9(02).
               10  WK-YEAR-YY          PIC 9(02).

           05  WK-CHANGE-CODE-CNT      PIC 9(05).
           05  WK-CHANGE-CODE-AREA.
               10  WK-CHANGE-CODE      PIC X(08).
               10  WK-CHANGE-CODE-DATE PIC X(12).

           EJECT
       01  DEBUG-HEADING-1.
           05  FILLER                  PIC X     VALUE SPACES.
           05  DBH1-IO-TYPE            PIC X(04) VALUE SPACES.
           05  FILLER                  PIC X     VALUE SPACES.
           05  DBH1-ELEMENT-NAME       PIC X(08) VALUE 'ELEMENT'.
           05  FILLER                  PIC X(01) VALUE SPACES.
           05  DBH1-OUTPUT             PIC X(90) VALUE
               '    INPUT AND OUTPUT DATA '.

       01  DEBUG-HEADING-2.
           05  FILLER                  PIC X     VALUE SPACES.
           05  DBH2-IO-TYPE            PIC X(04) VALUE ' IO'.
           05  FILLER                  PIC X     VALUE SPACES.
           05  DBH2-ELEMENT-NAME       PIC X(08) VALUE '  NAME'.
           05  FILLER                  PIC X(01) VALUE SPACES.
           05  DBH2-OUTPUT             PIC X(115) VALUE SPACES.

       01  DEBUG-DETAIL-LINE-IN.
           05  DEBUG-CC                PIC X.
           05  DEBUG-DATA-IN.
               10  DBI-IO-TYPE         PIC X(04).
               10  FILLER              PIC X(01).
               10  DBI-ELEMENT-NAME    PIC X(08).
               10  FILLER              PIC X(01).
               10  DBI-DATA-REC        PIC X(115).

       01  DISPLAY-TOTAL-LINE.
           05  FILLER                  PIC X(02).
           05  DT-DESC                 PIC X(30).
           05  FILLER                  PIC X(02).
           05  DT-COUNT                PIC ZZZ,ZZZ,ZZ9.
           05  FILLER                  PIC X(05).
           05  DT-DESC2                PIC X(06).
           05  FILLER                  PIC X(02).
           05  DT-COUNT2               PIC ZZZ,ZZZ,ZZ9.
           05  FILLER                  PIC X(05).
           05  DT-DESC3                PIC X(07).
           05  FILLER                  PIC X(02).
           05  DT-COUNT3               PIC ZZZ,ZZZ,ZZ9.

           EJECT
      *  COMMON REPORT HEADINGS AND FOOTER
      *  DATE-TIME WORK AREAS
      *  COMMON DEBUG WORK AREAS
      *  COMMON ABEND WORK AREA
           COPY JXCPW020.

           EJECT
      *  DATE CONVERSION WORK AREAS
           COPY JXCPW025.

           EJECT
      *+ INSTALLATION SPECIFIC CONSTANTS
           COPY JXINW005.
           EJECT
       LINKAGE SECTION.
      *  COMMON PROGRAM RUN PARAMETERS
           COPY JXCPL005.

           EJECT
       PROCEDURE DIVISION USING JXL-PARM-DATA.

           PERFORM A1000-INITIALIZATION THRU A1000-EXIT.

           PERFORM C1000-PROCESS-ACCOUNT-FILE
              THRU C1000-EXIT
              UNTIL END-OF-ACCOUNT-LINES.

           PERFORM A9000-TERMINATION THRU A9000-EXIT.
           GOBACK.

       A1000-INITIALIZATION.

           DISPLAY '**************************************************'
                  '**************************************************'.
           DISPLAY ' '.
           DISPLAY 'PROGRAM JXC300SC - EXTRACT ELEMENTS FROM '
                   'SCLM ACCOUNT FILE'
           MOVE ' DEBUG REPORT - EXTRACT ELEMENTS FROM SCLM ACCT FL'
                                                 TO JX-DEBUG-TITLE.

           MOVE RULER-LINE(2:115)  TO DBH2-OUTPUT.
           MOVE WS-PROGRAM         TO JXRD-PROGRAM
                                      JXCD-PROGRAM
                                      HD2-PROGRAM.
           MOVE SPACE              TO SOURCE-HISTORY-RECORD.

           MOVE 'CHANGEWIZ  SCLM   REL 1.0 VER 1.0' TO FT1-PRODUCT.
           MOVE '(C) 2005 RENAISSANCE DATA SYSTEMS INC.'
                                                    TO FT1-VENDOR.

      **  DISPLAY COMPILE DATE AND TIME
           COPY JXCPP010.

           DISPLAY ' '.

      **  PROCESS COMMON INPUT PARAMETER
           COPY JXCPP015.

      *+  INSTALLATION SPECIFIC INITIALIZATION ROUTINES
           COPY JXINP005.


           OPEN INPUT SCLM-ACCOUNT-FILE.
           OPEN OUTPUT ELEMENT-XTRCT-OUT
                       ELEMENT-LIST-OUT.

           IF JX-PRINT-DEBUG
               OPEN OUTPUT DEBUG-REPORT.

           IF SCLM-FILE-STATUS NOT = '00' AND '97'
               DISPLAY '   BAD OPEN ON ACCOUNT FILE. STATUS = '
                       SCLM-FILE-STATUS
               MOVE 'Y' TO END-OF-ACCOUNT-LINES-SW
               PERFORM Z9000-ABEND-PROGRAM THRU Z9000-EXIT
           ELSE
               PERFORM X1000-READ-ACCOUNT THRU X1000-EXIT
           END-IF.

      * CHECK STATUS OF OUTPUT FILE OPENS
           IF ELEM-XTRCT-STATUS NOT = '00' AND '97'
               DISPLAY '   BAD OPEN ON ELEMENT XTRCT FILE. STATUS = '
                       ELEM-XTRCT-STATUS
               PERFORM Z9000-ABEND-PROGRAM THRU Z9000-EXIT
           END-IF.

           IF ELEM-LIST-STATUS NOT = '00' AND '97'
               DISPLAY '   BAD OPEN ON ELEMENT LIST FILE. STATUS = '
                       ELEM-LIST-STATUS
               PERFORM Z9000-ABEND-PROGRAM THRU Z9000-EXIT
           END-IF.

           MOVE '1900-99-99'      TO WK-ACCOUNT-DATE
           MOVE '00:00:00'        TO WK-ACCOUNT-TIME
           MOVE WS-CDF-CCYY       TO WK-ACCOUNT-CCYY
           MOVE WS-CDF-CC         TO JX-CGREG-CC
           MOVE WS-CDF-YY         TO JX-CGREG-YY
           MOVE WS-CDF-MM         TO WK-ACCOUNT-MM
                                     JX-CGREG-MM
           MOVE WS-CDF-DD         TO WK-ACCOUNT-DD
                                     JX-CGREG-DD
           MOVE WS-CDF-HR         TO WK-ACCOUNT-HH
           MOVE WS-CDF-MIN        TO WK-ACCOUNT-MIN
           MOVE WS-CDF-SEC        TO WK-ACCOUNT-SS.

           MOVE SPACE             TO WK-ACCOUNT-JULDATE

           PERFORM W5100-CONVERT-GREG-TO-JULIAN THRU W5100-EXIT.

           IF JXCDE-RETURN-CD = 0
               MOVE '0000-000'           TO WK-ACCOUNT-JULDATE
               MOVE JX-CJUL-CC           TO WK-ACCT-JUL-CC
               MOVE JX-CJUL-YY           TO WK-ACCT-JUL-YY
               MOVE JX-CJUL-DDD          TO WK-ACCT-JUL-DDD
           ELSE
               DISPLAY 'INVALID CURRENT DATE'
               DISPLAY JX-CONV-DATE-ERROR
               MOVE SPACE                TO WK-ACCOUNT-JULDATE
               MOVE 'Y' TO END-OF-ACCOUNT-LINES-SW
               PERFORM Z9000-ABEND-PROGRAM THRU Z9000-EXIT
           END-IF.


           MOVE WK-ACCOUNT-DATE       TO SAVE-CAPTURE-DATE.
           MOVE WK-ACCOUNT-JULDATE    TO SAVE-CAPTURE-JULDATE.
           MOVE WK-ACCOUNT-TIME       TO SAVE-CAPTURE-TIME.

           IF END-OF-ACCOUNT-LINES
               DISPLAY 'NO RECORDS IN ACCOUNT FILE'
               GO TO A1000-EXIT
           END-IF.

      *+  INSTALLATION-SPECIFIC CODE TO DETERMINE DATE CUTOFF
           COPY JXINP010.

      * GET RUN TYPE FROM INPUT CARD
           PERFORM A1100-GET-RUN-TYPE THRU A1100-EXIT.

      * CREATE A DUMMY RECORD
           IF WS-EXTRACT-CHANGED
              MOVE SPACE TO SOURCE-HISTORY-RECORD
              MOVE '0000-00-00'          TO SH-EDIT-DATE-CYMD
              MOVE '0000-000'            TO SH-EDIT-DATE-JUL
              MOVE '00:00:00'            TO SH-EDIT-TIME
              MOVE '0000-00-00'          TO SH-PROMOTE-DATE-CYMD
              MOVE '0000-000'            TO SH-PROMOTE-DATE-JUL
              MOVE '00:00:00'            TO SH-PROMOTE-TIME
              MOVE ZERO                  TO SH-ELEMENT-LEVEL
              MOVE ZERO                  TO SH-NUMBER-LINES
              MOVE SAVE-CAPTURE-DATE     TO SH-CAPTURE-DATE-CYMD
              MOVE SAVE-CAPTURE-JULDATE  TO SH-CAPTURE-DATE-JUL
              MOVE SAVE-CAPTURE-TIME     TO SH-CAPTURE-TIME
              MOVE 'DUMMY'               TO SH-CHANGE-CODE
              PERFORM X3000-WRITE-ELEMENT THRU X3000-EXIT
           END-IF.

       A1000-EXIT.
           EXIT.

      ***********************************************************
      *   GET RUN TYPE FROM INPUT CARD
      *   INPUT CARD FORMAT IS:
      *      ----+----1----+----2
      *      ALL                  EXTRACT ALL ELEMENTS
      *      CHANGED 99999        99999 IS NUMBER OF DAYS TO PULL
      *
      *      USE 'ALL' TO CREATE THE FIRST HISTORY FILE
      *      USE 'CHANGED' FOR DAILY, WEEKLY OR MONTHLY UPDATES
      ***********************************************************
       A1100-GET-RUN-TYPE.
           OPEN INPUT RUN-TYPE-FILE.

           IF RUN-TYPE-STATUS NOT = '00' AND '97'
               DISPLAY '   BAD OPEN ON RUNTYPE FILE. STATUS = '
                       RUN-TYPE-STATUS
               PERFORM Z9000-ABEND-PROGRAM THRU Z9000-EXIT
           END-IF.

           READ RUN-TYPE-FILE INTO WS-RUN-TYPE-RECORD.

           IF RUN-TYPE-STATUS  = '10'
               DISPLAY '   NO DATA IN RUNTYPE FILE.'
               PERFORM Z9000-ABEND-PROGRAM THRU Z9000-EXIT
           END-IF.

           IF NOT WS-VALID-RUN-TYPE
              DISPLAY 'INVALID RUN-TYPE. VALID VALUES ARE: '
                      '"ALL" OR "CHANGED" '
               PERFORM Z9000-ABEND-PROGRAM THRU Z9000-EXIT
           END-IF.

           IF WS-EXTRACT-ALL
              DISPLAY 'EXTRACT ALL ELEMENTS IN SCLM ACCOUNT FILE'
              DISPLAY '    '
           ELSE
           IF WS-CHANGED-DAYS NOT NUMERIC
              DISPLAY 'CHANGED DAYS IS NOT NUMERIC      '
              DISPLAY '   CARD FORMAT IS "CHANGED 99999" '
              DISPLAY '      POS 9-13 IS 5 DIGIT NUMERIC '
              DISPLAY '    '
              PERFORM Z9000-ABEND-PROGRAM THRU Z9000-EXIT
           END-IF.

           IF WS-EXTRACT-CHANGED
              MOVE SAVE-CAPTURE-JULDATE TO WK-ACCOUNT-JULDATE
              MOVE WK-ACCT-JUL-DDD  TO WK-HOLD-DDD
              PERFORM UNTIL WS-CHANGED-DAYS < WK-HOLD-DDD
                 SUBTRACT 1 FROM WK-ACCT-JUL-CCYY
                 ADD 365 TO WK-HOLD-DDD
              END-PERFORM
              SUBTRACT WS-CHANGED-DAYS FROM WK-HOLD-DDD
              MOVE WK-HOLD-DDD           TO WK-ACCT-JUL-DDD
              MOVE WK-ACCOUNT-JULDATE TO SAVE-CHANGED-JULDATE
              DISPLAY 'EXTRACT ELEMENTS CHANGED IN LAST '
                     WS-CHANGED-DAYS ' DAYS SINCE ' WK-ACCOUNT-JULDATE
           END-IF.

           CLOSE RUN-TYPE-FILE.

       A1100-EXIT.
           EXIT.

      ***********************************************************
      *   END OF JOB PROCESSING
      ***********************************************************
       A9000-TERMINATION.

           CLOSE SCLM-ACCOUNT-FILE
                 ELEMENT-XTRCT-OUT
                 ELEMENT-LIST-OUT.

      *  ENABLE TOTALS TO PRINT ON DEBUG REPORT
           IF JX-PRINT-DEBUG OR DEBUG-PAGE-CNT > 0
              ADD 15 TO JX-PRINT-DEBUG-MAX
              IF NOT JX-PRINT-DEBUG
                 MOVE 'Y' TO JX-DEBUG-SW
              END-IF
              ADD 15 TO DEBUG-RPT-LINE-CNT
              IF DEBUG-RPT-LINE-CNT > INST-LINES-PER-PAGE
                 SUBTRACT 15 FROM DEBUG-RPT-LINE-CNT
                 PERFORM Y1025-DEBUG-HEADING THRU Y1025-EXIT
              ELSE
                 SUBTRACT 15 FROM DEBUG-RPT-LINE-CNT
                 MOVE SPACES        TO DEBUG-RPT-LINE
                 PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
                 PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
              END-IF
           END-IF.

           DISPLAY ' '.
           DISPLAY ' '.
           DISPLAY '****************************************'
                   '****************************************'
           DISPLAY ' '.
           DISPLAY 'END OF PROGRAM JXC300SC.'.
           DISPLAY ' '.


           IF JX-PRINT-DEBUG
               MOVE ALL '*'       TO DEBUG-RPT-LINE(2:125)
               PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
               MOVE SPACES        TO DEBUG-RPT-LINE
               PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
               MOVE ' END-OF-PROGRAM JXC300SC RECORD COUNTS' TO
                                     DEBUG-RPT-LINE
               PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
               MOVE SPACES        TO DEBUG-RPT-LINE
               PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
           END-IF.

           MOVE SPACE TO DISPLAY-TOTAL-LINE.

           MOVE 'SCLM ACCOUNT RECORDS READ    ' TO DT-DESC.
           MOVE READ-ACCOUNT-CNT                TO DT-COUNT
           DISPLAY DISPLAY-TOTAL-LINE
           IF JX-PRINT-DEBUG
              MOVE DISPLAY-TOTAL-LINE TO DEBUG-RPT-LINE
              PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
           END-IF.

           MOVE 'EXTRACT RECORDS WRITTEN  '  TO DT-DESC.
           MOVE WRITE-EXTRACT-CNT            TO DT-COUNT
           DISPLAY DISPLAY-TOTAL-LINE
           IF JX-PRINT-DEBUG
              MOVE DISPLAY-TOTAL-LINE TO DEBUG-RPT-LINE
              PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
           END-IF.

           MOVE 'ELEMENT LIST RECS WRITTEN '    TO DT-DESC
           MOVE WRITE-ELEMENT-CNT               TO DT-COUNT
           DISPLAY DISPLAY-TOTAL-LINE
           DISPLAY '   '
           IF JX-PRINT-DEBUG
              MOVE DISPLAY-TOTAL-LINE TO DEBUG-RPT-LINE
              PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
              MOVE SPACE              TO DEBUG-RPT-LINE
              PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
           END-IF.

           DISPLAY ' '.
           DISPLAY '****************************************'
                   '****************************************'
           DISPLAY ' '.

           IF JX-PRINT-DEBUG
               MOVE SPACES        TO DEBUG-RPT-LINE
               PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
               MOVE ALL '*'       TO DEBUG-RPT-LINE(2:125)
               PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
               MOVE SPACES        TO DEBUG-RPT-LINE
               PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
           END-IF.



      **  FINISH DEBUG REPORT
           IF DEBUG-PAGE-CNT > 0
               PERFORM Y1050-DEBUG-FOOTER THRU Y1050-EXIT.

           IF JX-PRINT-DEBUG OR DEBUG-PAGE-CNT > 0
               CLOSE DEBUG-REPORT.

       A9000-EXIT.
           EXIT.
           EJECT
      ************************************************************
      *  WRITE OUT AT LEAST ONE RECORD FOR EACH ELEMENT.
      *  WRITE A RECORD FOR EACH CHANGE CODE
      ************************************************************
       C1000-PROCESS-ACCOUNT-FILE.

      * WRITE OUT ONE LIST RECORD FOR EACH INPUT. THIS IS USED
      * TO IDENTIFY DELETED ELEMENTS

           MOVE SPACE TO ELEMENT-LIST-REC
           MOVE SA-ELEMENT-NAME      TO LST-ELEMENT-NAME
           MOVE SA-LIBRARY-TYPE      TO LST-ELEMENT-LIB
           PERFORM X4000-WRITE-LIST THRU X4000-EXIT.

      * WRITE OUT ALL ELEMENTS OR ONLY CHANGED ELEMENTS TO
      * EXTRACT FILE

           MOVE SPACE TO SOURCE-HISTORY-RECORD.
           MOVE '0000-00-00'         TO SH-EDIT-DATE-CYMD
           MOVE '0000-000'           TO SH-EDIT-DATE-JUL
           MOVE '00:00:00'           TO SH-EDIT-TIME
           MOVE '0000-00-00'         TO SH-PROMOTE-DATE-CYMD
           MOVE '0000-000'           TO SH-PROMOTE-DATE-JUL
           MOVE '00:00:00'           TO SH-PROMOTE-TIME

           IF SA-PROMOTE-DATE-TIME NOT = '000000000000' AND SPACE
              MOVE SA-PROMOTE-DATE-TIME  TO WORK-IBM-DATE-TIME
              PERFORM W1000-FORMAT-IBM-DATE
                 THRU W1000-EXIT

              MOVE WK-ACCOUNT-DATE      TO SH-PROMOTE-DATE-CYMD
              MOVE WK-ACCOUNT-JULDATE   TO SH-PROMOTE-DATE-JUL
              MOVE WK-ACCOUNT-TIME      TO SH-PROMOTE-TIME
           END-IF.

           IF WS-EXTRACT-CHANGED
              IF SH-PROMOTE-DATE-JUL <  SAVE-CHANGED-JULDATE
                 GO TO C1000-READ-NEXT
              END-IF
           END-IF.

           MOVE SA-ELEMENT-NAME      TO SH-ELEMENT-NAME
           MOVE ZERO                 TO SH-ELEMENT-LEVEL
           MOVE SA-LIBRARY-TYPE      TO SH-ELEMENT-LIB
           MOVE SA-ENVIR-LEVEL       TO SH-ENVIR-LEVEL
           MOVE SA-LANGUAGE          TO SH-ELEMENT-LANG
           MOVE SA-TOTAL-LINES       TO SH-NUMBER-LINES
           MOVE SA-EDIT-BY           TO SH-LAST-EDIT-BY
           MOVE SA-PROMOTE-BY        TO SH-LAST-PROMOTE-BY

           MOVE SAVE-CAPTURE-DATE     TO SH-CAPTURE-DATE-CYMD
           MOVE SAVE-CAPTURE-JULDATE  TO SH-CAPTURE-DATE-JUL
           MOVE SAVE-CAPTURE-TIME     TO SH-CAPTURE-TIME

           IF SA-CHANGE-DATE-TIME NOT = '000000000000' AND SPACE
              MOVE SA-CHANGE-DATE-TIME  TO WORK-IBM-DATE-TIME
              PERFORM W1000-FORMAT-IBM-DATE
                 THRU W1000-EXIT

              MOVE WK-ACCOUNT-DATE      TO SH-EDIT-DATE-CYMD
              MOVE WK-ACCOUNT-JULDATE   TO SH-EDIT-DATE-JUL
              MOVE WK-ACCOUNT-TIME      TO SH-EDIT-TIME
           END-IF.

           IF SA-CREATE-DATE-TIME NOT = '000000000000' AND SPACE
              MOVE SA-CREATE-DATE-TIME  TO WORK-IBM-DATE-TIME
              PERFORM W1000-FORMAT-IBM-DATE
                 THRU W1000-EXIT

              MOVE WK-ACCOUNT-DATE      TO SH-CREATE-DATE-CYMD
           END-IF.

           PERFORM D1000-CREATE-HISTORY THRU D1000-EXIT.

       C1000-READ-NEXT.
           PERFORM X1000-READ-ACCOUNT THRU X1000-EXIT.

       C1000-EXIT.
           EXIT.
           EJECT
      ***************************************************************
      *  ONLY PROCESS ALL ELEMENTS WHEN CREATING FIRST HISTORY FILE
      *  GET CHANGE CODES FROM END OF RECORD
      *  IF ONLY CREATING CHANGED HISTORY, ONLY GET CHANGE CODES
      *  WITH A DATE >= CUTOFF DATE.
      ***************************************************************
       D1000-CREATE-HISTORY.

      * 1. ACCOUNT RECORDS CREATED IN MIGRATION WILL NOT HAVE
      *    A PROMOTE DATE OR CHANGE CODE
      * 2. ACCOUNT RECORDS FOR NON PROD LEVELS  WILL NOT HAVE
      *    A PROMOTE DATE BUT THEY WILL HAVE A CHANGE CODE.

           IF (SA-NBR-CHANGE-CODES  = ZERO  OR
               SA-DATA-VARIABLE(1:20)  = SPACE  OR
               WS-EXTRACT-ALL) AND
               (SA-PROMOTE-DATE-TIME = '000000000000' OR SPACE)
              MOVE SH-EDIT-DATE-CYMD    TO SH-PROMOTE-DATE-CYMD
              MOVE SH-EDIT-DATE-JUL     TO SH-PROMOTE-DATE-JUL
              MOVE SH-EDIT-TIME         TO SH-PROMOTE-TIME
              IF WS-EXTRACT-ALL
                 MOVE SH-EDIT-DATE-CYMD TO SH-CAPTURE-DATE-CYMD
                 MOVE SH-EDIT-DATE-JUL  TO SH-CAPTURE-DATE-JUL
                 MOVE SH-EDIT-TIME      TO SH-CAPTURE-TIME
              END-IF
              IF SA-NBR-CHANGE-CODES = ZERO OR
                 SA-DATA-VARIABLE(1:20)  = SPACE
                 MOVE 'MIGRATED'           TO SH-CHANGE-CODE
                 PERFORM X3000-WRITE-ELEMENT THRU X3000-EXIT
                 GO TO D1000-EXIT
              END-IF
           END-IF.

           IF WS-EXTRACT-ALL
              MOVE SH-PROMOTE-DATE-CYMD  TO SH-CAPTURE-DATE-CYMD
              MOVE SH-PROMOTE-DATE-JUL   TO SH-CAPTURE-DATE-JUL
              MOVE SH-PROMOTE-TIME       TO SH-CAPTURE-TIME
           END-IF.


      *  SEE IF THERE ARE CHANGE CODES AND CREATE RECORDS FOR THEM

           MOVE 1 TO WK-POS.

      *  SKIP PAST THE COPYBOOKS AND INCLUDES
           IF SA-TOTAL-MODS = 0
              NEXT SENTENCE
           ELSE
           IF SA-MOD-TYPES > 2
              DISPLAY 'ELEMENT HAS > 2 MOD TYPES ' SA-ELEMENT-NAME
                      ' TOTAL-MODS = ' SA-TOTAL-MODS
                      ' MOD-TYPES = ' SA-MOD-TYPES
           ELSE
           IF SA-MOD-TYPES = 2
              MOVE SA-TOTAL-MODS              TO WK-CONV-COUNT
              ADD 3 TO WK-CONV-COUNT
              COMPUTE WK-POS = WK-POS + (WK-CONV-COUNT * 8)
           ELSE
           IF SA-DATA-VARIABLE(1:8)  = 'SQL     '
              ADD  8 TO WK-POS
              MOVE SA-DATA-VARIABLE(WK-POS:4) TO WK-CONV-COUNT-X
              ADD  4 TO WK-POS
              COMPUTE WK-POS = WK-POS + (WK-CONV-COUNT * 8)
           ELSE
              MOVE SA-TOTAL-MODS              TO WK-CONV-COUNT
              COMPUTE WK-POS = WK-POS + (WK-CONV-COUNT * 8)
           END-IF.

      *  WK-POS SHOULD BE POINTING TO FIRST CHANGE-CODE

           IF WK-POS > 31721
              DISPLAY 'CHANGE-CODE POINTING PAST END OF DATA. '
                      'CHECK ELEMENT ' SA-ELEMENT-NAME
                      ' TOTAL-MODS = ' SA-TOTAL-MODS
                      ' MOD-TYPES = ' SA-MOD-TYPES
              MOVE SH-EDIT-DATE-CYMD    TO SH-PROMOTE-DATE-CYMD
              MOVE SH-EDIT-DATE-JUL     TO SH-PROMOTE-DATE-JUL
              MOVE SH-EDIT-TIME         TO SH-PROMOTE-TIME
              MOVE 'MIGRATED'           TO SH-CHANGE-CODE
              PERFORM X3000-WRITE-ELEMENT THRU X3000-EXIT
              GO TO D1000-EXIT
           END-IF.

           IF WK-POS = ZERO
              DISPLAY 'WK-POS AT ZERO. CHECK CALCULATION FOR '
                      'ELEMENT ' SA-ELEMENT-NAME
                      ' TOTAL-MODS = ' SA-TOTAL-MODS
                      ' MOD-TYPES = ' SA-MOD-TYPES
              MOVE 1 TO WK-POS
           END-IF.

      * THERE IS NO CHANGE CODE MODULE
           IF SA-DATA-VARIABLE(WK-POS:8) = SPACE
      *       DISPLAY 'WK-POS POINTS TO SPACE. CHECK CALCULATION FOR '
      *               'ELEMENT ' SA-ELEMENT-NAME
      *               ' TOTAL-MODS = ' SA-TOTAL-MODS
      *               ' MOD-TYPES = ' SA-MOD-TYPES
              IF SA-PROMOTE-DATE-TIME = '000000000000' OR SPACE
                 MOVE SH-EDIT-DATE-CYMD    TO SH-PROMOTE-DATE-CYMD
                 MOVE SH-EDIT-DATE-JUL     TO SH-PROMOTE-DATE-JUL
                 MOVE SH-EDIT-TIME         TO SH-PROMOTE-TIME
                 MOVE 'MIGRATED'           TO SH-CHANGE-CODE
              ELSE
                 MOVE 'PROMOTED'           TO SH-CHANGE-CODE
              END-IF
              PERFORM X3000-WRITE-ELEMENT THRU X3000-EXIT
              GO TO D1000-EXIT
           END-IF.


      *  CREATE ONE RECORD FOR EACH CHANGE-CODE

           MOVE ZERO TO WK-CHANGE-CODE-CNT
           PERFORM E1000-PROCESS-CHANGE-CODE
              THRU E1000-EXIT
              UNTIL SA-DATA-VARIABLE(WK-POS:20) = SPACE
                    OR WK-POS > 31721.


       D1000-EXIT.
           EXIT.

      ***************************************************************
      *  CREATE ONE RECORD FOR EACH CHANGE-CODE
      ***************************************************************
       E1000-PROCESS-CHANGE-CODE.

           ADD 1      TO WK-CHANGE-CODE-CNT

           MOVE SA-DATA-VARIABLE(WK-POS:20) TO WK-CHANGE-CODE-AREA.

           MOVE WK-CHANGE-CODE       TO SH-CHANGE-CODE

           IF WK-CHANGE-CODE-CNT > 1
              MOVE WK-CHANGE-CODE-DATE  TO WORK-IBM-DATE-TIME
              PERFORM W1000-FORMAT-IBM-DATE
                 THRU W1000-EXIT
              MOVE WK-ACCOUNT-DATE      TO SH-EDIT-DATE-CYMD
              MOVE WK-ACCOUNT-JULDATE   TO SH-EDIT-DATE-JUL
              MOVE WK-ACCOUNT-TIME      TO SH-EDIT-TIME
              MOVE SH-EDIT-DATE-CYMD    TO SH-PROMOTE-DATE-CYMD
              MOVE SH-EDIT-DATE-JUL     TO SH-PROMOTE-DATE-JUL
              MOVE SH-EDIT-TIME         TO SH-PROMOTE-TIME
              IF WS-EXTRACT-ALL
                 MOVE SH-EDIT-DATE-CYMD TO SH-CAPTURE-DATE-CYMD
                 MOVE SH-EDIT-DATE-JUL  TO SH-CAPTURE-DATE-JUL
                 MOVE SH-EDIT-TIME      TO SH-CAPTURE-TIME
              END-IF
           END-IF.

           PERFORM X3000-WRITE-ELEMENT THRU X3000-EXIT.

      * IF ONLY CHANGED ELEMENTS, ONLY GET THE FIRST CHANGE CODE
           IF WS-EXTRACT-ALL
              ADD 20      TO WK-POS
           ELSE
              MOVE 31722  TO WK-POS
           END-IF.

       E1000-EXIT.
           EXIT.


           EJECT
      ***************************************************************
      *  FORMAT ACCOUNT RECORDS
      ***************************************************************
       W1000-FORMAT-IBM-DATE.

           MOVE ZERO                   TO JX-CONV-GREG-DATE
           MOVE ZERO                   TO JX-CONV-JUL-DATE
           MOVE ZERO                   TO WK-CONV-BYTE.
           MOVE '00:00:00'             TO WK-ACCOUNT-TIME.
           MOVE '0000-00-00'           TO WK-ACCOUNT-DATE
           MOVE '0000-000'             TO WK-ACCOUNT-JULDATE

           IF WK-IBM-DATE-YMD NUMERIC
              MOVE WK-IBM-DATE-YY      TO JX-CGREG-YY
              MOVE WK-IBM-DATE-MM      TO JX-CGREG-MM
              MOVE WK-IBM-DATE-DD      TO JX-CGREG-DD
              MOVE 19                  TO JX-CGREG-CC
           ELSE
           IF WK-IBM-DATE-HV = X'FFFF' OR X'0000'
              MOVE WK-IBM-DATE-YY-BIN  TO WK-YEAR-CCYY
              MOVE WK-YEAR-CC          TO JX-CGREG-CC
              MOVE WK-YEAR-YY          TO JX-CGREG-YY
              MOVE WK-IBM-DATE-MM1     TO WK-HALFWORD-2
              MOVE WK-CONV-BYTE        TO JX-CGREG-MM
              MOVE WK-IBM-DATE-DD1     TO WK-HALFWORD-2
              MOVE WK-CONV-BYTE        TO JX-CGREG-DD
           END-IF
           END-IF.

           IF JX-CONV-GREG-DATE = ZERO
              GO TO W1000-EXIT
           END-IF.


           PERFORM W5100-CONVERT-GREG-TO-JULIAN
              THRU W5100-EXIT.

           IF JXCDE-RETURN-CD = 0
               MOVE JX-CJUL-CC           TO WK-ACCT-JUL-CC
               MOVE JX-CJUL-YY           TO WK-ACCT-JUL-YY
               MOVE JX-CJUL-DDD          TO WK-ACCT-JUL-DDD
               MOVE JX-CGREG-CC          TO WK-ACCOUNT-CC
               MOVE JX-CGREG-YY          TO WK-ACCOUNT-YY
               MOVE JX-CGREG-MM          TO WK-ACCOUNT-MM
               MOVE JX-CGREG-DD          TO WK-ACCOUNT-DD
           ELSE
               DISPLAY 'INVALID DATE FOR ELEMENT ' SA-ELEMENT-NAME
               DISPLAY JX-CONV-DATE-ERROR
               GO TO W1000-EXIT
           END-IF.

           IF WK-IBM-TIME-2 NOT NUMERIC
               GO TO W1000-EXIT
           END-IF.

           MOVE WK-IBM-TIME-HH          TO WK-ACCOUNT-HH
           MOVE WK-IBM-TIME-MIN         TO WK-ACCOUNT-MIN
           MOVE WK-IBM-TIME-SEC         TO WK-ACCOUNT-SS.

       W1000-EXIT.
           EXIT.

           EJECT
      *  COMMON DATE CONVERSION ROUTINES
           COPY JXCPP025.

           EJECT
      ***************************************************************
      *  READ ACCOUNT FILE
      ***************************************************************
       X1000-READ-ACCOUNT.
           MOVE SPACE TO SA-DATA-VARIABLE.

           READ SCLM-ACCOUNT-FILE INTO SCLM-ACCOUNT-RECORD.

           IF SCLM-FILE-STATUS = '10'
               MOVE 'Y' TO END-OF-ACCOUNT-LINES-SW
               GO TO X1000-EXIT.

           ADD 1           TO READ-ACCOUNT-CNT.
           IF SCLM-FILE-STATUS NOT = '00'
               DISPLAY '  BAD READ ON ACCOUNT FILE. STATUS = '
                   SCLM-FILE-STATUS ' ON RECORD NBR ' READ-ACCOUNT-CNT
               GO TO X1000-EXIT.


           IF JX-PRINT-DEBUG
               MOVE SPACES               TO DEBUG-DATA-IN
               MOVE SA-ELEMENT-NAME      TO DBI-ELEMENT-NAME
               MOVE ' I'                 TO DBI-IO-TYPE
               MOVE SCLM-ACCOUNT-RECORD  TO DBI-DATA-REC
               MOVE DEBUG-DETAIL-LINE-IN TO DEBUG-RPT-LINE
               PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
           END-IF.


       X1000-EXIT.
           EXIT.

      ***************************************************************
      *  WRITE AN ELEMENT EXTRACT RECORD.
      ***************************************************************
       X3000-WRITE-ELEMENT.

           WRITE ELEMENT-XTRCT-REC FROM SOURCE-HISTORY-RECORD.
           ADD 1 TO WRITE-EXTRACT-CNT.

           IF JX-PRINT-DEBUG
               MOVE SPACES                  TO DEBUG-DATA-IN
               MOVE ' O'                    TO DBI-IO-TYPE
               MOVE SOURCE-HISTORY-RECORD   TO DBI-DATA-REC
               MOVE DEBUG-DETAIL-LINE-IN    TO DEBUG-RPT-LINE
               PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
           END-IF.

       X3000-EXIT.
           EXIT.

      ***************************************************************
      *  WRITE AN ELEMENT LIST RECORD.
      ***************************************************************
       X4000-WRITE-LIST.

           WRITE ELEMENT-LIST-REC.
           ADD 1 TO WRITE-ELEMENT-CNT.


       X4000-EXIT.
           EXIT.

           EJECT
      ***************************************************************
      ***  PRINT ROUTINES
      ***************************************************************

       Y1000-PRINT-DEBUG.
           ADD 1 TO JX-PRINT-DEBUG-CNT.
           IF JX-PRINT-DEBUG-CNT > JX-PRINT-DEBUG-SKIP
               IF DEBUG-RPT-LINE-CNT > INST-LINES-PER-PAGE
                   MOVE DEBUG-RPT-LINE TO HOLD-PRINT-LINE
                   PERFORM Y1025-DEBUG-HEADING THRU Y1025-EXIT
                   MOVE HOLD-PRINT-LINE TO DEBUG-RPT-LINE.

           IF JX-PRINT-DEBUG-CNT > JX-PRINT-DEBUG-SKIP
               WRITE DEBUG-RPT-LINE
                   AFTER ADVANCING 1 LINE
               ADD 1 TO DEBUG-RPT-LINE-CNT.

           IF JX-PRINT-DEBUG-CNT > JX-PRINT-DEBUG-MAX
               MOVE 'N' TO JX-DEBUG-SW.
           MOVE SPACES TO DEBUG-RPT-LINE.
       Y1000-EXIT.
           EXIT.

       Y1025-DEBUG-HEADING.
           ADD 1                   TO DEBUG-PAGE-CNT.
           MOVE DEBUG-PAGE-CNT     TO HD1-PAGE.
           MOVE JX-DEBUG-TITLE     TO HD2-TITLE.
           MOVE 'DB'               TO HD2-REPORT.
           MOVE '            CHANGEWIZ DEBUGGING REPORT           '
                                   TO HD1-COMPANY-NAME

           IF DEBUG-PAGE-CNT > 1
               PERFORM Y1050-DEBUG-FOOTER THRU Y1050-EXIT.

           WRITE DEBUG-RPT-LINE FROM HEADING-1
               AFTER ADVANCING PAGE.
           WRITE DEBUG-RPT-LINE FROM HEADING-2
               AFTER ADVANCING 1 LINE.
           WRITE DEBUG-RPT-LINE FROM DEBUG-HEADING-1
               AFTER ADVANCING 2 LINE.
           WRITE DEBUG-RPT-LINE FROM DEBUG-HEADING-2
               AFTER ADVANCING 1 LINE.
           MOVE SPACES TO DEBUG-RPT-LINE
           WRITE DEBUG-RPT-LINE
               AFTER ADVANCING 1 LINE.
           MOVE 6 TO DEBUG-RPT-LINE-CNT.
       Y1025-EXIT.
           EXIT.

       Y1050-DEBUG-FOOTER.
           MOVE SPACES TO DEBUG-RPT-LINE.

           PERFORM UNTIL DEBUG-RPT-LINE-CNT > INST-LINES-PER-PAGE
               WRITE DEBUG-RPT-LINE
                   AFTER ADVANCING 1 LINE
               ADD 1 TO DEBUG-RPT-LINE-CNT
           END-PERFORM.

           WRITE DEBUG-RPT-LINE
               AFTER ADVANCING 2 LINE.
           WRITE DEBUG-RPT-LINE FROM FOOTER-1
               AFTER ADVANCING 1 LINE.

       Y1050-EXIT.
           EXIT.

           EJECT
      *  COMMON ABEND ROUTINE
           COPY JXCPP099.


