       PROCESS NOADV
       IDENTIFICATION DIVISION.
       PROGRAM-ID. JXC300PD.
      *****************************************************************
      *                                                               *
      *  (C) 2005 RENAISSANCE DATA SYSTEMS INC.                       *
      *         CHANGEWIZ STAT REL 1.0 VER 1.0                        *
      *                                                               *
      * THESE PROGRAMS ARE DISTRIBUTED ON THE CBT TAPE WITH THE       *
      * PROVISO THAT THEY MAY BE FREELY DISTRIBUTED TO ANY OTHER      *
      * PARTY ON CONDITION THAT NO INDUCEMENT BEYOND REASONABLE       *
      * HANDLING COSTS IS OFFERED OR ACCEPTED BY EITHER SIDE FOR      *
      * SUCH DISTRIBUTION OR YOUR NORMAL CONSULTING COSTS FOR         *
      * INSTALLATION AND SUPPORT.                                     *
      *                                                               *
      *                                                               *
      *****************************************************************
      ****************************************************************
      *JX* CREATE AN ELEMENT EXTRACT FILE FROM PDS DIRECTORY LISTING.
      *
      *    THE PDS DIRECTORY LISTING IS CREATED BY IEHLIST
      *    WITH DUMP OPTION.  THIS IS CONVERTED TO DISPLAY FORMAT BY
      *    PROGRAM JXCU011. THIS REPORT FROM JXCU011 IS THE INPUT FILE
      *    TO THIS PROGRAM.
      *
      *    THE LIB-NAME FILE CONTAINS THE FILE ABBREVIATION TO USE.
      *
      *    THE ELEMENT-LIST FILE IS USED BY JXC320JX TO IDENTIFY
      *    DELETED ELEMENTS.
      *
      * A0000-  INITIALIZATION AND TERMINATION ROUTINES
      * C0000-  MAIN PROCESSING
      * W0000-  DATE CONVERSION ROUTINES
      * X0000-  PROGRAM READ, WRITE AND OTHER I-O ROUTINES
      * Y0000-  DEBUG ROUTINES
      * Z0000-  ABEND ROUTINES
      *
      *
      ****************************************************************

       ENVIRONMENT DIVISION.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.

           SELECT PDS-DIRECTORY-FILE
               STATUS IS PDS-DIR-STATUS     ASSIGN PDSDIR.
           SELECT LIB-NAME-FILE
               STATUS IS LIB-NAME-STATUS    ASSIGN LIBNAME.

           SELECT ELEMENT-XTRCT-OUT
               STATUS IS ELEM-XTRCT-STATUS  ASSIGN SRCXTRT.
           SELECT ELEMENT-LIST-OUT
               STATUS IS ELEM-LIST-STATUS   ASSIGN SRCLIST.

           SELECT DEBUG-REPORT              ASSIGN RDBUG.

       DATA DIVISION.
       FILE SECTION.

       FD  PDS-DIRECTORY-FILE
           RECORDING MODE IS F
           BLOCK CONTAINS 0 RECORDS.
      *  INPUT IEHLIST DIRECTORY DUMP
       01  PDS-DIRECTORY-REC           PIC X(133).

       FD  LIB-NAME-FILE
           RECORDING MODE IS F
           BLOCK CONTAINS 0 RECORDS.
       01  LIB-NAME-REC                PIC X(80).

       FD  ELEMENT-XTRCT-OUT
           RECORDING MODE IS F
           BLOCK CONTAINS 0 RECORDS.
       01  ELEMENT-XTRCT-REC           PIC X(200).

       FD  ELEMENT-LIST-OUT
           RECORDING MODE IS F
           BLOCK CONTAINS 0 RECORDS.
       01  ELEMENT-LIST-REC.
           05  LST-ELEMENT-NAME        PIC X(08).
           05  FILLER                  PIC X(01).
           05  LST-ELEMENT-LIB         PIC X(08).
           05  FILLER                  PIC X(03).


       FD  DEBUG-REPORT
           RECORDING MODE IS F
           BLOCK CONTAINS 0 RECORDS.
       01  DEBUG-RPT-LINE              PIC X(133).


      **************************************************************
       WORKING-STORAGE SECTION.

       01  WS-PROGRAM                  PIC X(08) VALUE 'JXC300PD'.

      *  VENDOR INFO
           COPY JXCPW001.

      *  REFWIZ PRODUCT INFO
           COPY JXCPW002.


           EJECT
       01  WS-LIB-NAME-RECORD.
           05  WS-LIB-NAME             PIC X(08).
           05  FILLER                  PIC X(01).
           05  WS-LIB-LANGUAGE         PIC X(08).
           05  FILLER                  PIC X(01).
           05  WS-LIB-RUNTYPE          PIC X(08).
           05  FILLER                  PIC X(01).
           05  WS-LIB-DEFAULT-DATE     PIC X(10).
           05  FILLER                  PIC X(01).
           05  WS-LIB-DEFAULT-JUL      PIC X(08).
           05  FILLER                  PIC X(34).

      *  INPUT FILE CREATED BY JXCU011
       01  DIR-DETAIL-LINE.
           05  FILLER                  PIC X.
           05  DR-MEMBER-NAME          PIC X(08).
           05  FILLER                  PIC X(08).
           05  DR-TTR                  PIC X(08).
           05  FILLER                  PIC X(03).
           05  DR-VER-MOD              PIC X(05).
           05  FILLER                  PIC X(03).
           05  DR-CREATE-DATE          PIC X(10).
           05  FILLER                  PIC X(03).
           05  DR-CHANGE-DATE          PIC X(10).
           05  FILLER                  PIC X(02).
           05  DR-CHANGE-TIME          PIC X(08).
           05  FILLER                  PIC X(01).
           05  DR-CURRENT-LINES        PIC X(09).
           05  FILLER                  PIC X(04).
           05  DR-INITIAL-LINES        PIC ZZZZZ,ZZ9.
           05  FILLER                  PIC X(04).
           05  DR-CHANGED-LINES        PIC ZZZZZ,ZZ9.
           05  FILLER                  PIC X(09).
           05  DR-USER-ID              PIC X(10).


      *  SOURCE HISTORY RECORD LAYOUT
           COPY JXCPR300.


       01  WORK-IO-FIELDS.
           05  READ-PDS-DIR-CNT        PIC S9(9) COMP-3 VALUE ZERO.
           05  WRITE-EXTRACT-CNT       PIC S9(9) COMP-3 VALUE ZERO.
           05  WRITE-ELEMENT-CNT       PIC S9(9) COMP-3 VALUE ZERO.
           05  DEBUG-RPT-LINE-CNT      PIC S9(3) COMP-3 VALUE +80.
           05  DEBUG-PAGE-CNT          PIC S9(7) COMP-3 VALUE ZERO.
           05  DISPLAY-COUNT           PIC ZZZ,ZZZ,ZZZ.
           05  PDS-DIR-STATUS          PIC X(02) VALUE SPACE.
           05  LIB-NAME-STATUS         PIC X(02) VALUE SPACE.
           05  ELEM-XTRCT-STATUS       PIC X(02) VALUE SPACE.
           05  ELEM-LIST-STATUS        PIC X(02) VALUE SPACE.
           05  END-OF-PDSDIR-LINES-SW      PIC X VALUE SPACE.
               88  END-OF-PDSDIR-LINES           VALUE 'Y'.

       01  HOLD-FIELDS.
           05  HOLD-PRINT-LINE         PIC X(133) VALUE SPACE.
           05  SAVE-CAPTURE-DATE       PIC X(10) VALUE SPACE.
           05  SAVE-CAPTURE-JULDATE    PIC X(08) VALUE SPACE.
           05  SAVE-CAPTURE-TIME       PIC X(08) VALUE SPACE.


       01  WK-CONVERSION-FIELDS.
           05  WK-PDS-DIR-DATE.
               10  WK-PDS-DIR-CCYY         PIC 9(04) VALUE ZERO.
               10  WK-PDS-DIR-CCYY-X REDEFINES WK-PDS-DIR-CCYY.
                   15  WK-PDS-DIR-CC       PIC 9(02).
                   15  WK-PDS-DIR-YY       PIC 9(02).
               10  FILLER                  PIC X(01) VALUE '-'.
               10  WK-PDS-DIR-MM           PIC 9(02) VALUE ZERO.
               10  FILLER                  PIC X(01) VALUE '-'.
               10  WK-PDS-DIR-DD           PIC 9(02) VALUE ZERO.

           05  WK-PDS-DIR-JULDATE.
               10  WK-PDSDIR-JUL-CCYY      PIC 9(04) VALUE ZERO.
               10  WK-PDSDIR-JUL-CCYY-X REDEFINES WK-PDSDIR-JUL-CCYY.
                   15  WK-PDSDIR-JUL-CC    PIC 9(02).
                   15  WK-PDSDIR-JUL-YY    PIC 9(02).
               10  FILLER                  PIC X(01) VALUE '-'.
               10  WK-PDSDIR-JUL-DDD       PIC 9(03) VALUE ZERO.

           05  WK-PDS-DIR-TIME.
               10  WK-PDS-DIR-HH           PIC 9(02) VALUE ZERO.
               10  FILLER                  PIC X(01) VALUE ':'.
               10  WK-PDS-DIR-MIN          PIC 9(02) VALUE ZERO.
               10  FILLER                  PIC X(01) VALUE ':'.
               10  WK-PDS-DIR-SS           PIC 9(02) VALUE ZERO.

           05  WK-SUB                  PIC 9(02).
           05  NEW-SUB                 PIC 9(02).
           05  WK-NEW-NUMBER-X.
               10  WK-NEW-NUMBER       PIC 9(09).

           EJECT
       01  DEBUG-HEADING-1.
           05  FILLER                  PIC X     VALUE SPACES.
           05  DBH1-IO-TYPE            PIC X(04) VALUE SPACES.
           05  FILLER                  PIC X     VALUE SPACES.
           05  DBH1-ELEMENT-NAME       PIC X(08) VALUE 'ELEMENT'.
           05  FILLER                  PIC X(01) VALUE SPACES.
           05  DBH1-OUTPUT             PIC X(90) VALUE
               '    INPUT AND OUTPUT DATA '.

       01  DEBUG-HEADING-2.
           05  FILLER                  PIC X     VALUE SPACES.
           05  DBH2-IO-TYPE            PIC X(04) VALUE ' IO'.
           05  FILLER                  PIC X     VALUE SPACES.
           05  DBH2-ELEMENT-NAME       PIC X(08) VALUE '  NAME'.
           05  FILLER                  PIC X(01) VALUE SPACES.
           05  DBH2-OUTPUT             PIC X(115) VALUE SPACES.

       01  DEBUG-DETAIL-LINE-IN.
           05  DEBUG-CC                PIC X(01)  VALUE SPACE.
           05  DEBUG-DATA-IN.
               10  DBI-IO-TYPE         PIC X(02).
               10  FILLER              PIC X(01).
               10  DBI-ELEMENT-NAME    PIC X(08).
               10  FILLER              PIC X(01).
               10  DBI-DATA-REC        PIC X(115).

       01  DISPLAY-TOTAL-LINE.
           05  FILLER                  PIC X(02).
           05  DT-DESC                 PIC X(30).
           05  FILLER                  PIC X(02).
           05  DT-COUNT                PIC ZZZ,ZZZ,ZZ9.
           05  FILLER                  PIC X(05).

           EJECT
      *  COMMON REPORT HEADINGS AND FOOTER
      *  DATE-TIME WORK AREAS
      *  COMMON DEBUG WORK AREAS
      *  COMMON ABEND WORK AREA
           COPY JXCPW020.

           EJECT
      *  DATE CONVERSION WORK AREAS
           COPY JXCPW025.

           EJECT
      *+ INSTALLATION SPECIFIC CONSTANTS
           COPY JXINW005.

           EJECT
       LINKAGE SECTION.
      *  COMMON PROGRAM RUN PARAMETERS
           COPY JXCPL005.

           EJECT
       PROCEDURE DIVISION USING JXL-PARM-DATA.

           PERFORM A1000-INITIALIZATION THRU A1000-EXIT.

           PERFORM C1000-PROCESS-PDS-DIR-FILE
              THRU C1000-EXIT
              UNTIL END-OF-PDSDIR-LINES.

           PERFORM A9000-TERMINATION THRU A9000-EXIT.
           GOBACK.

       A1000-INITIALIZATION.

           DISPLAY '**************************************************'
                  '**************************************************'.
           DISPLAY ' '.
           DISPLAY 'PROGRAM JXC300PD - EXTRACT ELEMENTS FROM '
                   'PDS DIRECTORY LISTING'
           MOVE ' DEBUG REPORT - EXTRACT ELEMENTS FROM PDS DIRECTORY'
                                                 TO JX-DEBUG-TITLE.

           MOVE RULER-LINE(2:115)  TO DBH2-OUTPUT.
           MOVE WS-PROGRAM         TO JXRD-PROGRAM
                                      JXCD-PROGRAM
                                      HD2-PROGRAM.
           MOVE SPACE              TO SOURCE-HISTORY-RECORD.
           MOVE 'CHANGEWIZ  PDS    REL 1.0 VER 1.0' TO FT1-PRODUCT.
           MOVE '(C) 2005 RENAISSANCE DATA SYSTEMS INC.'
                                                    TO FT1-VENDOR.


      **  DISPLAY COMPILE DATE AND TIME
           COPY JXCPP010.

           DISPLAY ' '.

      **  PROCESS COMMON INPUT PARAMETER
           COPY JXCPP015.

      *+  INSTALLATION SPECIFIC INITIALIZATION ROUTINES
           COPY JXINP005.


           OPEN INPUT PDS-DIRECTORY-FILE.
           OPEN OUTPUT ELEMENT-XTRCT-OUT
                       ELEMENT-LIST-OUT.

           IF JX-PRINT-DEBUG
               OPEN OUTPUT DEBUG-REPORT.

           IF PDS-DIR-STATUS NOT = '00' AND '97'
               DISPLAY '   BAD OPEN ON PDS DIR FILE. STATUS = '
                       PDS-DIR-STATUS
               MOVE 'Y' TO END-OF-PDSDIR-LINES-SW
               MOVE 300 TO JX-ABEND-CODE
               MOVE 'A1000-INITIALIZATION'  TO JX-ABEND-PARA
               MOVE 'BAD OPEN ON PDS DIR FILE' TO JX-ABEND-REASON-1
               PERFORM Z9000-ABEND-PROGRAM THRU Z9000-EXIT
           ELSE
               PERFORM X1000-READ-PDS-DIR THRU X1000-EXIT.

      * CHECK STATUS OF OUTPUT FILE OPENS
           IF ELEM-XTRCT-STATUS NOT = '00' AND '97'
               DISPLAY '   BAD OPEN ON ELEMENT XTRCT FILE. STATUS = '
                       ELEM-XTRCT-STATUS
               MOVE 301 TO JX-ABEND-CODE
               MOVE 'A1000-INITIALIZATION'  TO JX-ABEND-PARA
               MOVE 'BAD OPEN ON ELEMENT XTRCT FILE' TO
                                                  JX-ABEND-REASON-1
               PERFORM Z9000-ABEND-PROGRAM THRU Z9000-EXIT
           END-IF.

           IF ELEM-LIST-STATUS NOT = '00' AND '97'
               DISPLAY '   BAD OPEN ON ELEMENT LIST FILE. STATUS = '
                       ELEM-LIST-STATUS
               MOVE 302 TO JX-ABEND-CODE
               MOVE 'A1000-INITIALIZATION'  TO JX-ABEND-PARA
               MOVE 'BAD OPEN ON ELEMENT LIST FILE' TO
                                                  JX-ABEND-REASON-1
               PERFORM Z9000-ABEND-PROGRAM THRU Z9000-EXIT
           END-IF.

           MOVE '1900-99-99'      TO WK-PDS-DIR-DATE
           MOVE '00:00:00'        TO WK-PDS-DIR-TIME
           MOVE WS-CDF-CCYY       TO WK-PDS-DIR-CCYY
           MOVE WS-CDF-CC         TO JX-CGREG-CC
           MOVE WS-CDF-YY         TO JX-CGREG-YY
           MOVE WS-CDF-MM         TO WK-PDS-DIR-MM
                                     JX-CGREG-MM
           MOVE WS-CDF-DD         TO WK-PDS-DIR-DD
                                     JX-CGREG-DD
           MOVE WS-CDF-HR         TO WK-PDS-DIR-HH
           MOVE WS-CDF-MIN        TO WK-PDS-DIR-MIN
           MOVE WS-CDF-SEC        TO WK-PDS-DIR-SS.

           MOVE SPACE             TO WK-PDS-DIR-JULDATE

           PERFORM W5100-CONVERT-GREG-TO-JULIAN THRU W5100-EXIT.

           IF JXCDE-RETURN-CD = 0
               MOVE '0000-000'           TO WK-PDS-DIR-JULDATE
               MOVE JX-CJUL-CC           TO WK-PDSDIR-JUL-CC
               MOVE JX-CJUL-YY           TO WK-PDSDIR-JUL-YY
               MOVE JX-CJUL-DDD          TO WK-PDSDIR-JUL-DDD
           ELSE
               DISPLAY 'INVALID CURRENT DATE'
               DISPLAY JX-CONV-DATE-ERROR
               MOVE SPACE                TO WK-PDS-DIR-JULDATE
               MOVE 'Y' TO END-OF-PDSDIR-LINES-SW
               MOVE 303 TO JX-ABEND-CODE
               MOVE 'A1000-INITIALIZATION'  TO JX-ABEND-PARA
               MOVE 'INVALID CURRENT DATE ' TO
                                                  JX-ABEND-REASON-1
               PERFORM Z9000-ABEND-PROGRAM THRU Z9000-EXIT
           END-IF.

           MOVE WK-PDS-DIR-DATE       TO SAVE-CAPTURE-DATE.
           MOVE WK-PDS-DIR-JULDATE    TO SAVE-CAPTURE-JULDATE.
           MOVE WK-PDS-DIR-TIME       TO SAVE-CAPTURE-TIME.

           IF END-OF-PDSDIR-LINES
               DISPLAY 'NO RECORDS IN PDS DIRECTORY FILE'
               MOVE 304 TO JX-ABEND-CODE
               MOVE 'A1000-INITIALIZATION'  TO JX-ABEND-PARA
               MOVE 'NO RECORDS IN PDS DIRECTORY FILE ' TO
                                                  JX-ABEND-REASON-1
               PERFORM Z9000-ABEND-PROGRAM THRU Z9000-EXIT
           END-IF.

      *+  INSTALLATION-SPECIFIC CODE TO DETERMINE DATE CUTOFF
           COPY JXINP010.

      * GET LIBRARY NAME FROM INPUT CARD
           PERFORM A1100-GET-LIB-NAME THRU A1100-EXIT.

       A1000-EXIT.
           EXIT.

      ***********************************************************
      *   GET LIBRARY NAME FROM INPUT CARD
      *   INPUT CARD FORMAT IS:
      *      ----+----1----+----2
      *      JCLLIB
      *      PROCLIB
      *
      ***********************************************************
       A1100-GET-LIB-NAME.
           OPEN INPUT LIB-NAME-FILE.

           IF LIB-NAME-STATUS NOT = '00' AND '97'
               DISPLAY '   BAD OPEN ON LIBNAME FILE. STATUS = '
                       LIB-NAME-STATUS
               MOVE 310 TO JX-ABEND-CODE
               MOVE 'A1100-GET-LIB-NAME  '  TO JX-ABEND-PARA
               MOVE 'BAD OPEN ON LIBNAME FILE ' TO
                                                  JX-ABEND-REASON-1
               PERFORM Z9000-ABEND-PROGRAM THRU Z9000-EXIT
           END-IF.

           READ LIB-NAME-FILE INTO WS-LIB-NAME-RECORD.

           IF LIB-NAME-STATUS  = '10'
               DISPLAY '   NO DATA IN LIBNAME FILE.'
               MOVE 311 TO JX-ABEND-CODE
               MOVE 'A1100-GET-LIB-NAME  '  TO JX-ABEND-PARA
               MOVE 'NO DATA IN LIBNAME FILE ' TO
                                                  JX-ABEND-REASON-1
               PERFORM Z9000-ABEND-PROGRAM THRU Z9000-EXIT
           END-IF.

           IF WS-LIB-NAME = SPACE OR WS-LIB-LANGUAGE = SPACE
              DISPLAY 'INVALID LIB-NAME. CHANGE CARD AND RERUN'
              DISPLAY 'FORMAT FOR INPUT CARD IS: '
              DISPLAY '    ----+----1----+----2----+----3'
              DISPLAY '    XXXXXXXX XXXXXXXX XXXXXXXX '
              DISPLAY '        POS 1-8    IS LIBRARY NAME'
              DISPLAY '        POS 10-17  IS LANGUAGE OR TYPE'
              DISPLAY '        POS 10-17  IS LANGUAGE OR TYPE'
              DISPLAY '        POS 19-26  IS RUNTYPE AND OPTIONAL'
              DISPLAY '                      USE RUNTYPE ONLY FOR '
                      'FIRST TIME A PDS IS PROCESSED. '
              DISPLAY '        POS 28-37  IS DEFAULT DATE FOR '
                      'ELEMENTS WITH NO STATS - FOR FIRST OPTION ONLY'
              DISPLAY '        POS 39-46  IS DEFAULT JULIAN DATE FOR '
                      'ELEMENTS WITH NO STATS - FOR FIRST OPTION ONLY'
              DISPLAY ' EXAMPLE:     '
              DISPLAY '    ----+----1----+----2----+----3----+---'
                      '-4----+----5'
              DISPLAY '    JCLLIB   JCL          '
              DISPLAY '    PROCLIB  PROC     FIRST    2003-01-01 '
                      '2003-001 '
              DISPLAY '     '
              MOVE 312 TO JX-ABEND-CODE
              MOVE 'A1100-GET-LIB-NAME  '  TO JX-ABEND-PARA
              MOVE 'INVALID LIB CARD ' TO
                                                 JX-ABEND-REASON-1
              PERFORM Z9000-ABEND-PROGRAM THRU Z9000-EXIT
           ELSE
              DISPLAY 'READING PDS DIRECTORY FOR LIBRARY: '
              WS-LIB-NAME  '  AND LANGUAGE: ' WS-LIB-LANGUAGE
              IF WS-LIB-RUNTYPE = 'FIRST'
                 IF WS-LIB-DEFAULT-DATE = SPACE OR
                    WS-LIB-DEFAULT-JUL = SPACE
                    MOVE SAVE-CAPTURE-DATE    TO WS-LIB-DEFAULT-DATE
                    MOVE SAVE-CAPTURE-JULDATE TO WS-LIB-DEFAULT-JUL
                 END-IF
                 DISPLAY 'THIS IS FIRST TIME PDS IS PROCESSED. '
                         'PROMOTE DATE/TIME WILL BE SET TO '
                         'EDIT DATE/TIME'
                 DISPLAY 'MEMBERS WITH NO STATISTICS WILL BE '
                         'GIVEN A DATE OF ' WS-LIB-DEFAULT-DATE
                         '  ' WS-LIB-DEFAULT-JUL
              END-IF
           END-IF.


           CLOSE LIB-NAME-FILE.

       A1100-EXIT.
           EXIT.

      ***********************************************************
      *   END OF JOB PROCESSING
      ***********************************************************
       A9000-TERMINATION.

           CLOSE PDS-DIRECTORY-FILE
                 ELEMENT-XTRCT-OUT
                 ELEMENT-LIST-OUT.

      *  ENABLE TOTALS TO PRINT ON DEBUG REPORT
           IF JX-PRINT-DEBUG OR DEBUG-PAGE-CNT > 0
              ADD 15 TO JX-PRINT-DEBUG-MAX
              IF NOT JX-PRINT-DEBUG
                 MOVE 'Y' TO JX-DEBUG-SW
              END-IF
              ADD 15 TO DEBUG-RPT-LINE-CNT
              IF DEBUG-RPT-LINE-CNT > INST-LINES-PER-PAGE
                 SUBTRACT 15 FROM DEBUG-RPT-LINE-CNT
                 PERFORM Y1025-DEBUG-HEADING THRU Y1025-EXIT
              ELSE
                 SUBTRACT 15 FROM DEBUG-RPT-LINE-CNT
                 MOVE SPACES        TO DEBUG-RPT-LINE
                 PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
                 PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
              END-IF
           END-IF.

           DISPLAY ' '.
           DISPLAY ' '.
           DISPLAY '****************************************'
                   '****************************************'
           DISPLAY ' '.
           DISPLAY 'END OF PROGRAM JXC300PD.'.
           DISPLAY ' '.


           IF JX-PRINT-DEBUG
               MOVE ALL '*'       TO DEBUG-RPT-LINE(2:125)
               PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
               MOVE SPACES        TO DEBUG-RPT-LINE
               PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
               MOVE ' END-OF-PROGRAM JXC300PD RECORD COUNTS' TO
                                     DEBUG-RPT-LINE
               PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
               MOVE SPACES        TO DEBUG-RPT-LINE
               PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
           END-IF.


           MOVE SPACE TO DISPLAY-TOTAL-LINE.

           MOVE 'PDS DIRECTORY RECORDS READ  ' TO DT-DESC.
           MOVE READ-PDS-DIR-CNT               TO DT-COUNT
           DISPLAY DISPLAY-TOTAL-LINE
           IF JX-PRINT-DEBUG
              MOVE DISPLAY-TOTAL-LINE TO DEBUG-RPT-LINE
              PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
           END-IF.

           MOVE 'EXTRACT RECORDS WRITTEN     ' TO DT-DESC
           MOVE WRITE-EXTRACT-CNT              TO DT-COUNT
           DISPLAY DISPLAY-TOTAL-LINE
           IF JX-PRINT-DEBUG
              MOVE DISPLAY-TOTAL-LINE TO DEBUG-RPT-LINE
              PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
           END-IF.

           MOVE 'ELEMENT LIST RECS WRITTEN ' TO DT-DESC
           MOVE WRITE-ELEMENT-CNT            TO DT-COUNT
           DISPLAY DISPLAY-TOTAL-LINE
           DISPLAY '   '
           IF JX-PRINT-DEBUG
              MOVE DISPLAY-TOTAL-LINE TO DEBUG-RPT-LINE
              PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
              MOVE SPACE              TO DEBUG-RPT-LINE
              PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
           END-IF.

           DISPLAY ' '.
           DISPLAY '****************************************'
                   '****************************************'
           DISPLAY ' '.

           IF JX-PRINT-DEBUG
               MOVE SPACES        TO DEBUG-RPT-LINE
               PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
               MOVE ALL '*'       TO DEBUG-RPT-LINE(2:125)
               PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
               MOVE SPACES        TO DEBUG-RPT-LINE
               PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
           END-IF.


      **  FINISH DEBUG REPORT
           IF DEBUG-PAGE-CNT > 0
               PERFORM Y1050-DEBUG-FOOTER THRU Y1050-EXIT.

           IF JX-PRINT-DEBUG OR DEBUG-PAGE-CNT > 0
               CLOSE DEBUG-REPORT.

      *  ABEND IF NO PDS MEMBERS WERE WRITTEN OUT
           IF WRITE-EXTRACT-CNT = ZERO
              MOVE 900 TO JX-ABEND-CODE
              MOVE 'A9000-TERMINATION   '  TO JX-ABEND-PARA
              MOVE 'NO MEMBERS IN PDS LIBRARY FILE'  TO
                                                 JX-ABEND-REASON-1
              MOVE 'CHECK VOLSER FOR PDS LIBRARY  '  TO
                                                 JX-ABEND-REASON-2
              PERFORM Z9000-ABEND-PROGRAM THRU Z9000-EXIT
           END-IF.

       A9000-EXIT.
           EXIT.
           EJECT
      ************************************************************
      *  WRITE OUT AT LEAST ONE RECORD FOR EACH ELEMENT.
      *  WRITE A RECORD FOR EACH CHANGE CODE
      ************************************************************
       C1000-PROCESS-PDS-DIR-FILE.

           IF DR-MEMBER-NAME = SPACE OR ' MEMBER' OR '  NAME'
                   OR 'REFWIZ  ' OR 'CHANGEWI'
              GO TO C1000-READ-NEXT
           END-IF.

           IF DR-TTR         = SPACE OR 'TOTAL '
              GO TO C1000-READ-NEXT
           END-IF.

      * WRITE OUT ONE LIST RECORD FOR EACH INPUT. THIS IS USED
      * TO IDENTIFY DELETED ELEMENTS

           MOVE SPACE TO ELEMENT-LIST-REC
           MOVE DR-MEMBER-NAME       TO LST-ELEMENT-NAME
           MOVE WS-LIB-NAME          TO LST-ELEMENT-LIB
           PERFORM X4000-WRITE-LIST THRU X4000-EXIT.

      * WRITE OUT ALL ELEMENTS TO EXTRACT FILE

           MOVE SPACE TO SOURCE-HISTORY-RECORD.
           MOVE '0000-00-00'         TO SH-EDIT-DATE-CYMD
           MOVE '0000-000'           TO SH-EDIT-DATE-JUL
           MOVE '00:00:00'           TO SH-EDIT-TIME
           MOVE '0000-00-00'         TO SH-CREATE-DATE-CYMD
           MOVE '0000-00-00'         TO SH-PROMOTE-DATE-CYMD
           MOVE '0000-000'           TO SH-PROMOTE-DATE-JUL
           MOVE '00:00:00'           TO SH-PROMOTE-TIME
           MOVE SPACE                TO SH-LAST-PROMOTE-BY
           MOVE SPACE                TO SH-CHANGE-CODE

           MOVE DR-MEMBER-NAME       TO SH-ELEMENT-NAME
           MOVE WS-LIB-NAME          TO SH-ELEMENT-LIB
           MOVE WS-LIB-LANGUAGE      TO SH-ELEMENT-LANG
           MOVE ZERO                 TO SH-NUMBER-LINES

           IF WS-LIB-RUNTYPE = 'FIRST'
              MOVE 1                 TO SH-ELEMENT-LEVEL
           ELSE
              MOVE ZERO              TO SH-ELEMENT-LEVEL
           END-IF.

      * SOME MEMBERS HAVE NO SPF STATISTICS
           IF DR-VER-MOD = SPACE
              GO TO C1000-WRITE-EXTRACT
           END-IF.

           MOVE DR-VER-MOD           TO SH-PAN-STATUS

      *******************************************************
      *  CONVERT DISPLAY NUMBER OF LINES TO A NUMERIC VALUE
      *  CHANGE SPACES TO ZERO AND REMOVE COMMAS
      *******************************************************
           MOVE ZERO                 TO SH-NUMBER-LINES.
           MOVE ZERO TO WK-NEW-NUMBER.

           IF DR-CURRENT-LINES NOT = SPACE
              MOVE 9 TO NEW-SUB
              PERFORM VARYING WK-SUB FROM 9 BY -1
                      UNTIL WK-SUB < 1 OR
                            DR-CURRENT-LINES(WK-SUB:1) = SPACE
                 IF DR-CURRENT-LINES(WK-SUB:1) NOT = ','
                    MOVE DR-CURRENT-LINES(WK-SUB:1) TO
                                          WK-NEW-NUMBER-X(NEW-SUB:1)
                    SUBTRACT 1 FROM NEW-SUB
                 END-IF
              END-PERFORM
              MOVE WK-NEW-NUMBER     TO SH-NUMBER-LINES
           END-IF.


           IF DR-CREATE-DATE NOT = SPACE
              MOVE DR-CREATE-DATE    TO SH-CREATE-DATE-CYMD
              MOVE '-'               TO SH-CREATE-GREG-H1
                                        SH-CREATE-GREG-H2
           END-IF.

           MOVE DR-CHANGE-TIME       TO SH-EDIT-TIME.
           MOVE DR-USER-ID           TO SH-LAST-EDIT-BY.

      *******************************************************
      * CONVERT TO GREGORIAN FORMAT
      *******************************************************

           IF DR-CHANGE-DATE = SPACE
              GO TO C1000-WRITE-EXTRACT
           END-IF.

           MOVE DR-CHANGE-DATE       TO SH-EDIT-DATE-CYMD
                                        WK-PDS-DIR-DATE

           MOVE '-'                  TO SH-EDIT-GREG-H1
                                        SH-EDIT-GREG-H2

           MOVE WK-PDS-DIR-CC        TO JX-CGREG-CC
           MOVE WK-PDS-DIR-YY        TO JX-CGREG-YY
           MOVE WK-PDS-DIR-MM        TO JX-CGREG-MM
           MOVE WK-PDS-DIR-DD        TO JX-CGREG-DD

           PERFORM W1000-FORMAT-IBM-DATE
              THRU W1000-EXIT.

           MOVE WK-PDS-DIR-JULDATE   TO SH-EDIT-DATE-JUL.

       C1000-WRITE-EXTRACT.

           MOVE SAVE-CAPTURE-DATE     TO SH-CAPTURE-DATE-CYMD
           MOVE SAVE-CAPTURE-JULDATE  TO SH-CAPTURE-DATE-JUL
           MOVE SAVE-CAPTURE-TIME     TO SH-CAPTURE-TIME

           IF WS-LIB-RUNTYPE NOT = 'FIRST'
              MOVE SH-CAPTURE-DATE-CYMD TO SH-PROMOTE-DATE-CYMD
              MOVE SH-CAPTURE-DATE-JUL  TO SH-PROMOTE-DATE-JUL
              MOVE SH-CAPTURE-TIME      TO SH-PROMOTE-TIME
           END-IF.

      *  MEMBER HAS ISPF STATISTCS
           IF WS-LIB-RUNTYPE = 'FIRST'
              IF DR-CHANGE-DATE NOT = SPACE
                 MOVE SH-EDIT-DATE-CYMD    TO SH-PROMOTE-DATE-CYMD
                 MOVE SH-EDIT-DATE-JUL     TO SH-PROMOTE-DATE-JUL
                 MOVE SH-EDIT-TIME         TO SH-PROMOTE-TIME
                 MOVE SH-EDIT-DATE-CYMD    TO SH-CAPTURE-DATE-CYMD
                 MOVE SH-EDIT-DATE-JUL     TO SH-CAPTURE-DATE-JUL
                 MOVE SH-EDIT-TIME         TO SH-CAPTURE-TIME
              ELSE
                 MOVE WS-LIB-DEFAULT-DATE  TO SH-PROMOTE-DATE-CYMD
                 MOVE WS-LIB-DEFAULT-JUL   TO SH-PROMOTE-DATE-JUL
                 MOVE SH-CAPTURE-TIME      TO SH-PROMOTE-TIME
                 MOVE WS-LIB-DEFAULT-DATE  TO SH-CAPTURE-DATE-CYMD
                 MOVE WS-LIB-DEFAULT-JUL   TO SH-CAPTURE-DATE-JUL
              END-IF
           END-IF.


           PERFORM X3000-WRITE-ELEMENT THRU X3000-EXIT.

       C1000-READ-NEXT.
           PERFORM X1000-READ-PDS-DIR THRU X1000-EXIT.

       C1000-EXIT.
           EXIT.


           EJECT
      ***************************************************************
      *  FORMAT DATES - GET JULIAN DATE
      ***************************************************************
       W1000-FORMAT-IBM-DATE.

           MOVE ZERO                   TO JX-CONV-JUL-DATE
           MOVE '0000-000'             TO WK-PDS-DIR-JULDATE

           IF JX-CONV-GREG-DATE = ZERO
              GO TO W1000-EXIT
           END-IF.

           PERFORM W5100-CONVERT-GREG-TO-JULIAN
              THRU W5100-EXIT.

           IF JXCDE-RETURN-CD = 0
               MOVE JX-CJUL-CC           TO WK-PDSDIR-JUL-CC
               MOVE JX-CJUL-YY           TO WK-PDSDIR-JUL-YY
               MOVE JX-CJUL-DDD          TO WK-PDSDIR-JUL-DDD
           ELSE
               DISPLAY 'W1000- INVALID DATE FOR ELEMENT '
                       DR-MEMBER-NAME
               DISPLAY JX-CONV-DATE-ERROR
               GO TO W1000-EXIT
           END-IF.


       W1000-EXIT.
           EXIT.

           EJECT
      *  COMMON DATE CONVERSION ROUTINES
           COPY JXCPP025.

           EJECT
      ***************************************************************
      *  READ PDS-DIR FILE
      ***************************************************************
       X1000-READ-PDS-DIR.

           READ PDS-DIRECTORY-FILE INTO DIR-DETAIL-LINE.

           IF PDS-DIR-STATUS = '10'
               MOVE 'Y' TO END-OF-PDSDIR-LINES-SW
               GO TO X1000-EXIT.

           ADD 1           TO READ-PDS-DIR-CNT.
           IF PDS-DIR-STATUS NOT = '00'
               DISPLAY '  BAD READ ON PDS-DIR FILE. STATUS = '
                   PDS-DIR-STATUS ' ON RECORD NBR ' READ-PDS-DIR-CNT
               GO TO X1000-EXIT.


           IF JX-PRINT-DEBUG
               MOVE SPACES               TO DEBUG-DETAIL-LINE-IN
               MOVE DR-MEMBER-NAME       TO DBI-ELEMENT-NAME
               MOVE ' I'                 TO DBI-IO-TYPE
               MOVE DIR-DETAIL-LINE      TO DBI-DATA-REC
               MOVE DEBUG-DETAIL-LINE-IN TO DEBUG-RPT-LINE
               PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
           END-IF.

       X1000-EXIT.
           EXIT.

      ***************************************************************
      *  WRITE AN ELEMENT EXTRACT RECORD.
      ***************************************************************
       X3000-WRITE-ELEMENT.

           WRITE ELEMENT-XTRCT-REC FROM SOURCE-HISTORY-RECORD.
           ADD 1 TO WRITE-EXTRACT-CNT.

           IF JX-PRINT-DEBUG
               MOVE SPACES                  TO DEBUG-DETAIL-LINE-IN
               MOVE ' O'                    TO DBI-IO-TYPE
               MOVE SOURCE-HISTORY-RECORD   TO DBI-DATA-REC
               MOVE DEBUG-DETAIL-LINE-IN    TO DEBUG-RPT-LINE
               PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
               MOVE SPACES               TO DEBUG-RPT-LINE
               PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
           END-IF.

       X3000-EXIT.
           EXIT.

      ***************************************************************
      *  WRITE AN ELEMENT LIST RECORD.
      ***************************************************************
       X4000-WRITE-LIST.

           WRITE ELEMENT-LIST-REC.
           ADD 1 TO WRITE-ELEMENT-CNT.


       X4000-EXIT.
           EXIT.

           EJECT
      ***************************************************************
      ***  PRINT ROUTINES
      ***************************************************************

       Y1000-PRINT-DEBUG.
           ADD 1 TO JX-PRINT-DEBUG-CNT.
           IF JX-PRINT-DEBUG-CNT > JX-PRINT-DEBUG-SKIP
               IF DEBUG-RPT-LINE-CNT > INST-LINES-PER-PAGE
                   MOVE DEBUG-RPT-LINE TO HOLD-PRINT-LINE
                   PERFORM Y1025-DEBUG-HEADING THRU Y1025-EXIT
                   MOVE HOLD-PRINT-LINE TO DEBUG-RPT-LINE.

           IF JX-PRINT-DEBUG-CNT > JX-PRINT-DEBUG-SKIP
               WRITE DEBUG-RPT-LINE
                   AFTER ADVANCING 1 LINE
               ADD 1 TO DEBUG-RPT-LINE-CNT.

           IF JX-PRINT-DEBUG-CNT > JX-PRINT-DEBUG-MAX
               MOVE 'N' TO JX-DEBUG-SW.
           MOVE SPACES TO DEBUG-RPT-LINE.
       Y1000-EXIT.
           EXIT.

       Y1025-DEBUG-HEADING.
           ADD 1                   TO DEBUG-PAGE-CNT.
           MOVE DEBUG-PAGE-CNT     TO HD1-PAGE.
           MOVE JX-DEBUG-TITLE     TO HD2-TITLE.
           MOVE 'DB'               TO HD2-REPORT.
           MOVE '            CHANGEWIZ DEBUGGING REPORT           '
                                   TO HD1-COMPANY-NAME

           IF DEBUG-PAGE-CNT > 1
               PERFORM Y1050-DEBUG-FOOTER THRU Y1050-EXIT.

           WRITE DEBUG-RPT-LINE FROM HEADING-1
               AFTER ADVANCING PAGE.
           WRITE DEBUG-RPT-LINE FROM HEADING-2
               AFTER ADVANCING 1 LINE.
           WRITE DEBUG-RPT-LINE FROM DEBUG-HEADING-1
               AFTER ADVANCING 2 LINE.
           WRITE DEBUG-RPT-LINE FROM DEBUG-HEADING-2
               AFTER ADVANCING 1 LINE.
           MOVE SPACES TO DEBUG-RPT-LINE
           WRITE DEBUG-RPT-LINE
               AFTER ADVANCING 1 LINE.
           MOVE 6 TO DEBUG-RPT-LINE-CNT.
       Y1025-EXIT.
           EXIT.

       Y1050-DEBUG-FOOTER.
           MOVE SPACES TO DEBUG-RPT-LINE.

           PERFORM UNTIL DEBUG-RPT-LINE-CNT > INST-LINES-PER-PAGE
               WRITE DEBUG-RPT-LINE
                   AFTER ADVANCING 1 LINE
               ADD 1 TO DEBUG-RPT-LINE-CNT
           END-PERFORM.

           WRITE DEBUG-RPT-LINE
               AFTER ADVANCING 2 LINE.
           WRITE DEBUG-RPT-LINE FROM FOOTER-1
               AFTER ADVANCING 1 LINE.

       Y1050-EXIT.
           EXIT.

           EJECT
      *  COMMON ABEND ROUTINE
      * Z9000-ABEND-PROGRAM.
           COPY JXCPP099.


