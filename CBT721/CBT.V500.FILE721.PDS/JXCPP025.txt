      *************************************************************
      *     COPYBOOK JXCPP025
      *JX*  DATE CONVERSION ROUTINES
      *     USE WITH COPYBOOK JXCPW025
      *
      *     W5100-CONVERT-GREG-TO-JULIAN
      *     W5200-CONVERT-JULIAN-TO-GREG
      *
      *     USED BY PROGRAMS JXCU003
      *                      JXCU011
      *                      JXC125SM
      *                      JXC300PD
      *                      JXC300SC
      *                      JXC400TV
      *
      *************************************************************
      *************************************************************
      *    CONVERT GREGORIAN DATE  YYMMDD OR CCYYMMDD TO JULIAN
      *    INPUT  JX-CONV-GREG-DATE
      *    OUTPUT JX-CONV-JUL-DATE
      *
      *    MOVE CCYYMMDD OR YYMMDD TO JX-CONV-GREG-DATE
      *    PERFORM W5100-CONVERT-GREG-TO-JULIAN THRU W5100-EXIT.
      *    IF JXCDE-RETURN-CD = 0
      *        MOVE JX-CONV-JUL-DATE    TO CCYYDDD
      *    ELSE
      *        DISPLAY JX-CONV-DATE-ERROR.
      *
      *  THIS ROUTINE WILL DETERMINE IF DATE BEING READ IS
      *    CCYYMMDD OR YYMMDD AND CALC CENTURY IF NECESSARY.
      *
      *  IF A 6 DIGIT LEFT JUSTIFIED DATE IS PASSED, IT WILL BE
      *  RIGHT JUSTIFIED AND CENTURY WILL BE CALCULATED.
      *************************************************************
       W5100-CONVERT-GREG-TO-JULIAN.

           MOVE ZERO                   TO JX-CONV-JUL-DATE.
           MOVE ZERO                   TO JXCDE-RETURN-CD.
           MOVE SPACES                 TO JXCDE-RETURN-REASON.
           MOVE JX-CONV-JUL-DATE2      TO JXCDE-JUL-DATE

           IF JX-CGREG-DATE-6-DIGIT-ALPHA
               MOVE JX-CGREG-MM        TO JX-CGREG-DD
               MOVE JX-CGREG-YY        TO JX-CGREG-MM
               MOVE JX-CGREG-CC        TO JX-CGREG-YY
               IF JX-CGREG-YY > JX-CENTURY-BREAK
                   MOVE 19             TO JX-CGREG-CC
               ELSE
                   MOVE 20             TO JX-CGREG-CC
               END-IF.

           MOVE JX-CONV-GREG-DATE2     TO JXCDE-GREG-DATE.

           IF JX-CONV-GREG-DATE NOT NUMERIC
               MOVE 1                       TO JXCDE-RETURN-CD
               MOVE 'GREGORIAN DATE IS NOT NUMERIC      ' TO
                                               JXCDE-RETURN-REASON
               GO TO W5100-EXIT.

           IF JX-CONV-GREG-DATE = ZERO
               MOVE 2                       TO JXCDE-RETURN-CD
               MOVE 'GREGORIAN DATE = ZERO              ' TO
                                               JXCDE-RETURN-REASON
               GO TO W5100-EXIT.

           IF JX-CGREG-MM = 0 OR
              JX-CGREG-MM > 12
               MOVE 3                       TO JXCDE-RETURN-CD
               MOVE 'INVALID MONTH IS > 12 OR ZERO      ' TO
                                               JXCDE-RETURN-REASON
               GO TO W5100-EXIT.

           IF JX-CGREG-DD = 0
               MOVE 4                       TO JXCDE-RETURN-CD
               MOVE 'INVALID DAY = 0                    ' TO
                                               JXCDE-RETURN-REASON
               GO TO W5100-EXIT.


           IF JX-CGREG-DATE-6-DIGIT-NUM
               IF JX-CGREG-YY > JX-CENTURY-BREAK
                   MOVE 19              TO JX-CGREG-CC
               ELSE
                   MOVE 20              TO JX-CGREG-CC
               END-IF.

           IF JX-CGREG-YY = 0
               COMPUTE JX-LEAP-YR-DIV = JX-CGREG-CC / 4
           ELSE
               COMPUTE JX-LEAP-YR-DIV = JX-CGREG-YY / 4.

           IF JX-LEAP-YEAR
              IF JX-CGREG-DD > JX-LEAP-CAL-DAYS(JX-CGREG-MM)
                 MOVE 5                       TO JXCDE-RETURN-CD
                 MOVE 'INVALID NBR OF DAYS IN MONTH - LEAP' TO
                                               JXCDE-RETURN-REASON
                 GO TO W5100-EXIT
               END-IF
           ELSE
           IF JX-CGREG-DD > JX-NORMAL-CAL-DAYS(JX-CGREG-MM)
               MOVE 6                       TO JXCDE-RETURN-CD
               MOVE 'INVALID NBR OF DAYS IN MONTH       ' TO
                                               JXCDE-RETURN-REASON
               GO TO W5100-EXIT.

           MOVE JX-CGREG-CC             TO JX-CJUL-CC.
           MOVE JX-CGREG-YY             TO JX-CJUL-YY.
           MOVE JX-CGREG-DD             TO JX-CJUL-DDD.

           ADD JX-NORMAL-DAYS-TO-ADD(JX-CGREG-MM) TO JX-CJUL-DDD.
           IF JX-LEAP-YEAR AND JX-CGREG-MM > 2
               ADD 1 TO JX-CJUL-DDD.

       W5100-EXIT.
           EXIT.
           EJECT
      *************************************************************
      *    CONVERT JULIAN  CCYYDDD OR YYDDD  TO GREGORIAN CCYYMMDD
      *    INPUT  JX-CONV-JUL-DATE
      *    OUTPUT JX-CONV-GREG-DATE
      *
      *    MOVE CCYYDDD OR YYDDD TO JX-CONV-JUL-DATE
      *    PERFORM W5200-CONVERT-JULIAN-TO-GREG THRU W5200-EXIT.
      *    IF JXCDE-RETURN-CD = 0
      *        MOVE JX-CONV-GREG-DATE   TO CCYYMMDD
      *    ELSE
      *        DISPLAY JX-CONV-DATE-ERROR.
      *
      *  THIS ROUTINE WILL DETERMINE IF DATE BEING READ HAS CENTURY
      *     AND CALC CENTURY IF NECESSARY.
      *
      *  IF A 5 DIGIT LEFT JUSTIFIED DATE IS PASSED, IT WILL BE
      *  RIGHT JUSTIFIED AND CENTURY WILL BE CALCULATED.
      *************************************************************
       W5200-CONVERT-JULIAN-TO-GREG.
           MOVE ZERO                  TO JX-CONV-GREG-DATE.
           MOVE ZERO                  TO JXCDE-RETURN-CD.
           MOVE SPACES                TO JXCDE-RETURN-REASON.
           MOVE JX-CONV-JUL-DATE2     TO JXCDE-JUL-DATE
           MOVE JX-CONV-GREG-DATE2    TO JXCDE-GREG-DATE.

           IF JX-CJUL-DATE-5-DIGIT-ALPHA AND
                  JX-CJUL-ALPHA-YY  NUMERIC AND
                  JX-CJUL-ALPHA-DDD NUMERIC
                   MOVE JX-CJUL-ALPHA-DDD TO JX-MTH-SUB
                   MOVE JX-CJUL-ALPHA-YY  TO JX-CJUL-YY
                   MOVE ZERO              TO JX-CJUL-CC
                   MOVE JX-MTH-SUB        TO JX-CJUL-DDD.

           IF JX-CONV-JUL-DATE NOT NUMERIC
               MOVE 11                      TO JXCDE-RETURN-CD
               MOVE 'JULIAN DATE IS NOT NUMERIC         ' TO
                                               JXCDE-RETURN-REASON
               GO TO W5200-EXIT.

           IF JX-CJUL-DDD > 366
               MOVE 12                      TO JXCDE-RETURN-CD
               MOVE 'JULIAN DATE HAS > 366 DAYS         ' TO
                                               JXCDE-RETURN-REASON
               GO TO W5200-EXIT.

           IF JX-CJUL-DDD = 0
               MOVE 13                      TO JXCDE-RETURN-CD
               MOVE 'JULIAN DATE HAS >   0 DAYS         ' TO
                                               JXCDE-RETURN-REASON
               GO TO W5200-EXIT.

           IF JX-CJUL-DATE-5-DIGIT-NUM
               IF JX-CJUL-YY > JX-CENTURY-BREAK
                   MOVE 19              TO JX-CJUL-CC
               ELSE
                   MOVE 20              TO JX-CJUL-CC
               END-IF.

           MOVE JX-CJUL-CC              TO JX-CGREG-CC.
           MOVE JX-CJUL-YY              TO JX-CGREG-YY.

           IF JX-CJUL-YY = 0
               COMPUTE JX-LEAP-YR-DIV = JX-CJUL-CC / 4
           ELSE
               COMPUTE JX-LEAP-YR-DIV = JX-CJUL-YY / 4.

           MOVE 12 TO JX-MTH-SUB.
           IF JX-LEAP-YEAR
               PERFORM VARYING JX-MTH-SUB FROM 12 BY -1
                       UNTIL JX-MTH-SUB < 1
                   IF JX-LEAP-DAYS-TO-ADD(JX-MTH-SUB) < JX-CJUL-DDD
                       COMPUTE JX-CGREG-DD = JX-CJUL-DDD -
                                    JX-LEAP-DAYS-TO-ADD(JX-MTH-SUB)
                       MOVE JX-MTH-SUB TO JX-CGREG-MM
                       MOVE 1 TO JX-MTH-SUB
                  END-IF
               END-PERFORM
           ELSE
               PERFORM VARYING JX-MTH-SUB FROM 12 BY -1
                       UNTIL JX-MTH-SUB < 1
                   IF JX-NORMAL-DAYS-TO-ADD(JX-MTH-SUB) < JX-CJUL-DDD
                       COMPUTE JX-CGREG-DD = JX-CJUL-DDD -
                                    JX-NORMAL-DAYS-TO-ADD(JX-MTH-SUB)
                       MOVE JX-MTH-SUB TO JX-CGREG-MM
                       MOVE 1 TO JX-MTH-SUB
                  END-IF
               END-PERFORM.

       W5200-EXIT.
           EXIT.

      ****************************************************************
      *    END OF COPYBOOK                                           *
      ****************************************************************
