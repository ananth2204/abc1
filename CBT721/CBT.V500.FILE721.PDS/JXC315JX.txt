       PROCESS NOADV
       IDENTIFICATION DIVISION.
       PROGRAM-ID. JXC315JX.
      *****************************************************************
      *                                                               *
      * (C) 2005 RENAISSANCE DATA SYSTEMS INC.                        *
      *          CHANGEWIZ STAT REL 1.0 VER 1.0                       *
      *                                                               *
      * THESE PROGRAMS ARE DISTRIBUTED ON THE CBT TAPE WITH THE       *
      * PROVISO THAT THEY MAY BE FREELY DISTRIBUTED TO ANY OTHER      *
      * PARTY ON CONDITION THAT NO INDUCEMENT BEYOND REASONABLE       *
      * HANDLING COSTS IS OFFERED OR ACCEPTED BY EITHER SIDE FOR      *
      * SUCH DISTRIBUTION OR YOUR NORMAL CONSULTING COSTS FOR         *
      * INSTALLATION AND SUPPORT.                                     *
      *                                                               *
      *                                                               *
      *****************************************************************
      *JX*  FLAG DELETED ELEMENTS.
      *
      *    1. SORT BOTH FILES BY ELEMENT NAME, LIBRARY
      *    2. GET CAPTURE DATE/TIME FROM DUMMY ELEMENT RECORD.
      *    3. CREATE A NEW RECORD FOR DELETED ELEMENTS.
      *
      *
      * A0000-  INITIALIZATION AND TERMINATION ROUTINES
      * C0000-  MAIN PROCESSING
      * D0000-  FORMAT OUTPUT RECORDS
      * X0000-  PROGRAM READ, WRITE AND OTHER I-O ROUTINES
      * Y0000-  DEBUG ROUTINES
      * Z0000-  ABEND ROUTINES
      *
      *
      ****************************************************************

       ENVIRONMENT DIVISION.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.

      * INPUT FILES
           SELECT CHANGE-HIST-FILE
               STATUS IS CHANGE-HST-STATUS ASSIGN CHGHIST.
           SELECT ELEMENT-LIST-FILE
               STATUS IS ELEM-LIST-STATUS  ASSIGN SRCLIST.
           SELECT DATE-TIME-FILE
               STATUS IS DATE-FILE-STATUS  ASSIGN CHGDATE.

      * OUTPUT FILES
           SELECT NEW-CHANGE-FILE
               STATUS IS NEW-CHANGE-STATUS ASSIGN NEWCHG.

           SELECT DEBUG-REPORT             ASSIGN RDBUG.

       DATA DIVISION.
       FILE SECTION.

       FD  CHANGE-HIST-FILE
           RECORDING MODE IS F
           BLOCK CONTAINS 0 RECORDS.
       01  CHANGE-HIST-RECORD          PIC X(200).

       FD  ELEMENT-LIST-FILE
           RECORDING MODE IS F
           BLOCK CONTAINS 0 RECORDS.
       01  ELEMENT-LIST-RECORD.
           05  LST-ELEMENT-NAME        PIC X(08).
           05  FILLER                  PIC X(01).
           05  LST-ELEMENT-LIB         PIC X(08).
           05  FILLER                  PIC X(03).

       FD  DATE-TIME-FILE
           RECORDING MODE IS F
           BLOCK CONTAINS 0 RECORDS.
       01  DATE-TIME-RECORD            PIC X(200).

       FD  NEW-CHANGE-FILE
           RECORDING MODE IS F
           BLOCK CONTAINS 0 RECORDS.
       01  NEW-CHANGE-RECORD           PIC X(200).


       FD  DEBUG-REPORT
           RECORDING MODE IS F
           BLOCK CONTAINS 0 RECORDS.
       01  DEBUG-RPT-LINE              PIC X(133).


       WORKING-STORAGE SECTION.

       01  WS-PROGRAM                  PIC X(08) VALUE 'JXC315JX'.

      *  VENDOR INFO
           COPY JXCPW001.

      *  REFWIZ PRODUCT INFO
           COPY JXCPW002.


           EJECT
      *  CHANGE HISTORY RECORD LAYOUT
           COPY JXCPR300.

      *  CHANGE HISTORY RECORD LAYOUT 2
           COPY JXCPR305.


       01  WS-CURR-KEY.
           05  WS-CURR-ELEMENT         PIC X(08).
           05  WS-CURR-LIBRARY         PIC X(08).

       01  WS-LIST-KEY.
           05  WS-LIST-ELEMENT         PIC X(08).
           05  WS-LIST-LIBRARY         PIC X(08).


       01  WORK-IO-FIELDS.
           05  CHANGE-HIST-CNT         PIC 9(09) VALUE ZERO.
           05  ELEM-LIST-CNT           PIC 9(09) VALUE ZERO.
           05  DLY-DUMMY-CNT           PIC 9(09) VALUE ZERO.
           05  DLY-WRITTEN-CNT         PIC 9(09) VALUE ZERO.
           05  NEW-DELETE-CNT          PIC 9(09) VALUE ZERO.
           05  TOTAL-WRITTEN-CNT       PIC 9(09) VALUE ZERO.
           05  SAVE-HISTORY-RECORD     PIC X(300) VALUE SPACE.
           05  SAVE-HIST-LEVEL         PIC 9(05) VALUE ZERO.
           05  SAVE-HIST-PROMOTE-DATE  PIC X(10) VALUE SPACE.
           05  SAVE-HIST-PROMOTE-TIME  PIC X(08) VALUE SPACE.
           05  SAVE-HIST-CHANGE-CODE   PIC X(08) VALUE SPACE.
           05  SAVE-CAPTURE-DATE       PIC X(10) VALUE SPACE.
           05  SAVE-CAPTURE-JULDATE    PIC X(08) VALUE SPACE.
           05  SAVE-CAPTURE-TIME       PIC X(08) VALUE SPACE.
           05  DLY-CHANGE-LEVEL        PIC 9(05) VALUE ZERO.
           05  DISPLAY-CNT             PIC ZZZ,ZZZ,ZZ9.

           05  DEBUG-RPT-LINE-CNT      PIC S9(3) COMP-3 VALUE +80.
           05  DEBUG-PAGE-CNT          PIC S9(7) COMP-3 VALUE ZERO.
           05  CHANGE-HST-STATUS       PIC X(2) VALUE SPACE.
           05  NEW-CHANGE-STATUS       PIC X(2) VALUE SPACE.
           05  ELEM-LIST-STATUS        PIC X(2) VALUE SPACE.
           05  DATE-FILE-STATUS        PIC X(2) VALUE SPACE.
           05  END-OF-HIST-CHANGE-SW   PIC X    VALUE SPACE.
               88  END-OF-HIST-CHANGE           VALUE 'Y'.
           05  END-OF-LIST-SW          PIC X    VALUE SPACE.
               88  END-OF-LIST                   VALUE 'Y'.

           05  HOLD-PRINT-LINE         PIC X(133) VALUE SPACE.

           EJECT
       01  DEBUG-HEADING-1.
           05  FILLER                  PIC X(01) VALUE SPACES.
           05  DBH1-IO-TYPE            PIC X(04) VALUE SPACES.
           05  FILLER                  PIC X(01) VALUE SPACES.
           05  DBH1-ELEMENT            PIC X(08) VALUE 'ELEMENT'.
           05  FILLER                  PIC X(01) VALUE SPACES.
           05  DBH1-PROMOTE-DATE       PIC X(10) VALUE 'PROMOTE'.
           05  FILLER                  PIC X(01) VALUE SPACES.
           05  DBH1-PROMOTE-TIME       PIC X(08) VALUE 'PROMOTE'.
           05  FILLER                  PIC X(02) VALUE SPACES.
           05  DBH1-OUTPUT-1           PIC X(50) VALUE
               'ELEMENT  LEVEL LIBRARY  LANGUAGE # LINES USER ID  '.
           05  DBH1-OUTPUT-1           PIC X(45) VALUE
               '       CHG CODE EDIT DATE  JULIAN   EDIT TIME'.

       01  DEBUG-HEADING-2.
           05  FILLER                  PIC X(01) VALUE SPACES.
           05  DBH2-IO-TYPE            PIC X(04) VALUE ' IO'.
           05  FILLER                  PIC X(01) VALUE SPACES.
           05  DBH2-ELEMENT            PIC X(08) VALUE '  NAME'.
           05  FILLER                  PIC X(01) VALUE SPACES.
           05  DBH2-PROMOTE-DATE       PIC X(10) VALUE '   DATE '.
           05  FILLER                  PIC X(01) VALUE SPACES.
           05  DBH2-PROMOTE-TIME       PIC X(08) VALUE '  TIME'.
           05  FILLER                  PIC X(02) VALUE SPACES.
           05  DBH2-OUTPUT             PIC X(95) VALUE SPACES.

       01  DEBUG-DETAIL-LINE-IN.
           05  DEBUG-CC                PIC X  VALUE SPACE.
           05  DEBUG-DATA-IN.
               10  DBI-IO-TYPE         PIC X(04).
               10  FILLER              PIC X(01).
               10  DBI-ELEMENT         PIC X(08).
               10  FILLER              PIC X(01).
               10  DBI-PROMOTE-DATEX.
                   15  DBI-PROMOTE-DATE PIC X(10).
               10  FILLER              PIC X(01).
               10  DBI-PROMOTE-TIME    PIC X(08).
               10  FILLER              PIC X(02).
               10  DBI-DATA-REC.
                   15  FILLER          PIC X(95).

       01  DISPLAY-TOTAL-LINE.
           05  FILLER                  PIC X(02).
           05  DT-DESC                 PIC X(30).
           05  FILLER                  PIC X(02).
           05  DT-COUNT                PIC ZZZ,ZZZ,ZZ9.
           05  FILLER                  PIC X(05).

           EJECT
      *  COMMON REPORT HEADINGS AND FOOTER
      *  DATE-TIME WORK AREAS
      *  COMMON DEBUG WORK AREAS
      *  COMMON ABEND WORK AREA
           COPY JXCPW020.

           EJECT
      *+ INSTALLATION SPECIFIC CONSTANTS
           COPY JXINW005.

           EJECT
       LINKAGE SECTION.
      *  COMMON PROGRAM RUN PARAMETERS
           COPY JXCPL005.

           EJECT
       PROCEDURE DIVISION USING JXL-PARM-DATA.

           PERFORM A1000-INITIALIZATION THRU A1000-EXIT.

           PERFORM C1000-PROCESS-CHANGE-HIST
              THRU C1000-EXIT
              UNTIL END-OF-HIST-CHANGE AND
                    END-OF-LIST.

           PERFORM A9000-TERMINATION THRU A9000-EXIT.
           GOBACK.

       A1000-INITIALIZATION.

           DISPLAY '**************************************************'
                  '**************************************************'.
           DISPLAY ' '.
           DISPLAY 'PROGRAM JXC315JX - IDENTIFY DELETED ELEMENTS IN '
                   'CHANGE HISTORY FILE'
           MOVE ' DEBUG REPORT - IDENTIFY DELETED ELEMENTS'
                                                 TO JX-DEBUG-TITLE.

           MOVE RULER-LINE(2:95)   TO DBH2-OUTPUT.
           MOVE WS-PROGRAM         TO JXRD-PROGRAM
                                      JXCD-PROGRAM
                                      HD2-PROGRAM.

           MOVE 'CHANGEWIZ  HIST   REL 1.0 VER 1.0' TO FT1-PRODUCT.
           MOVE '(C) 2005 RENAISSANCE DATA SYSTEMS INC.'
                                                    TO FT1-VENDOR.


      **  DISPLAY COMPILE DATE AND TIME
           COPY JXCPP010.

      **  PROCESS COMMON INPUT PARAMETER
           COPY JXCPP015.

      *+  INSTALLATION SPECIFIC INITIALIZATION ROUTINES
           COPY JXINP005.

      * GET CAPTURE DATE FROM DUMMY ELEMENT RECORD
           PERFORM A2000-PROCESS-DATE-TIME THRU A2000-EXIT.

           OPEN INPUT CHANGE-HIST-FILE
                       ELEMENT-LIST-FILE.
           OPEN OUTPUT NEW-CHANGE-FILE.

           IF JX-PRINT-DEBUG
               OPEN OUTPUT DEBUG-REPORT.

           IF CHANGE-HST-STATUS NOT = '00' AND '97'
               DISPLAY '   BAD OPEN ON CHANGE HIST FILE. STATUS = '
                       CHANGE-HST-STATUS
               MOVE 'Y' TO END-OF-HIST-CHANGE-SW
               PERFORM Z9000-ABEND-PROGRAM THRU Z9000-EXIT
           ELSE
               PERFORM X1000-READ-CHANGE-HIST THRU X1000-EXIT.

           IF END-OF-HIST-CHANGE
               DISPLAY 'NO RECORDS IN CHANGE HISTORY FILE'
               PERFORM Z9000-ABEND-PROGRAM THRU Z9000-EXIT
           END-IF.

           IF ELEM-LIST-STATUS NOT = '00' AND '97'
               DISPLAY '   BAD OPEN ON ELEMENT LIST FILE. STATUS = '
                       ELEM-LIST-STATUS
               PERFORM Z9000-ABEND-PROGRAM THRU Z9000-EXIT
           ELSE
               PERFORM X2000-READ-ELEM-LIST THRU X2000-EXIT
           END-IF.

           IF END-OF-LIST
               DISPLAY 'NO RECORDS IN ELEMENT LIST FILE'
               PERFORM Z9000-ABEND-PROGRAM THRU Z9000-EXIT
           END-IF.

           IF NEW-CHANGE-STATUS NOT = '00' AND '97'
               DISPLAY '   BAD OPEN ON CHANGE HIST OUT FILE. STATUS = '
                       NEW-CHANGE-STATUS
               PERFORM Z9000-ABEND-PROGRAM THRU Z9000-EXIT
           END-IF.


      *+  INSTALLATION-SPECIFIC CODE TO DETERMINE DATE CUTOFF
           COPY JXINP010.


       A1000-EXIT.
           EXIT.

      ***********************************************************
      * GET CAPTURE DATE/TIME FROM DUMMY ELEMENT RECORD
      ***********************************************************
       A2000-PROCESS-DATE-TIME.

           OPEN INPUT  DATE-TIME-FILE.

           IF DATE-FILE-STATUS NOT = '00' AND '97'
               DISPLAY '   BAD OPEN ON DATE FILE. STATUS = '
                       DATE-FILE-STATUS
               PERFORM Z9000-ABEND-PROGRAM THRU Z9000-EXIT
           END-IF.

           READ DATE-TIME-FILE INTO SOURCE-HISTORY-RECORD.

           IF DATE-FILE-STATUS = '10'
               DISPLAY 'DATE/TIME FILE EMPTY '
               PERFORM Z9000-ABEND-PROGRAM THRU Z9000-EXIT
               GO TO A2000-EXIT
           END-IF.

           IF DATE-FILE-STATUS NOT = '00'
               DISPLAY '  BAD READ ON DATE/TIME FILE. '
                   ' STATUS = '
                   DATE-FILE-STATUS
               GO TO A2000-EXIT
           END-IF.



      *  SAVE DATES FROM DUMMY RECORD. THESE ARE USED FOR CREATING
      *  DELETE RECORDS

           MOVE SH-CAPTURE-DATE-CYMD  TO SAVE-CAPTURE-DATE
           MOVE SH-CAPTURE-DATE-JUL   TO SAVE-CAPTURE-JULDATE
           MOVE SH-CAPTURE-TIME       TO SAVE-CAPTURE-TIME.

           CLOSE DATE-TIME-FILE.

       A2000-EXIT.
           EXIT.

      ***********************************************************
      *   END OF JOB PROCESSING
      ***********************************************************
       A9000-TERMINATION.

           CLOSE CHANGE-HIST-FILE
                 ELEMENT-LIST-FILE
                 NEW-CHANGE-FILE.

      *  ENABLE TOTALS TO PRINT ON DEBUG REPORT
           IF JX-PRINT-DEBUG OR DEBUG-PAGE-CNT > 0
              ADD 15 TO JX-PRINT-DEBUG-MAX
              IF NOT JX-PRINT-DEBUG
                 MOVE 'Y' TO JX-DEBUG-SW
              END-IF
              ADD 15 TO DEBUG-RPT-LINE-CNT
              IF DEBUG-RPT-LINE-CNT > INST-LINES-PER-PAGE
                 SUBTRACT 15 FROM DEBUG-RPT-LINE-CNT
                 PERFORM Y1025-DEBUG-HEADING THRU Y1025-EXIT
              ELSE
                 SUBTRACT 15 FROM DEBUG-RPT-LINE-CNT
                 MOVE SPACES        TO DEBUG-RPT-LINE
                 PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
                 PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
              END-IF
           END-IF.

           DISPLAY ' '.
           DISPLAY ' '.
           DISPLAY '****************************************'
                   '****************************************'
           DISPLAY ' '.
           DISPLAY 'END OF PROGRAM JXC315JX.'.
           DISPLAY ' '.


           IF JX-PRINT-DEBUG
               MOVE ALL '*'       TO DEBUG-RPT-LINE(2:125)
               PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
               MOVE SPACES        TO DEBUG-RPT-LINE
               PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
               MOVE ' END-OF-PROGRAM JXC315JX RECORD COUNTS' TO
                                     DEBUG-RPT-LINE
               PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
               MOVE SPACES        TO DEBUG-RPT-LINE
               PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
           END-IF.

           MOVE SPACE TO DISPLAY-TOTAL-LINE.

           MOVE 'CHANGE HISTORY RECS READ '    TO DT-DESC.
           MOVE CHANGE-HIST-CNT                TO DT-COUNT.
           DISPLAY DISPLAY-TOTAL-LINE
           IF JX-PRINT-DEBUG
              MOVE DISPLAY-TOTAL-LINE TO DEBUG-RPT-LINE
              PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
           END-IF.

           MOVE 'ELEMENT LIST RECS READ     '  TO DT-DESC.
           MOVE ELEM-LIST-CNT                  TO DT-COUNT.
           DISPLAY DISPLAY-TOTAL-LINE
           DISPLAY '   '
           IF JX-PRINT-DEBUG
              MOVE DISPLAY-TOTAL-LINE TO DEBUG-RPT-LINE
              PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
              MOVE SPACE              TO DEBUG-RPT-LINE
              PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
           END-IF.

           MOVE 'TOTAL CHG RECORDS WRITTEN '   TO DT-DESC.
           MOVE TOTAL-WRITTEN-CNT              TO DT-COUNT.
           DISPLAY DISPLAY-TOTAL-LINE
           IF JX-PRINT-DEBUG
              MOVE DISPLAY-TOTAL-LINE TO DEBUG-RPT-LINE
              PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
           END-IF.

           MOVE '     NEW DELETED ELEMENTS   = ' TO DT-DESC.
           MOVE NEW-DELETE-CNT                   TO DT-COUNT.
           DISPLAY DISPLAY-TOTAL-LINE
           DISPLAY '   '
           IF JX-PRINT-DEBUG
              MOVE DISPLAY-TOTAL-LINE TO DEBUG-RPT-LINE
              PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
              MOVE SPACE              TO DEBUG-RPT-LINE
              PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
           END-IF.

           DISPLAY ' '.
           DISPLAY '****************************************'
                   '****************************************'
           DISPLAY ' '.

           IF JX-PRINT-DEBUG
               MOVE SPACES        TO DEBUG-RPT-LINE
               PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
               MOVE ALL '*'       TO DEBUG-RPT-LINE(2:125)
               PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
               MOVE SPACES        TO DEBUG-RPT-LINE
               PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
           END-IF.


      **  PRINT LAST CHANGE FILE DATA
           IF DEBUG-PAGE-CNT > 0
               PERFORM Y1050-DEBUG-FOOTER THRU Y1050-EXIT.

           IF JX-PRINT-DEBUG OR DEBUG-PAGE-CNT > 0
               CLOSE DEBUG-REPORT.


       A9000-EXIT.
           EXIT.
           EJECT
      **************************************************************
      *  1. MATCH ELEMENT LIST TO CHANGE HISTORY FILE.
      *  2. IF AN ELEMENT IS NOT IN THE ELEMENT LIST, THEN IT
      *     HAS BEEN DELETED. CREATE A NEW RECORD FOR IT.
      **************************************************************
       C1000-PROCESS-CHANGE-HIST.

      *+  SPECIAL CLIENT PROCESSING TO EXCLUDE ELEMENTS
           COPY JXINP300.


           IF END-OF-HIST-CHANGE
              DISPLAY 'END OF HISTORY FILE BEFORE END OF LIST'
              PERFORM Z9000-ABEND-PROGRAM THRU Z9000-EXIT
           END-IF.


      *  READ AND WRITE ALL HISTORY FOR AN ELEMENT
      *  SAVE THE HIGHEST LEVEL NUMBER

           MOVE SH-ELEMENT-NAME     TO WS-CURR-ELEMENT
           MOVE SH-ELEMENT-LIB      TO WS-CURR-LIBRARY

           PERFORM UNTIL END-OF-HIST-CHANGE OR
                         (WS-CURR-ELEMENT NOT = SH-ELEMENT-NAME) OR
                         (WS-CURR-ELEMENT  = SH-ELEMENT-NAME AND
                         (WS-CURR-LIBRARY NOT = SH-ELEMENT-LIB))
              MOVE SH-ELEMENT-LEVEL      TO SAVE-HIST-LEVEL
              MOVE SOURCE-HISTORY-RECORD TO SOURCE-HISTORY-RECORD-2
              PERFORM D1000-WRITE-HISTORY    THRU D1000-EXIT
              PERFORM X1000-READ-CHANGE-HIST THRU X1000-EXIT
           END-PERFORM.

      *  IF LIST KEY IS GREATER, THIS IS A DELETED ELEMENT
      *  CREATE A NEW HISTORY RECORD

           IF WS-CURR-KEY < WS-LIST-KEY
              IF SH2-PAN-STATUS NOT  = 'DELETE'
                 PERFORM D2000-WRITE-DELETED   THRU D2000-EXIT
              END-IF
              GO TO C1000-EXIT
           END-IF.

      *  IF LIST KEY IS LESS, SOMETHING IS WRONG

           IF WS-CURR-KEY > WS-LIST-KEY
              DISPLAY 'NO MATCH TO LIST FILE'
                      '  CHANGE ELEMENT = ' WS-CURR-KEY
                      '  LIST = ' WS-LIST-KEY
              PERFORM Z9000-ABEND-PROGRAM THRU Z9000-EXIT
           END-IF.

      * THE KEYS MATCH - CHECK NEXT ELEMENT

           PERFORM X2000-READ-ELEM-LIST   THRU X2000-EXIT.

       C1000-EXIT.
           EXIT.
           EJECT
      ***************************************************************
      *  WRITE HISTORY TO NEW HISTORY FILE
      ***************************************************************
       D1000-WRITE-HISTORY.
      *    DISPLAY 'D1000-WRITE-HISTORY      '.

           WRITE NEW-CHANGE-RECORD FROM SOURCE-HISTORY-RECORD.
           ADD 1 TO TOTAL-WRITTEN-CNT.

       D1000-EXIT.
           EXIT.

      ***************************************************************
      *  WRITE NEW RECORD TO SHOW ELEMENT HAS BEEN DELETED
      ***************************************************************
       D2000-WRITE-DELETED.
      *    DISPLAY 'D2000-WRITE-DELETED      '.

           IF JX-PRINT-DEBUG
              MOVE SPACES                TO DEBUG-DATA-IN
              MOVE 'ELEMENT DELETED   '
                                               TO DBI-DATA-REC
              MOVE DEBUG-DETAIL-LINE-IN  TO DEBUG-RPT-LINE
              PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
              MOVE SPACES                 TO DEBUG-DATA-IN
              MOVE 'IHST'                 TO DBI-IO-TYPE
              MOVE WS-CURR-ELEMENT        TO DBI-ELEMENT
              MOVE SH2-PROMOTE-TIME       TO DBI-PROMOTE-TIME
              MOVE SH2-PROMOTE-DATE-CYMD  TO DBI-PROMOTE-DATE
              MOVE SOURCE-HISTORY-RECORD-2  TO DBI-DATA-REC
              MOVE DEBUG-DETAIL-LINE-IN   TO DEBUG-RPT-LINE
              PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
           END-IF.

           ADD 1 TO SH2-ELEMENT-LEVEL.
           MOVE SAVE-CAPTURE-DATE      TO SH2-CAPTURE-DATE-CYMD
           MOVE SAVE-CAPTURE-JULDATE   TO SH2-CAPTURE-DATE-JUL
           MOVE SAVE-CAPTURE-TIME      TO SH2-CAPTURE-TIME
           MOVE SAVE-CAPTURE-DATE      TO SH2-PROMOTE-DATE-CYMD
           MOVE SAVE-CAPTURE-JULDATE   TO SH2-PROMOTE-DATE-JUL
           MOVE SAVE-CAPTURE-TIME      TO SH2-PROMOTE-TIME
           MOVE 'DELETE'               TO SH2-PAN-STATUS
           MOVE 'DELETED'              TO SH2-CHANGE-CODE.

           IF JX-PRINT-DEBUG
               MOVE SPACES                  TO DEBUG-DATA-IN
               MOVE 'OHST'                  TO DBI-IO-TYPE
               MOVE SH2-ELEMENT-NAME        TO DBI-ELEMENT
               MOVE SH2-CAPTURE-TIME        TO DBI-PROMOTE-TIME
               MOVE SH2-CAPTURE-DATE-CYMD   TO DBI-PROMOTE-DATE
               MOVE SOURCE-HISTORY-RECORD-2 TO DBI-DATA-REC
               MOVE DEBUG-DETAIL-LINE-IN    TO DEBUG-RPT-LINE
               PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
           END-IF.

           WRITE NEW-CHANGE-RECORD FROM SOURCE-HISTORY-RECORD-2.
           ADD 1 TO NEW-DELETE-CNT
                    TOTAL-WRITTEN-CNT.

       D2000-EXIT.
           EXIT.


           EJECT
      ***************************************************************
      *  READ CHANGE HISTORY FILE
      ***************************************************************
       X1000-READ-CHANGE-HIST.
           READ CHANGE-HIST-FILE INTO SOURCE-HISTORY-RECORD.

           IF CHANGE-HST-STATUS = '10'
               MOVE 'Y' TO END-OF-HIST-CHANGE-SW
               GO TO X1000-EXIT.

           ADD 1           TO CHANGE-HIST-CNT.
           IF CHANGE-HST-STATUS NOT = '00'
               DISPLAY '  BAD READ ON CHANGE HISTORY FILE. '
                   ' STATUS = '
                   CHANGE-HST-STATUS ' ON RECORD NBR '
                   CHANGE-HIST-CNT
               GO TO X1000-EXIT.


       X1000-EXIT.
           EXIT.

      ***************************************************************
      *  READ CHANGE DAILY FILE
      ***************************************************************
       X2000-READ-ELEM-LIST.
           READ ELEMENT-LIST-FILE.

           IF ELEM-LIST-STATUS = '10'
               MOVE 'Y' TO END-OF-LIST-SW
               MOVE HIGH-VALUES        TO WS-LIST-ELEMENT
               GO TO X2000-EXIT.

           ADD 1           TO ELEM-LIST-CNT.
           IF ELEM-LIST-STATUS NOT = '00'
               DISPLAY '  BAD READ ON ELEMENT LIST FILE. '
                   ' STATUS = '
                   ELEM-LIST-STATUS ' ON RECORD NBR '
                   ELEM-LIST-CNT
               GO TO X2000-EXIT.

           MOVE LST-ELEMENT-NAME TO WS-LIST-ELEMENT.
           MOVE LST-ELEMENT-LIB  TO WS-LIST-LIBRARY.

       X2000-EXIT.
           EXIT.


           EJECT
      ***************************************************************
      ***  PRINT ROUTINES
      ***************************************************************

       Y1000-PRINT-DEBUG.
           ADD 1 TO JX-PRINT-DEBUG-CNT.
           IF JX-PRINT-DEBUG-CNT > JX-PRINT-DEBUG-SKIP
               IF DEBUG-RPT-LINE-CNT > INST-LINES-PER-PAGE
                   MOVE DEBUG-RPT-LINE TO HOLD-PRINT-LINE
                   PERFORM Y1025-DEBUG-HEADING THRU Y1025-EXIT
                   MOVE HOLD-PRINT-LINE TO DEBUG-RPT-LINE.

           IF JX-PRINT-DEBUG-CNT > JX-PRINT-DEBUG-SKIP
               WRITE DEBUG-RPT-LINE
                   AFTER ADVANCING 1 LINE
               ADD 1 TO DEBUG-RPT-LINE-CNT.

           IF JX-PRINT-DEBUG-CNT > JX-PRINT-DEBUG-MAX
               MOVE 'N' TO JX-DEBUG-SW.
           MOVE SPACES TO DEBUG-RPT-LINE.
       Y1000-EXIT.
           EXIT.

       Y1025-DEBUG-HEADING.
           ADD 1                   TO DEBUG-PAGE-CNT.
           MOVE DEBUG-PAGE-CNT     TO HD1-PAGE.
           MOVE JX-DEBUG-TITLE     TO HD2-TITLE.
           MOVE 'DB'               TO HD2-REPORT.
           MOVE '            CHANGEWIZ DEBUGGING REPORT           '
                                   TO HD1-COMPANY-NAME


           IF DEBUG-PAGE-CNT > 1
               PERFORM Y1050-DEBUG-FOOTER THRU Y1050-EXIT.

           WRITE DEBUG-RPT-LINE FROM HEADING-1
               AFTER ADVANCING PAGE.
           WRITE DEBUG-RPT-LINE FROM HEADING-2
               AFTER ADVANCING 1 LINE.
           WRITE DEBUG-RPT-LINE FROM DEBUG-HEADING-1
               AFTER ADVANCING 2 LINE.
           WRITE DEBUG-RPT-LINE FROM DEBUG-HEADING-2
               AFTER ADVANCING 1 LINE.
           MOVE SPACES TO DEBUG-RPT-LINE
           WRITE DEBUG-RPT-LINE
               AFTER ADVANCING 1 LINE.
           MOVE 6 TO DEBUG-RPT-LINE-CNT.
       Y1025-EXIT.
           EXIT.

       Y1050-DEBUG-FOOTER.
           MOVE SPACES TO DEBUG-RPT-LINE.

           PERFORM UNTIL DEBUG-RPT-LINE-CNT > INST-LINES-PER-PAGE
               WRITE DEBUG-RPT-LINE
                   AFTER ADVANCING 1 LINE
               ADD 1 TO DEBUG-RPT-LINE-CNT
           END-PERFORM.

           WRITE DEBUG-RPT-LINE
               AFTER ADVANCING 2 LINE.
           WRITE DEBUG-RPT-LINE FROM FOOTER-1
               AFTER ADVANCING 1 LINE.

       Y1050-EXIT.
           EXIT.

           EJECT
      *  COMMON ABEND ROUTINE
           COPY JXCPP099.


