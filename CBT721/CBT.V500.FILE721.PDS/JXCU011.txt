       PROCESS NOADV
       IDENTIFICATION DIVISION.
       PROGRAM-ID. JXCU011.
      *****************************************************************
      *                                                               *
      *  (C) 2005 RENAISSANCE DATA SYSTEMS INC.                       *
      *              CHANGEWIZ  STAT  REL 1.0  VER 1.0                *
      *                                                               *
      *  THESE PROGRAMS ARE DISTRIBUTED ON THE CBT TAPE WITH THE      *
      *  PROVISO THAT THEY MAY BE FREELY DISTRIBUTED TO ANY OTHER     *
      *  PARTY ON CONDITION THAT NO INDUCEMENT BEYOND REASONABLE      *
      *  HANDLING COSTS IS OFFERED OR ACCEPTED BY EITHER SIDE FOR     *
      *  SUCH DISTRIBUTION OR YOUR NORMAL CONSULTING COSTS FOR        *
      *  INSTALLATION AND SUPPORT.                                    *
      *                                                               *
      *****************************************************************
      ****************************************************************
      *JX* TRANSLATE IEHLIST PDS DIRECTORY LISTING.
      *
      *    THE PDS DIRECTORY LISTING IS CREATED BY IEHLIST
      *    WITH DUMP OPTION.  CONVERT HEX DATA TO ISPF STATISTICS.
      *    THE OUTPUT WILL SHOW ISPF STATISTICS FOR A PDS LIBRARY.
      *
      *
      * A0000-  INITIALIZATION AND TERMINATION ROUTINES
      * C0000-  MAIN PROCESSING
      * D0000-  FORMAT DATA
      * W0000-  DATE CONVERSION ROUTINES
      * X0000-  PROGRAM READ, WRITE AND OTHER I-O ROUTINES
      * Y0000-  DEBUG ROUTINES
      * Z0000-  ABEND ROUTINES
      *
      *
      ****************************************************************

       ENVIRONMENT DIVISION.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.

           SELECT PDS-DIRECTORY-FILE
               STATUS IS PDS-DIR-STATUS     ASSIGN PDSDIRI.

           SELECT NEW-DIRECTORY-OUT
               STATUS IS NEW-DIR-STATUS     ASSIGN PDSDIRO.

           SELECT DEBUG-REPORT              ASSIGN RDBUG.

       DATA DIVISION.
       FILE SECTION.

       FD  PDS-DIRECTORY-FILE
           RECORDING MODE IS F
           BLOCK CONTAINS 0 RECORDS.
      *  INPUT FILE IS IEHLIST DIRECTORY DUMP
       01  PDS-DIRECTORY-REC           PIC X(121).


       FD  NEW-DIRECTORY-OUT
           RECORDING MODE IS F
           BLOCK CONTAINS 0 RECORDS.
       01  NEW-DIRECTORY-REC           PIC X(133).


       FD  DEBUG-REPORT
           RECORDING MODE IS F
           BLOCK CONTAINS 0 RECORDS.
       01  DEBUG-RPT-LINE              PIC X(133).


       WORKING-STORAGE SECTION.

       01  WS-PROGRAM                  PIC X(08) VALUE 'JXCU011'.

      *  VENDOR INFO
           COPY JXCPW001.

      *  OPEN SOURCE PRODUCT INFO
           COPY JXCPW003.

           EJECT
      *  INPUT DATA IS IN HEXDATA FORMAT
       01  PDS-DIR-RECORD.
           05  PDS-CC                 PIC X(01).
           05  PDS-MESSAGE            PIC X(10).
           05  PDS-ELEMENT-NAME       PIC X(08).
           05  FILLER                 PIC X(02).
           05  PDS-TTRC               PIC X(08).
           05  FILLER                 PIC X(02).
      *
           05  PDS-VER-MISC.
               10  PDS-VER-MOD        PIC X(04).
               10  PDS-UNKNOWN-1      PIC X(02).
               10  PDS-CHANGE-TIME-SS PIC X(02).
               10  PDS-CREATE-DATE-CC PIC X(02).
           05  FILLER                 PIC X(01).
      *
           05  PDS-BLOCK-4.
               10  PDS-CREATE-DATE-YY     PIC X(02).
               10  PDS-CREATE-DATE-DDD    PIC X(03).
               10  PDS-CREATE-DATE-SIGN   PIC X(01).
               10  PDS-CHANGE-DATE-CC     PIC X(02).
               10  PDS-CHANGE-DATE-YY     PIC X(02).
           05  FILLER                 PIC X(01).
      *
           05  PDS-BLOCK-5.
               10  PDS-CHANGE-DATE-DDD    PIC X(03).
               10  PDS-CHANGE-DATE-SIGN   PIC X(01).
               10  PDS-CHANGE-TIME-HH     PIC X(02).
               10  PDS-CHANGE-TIME-MM     PIC X(02).
               10  PDS-TOTAL-LINES-1      PIC X(02).
           05  FILLER                 PIC X(01).
      *
           05  PDS-BLOCK-6.
               10  PDS-TOTAL-LINES-2  PIC X(02).
               10  PDS-INITIAL-LINES  PIC X(04).
               10  PDS-CHANGED-LINES  PIC X(04).
           05  FILLER                 PIC X(01).
      *
           05  PDS-EDIT-BY-1          PIC X(10).
           05  FILLER                 PIC X(01).
      *
           05  PDS-EDIT-BY-2          PIC X(10).
           05  FILLER                 PIC X(25).


       01  WORK-IBM-DATE-TIME.
           05  WK-IBM-DATE.
               10  WK-IBM-DATE-CCX    PIC X(02).
               10  WK-IBM-DATE-YYX    PIC X(02).
               10  WK-IBM-DATE-DDDX   PIC X(03).


       01  WORK-IO-FIELDS.
           05  READ-PDS-DIR-CNT        PIC S9(9) COMP-3 VALUE ZERO.
           05  WRITE-NEW-DIR-CNT       PIC S9(9) COMP-3 VALUE ZERO.
           05  DEBUG-RPT-LINE-CNT      PIC S9(3) COMP-3 VALUE +80.
           05  DEBUG-PAGE-CNT          PIC S9(7) COMP-3 VALUE ZERO.
           05  NEW-DIR-LINE-CNT        PIC S9(3) COMP-3 VALUE +80.
           05  NEW-DIR-PAGE-CNT        PIC S9(7) COMP-3 VALUE ZERO.
           05  DISPLAY-COUNT           PIC ZZZ,ZZZ,ZZZ.
           05  PDS-DIR-STATUS          PIC X(02) VALUE SPACE.
           05  NEW-DIR-STATUS          PIC X(02) VALUE SPACE.
           05  END-OF-PDSDIR-LINES-SW      PIC X VALUE SPACE.
               88  END-OF-PDSDIR-LINES           VALUE 'Y'.

       01  HOLD-FIELDS.
           05  HOLD-PRINT-LINE         PIC X(133) VALUE SPACE.


       01  WK-CONVERSION-FIELDS.
           05  WK-PDS-NAME                 PIC X(45) VALUE SPACE.
           05  WK-PDS-DIR-DATE.
               10  WK-PDS-DIR-CCYY         PIC 9(04) VALUE ZERO.
               10  WK-PDS-DIR-CCYY-X REDEFINES WK-PDS-DIR-CCYY.
                   15  WK-PDS-DIR-CC       PIC 9(02).
                   15  WK-PDS-DIR-YY       PIC 9(02).
               10  FILLER                  PIC X(01) VALUE '-'.
               10  WK-PDS-DIR-MM           PIC 9(02) VALUE ZERO.
               10  FILLER                  PIC X(01) VALUE '-'.
               10  WK-PDS-DIR-DD           PIC 9(02) VALUE ZERO.

           05  WK-PDS-DIR-JULDATE.
               10  WK-PDSDIR-JUL-CCYY      PIC 9(04) VALUE ZERO.
               10  WK-PDSDIR-JUL-CCYY-X REDEFINES WK-PDSDIR-JUL-CCYY.
                   15  WK-PDSDIR-JUL-CC    PIC 9(02).
                   15  WK-PDSDIR-JUL-YY    PIC 9(02).
               10  FILLER                  PIC X(01) VALUE '-'.
               10  WK-PDSDIR-JUL-DDD       PIC 9(03) VALUE ZERO.

           05  WK-PDS-DIR-TIME.
               10  WK-PDS-DIR-HH           PIC 9(02) VALUE ZERO.
               10  FILLER                  PIC X(01) VALUE ':'.
               10  WK-PDS-DIR-MIN          PIC 9(02) VALUE ZERO.
               10  FILLER                  PIC X(01) VALUE ':'.
               10  WK-PDS-DIR-SS           PIC 9(02) VALUE ZERO.

           05  WK-ORIG-BYTE.
               10  WK-ORIG-BYTE-9          PIC 9.
           05  WK-BYTE-VALUE               PIC 9(3).
           05  WK-COMPUTE                  PIC 9(9).

           05  WK-VERSION-MOD.
               10  WK-VERSION                  PIC 9(2).
               10  FILLER                      PIC X  VALUE '.'.
               10  WK-MOD                      PIC 9(2).

           05  WK-HEXDATA-DISPLAY      PIC X(02).
           05  WK-CHAR-DISPLAY         PIC X(01).
           05  HSUB                    PIC S9(09) COMP.
           05  DSUB                    PIC S9(09) COMP.
           05  SSUB                    PIC S9(09) COMP.

           05  WS-APOST                PIC X          VALUE X'7D'.
           05  WS-QUOTE                PIC X          VALUE X'7F'.
           05  WS-ASCII-APOST          PIC X          VALUE X'27'.
           05  WS-ASCII-QUOTE          PIC X          VALUE X'22'.

           EJECT
       01  DEBUG-HEADING-1.
           05  FILLER                  PIC X     VALUE SPACES.
           05  DBH1-IO-TYPE            PIC X(04) VALUE SPACES.
           05  FILLER                  PIC X     VALUE SPACES.
           05  DBH1-ELEMENT-NAME       PIC X(08) VALUE 'ELEMENT'.
           05  FILLER                  PIC X(01) VALUE SPACES.
           05  DBH1-OUTPUT             PIC X(90) VALUE
               '    INPUT AND OUTPUT DATA '.

       01  DEBUG-HEADING-2.
           05  FILLER                  PIC X     VALUE SPACES.
           05  DBH2-IO-TYPE            PIC X(04) VALUE ' IO'.
           05  FILLER                  PIC X     VALUE SPACES.
           05  DBH2-ELEMENT-NAME       PIC X(08) VALUE '  NAME'.
           05  FILLER                  PIC X(01) VALUE SPACES.
           05  DBH2-OUTPUT             PIC X(115) VALUE SPACES.

       01  DEBUG-DETAIL-LINE-IN.
           05  DEBUG-CC                PIC X.
           05  DEBUG-DATA-IN.
               10  DBI-IO-TYPE         PIC X(04).
               10  FILLER              PIC X(01).
               10  DBI-ELEMENT-NAME    PIC X(08).
               10  FILLER              PIC X(01).
               10  DBI-DATA-REC        PIC X(115).

       01  DIR-HEADER-1.
           05  FILLER                  PIC X(01)  VALUE SPACE.
           05  DRH1-MEMBER-NAME        PIC X(08)  VALUE ' MEMBER '.
           05  FILLER                  PIC X(08)  VALUE SPACE.
           05  DRH1-TTR                PIC X(06)  VALUE ' TTR'.
           05  FILLER                  PIC X(03)  VALUE SPACE.
           05  DRH1-VER-MOD            PIC X(08)  VALUE 'VERS.MOD'.
           05  FILLER                  PIC X(02)  VALUE SPACE.
           05  DRH1-CREATE-DATE        PIC X(10)  VALUE ' CREATION'.
           05  FILLER                  PIC X(03)  VALUE SPACE.
           05  DRH1-CHANGE-DATE        PIC X(20)  VALUE
               '   DATE AND TIME    '.
           05  FILLER                  PIC X(04)  VALUE SPACE.
           05  DRH1-CURRENT-LINES      PIC X(09)  VALUE '  CURRENT'.
           05  FILLER                  PIC X(04)  VALUE SPACE.
           05  DRH1-INITIAL-LINES      PIC X(09)  VALUE '  INITIAL'.
           05  FILLER                  PIC X(04)  VALUE SPACE.
           05  DRH1-CHANGED-LINES      PIC X(09)  VALUE ' MODIFIED'.
           05  FILLER                  PIC X(06)  VALUE SPACE.
           05  DRH1-USER-ID            PIC X(10)  VALUE 'USER'.

       01  DIR-HEADER-2.
           05  FILLER                  PIC X(01)  VALUE SPACE.
           05  DRH2-MEMBER-NAME        PIC X(08)  VALUE '  NAME  '.
           05  FILLER                  PIC X(08)  VALUE SPACE.
           05  DRH2-TTR                PIC X(06)  VALUE '(HEX)'.
           05  FILLER                  PIC X(03)  VALUE SPACE.
           05  DRH2-VER-MOD            PIC X(08)  VALUE '  LEVEL'.
           05  FILLER                  PIC X(02)  VALUE SPACE.
           05  DRH2-CREATE-DATE        PIC X(10)  VALUE '   DATE  '.
           05  FILLER                  PIC X(03)  VALUE SPACE.
           05  DRH2-CHANGE-DATE        PIC X(20)  VALUE
               '   LAST MODIFIED    '.
           05  FILLER                  PIC X(04)  VALUE SPACE.
           05  DRH2-CURRENT-LINES      PIC X(09)  VALUE 'NO. LINES'.
           05  FILLER                  PIC X(04)  VALUE SPACE.
           05  DRH2-INITIAL-LINES      PIC X(09)  VALUE 'NO. LINES'.
           05  FILLER                  PIC X(04)  VALUE SPACE.
           05  DRH2-CHANGED-LINES      PIC X(09)  VALUE 'NO. LINES'.
           05  FILLER                  PIC X(06)  VALUE SPACE.
           05  DRH2-USER-ID            PIC X(10)  VALUE ' ID '.

       01  DIR-HEADER-3.
           05  FILLER                  PIC X(01)  VALUE SPACE.
           05  DRH3-MEMBER-NAME        PIC X(08)  VALUE ALL '-'.
           05  FILLER                  PIC X(08)  VALUE SPACE.
           05  DRH3-TTR                PIC X(06)  VALUE ALL '-'.
           05  FILLER                  PIC X(03)  VALUE SPACE.
           05  DRH3-VER-MOD            PIC X(08)  VALUE ALL '-'.
           05  FILLER                  PIC X(02)  VALUE SPACE.
           05  DRH3-CREATE-DATE        PIC X(10)  VALUE ALL '-'.
           05  FILLER                  PIC X(03)  VALUE SPACE.
           05  DRH3-CHANGE-DATE        PIC X(20)  VALUE ALL '-'.
           05  FILLER                  PIC X(01)  VALUE SPACE.
           05  DRH3-CURRENT-LINES      PIC X(09)  VALUE ALL '-'.
           05  FILLER                  PIC X(04)  VALUE SPACE.
           05  DRH3-INITIAL-LINES      PIC X(09)  VALUE ALL '-'.
           05  FILLER                  PIC X(04)  VALUE SPACE.
           05  DRH3-CHANGED-LINES      PIC X(09)  VALUE ALL '-'.
           05  FILLER                  PIC X(09)  VALUE SPACE.
           05  DRH3-USER-ID            PIC X(10)  VALUE ALL '-'.

       01  DIR-DETAIL-LINE.
           05  FILLER                  PIC X.
           05  DR-MEMBER-NAME          PIC X(08).
           05  FILLER                  PIC X(08).
           05  DR-TTR                  PIC X(08).
           05  FILLER                  PIC X(03).
           05  DR-VER-MOD              PIC X(05).
           05  FILLER                  PIC X(03).
           05  DR-CREATE-DATE          PIC X(10).
           05  FILLER                  PIC X(03).
           05  DR-CHANGE-DATE          PIC X(10).
           05  FILLER                  PIC X(02).
           05  DR-CHANGE-TIME          PIC X(08).
           05  FILLER                  PIC X(01).
           05  DR-CURRENT-LINES        PIC ZZZZZ,ZZ9.
           05  FILLER                  PIC X(04).
           05  DR-INITIAL-LINES        PIC ZZZZZ,ZZ9.
           05  FILLER                  PIC X(04).
           05  DR-CHANGED-LINES        PIC ZZZZZ,ZZ9.
           05  FILLER                  PIC X(09).
           05  DR-USER-ID              PIC X(10).

       01  DIR-DETAIL-DEBUG-LINE.
           05  FILLER                  PIC X.
           05  DRDBG-ELEMENT-NAME      PIC X(08).
           05  FILLER                  PIC X(08).
           05  DRDBG-TTR               PIC X(08).
           05  FILLER                  PIC X(03).
           05  DRDBG-VER-MOD           PIC X(05).
           05  FILLER                  PIC X(03).
           05  DRDBG-CREATE-DATE-CC    PIC X(02).
           05  DRDBG-CREATE-DATE-YY    PIC X(02).
           05  FILLER                  PIC X(01).
           05  DRDBG-CREATE-DATE-DDD   PIC X(03).
           05  FILLER                  PIC X(01).
           05  DRDBG-CREATE-DATE-SIGN  PIC X(01).
           05  FILLER                  PIC X(03).
           05  DRDBG-CHANGE-DATE-CC    PIC X(02).
           05  DRDBG-CHANGE-DATE-YY    PIC X(02).
           05  FILLER                  PIC X(01).
           05  DRDBG-CHANGE-DATE-DDD   PIC X(03).
           05  FILLER                  PIC X(01).
           05  DRDBG-CHANGE-DATE-SIGN  PIC X(01).
           05  FILLER                  PIC X(02).
           05  DRDBG-CHANGE-TIME-HH    PIC X(02).
           05  FILLER                  PIC X(01).
           05  DRDBG-CHANGE-TIME-MM    PIC X(02).
           05  FILLER                  PIC X(01).
           05  DRDBG-CHANGE-TIME-SS    PIC X(02).
           05  FILLER                  PIC X(06).
           05  DRDBG-TOTAL-LINES-1     PIC X(02).
           05  DRDBG-TOTAL-LINES-2     PIC X(02).
           05  FILLER                  PIC X(09).
           05  DRDBG-INITIAL-LINES     PIC X(04).
           05  FILLER                  PIC X(09).
           05  DRDBG-CHANGED-LINES     PIC X(04).
           05  FILLER                  PIC X(09).
           05  DRDBG-EDIT-BY-1         PIC X(10).
           05  FILLER                  PIC X(01).
           05  DRDBG-EDIT-BY-2         PIC X(10).

       01  DIR-TOTAL-LINE.
           05  FILLER                  PIC X(17)  VALUE SPACE.
           05  FILLER                  PIC X(15)  VALUE
               'TOTAL MEMBERS: '.
           05  DRT-MEMBER-TOTAL        PIC ZZZ,ZZZ,ZZ9.
           05  FILLER                  PIC X(08)  VALUE SPACE.

       01  DISPLAY-TOTAL-LINE.
           05  FILLER                  PIC X(02).
           05  DT-DESC                 PIC X(30).
           05  FILLER                  PIC X(02).
           05  DT-COUNT                PIC ZZZ,ZZZ,ZZ9.
           05  FILLER                  PIC X(05).

           EJECT
      *  COMMON REPORT HEADINGS AND FOOTER
      *  DATE-TIME WORK AREAS
      *  COMMON DEBUG WORK AREAS
      *  COMMON ABEND WORK AREA
           COPY JXCPW020.

           EJECT
      *  DATE CONVERSION WORK AREAS
           COPY JXCPW025.

           EJECT
      *  HEX DATA  WORK AREAS
           COPY JXCPW045.

           EJECT
      *+ INSTALLATION SPECIFIC CONSTANTS
           COPY JXINW005.
           EJECT
       LINKAGE SECTION.
      *  COMMON PROGRAM RUN PARAMETERS
           COPY JXCPL005.

           EJECT
       PROCEDURE DIVISION USING JXL-PARM-DATA.

           PERFORM A1000-INITIALIZATION THRU A1000-EXIT.

           PERFORM C1000-PROCESS-PDS-DIR-FILE
              THRU C1000-EXIT
              UNTIL END-OF-PDSDIR-LINES.

           PERFORM A9000-TERMINATION THRU A9000-EXIT.
           GOBACK.

       A1000-INITIALIZATION.

           DISPLAY '**************************************************'
                  '**************************************************'.
           DISPLAY ' '.
           DISPLAY 'PROGRAM JXCU011 - TRANSLATE IEHLIST DIRECTORY '
                   'LISTING'
           MOVE ' DEBUG REPORT - TRANSLATE IEHLIST DIRECTORY LISTING'
                                                 TO JX-DEBUG-TITLE.

           MOVE RULER-LINE(2:115)  TO DBH2-OUTPUT.
           MOVE WS-PROGRAM         TO JXRD-PROGRAM
                                      JXCD-PROGRAM
                                      HD2-PROGRAM.
           MOVE SPACE              TO DIR-DETAIL-LINE.

           MOVE 'CHANGEWIZ  STAT   REL 1.0 VER 1.0' TO FT1-PRODUCT.
           MOVE '(C) 2005 RENAISSANCE DATA SYSTEMS INC.'
                                                    TO FT1-VENDOR.

      *  CODE FOR MICROFOCUS COBOL - USE ASCII VALUES
           IF INST-ASCII
              MOVE WS-ASCII-APOST  TO WS-APOST
              MOVE WS-ASCII-QUOTE  TO WS-QUOTE
           END-IF.

      *  FOR EBCDIC DISPLAY VALUES
           MOVE WS-APOST        TO WS-HEX-LINE-1(125)
           MOVE WS-QUOTE        TO WS-HEX-LINE-1(127)
      *  FOR ASCII DISPLAY VALUES
           MOVE WS-ASCII-APOST  TO WS-HEX-LINE-1-ASCII(039)
           MOVE WS-ASCII-QUOTE  TO WS-HEX-LINE-1-ASCII(034)


      **  DISPLAY COMPILE DATE AND TIME
           COPY JXCPP010.

           DISPLAY ' '.

      **  PROCESS COMMON INPUT PARAMETER
           COPY JXCPP015.

      *+  INSTALLATION SPECIFIC INITIALIZATION ROUTINES
           COPY JXINP005.


           OPEN INPUT PDS-DIRECTORY-FILE.
           OPEN OUTPUT NEW-DIRECTORY-OUT

           IF JX-PRINT-DEBUG
               OPEN OUTPUT DEBUG-REPORT.

           IF PDS-DIR-STATUS NOT = '00' AND '97'
               DISPLAY '   BAD OPEN ON PDS DIR FILE. STATUS = '
                       PDS-DIR-STATUS
               MOVE 'Y' TO END-OF-PDSDIR-LINES-SW
               PERFORM Z9000-ABEND-PROGRAM THRU Z9000-EXIT
           ELSE
               PERFORM X1000-READ-PDS-DIR THRU X1000-EXIT.

      * CHECK STATUS OF OUTPUT FILE OPENS
           IF NEW-DIR-STATUS NOT = '00' AND '97'
               DISPLAY '   BAD OPEN ON ELEMENT XTRCT FILE. STATUS = '
                       NEW-DIR-STATUS
               PERFORM Z9000-ABEND-PROGRAM THRU Z9000-EXIT
           END-IF.


           IF END-OF-PDSDIR-LINES
               DISPLAY 'NO RECORDS IN PDS DIRECTORY FILE'
               GO TO A1000-EXIT
           END-IF.

      *+  INSTALLATION-SPECIFIC CODE TO DETERMINE DATE CUTOFF
           COPY JXINP010.

      * SKIP FIRST COUPLE LINES
           PERFORM UNTIL END-OF-PDSDIR-LINES OR
                         PDS-MESSAGE  = ' DIRECTORY'
               PERFORM X1000-READ-PDS-DIR THRU X1000-EXIT
           END-PERFORM.

      * THE NEXT LINE HAS THE PDS LIBRARY NAME
           IF NOT END-OF-PDSDIR-LINES
               PERFORM X1000-READ-PDS-DIR THRU X1000-EXIT
           END-IF.

           IF NOT END-OF-PDSDIR-LINES
              IF PDS-MESSAGE NOT = SPACE
                 MOVE PDS-DIR-RECORD(2:44) TO WK-PDS-NAME
                 DISPLAY ' DIRECTORY FOR: ' PDS-DIR-RECORD(2:45)
              END-IF
           END-IF.

           MOVE 'DIRECTORY FOR DATASET: '
                                   TO HD2-TITLE.
           MOVE WK-PDS-NAME        TO HEADING-2(66:37).

           PERFORM UNTIL END-OF-PDSDIR-LINES OR
                   PDS-ELEMENT-NAME = ' MEMBERS'
               PERFORM X1000-READ-PDS-DIR THRU X1000-EXIT
           END-PERFORM.

           IF NOT END-OF-PDSDIR-LINES
               PERFORM X1000-READ-PDS-DIR THRU X1000-EXIT
           END-IF.

       A1000-EXIT.
           EXIT.


      ***********************************************************
      *   END OF JOB PROCESSING
      ***********************************************************
       A9000-TERMINATION.

           MOVE SPACE          TO DIR-DETAIL-LINE.
           ADD 3 TO NEW-DIR-LINE-CNT
           IF NEW-DIR-LINE-CNT > INST-LINES-PER-PAGE
              SUBTRACT 3 FROM NEW-DIR-LINE-CNT
              PERFORM X3100-DIR-HEADING THRU X3100-EXIT
           ELSE
              SUBTRACT 3 FROM NEW-DIR-LINE-CNT
              PERFORM X3000-WRITE-ELEMENT THRU X3000-EXIT
              PERFORM X3000-WRITE-ELEMENT THRU X3000-EXIT
           END-IF.

           MOVE WRITE-NEW-DIR-CNT TO DRT-MEMBER-TOTAL.
           MOVE DIR-TOTAL-LINE TO DIR-DETAIL-LINE.
           PERFORM X3000-WRITE-ELEMENT THRU X3000-EXIT.

      **  FINISH DIRECTORY REPORT
           IF NEW-DIR-PAGE-CNT > 0
               PERFORM X3200-NEW-DIR-FOOTER THRU X3200-EXIT.

           CLOSE PDS-DIRECTORY-FILE
                 NEW-DIRECTORY-OUT.

      *  ENABLE TOTALS TO PRINT ON DEBUG REPORT
           IF JX-PRINT-DEBUG OR DEBUG-PAGE-CNT > 0
              ADD 15 TO JX-PRINT-DEBUG-MAX
              IF NOT JX-PRINT-DEBUG
                 MOVE 'Y' TO JX-DEBUG-SW
              END-IF
              ADD 15 TO DEBUG-RPT-LINE-CNT
              IF DEBUG-RPT-LINE-CNT > INST-LINES-PER-PAGE
                 SUBTRACT 15 FROM DEBUG-RPT-LINE-CNT
                 PERFORM Y1025-DEBUG-HEADING THRU Y1025-EXIT
              ELSE
                 SUBTRACT 15 FROM DEBUG-RPT-LINE-CNT
                 MOVE SPACES        TO DEBUG-RPT-LINE
                 PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
                 PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
              END-IF
           END-IF.

           DISPLAY ' '.
           DISPLAY ' '.
           DISPLAY '****************************************'
                   '****************************************'
           DISPLAY ' '.
           DISPLAY 'END OF PROGRAM JXCU011.'.
           DISPLAY ' '.


           IF JX-PRINT-DEBUG
               MOVE ALL '*'       TO DEBUG-RPT-LINE(2:125)
               PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
               MOVE SPACES        TO DEBUG-RPT-LINE
               PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
               MOVE ' END-OF-PROGRAM JXCU011  RECORD COUNTS' TO
                                     DEBUG-RPT-LINE
               PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
               MOVE SPACES        TO DEBUG-RPT-LINE
               PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
           END-IF.

           MOVE SPACE TO DISPLAY-TOTAL-LINE.

           MOVE 'PDS DIRECTORY RECORDS READ   ' TO DT-DESC.
           MOVE READ-PDS-DIR-CNT                TO DT-COUNT
           DISPLAY DISPLAY-TOTAL-LINE
           DISPLAY '   '
           IF JX-PRINT-DEBUG
              MOVE DISPLAY-TOTAL-LINE TO DEBUG-RPT-LINE
              PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
              MOVE SPACE              TO DEBUG-RPT-LINE
              PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
           END-IF.

           MOVE '    DIRECTORY MEMBERS        ' TO DT-DESC.
           MOVE WRITE-NEW-DIR-CNT               TO DT-COUNT
           DISPLAY DISPLAY-TOTAL-LINE
           DISPLAY '   '
           IF JX-PRINT-DEBUG
              MOVE DISPLAY-TOTAL-LINE TO DEBUG-RPT-LINE
              PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
              MOVE SPACE              TO DEBUG-RPT-LINE
              PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
           END-IF.


           DISPLAY ' '.
           DISPLAY '****************************************'
                   '****************************************'
           DISPLAY ' '.

           IF JX-PRINT-DEBUG
               MOVE SPACES        TO DEBUG-RPT-LINE
               PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
               MOVE ALL '*'       TO DEBUG-RPT-LINE(2:125)
               PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
               MOVE SPACES        TO DEBUG-RPT-LINE
               PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
           END-IF.



      **  FINISH DEBUG REPORT
           IF DEBUG-PAGE-CNT > 0
               PERFORM Y1050-DEBUG-FOOTER THRU Y1050-EXIT.

           IF JX-PRINT-DEBUG OR DEBUG-PAGE-CNT > 0
               CLOSE DEBUG-REPORT.

       A9000-EXIT.
           EXIT.
           EJECT
      ************************************************************
      *  WRITE OUT AT LEAST ONE RECORD FOR EACH ELEMENT.
      *  WRITE A RECORD FOR EACH CHANGE CODE
      ************************************************************
       C1000-PROCESS-PDS-DIR-FILE.

           IF PDS-ELEMENT-NAME = SPACE
              GO TO C1000-READ-NEXT
           END-IF.

      * LAST RECORD WILL HAVE MESSAGE "OF THE XXXX DIRECTORY BLOCKS "
           IF PDS-MESSAGE NOT = SPACE
              GO TO C1000-READ-NEXT
           END-IF.

           IF JX-PRINT-DEBUG
              MOVE SPACE TO DIR-DETAIL-DEBUG-LINE
              MOVE PDS-ELEMENT-NAME      TO DRDBG-ELEMENT-NAME
              MOVE PDS-TTRC              TO DRDBG-TTR
              MOVE PDS-VER-MOD           TO DRDBG-VER-MOD
              MOVE PDS-CREATE-DATE-CC    TO DRDBG-CREATE-DATE-CC
              MOVE PDS-CREATE-DATE-YY    TO DRDBG-CREATE-DATE-YY
              MOVE PDS-CREATE-DATE-DDD   TO DRDBG-CREATE-DATE-DDD
              MOVE PDS-CREATE-DATE-SIGN  TO DRDBG-CREATE-DATE-SIGN
              MOVE PDS-CHANGE-DATE-CC    TO DRDBG-CHANGE-DATE-CC
              MOVE PDS-CHANGE-DATE-YY    TO DRDBG-CHANGE-DATE-YY
              MOVE PDS-CHANGE-DATE-DDD   TO DRDBG-CHANGE-DATE-DDD
              MOVE PDS-CHANGE-DATE-SIGN  TO DRDBG-CHANGE-DATE-SIGN
              MOVE PDS-CHANGE-TIME-HH    TO DRDBG-CHANGE-TIME-HH
              MOVE PDS-CHANGE-TIME-MM    TO DRDBG-CHANGE-TIME-MM
              MOVE PDS-CHANGE-TIME-SS    TO DRDBG-CHANGE-TIME-SS
              MOVE PDS-TOTAL-LINES-1     TO DRDBG-TOTAL-LINES-1
              MOVE PDS-TOTAL-LINES-2     TO DRDBG-TOTAL-LINES-2
              MOVE PDS-INITIAL-LINES     TO DRDBG-INITIAL-LINES
              MOVE PDS-CHANGED-LINES     TO DRDBG-CHANGED-LINES
              MOVE PDS-EDIT-BY-1         TO DRDBG-EDIT-BY-1
              MOVE PDS-EDIT-BY-2         TO DRDBG-EDIT-BY-2
              MOVE DIR-DETAIL-DEBUG-LINE TO DEBUG-RPT-LINE
              PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
           END-IF.

      * WRITE OUT ALL ELEMENTS TO EXTRACT FILE

           ADD 1 TO WRITE-NEW-DIR-CNT.
           MOVE SPACE TO DIR-DETAIL-LINE.

           MOVE PDS-ELEMENT-NAME     TO DR-MEMBER-NAME

           MOVE PDS-TTRC(1:6)        TO DR-TTR.

           IF PDS-VER-MOD = SPACE OR '0000'
              GO TO C1000-WRITE-NEW-DIR
           END-IF.

      * CONVERT HEXDATA VERSION-MOD TO NUMBERS
           MOVE ZERO                 TO WK-VERSION
           MOVE ZERO                 TO WK-MOD
           MOVE PDS-VER-MOD(1:1)     TO WK-ORIG-BYTE
           PERFORM D1000-CONVERT-HEXDATA THRU D1000-EXIT
           COMPUTE WK-COMPUTE = WK-BYTE-VALUE * 16

           MOVE PDS-VER-MOD(2:1)     TO WK-ORIG-BYTE
           PERFORM D1000-CONVERT-HEXDATA THRU D1000-EXIT
           ADD  WK-BYTE-VALUE        TO WK-COMPUTE
           MOVE WK-COMPUTE           TO WK-VERSION

           MOVE PDS-VER-MOD(3:1)     TO WK-ORIG-BYTE
           PERFORM D1000-CONVERT-HEXDATA THRU D1000-EXIT
           COMPUTE WK-COMPUTE = WK-BYTE-VALUE * 16

           MOVE PDS-VER-MOD(4:1)     TO WK-ORIG-BYTE
           PERFORM D1000-CONVERT-HEXDATA THRU D1000-EXIT
           ADD  WK-BYTE-VALUE        TO WK-COMPUTE
           MOVE WK-COMPUTE           TO WK-MOD

           MOVE WK-VERSION-MOD       TO DR-VER-MOD


      * CONVERT HEXDATA CURRENT NUMBER OF LINES TO A NUMERIC VALUE
           IF (PDS-TOTAL-LINES-1 NOT = SPACE) AND
              (PDS-TOTAL-LINES-2 NOT = SPACE)
              MOVE ZERO                 TO DR-CURRENT-LINES
              MOVE PDS-TOTAL-LINES-1(1:1) TO WK-ORIG-BYTE
              PERFORM D1000-CONVERT-HEXDATA THRU D1000-EXIT
              COMPUTE WK-COMPUTE = WK-BYTE-VALUE * 4096
              MOVE PDS-TOTAL-LINES-1(2:1) TO WK-ORIG-BYTE
              PERFORM D1000-CONVERT-HEXDATA THRU D1000-EXIT
              COMPUTE WK-COMPUTE = (WK-BYTE-VALUE * 256) +
                                    WK-COMPUTE

              MOVE PDS-TOTAL-LINES-2(1:1) TO WK-ORIG-BYTE
              PERFORM D1000-CONVERT-HEXDATA THRU D1000-EXIT
              COMPUTE WK-COMPUTE = (WK-BYTE-VALUE * 16) +
                                       WK-COMPUTE
              MOVE PDS-TOTAL-LINES-2(2:1) TO WK-ORIG-BYTE
              PERFORM D1000-CONVERT-HEXDATA THRU D1000-EXIT
              ADD  WK-BYTE-VALUE        TO WK-COMPUTE
              MOVE WK-COMPUTE           TO DR-CURRENT-LINES
           END-IF.


      * CONVERT HEXDATA INITIAL NUMBER OF LINES TO A NUMERIC VALUE
           IF PDS-INITIAL-LINES NOT = SPACE
              MOVE ZERO                 TO DR-INITIAL-LINES
              MOVE PDS-INITIAL-LINES(1:1) TO WK-ORIG-BYTE
              PERFORM D1000-CONVERT-HEXDATA THRU D1000-EXIT
              COMPUTE WK-COMPUTE = WK-BYTE-VALUE * 4096

              MOVE PDS-INITIAL-LINES(2:1) TO WK-ORIG-BYTE
              PERFORM D1000-CONVERT-HEXDATA THRU D1000-EXIT
              COMPUTE WK-COMPUTE = (WK-BYTE-VALUE * 256) +
                                          WK-COMPUTE

              MOVE PDS-INITIAL-LINES(3:1) TO WK-ORIG-BYTE
              PERFORM D1000-CONVERT-HEXDATA THRU D1000-EXIT
              COMPUTE WK-COMPUTE = (WK-BYTE-VALUE * 16) +
                                          WK-COMPUTE
              MOVE PDS-INITIAL-LINES(4:1) TO WK-ORIG-BYTE
              PERFORM D1000-CONVERT-HEXDATA THRU D1000-EXIT
              ADD  WK-BYTE-VALUE        TO WK-COMPUTE

              MOVE WK-COMPUTE           TO DR-INITIAL-LINES
           END-IF.



      * CONVERT HEXDATA CHANGED NUMBER OF LINES TO A NUMERIC VALUE
           IF PDS-CHANGED-LINES NOT = SPACE
              MOVE ZERO                 TO DR-CHANGED-LINES
              MOVE PDS-CHANGED-LINES(1:1) TO WK-ORIG-BYTE
              PERFORM D1000-CONVERT-HEXDATA THRU D1000-EXIT
              COMPUTE WK-COMPUTE = WK-BYTE-VALUE * 4096

              MOVE PDS-CHANGED-LINES(2:1) TO WK-ORIG-BYTE
              PERFORM D1000-CONVERT-HEXDATA THRU D1000-EXIT
              COMPUTE WK-COMPUTE = (WK-BYTE-VALUE * 256) +
                                             WK-COMPUTE

              MOVE PDS-CHANGED-LINES(3:1) TO WK-ORIG-BYTE
              PERFORM D1000-CONVERT-HEXDATA THRU D1000-EXIT
              COMPUTE WK-COMPUTE = (WK-BYTE-VALUE * 16) +
                                             WK-COMPUTE
              MOVE PDS-CHANGED-LINES(4:1) TO WK-ORIG-BYTE
              PERFORM D1000-CONVERT-HEXDATA THRU D1000-EXIT
              ADD  WK-BYTE-VALUE        TO WK-COMPUTE

              MOVE WK-COMPUTE           TO DR-CHANGED-LINES
           END-IF.

      * BRING PARTS OF MODIFY DATE TOGETHER AND DETERMINE CENTURY
      * CONVERT TO GREGORIAN FORMAT

           IF PDS-CHANGE-DATE-CC = SPACE OR
              PDS-CHANGE-DATE-YY = SPACE OR
              PDS-CHANGE-DATE-DDD = SPACE
              NEXT SENTENCE
           ELSE
              MOVE SPACE                TO WK-IBM-DATE
              IF PDS-CHANGE-DATE-CC = '00'
                 MOVE '19'              TO WK-IBM-DATE-CCX
              END-IF
              IF PDS-CHANGE-DATE-CC = '01'
                 MOVE '20'              TO WK-IBM-DATE-CCX
              END-IF
              MOVE PDS-CHANGE-DATE-YY   TO WK-IBM-DATE-YYX
              MOVE PDS-CHANGE-DATE-DDD  TO WK-IBM-DATE-DDDX
              PERFORM W1000-FORMAT-IBM-DATE
                 THRU W1000-EXIT
              MOVE WK-PDS-DIR-DATE      TO DR-CHANGE-DATE
           END-IF.

      * FORMAT CHANGE TIME
           IF PDS-CHANGE-TIME-HH = SPACE OR
              PDS-CHANGE-TIME-MM = SPACE OR
              PDS-CHANGE-TIME-SS = SPACE
              NEXT SENTENCE
           ELSE
              MOVE '00:00:00'              TO WK-PDS-DIR-TIME
              MOVE PDS-CHANGE-TIME-HH      TO WK-PDS-DIR-HH
              MOVE PDS-CHANGE-TIME-MM      TO WK-PDS-DIR-MIN
              MOVE PDS-CHANGE-TIME-SS      TO WK-PDS-DIR-SS
              MOVE WK-PDS-DIR-TIME         TO DR-CHANGE-TIME
           END-IF.

      * BRING PARTS OF CREATE DATE TOGETHER AND DETERMINE CENTURY
      * CONVERT TO GREGORIAN FORMAT

           IF PDS-CREATE-DATE-CC = SPACE OR
              PDS-CREATE-DATE-YY = SPACE OR
              PDS-CREATE-DATE-DDD = SPACE
              NEXT SENTENCE
           ELSE
              MOVE SPACE                TO WK-IBM-DATE
              IF PDS-CREATE-DATE-CC = '00'
                 MOVE '19'              TO WK-IBM-DATE-CCX
              END-IF
              IF PDS-CREATE-DATE-CC = '01'
                 MOVE '20'              TO WK-IBM-DATE-CCX
              END-IF
              MOVE PDS-CREATE-DATE-YY   TO WK-IBM-DATE-YYX
              MOVE PDS-CREATE-DATE-DDD  TO WK-IBM-DATE-DDDX
              PERFORM W1000-FORMAT-IBM-DATE
                 THRU W1000-EXIT
              MOVE WK-PDS-DIR-DATE      TO DR-CREATE-DATE
           END-IF.


      * TRANSLATE USER ID TO CHARACTER FORMAT
           MOVE ZERO TO DSUB.
           MOVE SPACE TO DR-USER-ID

           IF PDS-EDIT-BY-1 NOT = SPACE
              PERFORM VARYING HSUB FROM 1 BY 2
                      UNTIL HSUB > 10
                  MOVE PDS-EDIT-BY-1(HSUB:2)  TO
                                            WK-HEXDATA-DISPLAY
                  PERFORM D2000-FIND-CHAR THRU D2000-EXIT
                  ADD 1 TO DSUB
                  MOVE WK-CHAR-DISPLAY TO DR-USER-ID(DSUB:1)
              END-PERFORM
           END-IF.

           IF PDS-EDIT-BY-2 NOT = SPACE
              PERFORM VARYING HSUB FROM 1 BY 2
                      UNTIL HSUB > 10
                  MOVE PDS-EDIT-BY-2(HSUB:2)  TO
                                           WK-HEXDATA-DISPLAY
                  PERFORM D2000-FIND-CHAR THRU D2000-EXIT
                  ADD 1 TO DSUB
                  MOVE WK-CHAR-DISPLAY TO DR-USER-ID(DSUB:1)
              END-PERFORM
           END-IF.


       C1000-WRITE-NEW-DIR.

           PERFORM X3000-WRITE-ELEMENT THRU X3000-EXIT.

       C1000-READ-NEXT.
           PERFORM X1000-READ-PDS-DIR THRU X1000-EXIT.

       C1000-EXIT.
           EXIT.

      ***************************************************************
      *  CONVERT DISPLAY HEX TO NUMERIC VALUES
      ***************************************************************
       D1000-CONVERT-HEXDATA.
           MOVE ZERO                 TO WK-BYTE-VALUE
           IF WK-ORIG-BYTE = 'A'
              MOVE 10 TO WK-BYTE-VALUE
           ELSE
           IF WK-ORIG-BYTE = 'B'
              MOVE 11 TO WK-BYTE-VALUE
           ELSE
           IF WK-ORIG-BYTE = 'C'
              MOVE 12 TO WK-BYTE-VALUE
           ELSE
           IF WK-ORIG-BYTE = 'D'
              MOVE 13 TO WK-BYTE-VALUE
           ELSE
           IF WK-ORIG-BYTE = 'E'
              MOVE 14 TO WK-BYTE-VALUE
           ELSE
           IF WK-ORIG-BYTE = 'F'
              MOVE 15 TO WK-BYTE-VALUE
           ELSE
              MOVE WK-ORIG-BYTE-9 TO WK-BYTE-VALUE
           END-IF.


       D1000-EXIT.
           EXIT.

      ***************************************************************
      *  CHANGE DISPLAY HEX TO CHARACTER FORMAT
      ***************************************************************
       D2000-FIND-CHAR.

           MOVE SPACE TO WK-CHAR-DISPLAY.

           IF WK-HEXDATA-DISPLAY = '40'
              GO TO D2000-EXIT
           END-IF.

           PERFORM VARYING SSUB FROM 1 BY 1
                   UNTIL SSUB > 256 OR
                   WS-HEXDATA-DISP(SSUB) = WK-HEXDATA-DISPLAY
           END-PERFORM.

           IF SSUB < 257 AND
              WS-HEXDATA-DISP(SSUB) = WK-HEXDATA-DISPLAY
              IF INST-EBCDIC
                 MOVE WS-HEX-LINE-1(SSUB) TO WK-CHAR-DISPLAY
              ELSE
                 MOVE WS-HEX-LINE-1-ASCII(SSUB) TO WK-CHAR-DISPLAY
              END-IF
           END-IF.

       D2000-EXIT.
           EXIT.

           EJECT
      ***************************************************************
      *  FORMAT PDS-DIR DATES - GET GREGORIAN DATE
      ***************************************************************
       W1000-FORMAT-IBM-DATE.

           MOVE ZERO                   TO JX-CONV-GREG-DATE
           MOVE ZERO                   TO JX-CONV-JUL-DATE
           MOVE '0000/00/00'           TO WK-PDS-DIR-DATE
           MOVE '0000/000'             TO WK-PDS-DIR-JULDATE

           IF WK-IBM-DATE = SPACE
              GO TO W1000-EXIT
           END-IF.

           MOVE WK-IBM-DATE-CCX     TO JX-CJUL-CC
           MOVE WK-IBM-DATE-YYX     TO JX-CJUL-YY
           MOVE WK-IBM-DATE-DDDX    TO JX-CJUL-DDD

           IF JX-CONV-JUL-DATE = ZERO
              GO TO W1000-EXIT
           END-IF.

           PERFORM W5200-CONVERT-JULIAN-TO-GREG
              THRU W5200-EXIT.

           IF JXCDE-RETURN-CD = 0
               MOVE JX-CJUL-CC           TO WK-PDSDIR-JUL-CC
               MOVE JX-CJUL-YY           TO WK-PDSDIR-JUL-YY
               MOVE JX-CJUL-DDD          TO WK-PDSDIR-JUL-DDD
               MOVE JX-CGREG-CC          TO WK-PDS-DIR-CC
               MOVE JX-CGREG-YY          TO WK-PDS-DIR-YY
               MOVE JX-CGREG-MM          TO WK-PDS-DIR-MM
               MOVE JX-CGREG-DD          TO WK-PDS-DIR-DD
           ELSE
               MOVE ' INVALID DATE FOR ELEMENT '
                                  TO DEBUG-RPT-LINE
               MOVE PDS-ELEMENT-NAME   TO DEBUG-RPT-LINE(27:8)
               DISPLAY DEBUG-RPT-LINE(1:40)
               DISPLAY JX-CONV-DATE-ERROR
               IF JX-PRINT-DEBUG
                  PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
                  MOVE JX-CONV-DATE-ERROR   TO DEBUG-RPT-LINE
                  PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
               END-IF
               GO TO W1000-EXIT
           END-IF.


       W1000-EXIT.
           EXIT.

           EJECT
      *  COMMON DATE CONVERSION ROUTINES
           COPY JXCPP025.

           EJECT
      ***************************************************************
      *  READ PDS-DIR FILE
      ***************************************************************
       X1000-READ-PDS-DIR.

           READ PDS-DIRECTORY-FILE INTO PDS-DIR-RECORD.

           IF PDS-DIR-STATUS = '10'
               MOVE 'Y' TO END-OF-PDSDIR-LINES-SW
               GO TO X1000-EXIT.

           ADD 1           TO READ-PDS-DIR-CNT.
           IF PDS-DIR-STATUS NOT = '00'
               DISPLAY '  BAD READ ON PDS-DIR FILE. STATUS = '
                   PDS-DIR-STATUS ' ON RECORD NBR ' READ-PDS-DIR-CNT
               GO TO X1000-EXIT.


           IF JX-PRINT-DEBUG
              MOVE SPACES               TO DEBUG-RPT-LINE
              MOVE PDS-DIR-RECORD       TO DEBUG-RPT-LINE(2:132)
              PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
           END-IF.

       X1000-EXIT.
           EXIT.

      ***************************************************************
      *  WRITE AN ELEMENT EXTRACT RECORD.
      ***************************************************************
       X3000-WRITE-ELEMENT.

           IF NEW-DIR-LINE-CNT > INST-LINES-PER-PAGE
              PERFORM X3100-DIR-HEADING THRU X3100-EXIT
           END-IF.

           WRITE NEW-DIRECTORY-REC FROM DIR-DETAIL-LINE.
           ADD 1 TO NEW-DIR-LINE-CNT.

           IF JX-PRINT-DEBUG
               MOVE DIR-DETAIL-LINE      TO DEBUG-RPT-LINE
               PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
               MOVE SPACE                TO DEBUG-RPT-LINE
               PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
           END-IF.

       X3000-EXIT.
           EXIT.

      ***************************************************************
      ***  PRINT HEADING FOR DIRECTORY REPORT
      ***************************************************************
       X3100-DIR-HEADING.

           ADD 1                   TO NEW-DIR-PAGE-CNT.
           MOVE NEW-DIR-PAGE-CNT   TO HD1-PAGE.
           MOVE '01'               TO HD2-REPORT.

           MOVE INST-COMPANY-NAME  TO HD1-COMPANY-NAME.


           IF NEW-DIR-PAGE-CNT > 1
               PERFORM X3200-NEW-DIR-FOOTER THRU X3200-EXIT.

           WRITE NEW-DIRECTORY-REC FROM HEADING-1
               AFTER ADVANCING PAGE.
           WRITE NEW-DIRECTORY-REC FROM HEADING-2
               AFTER ADVANCING 1 LINE.
           WRITE NEW-DIRECTORY-REC FROM DIR-HEADER-1
               AFTER ADVANCING 2 LINE.
           WRITE NEW-DIRECTORY-REC FROM DIR-HEADER-2
               AFTER ADVANCING 1 LINE.
           MOVE SPACES TO NEW-DIRECTORY-REC
           WRITE NEW-DIRECTORY-REC
               AFTER ADVANCING 1 LINE.
           MOVE 6 TO NEW-DIR-LINE-CNT.

       X3100-EXIT.
           EXIT.

       X3200-NEW-DIR-FOOTER.
           MOVE SPACES TO NEW-DIRECTORY-REC.

           PERFORM UNTIL NEW-DIR-LINE-CNT > INST-LINES-PER-PAGE
               WRITE NEW-DIRECTORY-REC
                   AFTER ADVANCING 1 LINE
               ADD 1 TO NEW-DIR-LINE-CNT
           END-PERFORM.

           WRITE NEW-DIRECTORY-REC
               AFTER ADVANCING 2 LINE.
           WRITE NEW-DIRECTORY-REC FROM FOOTER-1
               AFTER ADVANCING 1 LINE.

       X3200-EXIT.
           EXIT.


           EJECT
      ***************************************************************
      ***  PRINT ROUTINES
      ***************************************************************

       Y1000-PRINT-DEBUG.
           ADD 1 TO JX-PRINT-DEBUG-CNT.
           IF JX-PRINT-DEBUG-CNT > JX-PRINT-DEBUG-SKIP
               IF DEBUG-RPT-LINE-CNT > INST-LINES-PER-PAGE
                   MOVE DEBUG-RPT-LINE TO HOLD-PRINT-LINE
                   PERFORM Y1025-DEBUG-HEADING THRU Y1025-EXIT
                   MOVE HOLD-PRINT-LINE TO DEBUG-RPT-LINE.

           IF JX-PRINT-DEBUG-CNT > JX-PRINT-DEBUG-SKIP
               WRITE DEBUG-RPT-LINE
                   AFTER ADVANCING 1 LINE
               ADD 1 TO DEBUG-RPT-LINE-CNT.

           IF JX-PRINT-DEBUG-CNT > JX-PRINT-DEBUG-MAX
               MOVE 'N' TO JX-DEBUG-SW.
           MOVE SPACES TO DEBUG-RPT-LINE.
       Y1000-EXIT.
           EXIT.

       Y1025-DEBUG-HEADING.
           ADD 1                   TO DEBUG-PAGE-CNT.
           MOVE DEBUG-PAGE-CNT     TO HD1-PAGE.
           MOVE 'DB'               TO HD2-REPORT.

           MOVE '            CHANGEWIZ DEBUGGING REPORT           '
                                   TO HD1-COMPANY-NAME

           IF DEBUG-PAGE-CNT > 1
               PERFORM Y1050-DEBUG-FOOTER THRU Y1050-EXIT.

           WRITE DEBUG-RPT-LINE FROM HEADING-1
               AFTER ADVANCING PAGE.
           WRITE DEBUG-RPT-LINE FROM HEADING-2
               AFTER ADVANCING 1 LINE.
           WRITE DEBUG-RPT-LINE FROM DIR-HEADER-1
               AFTER ADVANCING 2 LINE.
           WRITE DEBUG-RPT-LINE FROM DIR-HEADER-2
               AFTER ADVANCING 1 LINE.
      *    WRITE DEBUG-RPT-LINE FROM DEBUG-HEADING-1
      *        AFTER ADVANCING 2 LINE.
      *    WRITE DEBUG-RPT-LINE FROM DEBUG-HEADING-2
      *        AFTER ADVANCING 1 LINE.
           WRITE DEBUG-RPT-LINE FROM RULER-LINE
               AFTER ADVANCING 1 LINE.
           MOVE SPACES TO DEBUG-RPT-LINE
           WRITE DEBUG-RPT-LINE
               AFTER ADVANCING 1 LINE.
           MOVE 7 TO DEBUG-RPT-LINE-CNT.
       Y1025-EXIT.
           EXIT.

       Y1050-DEBUG-FOOTER.
           MOVE SPACES TO DEBUG-RPT-LINE.

           PERFORM UNTIL DEBUG-RPT-LINE-CNT > INST-LINES-PER-PAGE
               WRITE DEBUG-RPT-LINE
                   AFTER ADVANCING 1 LINE
               ADD 1 TO DEBUG-RPT-LINE-CNT
           END-PERFORM.

           WRITE DEBUG-RPT-LINE
               AFTER ADVANCING 2 LINE.
           WRITE DEBUG-RPT-LINE FROM FOOTER-1
               AFTER ADVANCING 1 LINE.

       Y1050-EXIT.
           EXIT.

           EJECT
      *  COMMON ABEND ROUTINE
           COPY JXCPP099.

