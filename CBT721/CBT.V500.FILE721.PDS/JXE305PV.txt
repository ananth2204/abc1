 PARM LINK (JXE305PV R)
*******************************************************************
*      PROGRAM JXE305PV
*JX*  UPDATE THE PANVALET HISTORY FILE
*      (C) 2005 RENAISSANCE DATA SYSTEMS INC.
*          REFWIZ  JXREF VER 5.0
*
*     THIS DOES NOT USE THE LAYOUT FOR CHANGE HISTORY FILE JXCPR300.
*******************************************************************
*  UPDATE THE CHANGE HISTORY FILE
******************************************************************
 FILE RPT01 PRINTER

*  DATES IN OLD HISTORY FILE WITH LATEST DATE FIRST
*  ONLY FIRST DATE IS READ
 FILE PANDATE
  PD-REC              1 133 A
  PD-RPT-DATE       122  10 A

* PREVIOUS PANVALET HISTORY FILE
* ALL RECORDS IN THIS FILE ARE WRITTEN TO NEW FILE
 FILE PANOLD
  PO-REC              1 133 A
  PO-NAME             1  10 A   HEADING  (' NAME     ')

* EXTRACT FROM CURRENT PANVALET DIRECTORY REPORT
* ONLY RECORDS THAT ARE DIFFERENT FROM PREVIOUS REPORTS WILL
* BE WRITTEN TO THE HISTORY FILE
 FILE PANNEW
  PN-REC              1 133 A
  PN-NAME             1  10 A   HEADING  (' NAME     ')
  PN-LEVEL           12   4 A   HEADING  (' LVL')
  PN-TYPE            26  12 A   HEADING  (' TYPE       ')
  PN-STATUS          38   6 A   HEADING ('STATUS')
  PN-STAT-1          38   1 A
  PN-STAT-2          40   1 A
  PN-STAT-3          42   1 A
  PN-MAINT-DATE      52  10 A   HEADING  ('DATE' 'MAINTAINED')
  PN-MAINT-CC        52   2 A
  PN-MAINT-YY        54   2 A
  PN-MAINT-SL1       56   1 A
  PN-MAINT-MM        57   2 A
  PN-MAINT-SL2       59   1 A
  PN-MAINT-DD        60   2 A
  PN-NBR-STMT        83   7 A   HEADING ('   NBR' '   STMT')
  PN-RPT-DATE       122  10 A

*  UPDATED PANVALET HISTORY FILE
 FILE PANMRG     FB(133  27930)
  PM-REC              1 133 A
  PM-NAME             1  10 A   HEADING  (' NAME     ')
  PM-LEVEL           12   4 A   HEADING  (' LVL')
  PM-TYPE            26  12 A   HEADING  (' TYPE       ')
  PM-STATUS          38   6 A   HEADING ('STATUS')
  PM-STAT-1          38   1 A
  PM-STAT-2          40   1 A
  PM-STAT-3          42   1 A
  PM-MAINT-DATE      52  10 A   HEADING  ('DATE' 'MAINTAINED')
  PM-MAINT-CC        52   2 A
  PM-MAINT-YY        54   2 A
  PM-MAINT-SL1       56   1 A
  PM-MAINT-MM        57   2 A
  PM-MAINT-SL2       59   1 A
  PM-MAINT-DD        60   2 A
  PM-NBR-STMT        83   7 A   HEADING ('   NBR' '   STMT')
  PM-RPT-DATE       122  10 A

  WS-REC-WORK               W   133 A
  WS-RWORK        WS-REC-WORK   133 A
  WS-NAME         WS-RWORK       10 A   HEADING  (' NAME     ')

*  BECAUSE THERE CAN BE SEVERAL RECORDS FOR ONE PANVALET ELEMENT
*  ALL RECORDS FOR AN ELEMENT ARE READ AND ONLY THE LATEST IS USED
*  TO COMPARE TO THE CURRENT EXTRACT
  PW-REC-WORK               W   133 A
  PW-RWORK        PW-REC-WORK   133 A
  PW-NAME         PW-RWORK       10 A   HEADING  (' NAME     ')
  PW-LEVEL        PW-RWORK +11    4 A   HEADING  (' LVL')
  PW-TYPE         PW-RWORK +25   12 A   HEADING  (' TYPE       ')
  PW-STATUS       PW-RWORK +37    6 A   HEADING ('STATUS')
  PW-STAT-1       PW-RWORK +37    1 A
  PW-STAT-2       PW-RWORK +39    1 A
  PW-STAT-3       PW-RWORK +41    1 A
  PW-MAINT-DATE   PW-RWORK +51   10 A   HEADING  ('DATE' 'MAINTAINED')
  PW-MAINT-CC     PW-RWORK +51    2 A
  PW-MAINT-YY     PW-RWORK +53    2 A
  PW-MAINT-SL1    PW-RWORK +55    1 A
  PW-MAINT-MM     PW-RWORK +56    2 A
  PW-MAINT-SL2    PW-RWORK +58    1 A
  PW-MAINT-DD     PW-RWORK +59    2 A
  PW-NBR-STMT     PW-RWORK +82    7 A   HEADING ('   NBR' '   STMT')
  PW-RPT-DATE     PW-RWORK +121   10 A


 OLD-NAME    W  10 A  HEADING ('NAME    ')
 NEW-NAME    W  10 A  HEADING ('NAME    ')

 WSN-STATUS   W   6 A  HEADING ('STATUS')
 WSO-STATUS   W   6 A  HEADING ('STATUS')

 RP-RPT-DATE      W  10 A
 NEW-RPT-DATE     W  10 A

 WS-DATE          W  10 A
  WS-CC   WS-DATE     2 A
  WS-YY   WS-DATE +2  2 A
  WS-SL1  WS-DATE +4  1 A
  WS-MM   WS-DATE +5  2 A
  WS-SL2  WS-DATE +7  1 A
  WS-DD   WS-DATE +8  2 A

 WS-TIME          W   5 A
  WS-HH   WS-TIME     2 A
  WS-TT1  WS-TIME +2  1 A
  WS-MIN  WS-TIME +3  2 A

 FIRST-REC-SW     W   1 A  VALUE 'Y'

 RP-LINE          W  85 A
  RP-NAME       RP-LINE        10 A
  RP-TYPE1      RP-LINE   +12  12 A
  RP-TYPE2      RP-LINE   +26   4 A
  RP-LEVEL      RP-LINE   +32   4 A
  RP-TYPE       RP-LINE   +39  12 A
  RP-STATUS     RP-LINE   +53   6 A
  RP-MAINT-DATE RP-LINE   +60  10 A
  RP-NBR-STMT   RP-LINE   +72   7 A

 RP-LINE2         W  85 A
  RP-LIT1       RP-LINE2  +12  10 A
  RP-CNT        RP-LINE2  +27   7 A

 WS-CHANGE-CNT    W  7 N
 WS-DELETE-CNT    W  7 N
 WS-NEW-CNT       W  7 N
****************************************************************
*  WRITE ALL OLD PAN REPORT RECORDS
*  WRITE NEW PAN RECORDS IF THEY ARE DIFFERENT OR IF
*    THEY DO NOT EXIST IN THE OLD PAN REPORT FILE
****************************************************************
JOB INPUT(PANNEW)  START(START-JOB)  FINISH(PRT-OLD)

 NEW-NAME = PN-NAME
 WSN-STATUS = PN-STATUS

 IF FIRST-REC-SW = 'Y'
    FIRST-REC-SW = 'N'
    NEW-RPT-DATE = PN-RPT-DATE
 END-IF

SKIP-READ-NEW.
 IF EOF PANOLD
     IF OLD-NAME = NEW-NAME
         IF PW-LEVEL NE PN-LEVEL
             RP-LINE       = '  '
             RP-NAME       = OLD-NAME
             RP-TYPE1      = 'CHANGED'
             RP-TYPE2      = 'OLD'
             RP-LEVEL      = PW-LEVEL
             RP-TYPE       = PW-TYPE
             RP-STATUS     = WSO-STATUS
             RP-MAINT-DATE = PW-MAINT-DATE
             RP-NBR-STMT   = PW-NBR-STMT
             PRINT RPT1
             RP-LINE       = '  '
             RP-NAME       = NEW-NAME
             RP-TYPE1      = '       '
             RP-TYPE2      = 'NEW'
             RP-LEVEL      = PN-LEVEL
             RP-TYPE       = PN-TYPE
             RP-STATUS     = WSN-STATUS
             RP-MAINT-DATE = PN-MAINT-DATE
             RP-NBR-STMT   = PN-NBR-STMT
             PRINT RPT1
             RP-LINE       = '  '
             PRINT RPT1
             WS-CHANGE-CNT = WS-CHANGE-CNT + 1
             PM-REC  = PN-REC
             PUT PANMRG
         END-IF
     END-IF
     IF OLD-NAME NE NEW-NAME
         RP-LINE       = '  '
         RP-NAME       = NEW-NAME
         RP-TYPE1      = 'NEW ELEMENT'
         RP-TYPE2      = '   '
         RP-LEVEL      = PN-LEVEL
         RP-TYPE       = PN-TYPE
         RP-STATUS     = WSN-STATUS
         RP-MAINT-DATE = PN-MAINT-DATE
         RP-NBR-STMT   = PN-NBR-STMT
         PRINT RPT1
         RP-LINE       = '  '
         PRINT RPT1
         PM-REC  = PN-REC
         PUT PANMRG
         WS-NEW-CNT = WS-NEW-CNT + 1
     END-IF
     GO TO JOB
 END-IF

* WRITE CURR RECORDS IF THEY ARE DIFFERENT
 IF OLD-NAME = NEW-NAME
     IF PW-LEVEL NE PN-LEVEL
         RP-LINE       = '  '
         RP-NAME       = OLD-NAME
         RP-TYPE1      = 'CHANGED'
         RP-TYPE2      = 'OLD'
         RP-LEVEL      = PW-LEVEL
         RP-TYPE       = PW-TYPE
         RP-STATUS     = WSO-STATUS
         RP-MAINT-DATE = PW-MAINT-DATE
         RP-NBR-STMT   = PW-NBR-STMT
         PRINT RPT1
         RP-LINE       = '  '
         RP-NAME       = NEW-NAME
         RP-TYPE1      = '       '
         RP-TYPE2      = 'NEW'
         RP-LEVEL      = PN-LEVEL
         RP-TYPE       = PN-TYPE
         RP-STATUS     = WSN-STATUS
         RP-MAINT-DATE = PN-MAINT-DATE
         RP-NBR-STMT   = PN-NBR-STMT
         PRINT RPT1
         RP-LINE       = '  '
         PRINT RPT1
         PM-REC  = PN-REC
         PUT PANMRG
         WS-CHANGE-CNT = WS-CHANGE-CNT + 1
     END-IF
     PERFORM GET-PANOLD
     GO TO JOB
 END-IF

 IF OLD-NAME < NEW-NAME
     DO WHILE OLD-NAME < NEW-NAME AND (NOT EOF PANOLD)
        IF PW-STATUS NE 'DELETE'
           RP-LINE       = '  '
           RP-NAME       = OLD-NAME
           RP-TYPE1      = 'DELETED'
           RP-TYPE2      = '   '
           RP-LEVEL      = PW-LEVEL
           RP-TYPE       = PW-TYPE
           RP-STATUS     = WSO-STATUS
           RP-MAINT-DATE = PW-MAINT-DATE
           RP-NBR-STMT   = PW-NBR-STMT
           PRINT RPT1
           RP-LINE       = '  '
           RP-NAME       = OLD-NAME
           RP-TYPE1      = '       '
           RP-TYPE2      = '   '
           RP-TYPE       = PW-TYPE
           RP-STATUS     = 'DELETE'
           RP-MAINT-DATE = NEW-RPT-DATE
           RP-NBR-STMT   = PW-NBR-STMT
           PRINT RPT1
           RP-LINE       = '  '
           PRINT RPT1
           PM-REC  = PW-REC-WORK
           PM-STATUS = 'DELETE'
           PM-MAINT-DATE = NEW-RPT-DATE
           PM-RPT-DATE = NEW-RPT-DATE
           PUT PANMRG
           WS-DELETE-CNT = WS-DELETE-CNT + 1
        END-IF
        PERFORM GET-PANOLD
     END-DO
     GO TO SKIP-READ-NEW
 END-IF

* WRITE CURR RECORDS IF THEY ARE NEW
 IF OLD-NAME > NEW-NAME
     RP-LINE       = '  '
     RP-NAME       = NEW-NAME
     RP-TYPE1      = 'NEW ELEMENT'
     RP-TYPE2      = '   '
     RP-LEVEL      = PN-LEVEL
     RP-TYPE       = PN-TYPE
     RP-STATUS     = WSN-STATUS
     RP-MAINT-DATE = PN-MAINT-DATE
     RP-NBR-STMT   = PN-NBR-STMT
     PRINT RPT1
     RP-LINE       = '  '
     PRINT RPT1
     PM-REC  = PN-REC
     PUT PANMRG
     WS-NEW-CNT = WS-NEW-CNT + 1
     GO TO JOB
 END-IF


*******************************************************************
*   PROCS
*******************************************************************
 START-JOB.  PROC
  WS-TIME = SYSTIME
  WS-TT1  = ':'

  GET PANDATE
  IF NOT EOF PANDATE
      RP-RPT-DATE = PD-RPT-DATE
  END-IF

 GET PANOLD

 IF EOF PANOLD
     OLD-NAME = X'FFFFFFFFFFFFFFFF'
 ELSE
     PM-REC  = PO-REC
     PUT PANMRG
     PW-REC-WORK = PO-REC
     PERFORM GET-PANOLD
 END-IF

 END-PROC
*******************************************************************
*   GET OLD PAN REPORT
*******************************************************************
 GET-PANOLD. PROC

 IF EOF PANOLD
     OLD-NAME = X'FFFFFFFFFFFFFFFF'
     GO TO GET-PANOLD-EXIT
 END-IF

 PW-REC-WORK = PO-REC
 WS-REC-WORK = PO-REC

* WRITE ALL OLD RECORDS
  DO WHILE WS-NAME = PW-NAME AND (NOT EOF PANOLD)
      PW-REC-WORK = WS-REC-WORK
      GET PANOLD
      IF NOT EOF PANOLD
          PM-REC  = PO-REC
          WS-NAME  = PO-NAME
          PUT PANMRG
          WS-REC-WORK = PO-REC
      END-IF
  END-DO

 OLD-NAME = PW-NAME
 WSO-STATUS = PW-STATUS

 GET-PANOLD-EXIT.

 END-PROC
*******************************************************************
*  PRINT REMAINING RECORDS ON OLD PAN FILE
*******************************************************************
 PRT-OLD. PROC

*        IF PW-MAINT-DATE NE PN-MAINT-DATE OR +
 DO WHILE NOT EOF PANOLD
     PERFORM GET-PANOLD
     IF OLD-NAME NE NEW-NAME AND NOT EOF PANOLD
         RP-LINE       = '  '
         RP-NAME       = OLD-NAME
         RP-TYPE1      = 'DELETED'
         RP-TYPE2      = '   '
         RP-LEVEL      = PW-LEVEL
         RP-TYPE       = PW-TYPE
         RP-STATUS     = WSO-STATUS
         RP-MAINT-DATE = PW-MAINT-DATE
         RP-NBR-STMT   = PW-NBR-STMT
         PRINT RPT1
         RP-LINE       = '  '
         RP-NAME       = OLD-NAME
         RP-TYPE1      = '       '
         RP-TYPE2      = '   '
         RP-TYPE       = PW-TYPE
         RP-STATUS     = 'DELETE'
         RP-MAINT-DATE = NEW-RPT-DATE
         RP-NBR-STMT   = PW-NBR-STMT
         PRINT RPT1
         RP-LINE       = '  '
         PRINT RPT1
         PM-REC  = PW-REC-WORK
         PM-STATUS = 'DELETE'
         PM-MAINT-DATE = NEW-RPT-DATE
         PM-RPT-DATE = NEW-RPT-DATE
         PUT PANMRG
         WS-DELETE-CNT = WS-DELETE-CNT + 1
     END-IF
     IF OLD-NAME = NEW-NAME
         IF PW-LEVEL NE PN-LEVEL
             RP-LINE       = '  '
             RP-NAME       = OLD-NAME
             RP-TYPE1      = 'CHANGED'
             RP-TYPE2      = 'OLD'
             RP-LEVEL      = PW-LEVEL
             RP-TYPE       = PW-TYPE
             RP-STATUS     = WSO-STATUS
             RP-MAINT-DATE = PW-MAINT-DATE
             RP-NBR-STMT   = PW-NBR-STMT
             PRINT RPT1
             RP-LINE       = '  '
             RP-NAME       = NEW-NAME
             RP-TYPE1      = '       '
             RP-TYPE2      = 'NEW'
             RP-LEVEL      = PN-LEVEL
             RP-TYPE       = PN-TYPE
             RP-STATUS     = WSN-STATUS
             RP-MAINT-DATE = PN-MAINT-DATE
             RP-NBR-STMT   = PN-NBR-STMT
             PRINT RPT1
             RP-LINE       = '  '
             PRINT RPT1
             PM-REC  = PN-REC
             PUT PANMRG
             WS-CHANGE-CNT = WS-CHANGE-CNT + 1
         END-IF
     END-IF
 END-DO

 RP-LINE = '   '
 PRINT RPT1
 PRINT RPT1

 RP-LINE2 = '  '
 RP-LIT1  = 'CHANGED'
 RP-CNT   = WS-CHANGE-CNT
 RP-LINE = RP-LINE2
 PRINT RPT1

 RP-LIT1  = 'NEW    '
 RP-CNT   = WS-NEW-CNT
 RP-LINE = RP-LINE2
 PRINT RPT1

 RP-LIT1  = 'DELETED'
 RP-CNT   = WS-DELETE-CNT
 RP-LINE = RP-LINE2
 PRINT RPT1


 STOP

 END-PROC
****************************************
*  REPORTS
****************************************
REPORT RPT1 PRINTER RPT01  NOADJUST
TITLE 1 '                                       CHANGEWIZ      '
TITLE 2 WS-TIME '                            PANVALET CHANGES'
TITLE 3 'JXE305PV-RPT1             FROM' RP-RPT-DATE 'THRU' +
                                         NEW-RPT-DATE
 HEADING RP-LINE ('ELEMENT     TYPE OF             LEVEL               -
          DATE         NBR      ' +
                  ' NAME       CHANGE               NBR   ELEMENT TYPE -
 STATUS MAINTAINED    STMTS     ' +
                  '----------  ------------        -----  ------------ -
 ------ ----------  -------     ')
 LINE 1 RP-LINE

