      ***************************************************************
      *     COPYBOOK JXCPP099
      *JX*  COMMON ABEND ROUTINE
      *
      *     USED BY MOST REFWIZ PROGRAMS
      *     USE WITH COPYBOOK JXCPW020
      ***************************************************************
       Z9000-ABEND-PROGRAM.

           IF INST-CEE
              MOVE JX-ABEND-CODE TO JX-CEE-ABEND-CODE
              MOVE 1 TO JX-CEE-CLEANUP-CODE
              IF JX-CEE-ABEND-CODE = ZERO
                 MOVE 4001 TO JX-CEE-ABEND-CODE
              END-IF
              IF JX-RETURN-CODE = ZERO
                 MOVE JX-CEE-ABEND-CODE TO JX-RETURN-CODE
              END-IF
           ELSE
           IF JX-ABEND-CODE = ZERO
              MOVE 4001 TO JX-ABEND-CODE
              IF JX-RETURN-CODE = ZERO
                 MOVE JX-ABEND-CODE TO JX-RETURN-CODE
              END-IF
           END-IF.

           MOVE JX-RETURN-CODE TO RETURN-CODE.

           DISPLAY '***********************************************'.
           DISPLAY '    '.

           IF JX-RETURN-CODE NOT = ZERO
              DISPLAY 'PROGRAM IS ABENDING WITH RETURN CODE = '
                      JX-RETURN-CODE
           END-IF.

           IF JX-ABEND-PARA NOT = SPACE
              DISPLAY ' ABEND FROM PARAGRAPH '
                      JX-ABEND-PARA
           END-IF.

           IF JX-ABEND-REASON-1 NOT = SPACE
              DISPLAY ' ' JX-ABEND-REASON-1
           IF JX-ABEND-REASON-2 NOT = SPACE
              DISPLAY ' ' JX-ABEND-REASON-2
           END-IF.

           IF INST-CEE
              DISPLAY 'ABENDING PROGRAM ' WS-PROGRAM
                      ' WITH ABEND CODE ' JX-CEE-ABEND-CODE
                      '  CLEANUP = '      JX-CEE-CLEANUP-CODE
              CALL 'CEE3ABD' USING JX-CEE-ABEND-CODE
                                   JX-CEE-CLEANUP-CODE
           ELSE
              DISPLAY 'ABENDING PROGRAM ' WS-PROGRAM
                      ' WITH ABEND CODE ' JX-ABEND-CODE
              CALL 'ILBOABN0' USING JX-ABEND-CODE
           END-IF.

       Z9000-EXIT.
           EXIT.

      ****************************************************************
      *    END OF COPYBOOK                                           *
      ****************************************************************
