       PROCESS NOADV
       IDENTIFICATION DIVISION.
       PROGRAM-ID. JXC320JX.
      *****************************************************************
      *                                                               *
      * (C) 2005 RENAISSANCE DATA SYSTEMS INC.                        *
      *          CHANGEWIZ STAT REL 1.0 VER 1.0                       *
      *                                                               *
      * THESE PROGRAMS ARE DISTRIBUTED ON THE CBT TAPE WITH THE       *
      * PROVISO THAT THEY MAY BE FREELY DISTRIBUTED TO ANY OTHER      *
      * PARTY ON CONDITION THAT NO INDUCEMENT BEYOND REASONABLE       *
      * HANDLING COSTS IS OFFERED OR ACCEPTED BY EITHER SIDE FOR      *
      * SUCH DISTRIBUTION OR YOUR NORMAL CONSULTING COSTS FOR         *
      * INSTALLATION AND SUPPORT.                                     *
      *                                                               *
      *                                                               *
      *****************************************************************
      *JX*  CREATE A DATE-LAST-MAINT FILE FOR PROGRAM/COPYBOOK MEMBERS
      *
      *    1. SORT BY ELEMENT, LIBRARY, LEVEL
      *    2. WRITE OUT HIGHEST LEVEL FOR EACH ELEMENT
      *    3. KEEP DELETES IF THEY ARE LESS THAN AGE IN PARM
      *
      * A0000-  INITIALIZATION AND TERMINATION ROUTINES
      * B0000-  PROCESS CONTROL CARDS
      * C0000-  MAIN PROCESSING
      * D0000-  FORMAT OUTPUT RECORDS
      * X0000-  PROGRAM READ, WRITE AND OTHER I-O ROUTINES
      * Y0000-  DEBUG ROUTINES
      * Z0000-  ABEND ROUTINES
      *
      *
      ****************************************************************

       ENVIRONMENT DIVISION.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.

      * INPUT FILES
           SELECT CHANGE-HIST-FILE
               STATUS IS CHANGE-HST-STATUS ASSIGN CHGHIST.

           SELECT DELETE-DATE-FILE
               STATUS IS DELETE-DATE-STATUS ASSIGN DELDATE.

      * OUTPUT FILES
           SELECT NEW-CHANGE-FILE
               STATUS IS NEW-CHANGE-STATUS ASSIGN NEWCHG.

           SELECT DEBUG-REPORT             ASSIGN RDBUG.

       DATA DIVISION.
       FILE SECTION.

       FD  CHANGE-HIST-FILE
           RECORDING MODE IS F
           BLOCK CONTAINS 0 RECORDS.
       01  CHANGE-HIST-RECORD          PIC X(200).

       FD  DELETE-DATE-FILE
           RECORDING MODE IS F
           BLOCK CONTAINS 0 RECORDS.
      *    VALID VALUES ARE MONTHS=9999 OR MONTHS=ALL
       01  DELETE-DATE-RECORD.
           05  DELETE-MONTHS-LIT       PIC X(07).
               88  VALID-DEL-MONTHS-LIT    VALUE 'MONTHS='.
           05  DELETE-MONTHS-X.
               10  DELETE-MONTHS       PIC 9(04).
           05  FILLER                  PIC X(69).

       FD  NEW-CHANGE-FILE
           RECORDING MODE IS F
           BLOCK CONTAINS 0 RECORDS.
       01  NEW-CHANGE-RECORD           PIC X(200).


       FD  DEBUG-REPORT
           RECORDING MODE IS F
           BLOCK CONTAINS 0 RECORDS.
       01  DEBUG-RPT-LINE              PIC X(133).


       WORKING-STORAGE SECTION.

       01  WS-PROGRAM                  PIC X(08) VALUE 'JXC320JX'.

      *  VENDOR INFO
           COPY JXCPW001.

      *  REFWIZ PRODUCT INFO
           COPY JXCPW002.

           EJECT
      *  CHANGE HISTORY RECORD LAYOUT
           COPY JXCPR300.

           EJECT
      *  CHANGE HISTORY RECORD LAYOUT 2
           COPY JXCPR305.


           EJECT
       01  WS-CURR-KEY.
           05  WS-CURR-ELEMENT         PIC X(08).
           05  WS-CURR-LIBRARY         PIC X(08).

       01  WS-LIST-KEY.
           05  WS-LIST-ELEMENT         PIC X(08).
           05  WS-LIST-LIBRARY         PIC X(08).


       01  WORK-IO-FIELDS.
           05  CHANGE-HIST-CNT         PIC 9(09) VALUE ZERO.
           05  TOTAL-WRITTEN-CNT       PIC 9(09) VALUE ZERO.
           05  TOTAL-DEL-WRITTEN-CNT   PIC 9(09) VALUE ZERO.
           05  SAVE-HISTORY-RECORD     PIC X(300) VALUE SPACE.
           05  SAVE-HIST-LEVEL         PIC 9(05) VALUE ZERO.
           05  DLY-CHANGE-LEVEL        PIC 9(05) VALUE ZERO.
           05  DISPLAY-CNT             PIC ZZZ,ZZZ,ZZ9.

           05  WS-DELETE-MONTHS        PIC 9(04) VALUE ZERO.
           05  WS-DELETE-YEARS         PIC 9(04) VALUE ZERO.
           05  WS-DELETE-REM           PIC 9(04) VALUE ZERO.

           05  WS-DELETE-DATE-CMPR.
               10  WS-DELETE-DATE-CCYY   PIC 9(04).
               10  WS-DELETE-DATE-CCYY-X REDEFINES
                   WS-DELETE-DATE-CCYY.
                   15  WS-DELETE-DATE-CC  PIC 9(02).
                   15  WS-DELETE-DATE-YY  PIC 9(02).
               10  FILLER              PIC X(01).
               10  WS-DELETE-DATE-MM   PIC 9(02).
               10  FILLER              PIC X(01).
               10  WS-DELETE-DATE-DD   PIC 9(02).

           05  DEBUG-RPT-LINE-CNT      PIC S9(3) COMP-3 VALUE +80.
           05  DEBUG-PAGE-CNT          PIC S9(7) COMP-3 VALUE ZERO.
           05  CHANGE-HST-STATUS       PIC X(2) VALUE SPACE.
           05  DELETE-DATE-STATUS      PIC X(2) VALUE SPACE.
           05  NEW-CHANGE-STATUS       PIC X(2) VALUE SPACE.
           05  END-OF-HIST-CHANGE-SW   PIC X    VALUE SPACE.
               88  END-OF-HIST-CHANGE           VALUE 'Y'.
           05  END-OF-DEL-DATE-SW      PIC X    VALUE SPACE.
               88  END-OF-DEL-DATE              VALUE 'Y'.

           05  HOLD-PRINT-LINE         PIC X(133) VALUE SPACE.

           EJECT
       01  DEBUG-HEADING-1.
           05  FILLER                  PIC X(01) VALUE SPACES.
           05  DBH1-IO-TYPE            PIC X(04) VALUE SPACES.
           05  FILLER                  PIC X(01) VALUE SPACES.
           05  DBH1-ELEMENT            PIC X(08) VALUE 'ELEMENT'.
           05  FILLER                  PIC X(01) VALUE SPACES.
           05  DBH1-PROMOTE-DATE       PIC X(10) VALUE 'PROMOTE'.
           05  FILLER                  PIC X(01) VALUE SPACES.
           05  DBH1-PROMOTE-TIME       PIC X(08) VALUE 'PROMOTE'.
           05  FILLER                  PIC X(02) VALUE SPACES.
           05  DBH1-OUTPUT-1           PIC X(50) VALUE
               'ELEMENT  LEVEL LIBRARY  LANGUAGE # LINES USER ID  '.
           05  DBH1-OUTPUT-1           PIC X(45) VALUE
               '       CHG CODE EDIT DATE  JULIAN   EDIT TIME'.

       01  DEBUG-HEADING-2.
           05  FILLER                  PIC X(01) VALUE SPACES.
           05  DBH2-IO-TYPE            PIC X(04) VALUE ' IO'.
           05  FILLER                  PIC X(01) VALUE SPACES.
           05  DBH2-ELEMENT            PIC X(08) VALUE '  NAME'.
           05  FILLER                  PIC X(01) VALUE SPACES.
           05  DBH2-PROMOTE-DATE       PIC X(10) VALUE '   DATE '.
           05  FILLER                  PIC X(01) VALUE SPACES.
           05  DBH2-PROMOTE-TIME       PIC X(08) VALUE '  TIME'.
           05  FILLER                  PIC X(02) VALUE SPACES.
           05  DBH2-OUTPUT             PIC X(95) VALUE SPACES.

       01  DEBUG-DETAIL-LINE-IN.
           05  DEBUG-CC                PIC X  VALUE SPACE.
           05  DEBUG-DATA-IN.
               10  DBI-IO-TYPE         PIC X(04).
               10  FILLER              PIC X(01).
               10  DBI-ELEMENT         PIC X(08).
               10  FILLER              PIC X(01).
               10  DBI-PROMOTE-DATEX.
                   15  DBI-PROMOTE-DATE PIC X(10).
               10  FILLER              PIC X(01).
               10  DBI-PROMOTE-TIME    PIC X(08).
               10  FILLER              PIC X(02).
               10  DBI-DATA-REC.
                   15  FILLER          PIC X(95).

       01  DISPLAY-TOTAL-LINE.
           05  FILLER                  PIC X(02).
           05  DT-DESC                 PIC X(30).
           05  FILLER                  PIC X(02).
           05  DT-COUNT                PIC ZZZ,ZZZ,ZZ9.
           05  FILLER                  PIC X(05).


           EJECT
      *  COMMON REPORT HEADINGS AND FOOTER
      *  DATE-TIME WORK AREAS
      *  COMMON DEBUG WORK AREAS
      *  COMMON ABEND WORK AREA
           COPY JXCPW020.

           EJECT
      *  DATE CONVERSION WORK AREAS
      *    COPY JXCPW025.

           EJECT
      *+ INSTALLATION SPECIFIC CONSTANTS
           COPY JXINW005.
           EJECT
       LINKAGE SECTION.
      *  COMMON PROGRAM RUN PARAMETERS
      *  JXL-PARM-MEMBER CONTAINS THE NUMBER OF MONTHS TO KEEP DELETES
           COPY JXCPL005.

           EJECT
       PROCEDURE DIVISION USING JXL-PARM-DATA.

           PERFORM A1000-INITIALIZATION THRU A1000-EXIT.

           PERFORM C1000-PROCESS-CHANGE-HIST
              THRU C1000-EXIT
              UNTIL END-OF-HIST-CHANGE.

           PERFORM A9000-TERMINATION THRU A9000-EXIT.
           GOBACK.

       A1000-INITIALIZATION.

           DISPLAY '**************************************************'
                  '**************************************************'.
           DISPLAY ' '.
           DISPLAY 'PROGRAM JXC320JX - CREATE A DATE-LAST-MAINT FILE'
           MOVE ' DEBUG REPORT - CREATE A DATE-LAST-MAINT FILE   '
                                                 TO JX-DEBUG-TITLE.

           MOVE RULER-LINE(2:95)   TO DBH2-OUTPUT.
           MOVE WS-PROGRAM         TO JXRD-PROGRAM
                                      JXCD-PROGRAM
                                      HD2-PROGRAM.

           MOVE 'CHANGEWIZ  HIST   REL 1.0 VER 1.0' TO FT1-PRODUCT.
           MOVE '(C) 2005 RENAISSANCE DATA SYSTEMS INC.'
                                                    TO FT1-VENDOR.


      **  DISPLAY COMPILE DATE AND TIME
           COPY JXCPP010.


      **  PROCESS COMMON INPUT PARAMETER
           COPY JXCPP015.

      *   GET THE NUMBER OF MONTHS TO KEEP DELETES
           PERFORM B1000-DELETE-CUTOFF-DATE THRU B1000-EXIT.

      *+  INSTALLATION SPECIFIC INITIALIZATION ROUTINES
           COPY JXINP005.


           OPEN INPUT CHANGE-HIST-FILE
           OPEN OUTPUT NEW-CHANGE-FILE.

           IF JX-PRINT-DEBUG
               OPEN OUTPUT DEBUG-REPORT.

           IF CHANGE-HST-STATUS NOT = '00' AND '97'
               DISPLAY '   BAD OPEN ON CHANGE HIST FILE. STATUS = '
                       CHANGE-HST-STATUS
               MOVE 'Y' TO END-OF-HIST-CHANGE-SW
               PERFORM Z9000-ABEND-PROGRAM THRU Z9000-EXIT
           ELSE
               PERFORM X1000-READ-CHANGE-HIST THRU X1000-EXIT.

           IF END-OF-HIST-CHANGE
               DISPLAY 'NO RECORDS IN CHANGE HISTORY FILE'
               PERFORM Z9000-ABEND-PROGRAM THRU Z9000-EXIT
           END-IF.

           IF NEW-CHANGE-STATUS NOT = '00' AND '97'
               DISPLAY '   BAD OPEN ON CHANGE HIST OUT FILE. STATUS = '
                       NEW-CHANGE-STATUS
               PERFORM Z9000-ABEND-PROGRAM THRU Z9000-EXIT
           END-IF.


      *+  INSTALLATION-SPECIFIC CODE TO DETERMINE DATE CUTOFF
           COPY JXINP010.


       A1000-EXIT.
           EXIT.


      ***********************************************************
      *   END OF JOB PROCESSING
      ***********************************************************
       A9000-TERMINATION.

           CLOSE CHANGE-HIST-FILE
                 NEW-CHANGE-FILE.


      *  ENABLE TOTALS TO PRINT ON DEBUG REPORT
           IF JX-PRINT-DEBUG OR DEBUG-PAGE-CNT > 0
              ADD 15 TO JX-PRINT-DEBUG-MAX
              IF NOT JX-PRINT-DEBUG
                 MOVE 'Y' TO JX-DEBUG-SW
              END-IF
              ADD 15 TO DEBUG-RPT-LINE-CNT
              IF DEBUG-RPT-LINE-CNT > INST-LINES-PER-PAGE
                 SUBTRACT 15 FROM DEBUG-RPT-LINE-CNT
                 PERFORM Y1025-DEBUG-HEADING THRU Y1025-EXIT
              ELSE
                 SUBTRACT 15 FROM DEBUG-RPT-LINE-CNT
                 MOVE SPACES        TO DEBUG-RPT-LINE
                 PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
                 PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
              END-IF
           END-IF.

           DISPLAY ' '.
           DISPLAY ' '.
           DISPLAY '****************************************'
                   '****************************************'
           DISPLAY ' '.
           DISPLAY 'END OF PROGRAM JXC320JX.'.
           DISPLAY ' '.


           IF JX-PRINT-DEBUG
               MOVE ALL '*'       TO DEBUG-RPT-LINE(2:125)
               PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
               MOVE SPACES        TO DEBUG-RPT-LINE
               PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
               MOVE ' END-OF-PROGRAM JXC320JX RECORD COUNTS' TO
                                     DEBUG-RPT-LINE
               PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
               MOVE SPACES        TO DEBUG-RPT-LINE
               PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
           END-IF.

           MOVE SPACE TO DISPLAY-TOTAL-LINE.

           MOVE 'CHANGE HISTORY RECS READ   '  TO DT-DESC
           MOVE CHANGE-HIST-CNT                TO DT-COUNT.
           DISPLAY DISPLAY-TOTAL-LINE
           DISPLAY '   '
           IF JX-PRINT-DEBUG
              MOVE DISPLAY-TOTAL-LINE TO DEBUG-RPT-LINE
              PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
              MOVE SPACE              TO DEBUG-RPT-LINE
              PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
           END-IF.

           MOVE 'TOTAL CHG RECORDS WRITTEN   ' TO DT-DESC
           MOVE TOTAL-WRITTEN-CNT              TO DT-COUNT.
           DISPLAY DISPLAY-TOTAL-LINE
           IF JX-PRINT-DEBUG
              MOVE DISPLAY-TOTAL-LINE TO DEBUG-RPT-LINE
              PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
           END-IF.

           MOVE '   CHG HIST DELETE RECS OUT ' TO DT-DESC
           MOVE TOTAL-DEL-WRITTEN-CNT          TO DT-COUNT.
           DISPLAY DISPLAY-TOTAL-LINE
           DISPLAY '   '
           IF JX-PRINT-DEBUG
              MOVE DISPLAY-TOTAL-LINE TO DEBUG-RPT-LINE
              PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
              MOVE SPACE              TO DEBUG-RPT-LINE
              PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
           END-IF.

           DISPLAY ' '.
           DISPLAY '****************************************'
                   '****************************************'
           DISPLAY ' '.

           IF JX-PRINT-DEBUG
               MOVE SPACES        TO DEBUG-RPT-LINE
               PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
               MOVE ALL '*'       TO DEBUG-RPT-LINE(2:125)
               PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
               MOVE SPACES        TO DEBUG-RPT-LINE
               PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
           END-IF.



      **  PRINT LAST CHANGE FILE DATA
           IF DEBUG-PAGE-CNT > 0
               PERFORM Y1050-DEBUG-FOOTER THRU Y1050-EXIT.

           IF JX-PRINT-DEBUG OR DEBUG-PAGE-CNT > 0
               CLOSE DEBUG-REPORT.

       A9000-EXIT.
           EXIT.

      **************************************************************
      *   GET THE NUMBER OF MONTHS TO KEEP DELETES
      *   IF THERE IS NO VALUE, KEEP ALL DELETES.
      **************************************************************
       B1000-DELETE-CUTOFF-DATE.
           MOVE '0000-00-00'    TO WS-DELETE-DATE-CMPR.

           OPEN INPUT DELETE-DATE-FILE.

           IF DELETE-DATE-STATUS NOT = '00' AND '97'
              DISPLAY '   BAD OPEN ON DELETE-DATE FILE. STATUS = '
                      DELETE-DATE-STATUS
              MOVE 'Y' TO END-OF-DEL-DATE-SW
              GO TO B1000-EXIT
           ELSE
              PERFORM X2000-READ-DELETE-DATE THRU X2000-EXIT
           END-IF.

           IF END-OF-DEL-DATE
              DISPLAY 'NO RECORDS IN DELETE-DATE FILE'
              GO TO B1000-CLOSE-FILE
           END-IF.

           IF NOT VALID-DEL-MONTHS-LIT
              PERFORM B2000-DELETE-DATECARD-HELP THRU B2000-EXIT
              GO TO B1000-CLOSE-FILE
           END-IF.

           IF DELETE-MONTHS-X = 'ALL '
              MOVE '0000-00-00'    TO WS-DELETE-DATE-CMPR
              GO TO B1000-CLOSE-FILE
           END-IF.

           IF DELETE-MONTHS-X = 'NONE'
              MOVE '9999-99-99'    TO WS-DELETE-DATE-CMPR
              GO TO B1000-CLOSE-FILE
           END-IF.

           IF DELETE-MONTHS NOT NUMERIC
              DISPLAY 'NUMBER OF MONTHS IS NOT NUMERIC AND '
                      'NOT = "ALL" OR "NONE" '
              MOVE '0000-00-00'    TO WS-DELETE-DATE-CMPR
              PERFORM B2000-DELETE-DATECARD-HELP THRU B2000-EXIT
              GO TO B1000-CLOSE-FILE
           END-IF.

      *  MOVE RUN-DATE TO DELETE-CUTOFF-DATE
           MOVE JXRD-CC   TO WS-DELETE-DATE-CC
           MOVE JXRD-YY   TO WS-DELETE-DATE-YY
           MOVE JXRD-MM   TO WS-DELETE-DATE-MM
           MOVE JXRD-DD   TO WS-DELETE-DATE-DD.

           MOVE DELETE-MONTHS TO WS-DELETE-MONTHS
           MOVE ZERO          TO WS-DELETE-YEARS
           MOVE ZERO          TO WS-DELETE-REM

           IF WS-DELETE-MONTHS > 12
              DIVIDE WS-DELETE-MONTHS BY 12
                 GIVING WS-DELETE-YEARS
                 REMAINDER WS-DELETE-REM
           ELSE
              MOVE WS-DELETE-MONTHS TO WS-DELETE-REM
           END-IF.

           SUBTRACT WS-DELETE-YEARS FROM WS-DELETE-DATE-CCYY.

           IF WS-DELETE-REM >= WS-DELETE-DATE-MM
              SUBTRACT 1 FROM WS-DELETE-DATE-CCYY
              ADD 12       TO WS-DELETE-DATE-MM
              SUBTRACT WS-DELETE-REM FROM WS-DELETE-DATE-MM
           ELSE
              SUBTRACT WS-DELETE-REM FROM WS-DELETE-DATE-MM
           END-IF.


       B1000-CLOSE-FILE.
           DISPLAY 'KEEPING ' DELETE-MONTHS-X ' MONTHS OF DELETES'.
           DISPLAY 'DELETES AFTER ' WS-DELETE-DATE-CMPR
                   ' WILL BE KEPT IN THE LAST-MAINT FILE'.

           CLOSE DELETE-DATE-FILE.

       B1000-EXIT.
           EXIT.

      **************************************************************
      *   DISPLAY THE DATE CARD AND SHOW VALID VALUES FOR THE CARD
      **************************************************************
       B2000-DELETE-DATECARD-HELP.

           DISPLAY '         '.
           DISPLAY '****************************************'
                   '****************************************'
           DISPLAY 'INVALID DELETE DATE CARD.  ALL DELETES WILL '
                   'BE KEPT.  DEFAULT IS "MONTHS=ALL".'
           DISPLAY 'CARD=' DELETE-DATE-RECORD
           DISPLAY '     '
           DISPLAY '     ----+----1----+----2'
           DISPLAY '     MONTHS=9999    IS A VALID CARD.  '
                   '9999 IS THE NUMBER OF MONTHS TO KEEP DELETES'
           DISPLAY '     MONTHS=ALL     IS A VALID CARD.  '
                   'ALL DELETES WILL BE KEPT'
           DISPLAY '     MONTHS=NONE    IS A VALID CARD.  '
                   'NO DELETES WILL BE KEPT'.
           DISPLAY '****************************************'
                   '****************************************'
           DISPLAY '         '.

       B2000-EXIT.
           EXIT.

           EJECT
      **************************************************************
      *  1. WRITE OUT HIGHEST LEVEL FOR AN ELEMENT UNLESS IT WAS
      *       DELETED.
      *  2. WRITE OUT DELETES AT THE HIGHEST LEVEL IF IT WAS
      *       DELETED WITHIN A DATE RANGE SPECIFIED BY A DATE CARD
      **************************************************************
       C1000-PROCESS-CHANGE-HIST.

      *  READ ALL HISTORY FOR AN ELEMENT
      *  SAVE THE HIGHEST LEVEL NUMBER

           MOVE SH-ELEMENT-NAME     TO WS-CURR-ELEMENT
           MOVE SH-ELEMENT-LIB      TO WS-CURR-LIBRARY

           PERFORM UNTIL END-OF-HIST-CHANGE OR
                         (WS-CURR-ELEMENT NOT = SH-ELEMENT-NAME) OR
                         (WS-CURR-ELEMENT  = SH-ELEMENT-NAME AND
                         (WS-CURR-LIBRARY NOT = SH-ELEMENT-LIB))
              MOVE SH-ELEMENT-LEVEL      TO SAVE-HIST-LEVEL
              MOVE SOURCE-HISTORY-RECORD TO SOURCE-HISTORY-RECORD-2
              PERFORM X1000-READ-CHANGE-HIST THRU X1000-EXIT
           END-PERFORM.

           IF SH2-PAN-STATUS  = 'DELETE'
              IF SH2-CAPTURE-DATE-CYMD > WS-DELETE-DATE-CMPR
                 PERFORM D1000-WRITE-HISTORY THRU D1000-EXIT
                 ADD 1 TO TOTAL-DEL-WRITTEN-CNT
              END-IF
           ELSE
              PERFORM D1000-WRITE-HISTORY    THRU D1000-EXIT
           END-IF.


       C1000-EXIT.
           EXIT.
           EJECT
      ***************************************************************
      *  WRITE HISTORY TO NEW HISTORY FILE
      *  ONLY ONE RECORD WILL BE WRITTEN PER ELEMENT
      ***************************************************************
       D1000-WRITE-HISTORY.

           WRITE NEW-CHANGE-RECORD FROM SOURCE-HISTORY-RECORD-2.
           ADD 1 TO TOTAL-WRITTEN-CNT.

           IF JX-PRINT-DEBUG
               MOVE SPACES                  TO DEBUG-DATA-IN
               MOVE 'OHST'                  TO DBI-IO-TYPE
               MOVE SH2-ELEMENT-NAME        TO DBI-ELEMENT
               MOVE SH2-CAPTURE-TIME        TO DBI-PROMOTE-TIME
               MOVE SH2-CAPTURE-DATE-CYMD   TO DBI-PROMOTE-DATE
               MOVE SOURCE-HISTORY-RECORD-2 TO DBI-DATA-REC
               MOVE DEBUG-DETAIL-LINE-IN    TO DEBUG-RPT-LINE
               PERFORM Y1000-PRINT-DEBUG THRU Y1000-EXIT
           END-IF.

       D1000-EXIT.
           EXIT.


           EJECT
      ***************************************************************
      *  READ CHANGE HISTORY FILE
      ***************************************************************
       X1000-READ-CHANGE-HIST.
           READ CHANGE-HIST-FILE INTO SOURCE-HISTORY-RECORD.

           IF CHANGE-HST-STATUS = '10'
               MOVE 'Y' TO END-OF-HIST-CHANGE-SW
               GO TO X1000-EXIT.

           ADD 1           TO CHANGE-HIST-CNT.
           IF CHANGE-HST-STATUS NOT = '00'
               DISPLAY '  BAD READ ON CHANGE HISTORY FILE. '
                   ' STATUS = '
                   CHANGE-HST-STATUS ' ON RECORD NBR '
                   CHANGE-HIST-CNT
               GO TO X1000-EXIT
           END-IF.


       X1000-EXIT.
           EXIT.


      ***************************************************************
      *  READ DELETE DATE RANGE FILE
      ***************************************************************
       X2000-READ-DELETE-DATE.
           READ DELETE-DATE-FILE.

           IF DELETE-DATE-STATUS = '10'
               MOVE 'Y' TO END-OF-DEL-DATE-SW
               GO TO X2000-EXIT.

           IF DELETE-DATE-STATUS NOT = '00'
               DISPLAY '  BAD READ ON DELETE-DATE FILE. '
                   ' STATUS = '
                   DELETE-DATE-STATUS
               GO TO X2000-EXIT
           END-IF.


       X2000-EXIT.
           EXIT.


           EJECT
      ***************************************************************
      ***  PRINT ROUTINES
      ***************************************************************

       Y1000-PRINT-DEBUG.
           ADD 1 TO JX-PRINT-DEBUG-CNT.
           IF JX-PRINT-DEBUG-CNT > JX-PRINT-DEBUG-SKIP
               IF DEBUG-RPT-LINE-CNT > INST-LINES-PER-PAGE
                   MOVE DEBUG-RPT-LINE TO HOLD-PRINT-LINE
                   PERFORM Y1025-DEBUG-HEADING THRU Y1025-EXIT
                   MOVE HOLD-PRINT-LINE TO DEBUG-RPT-LINE.

           IF JX-PRINT-DEBUG-CNT > JX-PRINT-DEBUG-SKIP
               WRITE DEBUG-RPT-LINE
                   AFTER ADVANCING 1 LINE
               ADD 1 TO DEBUG-RPT-LINE-CNT.

           IF JX-PRINT-DEBUG-CNT > JX-PRINT-DEBUG-MAX
               MOVE 'N' TO JX-DEBUG-SW.
           MOVE SPACES TO DEBUG-RPT-LINE.
       Y1000-EXIT.
           EXIT.

       Y1025-DEBUG-HEADING.
           ADD 1                   TO DEBUG-PAGE-CNT.
           MOVE DEBUG-PAGE-CNT     TO HD1-PAGE.
           MOVE JX-DEBUG-TITLE     TO HD2-TITLE.
           MOVE 'DB'               TO HD2-REPORT.

           MOVE '            CHANGEWIZ DEBUGGING REPORT           '
                                   TO HD1-COMPANY-NAME


           IF DEBUG-PAGE-CNT > 1
               PERFORM Y1050-DEBUG-FOOTER THRU Y1050-EXIT.

           WRITE DEBUG-RPT-LINE FROM HEADING-1
               AFTER ADVANCING PAGE.
           WRITE DEBUG-RPT-LINE FROM HEADING-2
               AFTER ADVANCING 1 LINE.
           WRITE DEBUG-RPT-LINE FROM DEBUG-HEADING-1
               AFTER ADVANCING 2 LINE.
           WRITE DEBUG-RPT-LINE FROM DEBUG-HEADING-2
               AFTER ADVANCING 1 LINE.
           MOVE SPACES TO DEBUG-RPT-LINE
           WRITE DEBUG-RPT-LINE
               AFTER ADVANCING 1 LINE.
           MOVE 6 TO DEBUG-RPT-LINE-CNT.
       Y1025-EXIT.
           EXIT.

       Y1050-DEBUG-FOOTER.
           MOVE SPACES TO DEBUG-RPT-LINE.

           PERFORM UNTIL DEBUG-RPT-LINE-CNT > INST-LINES-PER-PAGE
               WRITE DEBUG-RPT-LINE
                   AFTER ADVANCING 1 LINE
               ADD 1 TO DEBUG-RPT-LINE-CNT
           END-PERFORM.

           WRITE DEBUG-RPT-LINE
               AFTER ADVANCING 2 LINE.
           WRITE DEBUG-RPT-LINE FROM FOOTER-1
               AFTER ADVANCING 1 LINE.

       Y1050-EXIT.
           EXIT.

           EJECT
      *  COMMON ABEND ROUTINE
           COPY JXCPP099.


