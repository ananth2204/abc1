       PROCESS NOADV
       IDENTIFICATION DIVISION.
       PROGRAM-ID. JXC370JX.
      *****************************************************************
      *                                                               *
      * (C) 2005 RENAISSANCE DATA SYSTEMS INC.                        *
      *          CHANGEWIZ STAT REL 1.0 VER 1.0                       *
      *                                                               *
      * THESE PROGRAMS ARE DISTRIBUTED ON THE CBT TAPE WITH THE       *
      * PROVISO THAT THEY MAY BE FREELY DISTRIBUTED TO ANY OTHER      *
      * PARTY ON CONDITION THAT NO INDUCEMENT BEYOND REASONABLE       *
      * HANDLING COSTS IS OFFERED OR ACCEPTED BY EITHER SIDE FOR      *
      * SUCH DISTRIBUTION OR YOUR NORMAL CONSULTING COSTS FOR         *
      * INSTALLATION AND SUPPORT.                                     *
      *                                                               *
      *                                                               *
      *****************************************************************
      *****************************************************************
      *JX* PRINT REPORT OF SOURCE ELEMENTS CHANGED.
      *
      *       DIFFERENT REPORTS CAN BE PRINTED DEPENDING ON THE
      *       LIBRARY-NAME FILE
      *
      *       THE INPUT PARM CARD WILL DETERMINE WHAT ELEMENTS ARE
      *       PULLED FOR THE REPORT BY DATE.
      *
      *       DATE CARD OPTIONS ARE:
      *
      *       ----+----1----+----2----+----3
      *       DAILY    +01                      PRINT CURRENT DAY +1
      *       PREVMTD                           PRINT LAST MONTH CHGS
      *       MTD      +02                      PRINT CURRENT MTD
      *                                           IF DAY < VALUE, PRINT
      *                                             PREV MTD + CURR MTH
      *       RANGE   CCYY-MM-DD CCYY-MM-DD     PRINT CHANGES IN RANGE
      *
      * A0000-  INITIALIZATION AND TERMINATION ROUTINES
      * C0000-  MAIN PROCESSING FOR EACH REPORT
      * X0000-  FILE IO
      * Y0000-  REPORT FORMATTING AND OUTPUT ROUTINES
      * Z0000-  ABEND ROUTINES
      *
      *
      *  THIS PROGRAM SHOULD NOT NEED ANY CLIENT-SPECIFIC CHANGES
      *+  LINES WITH CLIENT-SPECIFIC CODE
      *****************************************************************

       ENVIRONMENT DIVISION.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.

           SELECT CHANGE-HISTORY-FILE
               STATUS IS CHG-HIST-STATUS        ASSIGN CHGHIST.

           SELECT RPT-RANGE-FILE
               STATUS IS RPT-RANGE-STATUS       ASSIGN RPTRANGE.

           SELECT LIBRARY-NAME-FILE
               STATUS IS LIB-NAME-STATUS        ASSIGN LIBNAME.

           SELECT CHANGE-HISTORY-RPT            ASSIGN CHGRPT.

           SELECT DEBUG-REPORT                  ASSIGN RDBUG.


       DATA DIVISION.
       FILE SECTION.


       FD  CHANGE-HISTORY-FILE
           RECORDING MODE IS F
           BLOCK CONTAINS 0 RECORDS.
       01  CHANGE-HISTORY-REC          PIC X(200).

       FD  RPT-RANGE-FILE
           RECORDING MODE IS F
           BLOCK CONTAINS 0 RECORDS.
       01  RPT-RANGE-REC               PIC X(80).

       FD  LIBRARY-NAME-FILE
           RECORDING MODE IS F
           BLOCK CONTAINS 0 RECORDS.
       01  LIBRARY-NAME-REC.
           05  LIB-CROSS-REF           PIC X(70).
           05  FILLER                  PIC X(10).


       FD  CHANGE-HISTORY-RPT
           RECORDING MODE IS F
           BLOCK CONTAINS 0 RECORDS.
       01  CHANGE-HIST-RPTREC          PIC X(133).

       FD  DEBUG-REPORT
           RECORDING MODE IS F
           BLOCK CONTAINS 0 RECORDS.
       01  DEBUG-RPT-LINE              PIC X(133).


           EJECT
      **************************************************************
       WORKING-STORAGE SECTION.

       01  WS-PROGRAM                  PIC X(08) VALUE 'JXC370JX'.

      *  VENDOR INFO
           COPY JXCPW001.

      *  REFWIZ PRODUCT INFO
           COPY JXCPW002.


           EJECT
      *  CHANGE HISTORY RECORD LAYOUT
           COPY JXCPR300.

           EJECT
      *  SECOND COPY OF CHANGE HISTORY RECORD LAYOUT
           COPY JXCPR305.


       01  RPT-RANGE-WORK.
           05  RR-REPORT-TYPE          PIC X(08).
           05  FILLER                  PIC X(01).
           05  RR-START-DATE-CYMD.
               10  RR-START-GREG-CCYY  PIC 9(04).
               10  RR-START-GREG-H1    PIC X(01).
               10  RR-START-GREG-MM    PIC 9(02).
               10  RR-START-GREG-H2    PIC X(01).
               10  RR-START-GREG-DD    PIC 9(02).
           05  RR-MTD-PLUS-X REDEFINES RR-START-DATE-CYMD.
               10  RR-MTD-PLUS         PIC X(01).
               10  RR-MTD-PLUS-DAYS    PIC 9(02).
               10  FILLER              PIC X(07).
           05  FILLER                  PIC X(01).
           05  RR-END-DATE-CYMD.
               10  RR-END-GREG-CCYY    PIC 9(04).
               10  RR-END-GREG-H1      PIC X(01).
               10  RR-END-GREG-MM      PIC 9(02).
               10  RR-END-GREG-H2      PIC X(01).
               10  RR-END-GREG-DD      PIC 9(02).
           05  FILLER                  PIC X(50).

       01  RPT-DATA-TYPE.
           05  RR-REPORT-FORMAT        PIC X(08).
               88  RR-VALID-RPT-FORMAT           VALUE 'SCLM' 'PDS'.
               88  RR-SCLM-FORMAT                VALUE 'SCLM'.
               88  RR-PDS-FORMAT                 VALUE 'PDS '.
           05  FILLER                  PIC X(72).

           EJECT
       01  COUNTS-AREA.
           05  DISPLAY-CNT             PIC  ZZZ,ZZZ,ZZ9.
           05  CHG-HIST-CNT            PIC  9(9)        VALUE ZERO.
           05  RPT-CHG-CNT             PIC  9(9)        VALUE ZERO.
           05  RPT-DEL-CNT             PIC  9(9)        VALUE ZERO.
           05  RPT-NEW-CNT             PIC  9(9)        VALUE ZERO.

           05  RPT-LINE-CNT            PIC S9(3) COMP-3 VALUE +80.
           05  RPT-PAGE-CNT            PIC S9(7) COMP-3 VALUE ZERO.

       01  REPORT-CONSTANTS.
           05  RPT01-HEADING-CNT       PIC S9(3) COMP-3 VALUE +6.

           05  RPT01-REPORT-NBR        PIC X(02) VALUE '01'.

           05  RPT01-TITLE.
               10  FILLER                  PIC X(25) VALUE
               '     CHANGE HISTORY FROM '.
               10  RPT01-FROM-DATE         PIC X(10) VALUE SPACE.
               10  FILLER                  PIC X(04) VALUE ' TO '.
               10  RPT01-TO-DATE           PIC X(10) VALUE SPACE.
               10  FILLER                  PIC X(01) VALUE SPACE.

           05  RPT02-HEADING-CNT       PIC S9(3) COMP-3 VALUE +6.

           05  RPT02-REPORT-NBR        PIC X(02) VALUE '02'.

           05  RPT02-TITLE.
               10  FILLER                  PIC X(25) VALUE
               '     CHANGE HISTORY FROM '.
               10  RPT02-FROM-DATE         PIC X(10) VALUE SPACE.
               10  FILLER                  PIC X(04) VALUE ' TO '.
               10  RPT02-TO-DATE           PIC X(10) VALUE SPACE.
               10  FILLER                  PIC X(01) VALUE SPACE.


       01  WORK-STATUS.
           05  CHG-HIST-STATUS         PIC X(2) VALUE SPACE.
           05  RPT-RANGE-STATUS        PIC X(2) VALUE SPACE.
           05  LIB-NAME-STATUS         PIC X(2) VALUE SPACE.

       01  WORK-SWITCHES.
           05  END-OF-CHG-HIST-SW      PIC X VALUE SPACE.
               88  END-OF-CHG-HIST         VALUE 'Y'.
           05  END-OF-LIBNAME-SW       PIC X VALUE SPACE.
               88  END-OF-LIBNAME          VALUE 'Y'.


       01  MISC-WORK-AREAS.
           05  DEBUG-RPT-LINE-CNT      PIC S9(3) COMP-3 VALUE +80.
           05  DEBUG-PAGE-CNT          PIC S9(7) COMP-3 VALUE ZERO.
           05  DEBUG-PRINT-LINE        PIC X(133).
           05  WS-PLUS-DAYS            PIC 9(02)  VALUE ZERO.
           05  WS-CHANGE-TYPE          PIC X(12)  VALUE SPACE.
           05  WS-CHANGE-TYPE-SAVE     PIC X(12)  VALUE SPACE.
           05  WS-CURR-ELEMENT         PIC X(08).
           05  WS-CURR-LIBRARY         PIC X(08).

           05  WS-CURRENT-REPORT-DATE.
               10  WS-CURRENT-YEAR     PIC 9(04).
               10  WS-CURRENT-MONTH    PIC 9(02).
               10  WS-CURRENT-DAY      PIC 9(02).


           EJECT
      ****************************************************************
      *   REPORT 01
      ****************************************************************
       01  RPT01-HEADER-1.
           05  FILLER                  PIC X(01)  VALUE SPACES.
           05  RPT01HA-ELEMENT-NAME       PIC X(08) VALUE 'ELEMENT '.
           05  FILLER                     PIC X(02) VALUE SPACE.
           05  RPT01HA-ELEMENT-LIB        PIC X(08) VALUE SPACE.
           05  FILLER                     PIC X(02) VALUE SPACE.
           05  RPT01HA-CHANGE-TYPE        PIC X(12) VALUE 'TYPE OF'.
           05  FILLER                     PIC X(02) VALUE SPACE.
           05  RPT01HA-OLD-NEW            PIC X(04) VALUE SPACE.
           05  FILLER                     PIC X(01) VALUE SPACE.
           05  RPT01HA-ELEMENT-LEVEL      PIC X(05) VALUE '     '.
           05  FILLER                     PIC X(02) VALUE SPACE.
           05  RPT01HA-CHANGE-CODE        PIC X(08) VALUE ' CHANGE'.
           05  FILLER                     PIC X(02) VALUE SPACE.
           05  RPT01HA-PRM-DATE           PIC X(10) VALUE ' PROMOTE  '.
           05  FILLER                     PIC X(02) VALUE SPACE.
           05  RPT01HA-PRM-TIME           PIC X(08) VALUE 'PROMOTE'.
           05  FILLER                     PIC X(02) VALUE SPACE.
           05  RPT01HA-LAST-PROM          PIC X(08) VALUE 'PROMOTED'.
           05  FILLER                     PIC X(02) VALUE SPACE.
           05  RPT01HA-EDIT-DATE          PIC X(10) VALUE '   EDIT '.
           05  FILLER                     PIC X(02) VALUE SPACE.
           05  RPT01HA-LAST-EDIT-BY       PIC X(08) VALUE '  LAST  '.
           05  FILLER                     PIC X(01) VALUE SPACE.
           05  RPT01HA-NUMBER-LINES       PIC X(08) VALUE ' NUMBER'.
           05  FILLER                     PIC X(02) VALUE SPACE.
           05  RPT01HA-ELEMENT-LANG       PIC X(08) VALUE SPACE.

       01  RPT01-HEADER-2.
           05  FILLER                     PIC X(01) VALUE SPACES.
           05  RPT01HB-ELEMENT-NAME       PIC X(08) VALUE '  NAME  '.
           05  FILLER                     PIC X(02) VALUE SPACE.
           05  RPT01HB-ELEMENT-LIB        PIC X(08) VALUE 'LIBRARY'.
           05  FILLER                     PIC X(02) VALUE SPACE.
           05  RPT01HB-CHANGE-TYPE        PIC X(12) VALUE 'CHANGE'.
           05  FILLER                     PIC X(02) VALUE SPACE.
           05  RPT01HB-OLD-NEW            PIC X(04) VALUE SPACE.
           05  FILLER                     PIC X(01) VALUE SPACE.
           05  RPT01HB-ELEMENT-LEVEL      PIC X(05) VALUE 'LEVEL'.
           05  FILLER                     PIC X(02) VALUE SPACE.
           05  RPT01HB-CHANGE-CODE        PIC X(08) VALUE '  CODE'.
           05  FILLER                     PIC X(02) VALUE SPACE.
           05  RPT01HB-PRM-DATE           PIC X(10) VALUE '   DATE '.
           05  FILLER                     PIC X(02) VALUE SPACE.
           05  RPT01HB-PRM-TIME           PIC X(08) VALUE '  TIME'.
           05  FILLER                     PIC X(02) VALUE SPACE.
           05  RPT01HB-LAST-PROM          PIC X(08) VALUE '   BY   '.
           05  FILLER                     PIC X(02) VALUE SPACE.
           05  RPT01HB-EDIT-DATE          PIC X(10) VALUE '   DATE'.
           05  FILLER                     PIC X(02) VALUE SPACE.
           05  RPT01HB-LAST-EDIT-BY       PIC X(08) VALUE 'EDIT BY'.
           05  FILLER                     PIC X(01) VALUE SPACE.
           05  RPT01HB-NUMBER-LINES       PIC X(08) VALUE ' LINES'.
           05  FILLER                     PIC X(02) VALUE SPACE.
           05  RPT01HB-ELEMENT-LANG       PIC X(08) VALUE 'LANGUAGE'.

       01  RPT01-HEADER-3.
           05  FILLER                     PIC X(01)  VALUE SPACES.
           05  RPT01HC-ELEMENT-NAME       PIC X(08) VALUE ALL '-'.
           05  FILLER                     PIC X(02) VALUE SPACE.
           05  RPT01HC-ELEMENT-LIB        PIC X(08) VALUE ALL '-'.
           05  FILLER                     PIC X(02) VALUE SPACE.
           05  RPT01HC-CHANGE-TYPE        PIC X(12) VALUE ALL '-'.
           05  FILLER                     PIC X(02) VALUE SPACE.
           05  RPT01HC-OLD-NEW            PIC X(04) VALUE SPACE.
           05  FILLER                     PIC X(01) VALUE SPACE.
           05  RPT01HC-ELEMENT-LEVEL      PIC X(05) VALUE '-----'.
           05  FILLER                     PIC X(02) VALUE SPACE.
           05  RPT01HC-CHANGE-CODE        PIC X(08) VALUE ALL '-'.
           05  FILLER                     PIC X(02) VALUE SPACE.
           05  RPT01HC-PRM-DATE           PIC X(10) VALUE ALL '-'.
           05  FILLER                     PIC X(02) VALUE SPACE.
           05  RPT01HC-PRM-TIME           PIC X(08) VALUE 'HH:MM:SS'.
           05  FILLER                     PIC X(02) VALUE SPACE.
           05  RPT01HC-LAST-PROM          PIC X(08) VALUE ALL '-'.
           05  FILLER                     PIC X(02) VALUE SPACE.
           05  RPT01HC-EDIT-DATE          PIC X(10) VALUE ALL '-'.
           05  FILLER                     PIC X(02) VALUE SPACE.
           05  RPT01HC-LAST-EDIT-BY       PIC X(08) VALUE ALL '-'.
           05  FILLER                     PIC X(01) VALUE SPACE.
           05  RPT01HC-NUMBER-LINES       PIC X(08) VALUE ALL '-'.
           05  FILLER                     PIC X(02) VALUE SPACE.
           05  RPT01HC-ELEMENT-LANG       PIC X(08) VALUE ALL '-'.

       01  RPT01-DETAIL-LINE.
           05  FILLER                     PIC X(01).
           05  RPT01-ELEMENT-NAME         PIC X(08).
           05  FILLER                     PIC X(02).
           05  RPT01-ELEMENT-LIB          PIC X(08).
           05  FILLER                     PIC X(02).
           05  RPT01-CHANGE-TYPE          PIC X(12).
           05  FILLER                     PIC X(02).
           05  RPT01-OLD-NEW              PIC X(04).
           05  FILLER                     PIC X(01).
           05  RPT01-ELEMENT-LEVEL        PIC ZZZZ9.
           05  FILLER                     PIC X(02).
           05  RPT01-CHANGE-CODE          PIC X(08).
           05  FILLER                     PIC X(02).
           05  RPT01-PROMOTE-DATE         PIC X(10).
           05  FILLER                     PIC X(02).
           05  RPT01-PROMOTE-TIME         PIC X(08).
           05  FILLER                     PIC X(02).
           05  RPT01-LAST-PROMOTE-BY      PIC X(08).
           05  FILLER                     PIC X(02).
           05  RPT01-EDIT-DATE            PIC X(10).
           05  FILLER                     PIC X(02).
           05  RPT01-LAST-EDIT-BY         PIC X(08).
           05  FILLER                     PIC X(01).
           05  RPT01-NUMBER-LINES         PIC ZZZZ,ZZ9.
           05  FILLER                     PIC X(02).
           05  RPT01-ELEMENT-LANG         PIC X(08).


       01  RPT01-CONTROL-LINE.
           05  FILLER                     PIC X(10) VALUE SPACE.
           05  RPT01C-NAME                PIC X(30) VALUE SPACE.
           05  FILLER                     PIC X(02) VALUE SPACE.
           05  RPT01C-COUNT               PIC ZZZ,ZZZ,ZZ9.
           05  FILLER                     PIC X(02) VALUE SPACE.

       01  RPT01-CROSS-REF-LINE.
           05  FILLER                     PIC X(10) VALUE SPACE.
           05  RPT01X-LIBRARY-NAME        PIC X(70) VALUE SPACE.
           05  FILLER                     PIC X(02) VALUE SPACE.

       01  RPT02-HEADER-1.
           05  FILLER                     PIC X(01)  VALUE SPACE.
           05  RPT02HA-ELEMENT-NAME       PIC X(08)  VALUE 'ELEMENT'.
           05  FILLER                     PIC X(02)  VALUE SPACE.
           05  RPT02HA-ELEMENT-LIB        PIC X(08)  VALUE SPACE.
           05  FILLER                     PIC X(02)  VALUE SPACE.
           05  RPT02HA-CHANGE-TYPE        PIC X(12)  VALUE 'TYPE OF'.
           05  FILLER                     PIC X(02)  VALUE SPACE.
           05  RPT02HA-OLD-NEW            PIC X(04)  VALUE SPACE.
           05  FILLER                     PIC X(01)  VALUE SPACE.
           05  RPT02HA-ELEMENT-LEVEL      PIC X(05)  VALUE SPACE.
           05  FILLER                     PIC X(02)  VALUE SPACE.
           05  RPT02HA-PROMOTE-DATE       PIC X(10)  VALUE ' PROMOTE'.
           05  FILLER                     PIC X(02)  VALUE SPACE.
           05  RPT02HA-LAST-EDIT-BY       PIC X(08)  VALUE '  LAST'.
           05  FILLER                     PIC X(02)  VALUE SPACE.
           05  RPT02HA-EDIT-DATE          PIC X(10)  VALUE '  EDIT'.
           05  FILLER                     PIC X(02)  VALUE SPACE.
           05  RPT02HA-EDIT-TIME          PIC X(08)  VALUE '  EDIT'.
           05  FILLER                     PIC X(01)  VALUE SPACE.
           05  RPT02HA-NUMBER-LINES       PIC X(08)  VALUE ' NUMBER'.
           05  FILLER                     PIC X(02)  VALUE SPACE.
           05  RPT02HA-CREATE-DATE        PIC X(10)  VALUE '  CREATE'.

       01  RPT02-HEADER-2.
           05  FILLER                     PIC X(01)  VALUE SPACE.
           05  RPT02HB-ELEMENT-NAME       PIC X(08)  VALUE '  NAME '.
           05  FILLER                     PIC X(02)  VALUE SPACE.
           05  RPT02HB-ELEMENT-LIB        PIC X(08)  VALUE 'LIBRARY'.
           05  FILLER                     PIC X(02)  VALUE SPACE.
           05  RPT02HB-CHANGE-TYPE        PIC X(12)  VALUE 'CHANGE '.
           05  FILLER                     PIC X(02)  VALUE SPACE.
           05  RPT02HB-OLD-NEW            PIC X(04)  VALUE SPACE.
           05  FILLER                     PIC X(01)  VALUE SPACE.
           05  RPT02HB-ELEMENT-LEVEL      PIC X(05)  VALUE 'LEVEL'.
           05  FILLER                     PIC X(02)  VALUE SPACE.
           05  RPT02HB-PROMOTE-DATE       PIC X(10)  VALUE '   DATE '.
           05  FILLER                     PIC X(02)  VALUE SPACE.
           05  RPT02HB-LAST-EDIT-BY       PIC X(08)  VALUE 'EDIT BY'.
           05  FILLER                     PIC X(02)  VALUE SPACE.
           05  RPT02HB-EDIT-DATE          PIC X(10)  VALUE '  DATE'.
           05  FILLER                     PIC X(02)  VALUE SPACE.
           05  RPT02HB-EDIT-TIME          PIC X(08)  VALUE '  TIME'.
           05  FILLER                     PIC X(01)  VALUE SPACE.
           05  RPT02HB-NUMBER-LINES       PIC X(08)  VALUE ' LINES '.
           05  FILLER                     PIC X(02)  VALUE SPACE.
           05  RPT02HB-CREATE-DATE        PIC X(10)  VALUE '   DATE '.

       01  RPT02-HEADER-3.
           05  FILLER                     PIC X(01)  VALUE SPACE.
           05  RPT02HC-ELEMENT-NAME       PIC X(08)  VALUE ALL '-'.
           05  FILLER                     PIC X(02)  VALUE SPACE.
           05  RPT02HC-ELEMENT-LIB        PIC X(08)  VALUE ALL '-'.
           05  FILLER                     PIC X(02)  VALUE SPACE.
           05  RPT02HC-CHANGE-TYPE        PIC X(12)  VALUE ALL '-'.
           05  FILLER                     PIC X(02)  VALUE SPACE.
           05  RPT02HC-OLD-NEW            PIC X(04)  VALUE SPACE.
           05  FILLER                     PIC X(01)  VALUE SPACE.
           05  RPT02HC-ELEMENT-LEVEL      PIC X(05)  VALUE ALL '-'.
           05  FILLER                     PIC X(02)  VALUE SPACE.
           05  RPT02HC-PROMOTE-DATE       PIC X(10)  VALUE ALL '-'.
           05  FILLER                     PIC X(02)  VALUE SPACE.
           05  RPT02HC-LAST-EDIT-BY       PIC X(08)  VALUE ALL '-'.
           05  FILLER                     PIC X(02)  VALUE SPACE.
           05  RPT02HC-EDIT-DATE          PIC X(10)  VALUE ALL '-'.
           05  FILLER                     PIC X(02)  VALUE SPACE.
           05  RPT02HC-EDIT-TIME          PIC X(08)  VALUE ALL '-'.
           05  FILLER                     PIC X(01)  VALUE SPACE.
           05  RPT02HC-NUMBER-LINES       PIC X(08)  VALUE ALL '-'.
           05  FILLER                     PIC X(02)  VALUE SPACE.
           05  RPT02HC-CREATE-DATE        PIC X(10)  VALUE ALL '-'.

       01  RPT02-DETAIL-LINE.
           05  FILLER                     PIC X(01).
           05  RPT02-ELEMENT-NAME         PIC X(08).
           05  FILLER                     PIC X(02).
           05  RPT02-ELEMENT-LIB          PIC X(08).
           05  FILLER                     PIC X(02).
           05  RPT02-CHANGE-TYPE          PIC X(12).
           05  FILLER                     PIC X(02).
           05  RPT02-OLD-NEW              PIC X(04).
           05  FILLER                     PIC X(01).
           05  RPT02-ELEMENT-LEVEL        PIC ZZZZ9.
           05  FILLER                     PIC X(02).
           05  RPT02-PROMOTE-DATE         PIC X(10).
           05  FILLER                     PIC X(02).
           05  RPT02-LAST-EDIT-BY         PIC X(08).
           05  FILLER                     PIC X(02).
           05  RPT02-EDIT-DATE            PIC X(10).
           05  FILLER                     PIC X(02).
           05  RPT02-EDIT-TIME            PIC X(08).
           05  FILLER                     PIC X(01).
           05  RPT02-NUMBER-LINES         PIC ZZZZ,ZZ9.
           05  FILLER                     PIC X(02).
           05  RPT02-CREATE-DATE          PIC X(10).

           EJECT
      *  COMMON REPORT HEADINGS AND FOOTER
      *  DATE-TIME WORK AREAS
      *  COMMON DEBUG WORK AREAS
      *  COMMON ABEND WORK AREA
           COPY JXCPW020.

           EJECT
      *+ INSTALLATION SPECIFIC CONSTANTS
           COPY JXINW005.

           EJECT
       LINKAGE SECTION.
      *  COMMON PROGRAM RUN PARAMETERS
           COPY JXCPL005.


       PROCEDURE DIVISION USING JXL-PARM-DATA.

           PERFORM A1000-INITIALIZATION THRU A1000-EXIT.

           PERFORM C0100-CHG-HIST-REPORTS THRU C0100-EXIT.


           PERFORM A9000-TERMINATION THRU A9000-EXIT.
           GOBACK.

       A1000-INITIALIZATION.

           DISPLAY '**************************************************'
                  '**************************************************'.
           DISPLAY ' '.
           DISPLAY 'PROGRAM JXC370JX - PRINT SOURCE CHANGES'.

           MOVE WS-PROGRAM    TO JXRD-PROGRAM
                                 JXCD-PROGRAM
                                 HD2-PROGRAM.

           MOVE 'CHANGEWIZ  HIST   REL 1.0 VER 1.0' TO FT1-PRODUCT.
           MOVE '(C) 2005 RENAISSANCE DATA SYSTEMS INC.'
                                                    TO FT1-VENDOR.


      *+  INSTALLATION SPECIFIC INITIALIZATION ROUTINES
           COPY JXINP005.
      *+  END OF INSTALLATION SPECIFIC CODE

      **  DISPLAY COMPILE DATE AND TIME
           COPY JXCPP010.

      **  PROCESS COMMON INPUT PARAMETER
           COPY JXCPP015.

           IF JX-PRINT-DEBUG
               OPEN OUTPUT DEBUG-REPORT.

       A1000-EXIT.
           EXIT.

      ***********************************************************
      *   END OF JOB PROCESSING
      ***********************************************************
       A9000-TERMINATION.

           DISPLAY ' '.
           DISPLAY 'END OF PROGRAM JXC370JX.'.
           DISPLAY ' '.
           DISPLAY '**************************************************'
                  '**************************************************'.

           IF DEBUG-PAGE-CNT > 0
               PERFORM Y9050-DEBUG-FOOTER THRU Y9050-EXIT.

           IF JX-PRINT-DEBUG OR DEBUG-PAGE-CNT > 0
               CLOSE DEBUG-REPORT.


       A9000-EXIT.
           EXIT.

           EJECT
      ***********************************************************
      *  PRINT CHANGE-HISTORY REPORT
      ***********************************************************
       C0100-CHG-HIST-REPORTS.

           PERFORM C0110-INIT-CHANGE-HISTORY THRU C0110-EXIT.

           IF RR-SCLM-FORMAT
              PERFORM C0200-PROCESS-CHG-SCLM THRU C0200-EXIT
                  UNTIL END-OF-CHG-HIST
           ELSE
              PERFORM C0300-PROCESS-CHG-PDS  THRU C0300-EXIT
                  UNTIL END-OF-CHG-HIST
           END-IF.

           PERFORM C0900-TERMINATE-CHANGE-HISTORY THRU C0900-EXIT.

       C0100-EXIT.
           EXIT.

       C0110-INIT-CHANGE-HISTORY.

           OPEN INPUT CHANGE-HISTORY-FILE.

           OPEN OUTPUT CHANGE-HISTORY-RPT.

           IF CHG-HIST-STATUS NOT = '00' AND '97'
               DISPLAY '   BAD OPEN ON CHG-HISTORY '
                       ' FILE. STATUS = '
                       CHG-HIST-STATUS
               MOVE 'Y' TO END-OF-CHG-HIST-SW
               PERFORM Z9000-ABEND-PROGRAM THRU Z9000-EXIT
           ELSE
               PERFORM X0100-READ-CHG-HIST-MSTR THRU X0100-EXIT.

           IF END-OF-CHG-HIST
               DISPLAY '    '
               DISPLAY 'NO RECORDS IN CHG-HISTORY '
                       'FILE'
               GO TO C0110-EXIT.

           MOVE WS-CDF-CCYY   TO WS-CURRENT-YEAR
           MOVE WS-CDF-MM     TO WS-CURRENT-MONTH
           MOVE WS-CDF-DD     TO WS-CURRENT-DAY.

           MOVE 'MTD' TO RR-REPORT-TYPE.
           MOVE '9999-99-99'      TO RR-START-DATE-CYMD.
           MOVE WS-CURRENT-YEAR   TO RR-START-GREG-CCYY
           MOVE WS-CURRENT-MONTH  TO RR-START-GREG-MM
           MOVE 01                TO RR-START-GREG-DD
           MOVE '9999-99-99'      TO RR-END-DATE-CYMD.
           MOVE WS-CURRENT-YEAR   TO RR-END-GREG-CCYY
           MOVE WS-CURRENT-MONTH  TO RR-END-GREG-MM
           MOVE 31                TO RR-END-GREG-DD.

           PERFORM C0120-REPORT-RANGE
              THRU C0120-EXIT.

       C0110-EXIT.
           EXIT.

      **************************************************************
      *    PROCESS THE REPORT RANGE INPUT CARDS
      **************************************************************
       C0120-REPORT-RANGE.
           OPEN INPUT RPT-RANGE-FILE.

           IF RPT-RANGE-STATUS NOT = '00' AND '97'
               DISPLAY '   BAD OPEN ON RPT RANGE   '
                       ' FILE. STATUS = '
                       RPT-RANGE-STATUS
               DISPLAY 'DATE RANGE USED = ' RPT-RANGE-WORK(1:30)
           ELSE
               PERFORM X0200-READ-RPT-RANGE THRU X0200-EXIT
           END-IF.

           CLOSE RPT-RANGE-FILE.

       C0120-EXIT.
           EXIT.

      **************************************************************
      *    PRINT REPORT OF SOURCE CHANGES IN SCLM LIBRARIES
      **************************************************************
       C0200-PROCESS-CHG-SCLM.

      *  READ HISTORY FOR AN ELEMENT UNTIL DATE FALLS IN REPORT RANGE
      *  SAVE THE PREVIOUS RECORD FOR THE REPORT

           PERFORM UNTIL END-OF-CHG-HIST    OR
              (SH-PROMOTE-DATE-CYMD  >= RR-START-DATE-CYMD AND
               SH-PROMOTE-DATE-CYMD  <= RR-END-DATE-CYMD) OR
              (SH-CAPTURE-DATE-CYMD  >= RR-START-DATE-CYMD AND
               SH-CAPTURE-DATE-CYMD  <= RR-END-DATE-CYMD)
              MOVE SOURCE-HISTORY-RECORD TO SOURCE-HISTORY-RECORD-2
              PERFORM X0100-READ-CHG-HIST-MSTR THRU X0100-EXIT
           END-PERFORM.

           IF END-OF-CHG-HIST
              GO TO C0200-EXIT
           END-IF.

           ADD 2 TO RPT-LINE-CNT.
           IF RPT-LINE-CNT > INST-LINES-PER-PAGE
               SUBTRACT 2 FROM    RPT-LINE-CNT
               PERFORM Y0110-SCHEDHST-RPT01-HEADING
                  THRU Y0110-EXIT
           ELSE
               SUBTRACT 2 FROM    RPT-LINE-CNT
           END-IF.


           IF (SH-ELEMENT-NAME NOT = SH2-ELEMENT-NAME) OR
              (SH-ELEMENT-LIB  NOT = SH2-ELEMENT-LIB)
               MOVE 'NEW ELEMENT'  TO WS-CHANGE-TYPE
               ADD 1    TO RPT-NEW-CNT
           ELSE
           IF SH-PAN-STATUS = 'DELETE'
               MOVE 'DELETED'      TO WS-CHANGE-TYPE
               ADD 1    TO RPT-DEL-CNT
           ELSE
           IF SH2-PAN-STATUS = 'DELETE'
               MOVE 'NEW ELEMENT'  TO WS-CHANGE-TYPE
               ADD 1    TO RPT-NEW-CNT
           ELSE
               MOVE 'CHANGED'      TO WS-CHANGE-TYPE
               ADD 1    TO RPT-CHG-CNT
           END-IF.

           MOVE WS-CHANGE-TYPE      TO WS-CHANGE-TYPE-SAVE.

           MOVE SH-ELEMENT-NAME     TO WS-CURR-ELEMENT
           MOVE SH-ELEMENT-LIB      TO WS-CURR-LIBRARY

           PERFORM UNTIL END-OF-CHG-HIST    OR
                         (WS-CURR-ELEMENT NOT = SH-ELEMENT-NAME) OR
                         (WS-CURR-ELEMENT  = SH-ELEMENT-NAME AND
                         (WS-CURR-LIBRARY NOT = SH-ELEMENT-LIB))
              PERFORM Y0100-PRINT-SCHED-HIST-RPT01
                 THRU Y0100-EXIT
              MOVE SOURCE-HISTORY-RECORD TO SOURCE-HISTORY-RECORD-2
              PERFORM X0100-READ-CHG-HIST-MSTR THRU X0100-EXIT
           END-PERFORM.

      * PRINT A BLANK LINE
           MOVE SPACES                 TO RPT01-DETAIL-LINE
           IF RPT-LINE-CNT < INST-LINES-PER-PAGE
              WRITE CHANGE-HIST-RPTREC FROM RPT01-DETAIL-LINE
                  AFTER ADVANCING 1 LINE
              ADD 1 TO RPT-LINE-CNT
           END-IF.

       C0200-EXIT.
           EXIT.

      **************************************************************
      *    PRINT REPORT OF SOURCE CHANGES IN PDS  LIBRARIES
      **************************************************************
       C0300-PROCESS-CHG-PDS.

      *  READ HISTORY FOR AN ELEMENT UNTIL DATE FALLS IN REPORT RANGE
      *  SAVE THE PREVIOUS RECORD FOR THE REPORT

           PERFORM UNTIL END-OF-CHG-HIST    OR
              (SH-CAPTURE-DATE-CYMD  >= RR-START-DATE-CYMD AND
               SH-CAPTURE-DATE-CYMD  <= RR-END-DATE-CYMD)
              MOVE SOURCE-HISTORY-RECORD TO SOURCE-HISTORY-RECORD-2
              PERFORM X0100-READ-CHG-HIST-MSTR THRU X0100-EXIT
           END-PERFORM.

           IF END-OF-CHG-HIST
              GO TO C0300-EXIT
           END-IF.

           ADD 2 TO RPT-LINE-CNT.
           IF RPT-LINE-CNT > INST-LINES-PER-PAGE
               SUBTRACT 2 FROM    RPT-LINE-CNT
               PERFORM Y0110-SCHEDHST-RPT01-HEADING
                  THRU Y0110-EXIT
           ELSE
               SUBTRACT 2 FROM    RPT-LINE-CNT
           END-IF.


           IF (SH-ELEMENT-NAME NOT = SH2-ELEMENT-NAME) OR
              (SH-ELEMENT-LIB  NOT = SH2-ELEMENT-LIB)
               MOVE 'NEW ELEMENT'  TO WS-CHANGE-TYPE
               ADD 1    TO RPT-NEW-CNT
           ELSE
           IF SH-PAN-STATUS = 'DELETE'
               MOVE 'DELETED'      TO WS-CHANGE-TYPE
               ADD 1    TO RPT-DEL-CNT
           ELSE
           IF SH2-PAN-STATUS = 'DELETE'
               MOVE 'NEW ELEMENT'  TO WS-CHANGE-TYPE
               ADD 1    TO RPT-NEW-CNT
           ELSE
               MOVE 'CHANGED'      TO WS-CHANGE-TYPE
               ADD 1    TO RPT-CHG-CNT
           END-IF.

           MOVE WS-CHANGE-TYPE      TO WS-CHANGE-TYPE-SAVE.

           MOVE SH-ELEMENT-NAME     TO WS-CURR-ELEMENT
           MOVE SH-ELEMENT-LIB      TO WS-CURR-LIBRARY

           PERFORM UNTIL END-OF-CHG-HIST    OR
                         (WS-CURR-ELEMENT NOT = SH-ELEMENT-NAME) OR
                         (WS-CURR-ELEMENT  = SH-ELEMENT-NAME AND
                         (WS-CURR-LIBRARY NOT = SH-ELEMENT-LIB))
              PERFORM Y0300-PRINT-SCHED-HIST-RPT02
                 THRU Y0300-EXIT
              MOVE SOURCE-HISTORY-RECORD TO SOURCE-HISTORY-RECORD-2
              PERFORM X0100-READ-CHG-HIST-MSTR THRU X0100-EXIT
           END-PERFORM.

      * PRINT A BLANK LINE
           MOVE SPACES                 TO RPT02-DETAIL-LINE
           IF RPT-LINE-CNT < INST-LINES-PER-PAGE
              WRITE CHANGE-HIST-RPTREC FROM RPT02-DETAIL-LINE
                  AFTER ADVANCING 1 LINE
              ADD 1 TO RPT-LINE-CNT
           END-IF.

       C0300-EXIT.
           EXIT.


       C0900-TERMINATE-CHANGE-HISTORY.

           PERFORM C0910-PRINT-TOTALS
              THRU C0910-EXIT.

           PERFORM C0920-PRINT-LIB-NAME
              THRU C0920-EXIT.


           IF RPT-PAGE-CNT > 0
               PERFORM Y0120-RPT01-FOOTER THRU Y0120-EXIT.

           CLOSE CHANGE-HISTORY-FILE
                 CHANGE-HISTORY-RPT.


       C0900-EXIT.
           EXIT.

      ***************************************************************
      *  PRINT FINAL TOTALS
      ***************************************************************
       C0910-PRINT-TOTALS.
           MOVE SPACE TO RPT01-CONTROL-LINE.
           DISPLAY ' '.
           DISPLAY ' '.

           ADD  7 TO RPT-LINE-CNT.
           IF RPT-LINE-CNT > INST-LINES-PER-PAGE
               SUBTRACT  7 FROM    RPT-LINE-CNT
               PERFORM Y0110-SCHEDHST-RPT01-HEADING
                  THRU Y0110-EXIT
           ELSE
              SUBTRACT  7 FROM    RPT-LINE-CNT
              WRITE CHANGE-HIST-RPTREC FROM RPT01-CONTROL-LINE
                  AFTER ADVANCING 3 LINE
              ADD 3 TO RPT-LINE-CNT
           END-IF.

           MOVE SPACE TO RPT01-CONTROL-LINE.
           MOVE 'CHANGE HISTORY READ         = '  TO RPT01C-NAME
           MOVE CHG-HIST-CNT         TO RPT01C-COUNT
      *    WRITE CHANGE-HIST-RPTREC FROM RPT01-CONTROL-LINE
      *        AFTER ADVANCING 1 LINE.
           DISPLAY  RPT01-CONTROL-LINE.

           MOVE SPACE TO RPT01-CONTROL-LINE.
           DISPLAY  RPT01-CONTROL-LINE.

           MOVE '      NEW ELEMENTS          = '  TO RPT01C-NAME
           MOVE RPT-NEW-CNT          TO RPT01C-COUNT
           WRITE CHANGE-HIST-RPTREC FROM RPT01-CONTROL-LINE
               AFTER ADVANCING 1 LINE.
           DISPLAY  RPT01-CONTROL-LINE.

           MOVE '      CHANGED ELEMENTS      = '  TO RPT01C-NAME
           MOVE RPT-CHG-CNT          TO RPT01C-COUNT
           WRITE CHANGE-HIST-RPTREC FROM RPT01-CONTROL-LINE
               AFTER ADVANCING 1 LINE.
           DISPLAY  RPT01-CONTROL-LINE.

           MOVE '      DELETED ELEMENTS      = '  TO RPT01C-NAME
           MOVE RPT-DEL-CNT          TO RPT01C-COUNT
           WRITE CHANGE-HIST-RPTREC FROM RPT01-CONTROL-LINE
               AFTER ADVANCING 1 LINE.
           DISPLAY  RPT01-CONTROL-LINE.

           ADD 3 TO RPT-LINE-CNT.

           DISPLAY '   '.

       C0910-EXIT.
           EXIT.

      ***************************************************************
      *  PRINT LIBRARY NAME CROSS REFERENCE
      ***************************************************************
       C0920-PRINT-LIB-NAME.

           OPEN INPUT LIBRARY-NAME-FILE.

           IF LIB-NAME-STATUS NOT = '00' AND '97'
               DISPLAY '   BAD OPEN ON LIBRARY-NAME'
                       ' FILE. STATUS = '
                       LIB-NAME-STATUS
               MOVE 'Y' TO END-OF-LIBNAME-SW
           ELSE
               PERFORM X0300-READ-LIB-NAME  THRU X0300-EXIT
           END-IF.

           IF NOT END-OF-LIBNAME
              MOVE SPACE TO RPT01-CROSS-REF-LINE
              DISPLAY ' '
              DISPLAY ' '

              ADD  8 TO RPT-LINE-CNT
              IF RPT-LINE-CNT > INST-LINES-PER-PAGE
                 SUBTRACT  8 FROM    RPT-LINE-CNT
                 PERFORM Y0110-SCHEDHST-RPT01-HEADING
                    THRU Y0110-EXIT
              ELSE
                 SUBTRACT  8 FROM    RPT-LINE-CNT
                 WRITE CHANGE-HIST-RPTREC FROM RPT01-CROSS-REF-LINE
                     AFTER ADVANCING 3 LINE
                 ADD 3 TO RPT-LINE-CNT
              END-IF
           END-IF.

           MOVE SPACE TO RPT01-CROSS-REF-LINE.

           PERFORM UNTIL END-OF-LIBNAME
              IF RPT-LINE-CNT > INST-LINES-PER-PAGE
                  PERFORM Y0110-SCHEDHST-RPT01-HEADING
                     THRU Y0110-EXIT
              END-IF
              MOVE LIB-CROSS-REF    TO RPT01X-LIBRARY-NAME
              WRITE CHANGE-HIST-RPTREC FROM RPT01-CROSS-REF-LINE
                  AFTER ADVANCING 1 LINE
              ADD 1 TO RPT-LINE-CNT
              DISPLAY  RPT01-CROSS-REF-LINE
              PERFORM X0300-READ-LIB-NAME  THRU X0300-EXIT
           END-PERFORM.


           CLOSE LIBRARY-NAME-FILE.


       C0920-EXIT.
           EXIT.

           EJECT
      ***************************************************************
      *  READ CHG-HISTORY FILE
      ***************************************************************
       X0100-READ-CHG-HIST-MSTR.
           READ CHANGE-HISTORY-FILE INTO SOURCE-HISTORY-RECORD.

           IF CHG-HIST-STATUS = '10'
               MOVE 'Y' TO END-OF-CHG-HIST-SW
               GO TO X0100-EXIT.

           ADD 1           TO CHG-HIST-CNT
           IF CHG-HIST-STATUS NOT = '00'
               DISPLAY '  BAD READ ON CHG-HISTORY '
                   ' FILE. STATUS = '
                   CHG-HIST-STATUS
                   ' ON RECORD NBR ' CHG-HIST-CNT
               GO TO X0100-EXIT.

       X0100-EXIT.
           EXIT.

      ***************************************************************
      *  READ RPT-RANGE   FILE
      *    FIRST RECORD HAS THE REPORT RANGE FOR THE REPORT
      *       OPTIONS ARE DAILY, MTD, PREVMTD AND RANGE
      *       DAILY AND MTD CAN INCLUDE A NUMBER OF PREVIOUS DAYS
      *       EX: DAILY   +1  WILL INCLUDE TODAY AND YESTERDAY
      *    SECOND RECORD DETERMINES WHICH REPORT FORMAT TO USE.
      *       OPTIONS ARE "SCLM" AND "PDS"
      ***************************************************************
       X0200-READ-RPT-RANGE.
           MOVE ZERO TO WS-PLUS-DAYS.

           READ RPT-RANGE-FILE INTO RPT-RANGE-WORK.

           IF RPT-RANGE-STATUS = '10'
               DISPLAY 'NO DATE CARD. USING MTD DEFAULT'
               DISPLAY 'DATE RANGE USED = ' RPT-RANGE-WORK(1:50)
               GO TO X0200-EXIT
           END-IF.

           IF RPT-RANGE-STATUS NOT = '00'
               DISPLAY '  BAD READ ON RPT RANGE'
                   ' FILE. STATUS = '
                   RPT-RANGE-STATUS
               DISPLAY 'NO DATE CARD. USING MTD DEFAULT'
               DISPLAY 'DATE RANGE USED = ' RPT-RANGE-WORK(1:50)
               GO TO X0200-EXIT
           END-IF.


           DISPLAY '           '.
           DISPLAY 'DATE CARD READ  = ' RPT-RANGE-WORK(1:50)

           IF RR-REPORT-TYPE = 'PREVMTD'
              PERFORM X0201-RPT-RANGE-PREVMTD THRU X0201-EXIT
           ELSE
           IF RR-REPORT-TYPE = 'MTD'
              PERFORM X0202-RPT-RANGE-MTD THRU X0202-EXIT
           ELSE
           IF RR-REPORT-TYPE = 'RANGE'
              PERFORM X0203-RPT-RANGE-INCARD THRU X0203-EXIT
           ELSE
           IF RR-REPORT-TYPE = 'DAILY'
              PERFORM X0204-RPT-RANGE-DAILY THRU X0204-EXIT
           ELSE
              DISPLAY 'UNKNOWN REPORT TYPE. IF DATES ARE INVALID, '
                      'MTD DATE RANGE WILL BE USED.'
              PERFORM X0202-RPT-RANGE-MTD THRU X0202-EXIT
           END-IF.

           DISPLAY 'DATE RANGE USED = ' RPT-RANGE-WORK(1:50)
           MOVE RR-START-DATE-CYMD TO RPT01-FROM-DATE.
           MOVE RR-END-DATE-CYMD   TO RPT01-TO-DATE.
           MOVE RPT01-TITLE        TO RPT02-TITLE.

      * READ SECOND RECORD.  THIS SHOWS WHICH REPORT TO FORMAT
           READ RPT-RANGE-FILE INTO RPT-DATA-TYPE.

           IF RPT-RANGE-STATUS = '10'
               DISPLAY 'NO RPT TYPE CARD. USING PDS DEFAULT'
               MOVE 'PDS' TO RR-REPORT-FORMAT
               GO TO X0200-EXIT
           END-IF.

           IF RPT-RANGE-STATUS NOT = '00'
               DISPLAY '  BAD READ ON RPT RANGE'
                   ' FILE SECOND RECORD. STATUS = '
                   RPT-RANGE-STATUS
               DISPLAY 'NO RPT TYPE CARD. USING PDS DEFAULT'
               MOVE 'PDS' TO RR-REPORT-FORMAT
               GO TO X0200-EXIT
           END-IF.

           IF NOT RR-VALID-RPT-FORMAT
               DISPLAY 'INVALID RPT-TYPE CARD IN SECOND RECORD. '
                       'USING PDS DEFAULT'
               DISPLAY '   DATA TYPE ENTERED = ' RR-REPORT-FORMAT
               DISPLAY '   VALID VALUES ARE "SCLM" AND "PDS" '
               MOVE 'PDS' TO RR-REPORT-FORMAT
               GO TO X0200-EXIT
           ELSE
               DISPLAY '   REPORT FORMAT = ' RR-REPORT-FORMAT
           END-IF.


       X0200-EXIT.
           EXIT.

      ***************************************************************
      *  CREATE PREVIOUS MONTH DATE RANGE
      ***************************************************************
       X0201-RPT-RANGE-PREVMTD.

           IF RR-START-DATE-CYMD = SPACE
              MOVE '9999-99-99'      TO RR-START-DATE-CYMD
              MOVE WS-CURRENT-YEAR   TO RR-START-GREG-CCYY
              MOVE WS-CURRENT-MONTH  TO RR-START-GREG-MM
              MOVE 01                TO RR-START-GREG-DD
              SUBTRACT 1 FROM RR-START-GREG-MM
              IF RR-START-GREG-MM = 0
                 MOVE 12 TO RR-START-GREG-MM
                 SUBTRACT 1 FROM RR-START-GREG-CCYY
              END-IF
           END-IF

           IF RR-END-DATE-CYMD = SPACE
              MOVE '9999-99-99'      TO RR-END-DATE-CYMD
              MOVE RR-START-GREG-CCYY TO RR-END-GREG-CCYY
              MOVE RR-START-GREG-MM   TO RR-END-GREG-MM
              MOVE 31                 TO RR-END-GREG-DD
           END-IF.

       X0201-EXIT.
           EXIT.

      ***************************************************************
      *  CREATE MTD DATE RANGE
      ***************************************************************
       X0202-RPT-RANGE-MTD.

           IF RR-MTD-PLUS = '+' AND RR-MTD-PLUS-DAYS NUMERIC
              MOVE RR-MTD-PLUS-DAYS TO WS-PLUS-DAYS
              MOVE SPACE TO RR-START-DATE-CYMD
           END-IF.

           IF RR-START-DATE-CYMD = SPACE
              MOVE '9999-99-99'      TO RR-START-DATE-CYMD
              MOVE WS-CURRENT-YEAR   TO RR-START-GREG-CCYY
              MOVE WS-CURRENT-MONTH  TO RR-START-GREG-MM
              MOVE 01                TO RR-START-GREG-DD
           END-IF.
           IF RR-END-DATE-CYMD = SPACE
              MOVE '9999-99-99'      TO RR-END-DATE-CYMD
              MOVE WS-CURRENT-YEAR   TO RR-END-GREG-CCYY
              MOVE WS-CURRENT-MONTH  TO RR-END-GREG-MM
              MOVE 31                TO RR-END-GREG-DD
           END-IF.

           IF WS-PLUS-DAYS > 0 AND
              WS-CURRENT-DAY <= WS-PLUS-DAYS
              MOVE WS-PLUS-DAYS TO RR-END-GREG-DD
              SUBTRACT 1 FROM RR-START-GREG-MM
              IF RR-START-GREG-MM = 0
                 MOVE 12 TO RR-START-GREG-MM
                 SUBTRACT 1 FROM RR-START-GREG-CCYY
              END-IF
           END-IF.


       X0202-EXIT.
           EXIT.

      ***************************************************************
      *  USE DATE RANGE IN INPUT
      *  DEFAULT TO MONTH TO DATE
      ***************************************************************
       X0203-RPT-RANGE-INCARD.

           IF RR-START-DATE-CYMD = SPACE
              MOVE '9999-99-99'      TO RR-START-DATE-CYMD
              MOVE WS-CURRENT-YEAR   TO RR-START-GREG-CCYY
              MOVE WS-CURRENT-MONTH  TO RR-START-GREG-MM
              MOVE 01                TO RR-START-GREG-DD
           END-IF.

           IF RR-END-DATE-CYMD = SPACE
              MOVE '9999-99-99'      TO RR-END-DATE-CYMD
              MOVE WS-CURRENT-YEAR   TO RR-END-GREG-CCYY
              MOVE WS-CURRENT-MONTH  TO RR-END-GREG-MM
              MOVE 31                TO RR-END-GREG-DD
           END-IF.

       X0203-EXIT.
           EXIT.

      ***************************************************************
      *  USE TODAY'S DATE
      *  PULL NUMBER OF DAYS PREVIOUS FROM CARD
      ***************************************************************
       X0204-RPT-RANGE-DAILY.

           IF RR-MTD-PLUS = '+' AND RR-MTD-PLUS-DAYS NUMERIC
              MOVE RR-MTD-PLUS-DAYS TO WS-PLUS-DAYS
              MOVE SPACE TO RR-START-DATE-CYMD
           END-IF.

           IF RR-START-DATE-CYMD = SPACE
              MOVE '9999-99-99'      TO RR-START-DATE-CYMD
              MOVE WS-CURRENT-YEAR   TO RR-START-GREG-CCYY
              MOVE WS-CURRENT-MONTH  TO RR-START-GREG-MM
              MOVE WS-CURRENT-DAY    TO RR-START-GREG-DD
           END-IF.
           IF RR-END-DATE-CYMD = SPACE
              MOVE '9999-99-99'      TO RR-END-DATE-CYMD
              MOVE WS-CURRENT-YEAR   TO RR-END-GREG-CCYY
              MOVE WS-CURRENT-MONTH  TO RR-END-GREG-MM
              MOVE WS-CURRENT-DAY    TO RR-END-GREG-DD
           END-IF.

           IF WS-PLUS-DAYS > 0
              IF WS-CURRENT-DAY >  WS-PLUS-DAYS
                 SUBTRACT WS-PLUS-DAYS FROM RR-START-GREG-DD
              ELSE
                 SUBTRACT RR-END-GREG-DD FROM WS-PLUS-DAYS
                 SUBTRACT 1 FROM RR-START-GREG-MM
                 IF RR-START-GREG-MM = 0
                    MOVE 12 TO RR-START-GREG-MM
                    SUBTRACT 1 FROM RR-START-GREG-CCYY
                 END-IF
                 IF RR-START-GREG-MM = 9 OR 4 OR 6 OR 11
                    COMPUTE RR-START-GREG-DD = 30 - WS-PLUS-DAYS
                 ELSE
                 IF RR-START-GREG-MM = 2
                    COMPUTE RR-START-GREG-DD = 28 - WS-PLUS-DAYS
                 ELSE
                    COMPUTE RR-START-GREG-DD = 31 - WS-PLUS-DAYS
                 END-IF
                 END-IF
              END-IF
           END-IF.


       X0204-EXIT.
           EXIT.

      ***************************************************************
      *  READ LIBRARY-NAME-FILE
      ***************************************************************
       X0300-READ-LIB-NAME.

           READ LIBRARY-NAME-FILE.

           IF LIB-NAME-STATUS = '10'
               MOVE 'Y' TO END-OF-LIBNAME-SW
               GO TO X0300-EXIT
           END-IF.

           IF LIB-NAME-STATUS NOT = '00'
               DISPLAY '  BAD READ ON LIBRARY-NAME'
                   ' FILE. STATUS = '
                   LIB-NAME-STATUS
               MOVE 'Y' TO END-OF-LIBNAME-SW
               GO TO X0300-EXIT
           END-IF.


       X0300-EXIT.
           EXIT.

           EJECT
      ***************************************************************
      ***  PRINT REPORT 01
      ***
      ***  IF THIS IS A NEW ELEMENT, DON'T PRINT THE PREVIOUS RECORD.
      ***  IF THIS IS NOT THE FIRST CHANGE, DON'T PRINT THE PREVIOUS.
      ***************************************************************

       Y0100-PRINT-SCHED-HIST-RPT01.

           IF RPT-LINE-CNT > INST-LINES-PER-PAGE
               PERFORM Y0110-SCHEDHST-RPT01-HEADING
                  THRU Y0110-EXIT
           END-IF.

           IF WS-CHANGE-TYPE = 'NEW ELEMENT' OR 'DELETED' OR SPACE
              GO TO Y0100-PRINT-CURRENT
           END-IF.

           MOVE SPACES                 TO RPT01-DETAIL-LINE

           MOVE SH2-ELEMENT-NAME        TO RPT01-ELEMENT-NAME
           MOVE WS-CHANGE-TYPE          TO RPT01-CHANGE-TYPE
           MOVE SH2-ELEMENT-LEVEL       TO RPT01-ELEMENT-LEVEL
           MOVE SH2-EDIT-DATE-CYMD      TO RPT01-EDIT-DATE
           MOVE SH2-PROMOTE-DATE-CYMD   TO RPT01-PROMOTE-DATE
           MOVE SH2-PROMOTE-TIME        TO RPT01-PROMOTE-TIME
           MOVE SH2-LAST-EDIT-BY        TO RPT01-LAST-EDIT-BY
           MOVE SH2-LAST-PROMOTE-BY     TO RPT01-LAST-PROMOTE-BY

           MOVE 'OLD'                   TO RPT01-OLD-NEW
           IF SH2-NUMBER-LINES NUMERIC
              MOVE SH2-NUMBER-LINES     TO RPT01-NUMBER-LINES
           END-IF.

           MOVE SH2-ELEMENT-LANG        TO RPT01-ELEMENT-LANG
           MOVE SH2-ELEMENT-LIB         TO RPT01-ELEMENT-LIB.
           MOVE SH2-CHANGE-CODE         TO RPT01-CHANGE-CODE.

           WRITE CHANGE-HIST-RPTREC FROM RPT01-DETAIL-LINE
               AFTER ADVANCING 1 LINE.
           ADD 1 TO RPT-LINE-CNT.

           MOVE SPACE          TO WS-CHANGE-TYPE.

      *  PRINT CURRENT RECORD
       Y0100-PRINT-CURRENT.

           MOVE SPACES                 TO RPT01-DETAIL-LINE

           MOVE SH-ELEMENT-NAME         TO RPT01-ELEMENT-NAME
           MOVE WS-CHANGE-TYPE          TO RPT01-CHANGE-TYPE
           MOVE SH-ELEMENT-LEVEL        TO RPT01-ELEMENT-LEVEL
           MOVE SH-EDIT-DATE-CYMD       TO RPT01-EDIT-DATE
           MOVE SH-LAST-EDIT-BY         TO RPT01-LAST-EDIT-BY
           MOVE SH-LAST-PROMOTE-BY      TO RPT01-LAST-PROMOTE-BY

           MOVE 'CHG'                   TO RPT01-OLD-NEW

           IF WS-CHANGE-TYPE =  'DELETED'
              MOVE SH-CAPTURE-DATE-CYMD    TO RPT01-PROMOTE-DATE
              MOVE SH-CAPTURE-TIME         TO RPT01-PROMOTE-TIME
              MOVE 'OLD'                   TO RPT01-OLD-NEW
           ELSE
           IF WS-CHANGE-TYPE =  'NEW ELEMENT'
              MOVE SH-PROMOTE-DATE-CYMD    TO RPT01-PROMOTE-DATE
              MOVE SH-PROMOTE-TIME         TO RPT01-PROMOTE-TIME
              MOVE 'NEW'                   TO RPT01-OLD-NEW
           ELSE
              MOVE SH-PROMOTE-DATE-CYMD    TO RPT01-PROMOTE-DATE
              MOVE SH-PROMOTE-TIME         TO RPT01-PROMOTE-TIME
              MOVE 'CHG'                   TO RPT01-OLD-NEW
           END-IF.

           IF SH-NUMBER-LINES NUMERIC
              MOVE SH-NUMBER-LINES      TO RPT01-NUMBER-LINES
           END-IF.

           MOVE SH-ELEMENT-LANG         TO RPT01-ELEMENT-LANG
           MOVE SH-ELEMENT-LIB          TO RPT01-ELEMENT-LIB.
           MOVE SH-CHANGE-CODE          TO RPT01-CHANGE-CODE.

           WRITE CHANGE-HIST-RPTREC FROM RPT01-DETAIL-LINE
               AFTER ADVANCING 1 LINE.
           ADD 1 TO RPT-LINE-CNT.

           MOVE SPACE          TO WS-CHANGE-TYPE.

       Y0100-EXIT.
           EXIT.

       Y0110-SCHEDHST-RPT01-HEADING.
           ADD 1                   TO RPT-PAGE-CNT.
           MOVE RPT-PAGE-CNT       TO HD1-PAGE.

           MOVE INST-COMPANY-NAME  TO HD1-COMPANY-NAME.

           IF RR-SCLM-FORMAT
              MOVE RPT01-TITLE        TO HD2-TITLE
              MOVE RPT01-REPORT-NBR   TO HD2-REPORT
           ELSE
              MOVE RPT02-TITLE        TO HD2-TITLE
              MOVE RPT02-REPORT-NBR   TO HD2-REPORT
           END-IF.

           IF RPT-PAGE-CNT > 1
               PERFORM Y0120-RPT01-FOOTER THRU Y0120-EXIT.

           WRITE CHANGE-HIST-RPTREC FROM HEADING-1
               AFTER ADVANCING PAGE.
           WRITE CHANGE-HIST-RPTREC FROM HEADING-2
               AFTER ADVANCING 1 LINE.

           IF RR-SCLM-FORMAT
              WRITE CHANGE-HIST-RPTREC FROM RPT01-HEADER-1
                  AFTER ADVANCING 2 LINE
              WRITE CHANGE-HIST-RPTREC FROM RPT01-HEADER-2
                  AFTER ADVANCING 1 LINE
              WRITE CHANGE-HIST-RPTREC FROM RPT01-HEADER-3
                  AFTER ADVANCING 1 LINE
           ELSE
              WRITE CHANGE-HIST-RPTREC FROM RPT02-HEADER-1
                  AFTER ADVANCING 2 LINE
              WRITE CHANGE-HIST-RPTREC FROM RPT02-HEADER-2
                  AFTER ADVANCING 1 LINE
              WRITE CHANGE-HIST-RPTREC FROM RPT02-HEADER-3
                  AFTER ADVANCING 1 LINE
           END-IF.

           MOVE SPACES TO CHANGE-HIST-RPTREC.
           WRITE CHANGE-HIST-RPTREC
               AFTER ADVANCING 1 LINE.

           IF RR-SCLM-FORMAT
              MOVE RPT01-HEADING-CNT TO RPT-LINE-CNT
           ELSE
              MOVE RPT02-HEADING-CNT TO RPT-LINE-CNT
           END-IF.

       Y0110-EXIT.
           EXIT.

       Y0120-RPT01-FOOTER.
           MOVE SPACES TO CHANGE-HIST-RPTREC.

           PERFORM UNTIL RPT-LINE-CNT > INST-LINES-PER-PAGE
               WRITE CHANGE-HIST-RPTREC
                   AFTER ADVANCING 1 LINE
               ADD 1 TO RPT-LINE-CNT
           END-PERFORM.

           WRITE CHANGE-HIST-RPTREC
               AFTER ADVANCING 2 LINE.
           WRITE CHANGE-HIST-RPTREC FROM FOOTER-1
               AFTER ADVANCING 1 LINE.

       Y0120-EXIT.
           EXIT.

           EJECT
      ***************************************************************
      ***  PRINT REPORT 02
      ***
      ***  IF THIS IS A NEW ELEMENT, DON'T PRINT THE PREVIOUS RECORD.
      ***  IF THIS IS NOT THE FIRST CHANGE, DON'T PRINT THE PREVIOUS.
      ***************************************************************

       Y0300-PRINT-SCHED-HIST-RPT02.

           IF RPT-LINE-CNT > INST-LINES-PER-PAGE
               PERFORM Y0110-SCHEDHST-RPT01-HEADING
                  THRU Y0110-EXIT
           END-IF.

           IF WS-CHANGE-TYPE = 'NEW ELEMENT' OR 'DELETED' OR SPACE
              GO TO Y0300-PRINT-CURRENT
           END-IF.

           MOVE SPACES                  TO RPT02-DETAIL-LINE

           MOVE SH2-ELEMENT-NAME        TO RPT02-ELEMENT-NAME
           MOVE WS-CHANGE-TYPE          TO RPT02-CHANGE-TYPE
           MOVE SH2-ELEMENT-LEVEL       TO RPT02-ELEMENT-LEVEL
           MOVE SH2-CAPTURE-DATE-CYMD   TO RPT02-PROMOTE-DATE

           MOVE SH2-EDIT-DATE-CYMD      TO RPT02-EDIT-DATE
           MOVE SH2-EDIT-TIME           TO RPT02-EDIT-TIME
           MOVE SH2-LAST-EDIT-BY        TO RPT02-LAST-EDIT-BY
           MOVE SH2-CREATE-DATE-CYMD    TO RPT02-CREATE-DATE

           MOVE 'OLD'                   TO RPT02-OLD-NEW
           IF SH2-NUMBER-LINES NUMERIC
              MOVE SH2-NUMBER-LINES     TO RPT02-NUMBER-LINES
           END-IF.

           MOVE SH2-ELEMENT-LIB         TO RPT02-ELEMENT-LIB.

           WRITE CHANGE-HIST-RPTREC FROM RPT02-DETAIL-LINE
               AFTER ADVANCING 1 LINE.
           ADD 1 TO RPT-LINE-CNT.

           MOVE SPACE          TO WS-CHANGE-TYPE.

      *  PRINT CURRENT RECORD
       Y0300-PRINT-CURRENT.

           MOVE SPACES                  TO RPT02-DETAIL-LINE

           MOVE SH-ELEMENT-NAME         TO RPT02-ELEMENT-NAME
           MOVE WS-CHANGE-TYPE          TO RPT02-CHANGE-TYPE
           MOVE SH-ELEMENT-LEVEL        TO RPT02-ELEMENT-LEVEL
           MOVE SH-EDIT-DATE-CYMD       TO RPT02-EDIT-DATE
           MOVE SH-EDIT-TIME            TO RPT02-EDIT-TIME
           MOVE SH-LAST-EDIT-BY         TO RPT02-LAST-EDIT-BY
           MOVE SH-CAPTURE-DATE-CYMD    TO RPT02-PROMOTE-DATE
           MOVE SH-CREATE-DATE-CYMD     TO RPT02-CREATE-DATE

           MOVE 'CHG'                   TO RPT02-OLD-NEW

           IF WS-CHANGE-TYPE =  'DELETED'
              MOVE 'OLD'                   TO RPT02-OLD-NEW
           ELSE
           IF WS-CHANGE-TYPE =  'NEW ELEMENT'
              MOVE 'NEW'                   TO RPT02-OLD-NEW
           ELSE
              MOVE 'CHG'                   TO RPT02-OLD-NEW
           END-IF.

           IF SH-NUMBER-LINES NUMERIC
              MOVE SH-NUMBER-LINES      TO RPT02-NUMBER-LINES
           END-IF.

           MOVE SH-ELEMENT-LIB          TO RPT02-ELEMENT-LIB.

           WRITE CHANGE-HIST-RPTREC FROM RPT02-DETAIL-LINE
               AFTER ADVANCING 1 LINE.
           ADD 1 TO RPT-LINE-CNT.

           MOVE SPACE          TO WS-CHANGE-TYPE.

       Y0300-EXIT.
           EXIT.



           EJECT
      ***************************************************************
      ***  DEBUG PRINT ROUTINES
      ***************************************************************
       Y9000-PRINT-DEBUG.
           ADD 1 TO JX-PRINT-DEBUG-CNT.
           IF JX-PRINT-DEBUG-CNT > JX-PRINT-DEBUG-SKIP
               IF DEBUG-RPT-LINE-CNT > INST-LINES-PER-PAGE
                   PERFORM Y9025-DEBUG-HEADING THRU Y9025-EXIT.

           IF JX-PRINT-DEBUG-CNT > JX-PRINT-DEBUG-SKIP
               WRITE DEBUG-RPT-LINE FROM DEBUG-PRINT-LINE
                   AFTER ADVANCING 1 LINE
               ADD 1 TO DEBUG-RPT-LINE-CNT.

           IF JX-PRINT-DEBUG-CNT > JX-PRINT-DEBUG-MAX
               MOVE 'N' TO JX-DEBUG-SW.
           MOVE SPACES TO DEBUG-PRINT-LINE.
       Y9000-EXIT.
           EXIT.

       Y9025-DEBUG-HEADING.
           ADD 1                   TO DEBUG-PAGE-CNT.
           MOVE DEBUG-PAGE-CNT     TO HD1-PAGE.
           MOVE JX-DEBUG-TITLE     TO HD2-TITLE.
           MOVE 'DB'               TO HD2-REPORT.

           MOVE '            CHANGEWIZ DEBUGGING REPORT           '
                                   TO HD1-COMPANY-NAME

           IF DEBUG-PAGE-CNT > 1
               PERFORM Y9050-DEBUG-FOOTER THRU Y9050-EXIT.

           WRITE DEBUG-RPT-LINE FROM HEADING-1
               AFTER ADVANCING PAGE.
           WRITE DEBUG-RPT-LINE FROM HEADING-2
               AFTER ADVANCING 1 LINE.
      *    WRITE DEBUG-RPT-LINE FROM DEBUG-HEADER-1
      *        AFTER ADVANCING 2 LINE.
      *    WRITE DEBUG-RPT-LINE FROM DEBUG-HEADER-2
      *        AFTER ADVANCING 1 LINE.
      *    WRITE DEBUG-RPT-LINE FROM DEBUG-HEADER-3
      *        AFTER ADVANCING 1 LINE.
           MOVE SPACES TO DEBUG-RPT-LINE.
           WRITE DEBUG-RPT-LINE
               AFTER ADVANCING 1 LINE.
           MOVE 3 TO DEBUG-RPT-LINE-CNT.
       Y9025-EXIT.
           EXIT.

       Y9050-DEBUG-FOOTER.
           MOVE SPACES TO DEBUG-RPT-LINE.

           PERFORM UNTIL DEBUG-RPT-LINE-CNT > INST-LINES-PER-PAGE
               WRITE DEBUG-RPT-LINE
                   AFTER ADVANCING 1 LINE
               ADD 1 TO DEBUG-RPT-LINE-CNT
           END-PERFORM.

           WRITE DEBUG-RPT-LINE
               AFTER ADVANCING 2 LINE.
           WRITE DEBUG-RPT-LINE FROM FOOTER-1
               AFTER ADVANCING 1 LINE.

       Y9050-EXIT.
           EXIT.


           EJECT
      *  COMMON ABEND ROUTINE
           COPY JXCPP099.


