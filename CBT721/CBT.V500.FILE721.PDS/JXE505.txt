*******************************************************************
*  JXE505 - CREATE REPORT OF MEMBERS IN DEV1 AND UAT1 SCLM
*
*******************************************************************
*  MODIFICATION LOG
*  04/15/2005  SHH  RECOMPILE ABOVE THE LINE
*******************************************************************
 FILE RPT01 PRINTER

* TABLE OF PROGRAMMER ID AND MANAGER NAME
 FILE MGRTBL  TABLE (500)
  ARG   1    8 A
  DESC  10  15 A

* TABLE OF PROGRAM PREFIX ID AND MANAGER NAME
 FILE PGMTBL  TABLE (500)
  ARG   1    2 A
  DESC  5   15 A

* INPUT CHANGE HISTORY FILE
 FILE XRMSTR
  IN-RECORD                1 200 A
  IN-ELEMENT-NAME           1  8 A
  IN-ELEMENT-LIB           10  8 A
  IN-CHANGE-CODE           67  8 A
  IN-ENVIR-LEVEL          174  8 A

* SOURCE-HISTORY-RECORD
   %JZCPR300  REDEFINES 'W'

* REDEFINE FIELDS IN COPYBOOK
  SH-ELEMENT-NAME2                          +
            SOURCE-HISTORY-RECORD                     2 A
  SH-LAST-EDIT-BY3                         +
            SOURCE-HISTORY-RECORD             +41     3 A
  SH-NUMBER-LINES-9                        +
            SOURCE-HISTORY-RECORD   +33     7 N MASK ('ZZZZ,ZZ9')


  MANAGER-NAME       W   15 A
  REC-CNT            W    9 N

****************************************************************
* CREATE REPORTS OF ELEMENTS IN SCLM DEV1 AND UAT1
****************************************************************
JOB INPUT(XRMSTR)

* FILE IS SORTED IN DESCENDING ORDER BY DATE SO ONLY LAST
* CHANGE CODE WILL BE USED.
*  WHEN A MEMBER IS CHECKED OUT INTO DEV1, ALL CHANGE CODES EVER
*  USED FOR THE MEMBER ARE ALOS COPIED DOWN TO DEV1.

  IF IN-ELEMENT-NAME = SH-ELEMENT-NAME AND +
     IN-ELEMENT-LIB  = SH-ELEMENT-LIB  AND +
     IN-ENVIR-LEVEL  = SH-ENVIR-LEVEL
        GO TO JOB
  END-IF

  SOURCE-HISTORY-RECORD = IN-RECORD

  MANAGER-NAME = 'UNKNOWN       '

  SEARCH MGRTBL  WITH SH-LAST-EDIT-BY GIVING MANAGER-NAME
  IF NOT MGRTBL
      MANAGER-NAME = 'UNKNOWN'
  END-IF

  IF SH-LAST-EDIT-BY3 = 'ISQ' 'ISO'
     MANAGER-NAME = 'MIKE XXXXXX   '
  END-IF


  IF MANAGER-NAME NE 'UNKNOWN' 'MIKE XXXXXX'
     GO TO READY-TO-PRINT
  END-IF

  SEARCH PGMTBL  WITH SH-ELEMENT-NAME2  GIVING MANAGER-NAME
  IF NOT PGMTBL
      MANAGER-NAME = 'UNKNOWN'
  END-IF

 READY-TO-PRINT.
     PRINT RPT1

****************************************************
REPORT RPT1 PRINTER RPT01   NOADJUST  SPACE 2
SEQUENCE  MANAGER-NAME    SH-LAST-EDIT-BY +
          SH-ENVIR-LEVEL  SH-ELEMENT-LIB   SH-ELEMENT-NAME
CONTROL   MANAGER-NAME  NEWPAGE +
          SH-LAST-EDIT-BY

TITLE 1 '                                   CHANGEWIZ    '
TITLE 2 SYSTIME '                  PROGRAMS IN SCLM DEVELOPMENT'
TITLE 3 'JXE505-RPT1                  MANAGER  ' MANAGER-NAME

 HEADING SH-CHANGE-CODE    ('CHANGE' 'CODE')
 HEADING SH-ELEMENT-NAME   ('ELEMENT' 'NAME')
 HEADING SH-ELEMENT-LIB    ('SOURCE' 'TYPE')
 HEADING SH-ELEMENT-LANG   ('LANGUAGE')
 HEADING SH-LAST-EDIT-BY   ('CHANGED' 'BY')
 HEADING SH-NUMBER-LINES-9   ('NBR OF ' 'LINES')
 HEADING SH-EDIT-DATE-CYMD   ('EDIT DATE')
 HEADING SH-ENVIR-LEVEL    ('LEVEL   ')

 LINE 1  SH-LAST-EDIT-BY   +
         SH-ENVIR-LEVEL    +
         SH-ELEMENT-LIB    +
         SH-ELEMENT-NAME   +
         SH-ELEMENT-LANG   +
         SH-EDIT-DATE-CYMD +
         SH-CHANGE-CODE    +
         SH-NUMBER-LINES-9   +
         TALLY

