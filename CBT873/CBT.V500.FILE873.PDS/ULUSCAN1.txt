         MACRO
         ULUSCAN &WR1=,&UCBR=,&BREG=
* ------------------------------------------------------------------ *
*   ULUSCAN - Access one ULUT (UCB Lookup Table) entry and fill      *
*             in all the rest of the fields of the ULUDSECT control  *
*             block, which depend on the current particular ULUT     *
*             table entry.                                           *
* ------------------------------------------------------------------ *
*    The ULUSCAN macro must be invoked in AMODE 31.                  *
*    The user must supply a 64 byte workarea which contains the      *
*      data described by the ULUDSECT macro.                         *
*    The ULUDSECT work area may reside in 24 bit storage.            *
* ------------------------------------------------------------------ *
*        Registers indicated by WRn will be saved and restored.      *
* ------------------------------------------------------------------ *
*                                                                    *
*    Parameter descriptions:                                         *
*                                                                    *
*        UCBR    - the register pointing to the current UCB          *
*                  pointed to by this ULUT entry                     *
*        BREG    - BCT register initially containing ULUITOTC        *
*                  to count table entries as you pass through        *
*                  them.  Later contains descending count,           *
*                  ULUDRUNC.                                         *
*        WR1     - work register - saved and restored                *
*                                                                    *
* ------------------------------------------------------------------ *
         ST    &WR1,ULUDSVW1          Save work reg previous contents
         L     &WR1,ULUDCURR          Look at current ULUT entry
         ST    &BREG,ULUDRUNC         Save descending count.
         MVC   ULUDDEVA(2),0(&WR1)    Get hex device address in entry
         MVC   ULUDCNTR(2),4(&WR1)    Get counter of this ULUT entry
         L     &UCBR,8(,&WR1)         Get address of current UCB
         ST    &UCBR,ULUDUCBA         Save address in control block
         MVC   ULUDONLF(1),3(&UCBR)   Save byte to show if online
         MVC   ULUDDEVT(4),X'10'(&UCBR) Copy device type to ctrl block
*                                   Get address of UCB common extension
         ST    R1,ULUDSVR1            Save R1 in control block
         L     R1,ULUDUCBA            Load real UCB address
         ST    R0,ULUDSVR0            Save register 0
         IOSCMXR
         ST    R1,ULUDCEXT            Result of macro, UCB common ext.
         L     R0,ULUDSVR0            Restore register 0
         L     R1,ULUDSVR1            Restore register 1
         LA    &WR1,X'C'(,&WR1)       Go to next ULUT entry
         ST    &WR1,ULUDNEXT          And save it in control block
         L     &WR1,ULUDSVW1          Restore prev work reg contents
         MEND
