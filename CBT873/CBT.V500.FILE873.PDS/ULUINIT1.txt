         MACRO
         ULUINIT &WRKAREA=,&UREG=,&EXIT=,&BREG=,&WR1=,&WR2=
* ------------------------------------------------------------------ *
*   ULUINIT - Access the ULUT (UCB Lookup Table) and initialize      *
*             the ULUDSECT control block with some of the ULUT       *
*             information.                                           *
* ------------------------------------------------------------------ *
*    The ULUINIT macro must be invoked in AMODE 31.                  *
*    The ULUINIT macro initializes the ULUDSECT control block.       *
*    The user must supply a 64 byte workarea for ULUINIT to use.     *
*    The ULUDSECT work area may reside in 24 bit storage.            *
* ------------------------------------------------------------------ *
*        Registers indicated by WRn will be saved and restored.      *
* ------------------------------------------------------------------ *
*                                                                    *
*    Parameter descriptions:                                         *
*                                                                    *
*        WRKAREA - a 64 byte work area to contain the data           *
*                  described by the ULUDSECT control block           *
*        UREG    - the USING register for accessiblity to the        *
*                  ULUDSECT control block in the work area           *
*        EXIT    - a location to escape the ULUT access if           *
*                  there is an error                                 *
*        BREG    - BCT register initially containing ULUITOTC        *
*                  to count table entries as you pass through        *
*                  them.  May not have to be decremented by BCT.     *
*                  But it should be decremented for each ULUT        *
*                  entry that was passed through.                    *
*        WR1     - work register - saved and restored                *
*        WR2     - work register - saved and restored                *
*                                                                    *
* ------------------------------------------------------------------ *
         LA    &UREG,&WRKAREA         Point to work area
         USING ULUDSECT,&UREG
*                                     Get ULUT and initialize.
         ST    &UREG,ULUDSVUR         Save work regs in control block
         ST    &WR1,ULUDSVW1
         ST    &WR2,ULUDSVW2
         ST    &BREG,ULUDSVBR
         L     &WR1,16                CVT
         L     &WR1,X'7C'(,&WR1)      CVTIXAVL
         L     &WR1,X'D0'(,&WR1)      IOVT
         L     &WR2,8(,&WR1)          UCB LOOKUP TABLE (ULUT)
         LR    &WR1,&WR2              Point both registers to it.
         CLC   =C'ULUT',0(&WR1)       Eyecatcher there?
         BNE   &EXIT                  Nope. Not pointing to table.
         ST    &WR1,ULUDULUT          Start filling control block.
         CLI   4(&WR1),X'01'          Type 1?
         BE    ULUITYP1               Yes. Handle as type 1.
         CLI   4(&WR1),X'02'          Type 2?
         BE    ULUITYP2               Yes. Handle as type 2.
         B     &EXIT                  No. If new type, test for it here
ULUITYP1 DS    0H                     Handle Type 1
         MVI   ULUDTYPE,X'01'         Move type to control block.
         L     &WR1,X'0C'(,&WR1)      TYPE 1, addr of first entry
         ST    &WR1,ULUDCURR          Store entry addr in control block
         L     &WR2,X'10'(,&WR2)      Count of all entries.
         ST    &WR2,ULUDTOTC          Put number into control block.
         ST    &WR2,ULUDRUNC          Initialize running descend count
         B     ULUILSET               Do search processing.
*
ULUITYP2 DS    0H                     Handle Type 2
         MVI   ULUDTYPE,X'02'         Move type to control block.
         L     &WR1,X'14'(,&WR1)      TYPE 2, addr of first entry
         ST    &WR1,ULUDCURR          Store entry addr in control block
         L     &WR2,X'18'(,&WR2)      Count of all entries.
         ST    &WR2,ULUDTOTC          Put number into control block.
         ST    &WR2,ULUDRUNC          Initialize running descend count
*        B     ULUILSET               Do search processing.
*
ULUILSET DS    0H
         L     &BREG,ULUDTOTC         Load number of loops in register
         L     &WR1,ULUDSVW1          Restore work register contents
         L     &WR2,ULUDSVW2
*
         ULUDSECT
&SYSECT  CSECT ,                      Resume user's CSECT
         MEND
