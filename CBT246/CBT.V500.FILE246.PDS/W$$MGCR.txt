//SBGOLOBM JOB ,'SAM GOLOB',CLASS=B,
//    MSGCLASS=X,NOTIFY=&SYSUID
//*
//*  AUTHOR: H/SHERMAN
//*
//* ------------------------------------------------------------- *//
//HLASMCL  PROC
//*
//*     ASMACL                                                       *
//*                                                                  *
//* THIS PROCEDURE RUNS THE HIGH LEVEL ASSEMBLER, LINK-EDITS THE     *
//* NEWLY ASSEMBLED PROGRAM.                                         *
//*                                                                  *
//********************************************************************
//*
//C        EXEC PGM=ASMA90
//SYSLIB   DD  DSN=SYS1.MACLIB,DISP=SHR
//         DD  DSN=SYS1.MODGEN,DISP=SHR
//SYSUT1   DD  DSN=&&SYSUT1,SPACE=(4096,(120,120),,,ROUND),
//             UNIT=SYSALLDA,DCB=BUFNO=1
//SYSPRINT DD  SYSOUT=*
//SYSLIN   DD  DSN=&&OBJ,SPACE=(3040,(40,40),,,ROUND),
//             UNIT=SYSALLDA,DISP=(MOD,PASS),
//             DCB=(BLKSIZE=3040,LRECL=80,RECFM=FB,BUFNO=1)
//L        EXEC PGM=HEWL,PARM='MAP,LET,LIST,NCAL',COND=(8,LT,C)
//SYSLIN   DD  DSN=&&OBJ,DISP=(OLD,DELETE)
//         DD  DDNAME=SYSIN
//SYSLMOD  DD  DISP=(,PASS),UNIT=SYSALLDA,SPACE=(CYL,(1,1,1)),
//             DSN=&&GOSET(GO)
//SYSUT1   DD  DSN=&&SYSUT1,SPACE=(1024,(120,120),,,ROUND),
//             UNIT=SYSALLDA,DCB=BUFNO=1
//SYSPRINT DD  SYSOUT=*
//   PEND
//* ------------------------------------------------------------- *//
//*
//S1   EXEC  HLASMCL,
//    PARM.L='LIST,AC(1)'
//SYSTERM DD SYSOUT=*
//SYSIN DD  *
*---------------------------------------------------------------------*
*  ENTRY LINKAGE                                                      *
*---------------------------------------------------------------------*
W$$MGCR  CSECT
         YREGS
         SAVE  (14,12)            ;SAVE CALLER'S REGS
         LR    R12,R15            ;ADDR OF ENTRY POINT
         USING W$$MGCR,R12        ;ADDR TO THIS PROGRAM
         ST    R13,SAVEAREA+4     ;SET OUR BACKWARD LINK
         LA    R15,SAVEAREA       ;ADDR OF OUR SAVEAREA
         ST    R15,8(R13)         ;SET CALLER'S FORWARD LINK
         LR    R13,R15            ;ADDR OF OUR SAVEAREA
*
*---------------------------------------------------------------------*
*  MAIN   PROGRAM                                                     *
*---------------------------------------------------------------------*
         L     R1,0(,R1)          ;ADDR OF PARM
         LH    R11,0(,R1)         ;LENGTH OF PARM
         LA    R10,2(,R1)         ;POINT AT DATA
         C     R11,=A(L'TEXT)     ;PARM TOO LONG?
         BNH   PARMOK1            ;NO: GO ON
         ABEND 101,DUMP           ;YES: ABEND WITH DUMP
PARMOK1  DS    0H
         CLC   PARMPFX,0(R10)     ;DOES PARM START WITH PROPER PREFIX?
         BE    PARMOK2            ;YES: GO ON
         ABEND 102,DUMP           ;NO: ABEND WITH DUMP
PARMOK2  DS    0H
         B     USEROK3            ;BYPASS JOBNAME TEST
         L     R9,PSAAOLD-PSA      ;ADDR OF OUR ASCB
         L     R9,ASCBJBNI-ASCB(R9)  ;ADDR OF JOB NAME
         LTR   R9,R9              ; IS THERE A GOOD ADDRESS ?
         BZ    ISITSTC            ; IF ZERO, IS IT STC/MOUNT/LOGON
         CLI   0(R9),C'W'         ;TECH SUPPORT ... W-PREFIX JOB?
         B     USEROK3            ;YES - GO ON
         CLI   0(R9),C'S'         ;TECH SUPPORT ... S-PREFIX JOB?
         BE    USEROK3            ;YES - GO ON
         ABEND 103,DUMP           ;NO: ABEND WITH DUMP
ISITSTC  DS    0H
         B     USEROK3            ;BYPASS STCNAME TEST
         L     R9,PSAAOLD-PSA      ;ADDR OF OUR ASCB
         L     R9,ASCBJBNS-ASCB(R9)  ;ADDR OF STARTED TASK
         CLI   0(R9),C'W'         ;TECH SUPPORT ... W-PREFIX TASK?
         BE    USEROK3            ;YES - GO ON
         CLI   0(R9),C'S'         ;TECH SUPPORT ... S-PREFIX TASK?
         BE    USEROK3            ;YES - GO ON
         ABEND 103,DUMP           ;NO: ABEND WITH DUMP
USEROK3  DS    0H
         S     R11,=A(L'PARMPFX)  ;SUBTRACT LENGTH OF PREFIX
         LA    R10,L'PARMPFX(,R10) ;POINT PAST DATA PREFIX FOR COMMAND
         BCTR  R11,0              ;DECREMENT BY ONE FOR EXECUTE
         EX    R11,EXMVC          ;MOVE COMMAND TO TEXT AREA OF MGCR
         SR    R0,R0              ;INDICATE SYSTEM ISSUED COMMAND
         MODESET MODE=SUP,KEY=ZERO ;SUPERVISOR STATE, KEY 0
         MGCR  CMDBUF             ;ISSUE COMMAND
         MODESET MODE=PROB,KEY=NZERO ;BACK TO IMPOTENCE
         STIMER WAIT,BINTVL=WAITTIME ;WAIT A LITTLE BIT
*
*---------------------------------------------------------------------*
*  RETURN LINKAGE                                                     *
*---------------------------------------------------------------------*
RETURN   EQU   *
         L     R13,SAVEAREA+4     ;ADDR OF CALLER'S SAVEAREA
         RETURN (14,12),RC=(15)   ;RESTORE CALLER'S REGS AND RETURN
EXMVC    MVC   TEXT(0),0(R10)
*
*---------------------------------------------------------------------*
*  DATA  AREAS                                                        *
*---------------------------------------------------------------------*
SAVEAREA DS    18F
WAITTIME DC    A(200)             ;WAIT TIME, IN SECONDS/100
PARMPFX  DC    C'MGCR='           ;PREFIX FOR AUTHORIZED COMMAND PARM
*
CMDBUF   DS    0H
FLAG1    DC    X'80'              FLAG2 MEANINGFUL
LENGTH   DC    AL1(118)           MAX LENGTH
FLAG2    DC    X'0000'            NO TOKEN
TEXT     DC    CL128' '
CMDBUFL  EQU   *-CMDBUF
TOKEN    DC    AL4(0)
*
*---------------------------------------------------------------------*
*  SYSTEM DSECTS AND MACROS                                           *
*---------------------------------------------------------------------*
         IEZMGCR
         IHAPSA
         IHAASCB
*
*---------------------------------------------------------------------*
*  END OF PROGRAM                                                     *
*---------------------------------------------------------------------*
         END   W$$MGCR
//L.SYSLMOD DD DISP=SHR,DSN=SBGOLOB.W$$.LINKLIB
//L.SYSIN   DD *
  SETCODE AC(1)
  NAME W$$MGCR(R)
/*
