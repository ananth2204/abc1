/*  REXX                                                              */
/**********************************************************************/
/*                                                                    */
/*              BBBB   III   GGGG L      GGGG EEEEE TTTTT             */
/*              B   B   I   G     L     G     E       T               */
/*  MODULE  =   BBBB    I   G  GG L     G  GG EEEE    T               */
/*              B   B   I   G   G L     G   G E       T               */
/*              BBBB   III   GGGG LLLLL  GGGG EEEEE   T               */
/*                                                                    */
/*  #¢# AUTHOR             TKMILLE                                    */
/*  #¢# CREATED            TKMILLE 12/06/2006                         */
/*  #¢# LAST UPDATE        TKMILLE 07/06/2009                         */
/*                                                                    */
/*  PURPOSE = This exec will receive a string and return it in        */
/*            large block letters (6 lines per block - last line is   */
/*            blank).                                                 */
/*                                                                    */
/*  Example Function Call to Rexx Function exec BIGLGET:              */
/*                                                                    */
/*      result  = BIGLGET(string)                                     */
/*                                                                    */
/*      result will have the returned value of:                       */
/*                                                                    */
/*      LINE001===>abcdefg..........<===LINE002===>abcdefg......<===  */
/*      LINE003===>abcdefg..........<===LINE004===>abcdefg......<===  */
/*      LINE005===>abcdefg..........<===LINE006===>             <===  */
/*                                                                    */
/*      The maximum size of the string passed which will be           */
/*      displayed in block letters is 11 characters.                  */
/*                                                                    */
/*      Line 6 will contains spaces.  Each line will be from 1 to 80  */
/*      characters in length and delimited as described above.        */
/*                                                                    */
/*  PARMS:  DEBUG  = The parameter which displays the statements as   */
/*                   they are executed.                               */
/*                                                                    */
/**********************************************************************/
Arg parms

Trace "O"

userid   = Userid()
rtcd     = 0

debug     = ""
nbr_words = words(parms)

Do i = 1 to nbr_words
   If ABBREV('DEBUG',word(parms,i),3) = 1  then Do
      debug = "DEBUG"
      parms = DELWORD(parms,i,1)
      i = i - 1
      iterate
   End
End
If debug = "DEBUG"  then Do
   Trace "I"
End

PARSE source . How_Called .

If How_Called = 'COMMAND' Then Do
   Say "This exec must be called as a Function or Subroutine Call ..."
   Return
End

envirnmt = SYSVAR("sysenv")
nesting  = SYSVAR("sysnest")
sysicmd  = SYSVAR("sysicmd")
syspcmd  = SYSVAR("syspcmd")
sysscmd  = SYSVAR("sysscmd")

Init_Big_Letters:

T0='ABCDEFGHIJKLMNOPQRSTUVWXYZ#,$0123456789@%*/-()&+_¢|>:;.?"=!<¬{ '

T1=  '  A  BBBB  CCCCDDDD EEEEEFFFFF GGGGH   H III   JJJK  K L    M   M'
T2=  ' A A B   BC    D   DE    F    G    H   H  I     J K K  L    MM MM'
T3=  'A   ABBBB C    D   DEEEE FFFF G  GGHHHHH  I     J KK   L    M M M'
T4=  'AAAAAB   BC    D   DE    F    G   GH   H  I  J  J K K  L    M   M'
T5=  'A   ABBBB  CCCCDDDD EEEEEF     GGGGH   H III  JJ  K  K LLLLLM   M'
T1=T1'N   NOOOOOPPPP  QQQ RRRR  SSSSTTTTTU   UV   VW   WX   XY   YZZZZZ'
T2=T2'NN  NO   OP   PQ   QR   RS      T  U   UV   VW   W X X  Y Y    Z '
T3=T3'N N NO   OPPPP Q Q QRRRR  SSS   T  U   UV   VW W W  X    Y    Z  '
T4=T4'N  NNO   OP    Q  Q R  R     S  T  U   U V V WW WW X X   Y   Z   '
T5=T5'N   NOOOOOP     QQ QR   RSSSS   T   UUU   V  W   WX   X  Y  ZZZZZ'
T1=T1' # #       $$$$ 000   1  2222 3333 4  4 55555 666 77777 888  999 '
T2=T2'#####     $ $  0  00 11      2    34  4 5    6       7 8   89   9'
T3=T3' # #       $$$ 0 0 0  1     2  333 444445555 6666   7   888  9999'
T4=T4'##### ,,    $ $00  0  1    2      3   4     56   6  7  8   8    9'
T5=T5' # #   ,  $$$$  000  111 222223333    4 5555  666   7   888  999 '
T1=T1' @@@ %%  %* * *    /        (  )    &&&   +         *    |   >   '
T2=T2'@   @   %  ***    /        (    )  &      +        C*C   |    >  '
T3=T3'@ @@   %  *****  /  -----  (    )   &&  +++++     C *    |     > '
T4=T4'@     %    ***  /          (    )  &   &  +        C*C   |    >  '
T5=T5' @@@@%  %%* * */            (  )    &&&&  +  _____  *    |   >   '
T1=T1' ..   ..        ???  " "       ||     <        {{      '
T2=T2' ..   ..       ?   ?     ===== ||    <         {{      '
T3=T3'                  ?            ||   <   ¬¬¬¬¬  {{      '
T4=T4' ..   ,,   ..    ?       =====       <      ¬  {{      '
T5=T5' ..    ,   ..    ?             ..     <        {{      '

line1 = ""
line2 = ""
line3 = ""
line4 = ""
line5 = ""
line6 = " "

Build_Big_Block_Letters:

string = CENTER(Word(parms,1),11)

Do i = 1 to LENGTH(string)
  n = 1 + (INDEX(T0,TRANSLATE(SUBSTR(string,i,1))) -1) * 5
  line1 = line1" "SUBSTR(T1,n,5)
  line2 = line2" "SUBSTR(T2,n,5)
  line3 = line3" "SUBSTR(T3,n,5)
  line4 = line4" "SUBSTR(T4,n,5)
  line5 = line5" "SUBSTR(T5,n,5)
End

line1  = "LINE001===> "||line1||" <==="
line2  = "LINE002===> "||line2||" <==="
line3  = "LINE003===> "||line3||" <==="
line4  = "LINE004===> "||line4||" <==="
line5  = "LINE005===> "||line5||" <==="
line6  = "LINE006===> "||line6||" <==="

End_Up:
Return line1 line2 line3 line4 line5 line6
