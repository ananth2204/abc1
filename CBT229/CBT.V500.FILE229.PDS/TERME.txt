         MACRO                                                          00192
&TAG     TERME &OP,&POOL,&LEN,&RMK=                                     00193
         GBLC  &CR0,&CR1,&CR12,&CR13,&CR14,&CR15                        00194
         LCLA  &A                                                       00195
         LCLC  &C1,&C2                                                  00196
*******************   PROGRAM TERMINATION ROUTINE   ******************* 00197
         AIF   ('&RMK' NE 'YES').TR1   JUMP IF REMARKS NOT DESIRED      00198
* THIS MACRO WILL RESTORE THE REGISTERS, RELEASE THE SAVEAREA IF      * 00199
* OBTAIN BY 'PRIME' MACRO, AND RETURN TO NEXT HIGHER LEVEL.           * 00200
* UPON RETURN REGS. 0 & 1 WILL REMAIN THE SAME AS BEFORE THE TERME.   * 00201
*                                                                     * 00202
* (TAG)  TERME  (OP)(,POOL)(,LEN)(,RMK=NO)                            * 00203
*                                                                     * 00204
*    TAG = OPTIONAL - SYMBOLIC NAME IDENTIFIABLE TO 1ST GENERATED INST* 00205
*  TERME = REQUIRED - OPERATION CODE.                                 * 00206
*     OP = OPTIONAL - IF 'XCTL' IS SPECIFIED INSTRUCTIONS WILL BE     * 00207
*          GENERATED WHICH WILL ALLOW THE 'XCTL' MACRO TO BE ISSUED   * 00208
*          IMMEDIATLY FOLLOWING THE TERME MACRO. CONTROL WILL NOT BE  * 00209
*          GIVEN TO THE NEXT HIGHER LEVEL.                            * 00210
*   POOL = OPTIONAL - IT MUST BE PRESENT IF THE POOL OPTION IN 'PRIME'* 00211
*          WAS SPECIFIED, AND MUST INDICATE THE SAME SUBPOOL.         * 00212
*    LEN = OPTIONAL - IT MUST BE PRESENT IF THE LEN OPTION IN 'PRIME' * 00213
*          WAS SPECIFIED, AND MUST INDICATE THE SAME LENGTH.          * 00214
*    RMK = OPTIONAL (KEYWORD) - IF (RMK=YES) IS SPECIFIED THIS REMARK * 00215
*          BLOCK WILL BE LISTED IN THE MACRO EXPANSION.               * 00216
*********************************************************************** 00217
.TR1     ANOP                                                           00218
         AIF   ('&CR0' NE '').TR2      JUMP IF REG EQU IN PRIME         00219
&CR0     SETC  '0'                                                      00220
&CR1     SETC  '1'                                                      00221
&CR12    SETC  '12'                                                     00222
&CR13    SETC  '13'                                                     00223
&CR14    SETC  '14'                                                     00224
&CR15    SETC  '15'                                                     00225
.TR2     ANOP                                                           00226
&A       SETA  &SYSNDX                                                  00227
&TAG     L     &CR14,4(&CR13)                                           00228
         STM   &CR15,&CR1,16(&CR14)                                     00229
         XI    8(&CR14),X'DC'                                           00230
         BC    7,IXI5&A                                                 00231
         L     &CR1,8(&CR14)                                            00232
         AIF   ('&POOL' EQ '').TR10                                     00233
&C1      SETC  '&POOL'                                                  00234
         AGO   .TR20                                                    00235
.TR10    ANOP                                                           00236
&C1      SETC  '0'                                                      00237
.TR20    AIF   ('&LEN' EQ '').TR40                                      00238
         AIF   ('&LEN' LT '72').TR30                                    00239
&C2      SETC  '&LEN'                                                   00240
         AGO   .TR50                                                    00241
.TR30    MNOTE 4,'SAVEAREA LENGTH SPECIFIED LESS THAN 72 - ASSUME 72'   00242
.TR40    ANOP                                                           00243
&C2      SETC  '72'                                                     00244
.TR50    ANOP                                                           00245
         FREEMAIN R,LV=&C2,A=(1),SP=&C1                                 00246
IXI5&A   LR    &CR13,&CR14                                              00247
         L     &CR14,16(&CR13)                                          00248
         SPM   &CR14                                                    00249
         LM    &CR14,&CR12,12(&CR13)                                    00250
         XC    8(4,&CR13),8(&CR13) ----- VER.1 REL.6 -----              00251
         AIF   ('&OP' EQ 'XCTL').C                                      00252
         BR    &CR14                                                    00253
         MEXIT                                                          00254
.C       ANOP                                                           00255
         BALR  &CR15,0                                                  00256
         USING *,&CR15                                                  00257
         MEND                                                           00258
