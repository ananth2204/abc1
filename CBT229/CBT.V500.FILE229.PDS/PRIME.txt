         MACRO                                                          00028
&TAG     PRIME &REG,&AREA,&POOL,&LEN,&SAVE=,&R=,&RMK=                   00029
         GBLC  &CR0,&CR1,&CR12,&CR13,&CR14,&CR15                        00030
         LCLC  &C1,&C2                                                  00031
*******************   PROGRAM INITIATION ROUTINE   ******************** 00032
         AIF   ('&RMK' NE 'YES').PR10  JUMP IF REMARKS NOT DESIRED      00033
* THIS MACRO WILL CREATE A SAVEAREA, SAVE THE REGISTERS, AND PERFORM  * 00034
* THE NECESSARY ENTRY LINKAGE.  IT HAS THE FOLLOWING OPTIONS:         * 00035
*                                                                     * 00036
* (TAG)  PRIME (REG)(,AREA)(,POOL)(,LEN)(,SAVE=)(,R=)(,RMK=)          * 00037
*                                                                     * 00038
*    TAG = OPTIONAL - SYMBOLIC NAME IDENTIFIABLE TO 1ST GENERATED INST* 00039
*  PRIME = REQUIRED - OPERATION CODE.                                 * 00040
*    REG = OPTIONAL - INDICATE DESIRED BASE REGISTER OR REGISTERS.    * 00041
*          IF OMITTED REGISTER 13 ASSUMED.  IF MULTIPLE BASES DESIRED * 00042
*          SEPARATE BY COMMAS AND ENCLOSE IN PARENTHESIS (13,11,12).  * 00043
*          IF REG 13 IS SPECIFIED IT MUST BE FIRST.                   * 00044
*          DO NOT USE REG 13 AS A BASE IF RE-ENTERABLE CODING DESIRED.* 00045
*   AREA = OPTIONAL - SYMBOLIC NAME WHICH IF SPECIFIED WILL CAUSE A   * 00046
*          SAVEAREA TO BE GENERATED AND TAGGED WITH THIS NAME. IF REG * 00047
*          13 IS USED AS A BASE A SAVEAREA IS ALWAYS GENERATED AND    * 00048
*          WILL BE GIVEN THIS NAME. THIS AREA WILL NOT BE RELEASED IN * 00049
*          THE 'TERME' MACRO.  THIS IS DESIRABLE IF THE ROUTINE USING * 00050
*          THE 'PRIME & TERME' MACROS IS ENTERED SEVERAL TIMES. IF REG* 00051
*          13 IS NOT USED AS A BASE AND THIS OPTION IS NOT SPECIFIED  * 00052
*          PRIME WILL EXECUTE A 'GETMAIN' TO OBTAIN A SAVEAREA AND    * 00053
*          TERME WILL EXECUTE A 'FREEMAIN' TO RELEASE THE AREA.       * 00054
*          DO NOT SPECIFY THIS OPTION IF RE-ENTERABLE CODING DESIRED. * 00055
*   POOL = OPTIONAL - SPECIFIES THE SUBPOOL FROM WHICH THE SAVEAREA IS* 00056
*          TO BE OBTAINED. IF OMITTED SUBPOOL ZERO IS ASSUMED. THIS   * 00057
*          OPERAND IS IGNORED IF REG 13 IS USED AS A BASE OR IF THE   * 00058
*          AREA OR SAVE OPERANDS ARE SPECIFIED. IF THIS OPTION IS USE * 00059
*          THE SAME SUBPOOL MUST BE SPECIFIED IN THE 'TERME' MACRO.   * 00060
*    LEN = OPTIONAL - MAY BE USED TO SPECIFY THE AMOUNT OF STORAGE TO * 00061
*          BE OBTAINED FOR THE SAVEAREA.  THE OPERAND MUST BE NUMERIC * 00062
*          AND MUST NOT BE LESS THAN 72. THE FIRST 72 BYTES OF THE    * 00063
*          AREA ARE USED BY PRIME TO STORE THE REGISTERS. THE USER MAY* 00064
*          USE THE EXTRA AREA THAT WAS OBTAIN BEYOND 72 PASSED REG 13 * 00065
*          FOR A WORK AREA. THIS OPTION IS USEFUL FOR RE-ENTERABLE    * 00066
*          CODE AND IS IGNORED IF REG 13 IS USED AS A BASE, OR IF THE * 00067
*          AREA OR SAVE OPERANDS ARE SPECIFIED.                       * 00068
*   SAVE = OPTIONAL - SPECIFIES THE NAME OF AN 18 FULLWORD AREA WHERE * 00069
*          THE REGISTERS ARE TO BE SAVED.                             * 00070
*      R = OPTIONAL (KEY WORD) - IF (R=YES) IS SPECIFIED R0 THRU R15  * 00071
*          WILL BE EQUATED TO 0 THRU 15 TO SYMBOLICALLY IDENTIFY REGS.* 00072
*    RMK = OPTIONAL (KEYWORD) - IF (RMK=YES) IS SPECIFIED THIS REMARK * 00073
*          BLOCK WILL BE LISTED IN THE MACRO EXPANSION.               * 00074
*********************************************************************** 00075
.PR10    AIF   ('&CR0' NE '').PR40     JUMP IF REGS ALREADY EQUATED     00076
         AIF   ('&R' EQ '').PR20                                        00077
R0       EQU   0                                                        00078
R1       EQU   1                                                        00079
R2       EQU   2                                                        00080
R3       EQU   3                                                        00081
R4       EQU   4                                                        00082
R5       EQU   5                                                        00083
R6       EQU   6                                                        00084
R7       EQU   7                                                        00085
R8       EQU   8                                                        00086
R9       EQU   9                                                        00087
R10      EQU   10                                                       00088
R11      EQU   11                                                       00089
R12      EQU   12                                                       00090
R13      EQU   13                                                       00091
R14      EQU   14                                                       00092
R15      EQU   15                                                       00093
.PR20    ANOP                                                           00094
         AIF   ('&R' EQ '').PR30       JUMP IF NO EQUATE                00095
&CR0     SETC  'R0'                                                     00096
&CR1     SETC  'R1'                                                     00097
&CR12    SETC  'R12'                                                    00098
&CR13    SETC  'R13'                                                    00099
&CR14    SETC  'R14'                                                    00100
&CR15    SETC  'R15'                                                    00101
         AGO   .PR40                                                    00102
.PR30    ANOP                                                           00103
&CR0     SETC  '0'                                                      00104
&CR1     SETC  '1'                                                      00105
&CR12    SETC  '12'                                                     00106
&CR13    SETC  '13'                                                     00107
&CR14    SETC  '14'                                                     00108
&CR15    SETC  '15'                                                     00109
.PR40    DS    0F -------------------- VER.1 REL.6 -------------------- 00110
         AIF   ('&REG' NE '').PR50     JUMP IF REG PRESENT              00111
         USING *,&CR13                                                  00112
         AGO   .PR100                                                   00113
.PR50    AIF   (N'&REG GT 1).PR60           JUMP IF MORE THAN 1 REG     00114
         USING *,&REG(1)                                                00115
         AGO   .PR100                                                   00116
.PR60    AIF   (N'&REG GT 2).PR70           JUMP IF MORE THAN 2 REGS    00117
         USING *,&REG(1),&REG(2)                                        00118
         AGO   .PR100                                                   00119
.PR70    AIF   (N'&REG GT 3).PR80           JUMP IF MORE THAN 3 REGS    00120
         USING *,&REG(1),&REG(2),&REG(3)                                00121
         AGO   .PR100                                                   00122
.PR80    ANOP                                                           00123
         USING *,&REG(1),&REG(2),&REG(3),&REG(4)                        00124
         AIF   (N'&REG LT 5).PR100          JUMP IF LESS THAN 5 REGS    00125
      MNOTE 8,'** MAXIMUM NUMBER OF REGISTERS IS 4 - OTHER IGNORED **'  00126
.PR100   AIF   ('&REG' EQ '').PR110    JUMP IF REG NOT PRESENT          00127
         AIF   ('&REG(1)' EQ 'R13' OR '&REG(1)' EQ '13').PR110          00128
         AGO   .PR120                                                   00129
.PR110   AIF   ('&AREA' EQ '').PR115                                    00130
&AREA    DS    0F                                                       00131
.PR115   ANOP                                                           00132
&TAG     BAL   &CR15,76(&CR15)                                          00133
         DC    17F'0',H'4',H'4096'                                      00134
         SH    &CR15,68(&CR15)                                          00135
         STM   &CR14,&CR12,12(&CR13)                                    00136
         LR    &CR1,&CR15                                               00137
         AIF   (N'&REG LT 2).PR170                                      00138
         LR    &REG(2),&CR1                                             00139
         LA    &CR15,74(&CR1)                                           00140
         AGO   .PR150                                                   00141
.PR120   ANOP                                                           00142
&TAG     BALR  &CR15,0                                                  00143
         BCTR  &CR15,0                                                  00144
         BCTR  &CR15,0                                                  00145
         STM   &CR14,&CR12,12(&CR13)                                    00146
         LR    &REG(1),&CR15                                            00147
         AIF   ('&AREA' EQ '').PR130   JUMP IF AREA NOT DESIRED         00148
         BAL   &CR1,*+78                                                00149
&AREA    DC    18F'0',H'4096'                                           00150
         LA    &CR15,72(&CR1)                                           00151
         AGO   .PR140                                                   00152
.PR130   AIF   (N'&REG LT 2).PR160                                      00153
         BAL   &CR15,*+6                                                00154
         DC    H'4096'                                                  00155
.PR140   AIF   (N'&REG EQ 1).PR160     JUMP IF 1 REG                    00156
         LR    &REG(2),&REG(1)                                          00157
.PR150   AH    &REG(2),0(&CR15)                                         00158
         AIF   (N'&REG EQ 2).PR160     JUMP IF 2 REGS                   00159
         LR    &REG(3),&REG(2)                                          00160
         AH    &REG(3),0(&CR15)                                         00161
         AIF   (N'&REG EQ 3).PR160     JUMP IF 3 REGS                   00162
         LR    &REG(4),&REG(3)                                          00163
         AH    &REG(4),0(&CR15)                                         00164
.PR160   AIF   ('&REG(1)' EQ 'R13' OR '&REG(1)' EQ '13').PR170          00165
         AIF   ('&AREA' NE '').PR170   JUMP IF AREA                     00166
         AIF   ('&SAVE' EQ '').PR180   JUMP IF NO SAVE                  00167
         LA    &CR1,&SAVE                                               00168
.PR170   ST    &CR1,8(&CR13)                                            00169
         MVI   8(&CR13),X'A3'                                           00170
         AGO   .PR240                                                   00171
.PR180   AIF   ('&POOL' EQ '').PR190   JUMP IF NO POOL                  00172
&C1      SETC  '&POOL'                                                  00173
         AGO   .PR200                                                   00174
.PR190   ANOP                                                           00175
&C1      SETC  '0'                                                      00176
.PR200   AIF   ('&LEN' EQ '').PR220                                     00177
         AIF   ('&LEN' LT '72').PR210                                   00178
&C2      SETC  '&LEN'                                                   00179
         AGO   .PR230                                                   00180
.PR210   MNOTE 4,'SAVEAREA LENGTH SPECIFIED LESS THAN 72 - 72 ASSUMED'  00181
.PR220   ANOP                                                           00182
&C2      SETC  '72'                                                     00183
.PR230   GETMAIN R,LV=&C2,SP=&C1                                        00184
         XC    0(72,&CR1),0(&CR1)                                       00185
         ST    &CR1,8(&CR13)                                            00186
         MVI   8(&CR13),X'DC'                                           00187
.PR240   ST    &CR13,4(&CR1)                                            00188
         LM    &CR0,&CR1,20(&CR13)                                      00189
         L     &CR13,8(&CR13)                                           00190
         MEND                                                           00191
