IEBTBLK  CSECT                                                           00327
* -------------------------------------------------------------- *
*   This program can be used to write a tape file with over a    *
*   million short blocks.  Set the number of blocks, at          *
*   label LEER.  This program is used to test the high-order     *
*   block count fields in the EOF1 and EOV1 tape labels.  You    *
*   have to have a tape file with more than a million blocks,    *
*   to test if these fields are filled in properly.              *
* -------------------------------------------------------------- *
         YREGS
         USING *,R15                                                     00328
         B     EYECATCH                                                  00329
         DC    C'--- '            MAKE EYECATCHER VISIBLE                00330
         DC    C'IEBTBLK - WRITE A LOT OF BLOCKS   - '                   00331
         DC    C'&SYSDATE',C'  ',C'&SYSTIME ',C'--- '                    00332
         DC    C'LEVEL 001 '
         DC    C'--- '            MAKE EYECATCHER VISIBLE                00330
SAVEAREA DC    18F'0'                                                    00334
EYECATCH DS    0H             ENSURE ALIGNMENT                           00335
         SAVE  (14,12)                                                   00336
         LR    R5,R1          SAVE PARM POINTER                          00337
         LA    R1,SAVEAREA          NEW SAVE-AREA ADDR                   00338
         ST    R1,8(,R13)           FORWARD SAVE-AREA CHAIN PTR          00339
         ST    R13,4(,R1)           BACKWARD SAVE-AREA CHAIN PTR         00340
         LR    R13,R1               NEW SAVE AREA ADDRESS                00341
         DROP  R15                                                       00342
* -----                       SET UP ONE BASE REGISTER                   00343
         USING IEBTBLK,R12                                               00344
         LR    R12,R15                                                   00345
         LR    R1,R5          RESTORE PARM POINTER                       00354
         PRINT GEN
         OPEN  (SYSPRINT,OUTPUT)
         OPEN  (SYSUT2,OUTPUT)
         ZAP   OUTRCDS,=P'0'
LEER     CP    OUTRCDS,=P'1000200'
         BE    PRINTIT
         AP    OUTRCDS,=P'1'
         MVC   OUTRCDA(7),OUTRCDS
         OI    OUTRCDA+6,X'0F'
         UNPK  OUTAREA+6(8),OUTRCDA
         MVC   OUT08(8),OUTAREA+6
         PUT   SYSUT2,OUT08
         B     LEER
FIN      DS    0H
PRINTIT  MVC   OUTLINE,OUTLINE-1
         MVC   OUTLINE(133),HEADER
         PUT   SYSPRINT,OUTLINE
         MVC   MSG02+19(10),EDPAT
         ED    MSG02+19,OUTRCDS+2
         MVC   OUTLINE,OUTLINE-1
         MVC   OUTLINE(133),MSG02
         PUT   SYSPRINT,OUTLINE
         CLOSE SYSPRINT
THEEND   DS    0H                                                        01719
         L     R13,4(,R13)                                               01720
         RETURN (14,12),RC=0                                             01721
* ----- CONSTANTS ------ *
EDPAT    DC    X'40202020202020202120'
         DC    C' '
BLANKS   DC    CL133' '
OUTLINE  DS    CL133
HEADER   DC    CL133'1  IEBTBLK - WRITE MANY TAPE BLOCKS      '
         DC    CL133' '
MSG0A    DC    CL133'                                          '
         DC    CL133' '
MSG02    DC    CL133'     RECORDS OUT =                        '
         DC    CL133' '
INRCDS   DC    PL7'0'
OUTRCDS  DC    PL7'0'
OUTRCDA  DC    CL7' '
OUTAREA  DC    CL16'-'
ZERO16   DC    CL16'0'
OUT08    DC    CL8' '
         LTORG
SYSUT2   DCB   DDNAME=SYSUT2,MACRF=PM,DSORG=PS,RECFM=F,DEVD=TA,        X
               BLKSIZE=8,LRECL=8
SYSPRINT DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=PM,LRECL=133
AREA     DS    CL8
         END   IEBTBLK
