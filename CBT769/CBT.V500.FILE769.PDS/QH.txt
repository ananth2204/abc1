/* REXX exec to invoke Doug Nadel's ISPFHTML utility                  */
/*** trace ?r ***************************************************** \| *
*               (C) Copyright Robert AH Prins, 2007-2009               *
************************************************************************
*  ------------------------------------------------------------------  *
* | Date       | By   | Remarks                                      | *
* |------------+------+----------------------------------------------| *
* |            |      |                                              | *
* |------------+------+----------------------------------------------| *
* | 2009-07-03 | RAHP | Remove embedded non-GPL XMIT file            | *
* |------------+------+----------------------------------------------| *
* | 2009-03-16 | RAHP | Replace LISTDSI by BXPWDYN                   | *
* |------------+------+----------------------------------------------| *
* | 2007-09-24 | RAHP | Remove call to 'GETTEMP'                     | *
* |------------+------+----------------------------------------------| *
* | 2007-09-03 | RAHP | Include Doug Nadel's ISPFHTML load library   | *
* |------------+------+----------------------------------------------| *
* | 2007-08-30 | RAHP | Initial version                              | *
* |------------+------+----------------------------------------------| *
************************************************************************
* QH is a REXX exec to invoke Doug Nadel's ISTPHTML utility to capture *
* ISPF screens to HTML. Ideally it should be assigned to a PF key.     *
*                                                                      *
* See <http://www.sillysot.com/mvs/> for the manual                    *
*                                                                      *
* Important information:                                               *
*                                                                      *
* QH does NOT include the actual ISPFHTML code which is (C) Copyright  *
* Doug Nadel or IBM. The XMIT file that contains the code and that     *
* should be 'EPANQ'ed and added to this exec can be retrieved from     *
* <http://www.sillysot.com/ftp/ispfhtml.xmi>                           *
*                                                                      *
* This exec should NEVER be distributed with Doug Nadel's non-GPL code *
* embedded in it!                                                      *
************************************************************************
* Send questions and bug reports to:                                   *
*                                                                      *
* robert@prino.org / robert.ah.prins@gmail.com                         *
*                                                                      *
* Robert AH Prins                                                      *
* Taboralaan 46                                                        *
* 8400 Oostende                                                        *
* Belgium                                                              *
************************************************************************
* This program is free software: you can redistribute it and/or        *
* modify it under the terms of the GNU General Public License as       *
* published by the Free Software Foundation, either version 3 of       *
* the License, or (at your option) any later version.                  *
*                                                                      *
* This program is distributed in the hope that it will be useful,      *
* but WITHOUT ANY WARRANTY; without even the implied warranty of       *
* MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the         *
* GNU General Public License for more details.                         *
*                                                                      *
* You should have received a copy of the GNU General Public License    *
* along with this program. If not, see <http://www.gnu.org/licenses/>  *
***********************************************************************/
parse source source
parse value source with . . moi .

arg parm

parm = translate(parm)
!pds = getvar('PDS')

rc = sysdsn("'"userid()".ispfhtml.load"!pds"'")
if rc \= 'OK' then
  do
    call help
    exit
  end

select
  when parm = ''     then opt = ''
  when parm = 'NS'   then opt = ',nostart'
  when parm = 'NE'   then opt = ',noend'
  when parm = 'A'    then opt = ',append'
  when parm = 'AN'   then opt = ',append,nostart,noend'
  when parm = 'ANS'  then opt = ',append,nostart'
  when parm = 'ANE'  then opt = ',append,noend'
  otherwise
    do
      call help
      exit
    end
end

"ispexec libdef ispllib " ||,
                "dataset id('"userid()".ispfhtml.load"!pds"') stack"
"ispexec select pgm(ispfhtml) parm(html"opt")"
"ispexec libdef ispllib"
exit

/***********************************************************************
* HELP:                                                                *
*                                                                      *
* Display a small help panel                                           *
***********************************************************************/
help:
  call load_dynlib

  rc = sysdsn("'"userid()".ispfhtml.load"!pds"'")
  if rc \= 'OK' then
    do
      queue "receive indsn('"ispdyn"(ispfhtml)')"
      queue "dsn(ispfhtml.load"!pds") sysout(a)"

      zcmd = ';'moi ?
      "ispexec control nondispl end"
      "ispexec display panel(isr@prim)"
      exit
    end

  "ispexec libdef ispplib library id("dynlib") stack"

  zwinttl = 'QH Options'
  "ispexec addpop row(10) column(-1)"
  "ispexec display panel ("moi")"
  "ispexec rempop"

  "ispexec libdef ispplib"
  "free f("dynlib")"
return

/***********************************************************************
* LOAD_DYNLIB:                                                         *
*                                                                      *
* This procedure loads the via EPANQ generated panel, message and      *
* skeleton code into a library. Note that there is no reason to use    *
* different libraries for any of these objects, as long as they are    *
* named differently|                                                   *
***********************************************************************/
load_dynlib:
dynlib = 'dyn'random(99999)
alloc  = "alloc fi("dynlib") rtdsn(sysdsname) "   ||,
                   "lrecl(80) blksize(0) dir(5) " ||,
                   "new delete reuse "            ||,
                   "space(1,1)"
rc = bpxwdyn(alloc)

if rc = 0 then
  ispdyn = sysdsname
else
  ispdyn = 'NOT FOUND'

"newstack"

member = moi

queue ')attr default(%$_)'
queue '/' || '***************************************************' ||,
      '*******************' || '/'
queue '/' || '*              (C) Copyright Robert AH Prins, 2007-' ||,
      '2007              *' || '/'
queue '/' || '***************************************************' ||,
      '*******************' || '/'
queue '/' || '*  ------------------------------------------------' ||,
      '----------------  *' || '/'
queue '/' || '* | Date       | By   | Remarks                    ' ||,
      '                | *' || '/'
queue '/' || '* |------------+------+----------------------------' ||,
      '----------------| *' || '/'
queue '/' || '* |            |      |                            ' ||,
      '                | *' || '/'
queue '/' || '* |------------+------+----------------------------' ||,
      '----------------| *' || '/'
queue '/' || '* | 2007-08-30 | RAHP | Initial version            ' ||,
      '                | *' || '/'
queue '/' || '* |------------+------+----------------------------' ||,
      '----------------| *' || '/'
queue '/' || '***************************************************' ||,
      '*******************' || '/'
queue '/' || '* Panel - QH                                       ' ||,
      '                  *' || '/'
queue '/' || '*                                                  ' ||,
      '                  *' || '/'
queue '/' || '* This panel is used by the QH exec to display the ' ||,
      'possible options  *' || '/'
queue '/' || '* available.                                       ' ||,
      '                  *' || '/'
queue '/' || '***************************************************' ||,
      '*******************' || '/'
queue ' Â¢ type(text) intens(high) color(yellow)'
queue ' ~ type(text) intens(high) color(green)'
queue ' $ type(text) intens(high) color(yellow)'
queue ' '
queue ')body window(77,8)'
queue '%''?''  $-~Display this panel'
queue '%None $-~Capture the screen to a new file'
queue '%''NS'' $-~Capture screen to new file, without table start' ||,
      'ing html'
queue '%''NE'' $-~Capture screen to new file, without table endin' ||,
      'g html'
queue '%''A''  $-~Append screen to file'
queue '%''AN ''$-~Append screen to file, without table starting o' ||,
      'r ending html'
queue '%''ANE''$-~Append screen to file, without table ending htm' ||,
      'l'
queue '%''ANS''$-~Append screen to file, without table starting h' ||,
      'tml'
queue ')init'
queue ')proc'
queue '  .resp = end'
queue ')end'

call put_object

"newstack"

member = 'ispfhtml'

say 'Here you will have to put the output of the EPANQ''ed copy of'
say 'Doug Nadel''s ISPFHTML from <http://www.sillysot.com/mvs/>. It'
say 'is not included for copyright reasons.'
say
say 'You should use EPANQ with the ''BIN'' parameter to add a ''mere'''
say '582 lines of ''queue'' statements, rather than the 4682 that would'
say 'be added by just using ''EPANQ''.'
say
say 'Now download ISPFHTML.XMI, EPANQ it, add the code and try again|'

exit

put_object:
  tfil = 'tfil'random(9999)

  "alloc f("tfil") da('"ispdyn"("member")') shr reu"
  "execio" queued() "diskw "tfil" (finis"
  "free f("tfil")"

  "delstack"
return
