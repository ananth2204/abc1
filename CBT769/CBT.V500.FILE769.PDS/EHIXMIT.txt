/* REXX exec to build EHI.EXEC from my own exec library               */
/*** trace ?r ***************************************************** \| *
*               (C) Copyright Robert AH Prins, 2016-2016               *
************************************************************************
*  ------------------------------------------------------------------  *
* | Date       | By   | Remarks                                      | *
* |------------+------+----------------------------------------------| *
* |            |      |                                              | *
* |------------+------+----------------------------------------------| *
* | 2016-12-26 | RAHP | Added EHIPAN                                 | *
* |------------+------+----------------------------------------------| *
* | 2016-11-24 | RAHP | Initial version - Changed version of MAKECBT | *
* |------------+------+----------------------------------------------| *
************************************************************************
* EHIXMIT is a REXX exec to extract the relevant members from my own   *
* exec library to the latest version of my EHIEXEC library.            *
************************************************************************
* Send questions, suggestions and/or bug reports to:                   *
*                                                                      *
* robert@prino.org / robert.ah.prins@gmail.com                         *
*                                                                      *
* Robert AH Prins                                                      *
* Taboralaan 46                                                        *
* 8400 Oostende                                                        *
* Belgium                                                              *
************************************************************************
* This program is free software: you can redistribute it and/or        *
* modify it under the terms of the GNU General Public License as       *
* published by the Free Software Foundation, either version 3 of       *
* the License, or (at your option) any later version.                  *
*                                                                      *
* This program is distributed in the hope that it will be useful,      *
* but WITHOUT ANY WARRANTY; without even the implied warranty of       *
* MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the         *
* GNU General Public License for more details.                         *
*                                                                      *
* You should have received a copy of the GNU General Public License    *
* along with this program. If not, see <http://www.gnu.org/licenses/>  *
***********************************************************************/
parse source source
parse value source with . . moi .

/***********************************************************************
* Get the date                                                         *
***********************************************************************/
now  = date('S')
nows = left(now, 4)'/'substr(now, 5, 2)'/'right(now, 2)
nowd = left(now, 4)'-'substr(now, 5, 2)'-'right(now, 2)

/***********************************************************************
* Check for pseudo-recursive invocation as edit macro                  *
***********************************************************************/
"ispexec vget (rexxparm) shared"

if rc = 0 then
  do
    "ispexec verase (rexxparm) shared"

    "isredit macro"
    "isredit level 0"

    select
      when rexxparm = '$$$INDEX' then
        do
          l = '$$$INDEX - This member, the index of PRINO.EHI.EXEC'
          "isredit line 1 = (L)"

          l = '$$README - Disclaimer'
          "isredit line_before 1 = (L)"

          "isredit x all"
          "isredit f '$$README' 1 first"
          "isredit f 'GETVAR'   1 first"

          call get_members

          do m = 1 by 1 while exec.m \= -1
            "isredit f '"exec.m"' 1 first"
          end

          "isredit del x all"
          "isredit builtin sort"
        end

      otherwise
        nop
    end

    "isredit renum"
    "isredit end"

    exit
  end

/***********************************************************************
* Get my exec library (GETVAR retrieves client-site dependent data)    *
***********************************************************************/
$exec = getvar('execRAHP')
$ehi  = userid()'.EHI.EXEC'

"ispexec lmerase dataset('"$ehi"')"

"alloc f(ehi) da('"$ehi"') " ||,
             "new reu " ||,
             "space(30,30) dir(10) release " ||,
             "recfm(f b) lrecl(80) blksize(0) dsntype(pds)"

/***********************************************************************
* Copy selected members                                                *
***********************************************************************/
call get_members

drop exec
drop ehi

"ispexec lminit dataid(exec) dataset('"$exec"') enq(shr)"
"ispexec lminit dataid(ehi)  dataset('"$ehi"') enq(shr)"

do m = 1 by 1 while exec.m \= -1
  "ispexec lmcopy fromid("exec") frommem("exec.m") " ||,
                 "todataid("ehi") replace"
end

"ispexec lmfree dataid("exec")"
"ispexec lmfree dataid("ehi")"

/***********************************************************************
* Add the disclaimer and a sanitised 'GETVAR'                          *
***********************************************************************/
call add_disclaimer
call add_getvar

/***********************************************************************
* Update the ISPF statistics for selected members                      *
***********************************************************************/
drop ehi

"ispexec lminit dataid("ehi") ddname(ehi) enq(shrw)"
"ispexec lmopen dataid("ehi") option(input)"

/***********************************************************************
* Update the $$$INDEX member                                           *
***********************************************************************/
rexxparm = '$$$INDEX'
"ispexec vput (rexxparm) shared"
"ispexec edit dataid("ehi") member($$$INDEX) macro("moi")"

stats = "version(1) "      ||,
        "modlevel(0) "     ||,
        "modtime(00:00) "  ||,
        "created4("nows") " ||,
        "moddate4("nows") " ||,
        "initsize("0") "   ||,
        "modrecs("0") "    ||,
        "user(prino)"

"ispexec lmmstats dataid("ehi") member($$$INDEX) " || stats
"ispexec lmmstats dataid("ehi") member($$README) " || stats
"ispexec lmmstats dataid("ehi") member(GETVAR) "   || stats

"ispexec lmclose dataid("ehi")"

trap = outtrap('zap.')
"transmit TO."userid() "da('"$ehi"') "                       ||,
                       "outdsname('"$ehi".xmit') pds nolog " ||,
                       "msgdsname('"$ehi"($$README)') "      ||,
                       "nonotify sysout(z)"
trap = outtrap('OFF')

"free f(ehi)"

"ispexec lmerase dataset('"$ehi"')"

/***********************************************************************
* Remove my userid and traces of current site                          *
***********************************************************************/
"alloc f(xmit) da('"$ehi".xmit') reu"

"execio 10 diskr xmit (stem inrec. finis"

/***********************************************************************
* All data to be modified can be found before the INMR03 record        *
***********************************************************************/
s = ''
do r = 1 to inrec.0 until pos('INMR03', inrec.r) \= 0
  s = s || inrec.r
end

/***********************************************************************
* Round times look so much nicer...                                    *
***********************************************************************/
p = pos(date('S'), s)
if p \= 0 then
  s = overlay(date('S')'120000', s, p)

/***********************************************************************
* Write back all changed data                                          *
***********************************************************************/
do q = 1 to r
  "execio 1 diskru xmit" q
  pull zap

  out = substr(s, 1 + 80 * (q - 1), 80)

  push out
  "execio 1 diskw xmit (finis"
end

"free f(xmit)"

do m = 1 by 1 while ehi.m \= -1
  "ispexec vput (ehixmit) shared"
  "ispexec view dataset('"$exec"("ehi.m")') macro(ehirexx)"
  "ispexec verase (ehixmit) shared"
end
exit

/***********************************************************************
* ADD_DISCLAIMER:                                                      *
*                                                                      *
* Add the disclaimer to the dataset                                    *
***********************************************************************/
add_disclaimer:
  signal get_dis_text

/*
                          D I S C L A I M E R
                          -------------------
        This package is Copyright (C) Robert AH Prins 2007-$$$$

  The REXX execs in this PDS are free software: you can redistribute
  them and/or modify them under the terms of the GNU General Public
  License as published by the Free Software Foundation, either version
  3 of the License, or (at your option) any later version.

  These REXX execs are distributed in the hope that they will be
  useful, but WITHOUT ANY WARRANTY; without even the implied warranty
  of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
  General Public License for more details.

  You should have received a copy of the GNU General Public License
  along with these execs. If not, see <http://www.gnu.org/licenses/>

*/

get_dis_text:
  do i = sigl + 3 while substr(sourceline(i), 1, 2) \= '*' || '/'
    dis = left(sourceline(i), 72)

    p = pos('$$$$', dis)
    if p \= 0 then
      dis = overlay(left(date('S'), 4), dis, p, 4)

    queue dis
  end

  "alloc f(dis) da('"$ehi"($$README)') reu"
  "execio" queued() "diskw dis (finis"
  "free f(dis)"
return


/***********************************************************************
* GET_MEMBERS:                                                         *
*                                                                      *
* Build the list of members to process                                 *
***********************************************************************/
get_members:
  exec.   = -1
  exec.1  = '$$$INDEX'
  exec.2  = 'EHIASM'
  exec.3  = 'EHICOBOL'
  exec.4  = 'EHIHELP'
  exec.5  = 'EHIJCL'
  exec.6  = 'EHINONE'
  exec.7  = 'EHIPAN'
  exec.8  = 'EHIPLI'
  exec.9  = 'EHIREXX'
  exec.10 = 'EHISUPC'
  exec.11 = 'EHISUPP'
  exec.12 = 'EHIXMIT'
  exec.13 = 'GPLV3'

  ehi.    = -1
  ehi.1   = 'EHIASM'
  ehi.2   = 'EHICOBOL'
  ehi.3   = 'EHIHELP'
  ehi.4   = 'EHIJCL'
  ehi.5   = 'EHINONE'
  ehi.6   = 'EHIPAN'
  ehi.7   = 'EHIPLI'
  ehi.8   = 'EHIREXX'
  ehi.9   = 'EHISUPC'
  ehi.10  = 'EHISUPP'
return

/***********************************************************************
* ADD_GETVAR:                                                          *
*                                                                      *
* Add a sanitised 'GETVAR' member to the dataset                       *
***********************************************************************/
add_getvar:
  signal get_getvar_text

/*
/% REXX exec to return common values to calling execs                 %/
/%** trace ?r ***************************************************** \| *
*               (C) Copyright Robert AH Prins, 1996-YYYY               *
************************************************************************
*  ------------------------------------------------------------------  *
* | Date       | By   | Remarks                                      | *
* |------------+------+----------------------------------------------| *
* |            |      |                                              | *
* |------------+------+----------------------------------------------| *
* | YYYY-MM-DD | RAHP | Sanitised version for EHIXMIT                | *
* |------------+------+----------------------------------------------| *
* | 1996-05-24 | RAHP | Initial version                              | *
* |------------+------+----------------------------------------------| *
************************************************************************
* GETVAR is a REXX exec to return a number of commonly used values to  *
* the calling exec.                                                    *
*                                                                      *
* The objective of this exec is to make it possible to change the      *
* commonly used values at one single location, instead of having to    *
* change the values in every exec that uses them.                      *
**********************************************************************%/
parse arg $what

$pref = userid()

select
  when $what = 'execRAHP' then return $pref'rahp.exec'
  when $what = 'XMITNODE' then return 'FROM'
  otherwise                    return '?'
end
*/

get_getvar_text:
  do i = sigl + 3 while substr(sourceline(i), 1, 2) \= '*' || '/'
    dis = translate(left(sourceline(i), 72), '*', '%')

    p = pos('YYYY-MM-DD', dis)
    if p \= 0 then
      dis = overlay(nowd, dis, p, 10)

    p = pos('YYYY', dis)
    if p \= 0 then
      dis = overlay(left(now, 4), dis, p, 4)

    queue dis
  end

  "alloc f(dis) da('"$ehi"(getvar)') reu"
  "execio" queued() "diskw dis (finis"
  "free f(dis)"
return
/* Email to myself with release notes **********************************
* $$NOTE1 **************************************************************
Subject:    EHILITE execs in ZIP'ed XMIT format - Final pre-chop version
From:       robert@prino.org
Date:       Wed, 26 Dec 2016 14:01:00 +0200
To:         robert@prino.org

Hi all,

Attached is the "final" version of the ZIPped XMIT file containing the
EHIxxxx hilite macros. The dataset contains the following members:

$$$INDEX - This member, the index of PRINO.EHI.EXEC
$$README - Disclaimer
EHIASM   - Convert assembler to HTML with ISPF HILITE'ing
EHICOBOL - Convert COBOL to HTML with ISPF HILITE'ing
EHIHELP  - Common 'help' screen for the EHIJCL/PLI/REXX macros
EHIJCL   - Convert JCL to HTML with ISPF HILITE'ing
EHINONE  - Convert any text to HTML as non-HILITE'd ISPF Edit
EHIPAN   - Convert ISPF panel source to HTML with ISPF HILITE'ing
EHIPLI   - Convert PL/I to HTML with ISPF HILITE'ing
EHIREXX  - Convert REXX to HTML with ISPF HILITE'ing
EHISUPC  - Convert SuperC/Search-For output to HTML with ISPF HILITE'ing
EHISUPP  - Factored out support routines for the EHIxxxx execs
GPLV3    - Text of the GPL Version 3

As you can see, I've added a disclaimer, an index, the text of
the GPL V3 and two additional members,

EHIXMIT  - the exec I use to automagically build
           'PRINO.EHI.XMIT' from my own EXEC library
GETVAR   - REXX exec to return common values to calling execs

Again, please give me some feedback - use <robert@prino.org> or
<robert.ah.prins@gmail.com>

Robert
--
Robert AH Prins
Mobile: +32 487 594312
robert@prino.org (Private)
robert.ah.prins@gmail.com (Private)
***********************************************************************/
