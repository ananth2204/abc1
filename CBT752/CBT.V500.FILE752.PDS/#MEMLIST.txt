/**************************** REXX ************************************/
/** EXEC NAME      : #MEMLIST  AN EXTERNAL REXX ROUTINE              **/
/** AUTHOR         : ROLLIN E. COVILLE                               **/
/** PHONE          : WORK: 545-1596                                  **/
/** CREATE DATE    : 01/27/92                                        **/
/**                                                                  **/
/** Returns the number of members of a partitioned dataset           **/
/**        and the names of the members on the stack. If the         **/
/**        dataset is unavailable it returns a value of 0            **/
/**                                                                  **/
/**                                                                  **/
/** INPUT PARMS   -  NAME OF PARTITIONED LIBRARY                     **/
/**                     @dsname                                      **/
/** Example call:    @return=#MEMLIST(c.c4386.prod.proclib)          **/
/**                  IF @return > 0 THEN DO                          **/
/**                     DO @i=1 to @return                           **/
/**                        PULL @mem.@i                              **/
/**                        END                                       **/
/**                     END                                          **/
/**                  ELSE NOP                                        **/
/********************************************************************/
 ADDRESS TSO
 /*TRACE i*/
 PARSE UPPER ARG @dsname .
/******************************************************************/
/* Turn off prompts and messages                                  */
/******************************************************************/
@status = MSG('OFF')
@prmt = PROMPT('OFF')

/********************************************************/
/* @retcode controls processing.  Initialize it now     */
/********************************************************/
@retcode = 0
@a = 0

 ADDRESS TSO
 IF SYSDSN("'"@dsname"'") \= 'OK' THEN DO
     /********************************************************/
     /* See if library is available for processing           */
     /********************************************************/
     SAY ' '
     SAY @dsname 'is unavailable for processing'
     @retcode = 12
     END
 ELSE NOP

 IF @retcode = 0 THEN DO
     /********************************************************/
     /* Trap output of TSO LISTDS command to get list of     */
     /* members in PDS.  NOTE--List of member names begins   */
     /* on seventh line of trapped output.                   */
     /********************************************************/
     @x = OUTTRAP('@line.')
     "LISTDS '"@dsname"' MEMBERS"
     @x = OUTTRAP('OFF')
     DO @m = 7 to @line.0
        @a = @a + 1
        @mem = WORD(@line.@m,1)
        QUEUE @mem
        END
     END
 ELSE NOP
 RETURN @a
