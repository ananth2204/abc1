/*                                                               REXX

  Extract and format data from dsn db
  Filter list by values in CNTL DD as follows (may span lines):

    DSN( dsn1 dsn2 .. dsnn )
    DSNVOL( dsn1 vol1 dsn2 vol2 .. dsnn voln )
    VOL( vol1 vol2 .. voln )

  Lists are ORed when checked.

*/
 voli.=0
 dsni.=0
 dsvi.=0

 Call xmsg 'Extract and format data from dsn db'
 cc=RxVsamBa('get dd(dsndb) stack')
 if cc<>0 then exit xmsg('Read dsn db rc' cc rxvberrm,8)
 call xmsg queued() 'records read from dsndb'

 /* get filter list */
 parse value WFMCNTL() with . 'ERR('err')' .,
       0 . 'VOL('vol')' . 0 . 'DSN('dsn')' . 0 . 'DSNVOL('dsv')' .
 if err<>'' then exit 'Read cntl error' err
 pvol=(vol<>'')
 pdsn=(dsn<>'')
 pdsv=(dsv<>'')

 /* volume list as checkval */
 say 'Selecting disks' vol 'from db'
 if vol='ALL' then voli.=1
 else do m=1 to words(vol)
   v=word(vol,m)
   voli.v=1
 end

 /* dataset list as checkval */
 if dsn='' then dsni.=1
 else do m=1 to words(dsn)
   d=word(dsn,m)
   dsni.d=1
 end

 /* dataset+volume list as checkval */
 if dsv='' then dsvi.=1
 else do m=1 to words(dsnvol) by 2
   d=word(dsnvol,m+1)word(dsnvol,m)   /* vol || dsn */
   dsvi.d=1
 end

 /* reformat as vol(6) || dsn(44) || 'B' */
 qn=queued()
 do qn
   pull key vol dsn rest
   if pvol then if \voli.vol then iterate
   if pdsn then if \dsni.dsn then iterate
   vd = vol||dsn
   if pdsv then if \dsvi.vd  then iterate
   queue left(vol,6)left(dsn,44)'B'
 end
 qn=queued()
 say qn 'records passed filtering'
 "execio" queued() "diskw dbdsns (finis)"
 if rc<>0 then exit xmsg('Write extract dataset rc' rc,8)

 exit xmsg('Extract and reformat done')

XMsg: if arg(1)<>'' then say arg(1);return word(arg(2) 0,1)
