/*                                                                    rexx
   WFM DB Update
    - delete records by either dsn, vol, mbr or key

   Parm
     MSGLVL(n)            1 shows more messages i.e. dsn db updates
                          2 also show mbr db updates
     TYPRUN(t)            SIM or RUN
*/

 $rc=0
 arg p
 parse var p . 'MSGLVL('$msglvl')' . 0 . 'TYPRUN('$typrun')' .
 $msglvl  = word($msglvl 0,1)
 $typrun  = word($typrun 'RUN',1)

 if p<>'' then p='parm' p
 call xmsg 'Dsn db maintenance started' p
 say '  Type run' $typrun
 say '  Msglvl  ' $msglvl
 if wordpos($typrun,'SIM RUN')=0 then exit xmsg('Invalid typrun',8)

 Parse value wfmcntl('valid(ACT DSN VOL MBR KEY) REQ(ACT)'),
     with . 'ERR('err')'  . 0 . 'ACT('pact')' .,
       0  . 'DSN('pdsn')' . 0 . 'MBR('pmbr')' .,
       0  . 'VOL('pvol')' . 0 . 'KEY('pkey')' .
 if err<>'' then exit xmsg(err,8)

 Select
   when wordpos(pact,'DEL DELETE')>0 then $rc=Delete()
   otherwise exit xmsg('Invalid ACT',8)
 End
 say ''
 exit xmsg('Update done rc' $rc,$rc)

XMsg: if arg(1)<>'' then say time() arg(1);return word(arg(2) 0,1)
XTso: trace off; zz=outtrap(word(arg(2) '$.',1))
        address tso arg(1);zz=outtrap('off');xtsorc=rc;return rc
LowCase: return translate(arg(1),xrange('a','z'),xrange('A','Z'))
Str2Stem:
 parse arg str,stem,pfx,sfx
 do n=1 to words(str)
   zz=Value(stem''n,pfx''word(str,n)''sfx)
 end
 zz=Value(stem''0,n-1)
 return 0

/*
      Delete db entries
*/
Delete:
 say ''
 if pdsn pmbr pkey pvol='' then return xmsg('Missing data list',8)
 call xmsg 'Deleting db entries begin'

 if pdsn<>'' then call DelDsn
 if pmbr<>'' then call DelMbr
 if pvol<>'' then call DelVol
 if pkey<>'' then call DelKey
 Return 0

 /* delete records by dsn, load the dsn db to get keys */
DelDsn:
 zz=Str2Stem(pdsn,'DSNS.',,' ')
 call xmsg  'Deleting dataset records..'
 do n=1 to dsns.0
   say '  'dsns.n
 end
 cc=RxVsamBa('get dd(dsndb) textstem(dsns.) pos(21) stem(dbl.)')
 if dbl.0=0 then exit xmsg('No ds db records found')
 /* make key list */
 call xmsg 'Ds db records found'
 do n=1 to dbl.0
   say '  'dbl.n
   key.n = word(dbl.n,1)
 end
 key.0=dbl.0
 Return DelKeyX()

 call bpxwunix 'sort', 'key.', 'key.', 'STDERR.'
 /* delete dataset records */
 cc=RxVsamBa('del dd(dsndb) keystem(key.) list(ddel.)')
 if cc<>0 then exit xmsg('Delete dsn db rc' cc,8)
 call xmsg 'Records deleted:' rxvbdele+0
 /* delete member records */
 call xmsg  'Deleting member records..'
 cc=RxVsamBa('del dd(mbrdb) textstem(key.) POS(9) ENDPOS(20) list(mdel.)')
 if cc<>0 then exit xmsg('Delete mbr db rc' cc,8)
 call xmsg 'Records deleted:' rxvbdele+0
 if $msglvl>0 then do n=1 to mdel.0
   say '  ' mdel.n
 end
 return 0

 /* delete records by vol, load the dsn db to get keys */
DelVol:
 call xmsg  'Deleting volser records..'
 zz=Str2Stem(pvol,'VOLS.')
 do n=1 to vols.0
   say '  'vols.n
 end
 cc=RxVsamBa('get dd(dsndb) textstem(vols.) pos(14) stem(dbl.)')
 if dbl.0=0 then exit xmsg('No ds db records found')
 /* make key list */
 call xmsg dbl.0 'Ds db records found'
 if $msglvl>0 then,
 do n=1 to dbl.0
   say '  'dbl.n
   key.n = word(dbl.n,1)
 end
 key.0=dbl.0
 Return DelKeyX()

 /* delete records by member */
DelMbr:
 zz=Str2Stem(pmbr,'MBRS.')
 call xmsg  'Deleting member records..'
 do n=1 to mbrs.0
   say '  'mbrs.n
   mbr.n=left(mbr.n,8,'\')
 end
 cc=RxVsamBa('get dd(mbrdb) keystem(mbrs.) stem(dbl.)')
 if dbl.0=0 then exit xmsg('No mbr db records found')
 /* make key list */
 call xmsg 'Mbr db records found'
 do n=1 to dbl.0
   say '  'dbl.n
   key.n = substr(dbl.n,9,12)
 end
 key.0=dbl.0
 Return DelKeyX()

 /* delete records by keys */
DelKey:
 call xmsg  'Deleting records by key..'
 zz=Str2Stem(pkey,'KEY.')
 do n=1 to key.0
   say '  'key.n
 end
 Return DelKeyX()

 /* run delete records by keylist */
DelKeyX:
 call bpxwunix 'sort', 'key.', 'key.', 'STDERR.'
 /* delete dataset records */
 call xmsg 'Delete dsn db records'
 cc=RxVsamBa('del dd(dsndb) keystem(key.) list(ddel.)')
 if cc<>0 then exit xmsg('Delete dsn db rc' cc,8)
 call xmsg 'Records deleted:' rxvbdele+0
 if $msglvl>0 then do n=1 to ddel.0
   say '  ' ddel.n
 end
 /* delete member records */
 call xmsg 'Delete mbr db records'
 cc=RxVsamBa('del dd(mbrdb) textstem(key.) POS(9) ENDPOS(20) list(mdel.)')
 if cc<>0 then exit xmsg('Delete mbr db rc' cc,8)
 call xmsg 'Records deleted:' rxvbdele+0
 if $msglvl>1 then do n=1 to mdel.0
   say '  ' mdel.n
 end
 return 0
