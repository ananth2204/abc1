/*                                                                   REXX
  WFM4DEL - Delete records in dsn and mbr dbs based on datasetname

  msglvl 0  list statistics
         1  list datasets and members actually deleted
         2  list datasets and members potentially and actually deleted
*/
 call xmsg 'Delete database records, parm:' arg(1)
 $et = time('e')
 $rc = 0
 arg p
 parse var p . 'MSGLVL('$msglvl')' . 0 . 'SORTU('$sortu')' .,
         . 0 . 'TYPRUN('$typrun')' .
 $msglvl = word($msglvl  0,1)
 $sortu  = word($sortu  'N',1)
 if wordpos($sortu,'N Y')=0 then exit xmsg('Invalid sortu',8)
 $typrun = word($typrun 'UPDATE',1)
 if $typrun='RUN' then $typrun='UPDATE'
 if $typrun='SIM' then $typrun='DRY'
 if wordpos($typrun,'DRY UPDATE')=0 then exit xmsg('Invalid typrun',8)
 say 'msglvl:' $msglvl
 say 'sortu: ' $sortu
 say 'typrun:' $typrun

 /* read and filter dsn list */
 call xmsg 'Read and format dsn list'
 "execio * diskr updlist (stem updi. finis)"
 call xmsg updi.0 'records read'

 /* reformat list */
 ln=0
 do n=1 to updi.0
   if left(updi.n,1)='*' then iterate
   r=strip(updi.n)
   if pos(' ',r)<10 then r=space(subword(r,1,2),0)  /* vol dsn */
   ln=ln+1
   updl.ln=strip(left(r,50))    /* drop action char if any */
 end
 updl.0=ln
 call xmsg ln 'records in update list'
 drop updi.
 if $sortu='Y' then do
   call bpxwunix 'sort -u', 'updl.', 'updl.', 'STDERR.'
   call xmsg updl.0 'records after sort'
 end
 if $msglvl>1 then say 'Deleting..'
 do n=1 to updl.0
   updl.n=strip(insert(' ',updl.n,6))
   if $msglvl>1 then say '    'updl.n
 end
 say ''

 /* delete records from dsndb - save keys         */
 if $typrun='UPDATE' then do
   call xmsg 'Delete dsn db records'
   cc=RxVsamBa('del dd(dsndb) textstem(updl.) POS(14) ENDPOS('14+6+1+44')',
               'list(dkey.)')
   call xmsg 'Delete dsndb rc:' cc+0', records deleted:' rxvbdele+0,
         ||  ', records remaining in db:' rxvbread-rxvbdele
   if $msglvl>0 then do n=1 to dkey.0
     say '   'dkey.n
   end
 end
 else call xmsg 'Delete dsn records bypassed due to typrun' $typrun
 say ''

 /* delete records from mbrdb - sequential access */
 if $typrun='UPDATE' then do
   call xmsg 'Delete mbr db records'
   do n=1 to dkey.0
     dkey.n=left(dkey.n,12)
     if $msglvl>1 then say dkey.n
   end
   cc=RxVsamBa('del dd(mbrdb) textstem(dkey.) POS(9) ENDPOS(20)' ,
               'list(mdel.)')
   call xmsg 'Delete mbrdb rc:' cc+0', records deleted:' rxvbdele+0 ,
         ||  ', records remaining in db:' rxvbread-rxvbdele
   if $msglvl>1 then do n=1 to mdel.0
     say '   'mdel.n
   end
 end
 else call xmsg 'Delete mbr records bypassed due to typrun' $typrun
 say ''

 /* process ended */
 call xmsg 'Delete ended, elapsed time=' time('e') - $et
 exit $rc

XMsg: if arg(1)<>'' then say time() arg(1);return word(arg(2) 0,1)
