/*                                                                rexx

 WFM - read data from one or more VTOCs
 Output goes to SYSTSPRT
 CNTL Syntax (may span lines):
  VOL( volume-list )

*/

 say 'VTOC extract started at' time()

 /* get filter list */
 ctl = WFMCNTL('dd(disklist)')
 parse var ctl . 'ERR('err')' . 0 . 'VOL('vol')' .
 if err<>'' then exit xmsg('Read cntl error' err,8)
 if vol='' then exit xmsg('Missing VOL parm',8)

 /* run VTOC for each volser */
 vc=0
 do vn=1 to words(vol)
   say "VTOC" word(vol,vn) 'started' time()
   zz=outtrap('lst.')
   "VTOC" word(vol,vn),
   "LIMIT(DSO EQ PO) OR1(DSO EQ PE)",
   "NOHEADING PRINT(NEW (DSNAME VOLUME DSO RFM LRECL))"
   zz=outtrap('off')
   if rc<>0 then exit xmsg('VTOC' word(vol,vn) 'rc' rc,8)
   "execio" lst.0 "diskw vtocextr (stem lst.)"
   vc=vc+1
 end
 "execio 0 diskw vtocextr (finis)"

 if vol='ALL' then vc='ALL'
 Exit xmsg(vc 'VTOCs processed by' time())

XMsg: if arg(1)<>'' then say arg(1);return word(arg(2) 0,1)
