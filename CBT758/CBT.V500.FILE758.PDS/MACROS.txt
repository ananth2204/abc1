./ ADD NAME=$$CREATE 0101-07147-07147-1005-00002-00002-00000-DOTCWS
   To create this macro library, please run the $PDSLOAD job in
   File 758.  (S.Golob)
./ ADD NAME=$DOC     0101-07127-07127-1035-00004-00003-00000-DOTCWS
$DOC     This member
JRETURND Standard exit logic
JSAVED   Standard entry logic
REGISTER Register equates
./ ADD NAME=JRETURND 0100-07117-07117-1518-00067-00067-00000-DOTCWS
         MACRO
&LABEL   JRETURND  &A,&B,&RC=,&R=
         GBLC  &JSYSTEM
*
*        MACRO JRETURND VERSION 03
*        HILL FARMS REGIONAL COMPUTING CENTER
*        STANDARD LINKAGE CONVENTION/RESTORE MACRO
*
         AIF   ('&A' EQ 'DOS').DOS
         AIF   ('&A' EQ 'STATIC').DOS
         AIF   ('&A' EQ 'EXPRESS').DOS
         AIF   ('&A' EQ 'OS').OS
         AIF   ('&A' EQ 'DYNAMIC').OS
         AIF   ('&A' EQ 'REENTER').OS
         AIF   ('&A' NE '').ERROR
.* PASS THRU IF PARAMETER 1 IS OMITTED
         AIF   ('&JSYSTEM' EQ 'OS').OS
         AIF   ('&JSYSTEM' EQ 'DOS').DOS
.*
.* OPTION 1
.DOS     ANOP
         AIF   ('&R' EQ '').DOS1
         AIF   ('&R' NE 'FP').ERROR
         LD    0,80(13)                 RESTORE FLT PT REG 0
         LD    2,88(13)                 RESTORE FLT PT REG 2
         LD    4,96(13)                 RESTORE FLT PT REG 4
         LD    6,104(13)                RESTORE FLT PT REG 6
.DOS1    ANOP
&LABEL   L     13,4(0,13)               GET A(CALLERS SAVE AREA)
         AGO   .COMMON
.* OPTION 2
.OS      ANOP
&LABEL   L     13,4(0,13)               GET A(CALLERS SAVE AREA)
         STM   15,1,16(13)              PRESERVE REGISTERS 15,0,1
*                                       WHERE THEY WERE SAVED AT ENTRY
         L     1,8(0,13)                SET UP TO FREE SAVE AREA
         AIF   ('&R' EQ '').OS1
         AIF   ('&R' NE 'FP').ERROR
         LD    0,72(1)                  RESTORE FLT PT REG 0
         LD    2,80(1)                  RESTORE FLT PT REG 2
         LD    4,88(1)                  RESTORE FLT PT REG 4
         LD    6,96(1)                  RESTORE FLT PT REG 6
         FREEMAIN  R,LV=104,A=(1)       FREE SAVE AREA FOR THIS MODULE
         LM    15,1,16(13)              RESTORE 15,0,1
         AGO   .COMMON
.OS1     ANOP
         FREEMAIN R,LV=72,A=(1)       FREE SAVE AREA FOR THIS MODULE
         LM    15,1,16(13)              RESTORE 15,0,1
.COMMON  ANOP
         L     14,12(0,13)              RESTORE RETURN ADDRESS
         AIF   ('&RC' EQ '').NORC
         AIF   ('&B' EQ '').NOB1
         RETURN &B,RC=&RC
         AGO   .RETNT
.NOB1    ANOP
         RETURN (2,12),RC=&RC
         AGO   .RETNT
.NORC    AIF   ('&B' EQ '').NOB2
         RETURN &B
         AGO   .RETNT
.NOB2    ANOP
         RETURN (2,12)
.RETNT   ANOP
         MEXIT
.ERROR   MNOTE 8,'PARAMETER ERROR, GENERATION TERMINATED'
         MEXIT
         MEND
./ ADD NAME=JSAVED   0100-07117-07117-1518-00089-00089-00000-DOTCWS
         MACRO
&LABEL   JSAVED &A,&R=
         GBLC  &JSYSTEM
         GBLC  &PANSTAT
*
*        MACRO JSAVED  VERSION 04A
*        HILL FARMS REGIONAL COMPUTING CENTER
.*       INFO TECH (STATE OF WISCONSIN) - TSSR 01578
*        STANDARD LINKAGE CONVENTION/SAVE MACRO
*
&LABEL   SAVE  (14,12),,*
         AIF   ('&A' EQ 'DOS').DOS
         AIF   ('&A' EQ 'STATIC').DOS
         AIF   ('&A' EQ 'EXPRESS').DOS
         AIF   ('&A' EQ 'OS').OS
         AIF   ('&A' EQ 'DYNAMIC').OS
         AIF   ('&A' EQ 'REENTER').OS
         AIF   ('&A' NE '').ERROR
.* PASS THRU IF PARAMETER 1 IS OMITTED
         AIF   ('&JSYSTEM' EQ 'OS').OS
         AIF   ('&JSYSTEM' EQ 'DOS').DOS
.DOS     ANOP
*
*        STATIC SAVE AREA PROVIDED IN THIS PROGRAM
*
         AIF   ('&R' EQ '').DOS1
         AIF   ('&R' NE 'FP').ERROR
         CNOP  2,8                      PROVIDE ALIGNMENT
         BASR  1,0                      PROVIDE ADDRESSING FOR S.A.
         BAS   1,166(0,1)               BRANCH AROUND SAVE AREA
         DC    27F'0'                   SAVE AREA
         DC    CL8'&SYSDATE'            DATE OF ASSEMBLY
         DC    CL3' ',CL5'&SYSTIME'     TIME OF ASSEMBLY
         DC    CL38'&PANSTAT '
         STD   0,80(1)                  SAVE FLT PT REG 0
         STD   2,88(1)                  SAVE FLT PT REG 2
         STD   4,96(1)                  SAVE FLT PT REG 4
         STD   6,104(1)                 SAVE FLT PT REG 6
         AGO   .COM
.DOS1    ANOP
         CNOP  2,4                      PROVIDE ALIGNMENT
         BASR  1,0                      PROVIDE ADDRESSING FOR S.A.
         BAS   1,130(0,1)               BRANCH AROUND SAVE AREA
         DC    18F'0'                   SAVE AREA
         DC    CL8'&SYSDATE'            DATE OF ASSEMBLY
         DC    CL3' ',CL5'&SYSTIME'     TIME OF ASSEMBLY
         DC    CL38'&PANSTAT '
         AGO   .COM
.OS      ANOP
*
*        DYNAMIC SAVE AREA FOR RE-ENTRANT CODE
*
         AIF   ('&R' EQ '').OS1
         AIF   ('&R' NE 'FP').ERROR
         BASR  14,0                     PROVIDE ADDRESSING FOR CODE
         USING *,14
         B     *+L'*+8+8+38             FOR TSSR 01578 (11-26-96) (JTS)
         DC    CL8'&SYSDATE'            DATE OF ASSEMBLY
         DC    CL3' ',CL5'&SYSTIME'     TIME OF ASSEMBLY
         DC    CL38'&PANSTAT '          PANVALET MODULE/VERSION INFO
         GETMAIN R,LV=104               ACQUIRE STORAGE FOR SAVE AREA
         STD   0,72(1)                  SAVE FLT PT REG 0
         STD   2,80(1)                  SAVE FLT PT REG 2
         STD   4,88(1)                  SAVE FLT PT REG 4
         STD   6,96(1)                  SAVE FLT PT REG 6
         DROP  14
         AGO   .COM
.OS1     ANOP
         BASR  14,0                     PROVIDE ADDRESSING FOR CODE
         USING *,14
         B     *+L'*+8+8+38             FOR TSSR 01578 (11-26-96) (JTS)
         DC    CL8'&SYSDATE'            DATE OF ASSEMBLY
         DC    CL3' ',CL5'&SYSTIME'     TIME OF ASSEMBLY
         DC    CL38'&PANSTAT '          PANVALET MODULE/VERSION INFO
         GETMAIN R,LV=72                ACQUIRE STORAGE FOR SAVE AREA
         DROP  14
.COM     ANOP
         SR    14,14                    CLEAR R14 TO NULL FORWARD CHAIN
         STM   13,14,4(1)               STORE BACK CHAIN, 0 FWD CHAIN
         ST    1,8(0,13)                SET PREVIOUS S.A. FORWARD CHAIN
         LM    13,1,8(13)               RESTORE ENTRY REGISTERS
*
*        REGISTER 13 POINTS TO THE SAVE AREA FOR THIS PROGRAM
*        IT MUST REMAIN THIS WAY FOR ANY CALLS AND FOR RETURN
*
         MEXIT
.ERROR   MNOTE 8,'PARAMETER ERROR, GENERATION TERMINATED'
         MEXIT
         MEND
./ ADD NAME=REGISTER 0100-07117-07117-1518-00012-00012-00000-DOTCWS
         MACRO
         REGISTER &LABEL=R,&REG1=0,&REG2=15,&NUM1=0
         LCLA  &A,&B
&A       SETA  &REG1
&B       SETA  &NUM1
.COUNTOK ANOP
&LABEL.&A EQU  &B
         AIF   (&A EQ &REG2).END
&A       SETA  &A+1
&B       SETA  &B+1
         AGO   .COUNTOK
.END     MEND
