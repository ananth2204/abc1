000100$SET LITLINK
000200 IDENTIFICATION DIVISION.
000300 PROGRAM-ID. Y2KSCAN.
000400 AUTHOR. JAY MOSELEY.
000500 DATE-WRITTEN. AUGUST, 1998.
000600 DATE-COMPILED.
000700
000800* ************************************************************* *
000900* THE PURPOSE OF THIS PROGRAM IS TO SCAN THE SOURCE CODE FILE   *
001000* FOR A COMPUTER PROGRAM AND, USING A GLOSSARY FILE SUPPLIED AT *
001100* RUN TIME, IDENTIFY LINES IN THE SOURCE PROGRAM THAT MAY BE    *
001200* SUSPECTS FOR CONTAINING DATE FIELD REFERENCES.                *
001300*                                                               *
001400* THIS PROGRAM WAS BASED UPON THE CONCEPT OF A PROGRAM WRITTEN  *
001500* BY THE NATIONAL INSTITUTE OF STANDARDS AND TECHNOLOGY (NIST)  *
001600* AND PLACED INTO THE PUBLIC DOMAIN IN OCTOBER, 1996.           *
001700* ************************************************************* *
001800/
001900 ENVIRONMENT DIVISION.
002000 INPUT-OUTPUT SECTION.
002100 FILE-CONTROL.
002200     SELECT GLOSSARY-FILE ASSIGN TO UT-S-SYSIN
002300         ORGANIZATION IS LINE SEQUENTIAL.
002400
002500     SELECT SOURCE-CODE-FILE ASSIGN TO UT-S-SOURCE
002600         ORGANIZATION IS LINE SEQUENTIAL.
002700
002800     SELECT PRINT-FILE ASSIGN TO UT-S-SYSPRINT
002900         ORGANIZATION IS LINE SEQUENTIAL.
003000 DATA DIVISION.
003100
003200 FILE SECTION.
003300 FD  GLOSSARY-FILE
003400     RECORD CONTAINS 80 CHARACTERS
003500     DATA RECORD IS GLOSSARY-RECD.
003600* ************************************************************* *
003700* GLOSSARY RECORD IS FREE-FORM INPUT.  EACH RECORD DEFINES AN   *
003800* ENTRY IN THE GLOSSARY TABLE.  THE FIRST WORD ON THE RECORD    *
003900* BECOMES A "KEY" TO SEARCH FOR POSSIBLE MATCHS IN THE SOURCE   *
004000* CODE FILE.  BY PLACING AN EXCLAMATION POINT (!) AFTER THIS    *
004100* KEY WORD, ADDITIONAL WORDS MAY BE CODED TO SPECIFY WORDS THAT *
004200* CONTAIN THE KEY WORD AS A SUBSTRING AND ARE TO BE EXCLUDED    *
004300* FROM MATCHING SOURCE CODE ELEMENTS.  FOR EXAMPLE:             *
004400*                                                               *
004500*     DOW ! DOWN WINDOW                                         *
004600*                                                               *
004700* SPECIFIES THAT THE STRING "DOW" WHEN FOUND IN THE SOURCE CODE *
004800* SHOULD BE REPORTED UNLESS IT IS A PART OF THE LARGER STRINGS  *
004900* "DOWN" OR "WINDOW".                                           *
005000*                                                               *
005100* EACH WORD SPECIFIED MUST BE DELIMITED BY SPACES AND NO WORD   *
005200* MAY BE LARGER THAN 20 CHARACTERS.                             *
005300* ************************************************************* *
005400 01  GLOSSARY-RECD.
005500     02  GR-COMMENT-FLAG         PIC X(1).
005600     02  FILLER                  PIC X(79).
005700
005800 FD  SOURCE-CODE-FILE
005900     RECORD CONTAINS 80 CHARACTERS
006000     DATA RECORD IS SOURCE-CODE-RECD.
006100 01  SOURCE-CODE-RECD            PIC X(80).
006200
006300 FD  PRINT-FILE
006400     RECORD CONTAINS 132 CHARACTERS
006500     DATA RECORD IS PRINT-RECD.
006600 01  PRINT-RECD                 PIC X(132).
006700/
006800 WORKING-STORAGE SECTION.
006900
007000* ************************************************************* *
007100* FOLLOWING ARE GLOBAL CONTROL SWITCHES AND MISCELLANEOUS DATA. *
007200* ************************************************************* *
007300
007400 01  PROGRAM-CONTROL-FIELDS.
007500     02  INPUT-RECORD-COUNT      PIC S9(9) COMP.
007600     02  END-OF-DATA-SWITCH      PIC X(1).
007700         88  NOT-END-OF-DATA     VALUE 'N'.
007800         88  END-OF-DATA         VALUE 'Y'.
007900     02  SYSTEM-DATE-AND-TIME    PIC X(21).
008000     02  FILLER                  REDEFINES SYSTEM-DATE-AND-TIME.
008100         03  SYSTEM-DATE-YYYY    PIC 9(4).
008200         03  SYSTEM-DATE-MMDD    PIC 9(4).
008300         03  SYSTEM-TIME-HHMM    PIC 9(4).
008400         03  FILLER              REDEFINES SYSTEM-TIME-HHMM.
008500             04  SYSTEM-TIME-HH  PIC 9(2).
008600             04  FILLER          PIC X(2).
008700         03  FILLER              PIC X(9).
008800     02  PAGE-COUNT              PIC 999 VALUE ZERO.
008900     02  LINE-COUNT              PIC 99  VALUE 99.
009000     02  PAGE-SIZE               PIC 99  VALUE 42.
009100
009200* ************************************************************* *
009300* FOLLOWING IS THE DEFINITION OF THE GLOSSARY TABLE.            *
009400* ************************************************************* *
009500
009600 01  GLOSSARY-TABLE.
009700     02  GT-MAX                  PIC 9(8).
009800     02  GT-LOAD-COUNT           PIC S9(9) COMP VALUE +0.
009900     02  GT-EXCEPT-MAX           PIC 9(4).
010000     02  GT-DATA.
010100         03  GT-ENTRY            OCCURS 1000 TIMES
010200                                 INDEXED BY GT-INDEX
010300                                 ASCENDING KEY IS GT-WORD.
010400             04  GT-WORD         PIC X(20).
010500             04  GT-WORD-SIZE    PIC S9(4) COMP.
010600             04  GT-EXCEPT-COUNT PIC S9(4) COMP.
010700             04  GT-EXCEPTIONS.
010800                 05  GT-EXCEPT   OCCURS 5 TIMES
010900                                 INDEXED BY GT-EXCEPT-INDEX.
011000                     06  GT-EXCEPT-WORD
011100                                 PIC X(20).
011200                     06  GT-EXCEPT-OFFSET
011300                                 PIC S9(4) COMP.
011400                     06  GT-EXCEPT-SIZE
011500                                 PIC S9(4) COMP.
011600
011700* ************************************************************* *
011800* WORK AREA USED TO PROCESS GLOSSARY TABLE ENTRIES.             *
011900* ************************************************************* *
012000 01  GLOSSARY-TABLE-WORK-AREA.
012100     02  GTWA-INDEX-1            PIC S9(4) COMP VALUE +0.
012200     02  GTWA-INDEX-2            PIC S9(4) COMP VALUE +0.
012300     02  GTWA-CHARACTER          PIC X(1).
012400     02  EXCLUDE-SWITCH          PIC X(1).
012500         88  EXCLUDE-INDICATOR-NOTFOUND     VALUE 'N'.
012600         88  EXCLUDE-INDICATOR-FOUND        VALUE 'Y'.
012700     02  GTWA-WORD-MAX           PIC 9(4).
012800     02  GTWA-STATUS             PIC 9(1) VALUE 0.
012900         88  LOAD-STATUS-CONTINUE           VALUE 0, 1, 2.
013000         88  LOAD-STATUS-INITIAL            VALUE 0.
013100         88  LOAD-STATUS-INPROCESS          VALUE 1.
013200         88  LOAD-STATUS-SKIP-WORD          VALUE 2.
013300         88  LOAD-STATUS-SKIP-RECORD        VALUE 8.
013400         88  LOAD-STATUS-ABORT              VALUE 9.
013500     02  GTWA-WORD-LENGTH        PIC S9(4) COMP VALUE +0.
013600     02  GTWA-WORD               PIC X(20).
013700     02  GTWA-OFFSET             PIC S9(4) COMP VALUE +0.
013800     02  GTWA-EXCEPT-WORD        PIC X(20).
013900     02  GTWA-SUB-WORD           PIC X(20).
014000
014100* ************************************************************* *
014200* WORK AREA USED TO PROCESS SOURCE CODE RECORDS.                *
014300* ************************************************************* *
014400 01  SOURCE-CODE-WORK-AREA.
014500     02  MATCHED-RECORD-COUNT    PIC S9(9) COMP VALUE +0.
014600     02  SCWA-SCAN-ZONE-BEGIN    PIC S9(4) COMP VALUE +1.
014700     02  SCWA-SCAN-ZONE-END      PIC S9(4) COMP VALUE +80.
014800     02  SCWA-INDEX-1            PIC S9(4) COMP VALUE +0.
014900     02  SCWA-INDEX-2            PIC S9(4) COMP VALUE +0.
015000     02  SCWA-WORD               PIC X(20).
015100     02  SCWA-SEARCH-STATUS      PIC 9.
015200         88  SEARCH-INITIAL                 VALUE 0.
015300         88  SEARCH-KEYWORD-FOUND           VALUE 1.
015400         88  SEARCH-EXCLUDE-FOUND           VALUE 2.
015500
015600* ************************************************************* *
015700* FOLLOWING ARE THE REPORT HEADING AND DETAIL DESCRIPTIONS.     *
015800* ************************************************************* *
015900
016000 01  HEADING-1.
016100     02  FILLER      PIC X(6) VALUE 'DATE: '.
016200     02  H1-DATE-MD  PIC Z9/99/.
016300     02  H1-DATE-Y   PIC 9999.
016400     02  FILLER      PIC X(27) VALUE SPACES.
016500     02  FILLER      PIC X(43) VALUE
016600         'SCANNING SOURCE CODE FOR YEAR 2000 SUSPECTS'.
016700     02  FILLER      PIC X(27) VALUE SPACES.
016800     02  FILLER      PIC X(16) VALUE 'PROGRAM: Y2KSCAN'.
016900
017000 01  HEADING-2.
017100     02  FILLER      PIC X(6) VALUE 'TIME: '.
017200     02  H2-TIME     PIC Z9/99B.
017300     02  H2-TIME-M   PIC X(2).
017400     02  FILLER      PIC X(33) VALUE SPACES.
017500     02  H2-TITLE    PIC X(33) VALUE
017600         '   PHASE 1: GLOSSARY TABLE LOAD  '.
017700     02  FILLER      PIC X(40) VALUE SPACES.
017800     02  FILLER      PIC X(9) VALUE 'PAGE:   '.
017900     02  H2-PAGE     PIC ZZ9.
018000
018100 01  HEADING-3       PIC X(132) VALUE SPACES.
018200
018300 01  HEADING-3-PHASE-2.
018400     02  FILLER      PIC X(11) VALUE ' RECORD NO '.
018500     02  FILLER      PIC X(05) VALUE SPACES.
018600     02  FILLER      PIC X(80) VALUE 'LINE FROM SOURCE CODE FILE'.
018700     02  FILLER      PIC X(05) VALUE SPACES.
018800     02  FILLER      PIC X(31) VALUE 'GLOSSARY WORD FOUND'.
018900
019000 01  DETAIL-LINE                 PIC X(132) VALUE SPACES.
019100
019200 01  DETAIL-LINE-PHASE-1         REDEFINES DETAIL-LINE.
019300     02  DLP1-GLOSSARY-RECD      PIC X(80).
019400     02  DLP1-MESSAGE            PIC X(52).
019500
019600 01  SUMMARY-LINE-PHASE-1        REDEFINES DETAIL-LINE.
019700     02  SLP1-COUNT              PIC ZZ,ZZ9B.
019800     02  SLP1-MESSAGE            PIC X(125).
019900
020000 01  DETAIL-LINE-PHASE-2         REDEFINES DETAIL-LINE.
020100     02  DLP2-RECORD-NUMBER      PIC ZZZ,ZZZ,ZZ9.
020200     02  FILLER                  PIC X(05).
020300     02  DLP2-SOURCE-CODE-RECD   PIC X(80).
020400     02  FILLER                  PIC X(05).
020500     02  DLP2-GLOSSARY-WORD      PIC X(31).
020600
020700 01  SUMMARY-LINE-PHASE-2        REDEFINES DETAIL-LINE.
020800     02  SLP2-COUNT              PIC ZZZ,ZZZ,ZZ9B.
020900     02  SLP2-MESSAGE            PIC X(120).
021000
021100/
021200 LINKAGE SECTION.
021300
021400* ************************************************************* *
021500* AN OPTIONAL PROGRAM PARAMETER MAY BE SPECIFIED TO LIMIT THE   *
021600* AREA OF THE SOURCE CODE RECORD THAT IS SCANNED.               *
021700* ************************************************************* *
021800
021900 01  PROGRAM-PARAMETERS.
022000     02  PP-LENGTH               PIC S9(4)  COMP.
022100     02  PP-SCAN-BEGIN           PIC 9(4).
022200     02  PP-SCAN-END             PIC 9(4).
022300
022400/
022500 PROCEDURE DIVISION USING PROGRAM-PARAMETERS.
022600
022700 0000-MAIN SECTION.
022800
022900* ************************************************************* *
023000* INITIALIZE HEADINGS AND CONSTANTS THAT DO NOT CHANGE AGAIN    *
023100* THROUGHOUT THE PROGRAM'S EXECUTION.                           *
023200* ************************************************************* *
023300
023400 0050-SETUP.
023500
023600     MOVE FUNCTION CURRENT-DATE
023700       TO SYSTEM-DATE-AND-TIME.
023800     MOVE SYSTEM-DATE-MMDD TO H1-DATE-MD.
023900     MOVE SYSTEM-DATE-YYYY TO H1-DATE-Y.
024000     IF SYSTEM-TIME-HHMM = 1200
024100         MOVE 'M ' TO H2-TIME-M
024200     ELSE
024300         IF SYSTEM-TIME-HH < 12
024400             MOVE 'AM' TO H2-TIME-M
024500         ELSE
024600             MOVE 'PM' TO H2-TIME-M
024700             IF SYSTEM-TIME-HH > 13
024800                 SUBTRACT 12 FROM SYSTEM-TIME-HH
024900             END-IF
025000         END-IF
025100     END-IF.
025200     MOVE SYSTEM-TIME-HHMM TO H2-TIME.
025300     INSPECT H2-TIME REPLACING ALL '/' BY ':'.
025400
025500     MOVE FUNCTION LENGTH(SOURCE-CODE-RECD)
025600       TO SCWA-SCAN-ZONE-END.
025700
025800     IF PP-LENGTH > +0
025900         IF PP-SCAN-BEGIN IS NUMERIC
026000        AND PP-SCAN-END IS NUMERIC
026100             IF PP-SCAN-END > PP-SCAN-BEGIN
026200            AND PP-SCAN-BEGIN > 0
026300            AND (PP-SCAN-END NOT > SCWA-SCAN-ZONE-END)
026400                 MOVE PP-SCAN-BEGIN TO SCWA-SCAN-ZONE-BEGIN
026500                 MOVE PP-SCAN-END TO SCWA-SCAN-ZONE-END
026600             END-IF
026700         END-IF
026800     END-IF.
026900
027000* ************************************************************* *
027100* PROGRAM EXECUTION IS DIRECTED BY CALLING THE HIGHEST LEVEL    *
027200* PARAGRAPH REQUIRED TO COMPLETE THE PROGRAMS DESIGN FUNCTION.  *
027300* ************************************************************* *
027400
027500 0100-CONTROL.
027600
027700     OPEN OUTPUT PRINT-FILE.
027800     PERFORM 1000-LOAD-GLOSSARY.
027900     PERFORM 2000-PROCESS-SOURCE-FILE.
028000     CLOSE PRINT-FILE.
028100
028200     STOP RUN.
028300* - - - - - - - - - - - - - - - - PROGRAM EXIT POINT
028400
028500* ************************************************************* *
028600* THIS IS THE COMMON ROUTINE FOR PRINTING DETAIL LINES ON THE   *
028700* REPORT AND DETERMINING WHEN HEADINGS NEED TO BE PRINTED.      *
028800* ************************************************************* *
028900
029000 0200-PRINT-DETAIL-LINE.
029100     IF LINE-COUNT GREATER THAN PAGE-SIZE
029200         IF LINE-COUNT LESS THAN 99
029300             MOVE SPACES TO PRINT-RECD
029400             WRITE PRINT-RECD BEFORE ADVANCING PAGE
029500         END-IF
029600         ADD 1 TO PAGE-COUNT
029700         MOVE PAGE-COUNT TO H2-PAGE
029800         WRITE PRINT-RECD FROM HEADING-1 BEFORE ADVANCING 1
029900         WRITE PRINT-RECD FROM HEADING-2 BEFORE ADVANCING 2
030000         WRITE PRINT-RECD FROM HEADING-3 BEFORE ADVANCING 1
030100         MOVE 4 TO LINE-COUNT
030200     END-IF.
030300     WRITE PRINT-RECD FROM DETAIL-LINE BEFORE ADVANCING 1.
030400     ADD 1 TO LINE-COUNT.
030500     MOVE SPACES TO DETAIL-LINE.
030600* - - - - - - - - - - - - - - - - PERFORM EXIT POINT
030700
030800* ************************************************************* *
030900* THE GLOSSARY FILE IS LOADED INTO THE GLOSSARY TABLE.          *
031000* ************************************************************* *
031100 1000-LOAD-GLOSSARY.
031200
031300     OPEN INPUT GLOSSARY-FILE.
031400     SET NOT-END-OF-DATA TO TRUE.
031500     MOVE +0 TO INPUT-RECORD-COUNT.
031600     INITIALIZE GT-DATA.
031700     COMPUTE GT-MAX = FUNCTION LENGTH(GT-DATA) /
031800                      FUNCTION LENGTH(GT-ENTRY).
031900     MOVE FUNCTION LENGTH(GTWA-WORD) TO GTWA-WORD-MAX.
032000     COMPUTE GT-EXCEPT-MAX = FUNCTION LENGTH(GT-EXCEPTIONS) /
032100                             FUNCTION LENGTH(GT-EXCEPT).
032200
032300     PERFORM 1100-READ-GLOSSARY-FILE.
032400     PERFORM 1200-PROCESS-GLOSSARY-RECORD
032500       UNTIL END-OF-DATA
032600          OR LOAD-STATUS-ABORT.
032700
032800     PERFORM 0200-PRINT-DETAIL-LINE.
032900     MOVE INPUT-RECORD-COUNT TO SLP1-COUNT.
033000     MOVE 'RECORDS READ FROM GLOSSARY FILE'
033100       TO SLP1-MESSAGE.
033200     PERFORM 0200-PRINT-DETAIL-LINE.
033300     MOVE GT-LOAD-COUNT TO SLP1-COUNT.
033400     MOVE 'GLOSSARY ENTRIES LOADED'
033500       TO SLP1-MESSAGE.
033600     PERFORM 0200-PRINT-DETAIL-LINE.
033700
033800     IF RETURN-CODE = +8
033900         PERFORM 0200-PRINT-DETAIL-LINE
034000         MOVE 'SERIOUS ERRORS ENCOUNTERED DURING LOAD'
034100           TO DETAIL-LINE
034200         PERFORM 0200-PRINT-DETAIL-LINE.
034300
034400     IF RETURN-CODE = +4
034500         PERFORM 0200-PRINT-DETAIL-LINE
034600         MOVE 'GLOSSARY FILE SIZE EXCEEDED PROGRAM CAPACITY'
034700           TO DETAIL-LINE
034800         PERFORM 0200-PRINT-DETAIL-LINE.
034900
035000     CLOSE GLOSSARY-FILE.
035100* - - - - - - - - - - - - - - - - PERFORM EXIT POINT
035200
035300* ************************************************************* *
035400* A RECORD IS READ FROM THE GLOSSARY FILE FOR PROCESSING.       *
035500* ************************************************************* *
035600
035700 1100-READ-GLOSSARY-FILE.
035800
035900     READ GLOSSARY-FILE
036000         AT END
036100             MOVE 'Y' TO END-OF-DATA-SWITCH
036200         NOT AT END
036300             ADD +1 TO INPUT-RECORD-COUNT
036400     END-READ.
036500* - - - - - - - - - - - - - - - - PERFORM EXIT POINT
036600
036700* ************************************************************* *
036800* AN INDIVIDUAL RECORD IS PROCESSED FROM THE GLOSSARY FILE.     *
036900* CHARACTERS ARE ACCUMULATED TO FORM A WORD AND WHEN A SPACE    *
037000* IS ENCOUNTERED, THE WORD IS PROCESSED.                        *
037100* ************************************************************* *
037200
037300 1200-PROCESS-GLOSSARY-RECORD.
037400
037500     MOVE GLOSSARY-RECD TO DLP1-GLOSSARY-RECD.
037600     PERFORM 0200-PRINT-DETAIL-LINE.
037700
037800     IF GR-COMMENT-FLAG NOT = '*'
037900
038000         SET LOAD-STATUS-INITIAL TO TRUE
038100         MOVE SPACES TO GTWA-WORD
038200         SET EXCLUDE-INDICATOR-NOTFOUND TO TRUE
038300         MOVE +0 TO GTWA-WORD-LENGTH
038400
038500         PERFORM VARYING GTWA-INDEX-1
038600                    FROM +1 BY +1
038700                   UNTIL (GTWA-INDEX-1 > +80)
038800                      OR NOT LOAD-STATUS-CONTINUE
038900             MOVE GLOSSARY-RECD(GTWA-INDEX-1:1) TO GTWA-CHARACTER
039000             EVALUATE TRUE
039100                 WHEN GTWA-CHARACTER = SPACE
039200                     PERFORM 1210-PROCESS-WORD
039300                 WHEN GTWA-CHARACTER = '!'
039400                     SET EXCLUDE-INDICATOR-FOUND TO TRUE
039500                 WHEN OTHER
039600                     ADD +1 TO GTWA-WORD-LENGTH
039700                     IF GTWA-WORD-LENGTH NOT > GTWA-WORD-MAX
039800                         MOVE GLOSSARY-RECD(GTWA-INDEX-1:1)
039900                           TO GTWA-WORD(GTWA-WORD-LENGTH:1)
040000                     END-IF
040100             END-EVALUATE
040200         END-PERFORM
040300
040400         PERFORM 1210-PROCESS-WORD
040500
040600     END-IF.
040700
040800     PERFORM 1100-READ-GLOSSARY-FILE.
040900* - - - - - - - - - - - - - - - - PERFORM EXIT POINT
041000
041100* ************************************************************* *
041200* A WORD FROM THE GLOSSARY RECORD IS PROCESSED. IF IT IS LONGER *
041300* THAN THE MAXIMUM ALLOWABLE WORD, AN ERROR IS REPORTED AND THE *
041400* WORD IS DISCARDED. OTHERWISE, THE APPROPRIATE ROUTINE IS      *
041500* PERFORMED TO FURTHER VALIDATE AND STORE THE WORD AS EITHER A  *
041600* NEW KEY WORD ENTRY IN THE GLOSSARY TABLE OR AS AN EXCEPTION   *
041700* TO THE CURRENT WORD, DEPENDING UPON WHETHER THE EXCEPTION     *
041800* INDICATOR (!) HAS BEEN ENCOUNTERED.                           *
041900* ************************************************************* *
042000
042100 1210-PROCESS-WORD.
042200     IF GTWA-WORD-LENGTH > ZERO
042300         IF GTWA-WORD-LENGTH > GTWA-WORD-MAX
042400             STRING 'MAX WORD SIZE ('
042500                    GTWA-WORD-MAX
042600                    ') EXCEEDED'
042700                 DELIMITED BY SIZE INTO DLP1-MESSAGE
042800             PERFORM 0200-PRINT-DETAIL-LINE
042900             MOVE +8 TO RETURN-CODE
043000         ELSE
043100             MOVE FUNCTION UPPER-CASE(GTWA-WORD)
043200               TO GTWA-WORD
043300             IF EXCLUDE-INDICATOR-FOUND
043400                 PERFORM 1230-PROCESS-EXCLUDE-WORD
043500             ELSE
043600                 PERFORM 1220-PROCESS-KEYWORD
043700             END-IF
043800         END-IF
043900         MOVE SPACES TO GTWA-WORD
044000         MOVE +0 TO GTWA-WORD-LENGTH
044100     END-IF.
044200* - - - - - - - - - - - - - - - - PERFORM EXIT POINT
044300
044400* ************************************************************* *
044500* IF THIS IS THE FIRST WORD ON THE RECORD IT IS LOADED INTO THE *
044600* GLOSSARY TABLE.  IF IT IS THE SECOND WORD ENCOUNTERED WITHOUT *
044700* AN INTERVENING EXCEPTION INDICATOR (!), AN ERROR MESSAGE IS   *
044800* PRINTED AND THE WORD IS DISCARDED.  WHEN THE GLOSSARY TABLE   *
044900* BECOMES FULL, THE LOADING PROCESS IS STOPPED ... ALTHOUGH THE *
045000* PROGRAM WILL CONTINUE TO THE SCANNING PHASE AN ERROR MESSAGE  *
045100* IS PRINTED AND CORRECTIVE ACTION WILL NEED TO BE TAKEN TO     *
045200* OBTAIN COMPLETELY SATISFACTORY RESULTS.                       *
045300* ************************************************************* *
045400
045500 1220-PROCESS-KEYWORD.
045600     IF NOT LOAD-STATUS-INITIAL
045700         MOVE '! MISSING AFTER FIRST KEY WORD'
045800           TO DLP1-MESSAGE
045900         PERFORM 0200-PRINT-DETAIL-LINE
046000         MOVE +8 TO RETURN-CODE
046100         SET LOAD-STATUS-SKIP-RECORD TO TRUE
046200     ELSE
046300         ADD +1 TO GT-LOAD-COUNT
046400         SET GT-INDEX TO GT-LOAD-COUNT
046500         MOVE GTWA-WORD TO GT-WORD (GT-INDEX)
046600         MOVE GTWA-WORD-LENGTH TO GT-WORD-SIZE (GT-INDEX)
046700         SET LOAD-STATUS-INPROCESS TO TRUE
046800     END-IF.
046900
047000     IF GT-LOAD-COUNT = GT-MAX
047100         PERFORM 0200-PRINT-DETAIL-LINE
047200         STRING 'GLOSSARY TABLE SIZE ('
047300                GT-MAX
047400                ') EXCEEDED'
047500             DELIMITED BY SIZE INTO DETAIL-LINE
047600         PERFORM 0200-PRINT-DETAIL-LINE
047700         MOVE +4 TO RETURN-CODE
047800         SET LOAD-STATUS-ABORT TO TRUE
047900     END-IF.
048000
048100* - - - - - - - - - - - - - - - - PERFORM EXIT POINT
048200
048300* ************************************************************* *
048400* IF THIS IS THE FIRST WORD ON THE RECORD, THE RECORD IS DIS-   *
048500* CARDED AS THE EXCLUSION INDICATOR (!) PRECEEDED ANY WORDS.    *
048600*                                                               *
048700* OTHERWISE, WE LOCATE THE OFFSET OF THE KEY WORD IN THIS WORD. *
048800* IF THIS WORD DOES NOT CONTAIN THE KEY WORD, THIS WORD IS      *
048900* DISCARDED AND SCANNING OF THIS GLOSSARY RECORD PROCEEDS.      *
049000*                                                               *
049100* OTHERWISE, IF THERE ARE STILL OPEN EXCLUSION SLOTS, THIS WORD *
049200* IS ADDED TO THE CURRENT GLOSSARY RECORD.  IF THE SLOTS ARE    *
049300* ALL FILLED, PROCESSING OF THE REMAINDER OF THIS GLOSSARY      *
049400* RECORD IS ABANDONED.                                          *
049500* ************************************************************* *
049600
049700 1230-PROCESS-EXCLUDE-WORD.
049800     IF LOAD-STATUS-INITIAL
049900         MOVE '! PRECEEDS ANY KEY WORD ON RECORD'
050000           TO DLP1-MESSAGE
050100         PERFORM 0200-PRINT-DETAIL-LINE
050200         MOVE +8 TO RETURN-CODE
050300         SET LOAD-STATUS-SKIP-RECORD TO TRUE
050400     ELSE
050500         PERFORM 1235-FIND-CONTAINER-OFFSET
050600         IF GTWA-OFFSET = GTWA-WORD-MAX
050700             STRING 'KEYWORD NOT FOUND IN EXCLUSION WORD ('
050800                    GTWA-WORD ')'
050900                 DELIMITED BY SIZE INTO DLP1-MESSAGE
051000             PERFORM 0200-PRINT-DETAIL-LINE
051100             MOVE +8 TO RETURN-CODE
051200         ELSE
051300             ADD +1 TO GT-EXCEPT-COUNT (GT-INDEX)
051400             IF GT-EXCEPT-COUNT (GT-INDEX) > GT-EXCEPT-MAX
051500                 STRING 'WORD EXCLUSION CAPACITY ('
051600                        GT-EXCEPT-MAX
051700                        ') EXCEEDED'
051800                     DELIMITED BY SIZE INTO DLP1-MESSAGE
051900                 PERFORM 0200-PRINT-DETAIL-LINE
052000                 MOVE +8 TO RETURN-CODE
052100                 SET LOAD-STATUS-SKIP-RECORD TO TRUE
052200             ELSE
052300                 SET GT-EXCEPT-INDEX TO GT-EXCEPT-COUNT (GT-INDEX)
052400                 MOVE GTWA-WORD
052500                   TO GT-EXCEPT-WORD (GT-INDEX, GT-EXCEPT-INDEX)
052600                 MOVE GTWA-WORD-LENGTH
052700                   TO GT-EXCEPT-SIZE (GT-INDEX, GT-EXCEPT-INDEX)
052800                 SUBTRACT 1 FROM GTWA-OFFSET
052900                     GIVING GT-EXCEPT-OFFSET (GT-INDEX,
053000                                              GT-EXCEPT-INDEX)
053100             END-IF
053200         END-IF
053300     END-IF.
053400* - - - - - - - - - - - - - - - - PERFORM EXIT POINT
053500
053600* ************************************************************* *
053700* THE EXCLUSION WORD IS SEARCHED TO FIND THE OFFSET AT WHICH    *
053800* THE KEY WORD BEGINS. IF IT IS NOT FOUND, THE OFFSET WILL BE   *
053900* EQUAL TO THE MAXIMUM WORD SIZE.                               *
054000* ************************************************************* *
054100
054200 1235-FIND-CONTAINER-OFFSET.
054300
054400     PERFORM VARYING GTWA-OFFSET FROM +1 BY +1
054500               UNTIL GTWA-OFFSET > GTWA-WORD-MAX
054600         MOVE SPACES TO GTWA-SUB-WORD
054700         MOVE GTWA-WORD(GTWA-OFFSET:GT-WORD-SIZE(GT-INDEX))
054800           TO GTWA-SUB-WORD
054900         IF GTWA-SUB-WORD = GT-WORD (GT-INDEX)
055000             EXIT PERFORM
055100         END-IF
055200     END-PERFORM.
055300* - - - - - - - - - - - - - - - - PERFORM EXIT POINT
055400
055500* ************************************************************* *
055600* THE SOURCE FILE IS PROCESSED LOOKING FOR ANY OCCURANCE OF     *
055700* WORDS IN THE GLOSSARY TABLE IN ANY RECORD OF THE SOURCE FILE. *
055800* ************************************************************* *
055900
056000 2000-PROCESS-SOURCE-FILE.
056100
056200     OPEN INPUT SOURCE-CODE-FILE.
056300     SET NOT-END-OF-DATA TO TRUE.
056400     MOVE +0 TO INPUT-RECORD-COUNT.
056500     MOVE 99 TO LINE-COUNT.
056600     MOVE '    PHASE 2: SOURCE CODE SCAN    ' TO H2-TITLE.
056700     MOVE HEADING-3-PHASE-2 TO HEADING-3.
056800
056900     PERFORM 2100-READ-SOURCE-CODE-FILE.
057000     PERFORM 2200-PROCESS-SOURCE-RECORD
057100       UNTIL END-OF-DATA.
057200
057300     PERFORM 0200-PRINT-DETAIL-LINE.
057400     MOVE INPUT-RECORD-COUNT TO SLP2-COUNT.
057500     MOVE 'RECORDS READ FROM SOURCE CODE FILE'
057600       TO SLP2-MESSAGE.
057700     PERFORM 0200-PRINT-DETAIL-LINE.
057800     MOVE MATCHED-RECORD-COUNT TO SLP2-COUNT.
057900     MOVE 'SOURCE CODE RECORDS CONTAINING GLOSSARY WORDS'
058000       TO SLP2-MESSAGE.
058100     PERFORM 0200-PRINT-DETAIL-LINE.
058200
058300     CLOSE SOURCE-CODE-FILE.
058400* - - - - - - - - - - - - - - - - PERFORM EXIT POINT
058500
058600* ************************************************************* *
058700* A RECORD IS READ FROM THE SOURCE CODE FILE FOR PROCESSING.    *
058800* ************************************************************* *
058900
059000 2100-READ-SOURCE-CODE-FILE.
059100
059200     READ SOURCE-CODE-FILE
059300         AT END
059400             MOVE 'Y' TO END-OF-DATA-SWITCH
059500         NOT AT END
059600             ADD +1 TO INPUT-RECORD-COUNT
059700     END-READ.
059800* - - - - - - - - - - - - - - - - PERFORM EXIT POINT
059900
060000* ************************************************************* *
060100* AN INDIVIDUAL RECORD IS PROCESSED FROM THE SOURCE CODE FILE.  *
060200* THE RECORD IS SCANNED FROM LEFT TO RIGHT CHECKING EACH        *
060300* CHARACTER POSITION AGAINST EACH WORD IN THE GLOSSARY TABLE.   *
060400* IF A WORD IS FOUND, THAT IS NOT ALSO LISTED IN AN EXCEPTION   *
060500* ENTRY, THE SOURCE LINE IS LISTED ON THE SUSPECT REPORT.       *
060600* ************************************************************* *
060700
060800 2200-PROCESS-SOURCE-RECORD.
060900
061000     SET SEARCH-INITIAL TO TRUE.
061100
061200     PERFORM VARYING SCWA-INDEX-1
061300                FROM SCWA-SCAN-ZONE-BEGIN BY +1
061400               UNTIL SCWA-INDEX-1 > SCWA-SCAN-ZONE-END
061500
061600         PERFORM 2210-SEARCH-FOR-KEYWORD
061700         IF SEARCH-KEYWORD-FOUND
061800             EXIT PERFORM
061900         END-IF
062000
062100     END-PERFORM.
062200
062300     IF SEARCH-KEYWORD-FOUND
062400         ADD +1 TO MATCHED-RECORD-COUNT
062500         MOVE INPUT-RECORD-COUNT TO DLP2-RECORD-NUMBER
062600         MOVE SOURCE-CODE-RECD TO DLP2-SOURCE-CODE-RECD
062700         MOVE GT-WORD (GT-INDEX) TO DLP2-GLOSSARY-WORD
062800         PERFORM 0200-PRINT-DETAIL-LINE
062900     END-IF.
063000
063100     PERFORM 2100-READ-SOURCE-CODE-FILE.
063200* - - - - - - - - - - - - - - - - PERFORM EXIT POINT
063300
063400* ************************************************************* *
063500* TEST THE CURRENT SOURCE RECORD POSITION AGAINST EACH KEY WORD *
063600* IN THE GLOSSARY TABLE. IF A MATCH IS FOUND, TEST TO MAKE SURE *
063700* THE MATCH ISN'T REALLY AN EXCLUDED WORD.                      *
063800* ************************************************************* *
063900
064000 2210-SEARCH-FOR-KEYWORD.
064100
064200     PERFORM VARYING GT-INDEX FROM +1 BY +1
064300               UNTIL GT-INDEX > GT-LOAD-COUNT
064400
064500         MOVE SPACES TO SCWA-WORD
064600         MOVE SOURCE-CODE-RECD(SCWA-INDEX-1:GT-WORD-SIZE
064700                                              (GT-INDEX))
064800           TO SCWA-WORD
064900         MOVE FUNCTION UPPER-CASE(SCWA-WORD) TO SCWA-WORD
065000         IF SCWA-WORD = GT-WORD (GT-INDEX)
065100             SET SEARCH-KEYWORD-FOUND TO TRUE
065200             PERFORM 2220-CHECK-FOR-EXCLUSION
065300             IF SEARCH-KEYWORD-FOUND
065400                 EXIT PERFORM
065500             END-IF
065600         END-IF
065700
065800     END-PERFORM.
065900* - - - - - - - - - - - - - - - - PERFORM EXIT POINT
066000
066100* ************************************************************* *
066200* THE KEY WORD HAS MATCHED, SO CHECK TO SEE IF THIS LOCATION    *
066300* IS REALLY A MATCH TO AN EXCLUSION WORD.                       *
066400* ************************************************************* *
066500
066600 2220-CHECK-FOR-EXCLUSION.
066700
066800     PERFORM VARYING GT-EXCEPT-INDEX FROM +1 BY +1
066900               UNTIL GT-EXCEPT-INDEX > GT-EXCEPT-COUNT (GT-INDEX)
067000
067100         MOVE SCWA-INDEX-1 TO SCWA-INDEX-2
067200         SUBTRACT GT-EXCEPT-OFFSET (GT-INDEX, GT-EXCEPT-INDEX)
067300             FROM SCWA-INDEX-2
067400         IF SCWA-INDEX-2 > ZERO
067500             MOVE SPACES TO SCWA-WORD
067600             MOVE SOURCE-CODE-RECD(SCWA-INDEX-2:
067700                                   GT-EXCEPT-SIZE (GT-INDEX,
067800                                                GT-EXCEPT-INDEX))
067900               TO SCWA-WORD
068000             MOVE FUNCTION UPPER-CASE(SCWA-WORD) TO SCWA-WORD
068100             IF SCWA-WORD = GT-EXCEPT-WORD (GT-INDEX,
068200                                            GT-EXCEPT-INDEX)
068300                 SET SEARCH-EXCLUDE-FOUND TO TRUE
068400                 EXIT PERFORM
068500             END-IF
068600         END-IF
068700
068800     END-PERFORM.
068900* - - - - - - - - - - - - - - - - PERFORM EXIT POINT
069000
069100* ************************************************************* *
069200*               EXPANDING THE PROGRAM'S CAPACITY                *
069300* ************************************************************* *
069400* THE PROGRAM HAS SOME SPECIFIC LIMITS THAT MAY BE EXPANDED     *
069500* EASILY ENOUGH BY CHANGING A FEW DATA DEFINITION ELEMENTS AND  *
069600* RE-COMPILING (PROVIDED YOUR EXECUTION REGION IS LARGE ENOUGH  *
069700* TO CONTAIN THE RESULTANT PROGRAM).                            *
069800*                                                               *
069900* TO EXPAND THE MAXIMUM WORD LENGTH FOR BOTH THE KEYWORD AND    *
070000* EXCLUDED WORDS, SEARCH FOR AND CHANGE THE PICTURE CLAUSE OF:  *
070100*                                                               *
070200*     GTWA-WORD, GTWA-EXCEPT-WORD, GTWA-SUB-WORD,               *
070300*     SCWA-WORD, GT-WORD AND GT-EXCEPT-WORD                     *
070400*                                                               *
070500* TO INCREASE THE NUMBER OF GLOSSARY ENTRIES, SEARCH FOR AND    *
070600* CHANGE THE OCCURS CLAUSE FOR:                                 *
070700*                                                               *
070800*     GT-ENTRY                                                  *
070900*                                                               *
071000* TO INCREASE THE NUMBER OF EXCLUDED WORDS FOR EACH GLOSSARY    *
071100* ENTRY, SEARCH FOR AND CHANGE THE OCCURS CLAUSE FOR:           *
071200*                                                               *
071300*     GT-EXCEPT                                                 *
071400*                                                               *
071500* TO INCREASE THE SIZE OF THE SOURCE CODE RECORD, SEARCH FOR    *
071600* AND CHANGE THE RECORD CONTAINS AND PICTURE CLAUSES FOR:       *
071700*                                                               *
071800*     SOURCE-CODE-FILE AND SOURCE-CODE-RECD.                    *
071900*                                                               *
072000* THE PROGRAM CALCULATES SIZE AND TABLE LIMITS FOR THE DEFINED  *
072100* DATA STORAGE, SO IT IS NOT NECESSARY TO CHANGE ANY PROCEDURE  *
072200* DIVISION ENTRIES TO EFFECT THESE EXPANSIONS.                  *
072300* ************************************************************* *
072400
072500* - - - - - - - - - - - - - - - - END OF Y2KSCAN SOURCE CODE
072600 END PROGRAM Y2KSCAN.
