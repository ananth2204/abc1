000100$SET NESTCALL
000200 IDENTIFICATION DIVISION.
000300 PROGRAM-ID. ELAPSED.
000400
000500*    AUTHOR. JAY MOSELEY.
000600*    DATE-WRITTEN. JULY, 2000.
000700
000800* ************************************************************** *
000900* THIS IS A TRANSLATION OF A PROGRAM I WROTE IN APRIL, 1997 (IN  *
001000* C++) TO CALCULATE THE PERIOD OF TIME ELAPSED BETWEEN TWO DATES *
001100* WHICH MAY BOTH BE SPECIFIED ON THE PARAMETER LINE.  OPTIONALLY *
001200* ONLY ONE DATE MAY BE SPECIFIED AND THE SECOND DATE WILL BE     *
001300* TAKE FROM THE SYSTEM DATE                                      *
001400*                                                                *
001500* THE DETAIL OF THE CALCULATION (AND OUTPUT) IS BASED UPON THE   *
001600* INPUT:  IF ONLY THE YEAR IS ENTERED, THE DIFFERENCE WILL BE    *
001700* CALCULATED IN YEARS; IF THE YEAR AND MONTH IS ENTERED, THE     *
001800* DIFFERENCE WILL BE CALCUALTED IN YEARS AND MONTHS; AND IF A    *
001900* FULL DATE IS ENTERED, THE DIFFERENCE WILL BE CALCULATED IN     *
002000* YEARS, MONTHS, AND DAYS.                                       *
002100* ************************************************************** *
002200
002300 ENVIRONMENT DIVISION.
002400
002500 DATA DIVISION.
002600
002700 WORKING-STORAGE SECTION.
002800
002900* ************************************************************** *
003000* THE FOLLOWING VARIABLE BLOCK IS USED TO HOLD THE PARAMETERS    *
003100* THAT APPLY TO THE CURRENT EXECUTION.  THEY ARE DERIVED BY THE  *
003200* EMBEDDED SUBPROGRAM 'GETPARM' WHICH SCANS THE COMMAND LINE     *
003300* ARGUMENT TO EXTRACT THE PARAMETERS.                            *
003400* ************************************************************** *
003500
003600 01  RUN-TIME-PARAMETERS         GLOBAL.
003700
003800     02  SYNTAX-MESSAGE-SWITCH   PIC X(1)    VALUE 'N'.
003900         88  DISPLAY-SYNTAX                  VALUE 'Y'.
004000
004100     02  FROM-MODE               PIC 9(1).
004200     02  FROM-DATE               PIC 9(8)    VALUE ZEROS.
004300     02                          REDEFINES FROM-DATE.
004400         03  FROM-Y              PIC 9(4).
004500         03  FROM-M              PIC 9(2).
004600         03  FROM-D              PIC 9(2).
004700
004800     02  TO-MODE                 PIC 9(1).
004900     02  TO-DATE                 PIC 9(8)    VALUE ZEROS.
005000     02                          REDEFINES TO-DATE.
005100         03  TO-Y                PIC 9(4).
005200         03  TO-M                PIC 9(2).
005300         03  TO-D                PIC 9(2).
005400
005500* ************************************************************** *
005600* THE FOLLOWING VARIABLE BLOCK ARE LOCALLY USED VARIABLES.       *
005700* ************************************************************** *
005800
005900 01  LOCAL-WORK-FIELDS.
006000
006100     02  SWAP-MODE               PIC 9(1).
006200     02  SWAP-DATE               PIC 9(8).
006300
006400     02  OUTPUT-SUB              PIC S9(4)   COMP.
006500     02  OUTPUT-LINE             PIC X(75).
006600
006700     02  NUMBER-SUB              PIC S9(4)   COMP.
006800     02  EDIT-NUMBER             PIC ZZ,ZZ9/.
006900     02  MOVE-NUMBER             PIC X(7).
007000
007100* ************************************************************* *
007200* THE FOLLOWING VARIABLE BLOCK IS THE INTERFACE TO THE LONG     *
007300* AGE CALCULATION SUBROUTINE 'Y2KLAGE'.                         *
007400* ************************************************************* *
007500
007600 01  LAGE-PARAMETERS.
007700     02  LAGEP-DATE1             PIC 9(8).
007800     02                          REDEFINES LAGEP-DATE1.
007900         03  LAGEP-DATE1-M       PIC 9(2).
008000         03  LAGEP-DATE1-D       PIC 9(2).
008100         03  LAGEP-DATE1-Y       PIC 9(4).
008200     02  LAGEP-DATE2             PIC 9(8).
008300     02                          REDEFINES LAGEP-DATE2.
008400         03  LAGEP-DATE2-M       PIC 9(2).
008500         03  LAGEP-DATE2-D       PIC 9(2).
008600         03  LAGEP-DATE2-Y       PIC 9(4).
008700     02  LAGEP-RC                PIC 9(1).
008800     02  LAGEP-DAYS              PIC S9(2).
008900     02  LAGEP-MONTHS            PIC S9(2).
009000     02  LAGEP-YEARS             PIC S9(5).
009100
009200 PROCEDURE DIVISION.
009300
009400* ************************************************************** *
009500* CALL EMBEDDED PROGRAM 'GETPARM' TO EVALUATE COMMAND-LINE       *
009600* PARAMETER(S) AND SETUP THE FIELDS IN RUN-TIME-PARAMETERS.      *
009700* ************************************************************** *
009800
009900     CALL 'GETPARM'.
010000     IF DISPLAY-SYNTAX
010100         DISPLAY 'Usage: ELAPSED '
010200                 'yyyy | mm/yyyy | mm/dd/yyy '
010300                 '[yyyy | mm/yyyy | mm/dd/yyyy].'
010400         DISPLAY 'End of period assumed for omitted '
010500                 'elements.'
010600         DISPLAY '1900 assumed for short (2 digit) '
010700                 'years.'
010800         STOP RUN
010900     END-IF.
011000
011100* ************************************************************** *
011200* ENSURE THAT THE 'FROM' DATE IS CHRONOLOGICALLY BEFORE THE 'TO' *
011300* DATE.                                                          *
011400* ************************************************************** *
011500
011600     IF FROM-DATE GREATER THAN TO-DATE
011700         MOVE FROM-DATE TO SWAP-DATE
011800         MOVE FROM-MODE TO SWAP-MODE
011900         MOVE TO-DATE TO FROM-DATE
012000         MOVE TO-MODE TO FROM-MODE
012100         MOVE SWAP-DATE TO TO-DATE
012200         MOVE SWAP-MODE TO TO-MODE
012300     END-IF.
012400
012500* ************************************************************** *
012600* BEGIN BUILDING THE OUTPUT LINE BY USING THE DATE COMPONENTS    *
012700* EXTRACTED FROM THE COMMAND LINE.                               *
012800* ************************************************************** *
012900
013000     MOVE SPACES TO OUTPUT-LINE.
013100     MOVE +1 TO OUTPUT-SUB.
013200
013300     EVALUATE FROM-MODE
013400         WHEN 1
013500             STRING FROM-Y ' through '
013600                 DELIMITED BY SIZE
013700                 INTO OUTPUT-LINE
013800                 WITH POINTER OUTPUT-SUB
013900             END-STRING
014000         WHEN 3
014100             STRING FROM-M '/' FROM-Y ' through '
014200                 DELIMITED BY SIZE
014300                 INTO OUTPUT-LINE
014400                 WITH POINTER OUTPUT-SUB
014500             END-STRING
014600         WHEN 7
014700             STRING FROM-M '/' FROM-D '/' FROM-Y ' through '
014800                 DELIMITED BY SIZE
014900                 INTO OUTPUT-LINE
015000                 WITH POINTER OUTPUT-SUB
015100             END-STRING
015200     END-EVALUATE.
015300
015400     EVALUATE TO-MODE
015500         WHEN 1
015600             STRING TO-Y ': '
015700                 DELIMITED BY SIZE
015800                 INTO OUTPUT-LINE
015900                 WITH POINTER OUTPUT-SUB
016000             END-STRING
016100         WHEN 3
016200             STRING TO-M '/' TO-Y ': '
016300                 DELIMITED BY SIZE
016400                 INTO OUTPUT-LINE
016500                 WITH POINTER OUTPUT-SUB
016600             END-STRING
016700         WHEN 7
016800             STRING TO-M '/' TO-D '/' TO-Y ': '
016900                 DELIMITED BY SIZE
017000                 INTO OUTPUT-LINE
017100                 WITH POINTER OUTPUT-SUB
017200             END-STRING
017300     END-EVALUATE.
017400
017500* ************************************************************** *
017600* CALL 'Y2KLAGE' TO COMPUTE THE PERIOD BETWEEN THE DATES.        *
017700* ************************************************************** *
017800
017900     MOVE TO-M TO LAGEP-DATE1-M.
018000     MOVE TO-D TO LAGEP-DATE1-D.
018100     MOVE TO-Y TO LAGEP-DATE1-Y.
018200     MOVE FROM-M TO LAGEP-DATE2-M.
018300     MOVE FROM-D TO LAGEP-DATE2-D.
018400     MOVE FROM-Y TO LAGEP-DATE2-Y.
018500     CALL 'Y2KLAGE' USING LAGE-PARAMETERS.
018600
018700* ************************************************************** *
018800* FINISH BUILDING OUTPUT LINE AND DISPLAY IT.                    *
018900* ************************************************************** *
019000
019100     MOVE LAGEP-YEARS TO EDIT-NUMBER.
019200     MOVE SPACES TO MOVE-NUMBER.
019300     MOVE +0 TO NUMBER-SUB.
019400     INSPECT EDIT-NUMBER
019500         TALLYING NUMBER-SUB
019600         FOR LEADING ' '.
019700     ADD +1 TO NUMBER-SUB.
019800     MOVE EDIT-NUMBER(NUMBER-SUB:) TO MOVE-NUMBER.
019900     STRING MOVE-NUMBER ' Years, /'
020000         DELIMITED BY '/'
020100         INTO OUTPUT-LINE
020200         WITH POINTER OUTPUT-SUB
020300     END-STRING.
020400
020500     MOVE LAGEP-MONTHS TO EDIT-NUMBER.
020600     MOVE SPACES TO MOVE-NUMBER.
020700     MOVE +0 TO NUMBER-SUB.
020800     INSPECT EDIT-NUMBER
020900         TALLYING NUMBER-SUB
021000         FOR LEADING ' '.
021100     ADD +1 TO NUMBER-SUB.
021200     MOVE EDIT-NUMBER(NUMBER-SUB:) TO MOVE-NUMBER.
021300     STRING MOVE-NUMBER ' Months, /'
021400         DELIMITED BY '/'
021500         INTO OUTPUT-LINE
021600         WITH POINTER OUTPUT-SUB
021700     END-STRING.
021800
021900     MOVE LAGEP-DAYS TO EDIT-NUMBER.
022000     MOVE SPACES TO MOVE-NUMBER.
022100     MOVE +0 TO NUMBER-SUB.
022200     INSPECT EDIT-NUMBER
022300         TALLYING NUMBER-SUB
022400         FOR LEADING ' '.
022500     ADD +1 TO NUMBER-SUB.
022600     MOVE EDIT-NUMBER(NUMBER-SUB:) TO MOVE-NUMBER.
022700     STRING MOVE-NUMBER ' Days. /'
022800         DELIMITED BY '/'
022900         INTO OUTPUT-LINE
023000         WITH POINTER OUTPUT-SUB
023100     END-STRING.
023200
023300     DISPLAY OUTPUT-LINE.
023400
023500     STOP RUN.
023600
023700 IDENTIFICATION DIVISION.
023800 PROGRAM-ID. GETPARM.
023900
024000*    AUTHOR. JAY MOSELEY.
024100*    DATE-WRITTEN. JULY, 2000.
024200
024300* ************************************************************** *
024400* THIS EMBEDDED SUBPROGRAM PERFORMS ALL OF THE WORK TO ANALYZE   *
024500* THE COMMAND LINE ARGUMENT - BREAKING THE ARGUMENT INTO 'WORDS' *
024600* USING SPACES AS DELIMITERS, ANALYZING THE INDIVIDUAL PARAMETER *
024700* 'WORDS' TO DERIVE THE DISCRETE COMPONENTS OF THE DATES, AND    *
024800* VERIFYING THAT THE DATES SUBMITTED ARE VALID DATES.            *
024900* ************************************************************** *
025000
025100 WORKING-STORAGE SECTION.
025200
025300* ************************************************************** *
025400* THE FOLLOWING VARIABLE BLOCK CONTAINS ALL THE VARIABLES USED   *
025500* TO SCAN THE COMMAND LINE AND EXTRACT THE DATE INFORMATION.     *
025600* ************************************************************** *
025700
025800 01  PARM-EXTRACT-FIELDS.
025900
026000     02  PARM-LINE.
026100         03  PARM-CHAR           OCCURS 80 TIMES
026200                                 INDEXED BY PARM-X
026300                                 PIC X(1).
026400
026500     02  SUB1                    PIC S9(4)   COMP.
026600     02  SUB2                    PIC S9(4)   COMP.
026700
026800     02  WORD-X                  PIC S9(4)   COMP.
026900     02  PARM-WORDS              VALUE SPACES.
027000         03  PARM-WORD           OCCURS 2 TIMES
027100                                 PIC X(10).
027200
027300     02  DATE-PARM               PIC X(10).
027400
027500     02  DATE-PARM-Y             PIC X(4).
027600     02  DATE-PARM-YN            REDEFINES DATE-PARM-Y
027700                                 PIC 9(4).
027800     02  DATE-PARM-M             PIC X(2).
027900     02  DATE-PARM-D             PIC X(2).
028000
028100     02  DATE-ELEMENTS.
028200         03  DATE-ERROR-SWITCH   PIC 9(1).
028300             88  DATE-VALID                  VALUE 0.
028400             88  DATE-INVALID                VALUE 1.
028500         03  DATE-MODE           PIC 9(1).
028600             88  MODE-YEAR                   VALUE 1.
028700             88  MODE-YEAR-MONTH             VALUE 3.
028800             88  MODE-YEAR-MONTH-DAY         VALUE 7.
028900         03  DATE-Y              PIC 9(4).
029000         03  DATE-M              PIC 9(2).
029100         03  DATE-D              PIC 9(2).
029200
029300* ************************************************************** *
029400* THE FOLLOWING VARIABLE BLOCK IS USED BY THE ROUTINES WHICH     *
029500* VERIFY THAT THE SUBMITTED DATES ARE VALID.                     *
029600* ************************************************************** *
029700
029800 01  DATE-VALIDATION.
029900
030000     02  LEAP-YEAR-SWITCH        PIC X(1).
030100         88  IS-LEAP-YEAR                    VALUE 'Y'.
030200         88  IS-NOT-LEAP-YEAR                VALUE 'N'.
030300
030400     02  DAYS-IN-MONTHS          VALUE
030500         '312831303130313130313031'.
030600         03  DAYS-IN-MONTH       OCCURS 12 TIMES
030700                                 PIC 9(2).
030800 PROCEDURE DIVISION.
030900
031000* ************************************************************** *
031100* RETRIEVE THE COMMAND-LINE PARAMETER.                           *
031200* ************************************************************** *
031300
031400     ACCEPT PARM-LINE FROM COMMAND-LINE.
031500
031600* ************************************************************** *
031700* PARSE THE COMMAND LINE INTO DISCRETE WORDS.  IF ANY WORD       *
031800* IS LONGER THAN 10 CHARACTERS, DISPLAY ERROR MESSAGE AND SET    *
031900* SWITCH TO DISPLAY SYNTAX.                                      *
032000* ************************************************************** *
032100
032200     SET PARM-X TO +1.
032300     MOVE +0 TO SUB1, WORD-X.
032400     PERFORM UNTIL PARM-X GREATER THAN +80
032500         IF SUB1 EQUAL +0
032600             IF PARM-CHAR (PARM-X) NOT EQUAL SPACE
032700                 SET SUB1 TO PARM-X
032800             END-IF
032900         ELSE
033000             IF PARM-CHAR (PARM-X) EQUAL SPACE
033100                 SET SUB2 TO PARM-X
033200                 SUBTRACT SUB1 FROM SUB2
033300                 IF SUB2 GREATER THAN +10
033400                     DISPLAY 'Invalid parameter word ('
033500                             PARM-LINE(SUB1:SUB2) ').'
033600                     SET DISPLAY-SYNTAX TO TRUE
033700                 ELSE
033800                     ADD +1 TO WORD-X
033900                     IF WORD-X GREATER THAN +2
034000                         DISPLAY 'Too many parameters.'
034100                         SET DISPLAY-SYNTAX TO TRUE
034200                         SET PARM-X TO +81
034300                     ELSE
034400                         MOVE PARM-LINE(SUB1:SUB2)
034500                           TO PARM-WORD (WORD-X)
034600                     END-IF
034700                 END-IF
034800                 MOVE +0 TO SUB1
034900             END-IF
035000         END-IF
035100         SET PARM-X UP BY +1
035200     END-PERFORM.
035300
035400* ************************************************************** *
035500* IF NO PARAMETERS SUPPLIED, DISPLAY SYNTAX.  IF 1 PARAMETER     *
035600* SUPPLIED, USE SYSTEM-DATE AS TO-DATE AND EXTRACT PARAMETER     *
035700* INTO FROM-DATE.  IF 2 PARAMETERS SUPPLIED, EXTRACT INTO        *
035800* FROM-DATE AND TO-DATE, RESPECTIVELY.                           *
035900* ************************************************************** *
036000
036100     EVALUATE WORD-X
036200         WHEN +0
036300             SET DISPLAY-SYNTAX TO TRUE
036400         WHEN +1
036500             MOVE FUNCTION CURRENT-DATE(1:8) TO TO-DATE
036600             MOVE 7 TO TO-MODE
036700             MOVE PARM-WORD (1) TO DATE-PARM
036800             PERFORM ISOLATE-DATE-ELEMENTS
036900             IF DATE-VALID
037000                 MOVE DATE-MODE TO FROM-MODE
037100                 MOVE DATE-Y TO FROM-Y
037200                 MOVE DATE-M TO FROM-M
037300                 MOVE DATE-D TO FROM-D
037400             ELSE
037500                 SET DISPLAY-SYNTAX TO TRUE
037600                 DISPLAY 'Date entered is not valid!'
037700             END-IF
037800         WHEN +2
037900             MOVE PARM-WORD (1) TO DATE-PARM
038000             PERFORM ISOLATE-DATE-ELEMENTS
038100             IF DATE-VALID
038200                 MOVE DATE-MODE TO FROM-MODE
038300                 MOVE DATE-Y TO FROM-Y
038400                 MOVE DATE-M TO FROM-M
038500                 MOVE DATE-D TO FROM-D
038600             ELSE
038700                 SET DISPLAY-SYNTAX TO TRUE
038800                 DISPLAY 'First date entered is not valid!'
038900             END-IF
039000             MOVE PARM-WORD (2) TO DATE-PARM
039100             PERFORM ISOLATE-DATE-ELEMENTS
039200             IF DATE-VALID
039300                 MOVE DATE-MODE TO TO-MODE
039400                 MOVE DATE-Y TO TO-Y
039500                 MOVE DATE-M TO TO-M
039600                 MOVE DATE-D TO TO-D
039700             ELSE
039800                 SET DISPLAY-SYNTAX TO TRUE
039900                 DISPLAY 'Second date entered is not valid!'
040000             END-IF
040100     END-EVALUATE.
040200
040300     GOBACK.
040400
040500* ************************************************************** *
040600* BASED UPON THE NUMBER OF EMBEDDED SLASHES ('/'), ISOLATE AND   *
040700* VALIDATE THE INDIVIDUAL ELEMENTS OF THE DATE PARAMETER.        *
040800* ************************************************************** *
040900
041000 ISOLATE-DATE-ELEMENTS.
041100
041200     SET DATE-VALID TO TRUE.
041300
041400     MOVE +0 TO SUB1.
041500     INSPECT DATE-PARM
041600         TALLYING SUB1 FOR ALL '/'.
041700
041800* ************************************************************** *
041900* IF NO SLASHES, EXTRACT YEAR; IF 1 SLASH, EXTRACT MONTH AND     *
042000* YEAR; IF 2 SLASHES, EXTRACT MONTH, DAY, AND YEAR.              *
042100* ************************************************************** *
042200
042300     EVALUATE SUB1
042400         WHEN +0
042500             UNSTRING DATE-PARM
042600                 DELIMITED BY ' '
042700                 INTO DATE-PARM-Y
042800             END-UNSTRING
042900             MOVE '88' TO DATE-PARM-M
043000             MOVE '88' TO DATE-PARM-D
043100             SET MODE-YEAR TO TRUE
043200         WHEN +1
043300             MOVE +1 TO SUB2
043400             UNSTRING DATE-PARM
043500                 DELIMITED BY '/'
043600                 INTO DATE-PARM-M
043700                 WITH POINTER SUB2
043800             END-UNSTRING
043900             UNSTRING DATE-PARM
044000                 DELIMITED BY ' '
044100                 INTO DATE-PARM-Y
044200                 WITH POINTER SUB2
044300             END-UNSTRING
044400             MOVE '88' TO DATE-PARM-D
044500             SET MODE-YEAR-MONTH TO TRUE
044600         WHEN +2
044700             MOVE +1 TO SUB2
044800             UNSTRING DATE-PARM
044900                 DELIMITED BY '/'
045000                 INTO DATE-PARM-M
045100                 WITH POINTER SUB2
045200             END-UNSTRING
045300             UNSTRING DATE-PARM
045400                 DELIMITED BY '/'
045500                 INTO DATE-PARM-D
045600                 WITH POINTER SUB2
045700             END-UNSTRING
045800             UNSTRING DATE-PARM
045900                 DELIMITED BY ' '
046000                 INTO DATE-PARM-Y
046100                 WITH POINTER SUB2
046200             END-UNSTRING
046300             SET MODE-YEAR-MONTH-DAY TO TRUE
046400     END-EVALUATE.
046500
046600* ************************************************************** *
046700* IF EXTRACTED YEAR IS NOT NUMERIC AND THE LENGTH IS TWO         *
046800* CHARACTERS, PREPEND '19' TO THE TWO CHARACTERS EXTRACTED.      *
046900* ************************************************************** *
047000
047100     IF DATE-PARM-Y IS NUMERIC
047200         CONTINUE
047300     ELSE
047400         MOVE +0 TO SUB1
047500         INSPECT DATE-PARM-Y
047600             TALLYING SUB1
047700             FOR CHARACTERS
047800             BEFORE INITIAL ' '
047900         IF SUB1 EQUAL +2
048000             MOVE DATE-PARM-Y(1:2) TO DATE-PARM-Y(3:2)
048100             MOVE '19' TO DATE-PARM-Y(1:2)
048200         END-IF
048300     END-IF.
048400
048500* ************************************************************** *
048600* IF EXTRACTED MONTH IS NOT NUMERIC AND THE LENGTH IS ONE        *
048700* CHARACTER, PREPEND '0' TO THE ONE CHARACTER EXTRACTED.         *
048800* ************************************************************** *
048900
049000     IF DATE-PARM-M IS NUMERIC
049100         CONTINUE
049200     ELSE
049300         MOVE +0 TO SUB1
049400         INSPECT DATE-PARM-M
049500             TALLYING SUB1
049600             FOR CHARACTERS
049700             BEFORE INITIAL ' '
049800         IF SUB1 EQUAL +1
049900             MOVE DATE-PARM-M(1:1) TO DATE-PARM-M(2:1)
050000             MOVE '0' TO DATE-PARM-M(1:1)
050100         END-IF
050200     END-IF.
050300
050400* ************************************************************** *
050500* IF EXTRACTED DAY IS NOT NUMERIC AND THE LENGTH IS ONE          *
050600* CHARACTER, PREPEND '0' TO THE ONE CHARACTER EXTRACTED.         *
050700* ************************************************************** *
050800
050900     IF DATE-PARM-D IS NUMERIC
051000         CONTINUE
051100     ELSE
051200         MOVE +0 TO SUB1
051300         INSPECT DATE-PARM-D
051400             TALLYING SUB1
051500             FOR CHARACTERS
051600             BEFORE INITIAL ' '
051700         IF SUB1 EQUAL +1
051800             MOVE DATE-PARM-D(1:1) TO DATE-PARM-D(2:1)
051900             MOVE '0' TO DATE-PARM-D(1:1)
052000         END-IF
052100     END-IF.
052200
052300* ************************************************************** *
052400* AT THIS POINT, IF ANY OF THE EXTRACTED DATE ELEMENTS ARE NOT   *
052500* NUMERIC, THE ONLY RECOURSE IS TO HANDLE AS AN ERROR.           *
052600* ************************************************************** *
052700
052800     IF DATE-PARM-Y IS NOT NUMERIC
052900     OR DATE-PARM-M IS NOT NUMERIC
053000     OR DATE-PARM-D IS NOT NUMERIC
053100         SET DATE-INVALID TO TRUE
053200     END-IF.
053300
053400* ************************************************************** *
053500* IF THE EXTRACTED DATE ELEMENTS ARE VALID, USE THE MODE VALUE   *
053600* SET DURING THE EXTRACTION TO FILL IN MISSING PIECE(S) OF THE   *
053700* DATE ELEMENTS WITH DEFAULT VALUES FOR END OF THE PERIOD.       *
053800* ************************************************************** *
053900
054000     SET IS-NOT-LEAP-YEAR TO TRUE.
054100     DIVIDE DATE-PARM-YN BY 4
054200         GIVING SUB1
054300         REMAINDER SUB2.
054400     IF SUB2 NOT EQUAL ZERO
054500         CONTINUE
054600     ELSE
054700         DIVIDE DATE-PARM-YN BY 100
054800             GIVING SUB1
054900             REMAINDER SUB2
055000         IF SUB2 NOT EQUAL ZERO
055100             SET IS-LEAP-YEAR TO TRUE
055200         ELSE
055300             DIVIDE DATE-PARM-YN BY 400
055400                 GIVING SUB1
055500                 REMAINDER SUB2
055600             IF SUB2 NOT EQUAL ZERO
055700                 CONTINUE
055800             ELSE
055900                 SET IS-LEAP-YEAR TO TRUE
056000             END-IF
056100         END-IF
056200     END-IF.
056300
056400     IF IS-LEAP-YEAR
056500         MOVE 29 TO DAYS-IN-MONTH (2)
056600     ELSE
056700         MOVE 28 TO DAYS-IN-MONTH (2)
056800     END-IF.
056900
057000     IF DATE-VALID
057100         EVALUATE TRUE
057200             WHEN MODE-YEAR
057300                 MOVE '12' TO DATE-PARM-M
057400                 MOVE '31' TO DATE-PARM-D
057500             WHEN MODE-YEAR-MONTH
057600                 MOVE DATE-PARM-M TO SUB1
057700                 MOVE DAYS-IN-MONTH (SUB1) TO DATE-PARM-D
057800         END-EVALUATE
057900         MOVE DATE-PARM-Y TO DATE-Y
058000         MOVE DATE-PARM-M TO DATE-M
058100         MOVE DATE-PARM-D TO DATE-D
058200     END-IF.
058300
058400* ************************************************************** *
058500* IF THE EXTRACTED ELEMENTS HAVE BEEN VALID TO THIS POINT, ONE   *
058600* FINAL TEST IS MADE TO ENSURE THE MONTH AND DAY COMPONENTS ARE  *
058700* CORRECT AS ENTERED.                                            *
058800* ************************************************************** *
058900
059000     IF DATE-VALID
059100         IF (DATE-M GREATER THAN 0) AND
059200            (DATE-M LESS THAN 13)
059300             MOVE DATE-M TO SUB1
059400             IF (DATE-D GREATER THAN 0) AND
059500                (DATE-D NOT GREATER THAN DAYS-IN-MONTH (SUB1))
059600                 CONTINUE
059700             ELSE
059800                 SET DATE-INVALID TO TRUE
059900             END-IF
060000         ELSE
060100             SET DATE-INVALID TO TRUE
060200         END-IF
060300     END-IF.
060400
060500 END PROGRAM GETPARM.
060600
060700 END PROGRAM ELAPSED.
