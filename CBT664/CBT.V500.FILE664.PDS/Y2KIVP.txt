000200 IDENTIFICATION DIVISION.
000300 PROGRAM-ID. Y2KIVP.
000400 AUTHOR. JAY MOSELEY.
000500 DATE-WRITTEN. FEBRUARY, 1997.
000600 DATE-COMPILED.
000700
000800* ************************************************************* *
000900* THE PURPOSE OF THIS PROGRAM IS TO VERIFY THE SUCCESSFUL       *
001000* INSTALLATION AND FUNCTIONALITY OF THE SET OF DYNAMICALLY      *
001100* CALLABLE YEAR 2000 SUBROUTINES (YEAR 2000 COMPLIANT DATE      *
001200* ROUTINES).                                                    *
001300*                                                               *
001400* WHEN THE SUPPLIED DATA IS READ AND PROCESSED BY THIS PROGRAM, *
001500* EACH OF THE ROUTINES WILL BE CALLED AND ALL FUNCTIONALITY OF  *
001600* EACH IS THOROUGHLY TESTED.  THE OUTPUT REPORT SHOULD BE       *
001700* COMPARED WITH THE BENCHMARK REPORT PRODUCED WHEN THE ROUTINES *
001800* WERE ORIGINALLY CODED AND TESTED.  THE SHOULD BE IDENTICAL.   *
001900* ************************************************************* *
002000/
002100 ENVIRONMENT DIVISION.
002110 CONFIGURATION SECTION.
002112 SPECIAL-NAMES.
002114     C01 IS TO-FIRST-LINE.
002200 INPUT-OUTPUT SECTION.
002300 FILE-CONTROL.
002400     SELECT DATA-FILE
002500         ASSIGN TO UT-S-TESTDATA.
002600
002700     SELECT PRINT-FILE
002800         ASSIGN TO UR-S-REPORT.
002900 DATA DIVISION.
003000
003100 FILE SECTION.
003200 FD  DATA-FILE
003210     LABEL RECORDS ARE STANDARD
003215     BLOCK CONTAINS 0 RECORDS
003300     RECORD CONTAINS 80 CHARACTERS
003400     DATA RECORD IS DATA-RECD.
003500 01  DATA-RECORD                 PIC X(80).
003600
003700 FD  PRINT-FILE
003710     LABEL RECORDS ARE OMITTED
003800     RECORD CONTAINS 133 CHARACTERS
003900     DATA RECORD IS PRINT-RECD.
004000 01  PRINT-RECORD               PIC X(133).
004100/
004200 WORKING-STORAGE SECTION.
004300
004400* ************************************************************* *
004500* THESE ARE PROGRAM CONTROL SWITCHES AND MISCELLANEOUS FIELDS.  *
004600* ************************************************************* *
004700 01  PROGRAM-CONTROL-FIELDS.
004800     02  END-OF-DATA-SWITCH      PIC X(1) VALUE 'N'.
004900         88  END-OF-DATA         VALUE 'Y'.
005000     02  SYSTEM-TIME             PIC 9(8).
005100     02  FILLER                  REDEFINES SYSTEM-TIME.
005200         03  SYSTEM-TIME-SHORT   PIC 9(4).
005300         03  FILLER              PIC 9(4).
005400     02  PAGE-COUNT              PIC 999 VALUE ZERO.
005402     02  LINE-COUNT              PIC 99  VALUE 99.
005404     02  PAGE-SIZE               PIC 99  VALUE 42.
005500
005502     02  UNFORMATTED-GDATE       PIC X(8).
005504     02  FILLER                  REDEFINES UNFORMATTED-GDATE.
005506         03  UNFORMATTED-GMONTH  PIC 9(2).
005508         03  UNFORMATTED-GDAY    PIC 9(2).
005510         03  UNFORMATTED-GYEAR   PIC 9(4).
005512
005514     02  UNFORMATTED-JDATE       PIC X(7).
005516     02  FILLER                  REDEFINES UNFORMATTED-JDATE.
005518         03  UNFORMATTED-JYEAR   PIC 9(4).
005520         03  UNFORMATTED-JDAY    PIC 9(3).
005522
005604     02  FORMATTED-GDATE         PIC X(10).
005606     02  FILLER                  REDEFINES FORMATTED-GDATE.
005608         03  FORMATTED-GMONTH    PIC Z9.
005610         03  FILLER              PIC X(1).
005612         03  FORMATTED-GDAY      PIC 9(2).
005614         03  FILLER              PIC X(1).
005616         03  FORMATTED-GYEAR     PIC 9(4).
005700
005702     02  FORMATTED-JDATE         PIC X(8).
005704     02  FILLER                  REDEFINES FORMATTED-JDATE.
005706         03  FORMATTED-JYEAR     PIC 9(4).
005708         03  FILLER              PIC X(1).
005710         03  FORMATTED-JDAY      PIC 9(3).
005712
005800* ************************************************************* *
005900* FOLLOWING ARE SPECIFIC RECORD DESCRIPTIONS USED TO REDEFINE   *
006000* THE TEST DATA INPUT RECORD FOR EACH OF THE 12 ROUTINES.       *
006100* ************************************************************* *
006200 01  TEST-RECORD.
006300     02  TR-IDENTIFICATION       PIC X(4).
006400     02  FILLER                  PIC X(1).
006500     02  TR-DATA                 PIC X(75).
006600
006700     02  FILLER                  REDEFINES TR-DATA.
006800         03  TR-ATOG-ANUM        PIC X(7).
006900         03  FILLER              PIC X(68).
007000
007100     02  FILLER                  REDEFINES TR-DATA.
007200         03  TR-CONV-DATE        PIC X(6).
007300         03  FILLER              PIC X(1).
007400         03  TR-CONV-FORMAT      PIC X(1).
007500         03  FILLER              PIC X(1).
007600         03  TR-CONV-BREAK       PIC X(2).
007700         03  FILLER              PIC X(64).
007800
007900     02  FILLER                  REDEFINES TR-DATA.
008000         03  TR-DFMT-DATE        PIC X(8).
008100         03  FILLER              PIC X(1).
008200         03  TR-DFMT-MAJOR       PIC X(1).
008300         03  FILLER              PIC X(1).
008400         03  TR-DFMT-MINOR       PIC X(1).
008500         03  FILLER              PIC X(63).
008600
008700     02  FILLER                  REDEFINES TR-DATA.
008800         03  TR-DOWN-DATE        PIC X(8).
008900         03  FILLER              PIC X(67).
009000
009100     02  FILLER                  REDEFINES TR-DATA.
009200         03  TR-GTOA-DATE        PIC X(8).
009300         03  FILLER              PIC X(67).
009400
009500     02  FILLER                  REDEFINES TR-DATA.
009600         03  TR-GTOJ-DATE        PIC X(8).
009700         03  FILLER              PIC X(67).
009800
009900     02  FILLER                  REDEFINES TR-DATA.
010000         03  TR-JTOG-DATE        PIC X(7).
010100         03  FILLER              PIC X(68).
010200
010300     02  FILLER                  REDEFINES TR-DATA.
010400         03  TR-LAGE-DATE1       PIC X(8).
010500         03  FILLER              PIC X(1).
010600         03  TR-LAGE-DATE2       PIC X(8).
010700         03  FILLER              PIC X(58).
010800
010900     02  FILLER                  REDEFINES TR-DATA.
011000         03  TR-PROJ-DATE        PIC X(8).
011100         03  FILLER              PIC X(1).
011200         03  TR-PROJ-INCREMENT   PIC X(6).
011300         03  FILLER              PIC X(60).
011400
011500     02  FILLER                  REDEFINES TR-DATA.
011600         03  TR-SAGE-DATE1       PIC X(8).
011700         03  FILLER              PIC X(1).
011800         03  TR-SAGE-DATE2       PIC X(8).
011900         03  FILLER              PIC X(58).
012000
012100     02  FILLER                  REDEFINES TR-DATA.
012200         03  TR-TDOW-DATE        PIC X(8).
012300         03  FILLER              PIC X(1).
012400         03  TR-TDOW-DIRECTION   PIC X(1).
012500         03  FILLER              PIC X(1).
012600         03  TR-TDOW-DAY-NUMBER  PIC X(1).
012700         03  FILLER              PIC X(63).
012800
012900* ************************************************************* *
013000* FOLLOWING ARE SPECIFIC DATA DESCRIPTIONS FOR THE PARAMETERS   *
013100* REQUIRED FOR THE CALLS TO THE 12 Y2K ROUTINES.                *
013200* ************************************************************* *
013300 01  INTER-PROGRAM-DATA          PIC X(42).
013400
013500 01  ATOG-PARAMETERS             REDEFINES INTER-PROGRAM-DATA.
013600     02   ATOGP-ANUM             PIC X(7).
013700     02   ATOGP-RETURN-CODE      PIC 9(1).
013800     02   ATOGP-DATE             PIC 9(8).
013900     02   FILLER                 PIC X(26).
014000
014100 01  CONV-PARAMETERS             REDEFINES INTER-PROGRAM-DATA.
014200     02   CONVP-DATEIN           PIC X(6).
014300     02   CONVP-FORMAT           PIC X(1).
014400     02   CONVP-BREAK            PIC X(2).
014500     02   CONVP-RETURN-CODE      PIC 9(1).
014600     02   CONVP-DATE-G           PIC 9(8).
014700     02   CONVP-DATE-J           PIC 9(7).
014800     02   CONVP-ANUM             PIC S9(7).
014900     02   FILLER                 PIC X(10).
015000
015100 01  DFMT-PARAMETERS             REDEFINES INTER-PROGRAM-DATA.
015200     02   DFMTP-DATEIN           PIC X(8).
015300     02   DFMTP-MAJOR            PIC X(1).
015400     02   DFMTP-MINOR            PIC X(1).
015500     02   DFMTP-RETURN-CODE      PIC 9(1).
015600     02   DFMTP-OUTPUT-SIZE      PIC S9(2).
015700     02   DFMTP-OUTPUT           PIC X(29).
015800
015900 01  DOWN-PARAMETERS             REDEFINES INTER-PROGRAM-DATA.
016000     02   DOWNP-DATE             PIC X(8).
016100     02   DOWNP-RETURN-CODE      PIC 9(1).
016200     02   DOWNP-DAY-NUMBER       PIC 9(1).
016300     02   FILLER                 PIC X(32).
016400
016500 01  GETD-PARAMETERS             REDEFINES INTER-PROGRAM-DATA.
016600     02   GETDP-DATE-G           PIC 9(8).
016700     02   GETDP-DATE-J           PIC 9(7).
016800     02   GETDP-ANUM             PIC S9(7).
016900     02   FILLER                 PIC X(20).
017000
017100 01  GTOA-PARAMETERS             REDEFINES INTER-PROGRAM-DATA.
017200     02   GTOAP-DATE-G           PIC X(8).
017300     02   GTOAP-RETURN-CODE      PIC 9(1).
017400     02   GTOAP-ANUM             PIC S9(7).
017500     02   FILLER                 PIC X(26).
017600
017700 01  GTOJ-PARAMETERS             REDEFINES INTER-PROGRAM-DATA.
017800     02   GTOJP-DATE-G           PIC X(8).
017900     02   GTOJP-RETURN-CODE      PIC 9(1).
018000     02   GTOJP-DATE-J           PIC 9(7).
018100     02   FILLER                 PIC X(26).
018200
018300 01  JTOG-PARAMETERS             REDEFINES INTER-PROGRAM-DATA.
018400     02   JTOGP-DATE-J           PIC X(7).
018500     02   JTOGP-RETURN-CODE      PIC 9(1).
018600     02   JTOGP-DATE-G           PIC 9(8).
018700     02   FILLER                 PIC X(26).
018800
018900 01  LAGE-PARAMETERS             REDEFINES INTER-PROGRAM-DATA.
019000     02   LAGEP-DATE1            PIC X(8).
019100     02   LAGEP-DATE2            PIC X(8).
019200     02   LAGEP-RETURN-CODE      PIC 9(1).
019300     02   LAGEP-DAYS-PAST        PIC S9(2).
019400     02   LAGEP-MONTHS-PAST      PIC S9(2).
019500     02   LAGEP-YEARS-PAST       PIC S9(5).
019600     02   FILLER                 PIC X(16).
019700
019800 01  PROJ-PARAMETERS             REDEFINES INTER-PROGRAM-DATA.
019900     02   PROJP-DATE             PIC X(8).
020000     02   PROJP-INCREMENT        PIC X(6).
020100     02   PROJP-RETURN-CODE      PIC 9(1).
020200     02   PROJP-NEW-DATE         PIC 9(8).
020300     02   FILLER                 PIC X(19).
020400
020500 01  SAGE-PARAMETERS             REDEFINES INTER-PROGRAM-DATA.
020600     02   SAGEP-DATE1            PIC X(8).
020700     02   SAGEP-DATE2            PIC X(8).
020800     02   SAGEP-RETURN-CODE      PIC 9(1).
020900     02   SAGEP-DAYS-PAST        PIC S9(7).
021000     02   FILLER                 PIC X(18).
021100
021200 01  TDOW-PARAMETERS             REDEFINES INTER-PROGRAM-DATA.
021300     02   TDOWP-DATEIN           PIC X(8).
021400     02   TDOWP-DIRECTION        PIC X(1).
021500     02   TDOWP-DAY-NUMBER       PIC X(1).
021600     02   TDOWP-RETURN-CODE      PIC 9(1).
021700     02   TDOWP-NEW-DATE         PIC 9(8).
021800     02   FILLER                 PIC X(23).
021900
022000* ************************************************************* *
022100* FOLLOWING ARE THE REPORT HEADING AND DETAIL DESCRIPTIONS.     *
022200* ************************************************************* *
022300
022400 01  HEADING-1.
022410     02  FILLER  PIC X(1) VALUE ' '.
022500     02  FILLER  PIC X(6) VALUE 'DATE: '.
022600     02  H1-DATE PIC X(8).
022700     02  FILLER  PIC X(32) VALUE SPACES.
022800     02  FILLER  PIC X(36) VALUE
022900         'YEAR 2000 COMPLIANT DATE SUBROUTINES'.
023000     02  FILLER  PIC X(35) VALUE SPACES.
023100     02  FILLER  PIC X(15) VALUE 'PROGRAM: Y2KIVP'.
023200
023300 01  HEADING-2.
023310     02  FILLER  PIC X(1) VALUE ' '.
023400     02  FILLER  PIC X(6) VALUE 'TIME: '.
023500     02  H2-TIME PIC X(5).
023600     02  FILLER  PIC X(36) VALUE SPACES.
023700     02  FILLER  PIC X(33) VALUE
023800         'INSTALLATION VERIFICATION PROGRAM'.
023900     02  FILLER  PIC X(40) VALUE SPACES.
024000     02  FILLER  PIC X(9) VALUE 'PAGE:   '.
024100     02  H2-PAGE PIC ZZ9.
024200
024300 01  HEADING-3.
024310     02  FILLER  PIC X(1) VALUE ' '.
024400     02  FILLER  PIC X(22) VALUE
024500         'TEST DATA INPUT RECORD'.
024600     02  FILLER  PIC X(39) VALUE SPACES.
024700     02  FILLER  PIC X(7) VALUE 'ROUTINE'.
024800     02  FILLER  PIC X(2) VALUE SPACES.
024900     02  FILLER  PIC X(3) VALUE 'R/C'.
025000     02  FILLER  PIC X(59) VALUE SPACES.
025100
025200 01  DETAIL-LINE.
025210     02  FILLER  PIC X(1) VALUE ' '.
025300     02  DL-TEST-RECORD          PIC X(50).
025400     02  FILLER                  PIC X(11) VALUE SPACES.
025500     02  DL-ROUTINE-NAME         PIC X(7).
025600     02  FILLER                  PIC X(2) VALUE SPACES.
025610     02  DL-SLASH-1              PIC X.
025700     02  DL-RETURN-CODE          PIC X.
025710     02  DL-SLASH-2              PIC X.
025800     02  FILLER                  PIC X(2) VALUE SPACES.
025900     02  DL-COMMON-FORMAT        PIC X(57).
026000
026100 01  DETAIL-ATOG.
026200     02  DATOG-DATE              PIC X(10).
026300
026400 01  DETAIL-CONV.
026500     02  DCONV-DATEG             PIC X(10).
026600     02  FILLER                  PIC X(2) VALUE SPACES.
026700     02  DCONV-DATEJ             PIC X(8).
026800     02  FILLER                  PIC X(2) VALUE SPACES.
026900     02  DCONV-ANUM              PIC 9,999,999.
027000
027100 01  DETAIL-DFMT.
027200     02  DDFMT-COUNT             PIC 99.
027300     02  FILLER                  PIC X(14) VALUE
027400         ' BYTES OUTPUT='.
027500     02  DDFMT-FORMAT            PIC X(29).
027600
027700 01  DETAIL-DOWN.
027800     02  FILLER                  PIC X(23) VALUE
027900         'DAY OF WEEK NUMBER IS: '.
028000     02  DDOWN-NUMBER            PIC 9.
028100     02  FILLER                  PIC X(2) VALUE ' ('.
028200     02  DDOWN-NAME              PIC X(10).
028300
028400 01  DETAIL-GETD.
028500     02  DGETD-DATEG             PIC X(10).
028600     02  FILLER                  PIC X(2) VALUE SPACES.
028700     02  DGETD-DATEJ             PIC X(8).
028800     02  FILLER                  PIC X(2) VALUE SPACES.
028900     02  DGETD-ANUM              PIC 9,999,999.
029000
029100 01  DETAIL-GTOA.
029200     02  DGTOA-ANUM              PIC 9,999,999.
029300
029400 01  DETAIL-GTOJ.
029500     02  DGTOJ-DATE              PIC X(8).
029600
029700 01  DETAIL-JTOG.
029800     02  DJTOG-DATE              PIC X(10).
029900
030000 01  DETAIL-LAGE.
030100     02  DLAGE-YEARS             PIC ZZ,ZZ9.
030200     02  FILLER                  PIC X(8) VALUE ' YEARS, '.
030300     02  DLAGE-MONTHS            PIC Z9.
030400     02  FILLER                  PIC X(9) VALUE ' MONTHS, '.
030500     02  DLAGE-DAYS              PIC Z9.
030600     02  FILLER                  PIC X(13) VALUE ' DAYS BETWEEN'.
030700
030800 01  DETAIL-PROJ.
030900     02  DPROJ-DATE              PIC X(10).
031000
031100 01  DETAIL-SAGE.
031200     02  DSAGE-DAYS              PIC Z,ZZZ,ZZ9.
031300     02  FILLER                  PIC X(13) VALUE ' DAYS BETWEEN'.
031400
031500 01  DETAIL-TDOW.
031600     02  DTDOW-DATE              PIC X(10).
031700/
031800 PROCEDURE DIVISION.
031900
032000 0000-MAIN SECTION.
032100 0050-SETUP.
032110     MOVE TIME-OF-DAY TO UNFORMATTED-GDATE.
032112     MOVE '  :  ' TO FORMATTED-GDATE.
032114     MOVE UNFORMATTED-GMONTH TO FORMATTED-GMONTH.
032116     MOVE UNFORMATTED-GDAY   TO FORMATTED-GDAY.
032118     MOVE FORMATTED-GDATE TO H2-TIME.
032300     MOVE CURRENT-DATE TO H1-DATE.
032600
032700 0100-CONTROL.
032800     OPEN INPUT DATA-FILE,
032900          OUTPUT PRINT-FILE.
033000     PERFORM 0200-READ-DATA-FILE
033010        THRU 0299-READ-DATA-FILE-EXIT.
033100     PERFORM 0300-PROCESS-DATA
033200        THRU 0399-PROCESS-DATA-EXIT
033300       UNTIL END-OF-DATA.
033400     CLOSE DATA-FILE, PRINT-FILE.
033500     STOP RUN.
033600* - - - - - - - - - - - - - - - - PROGRAM EXIT POINT
033700
033800 0200-READ-DATA-FILE.
033900     READ DATA-FILE
034000         AT END MOVE 'Y' TO END-OF-DATA-SWITCH.
034010 0299-READ-DATA-FILE-EXIT.
034015     EXIT.
034100* - - - - - - - - - - - - - - - - PERFORM EXIT POINT
034200
034300 0300-PROCESS-DATA.
034400     MOVE SPACES TO DETAIL-LINE.
034500     MOVE DATA-RECORD TO TEST-RECORD, DL-TEST-RECORD.
034510     IF TR-IDENTIFICATION EQUAL 'ATOG'
034512         PERFORM 0400-CALL-ATOG THRU 0400-CALL-ATOG-EXIT
034513         GO TO 0300-CONTINUE.
034514
034610     IF TR-IDENTIFICATION EQUAL 'CONV'
034612         PERFORM 0405-CALL-CONV THRU 0405-CALL-CONV-EXIT
034613         GO TO 0300-CONTINUE.
034614
034710     IF TR-IDENTIFICATION EQUAL 'DFMT'
034712         PERFORM 0410-CALL-DFMT THRU 0410-CALL-DFMT-EXIT
034713         GO TO 0300-CONTINUE.
034714
034810     IF TR-IDENTIFICATION EQUAL 'DOWN'
034812         PERFORM 0415-CALL-DOWN THRU 0415-CALL-DOWN-EXIT
034813         GO TO 0300-CONTINUE.
034814
034910     IF TR-IDENTIFICATION EQUAL 'GETD'
034912         PERFORM 0420-CALL-GETD THRU 0420-CALL-GETD-EXIT
034913         GO TO 0300-CONTINUE.
034914
035010     IF TR-IDENTIFICATION EQUAL 'GTOA'
035012         PERFORM 0425-CALL-GTOA THRU 0425-CALL-GTOA-EXIT
035013         GO TO 0300-CONTINUE.
035014
035110     IF TR-IDENTIFICATION EQUAL 'GTOJ'
035112         PERFORM 0430-CALL-GTOJ THRU 0430-CALL-GTOJ-EXIT
035113         GO TO 0300-CONTINUE.
035114
035210     IF TR-IDENTIFICATION EQUAL 'JTOG'
035212         PERFORM 0435-CALL-JTOG THRU 0435-CALL-JTOG-EXIT
035213         GO TO 0300-CONTINUE.
035214
035310     IF TR-IDENTIFICATION EQUAL 'LAGE'
035312         PERFORM 0440-CALL-LAGE THRU 0440-CALL-LAGE-EXIT
035313         GO TO 0300-CONTINUE.
035314
035410     IF TR-IDENTIFICATION EQUAL 'PROJ'
035412         PERFORM 0445-CALL-PROJ THRU 0445-CALL-PROJ-EXIT
035413         GO TO 0300-CONTINUE.
035414
035510     IF TR-IDENTIFICATION EQUAL 'SAGE'
035512         PERFORM 0450-CALL-SAGE THRU 0450-CALL-SAGE-EXIT
035513         GO TO 0300-CONTINUE.
035514
035610     IF TR-IDENTIFICATION EQUAL 'TDOW'
035612         PERFORM 0455-CALL-TDOW THRU 0455-CALL-TDOW-EXIT
035613         GO TO 0300-CONTINUE.
035614
036000     MOVE '-------' TO DL-ROUTINE-NAME
036100     MOVE '*' TO DL-SLASH-1, DL-SLASH-2, DL-RETURN-CODE
036200     MOVE 'INVALID ROUTINE SPECIFIED' TO DL-COMMON-FORMAT.
036210
036220  0300-CONTINUE.
036400     IF LINE-COUNT LESS THAN PAGE-SIZE
036410         GO TO 0300-DETAIL.
036500     IF LINE-COUNT LESS THAN 99
036510         GO TO 0300-PAGEHEAD.
036600     MOVE SPACES TO PRINT-RECORD.
036700     WRITE PRINT-RECORD AFTER ADVANCING TO-FIRST-LINE.
036710  0300-PAGEHEAD.
036900     ADD 1 TO PAGE-COUNT
037000     MOVE PAGE-COUNT TO H2-PAGE
037100     WRITE PRINT-RECORD FROM HEADING-1 BEFORE ADVANCING 1
037200     WRITE PRINT-RECORD FROM HEADING-2 BEFORE ADVANCING 2
037300     WRITE PRINT-RECORD FROM HEADING-3 BEFORE ADVANCING 1
037400     MOVE 4 TO LINE-COUNT.
037405
037410 0300-DETAIL.
037600     WRITE PRINT-RECORD FROM DETAIL-LINE BEFORE ADVANCING 1.
037700     ADD 1 TO LINE-COUNT.
037800     PERFORM 0200-READ-DATA-FILE
037810        THRU 0299-READ-DATA-FILE-EXIT.
037900 0399-PROCESS-DATA-EXIT.
038000     EXIT.
038100* - - - - - - - - - - - - - - - - PERFORM EXIT POINT
038200
038300 0400-CALL-ATOG.
038400     MOVE 'Y2KATOG' TO DL-ROUTINE-NAME.
038500     MOVE TR-ATOG-ANUM TO ATOGP-ANUM.
038600     CALL 'Y2KATOG' USING ATOG-PARAMETERS.
038700     MOVE ATOGP-RETURN-CODE TO DL-RETURN-CODE.
038800     IF ATOGP-RETURN-CODE = 0
039000         MOVE ATOGP-DATE TO UNFORMATTED-GDATE
039002         PERFORM 0901-FORMAT-GDATE THRU 0901-FORMAT-GDATE-EXIT
039004         MOVE FORMATTED-GDATE TO DATOG-DATE
039100         MOVE DETAIL-ATOG TO DL-COMMON-FORMAT
039110         GO TO 0400-CALL-ATOG-EXIT.
039200
039300     MOVE '*' TO DL-SLASH-1, DL-SLASH-2.
039400     IF ATOGP-RETURN-CODE EQUAL 2
039500         MOVE 'INVALID ZONED-DECIMAL DATA'
039700           TO DL-COMMON-FORMAT
039710         GO TO 0400-CALL-ATOG-EXIT.
039715
039900     MOVE 'INVALID VALUE (OUT OF RANGE)'
040000       TO DL-COMMON-FORMAT.
040210 0400-CALL-ATOG-EXIT.
040212     EXIT.
040300* - - - - - - - - - - - - - - - - PERFORM EXIT POINT
040400
040500 0405-CALL-CONV.
040600     MOVE 'Y2KCONV' TO DL-ROUTINE-NAME.
040700     MOVE TR-CONV-DATE TO CONVP-DATEIN.
040800     MOVE TR-CONV-FORMAT TO CONVP-FORMAT.
040900     MOVE TR-CONV-BREAK TO CONVP-BREAK.
041000     CALL 'Y2KCONV' USING CONV-PARAMETERS.
041100     MOVE CONVP-RETURN-CODE TO DL-RETURN-CODE.
041200     IF CONVP-RETURN-CODE = 0
041400         MOVE CONVP-DATE-G TO UNFORMATTED-GDATE
041410         PERFORM 0901-FORMAT-GDATE THRU 0901-FORMAT-GDATE-EXIT
041415         MOVE FORMATTED-GDATE TO DCONV-DATEG
041500         MOVE CONVP-DATE-J TO UNFORMATTED-JDATE
041510         PERFORM 0902-FORMAT-JDATE THRU 0902-FORMAT-JDATE-EXIT
041515         MOVE FORMATTED-JDATE TO DCONV-DATEJ
041600         MOVE CONVP-ANUM TO DCONV-ANUM
041700         MOVE DETAIL-CONV TO DL-COMMON-FORMAT
041710         GO TO 0405-CALL-CONV-EXIT.
041800
041900     MOVE '*' TO DL-SLASH-1, DL-SLASH-2.
041910
042000     IF CONVP-RETURN-CODE EQUAL 2
042200         MOVE 'INVALID ZONED-DECIMAL DATA (DATE)'
042300           TO DL-COMMON-FORMAT
042310         GO TO 0405-CALL-CONV-EXIT.
042315
042400     IF CONVP-RETURN-CODE EQUAL 3
042500         MOVE 'INVALID ZONED-DECIMAL DATA (BREAK)'
042600           TO DL-COMMON-FORMAT
042605         GO TO 0405-CALL-CONV-EXIT.
042610
042700     IF CONVP-RETURN-CODE EQUAL 4
042800         MOVE 'INVALID VALUE (YEAR)'
042900           TO DL-COMMON-FORMAT
042905         GO TO 0405-CALL-CONV-EXIT.
042910
043000     IF CONVP-RETURN-CODE EQUAL 5
043100         MOVE 'INVALID DATE FORMAT CODE'
043200           TO DL-COMMON-FORMAT
043205         GO TO 0405-CALL-CONV-EXIT.
043210
043300     IF CONVP-RETURN-CODE EQUAL 6
043400         MOVE 'INVALID VALUE (MONTH)'
043500           TO DL-COMMON-FORMAT
043505         GO TO 0405-CALL-CONV-EXIT.
043510
043600     IF CONVP-RETURN-CODE EQUAL 8
043700         MOVE 'INVALID VALUE (DAY)'
043800           TO DL-COMMON-FORMAT.
044010 0405-CALL-CONV-EXIT.
044015     EXIT.
044100* - - - - - - - - - - - - - - - - PERFORM EXIT POINT
044200
044300 0410-CALL-DFMT.
044400     MOVE 'Y2KDFMT' TO DL-ROUTINE-NAME.
044500     MOVE TR-DFMT-DATE TO DFMTP-DATEIN.
044600     MOVE TR-DFMT-MAJOR TO DFMTP-MAJOR.
044700     MOVE TR-DFMT-MINOR TO DFMTP-MINOR.
044800     MOVE SPACES TO DFMTP-OUTPUT.
044900     CALL 'Y2KDFMT' USING DFMT-PARAMETERS.
045000     MOVE DFMTP-RETURN-CODE TO DL-RETURN-CODE.
045100     IF DFMTP-RETURN-CODE = 0
045300         MOVE DFMTP-OUTPUT-SIZE TO DDFMT-COUNT
045400         MOVE DFMTP-OUTPUT TO DDFMT-FORMAT
045500         MOVE DETAIL-DFMT TO DL-COMMON-FORMAT
045510         GO TO 0410-CALL-DFMT-EXIT.
045600
045700     MOVE '*' TO DL-SLASH-1, DL-SLASH-2.
045800     IF DFMTP-RETURN-CODE EQUAL 2
046000         MOVE 'INVALID ZONED-DECIMAL DATA'
046100           TO DL-COMMON-FORMAT
046105         GO TO 0410-CALL-DFMT-EXIT.
046110
046200     IF DFMTP-RETURN-CODE EQUAL 3
046300         MOVE 'INVALID MAJOR FORMAT CODE'
046400           TO DL-COMMON-FORMAT
046405         GO TO 0410-CALL-DFMT-EXIT.
046410
046500     IF DFMTP-RETURN-CODE EQUAL 4
046600         MOVE 'INVALID VALUE (YEAR)'
046700           TO DL-COMMON-FORMAT
046705         GO TO 0410-CALL-DFMT-EXIT.
046710
046800     IF DFMTP-RETURN-CODE EQUAL 5
046900         MOVE 'INVALID MINOR FORMAT CODE'
047000           TO DL-COMMON-FORMAT
047005         GO TO 0410-CALL-DFMT-EXIT.
047010
047100     IF DFMTP-RETURN-CODE EQUAL 6
047200         MOVE 'INVALID VALUE (MONTH)'
047300           TO DL-COMMON-FORMAT
047305         GO TO 0410-CALL-DFMT-EXIT.
047310
047400     IF DFMTP-RETURN-CODE EQUAL  8
047500         MOVE 'INVALID VALUE (DAY)'
047600           TO DL-COMMON-FORMAT.
047810 0410-CALL-DFMT-EXIT.
047812     EXIT.
047900* - - - - - - - - - - - - - - - - PERFORM EXIT POINT
048000
048100 0415-CALL-DOWN.
048200     MOVE 'Y2KDOWN' TO DL-ROUTINE-NAME.
048300     MOVE TR-DOWN-DATE TO DOWNP-DATE.
048400     CALL 'Y2KDOWN' USING DOWN-PARAMETERS.
048500     MOVE DOWNP-RETURN-CODE TO DL-RETURN-CODE.
048600     IF DOWNP-RETURN-CODE NOT EQUAL 0
048610         GO TO 0415-CALL-DOWN-ERR.
048612
048800     MOVE DOWNP-DAY-NUMBER TO DDOWN-NUMBER.
048900         IF DOWNP-DAY-NUMBER EQUAL 0
049000             MOVE 'MONDAY)' TO DDOWN-NAME
049005             MOVE DETAIL-DOWN TO DL-COMMON-FORMAT
049006             GO TO 0415-CALL-DOWN-EXIT
049010         ELSE IF DOWNP-DAY-NUMBER EQUAL 1
049100             MOVE 'TUESDAY)' TO DDOWN-NAME
049105             MOVE DETAIL-DOWN TO DL-COMMON-FORMAT
049106             GO TO 0415-CALL-DOWN-EXIT
049110         ELSE IF DOWNP-DAY-NUMBER EQUAL 2
049200             MOVE 'WEDNESDAY)' TO DDOWN-NAME
049205             MOVE DETAIL-DOWN TO DL-COMMON-FORMAT
049206             GO TO 0415-CALL-DOWN-EXIT
049210         ELSE IF DOWNP-DAY-NUMBER EQUAL 3
049300             MOVE 'THURSDAY)' TO DDOWN-NAME
049305             MOVE DETAIL-DOWN TO DL-COMMON-FORMAT
049306             GO TO 0415-CALL-DOWN-EXIT
049310         ELSE IF DOWNP-DAY-NUMBER EQUAL 4
049400             MOVE 'FRIDAY)' TO DDOWN-NAME
049405             MOVE DETAIL-DOWN TO DL-COMMON-FORMAT
049406             GO TO 0415-CALL-DOWN-EXIT
049410         ELSE IF DOWNP-DAY-NUMBER EQUAL 5
049500             MOVE 'SATURDAY)' TO DDOWN-NAME
049505             MOVE DETAIL-DOWN TO DL-COMMON-FORMAT
049506             GO TO 0415-CALL-DOWN-EXIT
049510         ELSE IF DOWNP-DAY-NUMBER EQUAL 6
049600             MOVE 'SUNDAY)' TO DDOWN-NAME
049800             MOVE DETAIL-DOWN TO DL-COMMON-FORMAT
049806             GO TO 0415-CALL-DOWN-EXIT.
049810
049900 0415-CALL-DOWN-ERR.
050000     MOVE '*' TO DL-SLASH-1, DL-SLASH-2.
050100     IF DOWNP-RETURN-CODE EQUAL 2
050300         MOVE 'INVALID ZONED-DECIMAL DATA'
050400           TO DL-COMMON-FORMAT
050410         GO TO 0415-CALL-DOWN-EXIT.
050500     IF DOWNP-RETURN-CODE EQUAL 4
050600         MOVE 'INVALID VALUE (YEAR)'
050700           TO DL-COMMON-FORMAT
050710         GO TO 0415-CALL-DOWN-EXIT.
050800     IF DOWNP-RETURN-CODE EQUAL 6
050900         MOVE 'INVALID VALUE (MONTH)'
051000           TO DL-COMMON-FORMAT
051010         GO TO 0415-CALL-DOWN-EXIT.
051100     IF DOWNP-RETURN-CODE EQUAL 8
051200         MOVE 'INVALID VALUE (DAY)'
051300           TO DL-COMMON-FORMAT.
051510 0415-CALL-DOWN-EXIT.
051520     EXIT.
051600* - - - - - - - - - - - - - - - - PERFORM EXIT POINT
051700
051800 0420-CALL-GETD.
051900     MOVE 'Y2KGETD' TO DL-ROUTINE-NAME.
052000     CALL 'Y2KGETD' USING GETD-PARAMETERS.
052100     MOVE GETDP-DATE-G TO DGETD-DATEG.
052200     MOVE GETDP-DATE-J TO DGETD-DATEJ.
052300     MOVE GETDP-ANUM TO DGETD-ANUM.
052400     MOVE DETAIL-GETD TO DL-COMMON-FORMAT.
052410 0420-CALL-GETD-EXIT.
052415     EXIT.
052500* - - - - - - - - - - - - - - - - PERFORM EXIT POINT
052600
052700 0425-CALL-GTOA.
052800     MOVE 'Y2KGTOA' TO DL-ROUTINE-NAME.
052900     MOVE TR-GTOA-DATE TO GTOAP-DATE-G.
053000     CALL 'Y2KGTOA' USING GTOA-PARAMETERS.
053100     MOVE GTOAP-RETURN-CODE TO DL-RETURN-CODE.
053200     IF GTOAP-RETURN-CODE = 0
053400         MOVE GTOAP-ANUM TO DGTOA-ANUM
053500         MOVE DETAIL-GTOA TO DL-COMMON-FORMAT
053510         GO TO 0425-CALL-GTOA-EXIT.
053600
053700     MOVE '*' TO DL-SLASH-1, DL-SLASH-2.
053800
053900     IF GTOAP-RETURN-CODE EQUAL 2
054000         MOVE 'INVALID ZONED-DECIMAL DATA'
054100           TO DL-COMMON-FORMAT
054110         GO TO 0425-CALL-GTOA-EXIT.
054200     IF GTOAP-RETURN-CODE EQUAL 4
054300         MOVE 'INVALID VALUE (YEAR)'
054400           TO DL-COMMON-FORMAT
054410         GO TO 0425-CALL-GTOA-EXIT.
054500     IF GTOAP-RETURN-CODE EQUAL 6
054600         MOVE 'INVALID VALUE (MONTH)'
054700           TO DL-COMMON-FORMAT
054710         GO TO 0425-CALL-GTOA-EXIT.
054800     IF GTOAP-RETURN-CODE EQUAL 8
054900         MOVE 'INVALID VALUE (DAY)'
055000           TO DL-COMMON-FORMAT.
055210 0425-CALL-GTOA-EXIT.
055215     EXIT.
055300* - - - - - - - - - - - - - - - - PERFORM EXIT POINT
055400
055500 0430-CALL-GTOJ.
055600     MOVE 'Y2KGTOJ' TO DL-ROUTINE-NAME.
055700     MOVE TR-GTOJ-DATE TO GTOJP-DATE-G.
055800     CALL 'Y2KGTOJ' USING GTOJ-PARAMETERS.
055900     MOVE GTOJP-RETURN-CODE TO DL-RETURN-CODE.
056000     IF GTOJP-RETURN-CODE = 0
056200         MOVE GTOJP-DATE-J TO UNFORMATTED-JDATE
056210         PERFORM 0902-FORMAT-JDATE THRU 0902-FORMAT-JDATE-EXIT
056215         MOVE FORMATTED-JDATE TO DGTOJ-DATE
056300         MOVE DETAIL-GTOJ TO DL-COMMON-FORMAT
056400         GO TO 0430-CALL-GTOJ-EXIT.
056500     MOVE '*' TO DL-SLASH-1, DL-SLASH-2.
056700     IF GTOJP-RETURN-CODE EQUAL 2
056800                 MOVE 'INVALID ZONED-DECIMAL DATA'
056900                   TO DL-COMMON-FORMAT
056920       GO TO 0430-CALL-GTOJ-EXIT.
057000     IF GTOJP-RETURN-CODE EQUAL 4
057100                 MOVE 'INVALID VALUE (YEAR)'
057200                   TO DL-COMMON-FORMAT
057210       GO TO 0430-CALL-GTOJ-EXIT.
057220     IF GTOJP-RETURN-CODE EQUAL 6
057400                 MOVE 'INVALID VALUE (MONTH)'
057500                   TO DL-COMMON-FORMAT
057510       GO TO 0430-CALL-GTOJ-EXIT.
057520     IF GTOJP-RETURN-CODE EQUAL 8
057700                 MOVE 'INVALID VALUE (DAY)'
057800                   TO DL-COMMON-FORMAT.
058010 0430-CALL-GTOJ-EXIT.
058012     EXIT.
058100* - - - - - - - - - - - - - - - - PERFORM EXIT POINT
058200
058300 0435-CALL-JTOG.
058400     MOVE 'Y2KJTOG' TO DL-ROUTINE-NAME.
058500     MOVE TR-JTOG-DATE TO JTOGP-DATE-J.
058600     CALL 'Y2KJTOG' USING JTOG-PARAMETERS.
058700     MOVE JTOGP-RETURN-CODE TO DL-RETURN-CODE.
058800     IF JTOGP-RETURN-CODE = 0
059000         MOVE JTOGP-DATE-G TO UNFORMATTED-GDATE
059020         PERFORM 0901-FORMAT-GDATE THRU 0901-FORMAT-GDATE-EXIT
059030         MOVE FORMATTED-GDATE TO DJTOG-DATE
059100         MOVE DETAIL-JTOG TO DL-COMMON-FORMAT
059110         GO TO 0435-CALL-JTOG-EXIT.
059200
059300     MOVE '*' TO DL-SLASH-1, DL-SLASH-2.
059400     IF JTOGP-RETURN-CODE EQUAL 2
059600                 MOVE 'INVALID ZONED-DECIMAL DATA'
059700                   TO DL-COMMON-FORMAT
059710            GO TO 0435-CALL-JTOG-EXIT.
059720     IF JTOGP-RETURN-CODE EQUAL 4
059900                 MOVE 'INVALID VALUE (YEAR)'
060000                   TO DL-COMMON-FORMAT
060010            GO TO 0435-CALL-JTOG-EXIT.
060020     IF JTOGP-RETURN-CODE EQUAL 8
060200                 MOVE 'INVALID VALUE (DAY)'
060300                   TO DL-COMMON-FORMAT.
060520 0435-CALL-JTOG-EXIT.
060530     EXIT.
060600* - - - - - - - - - - - - - - - - PERFORM EXIT POINT
060700
060800 0440-CALL-LAGE.
060900     MOVE 'Y2KLAGE' TO DL-ROUTINE-NAME.
061000     MOVE TR-LAGE-DATE1 TO LAGEP-DATE1.
061100     MOVE TR-LAGE-DATE2 TO LAGEP-DATE2.
061200     CALL 'Y2KLAGE' USING LAGE-PARAMETERS.
061300     MOVE LAGEP-RETURN-CODE TO DL-RETURN-CODE.
061400     IF LAGEP-RETURN-CODE = 0
061600         MOVE LAGEP-DAYS-PAST TO DLAGE-DAYS
061700         MOVE LAGEP-MONTHS-PAST TO DLAGE-MONTHS
061800         MOVE LAGEP-YEARS-PAST TO DLAGE-YEARS
061900         MOVE DETAIL-LAGE TO DL-COMMON-FORMAT
061910         GO TO 0440-CALL-LAGE-EXIT.
062000
062100     MOVE '*' TO DL-SLASH-1, DL-SLASH-2.
062200     IF LAGEP-RETURN-CODE EQUAL 2
062400                 MOVE 'INVALID ZONED-DECIMAL DATA (DATE 1)'
062500                   TO DL-COMMON-FORMAT
062510          GO TO 0440-CALL-LAGE-EXIT.
062520     IF LAGEP-RETURN-CODE EQUAL 3
062700                 MOVE 'INVALID ZONED-DECIMAL DATA (DATE 2)'
062800                   TO DL-COMMON-FORMAT
062810          GO TO 0440-CALL-LAGE-EXIT.
062820     IF LAGEP-RETURN-CODE EQUAL 4
063000                 MOVE 'INVALID VALUE (YEAR/DATE 1)'
063100                   TO DL-COMMON-FORMAT
063110          GO TO 0440-CALL-LAGE-EXIT.
063120     IF LAGEP-RETURN-CODE EQUAL 5
063300                 MOVE 'INVALID VALUE (YEAR/DATE 2)'
063400                   TO DL-COMMON-FORMAT
063410          GO TO 0440-CALL-LAGE-EXIT.
063420     IF LAGEP-RETURN-CODE EQUAL 6
063600                 MOVE 'INVALID VALUE (MONTH/DATE 1)'
063700                   TO DL-COMMON-FORMAT
063710          GO TO 0440-CALL-LAGE-EXIT.
063720     IF LAGEP-RETURN-CODE EQUAL 7
063900                 MOVE 'INVALID VALUE (MONTH/DATE 2)'
064000                   TO DL-COMMON-FORMAT
064110          GO TO 0440-CALL-LAGE-EXIT.
064120     IF LAGEP-RETURN-CODE EQUAL 8
064200                 MOVE 'INVALID VALUE (DAY/DATE 1)'
064300                   TO DL-COMMON-FORMAT
064310          GO TO 0440-CALL-LAGE-EXIT.
064320     IF LAGEP-RETURN-CODE EQUAL 9
064500                 MOVE 'INVALID VALUE (DAY/DATE 2)'
064600                   TO DL-COMMON-FORMAT.
064810 0440-CALL-LAGE-EXIT.
064820      EXIT.
064900* - - - - - - - - - - - - - - - - PERFORM EXIT POINT
065000
065100 0445-CALL-PROJ.
065200     MOVE 'Y2KPROJ' TO DL-ROUTINE-NAME.
065300     MOVE TR-PROJ-DATE TO PROJP-DATE.
065400     MOVE TR-PROJ-INCREMENT TO PROJP-INCREMENT.
065500     CALL 'Y2KPROJ' USING PROJ-PARAMETERS.
065600     MOVE PROJP-RETURN-CODE TO DL-RETURN-CODE.
065700     IF PROJP-RETURN-CODE = 0
065900         MOVE PROJP-NEW-DATE TO UNFORMATTED-GDATE
065910         PERFORM 0901-FORMAT-GDATE THRU 0901-FORMAT-GDATE-EXIT
065920         MOVE FORMATTED-GDATE TO DPROJ-DATE
066000         MOVE DETAIL-PROJ TO DL-COMMON-FORMAT
066010         GO TO 0445-CALL-PROJ-EXIT.
066100
066200     MOVE '*' TO DL-SLASH-1, DL-SLASH-2.
066300     IF PROJP-RETURN-CODE EQUAL 2
066500                 MOVE 'INVALID ZONED-DECIMAL DATA (DATE)'
066600                   TO DL-COMMON-FORMAT
066610         GO TO 0445-CALL-PROJ-EXIT.
066620     IF PROJP-RETURN-CODE EQUAL 3
066800                 MOVE 'INVALID ZONED-DECIMAL DATA (INCREMENT)'
066900                   TO DL-COMMON-FORMAT
066910         GO TO 0445-CALL-PROJ-EXIT.
066920     IF PROJP-RETURN-CODE EQUAL 4
067100                 MOVE 'INVALID VALUE (YEAR)'
067200                   TO DL-COMMON-FORMAT
067210         GO TO 0445-CALL-PROJ-EXIT.
067220     IF PROJP-RETURN-CODE EQUAL 5
067400                 MOVE 'INVALID VALUE (INCREMENT)'
067500                   TO DL-COMMON-FORMAT
067510         GO TO 0445-CALL-PROJ-EXIT.
067520     IF PROJP-RETURN-CODE EQUAL 6
067700                 MOVE 'INVALID VALUE (MONTH)'
067800                   TO DL-COMMON-FORMAT
067810         GO TO 0445-CALL-PROJ-EXIT.
067820     IF PROJP-RETURN-CODE EQUAL 8
068000                 MOVE 'INVALID VALUE (DAY)'
068100                   TO DL-COMMON-FORMAT.
068310 0445-CALL-PROJ-EXIT.
068320      EXIT.
068400* - - - - - - - - - - - - - - - - PERFORM EXIT POINT
068500
068600 0450-CALL-SAGE.
068700     MOVE 'Y2KSAGE' TO DL-ROUTINE-NAME.
068800     MOVE TR-SAGE-DATE1 TO SAGEP-DATE1.
068900     MOVE TR-SAGE-DATE2 TO SAGEP-DATE2.
069000     CALL 'Y2KSAGE' USING SAGE-PARAMETERS.
069100     MOVE SAGEP-RETURN-CODE TO DL-RETURN-CODE.
069200     IF SAGEP-RETURN-CODE = 0
069400         MOVE SAGEP-DAYS-PAST TO DSAGE-DAYS
069500         MOVE DETAIL-SAGE TO DL-COMMON-FORMAT
069510         GO TO 0450-CALL-SAGE-EXIT.
069600
069700     MOVE '*' TO DL-SLASH-1, DL-SLASH-2.
069800     IF SAGEP-RETURN-CODE EQUAL 2
070000                 MOVE 'INVALID ZONED-DECIMAL DATA (DATE 1)'
070100                   TO DL-COMMON-FORMAT
070110          GO TO 0450-CALL-SAGE-EXIT.
070120     IF SAGEP-RETURN-CODE EQUAL 3
070300                 MOVE 'INVALID ZONED-DECIMAL DATA (DATE 2)'
070400                   TO DL-COMMON-FORMAT
070410          GO TO 0450-CALL-SAGE-EXIT.
070420     IF SAGEP-RETURN-CODE EQUAL 4
070600                 MOVE 'INVALID VALUE (YEAR/DATE 1)'
070700                   TO DL-COMMON-FORMAT
070710          GO TO 0450-CALL-SAGE-EXIT.
070720     IF SAGEP-RETURN-CODE EQUAL 5
070900                 MOVE 'INVALID VALUE (YEAR/DATE 2)'
071000                   TO DL-COMMON-FORMAT
071010          GO TO 0450-CALL-SAGE-EXIT.
071020     IF SAGEP-RETURN-CODE EQUAL 6
071200                 MOVE 'INVALID VALUE (MONTH/DATE 1)'
071300                   TO DL-COMMON-FORMAT
071310          GO TO 0450-CALL-SAGE-EXIT.
071320     IF SAGEP-RETURN-CODE EQUAL 7
071500                 MOVE 'INVALID VALUE (MONTH/DATE 2)'
071600                   TO DL-COMMON-FORMAT
071610          GO TO 0450-CALL-SAGE-EXIT.
071620     IF SAGEP-RETURN-CODE EQUAL 8
071800                 MOVE 'INVALID VALUE (DAY/DATE 1)'
071900                   TO DL-COMMON-FORMAT
071910          GO TO 0450-CALL-SAGE-EXIT.
071920     IF SAGEP-RETURN-CODE EQUAL 9
072100                 MOVE 'INVALID VALUE (DAY/DATE 2)'
072200                   TO DL-COMMON-FORMAT.
072310 0450-CALL-SAGE-EXIT.
072320     EXIT.
072500* - - - - - - - - - - - - - - - - PERFORM EXIT POINT
072600
072700 0455-CALL-TDOW.
072800     MOVE 'Y2KTDOW' TO DL-ROUTINE-NAME.
072900     MOVE TR-TDOW-DATE TO TDOWP-DATEIN.
073000     MOVE TR-TDOW-DIRECTION TO TDOWP-DIRECTION.
073100     MOVE TR-TDOW-DAY-NUMBER TO TDOWP-DAY-NUMBER.
073200     CALL 'Y2KTDOW' USING TDOW-PARAMETERS.
073300     MOVE TDOWP-RETURN-CODE TO DL-RETURN-CODE.
073400     IF TDOWP-RETURN-CODE = 0
073600         MOVE TDOWP-NEW-DATE TO UNFORMATTED-GDATE
073610         PERFORM 0901-FORMAT-GDATE THRU 0901-FORMAT-GDATE-EXIT
073620         MOVE FORMATTED-GDATE TO DTDOW-DATE
073700         MOVE DETAIL-TDOW TO DL-COMMON-FORMAT
073710         GO TO 0455-CALL-TDOW-EXIT.
073800
073900     MOVE '*' TO DL-SLASH-1, DL-SLASH-2.
074000     IF TDOWP-RETURN-CODE EQUAL 2
074200                 MOVE 'INVALID ZONED-DECIMAL DATA (DATE)'
074300                   TO DL-COMMON-FORMAT
074310         GO TO 0455-CALL-TDOW-EXIT.
074320     IF TDOWP-RETURN-CODE EQUAL 3
074500                 MOVE 'INVALID DIRECTION CODE'
074600                   TO DL-COMMON-FORMAT
074610         GO TO 0455-CALL-TDOW-EXIT.
074620     IF TDOWP-RETURN-CODE EQUAL 4
074800                 MOVE 'INVALID VALUE (YEAR)'
074900                   TO DL-COMMON-FORMAT
074910         GO TO 0455-CALL-TDOW-EXIT.
074920     IF TDOWP-RETURN-CODE EQUAL 5
075100                 MOVE 'TARGET DATE WILL BE INVALID'
075200                   TO DL-COMMON-FORMAT
075210         GO TO 0455-CALL-TDOW-EXIT.
075220     IF TDOWP-RETURN-CODE EQUAL 7
075400                 MOVE 'INVALID DAY OF WEEK NUMBER'
075500                   TO DL-COMMON-FORMAT
075510         GO TO 0455-CALL-TDOW-EXIT.
075520     IF TDOWP-RETURN-CODE EQUAL 6
075700                 MOVE 'INVALID VALUE (MONTH)'
075800                   TO DL-COMMON-FORMAT
075810         GO TO 0455-CALL-TDOW-EXIT.
075820     IF TDOWP-RETURN-CODE EQUAL 8
076000                 MOVE 'INVALID VALUE (DAY)'
076100                   TO DL-COMMON-FORMAT.
076310 0455-CALL-TDOW-EXIT.
076320     EXIT.
076400* - - - - - - - - - - - - - - - - PERFORM EXIT POINT
076500
080002 0901-FORMAT-GDATE.
080004     MOVE '  /  /    ' TO FORMATTED-GDATE.
080006     MOVE UNFORMATTED-GMONTH TO FORMATTED-GMONTH.
080008     MOVE UNFORMATTED-GDAY   TO FORMATTED-GDAY.
080010     MOVE UNFORMATTED-GYEAR  TO FORMATTED-GYEAR.
080012 0901-FORMAT-GDATE-EXIT.
080014     EXIT.
080102 0902-FORMAT-JDATE.
080104     MOVE '    /   ' TO FORMATTED-JDATE.
080106     MOVE UNFORMATTED-JYEAR TO FORMATTED-JYEAR.
080108     MOVE UNFORMATTED-JDAY  TO FORMATTED-JDAY.
080110 0902-FORMAT-JDATE-EXIT.
080112     EXIT.
