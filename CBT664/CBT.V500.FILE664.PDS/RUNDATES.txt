000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. RUNDATES.
000300*
000400*  AUTHOR. JAY MOSELEY
000500*  DATE-WRITTEN. NOVEMBER, 1998.
000600*
000700* ************************************************************* *
000800* THIS PROGRAM USES THE SYSTEM DATE AND A SET OF RULES FOR      *
000900* DETERMINING NON-WORKING HOLIDAYS TO RETURN A SET OF WORKING   *
001000* DATES (CURRENT, PREVIOUS, AND NEXT) TO THE CALLING PROGRAM.   *
001100* ************************************************************* *
001200
001300 ENVIRONMENT DIVISION.
001400 INPUT-OUTPUT SECTION.
001500 FILE-CONTROL.
001600     SELECT FD1-HOLIDAY-RULES
001700         ASSIGN TO 'HOLIDATE.RUL'
001800         ORGANIZATION IS LINE SEQUENTIAL.
001900
002000 DATA DIVISION.
002100 FILE SECTION.
002200 FD  FD1-HOLIDAY-RULES
002300     RECORD CONTAINS 80 CHARACTERS.
002400 01  RD1-HOLIDAY-RULES.
002500     03  RD1-ORDINAL-MONTH       PIC 9(02).
002600     03                          REDEFINES RD1-ORDINAL-MONTH.
002700         05  RD1-COMMENT-FLAG    PIC X(01).
002800             88  RD1-COMMENT                 VALUE '*'.
002900         05                      PIC X(01).
003000     03  RD1-ORDINAL-DAY         PIC 9(02).
003100     03  RD1-WEEKDAY             PIC X(02).
003200         88  RD1-MONDAY                      VALUE 'MO'.
003300         88  RD1-TUESDAY                     VALUE 'TU'.
003400         88  RD1-WEDNESDAY                   VALUE 'WE'.
003500         88  RD1-THURSDAY                    VALUE 'TH'.
003600         88  RD1-FRIDAY                      VALUE 'FR'.
003700         88  RD1-SATURDAY                    VALUE 'SA'.
003800         88  RD1-SUNDAY                      VALUE 'SU'.
003900     03  RD1-WEEKEND-ADJUST-FLAG PIC 9(01).
004000         88  RD1-NO-WEEKEND-ADJUST           VALUE 0.
004100         88  RD1-WEEKEND-ADJUST-BACKWARD     VALUE 1.
004200         88  RD1-WEEKEND-ADJUST-FORWARD      VALUE 2.
004400     03  RD1-HOLIDAY-NAME        PIC X(50).
004500     03                          PIC X(23).
004600
004610 01  RD1-WORK-WEEK.
004620     03  RD1-WORK-WEEK-KEY       PIC 9(04).
004630     03  RD1-WORK-WEEK-TABLE     PIC X(07).
004640     03                          PIC X(69).
004650
004700 WORKING-STORAGE SECTION.
004800 01  WS-MISC-STORAGE.
004900     03  END-OF-FILE-SWITCH      PIC X(01)   VALUE 'N'.
005000         88  END-OF-FILE                     VALUE 'Y'.
005010
005100     03  WORK-GREG-DATE          PIC 9(08).
005200     03                          REDEFINES WORK-GREG-DATE.
005300         05  WORK-GREG-M         PIC 9(02).
005400         05  WORK-GREG-D         PIC 9(02).
005500         05  WORK-GREG-Y         PIC 9(04).
005510
005600     03  DATE-TEST-FLAG          PIC 9(01).
005700         88  VALID-WORKING-DATE              VALUE 0.
005800         88  DATE-IS-WEEKEND                 VALUE 1.
005900         88  DATE-IS-HOLIDAY                 VALUE 2.
006000
006010     03  WORK-WEEK-TABLE                     VALUE 'YYYYYNN'.
006020         05  WORK-WEEK-FLAG      OCCURS 7 TIMES
006030                                 INDEXED BY WORK-WEEK-IX
006040                                 PIC X(01).
006041             88  NON-WORKING-WEEKDAY         VALUE 'N'.
006042             88  WORKING-WEEKDAY             VALUE 'Y'.
006050
006100* ************************************************************* *
006200* THE TABLE OF OBSERVED HOLIDAYS IS BUILT FROM RULES READ FROM  *
006300* FD1-HOLIDAY-RULES. RULES MAY BE ADDED WITHOUT RECOMPILING THE *
006400* PROGRAM AS LONG AS THE NUMBER OF HOLIDAYS GENERATED DOES NOT  *
006500* EXCEED THE CAPACITY OF THIS TABLE.                            *
006600* ************************************************************* *
006700 01  OBSERVED-HOLIDAY-AREA.
006800     03  HOLIDAY-TABLE-CAPACITY  PIC S9(4)   COMP.
006900     03  HOLIDAY-TABLE-MAXIMUM   PIC S9(4)   COMP.
007000     03  HOLIDAY-TABLE.
007100         05  HOLIDAY-DATE        OCCURS 50 TIMES
007200                                 INDEXED BY HOLIDAY-IX
007300                                 PIC 9(08).
007400
007500 01  DATE-ROUTINE-PARAMETER-BLOCKS.
007600* ************************************************************* *
007700* PARAMETER AREA FOR Y2KATOG.                                   *
007800* ************************************************************* *
007900     02  ATOG-PARAMETERS.
008000         03  ATOGP-ANUM          PIC S9(07).
008100         03  ATOGP-RETURN-CODE   PIC 9(01).
008200         03  ATOGP-DATE-G        PIC 9(08).
008300         03                      REDEFINES ATOGP-DATE-G.
008400              04  ATOGP-DATE-G-M PIC 9(02).
008500              04  ATOGP-DATE-G-D PIC 9(02).
008600              04  ATOGP-DATE-G-Y PIC 9(04).
008700
008800* ************************************************************* *
008900* PARAMETER AREA FOR Y2KDOWN.                                   *
009000* ************************************************************* *
009100     02  DOWN-PARAMETERS.
009200         03  DOWNP-DATE-G        PIC 9(8).
009300         03  DOWNP-RETURN-CODE   PIC 9(1).
009400         03  DOWNP-DAYNUMBER     PIC 9(1).
009500
009600* ************************************************************* *
009700* PARAMETER AREA FOR Y2KESTR.                                   *
009800* ************************************************************* *
009801     02  ESTR-PARAMETERS.
009802         03  ESTRP-YEAR          PIC 9(04).
009803         03  ESTRP-RETURN-CODE   PIC X(01).
009804         03  ESTRP-DATE-G        PIC 9(08).
009805         03                      REDEFINES ESTRP-DATE-G.
009806             04  ESTRP-DATE-G-M  PIC 9(02).
009807             04  ESTRP-DATE-G-D  PIC 9(02).
009808             04  ESTRP-DATE-G-Y  PIC 9(04).
009810
009820* ************************************************************* *
009830* PARAMETER AREA FOR Y2KGETD.                                   *
009840* ************************************************************* *
009900     02  GETD-PARAMETERS.
010000         03  GETDP-DATE-G        PIC 9(08).
010100         03                      REDEFINES GETDP-DATE-G.
010200             04  GETDP-DATE-G-M  PIC 9(02).
010300             04  GETDP-DATE-G-D  PIC 9(02).
010400             04  GETDP-DATE-G-Y  PIC 9(04).
010500         03  GETDP-DATE-J        PIC 9(07).
010600         03  GETDP-ANUM          PIC S9(07).
010700
010800* ************************************************************* *
010900* PARAMETER AREA FOR Y2KGTOA.                                   *
011000* ************************************************************* *
011100     02  GTOA-PARAMETERS.
011200         03  GTOAP-DATE-G        PIC 9(08).
011300         03                      REDEFINES GTOAP-DATE-G.
011400             04  GTOAP-DATE-G-M  PIC 9(02).
011500             04  GTOAP-DATE-G-D  PIC 9(02).
011600             04  GTOAP-DATE-G-Y  PIC 9(04).
011700         03  GTOAP-RETURN-CODE   PIC 9(01).
011800         03  GTOAP-ANUM          PIC S9(07).
011900
012000* ************************************************************* *
012100* PARAMETER AREA FOR Y2KGTOJ.                                   *
012200* ************************************************************* *
012300     02  GTOJ-PARAMETERS.
012400         03  GTOJP-DATE-G        PIC 9(08).
012500         03  GTOJP-RETURN-CODE   PIC 9(01).
012600         03  GTOJP-DATE-J        PIC 9(07).
012700         03                      REDEFINES GTOJP-DATE-J.
012800             04  GTOJP-DATE-J-Y  PIC 9(04).
012900             04  GTOJP-DATE-J-D  PIC 9(03).
013000
013100* ************************************************************* *
013200* PARAMETER AREA FOR Y2KPROJ.                                   *
013300* ************************************************************* *
013400     02  PROJ-PARAMETERS.
013500         03  PROJP-DATE-O        PIC 9(08).
013600         03                      REDEFINES PROJP-DATE-O.
013700             04  PROJP-DATE-O-M  PIC 9(02).
013800             04  PROJP-DATE-O-D  PIC 9(02).
013900             04  PROJP-DATE-O-Y  PIC 9(04).
014000         03  PROJP-INCR          PIC S9(06).
014100         03  PROJP-RETURN-CODE   PIC 9(01).
014200         03  PROJP-DATE-N        PIC 9(08).
014300         03                      REDEFINES PROJP-DATE-N.
014400             04  PROJP-DATE-N-M  PIC 9(02).
014500             04  PROJP-DATE-N-D  PIC 9(02).
014600             04  PROJP-DATE-N-Y  PIC 9(04).
014700
014800* ************************************************************* *
014900* PARAMETER AREA FOR Y2KSAGE.                                   *
015000* ************************************************************* *
015100     02  SAGE-PARAMETERS.
015200         03  SAGEP-DATE-1        PIC 9(08).
015300         03  SAGEP-DATE-2        PIC 9(08).
015400         03  SAGEP-RETURN-CODE   PIC 9(01).
015500         03  SAGEP-DAYS          PIC S9(07).
015600
015700* ************************************************************* *
015800* PARAMETER AREA FOR Y2KTDOW.                                   *
015900* ************************************************************* *
016000     02  TDOW-PARAMETERS.
016100         03  TDOWP-DATE-O        PIC 9(08).
016200         03                      REDEFINES TDOWP-DATE-O.
016300             04  TDOWP-DATE-O-M  PIC 9(02).
016400             04  TDOWP-DATE-O-D  PIC 9(02).
016500             04  TDOWP-DATE-O-Y  PIC 9(04).
016600         03  TDOWP-NORP          PIC X(01).
016700         03  TDOWP-DAYNUMBER     PIC 9(01).
016800         03  TDOWP-RETURN-CODE   PIC 9(01).
016900         03  TDOWP-DATE-N        PIC 9(08).
017000         03                      REDEFINES TDOWP-DATE-N.
017100             04  TDOWP-DATE-N-M  PIC 9(02).
017200             04  TDOWP-DATE-N-D  PIC 9(02).
017300             04  TDOWP-DATE-N-Y  PIC 9(04).
017400
017500 LINKAGE SECTION.
017600     COPY 'RUNDCOPY'.
017700
017800 PROCEDURE DIVISION USING RUN-DATES-RECORD.
017900
018000 0000-PROCESS.
018100* ************************************************************* *
018200* 1. RETRIEVE SYSTEM DATE.                                      *
018300* 2. READ HOLIDAY RULES FILE AND CREATE TABLE OF HOLIDAYS.      *
018400* 3. TEST SYSTEM DATE FOR VALID WORKING DAY.                    *
018500* 4. BUILD THIS DATE INFORMATION FOR CURRENT SYSTEM DATE.       *
018600* 5. DETERMINE PRIOR DATE BACKWARD FROM 'THIS' DATE.            *
018700* 6. DETERMINE NEXT DATE FORWARD FROM 'THIS' DATE.              *
018800* 7. SET INDICATOR FLAGS FOR 'THIS' DATE.                       *
018900* ************************************************************* *
019000
019100     CALL 'Y2KGETD' USING GETD-PARAMETERS.
019200
019300     PERFORM 1000-ESTABLISH-HOLIDAY-TABLE.
019400
019410     PERFORM VARYING HOLIDAY-IX
019420             FROM +1 BY +1
019421             UNTIL HOLIDAY-IX GREATER THAN HOLIDAY-TABLE-MAXIMUM
019422         DISPLAY 'HOLIDAY DATE: '
019430                 HOLIDAY-DATE (HOLIDAY-IX)
019440     END-PERFORM.
019450
019500     MOVE GETDP-DATE-G TO WORK-GREG-DATE.
019600     PERFORM 0100-TEST-DATE.
019700     IF VALID-WORKING-DATE
019800         PERFORM 2000-BUILD-THIS-DATE
019900         PERFORM 3000-BUILD-PREV-DATE
020000         PERFORM 4000-BUILD-NEXT-DATE
020100         PERFORM 5000-FINISH-THIS
020200     ELSE
020300         DISPLAY 'RUNDATES: CURRENT SYSTEM DATE NOT VALID'
020400         MOVE +8 TO RETURN-CODE
020500     END-IF.
020600
020700     GOBACK.
020800* - - - - - - - - - - - - - - - - PROGRAM EXIT POINT
020900
021000 0100-TEST-DATE.
021100* ************************************************************* *
021200* CHECK THE DATE IN WORK-GREG-DATE FOR FALLING ON A NON-WORKING *
021300* WEEKDAY OR ON A HOLIDAY DATE.                                 *
021400* ************************************************************* *
021500     SET VALID-WORKING-DATE TO TRUE.
021600
021700     MOVE WORK-GREG-DATE TO DOWNP-DATE-G.
021800     CALL 'Y2KDOWN' USING DOWN-PARAMETERS.
021900     IF DOWNP-RETURN-CODE NOT EQUAL ZERO
022000         DISPLAY 'RUNDATES: ERROR CALLING Y2KDOWN'
022100         MOVE +16 TO RETURN-CODE
022200         GOBACK
022300     END-IF.
022301     ADD 1 TO DOWNP-DAYNUMBER.
022310     SET WORK-WEEK-IX TO DOWNP-DAYNUMBER.
022311     SUBTRACT 1 FROM DOWNP-DAYNUMBER.
022320     IF NON-WORKING-WEEKDAY (WORK-WEEK-IX)
022500         SET DATE-IS-WEEKEND TO TRUE
022600     END-IF.
022700
022800     IF VALID-WORKING-DATE
022900         PERFORM VARYING HOLIDAY-IX
023000                 FROM +1 BY +1
023100                 UNTIL HOLIDAY-IX GREATER THAN
023200                                  HOLIDAY-TABLE-MAXIMUM
023210                 OR DATE-IS-HOLIDAY
023300             IF WORK-GREG-DATE EQUAL HOLIDAY-DATE (HOLIDAY-IX)
023400                 SET DATE-IS-HOLIDAY TO TRUE
023600             END-IF
023700         END-PERFORM
023800     END-IF.
023900* - - - - - - - - - - - - - - - - PERFORM EXIT POINT
024000
024110 1000-ESTABLISH-HOLIDAY-TABLE.
024200* ************************************************************* *
024300* FOR EACH HOLIDAY RULE RECORD, COMPUTE THE HOLIDAY DATE FOR    *
024400* THE CURRENT YEAR AND INSERT INTO THE HOLIDAY DATE TABLE.      *
024500* ************************************************************* *
024600     COMPUTE HOLIDAY-TABLE-CAPACITY =
024700         FUNCTION LENGTH(HOLIDAY-TABLE) /
024800         FUNCTION LENGTH(HOLIDAY-DATE).
024900
025000     SET HOLIDAY-IX TO +1.
025100
025200     OPEN INPUT FD1-HOLIDAY-RULES.
025300
025400     PERFORM 1100-READ-HOLIDAY-RULE.
025500
025600     PERFORM 1200-PROCESS-HOLIDAY-RULE
025700         UNTIL END-OF-FILE.
025800
025900     CLOSE FD1-HOLIDAY-RULES.
026000* - - - - - - - - - - - - - - - - PERFORM EXIT POINT
026100
026200 1100-READ-HOLIDAY-RULE.
026300     READ FD1-HOLIDAY-RULES
026400         AT END
026500             SET END-OF-FILE TO TRUE
026600     END-READ.
026700* - - - - - - - - - - - - - - - - PERFORM EXIT POINT
026800
026900 1200-PROCESS-HOLIDAY-RULE.
027000* ************************************************************* *
027100* FOR EACH NON-COMMENT HOLIDAY RULE RECORD, COMPUTE THE HOLIDAY *
027200* DATE FOR THE CURRENT YEAR. IF THE CURRENT MONTH IS 12 AND THE *
027300* HOLIDAY RULE IS FOR MONTH 1 (OR IF CURRENT MONTH IS 1 AND THE *
027400* HOLIDAY RULE IS FOR MONTH 12) COMPUTE THE HOLIDAY DATE FOR    *
027500* THE NEXT (OR PREVIOUS) YEAR AS WELL.                          *
027600* ************************************************************* *
027700     IF NOT RD1-COMMENT
027710         IF RD1-WORK-WEEK-KEY EQUAL '0000'
027720             MOVE RD1-WORK-WEEK-TABLE TO WORK-WEEK-TABLE
027730         ELSE
027800             PERFORM 1300-BUILD-HOLIDAY
027900             EVALUATE GETDP-DATE-G-M ALSO RD1-ORDINAL-MONTH
028000                 WHEN 12 ALSO 1
028100                     ADD 1 TO GETDP-DATE-G-Y
028200                     PERFORM 1300-BUILD-HOLIDAY
028300                     SUBTRACT 1 FROM GETDP-DATE-G-Y
028400                 WHEN 1 ALSO 12
028500                     SUBTRACT 1 FROM GETDP-DATE-G-Y
028600                     PERFORM 1300-BUILD-HOLIDAY
028700                     ADD 1 TO GETDP-DATE-G-Y
028800             END-EVALUATE
028900         END-IF
028910     END-IF.
029000
029100     PERFORM 1100-READ-HOLIDAY-RULE.
029200* - - - - - - - - - - - - - - - - PERFORM EXIT POINT
029300
029400 1300-BUILD-HOLIDAY.
029500* ************************************************************* *
029600* IF THE HOLIDAY TABLE IS FULL, DISPLAY A MESSAGE AND SET THE   *
029700* RETURN CODE TO 16. OTHERWISE, DERIVE THE HOLIDAY DATE FOR THE *
029800* CURRENT YEAR VALUE AND INSERT INTO THE HOLIDAY TABLE.         *
029900* ************************************************************* *
030000     IF HOLIDAY-TABLE-MAXIMUM EQUAL HOLIDAY-TABLE-CAPACITY
030100         DISPLAY 'RUNDATES: HOLIDAY TABLE CAPACITY EXCEEDED'
030200         MOVE +16 TO RETURN-CODE
030300         GOBACK
030400     END-IF.
030500
030600     IF RD1-WEEKDAY EQUAL SPACES
030610         IF RD1-ORDINAL-MONTH EQUAL 99
030630             PERFORM 6000-PROGRAMMED-HOLIDAY
030640         ELSE
030700            MOVE RD1-ORDINAL-MONTH TO WORK-GREG-M
030800            MOVE RD1-ORDINAL-DAY TO WORK-GREG-D
030900            MOVE GETDP-DATE-G-Y TO WORK-GREG-Y
030910         END-IF
031000     ELSE
031100         PERFORM 1400-DERIVE-HOLIDAY
031200     END-IF.
031300
031400     PERFORM 1500-CHECK-FOR-WEEKEND.
031500
031600     MOVE WORK-GREG-DATE TO HOLIDAY-DATE (HOLIDAY-IX).
031700     SET HOLIDAY-TABLE-MAXIMUM TO HOLIDAY-IX.
031800     SET HOLIDAY-IX UP BY +1.
031900* - - - - - - - - - - - - - - - - PERFORM EXIT POINT
032000
032100 1400-DERIVE-HOLIDAY.
032200* ************************************************************* *
032300* USING THE HOLIDAY RULES RECORD ENTRIES, CALL DATE ROUTINES TO *
032400* ESTABLISH THE PROPER HOLIDAY DATE.                            *
032500* ************************************************************* *
032600     MOVE RD1-ORDINAL-MONTH TO TDOWP-DATE-O-M.
032700     MOVE 1                 TO TDOWP-DATE-O-D.
032800     MOVE GETDP-DATE-G-Y    TO TDOWP-DATE-O-Y.
032900
033000     IF RD1-ORDINAL-DAY EQUAL 99
033100         ADD 1 TO TDOWP-DATE-O-M
033200         MOVE 'P' TO TDOWP-NORP
033300     ELSE
033400         MOVE 'N' TO TDOWP-NORP
033500     END-IF.
033600
033700     EVALUATE TRUE
033800         WHEN RD1-MONDAY
033900             MOVE 0 TO TDOWP-DAYNUMBER
034000         WHEN RD1-TUESDAY
034100             MOVE 1 TO TDOWP-DAYNUMBER
034200         WHEN RD1-WEDNESDAY
034300             MOVE 2 TO TDOWP-DAYNUMBER
034400         WHEN RD1-THURSDAY
034500             MOVE 3 TO TDOWP-DAYNUMBER
034600         WHEN RD1-FRIDAY
034700             MOVE 4 TO TDOWP-DAYNUMBER
034800         WHEN RD1-SATURDAY
034900             MOVE 5 TO TDOWP-DAYNUMBER
035000         WHEN RD1-SUNDAY
035100             MOVE 6 TO TDOWP-DAYNUMBER
035200     END-EVALUATE.
035300
035400     CALL 'Y2KTDOW' USING TDOW-PARAMETERS.
035500     IF TDOWP-RETURN-CODE NOT EQUAL ZERO
035600         DISPLAY 'RUNDATES: ERROR CALLING Y2KTDOW'
035700         MOVE +16 TO RETURN-CODE
035800         GOBACK
035900     END-IF.
036000
036100     IF RD1-ORDINAL-DAY NOT EQUAL 99
036200         MOVE TDOWP-DATE-N TO PROJP-DATE-O
036300         COMPUTE PROJP-INCR = (RD1-ORDINAL-DAY - 1) * 7
036400         CALL 'Y2KPROJ' USING PROJ-PARAMETERS
036500         IF PROJP-RETURN-CODE NOT EQUAL ZERO
036600             DISPLAY 'RUNDATES: ERROR CALLING Y2KPROJ'
036700             MOVE +16 TO RETURN-CODE
036800             GOBACK
036900         END-IF
037000         MOVE PROJP-DATE-N TO WORK-GREG-DATE
037100     ELSE
037200         MOVE TDOWP-DATE-N TO WORK-GREG-DATE
037300     END-IF.
037400* - - - - - - - - - - - - - - - - PERFORM EXIT POINT
037500
037600 1500-CHECK-FOR-WEEKEND.
037700* ************************************************************* *
037800* IF HOLIDAY RULE SPECIFIES HOLIDAYS FALLING ON WEEKENDS MOVE   *
037900* FORWARD OR BACK TO NEXT WORKING DAY AND DERIVED HOLIDAY DATE  *
038000* FALLS ON A WEEKEND, ADJUST TO PROPER DAY (FORWARD OR BACK).   *
038100* ************************************************************* *
038200     IF RD1-NO-WEEKEND-ADJUST
038300         CONTINUE
038400     ELSE
038420         PERFORM WITH TEST AFTER
038430                 UNTIL WORKING-WEEKDAY (WORK-WEEK-IX)
038500             MOVE WORK-GREG-DATE TO DOWNP-DATE-G
038600             CALL 'Y2KDOWN' USING DOWN-PARAMETERS
038700             IF DOWNP-RETURN-CODE NOT EQUAL ZERO
038800                 DISPLAY 'RUNDATES: ERROR CALLING Y2KDOWN'
038900                 MOVE +16 TO RETURN-CODE
039000                 GOBACK
039100             END-IF
039101             ADD 1 TO DOWNP-DAYNUMBER
039110             SET WORK-WEEK-IX TO DOWNP-DAYNUMBER
039120             IF NON-WORKING-WEEKDAY (WORK-WEEK-IX)
039121                 MOVE WORK-GREG-DATE TO PROJP-DATE-O
039130                 IF RD1-WEEKEND-ADJUST-BACKWARD
039140                     MOVE -1 TO PROJP-INCR
039150                 ELSE
039160                     MOVE +1 TO PROJP-INCR
039170                 END-IF
040300                 CALL 'Y2KPROJ' USING PROJ-PARAMETERS
040400                 IF PROJP-RETURN-CODE NOT EQUAL ZERO
040500                     DISPLAY 'RUNDATES: ERROR CALLING Y2KPROJ'
040600                     MOVE +16 TO RETURN-CODE
040700                     GOBACK
040800                 END-IF
040900                 MOVE PROJP-DATE-N TO WORK-GREG-DATE
040910             END-IF
041000         END-PERFORM
041100     END-IF.
041200* - - - - - - - - - - - - - - - - PERFORM EXIT POINT
041300
041400 2000-BUILD-THIS-DATE.
041500* ************************************************************* *
041600* FORMAT 'THIS' DATE AREA IN CALLERS STORAGE USING THE SYSTEM   *
041700* CURRENT DATE AND CALLS TO DATE ROUTINES.                      *
041800* ************************************************************* *
042200     MOVE GETDP-DATE-G TO THIS-DATE-GREGORIAN.
042300     MOVE GETDP-DATE-J TO THIS-DATE-JULIAN.
042400     MOVE GETDP-ANUM TO THIS-DATE-ASTRO.
042500
042600* ************************************************************* *
042700* NOTE: SINCE Y2KDOWN WAS LAST CALLED WITH THE DATE WE ARE NOW  *
042710* PROCESSING (FROM 0000-PROCESS -> 0100-TEST-DATE) WE CAN JUST  *
042720* UTILIZE THE PARAMETER RETURN AREA WITHOUT AN ADDITIONAL CALL. *
042800* ************************************************************* *
043300     MOVE DOWNP-DAYNUMBER TO THIS-DATE-DAYNUMBER.
043400     EVALUATE DOWNP-DAYNUMBER
043500         WHEN 0
043600             MOVE 'MONDAY' TO THIS-DATE-DAYNAME
043700         WHEN 1
043800             MOVE 'TUESDAY' TO THIS-DATE-DAYNAME
043900         WHEN 2
044000             MOVE 'WEDNESDAY' TO THIS-DATE-DAYNAME
044100         WHEN 3
044200             MOVE 'THURSDAY' TO THIS-DATE-DAYNAME
044300         WHEN 4
044400             MOVE 'FRIDAY' TO THIS-DATE-DAYNAME
044500         WHEN 5
044600             MOVE 'SATURDAY' TO THIS-DATE-DAYNAME
044700         WHEN 6
044800             MOVE 'SUNDAY' TO THIS-DATE-DAYNAME
044900     END-EVALUATE.
045000
045100     MOVE ALL 'N' TO THIS-DATE-FLAGS.
045500* - - - - - - - - - - - - - - - - PERFORM EXIT POINT
045600
045700 3000-BUILD-PREV-DATE.
045800* ************************************************************* *
045900* FORMAT 'PREV' DATE AREA IN CALLERS STORAGE BY SEARCHING BACK  *
046000* TO THE FIRST PRIOR WORKING DATE.                              *
046100* ************************************************************* *
046500     MOVE THIS-DATE-GREGORIAN TO WORK-GREG-DATE.
046700
046820     PERFORM WITH TEST AFTER UNTIL VALID-WORKING-DATE
046825         MOVE WORK-GREG-DATE TO PROJP-DATE-O
046826         MOVE -1 TO PROJP-INCR
046830         CALL 'Y2KPROJ' USING PROJ-PARAMETERS
046840         IF PROJP-RETURN-CODE NOT EQUAL ZERO
046850             DISPLAY 'RUNDATES: ERROR CALLING Y2KPROJ'
046860             MOVE +16 TO RETURN-CODE
046870             GOBACK
046880         END-IF
046890         MOVE PROJP-DATE-N TO WORK-GREG-DATE
047000         PERFORM 0100-TEST-DATE
047100     END-PERFORM.
047200
048300     MOVE WORK-GREG-DATE TO PREV-DATE-GREGORIAN,
048400                            GTOJP-DATE-G
048500                            GTOAP-DATE-G.
048600
048700     CALL 'Y2KGTOJ' USING GTOJ-PARAMETERS.
048800     IF GTOJP-RETURN-CODE NOT EQUAL ZERO
048900         DISPLAY 'RUNDATES: ERROR CALLING Y2KGTOJ'
049000         MOVE +16 TO RETURN-CODE
049100         GOBACK
049200     END-IF.
049300     MOVE GTOJP-DATE-J TO PREV-DATE-JULIAN.
049400
049500     CALL 'Y2KGTOA' USING GTOA-PARAMETERS.
049600     IF GTOAP-RETURN-CODE NOT EQUAL ZERO
049700         DISPLAY 'RUNDATES: ERROR CALLING Y2KGTOA'
049800         MOVE +16 TO RETURN-CODE
049900         GOBACK
050000     END-IF.
050100     MOVE GTOAP-ANUM TO PREV-DATE-ASTRO.
050200
050300* ************************************************************* *
050400* NOTE: SINCE Y2KDOWN WAS LAST CALLED WITH THE DATE WE ARE NOW  *
050500* PROCESSING (FROM 0100-TEST-DATE ABOVE) WE CAN JUST UTILIZE    *
050600* THE PARAMETER RETURN AREA WITHOUT AN ADDITIONAL CALL.         *
050700* ************************************************************* *
051000     MOVE DOWNP-DAYNUMBER TO PREV-DATE-DAYNUMBER.
051100     EVALUATE DOWNP-DAYNUMBER
051200         WHEN 0
051300             MOVE 'MONDAY' TO PREV-DATE-DAYNAME
051400         WHEN 1
051500             MOVE 'TUESDAY' TO PREV-DATE-DAYNAME
051600         WHEN 2
051700             MOVE 'WEDNESDAY' TO PREV-DATE-DAYNAME
051800         WHEN 3
051900             MOVE 'THURSDAY' TO PREV-DATE-DAYNAME
052000         WHEN 4
052100             MOVE 'FRIDAY' TO PREV-DATE-DAYNAME
052200         WHEN 5
052300             MOVE 'SATURDAY' TO PREV-DATE-DAYNAME
052400         WHEN 6
052500             MOVE 'SUNDAY' TO PREV-DATE-DAYNAME
052600     END-EVALUATE.
053000* - - - - - - - - - - - - - - - - PERFORM EXIT POINT
053100
053200 4000-BUILD-NEXT-DATE.
053600* ************************************************************* *
053700* FORMAT 'NEXT' DATE AREA IN CALLERS STORAGE BY SEARCHING       *
053800* FORWARD TO THE FIRST WORKING DATE.                            *
053900* ************************************************************* *
054000     MOVE THIS-DATE-GREGORIAN TO PROJP-DATE-O.
054100     MOVE +1 TO PROJP-INCR.
054200
054300     PERFORM WITH TEST AFTER UNTIL VALID-WORKING-DATE
054400         CALL 'Y2KPROJ' USING PROJ-PARAMETERS
054500         IF PROJP-RETURN-CODE NOT EQUAL ZERO
054600             DISPLAY 'RUNDATES: ERROR CALLING Y2KPROJ'
054700             MOVE +16 TO RETURN-CODE
054800             GOBACK
054900         END-IF
055000         MOVE PROJP-DATE-N TO WORK-GREG-DATE,
055100                              PROJP-DATE-O
055200         PERFORM 0100-TEST-DATE
055300     END-PERFORM.
055400
055500     MOVE WORK-GREG-DATE TO NEXT-DATE-GREGORIAN,
055600                            GTOJP-DATE-G
055700                            GTOAP-DATE-G.
055800
055900     CALL 'Y2KGTOJ' USING GTOJ-PARAMETERS.
056000     IF GTOJP-RETURN-CODE NOT EQUAL ZERO
056100         DISPLAY 'RUNDATES: ERROR CALLING Y2KGTOJ'
056200         MOVE +16 TO RETURN-CODE
056300         GOBACK
056400     END-IF.
056500     MOVE GTOJP-DATE-J TO NEXT-DATE-JULIAN.
056600
056700     CALL 'Y2KGTOA' USING GTOA-PARAMETERS.
056800     IF GTOAP-RETURN-CODE NOT EQUAL ZERO
056900         DISPLAY 'RUNDATES: ERROR CALLING Y2KGTOA'
057000         MOVE +16 TO RETURN-CODE
057100         GOBACK
057200     END-IF.
057300     MOVE GTOAP-ANUM TO NEXT-DATE-ASTRO.
057400
057500* ************************************************************* *
057600* NOTE: SINCE Y2KDOWN WAS LAST CALLED WITH THE DATE WE ARE NOW  *
057700* PROCESSING (FROM 0100-TEST-DATE ABOVE) WE CAN JUST UTILIZE    *
057800* THE PARAMETER RETURN AREA WITHOUT AN ADDITIONAL CALL.         *
057900* ************************************************************* *
058200     MOVE DOWNP-DAYNUMBER TO NEXT-DATE-DAYNUMBER.
058300     EVALUATE DOWNP-DAYNUMBER
058400         WHEN 0
058500             MOVE 'MONDAY' TO NEXT-DATE-DAYNAME
058600         WHEN 1
058700             MOVE 'TUESDAY' TO NEXT-DATE-DAYNAME
058800         WHEN 2
058900             MOVE 'WEDNESDAY' TO NEXT-DATE-DAYNAME
059000         WHEN 3
059100             MOVE 'THURSDAY' TO NEXT-DATE-DAYNAME
059200         WHEN 4
059300             MOVE 'FRIDAY' TO NEXT-DATE-DAYNAME
059400         WHEN 5
059500             MOVE 'SATURDAY' TO NEXT-DATE-DAYNAME
059600         WHEN 6
059700             MOVE 'SUNDAY' TO NEXT-DATE-DAYNAME
059800     END-EVALUATE.
060200* - - - - - - - - - - - - - - - - PERFORM EXIT POINT
060300
060400 5000-FINISH-THIS.
060500* ************************************************************* *
060600* COMPUTE NUMBER OF DAYS BETWEEN 'THIS' AND 'NEXT' DATES,       *
060700* BETWEEN 'THIS' AND 'PREV' DATES, AND SET FLAGS FOR 'THIS'     *
060800* DATE.                                                         *
060900* ************************************************************* *
061300     MOVE THIS-DATE-GREGORIAN TO SAGEP-DATE-1.
061400     MOVE PREV-DATE-GREGORIAN TO SAGEP-DATE-2.
061500     CALL 'Y2KSAGE' USING SAGE-PARAMETERS.
061600     IF SAGEP-RETURN-CODE NOT EQUAL ZERO
061700         DISPLAY 'RUNDATES: ERROR CALLING Y2KSAGE'
061800         MOVE +16 TO RETURN-CODE
061900         GOBACK
062000     END-IF.
062100     MOVE SAGEP-DAYS TO DAYS-UNTIL-THIS.
062200
062300     MOVE NEXT-DATE-GREGORIAN TO SAGEP-DATE-2.
062400     CALL 'Y2KSAGE' USING SAGE-PARAMETERS.
062500     IF SAGEP-RETURN-CODE NOT EQUAL ZERO
062600         DISPLAY 'RUNDATES: ERROR CALLING Y2KSAGE'
062700         MOVE +16 TO RETURN-CODE
062800         GOBACK
062900     END-IF.
063000     MOVE SAGEP-DAYS TO DAYS-SINCE-THIS.
063100
063200     IF THIS-DATE-DAYNUMBER IS LESS THAN PREV-DATE-DAYNUMBER
063300         SET TODAY-IS-WEEK-BEGIN TO TRUE
063400     END-IF.
063500
063600     IF NEXT-DATE-DAYNUMBER IS LESS THAN THIS-DATE-DAYNUMBER
063700         SET TODAY-IS-WEEK-END TO TRUE
063800     END-IF.
063900
064000     IF TDG-MONTH IS NOT EQUAL PDG-MONTH
064100         SET TODAY-IS-MONTH-BEGIN TO TRUE
064200     END-IF.
064300
064400     IF NDG-MONTH IS NOT EQUAL TDG-MONTH
064500         SET TODAY-IS-MONTH-END TO TRUE
064600     END-IF.
064700
064800     IF TODAY-IS-MONTH-BEGIN
064900         IF TDG-MONTH IS EQUAL 1 OR 4 OR 7 OR 10
065000             SET TODAY-IS-QUARTER-BEGIN TO TRUE
065100         END-IF
065200     END-IF.
065300
065400     IF TODAY-IS-MONTH-END
065500         IF TDG-MONTH IS EQUAL 3 OR 6 OR 9 OR 12
065600             SET TODAY-IS-QUARTER-END TO TRUE
065700         END-IF
065800     END-IF.
065900
066000     IF TDG-YEAR IS NOT EQUAL PDG-YEAR
066100         SET TODAY-IS-YEAR-BEGIN TO TRUE
066200     END-IF.
066300
066400     IF NDG-YEAR IS NOT EQUAL TDG-YEAR
066500         SET TODAY-IS-YEAR-END TO TRUE
066600     END-IF.
067000* - - - - - - - - - - - - - - - - PERFORM EXIT POINT
067100
067101 6000-PROGRAMMED-HOLIDAY.
067102* ************************************************************* *
067103* A FEW HOLIDAYS (EG. EASTER) MUST BE DERIVED BY SPECIAL        *
067104* COMPUTATIONS. THOSE HOLIDAYS ARE COMPUTED IN THIS SECTION.    *
067107* ************************************************************* *
067108     EVALUATE RD1-ORDINAL-DAY
067109         WHEN 01
067110             PERFORM 6100-EASTER
067111         WHEN 02
067112             PERFORM 6200-GOOD-FRIDAY
067113         WHEN 03
067114             PERFORM 6300-ELECTION-DAY
067115         WHEN OTHER
067116             DISPLAY 'RUNDATES: PROGRAMMED HOLIDAY CODING ERROR'
067117             MOVE +16 TO RETURN-CODE
067118             GOBACK
067119     END-EVALUATE.
067120* - - - - - - - - - - - - - - - - PERFORM EXIT POINT
067121
067122 6100-EASTER.
067123* ************************************************************* *
067124* CALL Y2KESTR TO DETERMINE EASTER SUNDAY DATE.                 *
067125* ************************************************************* *
067126     MOVE WORK-GREG-Y TO ESTRP-YEAR.
067127     CALL 'Y2KESTR' USING ESTR-PARAMETERS.
067128     IF ESTRP-RETURN-CODE NOT EQUAL ZERO
067129         DISPLAY 'RUNDATES: ERROR CALLING Y2KESTR'
067130         MOVE +16 TO RETURN-CODE
067131         GOBACK
067132     END-IF.
067133     MOVE ESTRP-DATE-G TO WORK-GREG-DATE.
067140* - - - - - - - - - - - - - - - - PERFORM EXIT POINT
067150
067170 6200-GOOD-FRIDAY.
067180* ************************************************************* *
067190* DETERMINE EASTER SUNDAY DATE THEN PROJECT BACK 2 DAYS TO GET  *
067191* GOOD FRIDAY'S DATE.                                           *
067192* ************************************************************* *
067193     PERFORM 6100-EASTER.
067194
067195     MOVE WORK-GREG-DATE TO PROJP-DATE-O.
067196     MOVE -2 TO PROJP-INCR.
067197     CALL 'Y2KPROJ' USING PROJ-PARAMETERS.
067198     IF PROJP-RETURN-CODE NOT EQUAL ZERO
067199         DISPLAY 'RUNDATES: ERROR CALLING Y2KPROJ'
067200         MOVE +16 TO RETURN-CODE
067300         GOBACK
067400     END-IF.
067500     MOVE PROJP-DATE-N TO WORK-GREG-DATE.
067510* - - - - - - - - - - - - - - - - PERFORM EXIT POINT
067600
067700 6300-ELECTION-DAY.
067710* ************************************************************* *
067720* ELECTION DAY IS THE 1ST TUESDAY FOLLOWING THE FIRST MONDAY IN *
067730* NOVEMBER.                                                     *
067740* ************************************************************* *
067800     MOVE 10310000 TO TDOWP-DATE-O.
067801     MOVE WORK-GREG-Y TO TDOWP-DATE-O-Y.
067802     MOVE 'N' TO TDOWP-NORP.
067803     MOVE 0 TO TDOWP-DAYNUMBER.
067804     CALL 'Y2KTDOW' USING TDOW-PARAMETERS.
067805     IF TDOWP-RETURN-CODE NOT EQUAL ZERO
067806         DISPLAY 'RUNDATES: ERROR CALLING Y2KTDOW'
067807         MOVE +16 TO RETURN-CODE
067808         GOBACK
067809     END-IF.
067810     MOVE TDOWP-DATE-N TO TDOWP-DATE-O.
067811     MOVE 'N' TO TDOWP-NORP.
067812     MOVE 1 TO TDOWP-DAYNUMBER.
067813     CALL 'Y2KTDOW' USING TDOW-PARAMETERS.
067814     IF TDOWP-RETURN-CODE NOT EQUAL ZERO
067815         DISPLAY 'RUNDATES: ERROR CALLING Y2KTDOW'
067816         MOVE +16 TO RETURN-CODE
067817         GOBACK
067818     END-IF.
067819     MOVE TDOWP-DATE-N TO WORK-GREG-DATE.
067822* - - - - - - - - - - - - - - - - PERFORM EXIT POINT
067830
067900 END PROGRAM RUNDATES.
