         TITLE 'Y2KGTOJ - CONVERT GREGORIAN DATE TO JULIAN DATE'
*                                                                     *
*   YY   YY   222   KK   KK  GGGGG TTTTTTTT  OOOOO       JJ           *
*   YY   YY  2   2  KK  KK  GG   GG   TT    OO   OO      JJ           *
*   YY   YY      2  KK KK   GG   GG   TT    OO   OO      JJ           *
*    YY YY       2  KKKK    GG        TT    OO   OO      JJ           *
*     YYY     222   KKKK    GG  GGG   TT    OO   OO      JJ           *
*     YY     2      KK KK   GG   GG   TT    OO   OO JJ   JJ           *
*     YY     2      KK  KK  GG   GG   TT    OO   OO JJ   JJ           *
*     YY     22222  KK   KK  GGGGG    TT     OOOOO   JJJJJ            *
*                                                                     *
* ******************************************************************* *
* ******************************************************************* *
*    THIS SUBROUTINE COMPUTES THE JULIAN DATE(YYYYDDD) FOR A GIVEN    *
*    A GREGORIAN DATE (MMDDYYYY)                                      *
*                                                                     *
*    1.  VALID INPUT YEARS FOR THIS ROUTINE ARE THE YEARS 1601        *
*        THROUGH 3399 A. D. (INCLUSIVE)                               *
*                                                                     *
*    2.  VALID INPUT MONTH VALUES FOR THIS ROUTINE ARE 01 THROUGH 12. *
*                                                                     *
*    3.  VALID INPUT DAY VALUES FOR THIS ROUTINE ARE 01 THROUGH 31    *
*        WITH EXCEPTIONS FOR THE MONTHS LISTED:                       *
*             MONTH          MAXIMUM DAY VALUE                        *
*              04                    30                               *
*              06                    30                               *
*              09                    30                               *
*              11                    30                               *
*              02                    28                               *
*              02 DURING LEAP YEAR   29                               *
*                                                                     *
*    4.  THE DATE FORMAT MUST BE MMDDYYYY, WHERE MM = THE 2 DIGIT     *
*        MONTH VALUE, DD = THE 2 DIGIT DAY VALUE, AND YYYY = THE      *
*        4 DIGIT YEAR VALUE.                                          *
*                                                                     *
*    5.  THREE FIELDS ARE PASSED AS PARAMETERS TO THE ROUTINE:        *
*        1)  AN 8 BYTE FIELD CONTAINING THE DATE (IN ZONED-           *
*            DECIMAL FORMAT) FOR WHICH THE JULIAN DAY NUMBER IS       *
*            TO BE CALCULATED,                                        *
*        2)  A 1 BYTE ZONED-DECIMAL RETURN CODE, AND                  *
*        3)  A 7 BYTE FIELD WHICH WILL RECEIVE THE YEAR AND JULIAN    *
*            DAY NUMBER (IN ZONED-DECIMAL FORMAT).                    *
*        THE ROUTINE WILL NOT ALTER THE DATE PASSED TO IT.            *
*                                                                     *
*        SUGGESTED CALLING SYNTAX FOR ALC CALLERS:                    *
*                                                                     *
*                      CALL  Y2KGTOJ,(DATEG)                          *
*                         . . .                                       *
*             DATEG    DS    ZL8'01011997'                            *
*             RC       DS    ZL1'0'                                   *
*             JDATE    DS    ZL7'0'                                   *
*                                                                     *
*        SUGGESTED CALLING SYNTAX FOR COBOL CALLERS:                  *
*                                                                     *
*             01  GTOJ-PARAMETERS.                                    *
*                 02  DATEG PIC 9(8) VALUE 01011997.                  *
*                 02  RC    PIC 9(1) VALUE 0.                         *
*                 02  JDATE PIC 9(7) VALUE 0.                         *
*             CALL 'Y2KGTOJ' USING GTOJ-PARAMETERS.                   *
*                                                                     *
*    6.  THE POSSIBLE VALUES FOR THE RETURN CODE FIELD ARE:           *
*                                                                     *
*        0 INDICATES SUCCESSFUL EXECUTION OF THE ROUTINE.             *
*                                                                     *
*        2 INDICATES INVALID DATA WAS FOUND IN THE DATE FIELD.        *
*        INVALID DATA ARE DETERMINED IF THE FIELD'S LOW ORDER         *
*        BYTE'S ZONE CONTAINS AN INVALID SIGN, (NOT ONE OF            *
*        X'C', X'D' OR X'F'), OR IF THE PRECEDING BYTES' ZONES        *
*        ARE OTHER THAN X'F', OR IF ANY BYTE'S LOW ORDER NIBBLE       *
*        CONTAINS A VALUE GREATER THAN X'9'.                          *
*                                                                     *
*        4 INDICATES THE VALUE OF THE YEAR SPECIFIED WAS NOT IN       *
*        THE RANGE SPECIFIED IN 1 (ABOVE).                            *
*                                                                     *
*        6 INDICATES THE VALUE OF THE MONTH SPECIFIED WAS NOT IN      *
*        THE RANGE SPECIFIED IN 2 (ABOVE).                            *
*                                                                     *
*        8 INDICATES THE VALUE OF THE DAY SPECIFIED WAS NOT IN        *
*        THE RANGE SPECIFIED IN 3 (ABOVE).                            *
*                                                                     *
*    7.  UPON SUCCESSFUL EXECUTION, THE 4 DIGIT YEAR PLUS THE 3       *
*        DIGIT JULIAN DAY NUMBER (YYYYDDD) WILL BE PLACED IN THE      *
*        3RD PARAMETER FIELD (IN ZONED-DECIMAL FORMAT).  IF THE       *
*        RETURN CODE IS A NON-ZERO VALUE (UNSUCCESSFUL EXECUTION),    *
*        THE 3RD PARAMETER FIELD WILL CONTAIN ZEROS.                  *
*                                                                     *
* ******************************************************************* *
* ******************************************************************* *
         EJECT
Y2KGTOJ  CSECT
*         PRINT NOGEN
         EYEC  'CONVERT GREGORIAN DATE TO JULIAN (DAY OF YEAR)'
*
         SAVE  (14,12)                 SAVE CALLER'S REGISTERS
*
         LR    R12,R15                 LOAD BASE WITH ENTRY ADDRESS
         USING Y2KGTOJ,R12             ESTABLISH ADDRESSABILITY
*
         L     R11,0(,R1)              LOAD A(PARAMETERS)
         USING $PARMS,R11              ESTABLISH ADDRESSABILITY
*
         GETMAIN R,LV=$DYNLEN          GETMAIN DYNAMIC WORKAREA
         LR    R10,R1                  SAVE A(MEMORY)
*
         ST    R13,4(,R10)             STORE A(CALLER'S SAVE AREA)
         ST    R10,8(,R13)             BACK CHAIN A(MY SAVE AREA)
         LR    R13,R10                 SET A(MY SAVE AREA)
         USING $DYNMEM,R13             ESTABLISH ADDRESSABILITY
*
*    INSERT EYECATCHER MARKS AT BEGIN/END OF GETMAIN'D AREA.
*
         MVC   TOPFLAG,=C'Y2KGTOJ GETMAIN BEGINS->'
         MVC   BOTFLAG,=C'<-Y2KGTOJ GETMAIN ENDS'
*
*    INITIALIZE RETURN CODE AND JULIAN OUTPUT FIELD.
*
         MVI   PRC,C'0'                ASSUME GOOD RETURN
         MVC   PRC+1(7),PRC            NULL JULIAN DATE
*
*    RETRIEVE ASTRONOMICAL NUMBER FOR DATE PASSED
*
         MVC   DATEG,PDATEG            LOAD DATE TO PASS
         LA    R1,GTOAPARM             ADDRESS OF Y2KGTOA PARMS
         ST    R1,PLIST                STORE IN DYNAMIC AREA
         LA    R1,PLIST                ADDRESS TO PASS
         L     R15,=V(Y2KGTOA)         ADDRESS OF ROUTINE
         BALR  R14,R15                 CALL ROUTINE
*
*    SEE IF DATE WE RECEIVED FROM CALLER PASSED EDITS IN Y2KGTOA.
*    THIS MEANS WE DON'T HAVE TO REPEAT THE EDIT CODE HERE <G>!
*
         MVC   PRC,RCG                 ADOPT RECEIVED RETURN CODE
         CLI   RCG,C'0'                WAS EXECUTION SUCCESSFUL?
         BNE   RETURN                  IF NOT, EXIT TO CALLER
*
*    SAVE THE ASTRONOMICAL NUMBER FOR DAY PASSED
*
         PACK  WORK(4),ANUMG(7)        SAVE ASTRO NUMBER
*
*    RETRIEVE THE ASTRONOMICAL NUMBER FOR JANUARY 1 OF THE SAME YEAR.
*
         MVC   DATEG(4),=C'0101'       SET DATE TO JAN 1
         LA    R1,PLIST                ADDRESS TO PASS
         L     R15,=V(Y2KGTOA)         ADDRESS OF ROUTINE
         BALR  R14,R15                 CALL ROUTINE
*
*    DECREASE BY 1 BECAUSE WE REALLY WANT TO SUBTRACT 12/31 OF THE
*    PRIOR YEAR (IT'S JUST LESS WORK TO DO IT THIS WAY).
*
         PACK  WORK2(4),ANUMG(7)       PACK ASTRO NUMBER
         SP    WORK2,=P'1'             SUBTRACT 1 FROM ASTRO NUMBER
*
*    SUBTRACT THAT NUMBER FROM THE PRIOR ONE.
*
         SP    WORK,WORK2              YIELDS DAY NUMBER
         UNPK  PDATEJ+4(3),WORK+2(2)   UNPACK DAY NUMBER INTO OUTPUT
         OI    PDATEJ+6,X'F0'          CLEAR SIGN
         MVC   PDATEJ(4),PDATEG+4      PREPEND YEAR TO OUTPUT
*
*    RESTORE REGISTERS, FREE STORAGE, AND RETURN
*
RETURN   LR    R1,R13                  LOAD ADDRESS OF GETMAINED AREA
         L     R13,4(,R13)             RELOAD PREVIOUS SAVE AREA
         LA    R0,$DYNLEN              LOAD LENGTH OF GETMAINED AREA
         FREEMAIN R,LV=(0),A=(1)       FREE GETMAINED AREA
         RETURN (14,12),RC=0           RETURN TO CALLER WITH RC=0
*
         LTORG
         EJECT
* ******************************************************************* *
* ***************************** EQUATES ***************************** *
* ******************************************************************* *
         YREGS
* ******************************************************************* *
         EJECT
* ******************************************************************* *
* ***************************** DSECTS ****************************** *
* ******************************************************************* *
$DYNMEM  DSECT                         DYNAMICALLY ALLOCATED MEMORY
SAVEAREA DS    18F                     REGISTER SAVE AREA
TOPFLAG  DS    CL24                    EYECATCHER
PLIST    DS    A                       PARMLIST ADDR FOR SUB CALLS
GTOAPARM DS    0H                      PARAMETERS FOR Y2KGTOA
DATEG    DS    CL8                       INPUT DATE
RCG      DS    CL1                       RETURN CODE
ANUMG    DS    ZL7                       ASTRO NUMBER
WORK     DS    PL4                     WORK FIELD
WORK2    DS    PL4                     WORK FIELD
BOTFLAG  DS    CL22                    EYECATCHER
*
$DYNLEN  EQU   (*-$DYNMEM)             LENGTH OF GETMAIN'D AREA
*
$PARMS   DSECT                         PARAMETERS FROM/TO CALLER
PDATEG   DS    CL8                     GREGORIAN DATE INPUT
PRC      DS    CL1                     RETURN CODE (0/2/4/6/8)
PDATEJ   DS    CL7                     GREGORIAN DATE OUTPUT
* ******************************************************************* *
         END
