/* REXX/370 */
/*

Function:
       Run TSO command, trap command output and view/browse/edit it.
       This procedure primarily designed for use on ISPF C/S(ISPF
       Client/Server) session, where normal TSO command output issued
       via TPUT service didn't displayed. Also may be usefull during
       "normal" ISPF session to run TSO command or CLIST that produces
       large output, like LISTALC, LISTCAT etc. Especially useful with
       TSO HELP.
       Alternative of Mark Zelden's TSOB, TSOV, TSOE.

Syntax:
       TRAP command ¢Browse ¦ View ¦ Edit!

(C) 2006-2018 Gregori Bliznets gbliznets@iba.by

*/
trace off
parse source system . myid . . . . ispf .
if ispf <> 'ISPF'
then do
  say myid' should run under ISPF'
  exit 20
  end
parse arg command
viewer = ''
command = strip(command)
p = lastpos(' ', command)
if p > 0
then do
  lastword = translate(substr(command, p+1))
  select
  when abbrev('BROWSE', lastword, 1)
  then viewer = 'BROWSE'
  when abbrev('VIEW', lastword, 1)
  then viewer = 'VIEW'
  when abbrev('EDIT', lastword, 1)
  then viewer = 'EDIT'
  otherwise nop
  end
  end
if viewer <> ''
then command = substr(command, 1, p-1)
else viewer = 'BROWSE'
address ISPEXEC 'CONTROL ERRORS RETURN'
call outtrap 'responce.'
address TSO command
cc = rc
call outtrap 'OFF'
if cc <> 0
then do
  zerrsm = "Return code "cc
  zerrlm = "Return code from command is "cc
  zerralrm = "YES"
  zerrhm = "*"
  address ISPEXEC "SETMSG MSG(ISRZ002)"
  end
if responce.0 > 0
then do
  address ISPEXEC 'VGET (ZTEMPN ZTEMPF)'
  address TSO 'EXECIO * DISKW 'ztempn' (FINIS STEM RESPONCE.'
  address ISPEXEC viewer" DATASET('"ztempf"')"
exit cc
