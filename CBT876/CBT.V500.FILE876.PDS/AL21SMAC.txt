./ ADD NAME=ALGOL    0201-12230-12230-1200-00056-00056-00000-LEVEL
         MACRO
         ALGOL &PUNCH=NODECK,&TYPERUN=LOAD,&SORCODE=EBCDIC,            C
               &SORLIST=SOURCE,                                        C
               &SIZE=45056,&PRECISN=SHORT
.*
.*       COMPONENT ID - 360S-AL-531 ALGOL F COMPILER
.*
.*       SYSTEM GENERATION OPTIONS MACRO FOR THE COMPILER
.*
         GBLB  &SGALGOB(5)
         GBLA  &SGALGOA(1)
.*
         AIF  ('&PUNCH' NE 'NODECK' AND '&PUNCH' NE 'DECK').E2
         MNOTE *,'   PUNCH OPTION IS &PUNCH'
&SGALGOB(1)  SETB  ('&PUNCH' EQ 'NODECK')
.A2      ANOP
         AIF ('&TYPERUN' NE 'LOAD' AND '&TYPERUN' NE 'NOLOAD').E3
         MNOTE *,'   TYPERUN OPTION IS &TYPERUN'
&SGALGOB(2)  SETB  ('&TYPERUN' EQ 'NOLOAD')
.A3      ANOP
         AIF ('&SORCODE' NE 'EBCDIC' AND '&SORCODE' NE 'ISO').E4
         MNOTE *,'   SORCODE OPTION IS &SORCODE'
&SGALGOB(3)  SETB  ('&SORCODE' EQ 'ISO')
.A4      ANOP
         AIF ('&SORLIST' NE 'SOURCE' AND '&SORLIST' NE 'NOSOURCE').E5
         MNOTE *,'   SORLIST OPTION IS &SORLIST'
&SGALGOB(4)  SETB  ('&SORLIST' EQ 'NOSOURCE')
.A5      ANOP
         AIF (&SIZE LT 45056 OR &SIZE GT 999999 OR T'&SIZE NE 'N').E6
         MNOTE *,'   SIZE SPECIFIED IS &SIZE BYTES OF MAIN STORAGE'
&SGALGOA(1)  SETA  &SIZE
.A6      ANOP
         AIF ('&PRECISN' NE 'SHORT' AND '&PRECISN' NE 'LONG').E7
         MNOTE *,'   PRECISN OPTION IS &PRECISN'
&SGALGOB(5)  SETB  ('&PRECISN' EQ 'LONG')
.A7      ANOP
         MEXIT
.E2      MNOTE 8,'* * * IEIALG102 PUNCH VALUE &PUNCH INVALID'
.*
         AGO   .A2
.E3      MNOTE 8,'* * * IEIALG103 TYPERUN VALUE &TYPERUN INVALID'
.*
         AGO   .A3
.E4      MNOTE 8,'* * * IEIALG104 SORCODE VALUE &SORCODE INVALID'
.*
         AGO   .A4
.E5      MNOTE 8,'* * * IEIALG105 SORLIST VALUE &SORLIST INVALID'
.*
         AGO   .A5
.E6      MNOTE 8,'* * * IEIALG106 SIZE VALUE &SIZE INVALID'
.*
         AGO   .A6
.E7      MNOTE 8,'* * * IEIALG107 PRECISN VALUE &PRECISN INVALID'
.*
         AGO   .A7
         MEND
./ ADD NAME=IEXCGEN  0201-12230-12230-1200-00345-00345-00000-LEVEL
         MACRO
&LABEL   IEXCGEN &GENTYPE,&STRING
.*
.*       COMPONENT ID - 360S-AL-531 ALGOL F COMPILER
.*
.*       LEVEL 2.1 -
.*       THIS IS A NEW MACRO REQUIRED AS PART OF THE
.*       REFURBISHMENT OF THE LAST IBM RELEASE OF
.*       THE ALGOL F COMPILER
.*
.*
.*       FUNCTION -
.*       IEXCGEN CONVERTS THE EBCDIC STRING PROVIDED IN THE
.*       &STRING PARAMETER INTO THE INTERNAL CODE USED BY THE
.*       ALGOL F COMPILER FOR SOURCE PROGRAM SCANNING
.*
.*       THE GENTYPE PARAMETER ACCEPTS EQU TO GENERATE THE
.*       CONVERTED STRING AS AN EQUATE OR IT ACCEPTS DC TO
.*       GENERATE THE CONVERTED STRING INTO A DC OF A HEX
.*       STRING
.*
.*       NOTES -
.*       1. THIS MACRO WAS DESIGNED TO EXECUTE IN THE
.*          OS/360 ASSEMBLER IEUASM ENVIRONMENT SO THAT THE
.*          ALGOL F SOURCE CODE CAN BE BUILT TO MATCH
.*          THE OS/360 GENERATED ALGOL F OBJECT MODULES
.*          USE OF THE VARIOUS ENHANCEMENTS PROVIDED BY
.*          HLASM WAS THEREFORE PRECLUDED
.*
.*
.*       DECLARE LCLS
.*
         LCLA  &I                      OUTER LOOP VARIABLE
         LCLA  &J                      INNER LOOP VARIABLE
         LCLA  &OUT                    OUTPUT CHAR POINTER
         LCLA  &TABLEN                 NUMBER OF CHARS IN TRANSLATE TAB
         LCLA  &STRLEN                 STRING LENGTH
         LCLB  &SUPCHAR                FLAG TO SUPRESS NEXT SPEC CHAR
         LCLC  &CEB(91)                TABLE OF EBCDIC CHARACTERS
         LCLC  &CIC(91)                INTERNAL CODE EQUIVALENT OF
.*                                     THE EBCDIC CHARACTER
         LCLC  &X(40)                  RESULT OF TRANSLATION TO
.*                                     INTERNAL CODE
         LCLC  &BAD                    DISPLAY AN INVALID CHAR
         AIF   ('&GENTYPE' EQ 'EQU').GENOK
.*
.*       VALIDATE GENTYPE
.*
         AIF   ('&GENTYPE' EQ 'DC').GENOK
.*
.*       GENTYPE INVALID
.*
         MNOTE 12,'ERROR - GENTYPE REQUESTED &GENTYPE NOT DC OR EQU'
         AGO   .EXIT
.*
.*       VALIDATE STRING
.*
.GENOK   ANOP
         AIF   ('&STRING' NE '').STROK1
         MNOTE 12,'ERROR - NULL STRING FOR CONVERSION'
         AGO   .EXIT
.STROK1  ANOP
&STRLEN  SETA  K'&STRING
         AIF   ('&STRING'(1,1) EQ '''').STROK2
         AIF   ('&STRING'(1,&STRLEN) EQ '''').STROK2
         MNOTE 12,'ERROR - STRING NOT ENCLOSED IN QUOTES'
         AGO   .EXIT
.STROK2  ANOP
.*
.*       INITIALIZE STRINGS WITH EBCDIC CHAR AND TRANSLATED
.*       ALGOL F INTERNAL HEX VALUE
.*
.*       CHARACTERS A - Z
.*
&CEB(1)  SETC  'A'
&CIC(1)  SETC  '40'
&CEB(2)  SETC  'B'
&CIC(2)  SETC  '41'
&CEB(3)  SETC  'C'
&CIC(3)  SETC  '42'
&CEB(4)  SETC  'D'
&CIC(4)  SETC  '43'
&CEB(5)  SETC  'E'
&CIC(5)  SETC  '44'
&CEB(6)  SETC  'F'
&CIC(6)  SETC  '45'
&CEB(7)  SETC  'G'
&CIC(7)  SETC  '46'
&CEB(8)  SETC  'H'
&CIC(8)  SETC  '47'
&CEB(9)  SETC  'I'
&CIC(9)  SETC  '48'
&CEB(10) SETC  'J'
&CIC(10) SETC  '49'
&CEB(11) SETC  'K'
&CIC(11) SETC  '4A'
&CEB(12) SETC  'L'
&CIC(12) SETC  '4B'
&CEB(13) SETC  'M'
&CIC(13) SETC  '4C'
&CEB(14) SETC  'N'
&CIC(14) SETC  '4D'
&CEB(15) SETC  'O'
&CIC(15) SETC  '4E'
&CEB(16) SETC  'P'
&CIC(16) SETC  '4F'
&CEB(17) SETC  'Q'
&CIC(17) SETC  '50'
&CEB(18) SETC  'R'
&CIC(18) SETC  '51'
&CEB(19) SETC  'S'
&CIC(19) SETC  '52'
&CEB(20) SETC  'T'
&CIC(20) SETC  '53'
&CEB(21) SETC  'U'
&CIC(21) SETC  '54'
&CEB(22) SETC  'V'
&CIC(22) SETC  '55'
&CEB(23) SETC  'W'
&CIC(23) SETC  '56'
&CEB(24) SETC  'X'
&CIC(24) SETC  '57'
&CEB(25) SETC  'Y'
&CIC(25) SETC  '58'
&CEB(26) SETC  'Z'
&CIC(26) SETC  '59'
&CEB(27) SETC  '~'                     UNRECOGNIZED CHAR
&CIC(27) SETC  '2C'
.*
.*       NUMERIC 0 - 9
.*
&CEB(28) SETC  '0'
&CIC(28) SETC  '30'
&CEB(29) SETC  '1'
&CIC(29) SETC  '31'
&CEB(30) SETC  '2'
&CIC(30) SETC  '32'
&CEB(31) SETC  '3'
&CIC(31) SETC  '33'
&CEB(32) SETC  '4'
&CIC(32) SETC  '34'
&CEB(33) SETC  '5'
&CIC(33) SETC  '35'
&CEB(34) SETC  '6'
&CIC(34) SETC  '36'
&CEB(35) SETC  '7'
&CIC(35) SETC  '37'
&CEB(36) SETC  '8'
&CIC(36) SETC  '38'
&CEB(37) SETC  '9'
&CIC(37) SETC  '39'
.*
.*       SPECIAL CHARS
.*
&CEB(38) SETC  ' '
&CIC(38) SETC  '2B'
&CEB(39) SETC  '.'
&CIC(39) SETC  '2D'
&CEB(40) SETC  '<'
&CIC(40) SETC  '11'
&CEB(41) SETC  '('
&CIC(41) SETC  '06'
&CEB(42) SETC  '+'
&CIC(42) SETC  '00'
&CEB(43) SETC  '|'
&CIC(43) SETC  '22'
&CEB(44) SETC  '&&'                    DO NOT CHANGE THIS CHARS POS
&CIC(44) SETC  '23'                    IN THE TAB SEE LOOPS BELOW
&CEB(45) SETC  '!'
&CIC(45) SETC  '2C'                    ##
&CEB(46) SETC  '$'
&CIC(46) SETC  '5A'                    ##
&CEB(47) SETC  '*'
&CIC(47) SETC  '02'
&CEB(48) SETC  ')'
&CIC(48) SETC  '26'
&CEB(49) SETC  ';'
&CIC(49) SETC  '0B'
&CEB(50) SETC  '¬'
&CIC(50) SETC  '20'
&CEB(51) SETC  '-'
&CIC(51) SETC  '01'
&CEB(52) SETC  '/'
&CIC(52) SETC  '03'
&CEB(53) SETC  ','
&CIC(53) SETC  '25'
&CEB(54) SETC  '%'
&CIC(54) SETC  '2C'
&CEB(55) SETC  '_'
&CIC(55) SETC  '5B'
&CEB(56) SETC  '>'
&CIC(56) SETC  '12'
&CEB(57) SETC  'Ý'
&CIC(57) SETC  '08'
&CEB(58) SETC  ':'
&CIC(58) SETC  '07'
&CEB(59) SETC  '#'
&CIC(59) SETC  '5C'
&CEB(60) SETC  '@'
&CIC(60) SETC  '5D'
&CEB(61) SETC  ''''                    DO NOT CHANGE THIS CHARS POS
&CIC(61) SETC  '2E'                    IN THE TAB SEE LOOPS BELOW
&CEB(62) SETC  '='
&CIC(62) SETC  '10'
&CEB(63) SETC  '"'
&CIC(63) SETC  '0C'
&CEB(64) SETC  '¨'
&CIC(64) SETC  '28'
.*
.*       LOWER CASE CHARACTERS A - Z
.*
&CEB(65) SETC  'a'
&CIC(65) SETC  '40'
&CEB(66) SETC  'b'
&CIC(66) SETC  '41'
&CEB(67) SETC  'c'
&CIC(67) SETC  '42'
&CEB(68) SETC  'd'
&CIC(68) SETC  '43'
&CEB(69) SETC  'e'
&CIC(69) SETC  '44'
&CEB(70) SETC  'f'
&CIC(70) SETC  '45'
&CEB(71) SETC  'g'
&CIC(71) SETC  '46'
&CEB(72) SETC  'h'
&CIC(72) SETC  '47'
&CEB(73) SETC  'i'
&CIC(73) SETC  '48'
&CEB(74) SETC  'j'
&CIC(74) SETC  '49'
&CEB(75) SETC  'k'
&CIC(75) SETC  '4A'
&CEB(76) SETC  'l'
&CIC(76) SETC  '4B'
&CEB(77) SETC  'm'
&CIC(77) SETC  '4C'
&CEB(78) SETC  'n'
&CIC(78) SETC  '4D'
&CEB(79) SETC  'o'
&CIC(79) SETC  '4E'
&CEB(80) SETC  'p'
&CIC(80) SETC  '4F'
&CEB(81) SETC  'q'
&CIC(81) SETC  '50'
&CEB(82) SETC  'r'
&CIC(82) SETC  '51'
&CEB(83) SETC  's'
&CIC(83) SETC  '52'
&CEB(84) SETC  't'
&CIC(84) SETC  '53'
&CEB(85) SETC  'u'
&CIC(85) SETC  '54'
&CEB(86) SETC  'v'
&CIC(86) SETC  '55'
&CEB(87) SETC  'w'
&CIC(87) SETC  '56'
&CEB(88) SETC  'x'
&CIC(88) SETC  '57'
&CEB(89) SETC  'y'
&CIC(89) SETC  '58'
&CEB(90) SETC  'z'
&CIC(90) SETC  '59'
.*
&TABLEN  SETA  90                      MAXIMUM CHARS IN TAB
.*
.*       INITIALIZE LOOP VARIABLES
.*
&STRLEN  SETA  &STRLEN-1               ALLOW FOR ENCLOSING QUOTES
&I       SETA  1                       BYPASS FIRST QUOTE
&OUT     SETA  1                       OUTPUT CHAR POINTER
.*
.*       OUTER LOOP THROUGH STRING TO PROCESS EACH CHAR
.*
.LOOP    ANOP
&I       SETA  &I+1                    SELECT NEXT CHAR
&J       SETA  0                       INIT INNER LOOP VARIABLE
.*
.*       INNER LOOP THROUGH TABLE TO OBTAIN TRANSLATED VALUE
.*
.INLOOP  ANOP
&J       SETA  &J+1
         AIF   (&J LE &TABLEN).TESTC  SEARCHED THE TABLE ?
&BAD     SETC  '&STRING'(&I,1)
         MNOTE 12,'ERROR - INVALID CHARACTER FOR TRANSLATION |&BAD| '
         AGO   .EXIT
.TESTC   ANOP
.*       MNOTE *,'I VALUE = &I'       DEBUGGING
.*       MNOTE *,'J VALUE = &J'
         AIF   ('&STRING'(&I,1) NE '&CEB(&J)'(1,1)).INLOOP
.*
.*       FOUND A MATCH
.*
.*       IS THE CHAR A QUOTE OR AMPERSAND ?
.*       USE VALUE IN COMPARE TABLE TO AVOID ASSEMBLER SPECIAL CHAR
.*       ISSUES
.*
         AIF   (&J EQ 44 OR &J EQ 61).SCHAR  FOR SPEC CHAR GOTO SCHAR
&SUPCHAR SETB  0                       TURN OFF SUPRESS CHAR FLAG
         AGO   .ASSIGN                 AND ASSIGN TRANSLATED VALUE
.SCHAR   ANOP                          EITHER A QUOTE OR AMPERSAND
         AIF   (&SUPCHAR).SCHARA       SPECIAL CHAR FOUND PREV ? SCHARA
&SUPCHAR SETB  1                       SUPRESS NEXT SPECIAL CHAR
         AGO   .ASSIGN                 ASSIGN THIS ONE
.SCHARA  ANOP
&SUPCHAR SETB  0                       TURN OFF SUPRESS CHAR
         AGO   .SCHARB                 DO NOT ASSIGN CHAR
.ASSIGN  ANOP
&X(&OUT) SETC  '&CIC(&J)'              MOVE TRANSLATED HEX CHARS
&OUT     SETA  &OUT+1                  INCREMENT OUTPUT COUNTER
.SCHARB  ANOP
         AIF   (&I LT &STRLEN).LOOP    PROCESS NEXT CHAR
.*
.*       ALL CHARS PROCESSED
.*
.*       GENERATE REQUESTED TRANSLATED HEX STRING
.*
         AIF   ('&GENTYPE' EQ 'EQU').GENEQU
.*
.*       DC REQUESTED
.*
.*       NOTE ONLY TWO CONTINUATION RECORDS ALLOWED FOR IEUASM AND
.*       IFOX00
.*
&LABEL   DC    X'&X(1).&X(2).&X(3).&X(4).&X(5).&X(6).&X(7).&X(8).&X(9).C
               &X(10).&X(11).&X(12).&X(13).&X(14).&X(15).&X(16).&X(17).C
               &X(18).&X(19).&X(20).&X(21).&X(22).&X(23).&X(24)'
         AIF   ('&X(25)' EQ '').EXIT    NO NEED FOR ADDITIONAL DC
         DC    X'&X(25).&X(26).&X(27).&X(28).&X(29).&X(30).&X(31).&X(32C
               ).&X(33).&X(34).&X(35).&X(36).&X(37).&X(38).&X(39)'
         AGO   .EXIT
.*
.*       EQU REQUESTED
.*
.GENEQU  ANOP
.*       EQUATES LIMITED TO 24 CHARS
&LABEL   EQU   X'&X(1).&X(2).&X(3).&X(4).&X(5).&X(6).&X(7).&X(8).&X(9).C
               &X(10).&X(11).&X(12).&X(13).&X(14).&X(15).&X(16).&X(17).C
               &X(18).&X(19).&X(20).&X(21).&X(22).&X(23).&X(24)'
         AGO   .EXIT
.*
.*       EXIT MACRO
.*
.EXIT    ANOP
         MEND
./ ADD NAME=IEXCHAR  0201-12230-12230-1200-00126-00126-00000-LEVEL
         MACRO
&LABEL   IEXCHAR
.*
.*       COMPONENT ID - 360S-AL-531 ALGOL F COMPILER
.*
.*       LEVEL 2.1 -
.*       THIS IS A NEW MACRO REQUIRED AS PART OF THE
.*       REFURBISHMENT OF THE LAST IBM RELEASE OF
.*       THE ALGOL F COMPILER
.*
.*       FUNCTION -
.*       IEXCHAR GENERATES EQUATES FOR ALL THE CHARACTERS USED
.*       IN THE TEXT SCANNING ROUTINES AFTER THE TEXT HAS BEEN
.*       TRANSLATED TO ALGOL INTERNAL CODE
.*
.*       NOTES -
.*       1. THIS MACRO WAS DESIGNED TO EXECUTE IN THE
.*          OS/360 ASSEMBLER IEUASM ENVIRONMENT SO THAT THE
.*          ALGOL F SOURCE CODE CAN BE BUILT TO MATCH
.*          THE OS/360 GENERATED ALGOL F OBJECT MODULES
.*          USE OF THE VARIOUS ENHANCEMENTS PROVIDED BY
.*          HLASM WAS THEREFORE PRECLUDED
.*
.*       DECLARE LCLS
.*
         LCLC  &XF                     PREFIX TO ASSIGNED NAME
&XF      SETC  'XF'
.*
.*       GENERATE EQUATES FOR THE INTERNAL CHARACTER SET AND
.*       CONTROL CODES
.*
*
*        CHARACTER A - Z
*
&XF.A    IEXCGEN EQU,'A'
&XF.B    IEXCGEN EQU,'B'
&XF.C    IEXCGEN EQU,'C'
&XF.D    IEXCGEN EQU,'D'
&XF.E    IEXCGEN EQU,'E'
&XF.F    IEXCGEN EQU,'F'
&XF.G    IEXCGEN EQU,'G'
&XF.H    IEXCGEN EQU,'H'
&XF.I    IEXCGEN EQU,'I'
&XF.J    IEXCGEN EQU,'J'
&XF.K    IEXCGEN EQU,'K'
&XF.L    IEXCGEN EQU,'L'
&XF.M    IEXCGEN EQU,'M'
&XF.N    IEXCGEN EQU,'N'
&XF.O    IEXCGEN EQU,'O'
&XF.P    IEXCGEN EQU,'P'
&XF.Q    IEXCGEN EQU,'Q'
&XF.R    IEXCGEN EQU,'R'
&XF.S    IEXCGEN EQU,'S'
&XF.T    IEXCGEN EQU,'T'
&XF.U    IEXCGEN EQU,'U'
&XF.V    IEXCGEN EQU,'V'
&XF.W    IEXCGEN EQU,'W'
&XF.X    IEXCGEN EQU,'X'
&XF.Y    IEXCGEN EQU,'Y'
&XF.Z    IEXCGEN EQU,'Z'
*
*        NATIONAL CHARACTERS
*
&XF.DOLLAR IEXCGEN EQU,'$'
&XF.UNDER  IEXCGEN EQU,'_'
&XF.HASH   IEXCGEN EQU,'#'
&XF.AT     IEXCGEN EQU,'@'
*
*        NUMERIC 0 - 9
*
&XF.0    IEXCGEN EQU,'0'
&XF.1    IEXCGEN EQU,'1'
&XF.2    IEXCGEN EQU,'2'
&XF.3    IEXCGEN EQU,'3'
&XF.4    IEXCGEN EQU,'4'
&XF.5    IEXCGEN EQU,'5'
&XF.6    IEXCGEN EQU,'6'
&XF.7    IEXCGEN EQU,'7'
&XF.8    IEXCGEN EQU,'8'
&XF.9    IEXCGEN EQU,'9'
*
*        SPECIAL CHARS
*
&XF.PLUS   IEXCGEN EQU,'+'
&XF.MINUS  IEXCGEN EQU,'-'
&XF.ASTER  IEXCGEN EQU,'*'
&XF.SLASH  IEXCGEN EQU,'/'
&XF.LBRAC  IEXCGEN EQU,'('
&XF.COLON  IEXCGEN EQU,':'
&XF.LSQBR  IEXCGEN EQU,'Ý'
&XF.SCOLON IEXCGEN EQU,';'
&XF.EQUAL  IEXCGEN EQU,'='
&XF.LT     IEXCGEN EQU,'<'
&XF.GT     IEXCGEN EQU,'>'
&XF.NOT    IEXCGEN EQU,'¬'
&XF.OR     IEXCGEN EQU,'|'
&XF.AMPER  IEXCGEN EQU,'&&'
&XF.COMMA  IEXCGEN EQU,','
&XF.RBRAC  IEXCGEN EQU,')'
&XF.RSQBR  IEXCGEN EQU,'¨'
&XF.BLANK  IEXCGEN EQU,' '
&XF.PERIOD IEXCGEN EQU,'.'
&XF.QUOTE  IEXCGEN EQU,''''
*
&XF.DQUOTE IEXCGEN EQU,'"'
*
&XF.EXCLM  IEXCGEN EQU,'!'
&XF.PERCT  IEXCGEN EQU,'%'
*
*        INTERNAL CONTROL CODES
*
&XF.POWER  EQU   X'05'
&XF.ASSIGN EQU   X'16'
&XF.GOTO   EQU   X'17'
&XF.FOR    EQU   X'18'
&XF.IF     EQU   X'1D'
&XF.LABEL  EQU   X'27'
&XF.DELTA  EQU   X'29'
&XF.END    EQU   X'2C'
&XF.ZETA   EQU   X'2F'
&XF.DECPT  EQU   X'3E'
.*
.*       EXIT MACRO
.*
.EXIT    ANOP
         MEND
./ ADD NAME=IEXENTRY 0201-12230-12230-1200-00055-00055-00000-LEVEL
         MACRO
&NAME    IEXENTRY  &REG=R15,&ID
.*
.*       IEXENTRY
.*
.*       COMPONENT ID - 360S-AL-531 ALGOL F COMPILER
.*
.*       GENERATE A MODULE EYE CATCHER OF THE SAME FORMAT AS THE
.*       SAVE MACRO BUT DO NOT SAVE REGISTERS AS THE ALGOL COMPILER
.*       OFTEN DOES NOT PROVIDE FOR THE SAVING OF REGS IN THE
.*       STANDARD MANNER AND FOR STANDARD MODULE ENTRY
.*       THE BRANCH REGISTER MAY BE SET TO ANOTHER REGISTER IF
.*       R15 IS NOT THE ENTRY POINT REGISTER BY USE OF THE
.*       REG PARAMETER
.*
         LCLA  &A,&B,&C
         LCLC  &E,&F,&G,&H
         AIF   ('&ID' EQ '').NULLID
         AIF   ('&ID' EQ '*').SPECID
         AIF   ('&ID'(1,1) NE '''').NOQUOTE
&B       SETA  1
&C       SETA  2
.NOQUOTE ANOP
&A       SETA  ((K'&ID+2)/2)*2+4-&C
*
&NAME    B     &A.(,&REG)          BRANCH AROUND ID
&A       SETA  K'&ID-&C
         DC    AL1(&A)             LENGTH OF IDENTIFIER
&E       SETC  '&ID'(&B+1,&A)
         DC    CL&A'&E'                          IDENTIFIER
         AGO   .CONTA
.NULLID  ANOP
&NAME    DS    0H
         AGO   .CONTA
.SPECID  AIF   ('&NAME' EQ '').CSECTN
&E       SETC  '&NAME'
&A       SETA  1
.CONTQ   AIF   ('&E'(1,&A) EQ '&E').LEAVE
&A       SETA  &A+1
         AGO   .CONTQ
.LEAVE   ANOP
&B       SETA  ((&A+2)/2)*2+4
*
&NAME    B     &B.(,&REG)          BRANCH AROUND ID
         DC    AL1(&A)
         DC    CL&A'&E'            IDENTIFIER
         AGO   .CONTA
.CSECTN  AIF   ('&SYSECT' EQ '').E4
&E       SETC  '&SYSECT'
&A       SETA  1
         AGO   .CONTQ
.E4      IHBERMAC 78,360                    CSECT NAME NULL
.CONTA   ANOP
         MEXIT
         MEND
./ ADD NAME=SGIEX001 0201-12230-12230-1200-00328-00328-00000-LEVEL
         MACRO
         SGIEX001 &PRC,&LST,&TRN,&PUN,&SCD,&SZE
.*
.*       COMPONENT ID - 360S-AL-531 ALGOL F COMPILER
.*
.*       SYSTEM GENERATION OPTIONS MACRO
.*
         GBLB  &SGALGOB(5)
         GBLA  &SGALGOA(1)
.*
         TITLE 'IEX00001 - COMPILER COMMON WORKAREA, ALGOL F'
*
*        COMPONENT ID - 360S-AL-531 ALGOL F COMPILER
*
*        FUNCTION/OPERATION -
*        THIS MODULE IS A COLLECTION OF DATA AND WORKING STORAGE
*        WHICH MUST BE READILY ACCESSIBLE TO MORE THAN ONE
*        COMPILER PHASE
*
*        R13 -> THIS MODULE DURING THE ENTIRE COMPILATION
*
*        ENTRY POINTS - N/A
*
*        INPUT - N/A
*
*        OUTPUT - N/A
*
*        EXTERNAL ROUTINES - N/A
*
*        EXITS - NORMAL - N/A
*
*        EXITS - ERROR - N/A
*
*        TABLES/WORK AREAS -
*        THE FOLLOWING AREAS ARE PERMANENTLY ASSIGNED DURING THE
*        WHOLE COMPILATION -
*        1. SAVEAREA, REGISTER SAVEAREA IN THE STANDARD FORMAT
*        2. DCBTABLE, DCB ADDRS FOR ALL DATASETS USED
*        3. STARTING AT EODUT1, END OF DATA EXIT ADDRESSES FOR
*           SYSUT1, SYSUT2, SYSUT3 AND SYSIN
*        4. COMPFLGS, FOUR BYTES WITH VARIOUS SWITCHES AND
*           INDICATORS FOR CONTROLLING THE PROGRAM FLOW
*
*           ALLOCATION OF THE BIT POSITIONS IN COMPFLGS -
*
*           PURPOSE                   POSITION
*                                     BYTE 1    BYTE 2    BYTE 3
*                                     01234567  01234567  01234567
*
*           COMPMODE (SYNTAX CHECK)   X
*           SUBSCRIPT OPTIMIZATION     X
*           WARNING ERROR               X
*           SERIOUS ERROR                X
*           TERMINATING ERROR             X
*           PROCEDURE/PROGRAM              X
*           LONG/SHORT PRECISION            X
*           OPERAND                          X
*
*           NOSOURCE/SOURCE                     X
*           NOLOAD/LOAD                          X
*           NODECK/DECK                           X
*           ISO/EBCDIC                             X
*           PROGRAM INTERRUPT                       X
*           TERMINATING PHASE ENTERED                X
*           NO BUFFERS ASSIGNED                       X
*           NO COMPILATION POSSIBLE                    X
*
*           SYSPRINT DOWN                                 X
*           WHOLE SOURCE PROG IN CORE                      X
*           NO OPTAB                                        X
*           SYSPRINT NOT OPENED                              X
*           ERROR UNRELATED TO SEMICOLON NR                   X
*           NOTEST/TEST (SC COUNT IN CODE, NOT SYSGEN OPT)     X
*           60 CHARACTER SET                                    X
*           (RESERVED)                                           X
*
*        5. STARTING AT SIZE, MISC CONTROL INFORMATION
*        6. STARTING AT ERRPOOL, ADDR POINTERS TO THE ERROR
*           POOL AND THE SOURCE PROGRAM BUFFER 1
*        7. STARTING AT INBLKS, A TABLE OF THE LENGTHS OF I/O
*           BUFFERS AND OTHER AREAS
*        8. PAGEHEAD, THREE HEADLINES TO BE PRINTED AT THE TOP
*           OF EVERY PAGE
*
*        THE FOLLOWING AREAS ARE USED ONLY DURING CERTAIN PHASES
*        OF THE COMPILATION, AS INDICATED IN THE ASSEMBLY
*        LISTING. FOR THIS REASON, SOME OF THEM OVERLAY EACH
*        OTHER, AND ANY SPACE NOT RESERVED DURING A CERTAIN PHASE
*        MAY BE USED AS WORKING SPACE BY THAT PHASE
*
*        9.  PRELPOOL, A PRELIMINARY ERROR POOL USED DURING THE
*            FIRST PART OF THE INITIALIZATION PHASE
*        10. DCB FOR SYSIN
*        11. PBTAB2, PBTAB1, FSTAB, TABLES USED BY CERTAIN
*            PHASES
*        12. DCB FOR SYSUT1
*        13. SPTAB, GPTAB, TABLES USED BY SCAN I/II AND SCAN
*            III
*
*        THE REMAINING SPACE UP TO 4K IS USED AS WORKING
*        STORAGE
*
*        NOTES -
*        THIS MODULE IS ASSEMBLED AT SYSTEM GENERATION TIME THE
*        OPTION SWITCHES IN COMPFLGS ARE INITIALIZED TO REFLECT
*        THE DESIRED DEFAULT OPTIONS. THE ASSEMBLED MODULE IS
*        LINKAGE EDITED WITH THE MODULE IEX00000 TO FORM THE
*        RESIDENT PART OF THE COMPILER DIRECTORY
*
*        THIS MODULE IS MAPPED BY THE DSECT WORKAREA. ANY CHANGES
*        MUST ALSO BE REFLECTED IN THE WORKAREA DSECT
*
IEX00001 CSECT
*
         EXTRN IEX00SYN,IEX00ED1,IEX00EDI
*
SAVEAREA DC    18F'0'
*
*        DCB ADDRS
*
DCBTABLE DC    0F'0'
ALINDCB  DC    V(IEX00LIN)         SYSLIN
         DC    A(0)                NOT USED
         DC    A(0)                   ..
         DC    A(0)                   ..
ASYSDCB  DC    A(SYSIN)            SYSIN DCB RESIDENT IN THIS MODULE
APRTDCB  DC    V(IEX00PRT)         SYSPRINT
APCHDCB  DC    V(IEX00PCH)         SYSPUNCH
AUT1DCB  DC    A(SYSUT1)           SYSUT1 DCB RESIDENT IN THIS MODULE
AUT2DCB  DC    V(IEX00UT2)         SYSUT2
AUT3DCB  DC    V(IEX00UT3)         SYSUT3
*
*        END OF DATA EXIT ADDRS
*
EODUT1   DC    A(0)                SYSUT1
EODUT2   DC    A(0)                SYSUT2
EODUT3   DC    A(0)                SYSUT3
EODIN    DC    A(0)                SYSIN
*
*        OPTION SWITCHES IN COMPFLGS SET BY SYSGEN OPTIONS
*
COMPFLGS DC    BL2'&PRC.0&LST&TRN&PUN&SCD.0010',BL2'0'
*
*        OPTION SWITCHES IN COMPFLGS
*
PGR      EQU   X'FB'
PROC     EQU   X'04'
*
SHRT     EQU   X'FD'
LNG      EQU   X'02'
*
OPERAND  EQU   X'01'
COMPMODE EQU   X'80'
*
*        ERROR SEVERITY INDICATORS IN COMPFLGS
*
WERR     EQU   X'20'                   WARNING ERROR
SERR     EQU   X'10'                   SERIOUS ERROR
TERR     EQU   X'08'                   TERMINATING ERROR
*
*        OPTION SWITCHES IN COMPFLGS+1
*
SRCE     EQU   X'7F'
NSRCE    EQU   X'80'
*
LOAD     EQU   X'BF'
NLOAD    EQU   X'40'
*
DECK     EQU   X'DF'
NDECK    EQU   X'20'
*
EBCDIC   EQU   X'EF'
ISO      EQU   X'10'
*
*        TERMINATION SWITCHES IN COMPFLGS+1
*
ERR      EQU   X'08'     PROG INTERRUPTION HAS OCCURED IN COMPILER
TERM     EQU   X'04'     LAST PHASE HAS BEEN ENTERED
NOBUF    EQU   X'02'     ERROR POOL IS IN WORKAREA, NO SCE PROG BUFF 1
NOGO     EQU   X'01'     COMPILATION IS IMPOSSIBLE, DO NOT START SCAN 1
NOBUNOGO EQU   X'03'     NOBUF AND NOGO
*
*        SWITCES IN COMPFLGS+2
*
PRT      EQU   X'80'                   SYSPRINT IS DOWN
SPIC     EQU   X'40'                   SOURCE PROG IN STORAGE
NOPT     EQU   X'20'                   NO OPTAB
PRTNO    EQU   X'10'                   SYSPRINT NOT OPENED
NOSC     EQU   X'08'                   SEMICOLON COUNTER NOT VALID
NOTEST   EQU   X'04'
TEST     EQU   X'FB'                   EMBED SC COUNT IN CODE (DEFAULT)
SET60    EQU   X'02'                   60 CHARACTER SET IS TO BE USED
*
*        MISCELLANEOUS CONTROL INFORMATION
*
SIZE     DC    F'&SZE'             AVAILABLE MAIN STORAGE - NOT USED
PICAADD  DC    V(IEX00PIC)         ADDR OF PICA OF THE COMPILER
HDING    DC    F'0'                ADDR OF HEADING INFO OF THE INVOKER
ERET     DC    F'0'                RETURN ADDR FOR PROGRAM
*                                  AND I/O ERRORS
PAGECNT  DC    PL4'0'              PAGE COUNT
LINCNT   DC    H'0'                COUNTER OF LINES PER PAGE
MAXLINES DC    H'56'               MAX NUMBER OF PRINT LINES PER PAGE
SEMCNT   DC    H'0'                SEMICOLON COUNTER
PBN      DC    H'50'               HIGHEST PROGRAM BLOCK NUMBER
KBN      DC    H'0'                HIGHEST CONSTANT POOL NUMBER
LATNR    EQU   28                  NR OF LIBRARY STAND FUNCTIONS
LATBEG   EQU   4*(LATNR-1)
LN       DC    AL2(LATBEG)         LAST USED DISPLACEMENT IN LAT
PRPT     DC    F'0'                PROGRAM POINTER
SAVOUTA  DC    F'0'
OUTAREA2 DC    F'0'                SYSPUNCH SAVE AREA
PIDENT   DC    CL4' '              PROGRAM IDENTIFICATION
CARDCNT  DC    PL4'0'              OBJECT PROGRAM DECK SEQ NUMBER
PRTRTADD DC    V(IEX00PRI)         ADDR OF PRINT ROUTINE
*
*        ADDRS OF AREAS WHICH ARE USED BY MORE THAN A SINGLE PHASE
*
ERRPOOL  DC    A(PRELPOOL)         FIRST BYTE OF PRELIMINARY ERROR POOL
NEXTERR  DC    A(PRELPOOL)         NEXT FREE PLACE IN ERROR POOL
ENDPOOL  DC    A(0)                LAST BYTE OF ERROR POOL-23
SRCE1ADD DC    A(0)                SOURCE PROGRAM BUFFER 1
SRCE1END DC    A(0)                ADDR OF LAST BYTE+1
SULTSTRT DC    F'0'                IDENT OF LAST ITAB RECORD
*
*        TABLE OF THE LENGTHS OF VARIABLE SIZE AREAS
*
INBLKS   DC    H'3200'             MAX BLKSIZE FOR SYSIN    - NOT USED
PRTBLKS  DC    H'3640'             MAX BLKSIZE SYSPRINT     - NOT USED
LINBLKS  DC    H'3200'             MAX BLKSIZE FOR SYSLIN   - NOT USED
PCHBLKS  DC    H'3200'             MAX BLKSIZE FOR SYSPUNCH - NOT USED
POOLS    DC    F'2000'             SIZE OF ERROR POOL
SRCE1S   DC    F'2000'             SIZE OF SOURCE PROG BUFFERS 1 AND 2
SRCE3S   EQU   SRCE1S              SIZE OF SOURCE PROG BUFFERS 3 AND 4
ITAB10S  DC    F'32736'            SIZE OF ITAB FOR PHASE 10
ITAB20S  DC    F'162000'           SIZE OF ITAB FOR PHASE 20
ITAB30S  DC    F'58000'            SIZE OF ITAB FOR PHASE 30
CRIDTABS DC    F'3600'             SIZE OF CRIDTAB FOR PHASE 30
SUTAB30S DC    F'1400'             SIZE OF SUTAB BUFFER OF PHASE 30
LVTAB30S DC    F'1600'             SIZE OF LVTAB BUFFER FOR PHASE 30
OPTABS   DC    F'1792'             SIZE OF OPTAB BUFFERS 1 AND 2
SUTAB40S DC    F'25600'            SIZE OF SUTAB IN PHASE 40
LVTAB40S DC    F'56000'            SIZE OF LVTAB IN PHASE 40
OOSTACKS DC    F'6144'             SIZE OF OPERATOR/OPERAND STACK
*
*        AREA FOR HEADING INFORMATION TO APPEAR AT THE TOP OF
*        EACH NEW PAGE
*
PAGEHEAD EQU   *
PAGEHD1  DC    CL121' '                 FIRST HEADLINE
         ORG   PAGEHD1
PAGEHD1C DC    C' '                     ASA CNTL
         DC    CL10' '                  SPACER
PAGEHD1D DC    CL100' '                 PAGE TEXT HEADING
         ORG   PAGEHD1+113
PAGEHD1P DC    CL4'PAGE'                PAGE
PAGENUMB DC    CL4' '                   PAGE COUNTER
         ORG
*
PAGEHD2  DC    CL121' '                 SECOND HEADLINE
         ORG   PAGEHD2
PAGEHD2C DC    C' '                     ASA CNTL
         DC    CL10' '                  SPACER
PAGEHD2D DC    CL100' '                 PAGE TEXT HEADING
         ORG
*
PAGEHD3  DC    CL121' '                 THIRD HEADLINE
         ORG   PAGEHD3
PAGEHD3C DC    C' '                     ASA CNTL
         DC    CL10' '                  SPACER
PAGEHD3D DC    CL100' '                 PAGE TEXT HEADING
         ORG
*
*        END OF STANDARD COMMON AREA
*
STANDX   EQU   *
*
*        THE FOLLOWING AREAS ARE NEEDED BY SOME BUT NOT ALL
*        PHASES AND PARTLY OVERLAY EACH OTHER
*
*                              NAME OR PURPOSE        NEEDED BY PHASES
*
         DC    0F'0'
PRELPOOL DC    236C' ',20C'X'  PRELIMINARY ERROR POOL     IEX10
         ORG   PRELPOOL+414
*
*        DCB FOR SYSIN
*
         PRINT NOGEN
*
SYSIN    DCB   DDNAME=SYSIN,                                           X
               DSORG=PS,                                               X
               MACRF=(GM),                                             X
               RECFM=FB,                                               X
               LRECL=80,                                               X
               BFTEK=S,                                                X
               SYNAD=IEX00SYN,                                         X
               EODAD=IEX00EDI
*
         PRINT GEN
*
         ORG   PRELPOOL
PBTAB2   DS    CL510          PROGR. BLOCK TABLE 2          20-50
         DS    0F
PBTAB1   DS    CL255          PROGR. BLOCK TABLE 1          11-20
         ORG   PBTAB1
FSTAB    DS    CL255          FOR STATEMENT TABLE           30-40
*                             DCB FOR SYSUT1                11-30
*
SYSUT1   DCB   DDNAME=SYSUT1,                                          X
               DSORG=PS,                                               X
               MACRF=(R,W),                                            X
               RECFM=F,                                                X
               SYNAD=IEX00SYN,                                         X
               EODAD=IEX00ED1
*
         DS    0F
SPTAB    DS    CL255          SCOPE TABLE                   11-30
         DS    0F
GPTAB    EQU   *-3            GROUP TABLE                   11-30
         DS    CL1510
*
*        END OF SYMLIB PART OF COMMON WORK AREA
*
         ORG   STANDX+2863
         DC    C'0'
*
         MEND
./ ADD NAME=WAEXT    0201-12230-12230-1200-00069-00069-00000-LEVEL
*
*        AREA USED BY COMPILATION PHASE
*
         ORG   SYSUT1
*
RETADR   DS    17F                      SAVE AREA
PLACE14  DS    F                           -''-
RUTI     DS    9F                       GPR CONTROL
RUTR     DS    4F                       FLREG CONTROL
GPROLN   DS    H                        LABEL NR OF OBJ PRG ENTRY POINT
KONSUM   DS    H                        WORKPLACE
WORKPL   DS    F                        WORKPLACE
IBUF1    EQU   SRCE1ADD                 ADDR OF FIRST SOURCE BUFFER
IBUF2    DS    A                          -''  SECOND    -''-
SOURCEB  DS    A                          -''  CURRENT   -''-
RSRCB    DS    A                          -''  READ      -''-
OPBUF1   DS    A                        ADDR OF FIRST OPTAB BUFFER
OPBUF2   DS    A                          -''-  SECOND    -''-
OPBUFB   DS    A                          -''-  CURRENT   -''-
ROPTB    DS    A                          -''-  READ      -''-
AOPTABE  DS    A                        ADDR OF CURRENT OPTAB ENTRY
LATAB    DS    A                        ADDR OF LABEL ADDR TABLE
APBTAB4  EQU   LATAB                    ADDRESS OF PBTAB4
SUTABCA  DS    A                        ADDR OF LAST USED SUTAB ENTRY
STRETURN DS    F                        RETURN ADDRESS
FREEMADR DS    A                        ADDR FOR FREEMAIN IN 50000
FREEMSIZ DS    F                        SIZE          -''-
         SPACE
PBTAB3   DS    1024C                    PROGRAM BLOCK TABLE, 3RD VERS
         SPACE
IOTAB    DC    18X'00'                  LIST OF DATA SETS
CII      DC    H'0'                     REGISTER CONTROL
CIR      DC    H'0'                          -''-
RII      DC    H'0'                          -''-
RIR      DC    H'0'                          -''-
OPDPBN   DC    H'0'                     PROGRAM BLOCK NO (BYTE 2 OF OPD
OPDADR   DC    H'0'                     BYTES 3 AND 4 OF OPERAND
OPDLN    DC    H'0'                     LABEL NUMBER TIMES FOUR
ZEROHW   DC    H'0'                     ZEROES
CFSN     DC    X'00'                    CURRENT FS NUMBER
MAXFSN   DC    X'FF'                    MAX FS NUMBER
CLEARDIS DC    X'F000'                  FOR CLEARING OF DISPLACEM PART
ONEENTRY DC    H'4'                     SHORT, FOR INCR OF OT STACK PTR
         ORG   ONEENTRY
         DC    H'8'                     LONG            -''-
PRECMASK DC    X'10'                    SHORT, TO MODIFY INSTRUCTIONS
         ORG   PRECMASK
         DC    X'00'                    LONG            -''-
NUMBBL   DC    X'00'                    RECORD COUNTER
         DS    H
SPBNST   DC    H'0'                     CURRENT PBN
         DS    H                                    AND DISPLACEMENT
GPBN     DC    H'255'                   GLOBAL DSA CONTROL
MAXOVERF DC    H'4092'                  SHORT, TO CHECK OT STACK OVERFL
         ORG   MAXOVERF
         DC    H'4088'                  LONG            -''-
HALFW    DS    H                        WORKPLACE
USPEI2   DS    H                        FOR ARRAY DECLA-
USPEI4   DS    H                                        RATION HANDLING
WPLACE   DS    H                        FOR INSTRUCTION GENERATION
XPLACE   DS    H
YPLACE   DS    H
UPLACE   DS    H
VPLACE   DS    C                                    -''-
STRDNAME DS    5C                       TO STORE OPERAND
CBVTAB   DS    CL48                     CALLED-BY-VALUE TABLE
SUTABC   DS    CL768                    FOR SUBSCRIPT OPTIMIZATION
         ORG   SUTABC
DSTAB    DS    CL608                    DATA SET TABLE
./ ADD NAME=WORKAREA 0201-12230-12230-1200-00245-00245-00000-LEVEL
*
*        WORKAREA - MAPPING CSECT IEX00001
*
*        ANY CHANGES MADE TO IEX00001 MUST BE REFLECTED IN THIS DSECT
*
SAVEAREA DC    18F'0'
*
*        DCB ADDRS
*
DCBTABLE DC    0F'0'
ALINDCB  DC    A(0)
         DC    A(0)
         DC    A(0)
         DC    A(0)
ASYSDCB  DC    A(0)
APRTDCB  DC    A(0)
APCHDCB  DC    A(0)
AUT1DCB  DC    A(0)
AUT2DCB  DC    A(0)
AUT3DCB  DC    A(0)
*
*        END OF DATA EXIT ADDRS
*
EODUT1   DC    A(0)                    SYSUT1
EODUT2   DC    A(0)                    SYSUT2
EODUT3   DC    A(0)                    SYSUT3
EODIN    DC    A(0)                    SYSIN
*
*        OPTION SWITCHES IN COMPFLGS
*
*        ALLOCATION OF THE BIT POSITIONS IN COMPFLGS -
*
*        PURPOSE                   POSITION
*                                  BYTE 1    BYTE 2    BYTE 3
*                                  01234567  01234567  01234567
*
*        COMPMODE (SYNTAX CHECK)   X
*        SUBSCRIPT OPTIMIZATION     X
*        WARNING ERROR               X
*        SERIOUS ERROR                X
*        TERMINATING ERROR             X
*        PROCEDURE/PROGRAM              X
*        LONG/SHORT PRECISION            X
*        OPERAND                          X
*
*        NOSOURCE/SOURCE                     X
*        NOLOAD/LOAD                          X
*        NODECK/DECK                           X
*        ISO/EBCDIC                             X
*        PROGRAM INTERRUPT                       X
*        TERMINATING PHASE ENTERED                X
*        NO BUFFERS ASSIGNED                       X
*        NO COMPILATION POSSIBLE                    X
*
*        SYSPRINT DOWN                                 X
*        WHOLE SOURCE PROG IN CORE                      X
*        NO OPTAB                                        X
*        SYSPRINT NOT OPENED                              X
*        ERROR UNRELATED TO SEMICOLON NR                   X
*        NOTEST/TEST (SC COUNT IN CODE, NOT SYSGEN OPT)     X
*        60 CHARACTER SET                                    X
*        (RESERVED)                                           X
*
COMPFLGS DC    X'00220000'             PARAMETERS AND SWITCHES
*
*        OPTION SWITCHES IN COMPFLGS
*
COMPMODE EQU   X'80'                   SYNTAX CHECK MODE
SUBSCOPT EQU   X'40'                   SUBSCRIPT OPTIMIZATION
PGR      EQU   X'FB'
PROC     EQU   X'04'                   PRECOMPILED PROCEDURE
*
SHRT     EQU   X'FD'
LNG      EQU   X'02'
OPERAND  EQU   X'01'
*
*        ERROR SEVERITY INDICATORS IN COMPFLGS
*
WERR     EQU   X'20'                   WARNING ERROR
SERR     EQU   X'10'                   SERIOUS ERROR
TERR     EQU   X'08'                   TERMINATING ERROR
*
*        OPTION SWITCHES IN COMPFLGS+1
*
SRCE     EQU   X'7F'
NSRCE    EQU   X'80'
*
LOAD     EQU   X'BF'
NLOAD    EQU   X'40'
*
DECK     EQU   X'DF'
NDECK    EQU   X'20'
*
EBCDIC   EQU   X'EF'
ISO      EQU   X'10'
*
*        TERMINATION SWITCHES IN COMPFLGS+1
*
ERR      EQU   X'08'                   PROGRAM INTERRUPT HAS
*                                      OCCURED IN COMPILER
TERM     EQU   X'04'                   LAST PHASE HAS BEEN ENTERED
NOBUF    EQU   X'02'                   ERROR POOL IS IN WORKAREA
*                                      NO SCE PROG BUFF 1
NOGO     EQU   X'01'                   COMPILATION NOT POSSIBLE
*                                      DO NOT START SCAN 1
NOBUNOGO EQU   X'03'                   NOBUF AND NOGO
*
*        SWITCHES IN COMPFLGS+2
*
PRT      EQU   X'80'                   SYSPRINT NOT AVAILABLE
SPIC     EQU   X'40'                   SOURCE PROGRAM IN STORAGE
NOPT     EQU   X'20'                   NO SUBSCRIPT OPTIMIZATION
PRTNO    EQU   X'10'                   SYSPRINT NOT OPENED
NOSC     EQU   X'08'                   SEMICOLON COUNTER NOT VALID
*
NOTEST   EQU   X'04'
TEST     EQU   X'FB'                   EMBED SC COUNT IN CODE (DEFAULT)
*
SET60    EQU   X'02'                   60 CHARACTER SET IS TO BE USED
*
*        MISCELLANEOUS CONTROL INFORMATION
*
SIZE     DC    F'45056'            AVAILABLE MAIN STORAGE - NOT USED
PICAADD  DC    A(0)                ADDR OF PICA OF THE INVOKER
HDING    DC    F'0'                ADDR OF HEADING INFO OF THE INVOKER
ERET     DC    F'0'                RETURN ADDR FOR PROGRAM
*                                  AND I/O ERRORS
PAGECNT  DC    PL4'0'              PAGE COUNT
LINCNT   DC    H'0'                COUNTER OF LINES PER PAGE
MAXLINES DC    H'56'               MAX NUMBER OF PRINT LINES PER PAGE
SEMCNT   DC    H'0'                SEMICOLON COUNTER
PBN      DC    H'50'               HIGHEST PROGRAM BLOCK NUMBER
KBN      DC    H'0'                HIGHEST CONSTANT POOL NUMBER
LATNR    EQU   28                  NR OF LIBRARY STAND FUNCTIONS
LATBEG   EQU   4*(LATNR-1)
LN       DC    AL2(LATBEG)         LAST USED DISPLACEMENT IN LAT
PRPT     DC    F'0'                PROGRAM POINTER
SAVOUTA  DC    F'0'
OUTAREA2 DS    F                   SYSPUNCH SAVE AREA
PIDENT   DC    CL4' '              PROGRAM IDENTIFICATION
CARDCNT  DC    PL4'0'              OBJECT PROGRAM DECK SEQUENCE NUMBER
PRTRTADD DC    A(0)                ADDR OF PRINT ROUTINE
*
*        ADDRS OF AREAS WHICH ARE USED BY MORE THAN A SINGLE PHASE
*
ERRPOOL  DC    A(PRELPOOL)         FIRST BYTE OF PRELIMINARY ERROR POOL
NEXTERR  DC    A(PRELPOOL)         NEXT FREE PLACE IN ERROR POOL
ENDPOOL  DS    F                   LAST BYTE OF ERROR POOL-23
SRCE1ADD DS    F                   SOURCE PROGRAM BUFFER 1
SRCE1END DS    F                   ADDR OF LAST BYTE+1
SULTSTRT DS    F                   ID OF LAST ITAB RECORD
*
*
*        TABLE OF THE LENGTHS OF VARIABLE SIZE AREAS
*
INBLKS   DS    H                   MAX BLKSIZE FOR SYSIN    - NOT USED
PRTBLKS  DS    H                   MAX BLKSIZE SYSPRINT     - NOT USED
LINBLKS  DS    H                   MAX BLKSIZE FOR SYSLIN   - NOT USED
PCHBLKS  DS    H                   MAX BLKSIZE FOR SYSPUNCH - NOT USED
POOLS    DS    F                   SIZE OF ERROR POOL
SRCE1S   DS    F                   SIZE OF SOURCE PROG BUFFERS 1 AND 2
SRCE3S   EQU   SRCE1S              SIZE OF SOURCE PROG BUFFERS 3 AND 4
ITAB10S  DS    F                   SIZE OF ITAB FOR PHASE 10
ITAB20S  DS    F                   SIZE OF ITAB FOR PHASE 20
ITAB30S  DS    F                   SIZE OF ITAB FOR PHASE 30
CRIDTABS DS    F                   SIZE OF CRIDTAB FOR PHASE 30
SUTAB30S DS    F                   SIZE OF SUTAB BUFFER OF PHASE 30
LVTAB30S DS    F                   SIZE OF LVTAB BUFFER FOR PHASE 30
OPTABS   DS    F                   SIZE OF OPTAB BUFFERS 1 AND 2
SUTAB40S DS    F                   SIZE OF SUTAB IN PHASE 40
LVTAB40S DS    F                   SIZE OF LVTAB IN PHASE 40
OOSTACKS DS    F                   SIZE OF OPERATOR/OPERAND STACK
*
*        AREA FOR HEADING INFORMATION TO APPEAR AT THE TOP OF
*        EACH NEW PAGE
*
PAGEHEAD EQU   *
PAGEHD1  DC    CL121' '                 FIRST HEADLINE
         ORG   PAGEHD1
PAGEHD1C DC    C'1'                     ASA CNTL
         DC    CL10' '                  SPACER
PAGEHD1D DC    CL100' '                 PAGE TEXT HEADING
         ORG   PAGEHD1+113
PAGEHD1P DC    CL4'PAGE'                PAGE
PAGENUMB DC    CL4' '                   PAGE COUNTER
         ORG
*
PAGEHD2  DC    CL121' '                 SECOND HEADLINE
         ORG   PAGEHD2
PAGEHD2C DC    C' '                     ASA CNTL
         DC    CL10' '                  SPACER
PAGEHD2D DC    CL100' '                 PAGE TEXT HEADING
         ORG
*
PAGEHD3  DC    CL121' '                 THIRD HEADLINE
         ORG   PAGEHD3
PAGEHD3C DC    C' '                     ASA CNTL
         DC    CL10' '                  SPACER
PAGEHD3D DC    CL100' '                 PAGE TEXT HEADING
         ORG
*
*
*        END OF STANDARD COMMON AREA
*
STANDX   EQU   *
*
*        THE FOLLOWING AREAS ARE NEEDED BY SOME BUT NOT ALL
*        PHASES AND PARTLY OVERLAY EACH OTHER
*
*                             NAME OR PURPOSE          NEEDED BY PHASES
*
         DC    0F'0'
PRELPOOL DC    236C' ',20C'X' PRELIMINARY ERROR POOL     IEX10
         ORG   PRELPOOL+414
*                             DCB FOR SYSIN                 11
SYSIN    DCB   DDNAME=SYSIN,                                           X
               DSORG=PS,                                               X
               MACRF=(GM),                                             X
               RECFM=FB,                                               X
               LRECL=80,                                               X
               BFTEK=S
*              SYNAD=SYNAD    (ASSEMBLED IN IEX00001)
*              EODAD=EODADIN  (INSERTED BY IEX11)
         ORG   PRELPOOL
PBTAB2   DS    CL510          PROGR. BLOCK TABLE 2          20-50
         DS    0F
PBTAB1   DS    CL255          PROGR. BLOCK TABLE 1          11-20
         ORG   PBTAB1
FSTAB    DS    CL255          FOR STATEMENT TABLE           30-40
*                             DCB FOR SYSUT1                11-30
SYSUT1   DCB   DDNAME=SYSUT1,                                          X
               DSORG=PS,                                               X
               MACRF=(R,W),                                            X
               RECFM=F
*              SYNAD=SYNAD,   (ASSEMBLED IN IEX00001)
*              EODAD=EODAD1
*
         DS    0F
SPTAB    DS    CL255          SCOPE TABLE                   11-30
         DS    0F
GPTAB    EQU   *-3            GROUP TABLE                   11-30
         DS    CL1510
*
*        END OF SYMLIB PART OF COMMON WORK AREA
*
