      *=======================*                                         R080101
       IDENTIFICATION DIVISION.                                         R080101
      *=======================*                                         R080101
       PROGRAM-ID.    SMF30EX2.                                         R080101
       AUTHOR.        KANG DENGSONG.                                    R080101
       DATE-WRITTEN.  01 JAN 2008.                                      R080101
      *================================================================*
      *  SSSSSSSSSS    MMMMMMMMMMMMM    FFFFFFFFF  33333333   00000000
      * SSSSSSSSSSS   MMMMMMMMMMMMMMM   FFFFFFFFF  33333333  0000000000
      * SSSS          MMMMMMMMMMMMMMM   FFFF           3333  000    000
      * SSSS          MMM   MMM   MMM   FFFF           3333  000    000
      * SSSS          MMM   MMM   MMM   FFFF           3333  000    000
      * SSSSSSSSS     MMM   MMM   MMM   FFFFFFFFF  33333333  000    000
      *    SSSSSSSS   MMM   MMM   MMM   FFFFFFFFF  33333333  000    000
      *       SSSSS   MMM   MMM   MMM   FFFF           3333  000    000
      *       SSSSS   MMM   MMM   MMM   FFFF           3333  000    000
      *       SSSSS   MMM   MMM   MMM   FFFF           3333  000    000
      *       SSSSS   MMM   MMM   MMM   FFFF           3333  000    000
      *SSSSSSSSSSSS   MMM   MMM   MMM   FFFF       33333333  0000000000
      *SSSSSSSSSSS    MMM   MMM   MMM   FFFF       33333333   00000000
      *=================================================================
      ******************************************************************
      *================================================================*
      *   This program is distributed without warranty of any kind,    *
      * including  (without limitation) warranties of merchantability  *
      * and of fitness for a particular purpose. it is up to the user  *
      * to make his own determination of the usefulness of this progra *
      * in his own system.                                             *
      *================================================================*
      *=================================================================R080101
      *  PLS SORT SMF30IN BY JOB ID JOB NAME                           *R080101
      *=================================================================R080101
      *================================================================*R080101
      * Change Log                                                     *R080101
      *================================================================*R080101
      *  Logno      mender               Desc                          *R080101
      *----------------------------------------------------------------*R080101
      * R080101     Kangdengsong        Initial Version                *R080101
      * R081211     Kangdengsong        Updated Version                *R081211
      *             1) Add user id as selection criteria from SYSIN    *R081211
      *             2) Add Execute Channel Program (EXCP) Section      *R081211
      *             3) Null                                            *R081211
      *================================================================*R080101
                                                                        R080101
      *=====================*                                           R080101
       ENVIRONMENT DIVISION.                                            R080101
      *=====================*                                           R080101
       CONFIGURATION SECTION.                                           R080101
       SOURCE-COMPUTER. IBM-ES9000.                                     R080101
       OBJECT-COMPUTER. IBM-ES9000.                                     R080101
       INPUT-OUTPUT SECTION.                                            R080101
       FILE-CONTROL.                                                    R080101
           SELECT SMFRPT01  ASSIGN         TO SMFRPT01                  R080101
                            ORGANIZATION   IS SEQUENTIAL                R080101
                            ACCESS MODE    IS SEQUENTIAL                R080101
                            FILE STATUS    IS WS-PRT-ST.                R080101
           SELECT SMF30IN   ASSIGN         TO SMF30IN                   R080101
                            ORGANIZATION   IS SEQUENTIAL                R080101
                            ACCESS MODE    IS SEQUENTIAL                R080101
                            FILE STATUS    IS WS-SMFIN-ST.              R080101
           SELECT SMF30OUT  ASSIGN         TO SMF30OUT                  R081211
                            ORGANIZATION   IS SEQUENTIAL                R081211
                            ACCESS MODE    IS SEQUENTIAL                R081211
                            FILE STATUS    IS WS-SMFOUT-ST.             R081211
      *=============*                                                   R080101
       DATA DIVISION.                                                   R080101
      *=============*                                                   R080101
       FILE SECTION.                                                    R080101
       FD  SMFRPT01.                                                    R080101
       01  SMFRPT01-REC               PIC X(150).                       R080101
      *                                                                 R080101
       FD  SMF30IN.                                                     R080101
       01  SMF30-ALL-X.                                                 R080101
           COPY SMF30CPX.                                               R080101
       FD  SMF30OUT.                                                    R081211
       01  SMF30-OUT-X.                                                 R081211
           05 SMF30-OUT-JOB-ID           PIC X(08).                     R081201
           05 SMF30-OUT-JOB-NAME         PIC X(08).                     R081201
           05 SMF30-OUT-BEGIN-DTE        PIC X(08).                     R081201
           05 SMF30-OUT-BEGIN-TME        PIC X(08).                     R081201
           05 SMF30-OUT-END-DTE          PIC X(08).                     R081201
           05 SMF30-OUT-END-TME          PIC X(08).                     R081201
           05 SMF30-OUT-USER             PIC X(08).                     R081201
           05 SMF30-OUT-COST-TIME        PIC S9(08)V99 COMP-3.          R081201
           05 SMF30-OUT-EXCP             PIC S9(12) COMP-3.             R081201
           05 SMF30-OUT-CPU              PIC S9(12)V99 COMP-3.          R081201
           05 SMF30-OUT-FILLER           PIC X(23).                     R081201
      *                                                                 R080101
                                                                        R080101
       WORKING-STORAGE SECTION.                                         R080101
       01  FILLER                          PIC X(50) VALUE              R080101
              'SMF30EX2 WORKING STORAGE START HERE'.                    R080101
       01  WS-SW.                                                       R080101
           05 WS-WHEN-COMPILED             PIC X(22) VALUE SPACE.       R080101
           05 WS-END-EOF                   PIC X(01) VALUE SPACE.       R080101
              88 K-END-FILE                VALUE 'Y'.                   R080101
              88 K-X-END-FILE              VALUE 'N'.                   R080101
           05 WS-FIRST-FLAGE               PIC X(01) VALUE SPACE.       R080101
              88 WS-FIRST-YES              VALUE 'Y'.                   R080101
              88 WS-LAST-YES               VALUE 'N'.                   R080101
           05 WS-PREV-JOB-ID               PIC X(08) VALUE SPACE.       R080101
           05 WS-TIME-INT                  PIC 9(09) VALUE ZERO.        R080101
           05 WS-DAY                       PIC 9(07) VALUE ZERO.        R080101
           05 WS-JOB-DT                    PIC 9(07) VALUE ZERO.        R080101
           05 WS-JOB-TM                    PIC S9(07)V99  COMP-3.       R080101
           05 WS-BRIDGE                    PIC S9(01)V99.               R080101
           05 WS-REMIND                    PIC 9(12) COMP VALUE 0.      R080101
           05 WS-HH                        PIC 9(02) VALUE 0.           R080101
           05 WS-MM1                       PIC 9(02) VALUE 0.           R080101
           05 WS-SS                        PIC 9(02) VALUE 0.           R080101
           05 WS-PAGE-NO                   PIC 9(05) VALUE 0.           R080101
           05 WS-LINE-NO                   PIC 9(03) VALUE 1.           R080101
           05 WS-TIME-SECOND               PIC 9(16)V99 VALUE ZERO.     R080101
           05 WS-TIME-SECOND-X             PIC Z,ZZZ,ZZZ,Z99.99.        R080101
           05 WS-TEST                      PIC -ZZZ,ZZZ,Z99.99.         R080101
           05 WS-SYS-DATE                  PIC X(21).                   R080101
           05 WS-CCYYDDD1                  PIC 9(07) VALUE 0.           R080101
           05 FILLER REDEFINES WS-CCYYDDD1.                             R080101
              15 WS-YYYY1                  PIC 9(04).                   R080101
              15 WS-DDD1                   PIC 9(03).                   R080101
           05 WS-CCYYDDD2                  PIC 9(07) VALUE 0.           R080101
           05 FILLER REDEFINES WS-CCYYDDD2.                             R080101
              15 WS-YYYY2                  PIC 9(04).                   R080101
              15 WS-DDD2                   PIC 9(03).                   R080101
           05 WS-CCYYMMDD                  PIC 9(08).                   R080101
           05 FILLER REDEFINES WS-CCYYMMDD.                             R080101
              15 WS-YYYY                   PIC 9(04).                   R080101
              15 WS-MM                     PIC 9(02).                   R080101
              15 WS-DD                     PIC 9(02).                   R080101
           05 WS-TIME.                                                  R080101
              10 SYS-HH                    PIC 9(02).                   R080101
              10 FILLER                    PIC X(01) VALUE ':'.         R080101
              10 SYS-MM                    PIC 9(02).                   R080101
              10 FILLER                    PIC X(01) VALUE ':'.         R080101
              10 SYS-SS                    PIC 9(02).                   R080101
              10 FILLER                    PIC X(01) VALUE '.'.         R080101
              10 SYS-HS                    PIC 9(02).                   R080101
           05 WS-RETURN-CODE               PIC 9(02) VALUE ZERO.        R080101
           05 WS-ALL-REC-NO                PIC 9(12) VALUE ZERO.        R080101
           05 WS-ALL-JOB-NO                PIC 9(12) VALUE ZERO.        R080101
           05 WS-K                         PIC 9(10) VALUE ZERO.        R080101
           05 WS-TOT-TIME                  PIC ZZ,ZZZ,ZZZ,ZZZ.99.       R080101
           05 RPT-DT.                                                   R080101
              10 RPT-YY                    PIC X(02).                   R080101
              10 FILLER                    PIC X(01) VALUE '-'.         R080101
              10 RPT-YM                    PIC X(02).                   R080101
              10 FILLER                    PIC X(01) VALUE '-'.         R080101
              10 RPT-DD                    PIC X(02).                   R080101
              10 FILLER                    PIC X(02) VALUE ' '.         R080101
           05 RPT-TIME.                                                 R080101
              10 RPT-HH                    PIC 9(02).                   R080101
              10 FILLER                    PIC X(01) VALUE ':'.         R080101
              10 RPT-MM                    PIC 9(02).                   R080101
              10 FILLER                    PIC X(01) VALUE ':'.         R080101
              10 RPT-SS                    PIC 9(02).                   R080101
              10 FILLER                    PIC X(01) VALUE '.'.         R080101
              10 RPT-HS                    PIC 9(02).                   R080101
              10 FILLER                    PIC X(01) VALUE ' '.         R080101
                                                                        R080101
       01 WS-FILE-STATUS.                                               R080101
           05 WS-SMFIN-ST                  PIC 9(02) VALUE 0.           R080101
           05 WS-SMFOUT-ST                 PIC 9(02) VALUE 0.           R081211
           05 WS-PRT-ST                    PIC 9(02) VALUE 0.           R080101
       77 K-SMF-COM-ADD                    PIC 9(03) VALUE 30.          R080101
       77 K-SMF-START-UOW                  PIC 9(02) VALUE 1.           R080101
       77 K-SMF-ACT-PRE-INTERVAL           PIC 9(02) VALUE 2.           R080101
       77 K-SMF-LAST-INTERVAL              PIC 9(02) VALUE 3.           R080101
       77 K-SMF-STEP-TOT                   PIC 9(02) VALUE 4.           R080101
       77 K-SMF-UOW-TERM                   PIC 9(02) VALUE 5.           R080101
       77 K-SMF-SYSTEM-ADD                 PIC 9(02) VALUE 6.           R080101
       77 K-MAX-LINE-PER-PAGE              PIC 9(02) VALUE 60.          R080101
      *77 K-FIX-POS                        PIC 9(09) VALUE 3.           R080101
      *77 K-MICROSECOND                    PIC 9(07) VALUE 1000000.     R080101
      *77 K-ONE-1K                         PIC 9(05) VALUE 1024.        R080101
      *77 K-NULL-DATE                      PIC X(04) VALUE X'0000000F'. R080101
      *77 K-LOW-VALUE                      PIC X(01) VALUE X'00'.       R080101
      *77 K-NINE                           PIC X(01) VALUE X'09'.       R080101
       77 K-TEST-REC-NO                    PIC 9(07) VALUE 9999999.     R080101
           COPY SMF30CRP.                                               R080101
                                                                        R080101
      *===============*                                                 R080101
       LINKAGE SECTION.                                                 R080101
      *===============*                                                 R080101
      *01  PARM-INPUT.                                                  R080101
      *    03  PARM-LENTH                      PIC 9(4) COMP.           R080101
      *    03  PARM                            PIC X(40).               R080101
       PROCEDURE DIVISION.                                              R080101
      *                                                                 R080101
      *==================*                                              R080101
       0000-MAIN-ROUTINE.                                               R080101
      *==================*                                              R080101
      *                                                                 R080101
           PERFORM A000-OPEN-FILES   THRU  A000-EXIT                    R080101
           PERFORM A200-PRINT-REPORT THRU  A200-EXIT                    R080101
           PERFORM 9800-CLOSE-FILES  THRU  9800-EXIT.                   R080101
      *                                                                 R080101
      *==================*                                              R080101
       0000-EXIT.                                                       R080101
      *==================*                                              R080101
      *                                                                 R080101
           GOBACK.                                                      R080101
      *                                                                 R080101
      *==================*                                              R080101
       A000-OPEN-FILES.                                                 R080101
      *==================*                                              R080101
      *                                                                 R080101
            MOVE FUNCTION  WHEN-COMPILED     TO WS-WHEN-COMPILED        R080101
            MOVE FUNCTION CURRENT-DATE       TO WS-SYS-DATE             R080101
            OPEN OUTPUT SMFRPT01                                        R080101
            IF WS-PRT-ST NOT = 00                                       R080101
              MOVE  WS-PRT-ST                   TO WS-RETURN-CODE       R080101
              DISPLAY 'OPEN SMFRPT01 FILE FAIL=' WS-PRT-ST              R080101
              PERFORM 9000-CHECK-RETURN-CODE    THRU 9000-EXIT          R080101
            END-IF                                                      R080101
            OPEN INPUT SMF30IN                                          R080101
            IF WS-SMFIN-ST NOT = 00                                     R080101
              MOVE  WS-SMFIN-ST                 TO WS-RETURN-CODE       R080101
              DISPLAY 'OPEN SMFRPT01 FILE FAIL=' WS-SMFIN-ST            R080101
              PERFORM 9000-CHECK-RETURN-CODE    THRU 9000-EXIT          R080101
            END-IF                                                      R080101
            OPEN OUTPUT SMF30OUT                                        R081211
            IF WS-SMFOUT-ST NOT = 00                                    R081211
              MOVE  WS-SMFOUT-ST                 TO WS-RETURN-CODE      R081211
              DISPLAY 'OPEN SMFOUT FILE FAIL=' WS-SMFOUT-ST             R081211
              PERFORM 9000-CHECK-RETURN-CODE    THRU 9000-EXIT          R081211
            END-IF                                                      R081211
            MOVE SPACE                          TO SMF30-ALL-X          R080101
            INITIALIZE                           SMF30-ALL-X            R080101
            INITIALIZE                           SMF30-OUT-X            R080101
            MOVE WS-SYS-DATE(1:4)               TO REPORT-DATE-YYYY     R080101
            MOVE WS-SYS-DATE(5:2)               TO REPORT-DATE-MM       R080101
            MOVE WS-SYS-DATE(7:2)               TO REPORT-DATE-DD       R080101
            EXIT.                                                       R080101
      *                                                                 R080101
      *==================*                                              R080101
       A000-EXIT.                                                       R080101
      *==================*                                              R080101
      *                                                                 R080101
           EXIT.                                                        R080101
      *                                                                 R080101
      *==================*                                              R080101
       A200-PRINT-REPORT.                                               R080101
      *==================*                                              R080101
      *                                                                 R080101
           PERFORM R100-PRINT-HEADING         THRU R100-EXIT            R080101
           PERFORM R200-PRINT-TITLE           THRU R200-EXIT            R080101
           COMPUTE WS-ALL-REC-NO = 0                                    R080101
           COMPUTE WS-ALL-JOB-NO = 0                                    R080101
           READ SMF30IN AT END SET K-END-FILE  TO TRUE END-READ         R080101
           IF WS-PRT-ST NOT = 00                                        R080101
             MOVE WS-SMFIN-ST                  TO WS-RETURN-CODE        R080101
             DISPLAY 'READ SMF30IN FILE FAIL=' WS-SMFIN-ST              R080101
             PERFORM 9000-CHECK-RETURN-CODE    THRU 9000-EXIT           R080101
           END-IF                                                       R080101
           PERFORM UNTIL K-END-FILE OR WS-ALL-REC-NO > K-TEST-REC-NO    R080101
              ADD 1                TO WS-ALL-REC-NO                     R080101
      *       DISPLAY 'RECORD SEQ=' WS-ALL-REC-NO                       R080101
              PERFORM A210-PROCESS-REPORT   THRU A210-EXIT              R080101
              MOVE SMF30JNM-X      TO WS-PREV-JOB-ID                    R080101
              MOVE SMF30JNM-X      TO WS-PREV-JOB-ID                    R080101
              READ SMF30IN AT END SET K-END-FILE TO TRUE END-READ       R080101
              IF WS-SMFIN-ST NOT = 00                                   R080101
                DISPLAY 'READ SMF30 FILE FAIL=' WS-SMFIN-ST             R080101
                MOVE WS-SMFIN-ST                   TO WS-RETURN-CODE    R080101
                PERFORM 9000-CHECK-RETURN-CODE THRU 9000-EXIT           R080101
              END-IF                                                    R080101
           END-PERFORM                                                  R080101
           PERFORM R500-PRINT-TOT-END         THRU R500-EXIT            R080101
           DISPLAY 'TOT RECORD IS ='  WS-ALL-REC-NO                     R080101
           EXIT.                                                        R080101
      *                                                                 R080101
      *==================*                                              R080101
       A200-EXIT.                                                       R080101
      *==================*                                              R080101
      *                                                                 R080101
           EXIT.                                                        R080101
      *                                                                 R080101
      *====================*                                            R080101
       A210-PROCESS-REPORT.                                             R080101
      *====================*                                            R080101
      *                                                                 R080101
           IF SMF30TYP-X = K-SMF-START-UOW                              R080101
TSTKDS*       DISPLAY 'SMF30DTE-X=' SMF30DTE-X ':' WS-ALL-REC-NO        R080101
              INITIALIZE SMF30-OUT-X                                    R081211
              MOVE SMF30DTE-X              TO WS-JOB-DT                 R080101
              COMPUTE WS-DAY = FUNCTION INTEGER-OF-DAY(SMF30DTE-X)      R081211
              COMPUTE WS-CCYYMMDD = FUNCTION DATE-OF-INTEGER(WS-DAY)    R081211
              MOVE WS-CCYYMMDD             TO SMF30-OUT-BEGIN-DTE       R081211
              DIVIDE SMF30TME-X BY 3600                                 R081211
                                 GIVING WS-HH REMAINDER WS-REMIND       R081211
              DIVIDE WS-REMIND BY 60                                    R081211
                                 GIVING WS-MM1 REMAINDER WS-SS          R081211
              MOVE WS-HH                   TO SMF30-OUT-BEGIN-TME(1:2)  R081211
              MOVE ':'                     TO SMF30-OUT-BEGIN-TME(3:1)  R081211
              MOVE WS-MM1                   TO SMF30-OUT-BEGIN-TME(4:2) R081211
              MOVE ':'                     TO SMF30-OUT-BEGIN-TME(6:1)  R081211
              MOVE WS-SS                   TO SMF30-OUT-BEGIN-TME(7:2)  R081211
              MOVE SMF30TME-X              TO WS-JOB-TM                 R080101
              MOVE SMF30JBN-X              TO RPT-SUB-NAME              R080101
              MOVE SMF30JNM-X              TO RPT-SUB-ID                R080101
              PERFORM R600-PRINT-JOB-BEGIN THRU R600-EXIT               R080101
TSTKDS*       DISPLAY 'WS-JOB-DT=' WS-JOB-DT                            R080101
TSTKDS*       DISPLAY 'WS-JOB-TM=' WS-JOB-TM                            R080101
           END-IF                                                       R080101
           IF SMF30TYP-X = K-SMF-STEP-TOT                               R080101
      *         MOVE SMF30JBN-X          TO RPT-JOB-NAME                R080101
      *         MOVE SMF30JNM-X          TO RPT-JOB-ID                  R080101
              PERFORM R300-PRINT-DETAIL   THRU R300-EXIT                R080101
           END-IF                                                       R080101
           IF SMF30TYP-X = K-SMF-UOW-TERM                               R080101
              MOVE SMF30JBN-X             TO RPT-END-NAME               R080101
              MOVE SMF30JNM-X             TO RPT-END-ID                 R080101
              ADD  1                      TO WS-ALL-JOB-NO              R080101
              PERFORM R700-PRINT-JOB-END  THRU R700-EXIT                R080101
           END-IF                                                       R080101
           EXIT.                                                        R080101
      *                                                                 R080101
      *==================*                                              R080101
       A210-EXIT.                                                       R080101
      *==================*                                              R080101
      *                                                                 R080101
           EXIT.                                                        R080101
      *                                                                 R080101
      *==================*                                              R080101
       R100-PRINT-HEADING.                                              R080101
      *==================*                                              R080101
      *                                                                 R080101
           COMPUTE WS-PAGE-NO = WS-PAGE-NO + 1                          R080101
           MOVE WS-PAGE-NO           TO RPT-PAGE-COUNT                  R080101
           WRITE SMFRPT01-REC  FROM SMF30-RPT-JOB-END-L2 AFTER PAGE     R080101
           WRITE SMFRPT01-REC  FROM SMF30-RPT-HEAD-L1    AFTER 1        R080101
           WRITE SMFRPT01-REC  FROM SMF30-RPT-HEAD-L2   AFTER 1         R080101
           MOVE 3                   TO WS-LINE-NO                       R080101
           EXIT.                                                        R080101
      *                                                                 R080101
      *==================*                                              R080101
       R100-EXIT.                                                       R080101
      *==================*                                              R080101
      *                                                                 R080101
           EXIT.                                                        R080101
      *                                                                 R080101
      *==================*                                              R080101
       R200-PRINT-TITLE.                                                R080101
      *==================*                                              R080101
      *                                                                 R080101
           MOVE WS-PAGE-NO           TO RPT-PAGE-COUNT                  R080101
           WRITE SMFRPT01-REC  FROM SMF30-RPT-TITLE-L1  AFTER 2         R080101
           WRITE SMFRPT01-REC  FROM SMF30-RPT-TITLE-L2                  R080101
           WRITE SMFRPT01-REC  FROM SMF30-RPT-TITLE-L3                  R080101
           COMPUTE WS-LINE-NO = WS-LINE-NO + 3                          R080101
           EXIT.                                                        R080101
      *                                                                 R080101
      *==================*                                              R080101
       R200-EXIT.                                                       R080101
      *==================*                                              R080101
      *                                                                 R080101
           EXIT.                                                        R080101
      *                                                                 R080101
      *==================*                                              R080101
       R300-PRINT-DETAIL.                                               R080101
      *==================*                                              R080101
      *                                                                 R080101
           IF WS-LINE-NO + 1 > K-MAX-LINE-PER-PAGE                      R080101
              PERFORM R100-PRINT-HEADING  THRU R100-EXIT                R080101
              PERFORM R200-PRINT-TITLE    THRU R200-EXIT                R080101
           END-IF                                                       R080101
           MOVE SMF30STM-X               TO RPT-STEP-NAME               R080101
           MOVE SMF30CLS-X               TO RPT-JOB-CLASS               R080101
           MOVE SMF30PGM-X               TO RPT-PROGRAM                 R080101
           IF SMF30RED-X > 1600001   AND                                R080101
              SMF30RED-X < 2099001                                      R080101
               COMPUTE WS-DAY = FUNCTION INTEGER-OF-DAY(SMF30RED-X)     R080101
               COMPUTE WS-CCYYMMDD = FUNCTION DATE-OF-INTEGER(WS-DAY)   R080101
               MOVE WS-YYYY(3:2)      TO RPT-YY                         R080101
               MOVE WS-MM             TO RPT-YM                         R080101
               MOVE WS-DD             TO RPT-DD                         R080101
           END-IF                                                       R080101
           MOVE SMF30RET-X            TO WS-TIME-INT                    R080101
      *    MOVE SMF30RET-X            TO WS-TEST                        R080101
           IF WS-TIME-INT NOT = 0                                       R080101
              COMPUTE RPT-HH = (WS-TIME-INT / 3600)                     R080101
              COMPUTE RPT-MM = ((WS-TIME-INT - 3600 * RPT-HH) / 60)     R080101
              COMPUTE RPT-SS = WS-TIME-INT -                            R080101
                      3600 * RPT-HH - RPT-MM * 60                       R080101
              COMPUTE WS-BRIDGE = SMF30RET-X - WS-TIME-INT              R080101
              COMPUTE RPT-HS = WS-BRIDGE * 100                          R080101
TSTKDS*       DISPLAY 'SMF30RET-X: INT:DI=' SMF30RET-X ':'  WS-TIME-INT R080101
TSTKDS*                ':' WS-TEST                                      R080101
           END-IF                                                       R080101
TSTKDS*    DISPLAY 'WS-TIME-INT=' WS-TIME-INT                           R080101
           IF SMF30STD-X > 1600001   AND                                R080101
              SMF30STD-X < 2099001                                      R080101
               COMPUTE WS-DAY = FUNCTION INTEGER-OF-DAY(SMF30STD-X)     R080101
               COMPUTE WS-CCYYMMDD = FUNCTION DATE-OF-INTEGER(WS-DAY)   R080101
               MOVE WS-YYYY(3:2)      TO RPT-YY                         R080101
               MOVE WS-MM             TO RPT-YM                         R080101
               MOVE WS-DD             TO RPT-DD                         R080101
               MOVE RPT-DT            TO RPT-ST-RUN-DT                  R080101
           ELSE                                                         R080101
      *       DISPLAY 'INVALID DATE2=' SMF30STD-X                       R080101
              MOVE '  N/A'            TO RPT-ST-RUN-DT                  R080101
           END-IF                                                       R080101
           MOVE SMF30SIT-X            TO WS-TIME-INT                    R080101
           IF WS-TIME-INT NOT = 0                                       R080101
              COMPUTE RPT-HH = (WS-TIME-INT / 3600)                     R080101
              COMPUTE RPT-MM = ((WS-TIME-INT - 3600 * RPT-HH) / 60)     R080101
              COMPUTE RPT-SS = WS-TIME-INT -                            R080101
                      3600 * RPT-HH - RPT-MM * 60                       R080101
              COMPUTE WS-BRIDGE = SMF30SIT-X - WS-TIME-INT              R080101
              COMPUTE RPT-HS = WS-BRIDGE * 100                          R080101
              MOVE RPT-TIME              TO RPT-ST-RUN-TIME             R080101
           ELSE                                                         R080101
              MOVE ' N/A  '              TO RPT-ST-RUN-TIME             R080101
           END-IF                                                       R080101
           IF SMF30DTE-X > 1600001   AND                                R080101
              SMF30DTE-X < 2099001                                      R080101
               COMPUTE WS-DAY = FUNCTION INTEGER-OF-DAY(SMF30DTE-X)     R080101
               COMPUTE WS-CCYYMMDD = FUNCTION DATE-OF-INTEGER(WS-DAY)   R080101
               MOVE WS-YYYY(3:2)      TO RPT-YY                         R080101
               MOVE WS-MM             TO RPT-YM                         R080101
               MOVE WS-DD             TO RPT-DD                         R080101
               MOVE RPT-DT            TO RPT-ED-RUN-DT                  R080101
           ELSE                                                         R080101
              DISPLAY 'INVALID DATE3=' SMF30STD-X                       R080101
              MOVE '  N/A'            TO RPT-ED-RUN-DT                  R080101
           END-IF                                                       R080101
           MOVE SMF30TME-X            TO WS-TIME-INT                    R080101
           IF WS-TIME-INT NOT = 0                                       R080101
              COMPUTE RPT-HH = (WS-TIME-INT / 3600)                     R080101
              COMPUTE RPT-MM = ((WS-TIME-INT - 3600 * RPT-HH) / 60)     R080101
              COMPUTE RPT-SS = WS-TIME-INT -                            R080101
                      3600 * RPT-HH - RPT-MM * 60                       R080101
              COMPUTE WS-BRIDGE = SMF30TME-X - WS-TIME-INT              R080101
              COMPUTE RPT-HS = WS-BRIDGE * 100                          R080101
              MOVE RPT-TIME              TO RPT-ED-RUN-TIME             R080101
           ELSE                                                         R080101
              MOVE ' N/A  '              TO RPT-ED-RUN-TIME             R080101
           END-IF                                                       R080101
           MOVE SMF30STD-X               TO WS-CCYYDDD1                 R080101
           MOVE SMF30DTE-X               TO WS-CCYYDDD2                 R080101
           IF SMF30STD-X = SMF30DTE-X                                   R080101
              COMPUTE WS-TIME-SECOND ROUNDED = SMF30TME-X - SMF30SIT-X  R080101
           ELSE                                                         R080101
              IF WS-YYYY2 = WS-YYYY1                                    R080101
                 COMPUTE WS-TIME-SECOND ROUNDED =                       R080101
                         (SMF30TME-X - SMF30SIT-X) +                    R080101
                         (WS-DDD2 - WS-DDD1) * 24 * 3600                R080101
              ELSE                                                      R080101
                 COMPUTE WS-TIME-SECOND ROUNDED =                       R080101
                         (SMF30TME-X - SMF30SIT-X) +                    R080101
                         (WS-DDD2 - WS-DDD1) * 24 * 3600 +              R080101
                         (WS-YYYY2 - WS-YYYY1) * 365 * 24 * 3600        R080101
              END-IF                                                    R080101
           END-IF                                                       R080101
           MOVE WS-TIME-SECOND           TO RPT-RUN-R-Z                 R080101
           IF COMP-VALID                                                R080101
              MOVE SMF30SCC-X            TO RPT-JOB-RC-CODE             R080101
           ELSE                                                         R080101
              MOVE SPACE                 TO RPT-JOB-RC-CODE             R080101
           END-IF                                                       R080101
           IF PACC-VALID                                                R081211
              MOVE SMF30CPT-X            TO RPT-CPU                     R081211
              MOVE SMF30CPS-X            TO RPT-TCB                     R081211
              MOVE SMF30HPT-X            TO RPT-SRB                     R081211
           ELSE                                                         R081211
              MOVE ZERO                  TO RPT-CPU SMF30CPT-X          R081211
              MOVE ZERO                  TO RPT-TCB SMF30CPS-X          R081211
              MOVE ZERO                  TO RPT-SRB SMF30HPT-X          R081211
           END-IF                                                       R081211
           IF IO-VALID                                                  R081211
              MOVE SMF30TEP-X            TO RPT-EXCP                    R081211
              MOVE SMF30AIC-X            TO RPT-CONN                    R081211
           ELSE                                                         R081211
              MOVE ZERO                  TO RPT-EXCP SMF30TEP-X         R081211
              MOVE ZERO                  TO RPT-CONN SMF30AIC-X         R081211
           END-IF                                                       R081211
           COMPUTE SMF30-OUT-EXCP = SMF30-OUT-EXCP + SMF30TEP-X         R081211
           COMPUTE SMF30-OUT-CPU  = SMF30-OUT-CPU  + SMF30CPT-X         R081211
           MOVE SMF30RUD-X               TO RPT-USER                    R080101
           MOVE SMF30RUD-X               TO SMF30-OUT-USER              R081211
R08121*    MOVE SMF30PSN-X               TO RPT-INVOKE-PRC       R081211R080101
R08121*    IF COMP-VALID                                         R081211R080101
R08121*       MOVE SMF30ARC-X            TO RPT-ABEND            R081211R080101
R08121*    ELSE                                                  R081211R080101
R08121*       MOVE SPACE                 TO RPT-ABEND            R081211R080101
R08121*    END-IF                                                R081211R080101
           WRITE SMFRPT01-REC FROM SMF30-RPT-DTL-L1 AFTER 1             R080101
           COMPUTE WS-LINE-NO = WS-LINE-NO + 1                          R080101
           EXIT.                                                        R080101
      *                                                                 R080101
      *==================*                                              R080101
       R300-EXIT.                                                       R080101
      *==================*                                              R080101
      *                                                                 R080101
           EXIT.                                                        R080101
      *                                                                 R080101
      *                                                                 R080101
      *==================*                                              R080101
       R500-PRINT-TOT-END.                                              R080101
      *==================*                                              R080101
      *                                                                 R080101
           IF WS-LINE-NO + 5 > K-MAX-LINE-PER-PAGE                      R080101
              PERFORM R100-PRINT-HEADING  THRU R100-EXIT                R080101
              PERFORM R200-PRINT-TITLE    THRU R200-EXIT                R080101
           END-IF                                                       R080101
           MOVE WS-ALL-REC-NO         TO RPT-REC-NO                     R080101
           MOVE WS-ALL-JOB-NO         TO RPT-JOB-NO                     R080101
           MOVE 0                     TO WS-ALL-JOB-NO                  R080101
           WRITE SMFRPT01-REC  FROM SMF30-RPT-JOB-END-L2                R080101
           WRITE SMFRPT01-REC  FROM SMF30-RPT-TOT-L1                    R080101
           WRITE SMFRPT01-REC  FROM SMF30-RPT-TOT-L2                    R080101
           WRITE SMFRPT01-REC  FROM SMF30-RPT-TOT-L3                    R080101
           WRITE SMFRPT01-REC  FROM SMF30-RPT-TOT-L4                    R080101
           COMPUTE WS-LINE-NO = WS-LINE-NO + 5                          R080101
           EXIT.                                                        R080101
      *                                                                 R080101
      *==================*                                              R080101
       R500-EXIT.                                                       R080101
      *==================*                                              R080101
      *                                                                 R080101
           EXIT.                                                        R080101
      *                                                                 R080101
      *====================*                                            R080101
       R600-PRINT-JOB-BEGIN.                                            R080101
      *====================*                                            R080101
      *                                                                 R080101
           IF WS-LINE-NO + 1 > K-MAX-LINE-PER-PAGE                      R080101
              PERFORM R100-PRINT-HEADING  THRU R100-EXIT                R080101
              PERFORM R200-PRINT-TITLE    THRU R200-EXIT                R080101
           END-IF                                                       R080101
           WRITE SMFRPT01-REC  FROM SMF30-RPT-SUB-L1 AFTER 1            R080101
           COMPUTE WS-LINE-NO = WS-LINE-NO + 1                          R080101
           EXIT.                                                        R080101
      *                                                                 R080101
      *==================*                                              R080101
       R600-EXIT.                                                       R080101
      *==================*                                              R080101
      *                                                                 R080101
           EXIT.                                                        R080101
      *                                                                 R080101
      *====================*                                            R080101
       R700-PRINT-JOB-END.                                              R080101
      *====================*                                            R080101
      *                                                                 R080101
      * RUN  TIME                                                       R080101
           IF WS-LINE-NO + 2 > K-MAX-LINE-PER-PAGE                      R080101
              PERFORM R100-PRINT-HEADING  THRU R100-EXIT                R080101
              PERFORM R200-PRINT-TITLE    THRU R200-EXIT                R080101
           END-IF                                                       R080101
           MOVE WS-JOB-DT                TO WS-CCYYDDD1                 R080101
           MOVE SMF30DTE-X               TO WS-CCYYDDD2                 R080101
           COMPUTE WS-DAY = FUNCTION INTEGER-OF-DAY(SMF30DTE-X)         R081211
           COMPUTE WS-CCYYMMDD = FUNCTION DATE-OF-INTEGER(WS-DAY)       R081211
           MOVE WS-CCYYMMDD             TO SMF30-OUT-END-DTE            R081211
           DIVIDE SMF30TME-X BY 3600                                    R081211
                              GIVING WS-HH REMAINDER WS-REMIND          R081211
           DIVIDE WS-REMIND BY 60                                       R081211
                              GIVING WS-MM1 REMAINDER WS-SS             R081211
           MOVE WS-HH                   TO SMF30-OUT-END-TME(1:2)       R081211
           MOVE ':'                     TO SMF30-OUT-END-TME(3:1)       R081211
           MOVE WS-MM1                  TO SMF30-OUT-END-TME(4:2)       R081211
           MOVE ':'                     TO SMF30-OUT-END-TME(6:1)       R081211
           MOVE WS-SS                   TO SMF30-OUT-END-TME(7:2)       R081211
           IF WS-CCYYDDD1 = WS-CCYYDDD2                                 R080101
              COMPUTE WS-TIME-SECOND ROUNDED = SMF30TME-X - WS-JOB-TM   R080101
           ELSE                                                         R080101
              IF WS-CCYYDDD1 NOT = 0 AND WS-CCYYDDD2 NOT = 0            R080101
                 IF WS-YYYY2 = WS-YYYY1                                 R080101
                    COMPUTE WS-TIME-SECOND ROUNDED =                    R080101
                            (SMF30TME-X - WS-JOB-TM) +                  R080101
                            (WS-DDD2 - WS-DDD1) * 24 * 3600             R080101
                 ELSE                                                   R080101
                    COMPUTE WS-TIME-SECOND ROUNDED =                    R080101
                            (SMF30TME-X - WS-JOB-TM) +                  R080101
                            (WS-DDD2 - WS-DDD1) * 24 * 3600 +           R080101
                            (WS-YYYY2 - WS-YYYY1) * 365 * 24 * 3600     R080101
                 END-IF                                                 R080101
              ELSE                                                      R080101
                 COMPUTE WS-TIME-SECOND ROUNDED = 0                     R080101
              END-IF                                                    R080101
           END-IF                                                       R080101
           MOVE SMF30JBN-X            TO SMF30-OUT-JOB-NAME             R081211
           MOVE SMF30JNM-X            TO SMF30-OUT-JOB-ID               R081211
           MOVE WS-TIME-SECOND        TO RPT-END-RUN                    R080101
           MOVE WS-TIME-SECOND        TO SMF30-OUT-COST-TIME            R081211
           WRITE SMFRPT01-REC  FROM SMF30-RPT-JOB-END-L1 AFTER 1        R080101
           WRITE SMFRPT01-REC  FROM SMF30-RPT-JOB-END-L2 AFTER 1        R080101
           COMPUTE WS-LINE-NO = WS-LINE-NO + 2                          R080101
           WRITE SMF30-OUT-X                                            R080101
           INITIALIZE SMF30-OUT-X                                       R081211
           EXIT.                                                        R080101
      *                                                                 R080101
      *==================*                                              R080101
       R700-EXIT.                                                       R080101
      *==================*                                              R080101
      *                                                                 R080101
           EXIT.                                                        R080101
      *                                                                 R080101
      *======================*                                          R080101
       9000-CHECK-RETURN-CODE.                                          R080101
      *======================*                                          R080101
      *                                                                 R080101
           IF WS-RETURN-CODE > 4 AND WS-RETURN-CODE NOT = 10            R080101
           THEN                                                         R080101
              MOVE WS-RETURN-CODE      TO RETURN-CODE                   R080101
              PERFORM 9800-CLOSE-FILES  THRU 9800-EXIT                  R080101
           ELSE                                                         R080101
              IF WS-RETURN-CODE <= 4                                    R080101
                 DISPLAY 'SMF30EX2 WARNING'                             R080101
                 IF WS-RETURN-CODE = 02                                 R080101
                    DISPLAY 'SMF30EX2 DUPLICATE KEY WAS DETECTED'       R080101
                 END-IF                                                 R080101
      *          IF WS-RETURN-CODE = 04                                 R080101
      *          END-IF                                                 R080101
                 DISPLAY 'SMF30EX2 FILE LENGTH DID NOT CONFORM'         R080101
                 DISPLAY 'RECORD POS IS:' WS-ALL-REC-NO                 R080101
              ELSE                                                      R080101
                 DISPLAY 'SMF30EX2 NOW READ FILE TO END'                R080101
              END-IF                                                    R080101
           END-IF                                                       R080101
           EXIT.                                                        R080101
      *                                                                 R080101
      *==================*                                              R080101
       9000-EXIT.                                                       R080101
      *==================*                                              R080101
      *                                                                 R080101
           EXIT.                                                        R080101
      *                                                                 R080101
      *==================*                                              R080101
       9800-CLOSE-FILES.                                                R080101
      *==================*                                              R080101
      *                                                                 R080101
           CLOSE SMFRPT01                                               R080101
           CLOSE SMF30IN                                                R080101
           CLOSE SMF30OUT                                               R080101
           PERFORM 9999-EXIT-PROG     THRU 9999-EXIT.                   R080101
      *                                                                 R080101
      *==================*                                              R080101
       9800-EXIT.                                                       R080101
      *==================*                                              R080101
      *                                                                 R080101
           EXIT.                                                        R080101
      *                                                                 R080101
      *==================*                                              R080101
       9999-EXIT-PROG.                                                  R080101
      *==================*                                              R080101
      *                                                                 R080101
           STOP RUN.                                                    R080101
      *                                                                 R080101
      *==================*                                              R080101
       9999-EXIT.                                                       R080101
      *==================*                                              R080101
      *                                                                 R080101
           EXIT.                                                        R080101
      *                                                                 R080101
