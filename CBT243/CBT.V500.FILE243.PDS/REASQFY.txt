*PROCESS M,OPT(TIME),S,GN;
 /*
//PGMVCI  JOB 'IBI','V CHERNYAK',REGION=1000K,NOTIFY=PGMVC,CLASS=A,
//        MSGCLASS=A,MSGLEVEL=2
//PROCLIB  JCLLIB ORDER=PGMVYC.REASM.PLI
//        EXEC PLIXCL,PARM.LKED='LET,NCAL'
//PLI.SYSIN     DD DSN=PGMVYC.REASM.PLI(REASQFY),DISP=SHR
//PLI.SYSLIB    DD DSN=PGMVYC.REASM.PLI,DISP=SHR
//LKED.SYSLMOD  DD DSN=PGMVYC.REASM.SUBLOAD(REASQFY),DISP=SHR
//
 */
 REASQFY:
    PROC (Q) REORDER RETURNS(BIN FIXED(31));

 %NOPRINT;
 %INCLUDE REASMBAS;
 %PRINT;

 DCL     (Q,P)         PTR,
         F1            BIN FIXED(31)            INIT( 1),
         F4            BIN FIXED(31)            INIT( 4),
         F8            BIN FIXED(31)            INIT( 8),
         F50           BIN FIXED(31)            INIT(50),
         OFFSET        BIN FIXED(31)            INIT(-1),
         NROWS         BIN FIXED                INIT(0),
         TABLE         CHAR(9) STATIC INT       INIT('REASM04 '),
         C             CHAR(1)                  INIT(''),
         RQFNAME       CHAR(8)                  INIT(''),
         RQFOFF        BIN FIXED(31)            INIT(0),
         RQFOFF1       BIN FIXED(31)            INIT(0),
         RQFDESC       CHAR(50)                 INIT('');

 DCL     ISPLINK ENTRY EXT OPTIONS(ASM INTER RETCODE),
         (NULL,BIN,PLIRETV) BUILTIN;

     CALL ISPLINK ('VDEFINE ', 'C       ', C,       'CHAR ',  F1, ' ' );
     CALL ISPLINK ('VDEFINE ', 'RQFNAME ', RQFNAME, 'CHAR ',  F8, ' ' );
     CALL ISPLINK ('VDEFINE ', 'RQFOFF  ', RQFOFF,  'HEX  ',  F4, ' ' );
     CALL ISPLINK ('VDEFINE ', 'RQFOFF1 ', RQFOFF1, 'FIXED ', F4, ' ' );
     CALL ISPLINK ('VDEFINE ', 'RQFDESC ', RQFDESC, 'CHAR ', F50, ' ' );

     CALL ISPLINK ('TBCREATE', TABLE, ' ',
          '(C,RQFNAME,RQFOFF,RQFOFF1,RQFDESC)',
          'NOWRITE ', ' ');
     IF PLIRETV() ¬= 0 THEN DO;
        CALL ISPLINK('SETMSG ', 'REAS074 ');
        GOTO L_DEL;
     END;

     P = Q;
     DO WHILE(P¬=NULL);
        RQFNAME = P->QUALIFY.NAME;
        RQFOFF  = P->QUALIFY.OFF;
        RQFOFF1 = RQFOFF;
        RQFDESC = P->QUALIFY.DESC;
        CALL ISPLINK ('TBADD ', TABLE);
        NROWS   = NROWS+1;
        P       = P->QUALIFY.NEXT;
     END;

     CALL ISPLINK ('TBSORT  ', TABLE, '(RQFOFF1,N)');
     CALL ISPLINK ('TBTOP   ', TABLE);

 L_DISPL:
     CALL ISPLINK ('TBDISPL ', TABLE, TABLE, ' ', ' ', F1, F1,
                   'YES ', ' ', ' ');
     IF PLIRETV() <  8 THEN DO;
        SELECT (C);
          WHEN ('S','*',':') DO;
            OFFSET = RQFOFF;
            GOTO L_END;
          END;
          OTHER DO;
            CALL ISPLINK('SETMSG ','REAS073 ');
            GOTO L_DISPL;
          END;
        END;
     END;

 L_END:
     CALL ISPLINK ('TBEND   ', TABLE);
 L_DEL:
     CALL ISPLINK ('VDELETE ',
          '(C,RQFNAME,RQFOFF,RQFOFF1,RQFDESC)');

     RETURN(OFFSET);
 END REASQFY;
