 /*
//PGMVCI  JOB 'IBI','V CHERNYAK',REGION=1000K,NOTIFY=PGMVC,CLASS=A,
//        MSGCLASS=A,MSGLEVEL=(1,1)
//PROCLIB  JCLLIB ORDER=PGMVYC.REASM.PLI
//        EXEC PLIXCL,PARM.LKED='LET,NCAL'
//PLI.SYSIN     DD DSN=PGMVYC.REASM.PLI(REASMRC),DISP=SHR
//PLI.SYSLIB    DD DSN=PGMVYC.REASM.PLI,DISP=SHR
//LKED.SYSLMOD  DD DSN=PGMVYC.REASM.SUBLOAD(REASMRC),DISP=SHR
//
 */
 REASMRC:
   PROC(DDNAME, OPT) RETURNS(BIN FIXED);
   DCL  DDNAME CHAR(8),     /* RECOVERY DD NAME */
        OPT    CHAR(1);     /* OPTION: 'C'-CREATE, 'L'-CREATE AS LIB */
                            /*         'D'-DELETE                    */

   DCL  ISPLINK        EXT ENTRY OPTIONS(ASM INTER RETCODE);
   DCL  DYNWORK        BIN FIXED(31)  INIT(0);
   DCL  DYNMSG         CHAR(256);
   DCL  1 DYNAREA      UNALIGNED,
          2 DDNAME     CHAR(8),
          2 DSNAME     CHAR(44),
          2 DSORG      BIT(16),
          2 VOL        CHAR(6);
   DCL  1 DYNINFO      UNALIGNED,
          2 DDNAME     CHAR(8),
          2 DSNAME     CHAR(44),
          2 MEMBER     CHAR(8),
          2 STATUS     BIT(8),
          2 DISP       BIT(8),
          2 COND_DISP  BIT(8),
          2 DSORG      BIT(16),
          2 LIMIT      BIN FIXED(15),
          2 ATTRIBUTE  BIT(8),
          2 LAST_ENTRY BIT(8),
          2 TYPE       BIT(8);

   DCL  DYNAM   ENTRY OPTIONS(ASSEMBLER, RETCODE);
   DCL  (PLIRETV, INDEX, SUBSTR) BUILTIN;
   DCL  DSNAME  CHAR(44) VAR,
        JOBNAME CHAR(8), JN CHAR(8) VAR,
        TXT     CHAR(255) VAR,
        T       CHAR(255),
        #8      BIN FIXED(31) INIT(8),
        RC      BIN FIXED INIT(0);

   CALL ISPLINK('VCOPY ', 'ZUSER ', #8, JOBNAME, 'MOVE ');
   JN = JOBNAME;
   IF INDEX(JN,' ') >1 THEN JN = SUBSTR(JN,1,INDEX(JN,' ')-1);
   DSNAME = JN || '.REASM.RECOVERY';
   SELECT (OPT);
     WHEN   ('D');
     WHEN   ('C','L') DO;
       CALL DYNAM(DYNWORK, 'INIT ', DYNMSG);
       CALL DYNAM(DYNWORK, 'INFO ', DYNINFO,
                  'DD=' || DDNAME || ';');
       IF (PLIRETV ¬= 0)  |
          (DYNINFO.TYPE = '10000000'B) |
          (DYNINFO.DSNAME  = 'NULLFILE ') THEN DO;
          IF PLIRETV=0 THEN
             CALL DYNAM(DYNINFO, 'UNALLOC ',
                        'DD=' || DDNAME || ';');
          TXT = 'DD=' || DDNAME || ' DSN=' || DSNAME;
          T = TXT || ' SHR;';
          CALL DYNAM(DYNWORK, 'ALLOCR ', DYNAREA, T);
          /* PUT SKIP LIST(MESSAGE); */
          IF PLIRETV ¬= 0 THEN DO;
             TXT = TXT || ' NEW CATLG RECFM=UT BLKSIZE=32000';
             TXT = TXT || ' TRK PRIMARY=5 SECONDARY=5';
             IF OPT='C' THEN
                TXT = TXT || ' DSORG=PS';
             ELSE
                TXT = TXT || ' DSORG=PO DIRECTORY=2';
             T = TXT || ';';
             CALL DYNAM(DYNWORK, 'ALLOCR ', DYNAREA, T);
             /* PUT SKIP LIST(MESSAGE); */
             RC = PLIRETV;
          END;
       END;
       CALL DYNAM(DYNWORK, 'END ');
     END;
   END;

   RETURN(RC);
 END REASMRC;
