*PROCESS M,OPT(TIME),S,GN;
 /*
//PGMVCI JOB 'IBI','V CHERNYAK',REGION=1000K,NOTIFY=PGMVC,CLASS=A,
//        MSGCLASS=A,MSGLEVEL=(1,1)
//PROCLIB  JCLLIB ORDER=PGMVYC.REASM.PLI
//        EXEC PLIXCL,PARM.LKED='LET,NCAL'
//PLI.SYSIN     DD DSN=PGMVYC.REASM.PLI(REASLIB),DISP=SHR
//PLI.SYSLIB    DD DSN=PGMVYC.REASM.PLI,DISP=SHR
//LKED.SYSLMOD  DD DSN=PGMVYC.REASM.SUBLOAD(REASLIB),DISP=SHR
//
 */
 REASLIB: PROC(DDNAME,MODE,CARD) REORDER RETURNS(BIN FIXED);
   DCL    DDNAME   CHAR(8),
          MODE     CHAR(*),
          CARD     CHAR(*),
          RC       BIN FIXED INIT(1),
          ERRMSG   CHAR(8)   INIT(''),
          MTIME    CHAR(5)   INIT(''),
          MUSER    CHAR(8)   INIT(''),
          MDATE    CHAR(8)   INIT(''),
          MEMBER   CHAR(12)  INIT(''),
          MEMOUT   CHAR(8)   STATIC INT INIT(''),
          #C       CHAR(80)  INIT(''),
          DATAIDV  CHAR(8)   INIT(''),
          ORG      CHAR(2)   INIT(''),
          PRJ      CHAR(8)   INIT(''),
          LIB      CHAR(8)   INIT(''),
          TYP      CHAR(8)   INIT(''),
          DSN      CHAR(44)  INIT(''),
          VOL      CHAR(6)   INIT(''),
          PSWD     CHAR(8)   INIT(''),
          YN       CHAR(3)   INIT(''),
          F2                 INIT(2)  BIN FIXED(31),
          F3                 INIT(3)  BIN FIXED(31),
          F5                 INIT(5)  BIN FIXED(31),
          F6                 INIT(6)  BIN FIXED(31),
          F8                 INIT(8)  BIN FIXED(31),
          F12                INIT(12) BIN FIXED(31),
          F44                INIT(44) BIN FIXED(31),
          F80                INIT(80) BIN FIXED(31),
          (CSIZE,ISIZE,MSIZE)INIT(0)  BIN FIXED(31),
          ISPLINK            ENTRY  EXT OPTIONS(ASM INTER RETCODE),
          (PLIRETV)          BUILTIN;


   CALL ISPLINK ('VDEFINE ', 'DATAIDV ', DATAIDV,  'CHAR ',  F8 );
   CALL ISPLINK ('VDEFINE ', 'ORG     ', ORG,      'CHAR ',  F2 );
   CALL ISPLINK ('VDEFINE ', 'PRJ2    ', PRJ,      'CHAR ',  F8 );
   CALL ISPLINK ('VDEFINE ', 'LIB2    ', LIB,      'CHAR ',  F8 );
   CALL ISPLINK ('VDEFINE ', 'TYP2    ', TYP,      'CHAR ',  F8 );
   CALL ISPLINK ('VDEFINE ', 'MEM2    ', MEMOUT,   'CHAR ',  F8 );
   CALL ISPLINK ('VDEFINE ', 'DSN2    ', DSN,      'CHAR ',  F44);
   CALL ISPLINK ('VDEFINE ', 'VOL2    ', VOL,      'CHAR ',  F6 );
   CALL ISPLINK ('VDEFINE ', 'PSWD2   ', PSWD,     'CHAR ',  F8 );
   CALL ISPLINK ('VDEFINE ', 'YN2     ', YN,       'CHAR ',  F3 );
   CALL ISPLINK ('VDEFINE ', '#C      ', #C,       'CHAR ',  F80);

   SELECT (MODE);
     WHEN ('OPEN') DO;
        MEMOUT = CARD;
 L_DISPL:
        CALL ISPLINK ('DISPLAY ', 'REASM05 ');
        RC = PLIRETV();
        IF RC ¬= 0 THEN GOTO L_RET;
        CALL ISPLINK ('LMINIT ', 'DATAIDV ', PRJ, LIB, ' ', ' ',
                      ' ', TYP, DSN, ' ', VOL, PSWD,
                      'EXCLU ', 'ORG ');
        DDNAME = DATAIDV;
        RC = PLIRETV();
        IF RC¬=0 THEN DO;
           CALL ISPLINK('VCOPY  ', 'ZERRMSG ', F8, ERRMSG, 'MOVE ');
           CALL ISPLINK('SETMSG ', ERRMSG);
           GOTO L_DISPL;
        END;
        IF ORG ¬= 'PO'  &  ORG ¬= 'POU' THEN DO;
           CALL ISPLINK('SETMSG ', 'REAS065 ');
           GOTO L_DISPL;
        END;

        CALL ISPLINK ('LMOPEN ', DDNAME, 'OUTPUT ');
        RC = PLIRETV();

        IF RC=0 THEN DO;
           IF MEMOUT='*' | MEMOUT=':' | MEMOUT=' ' THEN DO;
              CALL ISPLINK ('LMMDISP ', DDNAME, 'DISPLAY ', ' ');
              RC = PLIRETV();
              CALL ISPLINK ('LMMDISP ', DDNAME, 'FREE ');
              IF RC=0 THEN DO;
                 CALL ISPLINK ('VCOPY ','ZLMEMBER ',F12,MEMBER,'MOVE ');
                 MEMOUT = MEMBER;
 L_RETRY:        CALL ISPLINK ('LMCLOSE ', DDNAME);
                 CALL ISPLINK ('LMFREE  ', DDNAME);
                 GOTO L_DISPL;
              END;
           END;
           ELSE DO;
              IF YN = 'NO' THEN DO;        /* DO NOT REPLACE MEMBER */
                 CALL ISPLINK ('LMCLOSE ', DDNAME);
                 CALL ISPLINK ('LMOPEN  ', DDNAME, 'INPUT  ');
                 CALL ISPLINK ('LMMFIND ', DDNAME, MEMOUT);
                 IF PLIRETV()=0 THEN DO;  /* MEMBER EXISTS YET     */
                    CALL ISPLINK('SETMSG ', 'REAS048 ');
                    GOTO L_RETRY;
                 END;
                 CALL ISPLINK ('LMCLOSE ', DDNAME);
                 CALL ISPLINK ('LMOPEN  ', DDNAME, 'OUTPUT ');
              END;
           END;
        END;
        CSIZE,ISIZE,MSIZE = 0;
        CARD = MEMOUT;
     END;

     WHEN ('CLOSE') DO;
 L_CLOSE:
        CALL ISPLINK ('LMMREP  ', DDNAME, MEMOUT, 'YES ');
        CALL ISPLINK ('LMCLOSE ', DDNAME);

        CALL ISPLINK ('LMOPEN  ', DDNAME, 'INPUT  ');
        CALL ISPLINK ('VCOPY ', 'ZTIME ', F5, MTIME, 'MOVE ');
        CALL ISPLINK ('VCOPY ', 'ZDATE ', F8, MDATE, 'MOVE ');
        CALL ISPLINK ('VCOPY ', 'ZUSER ', F8, MUSER, 'MOVE ');
        CALL ISPLINK ('LMMSTATS', DDNAME, MEMOUT, ' ', ' ',
                      MDATE, MDATE, MTIME, CSIZE, ISIZE, MSIZE, MUSER);
        CALL ISPLINK ('LMCLOSE ', DDNAME);
        CALL ISPLINK ('LMFREE  ', DDNAME);

        CALL ISPLINK('SETMSG ', 'REAS005 ');
        RC = 0;
     END;

     WHEN ('WRITE') DO;
        #C = CARD;
        CALL ISPLINK ('LMPUT ', DDNAME, 'INVAR ', '#C ', F80, MEMOUT,
                      'NOBSCAN ', ' ', ' ', 'YES');
        RC = PLIRETV();
        IF RC ¬= 0 THEN GOTO L_CLOSE;
        ISIZE = ISIZE+1;
        CSIZE = CSIZE+1;
     END;
   END;

 L_RET:
   CALL ISPLINK ('VDELETE ',
   '(DATAIDV,ORG,PRJ2,LIB2,TYP2,DSN2,VOL2,PSWD2,MEM2,YN2,#C)');

   RETURN (RC);

 END REASLIB;
