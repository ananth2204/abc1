*PROCESS M,OPT(TIME),S,GN;
 /*
//PGMVCM JOB 'IBI','V CHERNYAK',REGION=1000K,NOTIFY=PGMVC,CLASS=A,
//        MSGCLASS=A,MSGLEVEL=(1,1)
//PROCLIB  JCLLIB ORDER=PGMVYC.REASM.PLI
//        EXEC PLIXCL,PARM.LKED='LET,NCAL'
//PLI.SYSIN     DD DSN=PGMVYC.REASM.PLI(REASMLB),DISP=SHR
//PLI.SYSLIB    DD DSN=PGMVYC.REASM.PLI,DISP=SHR
//LKED.SYSLMOD  DD DSN=PGMVYC.REASM.SUBLOAD(REASMLB),DISP=SHR
//
 */
 REASMLB:
    PROC (DDNAME) REORDER RETURNS(CHAR(8) VAR);

     DCL  DDNAME   CHAR(8);
     DCL  DID      CHAR(8);
     DCL  MLBMEM    CHAR(8) VAR INIT('?');
     DCL  MEM       CHAR(8);
     DCL  ZPDFREL   CHAR(8);
     DCL  REL       BIN FIXED INIT(0);
     DCL  F8        BIN FIXED(31) INIT(8);
     DCL  #F8       BIN FIXED(31);

     DCL     ISPLINK ENTRY EXT OPTIONS(ASM INTER RETCODE),
            (SUBSTR,UNSPEC,DIM,BIN,PLIRETV) BUILTIN;

     #F8 = 8;
     CALL ISPLINK ('VCOPY ', 'ZPDFREL ', #F8, ZPDFREL, 'MOVE ');
     IF  PLIRETV() = 0 THEN
         REL = BIN(SUBSTR(ZPDFREL,5,1));

     CALL ISPLINK ('VDEFINE ', 'DID     ', DID,     'CHAR  ',  F8 );

     CALL ISPLINK ('LMINIT ', 'DID', ' ', ' ', ' ', ' ', ' ', ' ',
                   ' ', DDNAME, ' ', ' ', 'SHR', ' ');
     IF PLIRETV() = 0 THEN DO;
        CALL ISPLINK ('LMOPEN  ', DID, 'INPUT ', ' ', ' ' );
        IF REL>=3 THEN
           CALL ISPLINK ('ADDPOP  ', ' ', ' ', ' ');
        CALL ISPLINK ('LMMDISP ', DID, 'DISPLAY ', '*');
        IF PLIRETV() = 0 THEN DO;
           #F8 = 8;
           CALL ISPLINK ('VCOPY ',   'ZLMEMBER ', #F8, MEM,'MOVE ');
           MLBMEM = MEM;
        END;
        IF MLBMEM='' THEN MLBMEM = '?';
        IF REL>=3 THEN
           CALL ISPLINK ('REMPOP  ', ' ');
        CALL ISPLINK ('LMCLOSE ', DID);
        CALL ISPLINK ('LMFREE  ', DID);
     END;

     CALL ISPLINK ('VDELETE ','(DID)' );
     RETURN(MLBMEM);
 END REASMLB;
