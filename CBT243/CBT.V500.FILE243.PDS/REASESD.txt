*PROCESS M,OPT(TIME),S,GN,OL,XREF;
 /*
//PGMVCI  JOB 'IBI','V CHERNYAK',REGION=1000K,NOTIFY=PGMVC,CLASS=A,
//        MSGCLASS=A,MSGLEVEL=(1,1)
//PROCLIB  JCLLIB ORDER=PGMVYC.REASM.PLI
//        EXEC PLIXCL,PARM.LKED='LET,NCAL'
//PLI.SYSIN     DD DSN=PGMVYC.REASM.PLI(REASESD),DISP=SHR
//PLI.SYSLIB    DD DSN=PGMVYC.REASM.PLI,DISP=SHR
//LKED.SYSLMOD  DD DSN=PGMVYC.REASM.SUBLOAD(REASESD),DISP=SHR
//
 */
 REASESD:
    PROC (ESDARR, DIME) REORDER RETURNS(CHAR(8));

 DCL 1   ESDARR(*),
      2  NAME          CHAR(8),
      2  TYPE          BIT(8),
      2  OFFSET        BIT(24),
      2  ALIGN         BIT(8),
      2  LENGTH        BIT(24),
      2  REF           BIN FIXED,
      2  ID            BIN FIXED,
         DIME          BIN FIXED;

 DCL     (I,J),
         #CSECT        BIT(8) STATIC INT INIT('00000000'B),
         #ENTRY        BIT(8) STATIC INT INIT('00000011'B),
         #MASK         BIT(8) STATIC INT INIT('00001111'B),
         F1            BIN FIXED(31) STATIC INT INIT(1),
         F4            BIN FIXED(31) STATIC INT INIT(4),
         F5            BIN FIXED(31) STATIC INT INIT(5),
         F8            BIN FIXED(31) STATIC INT INIT(8),
         F60           BIN FIXED(31) STATIC INT INIT(60),
         CSECT         CHAR(8)                  INIT('........'),
         NROWS         BIN FIXED                INIT(0),
         ROW           BIN FIXED(31),
         TABNAME       CHAR(8),
         BYTE          BIT(8),
         LOCVAR        CHAR(60) VAR             INIT(''),
         TABLE         CHAR(9) STATIC INT       INIT('REASM03 '),
         RTBCRP        BIN FIXED(31),
         RTBCMD        CHAR(60)                 INIT(''),
         C             CHAR(1)                  INIT(''),
         RTBNAME       CHAR(8)                  INIT(''),
         RTBOFF        BIN FIXED(31)            INIT(0),
         RTBOFF1       BIN FIXED(31)            INIT(0),
         RTBLEN        BIN FIXED(31)            INIT(0),
         RTBLENB       BIN FIXED(31)            INIT(0),
         RTBTYPE       CHAR(5)                  INIT(''),
         RTBREF        CHAR(8)                  INIT('');

 DCL     ISPLINK ENTRY EXT OPTIONS(ASM INTER RETCODE),
         (VERIFY,SUBSTR,UNSPEC,DIM,BIN,PLIRETV) BUILTIN;

     CALL ISPLINK ('VDEFINE ', 'RTBCRP  ', RTBCRP,  'FIXED ',  F4 );
     CALL ISPLINK ('VDEFINE ', 'RTBCMD  ', RTBCMD,  'CHAR  ', F60 );
     CALL ISPLINK ('VDEFINE ', 'C       ', C,       'CHAR  ',  F1 );
     CALL ISPLINK ('VDEFINE ', 'RTBNAME ', RTBNAME, 'CHAR  ',  F8 );
     CALL ISPLINK ('VDEFINE ', 'RTBOFF  ', RTBOFF,  'HEX   ',  F4 );
     CALL ISPLINK ('VDEFINE ', 'RTBOFF1 ', RTBOFF1, 'FIXED ',  F4 );
     CALL ISPLINK ('VDEFINE ', 'RTBLEN  ', RTBLEN,  'HEX   ',  F4 );
     CALL ISPLINK ('VDEFINE ', 'RTBLENB ', RTBLENB, 'FIXED ',  F4 );
     CALL ISPLINK ('VDEFINE ', 'RTBTYPE ', RTBTYPE, 'CHAR  ',  F5 );
     CALL ISPLINK ('VDEFINE ', 'RTBREF  ', RTBREF,  'CHAR  ',  F8 );

     CALL ISPLINK ('TBCREATE', TABLE, '(RTBNAME)',
          '(C,RTBOFF,RTBOFF1,RTBLEN,RTBLENB,RTBTYPE,RTBREF)',
          'NOWRITE ', ' ');
     IF PLIRETV() ¬= 0 THEN DO;
        CALL ISPLINK('SETMSG ', 'REAS071 ');
        GOTO L_DEL;
     END;

     DO I=1 TO DIME;
        UNSPEC(BYTE) = UNSPEC(ESDARR(I).TYPE) & #MASK;
        C       = '';
        RTBTYPE = '';
        RTBREF  = '';
        RTBNAME = ESDARR(I).NAME;
        RTBOFF  = BIN(ESDARR(I).OFFSET,31);
        RTBOFF1 = RTBOFF;
        RTBLEN  = BIN(ESDARR(I).LENGTH,31);
        SELECT (UNSPEC(BYTE));
          WHEN (#CSECT) DO;
             RTBTYPE = 'CSECT';
          END;
          WHEN (#ENTRY) DO;
             RTBTYPE = 'ENTRY';
             DO J=1 TO DIME;
                IF (ESDARR(J).ID = ESDARR(I).REF) & (J¬=I) THEN DO;
                   RTBREF = ESDARR(J).NAME;
                   RTBLEN = ESDARR(J).LENGTH;
                END;
             END;
          END;
          OTHER;
        END;
        RTBLENB = RTBLEN;

        IF RTBTYPE ¬= '' THEN DO;
           CALL ISPLINK ('TBADD ', TABLE);
           NROWS = NROWS+1;
        END;
     END;

     IF NROWS=0 THEN DO;
        CALL ISPLINK('SETMSG ', 'REAS072 ');
        GOTO L_END;
     END;

 L_TBTOP:
     CALL ISPLINK ('TBTOP   ', TABLE);
     ROW = 1;
 L_DISPL:
     IF ROW > 1 THEN DO;
        CALL ISPLINK ('TBTOP   ', TABLE);
        CALL ISPLINK ('TBSKIP  ', TABLE, ROW);
     END;
     CALL ISPLINK ('TBDISPL ', TABLE, TABLE, ' ', ' ', ROW, F1,
                   'YES ', ' ', ' ');

     IF PLIRETV() = 0  & RTBCMD ¬= '' THEN DO;

        IF SUBSTR(RTBCMD,1,1) = 'L' THEN DO;
           LOCVAR = SUBSTR(RTBCMD,2);
           IF LOCVAR¬='' THEN DO;
              RTBNAME = SUBSTR(LOCVAR,VERIFY(LOCVAR,' '));
              CALL ISPLINK ('TBTOP   ', TABLE);
              CALL ISPLINK ('TBSCAN ', TABLE, '(RTBNAME)',
                           ' ',' ','NEXT ','NOREAD ',
                           'RTBCRP ', '(GE) ' );
              IF PLIRETV() = 0 THEN DO;
                CALL ISPLINK ('TBTOP   ', TABLE);
                CALL ISPLINK ('TBSKIP ', TABLE, RTBCRP);
              END;
           END;
           GOTO L_DISPL;
        END;

        SELECT (RTBCMD);
          WHEN  ('SA', 'S A', 'SORT A', 'SORT ADDR') DO;
            CALL ISPLINK ('TBSORT ', TABLE, '(RTBOFF1,N)');
            CALL ISPLINK('SETMSG ','REAS077 ');
            GOTO L_TBTOP;
          END;
          WHEN  ('SN', 'S N', 'SORT N', 'SORT NAME', 'SORT') DO;
            CALL ISPLINK ('TBSORT ', TABLE, '(RTBNAME,C)');
            CALL ISPLINK('SETMSG ','REAS078 ');
            GOTO L_TBTOP;
          END;
          WHEN  ('SL', 'S L', 'SORT L', 'SORT LEN') DO;
            CALL ISPLINK ('TBSORT ', TABLE,
                          '(RTBLENB,N,D,RTBOFF1,N,A)');
            CALL ISPLINK('SETMSG ','REAS078 ');
            GOTO L_TBTOP;
          END;
          WHEN (' ')
            GOTO L_DISPL;
          OTHER DO;
            CALL ISPLINK('SETMSG ','REAS075 ');
            GOTO L_TBTOP;
          END;
        END;
     END;

     IF PLIRETV() < 8 THEN DO;
        SELECT (C);
          WHEN ('S','*',':','B') DO;
            CSECT = RTBNAME;
            GOTO L_END;
          END;
          WHEN (' ')
            GOTO L_DISPL;
          OTHER DO;
            CALL ISPLINK ('TBPUT ', TABLE);
            CALL ISPLINK('SETMSG ','REAS073 ');
            GOTO L_DISPL;
          END;
        END;
     END;

 L_END:
     CALL ISPLINK ('TBEND   ', TABLE);
 L_DEL:
     CALL ISPLINK ('VDELETE ',
     '(RTBCMD, C, RTBNAME, RTBOFF, RTBLEN, RTBTYPE, RTBREF)');
     CALL ISPLINK ('VDELETE ',
     '(RTBCRP, RTBOFF1)' );

     RETURN(CSECT);
 END REASESD;
