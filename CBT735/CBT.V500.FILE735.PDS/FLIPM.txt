      ISREDIT MACRO NOPROCESS
      ISREDIT PROCESS RANGE C
      ISREDIT (CMD) = RANGE_CMD
  /***************************************************************/
  /*  WRITTEN BY TOM BARTHOLD (TBARTHOLD@COMCAST.NET)            */
  /***************************************************************/
  /* THIS MACRO IS INTENDED FOR USE IN COBOL PROGRAMS.           */
  /* IT REVERSES MOVE STATEMENTS THAT ARE BLOCKED OUT BY         */
  /* "CC" LINE COMMANDS. THE MOVE STATEMENT MUST BE COMPLETE ON  */
  /* ONE LINE.                                                   */
  /*     E.G. MOVE FIELD1 (SUB) TO FIELD2 (SUB) BECOMES          */
  /*          MOVE FIELD2 (SUB) TO FIELD1 (SUB)                  */
  /* MACRO ALIGN IS CALLED FROM THIS MACRO                       */
  /* YOU CAN COMMENT OUT THE CALL TO ALIGN IF DESIRED            */
  /***************************************************************/

      IF &CMD NE &STR(C) THEN +
         DO
           WRITE 'NO "C" LINE COMMANDS USED'
           WRITE 'BLOCK OUT LINES WITH CC'
           EXIT
         END

      ISREDIT NUMBER ON COBOL
      ISREDIT (FIRST)=LINENUM .ZFRANGE
      ISREDIT (LAST) =LINENUM .ZLRANGE
      SET &CNT=&LAST-&FIRST+1
      SET &CUR=&FIRST
      DO WHILE &CUR ¼> &LAST
         ISREDIT (LV) = LINE &CUR
         SET &F=1
         SET &T=4
         DO UNTIL &SUBSTR(&F:&T,&LV)=&STR(MOVE) | &F > 62
            SET &F=&F+1
            SET &T=&F+3
         END
         IF &F > 62 THEN GOTO ENDLINE
         SET &MLB=&STR()
         SET &ML =&F-6
         DO WHILE &ML > 0
            SET &MLB=&STR(&MLB.&STR( ))
            SET &ML = &ML-1
         END
         SET &F=&F+4
         SET &T=&T+4
         DO UNTIL &SUBSTR(&F:&F,&LV)¼=&STR( ) | &F > 62
            SET &F=&F+1
         END
         IF &F > 62 THEN GOTO ENDLINE
         SET &FROM=&STR( )
         SET &F=&F-1
         SET &T=&F+3
         DO UNTIL &SUBSTR(&F:&T,&LV)=&STR( TO ) | &F > 62
            SET &FROM=&STR(&FROM.&SUBSTR(&F:&F,&LV))
            SET &F=&F+1
            SET &T=&F+3
         END
         IF &F > 62 THEN GOTO ENDLINE
         SET &F=&F+4
         SET &T=&F+4
         DO UNTIL &SUBSTR(&F:&F,&LV)¼=&STR( ) | &F > 66
            SET &F=&F+1
         END
         IF &F > 66 THEN GOTO ENDLINE
         SET &F=&F-1
         SET &TO=&STR( )
         DO UNTIL &F>66
            SET &TO=&STR(&TO.&SUBSTR(&F:&F,&LV))
            SET &F=&F+1
         END
         SET &LF=&LENGTH(&STR(&FROM))
         SET &LT=&LENGTH(&STR(&TO))
         DO WHILE &SUBSTR(&LF:&LF,&FROM)=&STR( )
            SET &LF=&LF-1
         END
         DO WHILE &SUBSTR(&LT:&LT,&TO)=&STR( )
            SET &LT=&LT-1
            IF &LT=0 THEN GOTO ENDLINE
         END
         SET &PER=&STR( )
         IF &SUBSTR(&LT:&LT,&TO)=&STR(.) THEN +
            DO
              SET &LT=&LT-1
              SET &PER=&STR(.)
            END
         SET &FR=&STR(&SUBSTR(2:&LF,&FROM))
         SET &T =&STR(&SUBSTR(1:&LT,&TO))
         SET LDATA = &STR(     &MLB.MOVE&T TO&FR&PER)
         ISREDIT LINE &CUR = (LDATA)
ENDLINE: SET &CUR=&CUR+1
     END
/**  LOGIT FLIPM */
     ALIGN
     ISREDIT RESET
     ISREDIT RENUM STD COBOL
     EXIT
