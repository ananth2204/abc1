JRP100 TITLE 'JRP MANAGER'
***********************************************************************
*                                                                     *
*             MODULE NAME = JRP100                                    *
*                                                                     *
*             DESCRIPTIVE NAME =  INITIALIZATION, MVS COMMAND MONITOR,*
*                               SUBTASK MONITOR AND 100.              *
*                                                                     *
*             AUTHOR = MIKE SCHOR MAN -                               *
*                                                                     *
*             COPYRIGHT = 1/8/84                                      *
*                                                                     *
*             MODIFIED =                                              *
*                SKV - 11/20/02 - ALLOWED IN COLUMN 1 OF EITHER       *
*                      A * OR A # TO DESIGNATE A COMMENT CARD.        *
*                SKV - 12/02/02 - CREATED AN ALIS SHUTDOWN = ICLOSE   *
*                      ALSO SHUT.                                     *
*                SKV - 01/23/06 - CREATED OPTION TO SPECIFY JRP       *
*                      APPLICATION NAME. (JRPAPP=)                    *
*                                                                     *
*             STATUS =                                                *
*                                                                     *
*             FUNCTION =                                              *
*                READ INITIALIZATION DATA SET FROM DD JRPDATA,        *
*                PASS INFO TO P110 VIA LINK,ATTACH JRP200 SUBTASK, *
*                MONITOR MVS COMM-TASK FOR MODIFY COMMANDS,AND MAKE   *
*                CLOSE DOWN JUDGEMENTS.                               *
*                                                                     *
*              NOTES =                                                *
*                                                                     *
*                 DEPENDENCIES = VTAM                             *SKV*
*                                JRPDMAC AND/OR JRPSMAC MUST NOT      *
*                              BE CHANGED WITHOUT REASSEMBLY.         *
*                                                                     *
*                                                                     *
*                 REGISTER CONVENTIONS = STANDARD CONVENTIONS.        *
*                    REGISTERS 1 TO 11 = WORK REGISTERS               *
*                    REGISTER  12      = ADDRESSABILITY TO JRP110     *
*                                        CSECT                        *
*                    REGISTER  13      = SAVE AREA REGISTER AND       *
*                                        ADDRESSABILITY TO GETMAINED  *
*                                        WORK AREA                    *
*                    REGISTERS 14,15   = WORK REGISTERS               *
*                                                                     *
*                                                                     *
*                PROCESSOR = ASM                                      *
*                                                                     *
*                MODULE SIZE =                                        *
*                                                                     *
*                ATTRIBUTES = SCHEDULER KEY 0, SUPERVISOR STATE(AC=1) *
*                                                                     *
*             ENTRY POINTS = JRP100 (ONLY ENTRY POINT)                *
*                                                                     *
*                LINKAGE =                                            *
*                   FROM MVS AS ADDRESS SPACE.                        *
*                                                                     *
*             INPUT = JRPDATA DD FOR INITIALIZATION,FROM MVS          *
*                OPERATOR MODIFY OR STOP COMMAND OR BY THE USE    *SKV*
*                OF A WTOR.                                       *SKV*
*                                                                     *
*             OUTPUT = NONE                                           *
*                                                                     *
*             EXIT - NORMAL = AT PROGRAM END VIA BRANCH REGISTER 14   *
*                                                                     *
*                                                                     *
*             EXTERNAL REFERENCES =                                   *
*                                                                     *
*                ROUTINES = JRP110.                                   *
*                                                                     *
*                SUBTASKS = JRP200==>JRP300                           *
*                                                                     *
*                DATA AREAS = NONE                                    *
*                                                                     *
*                CONTROL BLOCKS = PWT                                 *
*                                                                     *
*                                                                     *
*             TABLES = DATA AREA TO BE GETMAINED.  MAPPED BY DSECT    *
*                BEGINNING AT LABEL P1DWA.                            *
*                                                                     *
*             MACROS = JRPSAVE,JRPRTRN,EXTRACT,QEDIT,WAIT,GETMAIN,    *
*                      LINK,ATTACH,STATUS,DETACH,OPEN,CLOSE,ACB,      *
*                      GET,FREEPOOL,WTO,MODESET                       *
*                                                                     *
*             CHANGE ACTIVITY =                                       *
*                 SKV 12/04/05 CHANGED TO ACCEPT EITHER WTOR OR       *
*                    MVS CONSOLE STOP/MODIFY COMMAND.                 *
*                 SKV 12/16/06 ADDED SUPPORT FOR THE COMMCHR=         *
*                                                                     *
*             MESSAGES =                                              *
*                                                                     *
*                THIS PROGRAM IS THE PROPERTY OF MIKE SCHORTMAN,      *
*                CONN. MUTUAL LIFE INS. CO.                           *
*                                                                     *
*             ABEND CODES = NONE                                      *
*                                                                     *
***********************************************************************
*                                                                   SKV
         COPY  JRPGLBL                                              SKV
*                                                                   SKV
JRP100 CSECT
         JRPSAVE REG=(12),ID=*,LV=P1DSASIZ     ENTRY CHAIN
         USING DSA,R13
         L     R11,0(R1)                       GET PARM IF ANY
         LOAD  EP=JRP400                LOAD THE JRP400 PROGRAM    *SKV
         ST    R0,JRP400EP              SAVE EP ADDRESS            *SKV
*
**       MOVE MODEL DATA TO GOTTEN AREA, ZERO REST.
*
P1WINIT  DS    0H
         LA    R0,P1DWA                        START OF DYNAMIC AREA
         LA    R1,(P1DCLRE-P1DWA)              LENGTH OF DYNAMIC AREA
         LA    R14,P1SWA                       START OF STATIC DATA
         LA    R15,P1SMDL                      LENGTH OF MODEL DATA
         MVCL  R0,R14                          MOVE TO BE RENT,REUS
*
**       GET STORAGE FOR JRP400
*
         GETMAIN RC,LV=2000             GET STRG FOR JRP400        *SKV
         LR    R14,R1                   COPY THE STRG LOC          *SKV
         LTR   R15,R15                  DID WE GET IT?             *SKV
         BZ    JRP4GSTE                 YES CONTINUE               *SKV
         WTO   'JRP990E - UNABLE TO GET STORAGE - TERMINATING.'    *SKV
         B     JRPRTRN                  GO EXIT                    *SKV
JRP400SX DC    F'0'                                                *SKV
*                                                                  *SKV
*   INIT JRP400 STORAGE AREA                                       *SKV
*                                                                  *SKV
JRP4GSTE DS    0H                                                  *SKV
         ST    R14,JRP400SX             SAVE STORAGE POINTER       *SKV
         USING JRP4DS,R14               MAP THE STORAGE AREA       *SKV
         L     R0,JRP400EP              GET  EP ADDRESS            *SKV
         ST    R0,JRP4EPA               SAVE EP ADDRESS            *SKV
         DROP  R14                      MAP THE STORAGE AREA       *SKV
***                                                                 ***
*   INITIALIZE INPUT DCB                                              *
***                                                                 ***
         LA    R10,TWT
         ST    R10,J2STWT
         MVI   P1DOPENX,X'80'                  ONLY ONE IN LIST
         LA    R2,P1DDCBI                      GET INPUT DCB ADDR
         RDJFCB ((R2))              READ THE JFCB OF THE FILE
         LTR   R15,R15              DO WE HAVE A DD STATEMENT?      SKV
         BZ    P1GC1                YES - GOOD, CONTINUE            SKV
         WTO   'JRP100E: NO JRPDATA JCL DD STATEMENT - TERMINATING' SKV
         ABEND 001                 TERMINATE                        SKV
P1GC1    DS    0H                                                   SKV
         USING INFMJFCB,R15         MAP THE JFCB AREA               SKV
         LA    R15,JFCB             SET BASE ADDR OF JFCB AREA      SKV
         MVC   INFIDSN(44),JFCBDSNM SAVE DSN OF THE INPUT FILE      SKV
         MVC   INFIDSNM(8),JFCBELNM SAVE DSN OF THE INPUT FILE      SKV
         DROP  R15                  DROP JFCB BASE ADDRESS          SKV
         OPEN  ((R2),INPUT),MF=(E,P1DOPENX)
         MVI   P1DPFLG,J1PINITF                P110 INITIALIZATION FLG
         LA    R10,PWT                          ADDRESS OF P.A.T.
*                                              (PRINTER ADDRESS TABLE)
         TITLE  'LOOP GETTING/WRITING DATA SET UNTIL END'
P1GLOOP  DS    0H
         GET   P1DDCBI
         LA    R11,9(R1)                       SAVE DATA AREA
         CLI   0(R1),C'*'           IS THIS A COMMENT LINE?        *SKV
         BE    P1GLOOP               YES - SKIP IT                 *SKV
         CLI   0(R1),C'#'           IS THIS A COMMENT LINE?        *SKV
         BE    P1GLOOP               YES - SKIP IT                 *SKV
         CLC   0(4,R1),=C'WTOR'     IS A WTOR REQUESTED?           *SKV
         BNE   P1GLOOP1              YES - GO CHECK NEXT           *SKV
         MVI   WTORNO,C'Y'          SET TO USE WTOR                *SKV
         B     P1GLOOP              GO BACK AND READ ANOTHER PARM  *SKV
P1GLOOP1 CLC   0(6,R1),=C'NOWTOR'   IS A NOWTOR REQUESTED?         *SKV
         BNE   P1GLOOP2              YES - GO CHECK NEXT           *SKV
         MVI   WTORNO,C'N'          SET TO USE MODIFY / STOP       *SKV
         B     P1GLOOP              GO BACK AND READ ANOTHER PARM  *SKV
*                                                                  *SKV
P1GLOOP2 CLC   0(6,R1),=C'JRPLOG'   IS A LOG TO JRPLOG DD?         *SKV
         BE    P1GLOOP3              YES - SKIP IT                 *SKV
         CLC   0(6,R1),=C'SYSLOG'   IS A LOG TO SYSLOG?            *SKV
         BE    P1GLOOP3              YES - SKIP IT                 *SKV
         CLC   0(7,R1),=C'CONSOLE'  IS A LOG TO CONSOLE?           *SKV
         BE    P1GLOOP3              YES - SKIP IT                 *SKV
P1GLOOP3 CLC   0(8,R1),=C'COMMCHR=' IS COMMAND A COMMCHR=?         *SKV
         BNE   P1GLOOP4              NO - CONTINUE ON              *SKV
         MVC   COMMCHR(1),8(R1)     SAVE COMMAND CHARACTER         *SKV
         B     P1GLOOP              GO CONTINUE ON                 *SKV
P1GLOOP4 CLC   0(7,R1),=C'JRPAPP='  IS COMMAND A JRPAPP=?          *SKV
         BNE   P1GLOOP5              NO - CONTINUE ON              *SKV
         MVC   P1SAPPNM(4),7(R1)    SAVE APPLICATION NAME          *SKV
         B     P1GLOOP              GO CONTINUE ON                 *SKV
P1GLOOP5 CLC   0(7,R1),=C'JRPCPP='  IS COMMAND A JRPCPP=?          *SKV
         BNE   P1GLOOP6              NO - CONTINUE ON              *SKV
         MVC   P1SACPNM(4),7(R1)    SAVE APPLICATION NAME          *SKV
         B     P1GLOOP              GO CONTINUE ON                 *SKV
P1GLOOP6 CLC   0(6,R1),=C'SMFID='   IS COMMAND A SMFID=?           *SKV
         BNE   P1GLOOP7              NO - CONTINUE ON              *SKV
         CLI   6(R1),X'F0'          FIRST BYTE LOW                 *SKV
         BL    P1GLP6ER              YES - DO ERROR                *SKV
         CLI   6(R1),X'F9'          FIRST BYTE LOW                 *SKV
         BH    P1GLP6ER              YES - DO ERROR                *SKV
         CLI   7(R1),X'F0'          SECOND BYTE LOW                *SKV
         BL    P1GLP6ER              YES - DO ERROR                *SKV
         CLI   7(R1),X'F9'          SECOND BYTE LOW                *SKV
         BH    P1GLP6ER              YES - DO ERROR                *SKV
         CLI   8(R1),X'F0'          SECOND BYTE LOW                *SKV
         BL    P1GLP6ER              YES - DO ERROR                *SKV
         CLI   8(R1),X'F9'          SECOND BYTE LOW                *SKV
         BH    P1GLP6ER              YES - DO ERROR                *SKV
         MVC   DPFLD(8),=PL8'0'     INIT WORK AREA                 *SKV
         PACK  DPFLD,6(3,R1)        CONVERT TO PACKED FORMAT       *SKV
         CVB   R0,DPFLD             AND CONVERT TO BINARY          *SKV
         ST    R0,FWFLD             STORE THE VALUE                *SKV
         MVC   SMFIDHLD(1),FWFLD+3  SAVE THE VALUE                 *SKV
         B     P1GLOOP              GO CONTINUE ON                 *SKV
P1GLP6ER DS    0H                                                  *SKV
         WTO   'JRP100E - SMFID= VALUE ERROR - VALUE IGNORED.'     *SKV
P1GLOOP7 DS    0H                                                  *SKV
         LR    R3,R1                           SAVE REG1
         TM    P1DPFLG,J1PCNTF                 CONTINUATION FLAG ON
         BO    P1DSETLK                        YES BYPASS PRTR INIT.
         MVC   0(8,R10),0(R1)                  SAVE PRINTER NAME
         LA    R2,(J1DSASIZ)                   GETMAIN -
         GETMAIN RC,LV=(R2)                    FOR -
         ST    R1,8(R10)                       STORE ADDRESS IN P.W.T.
         L     R8,8(R10)                          SAVE GETMAIN AREA
         USING J1DWA,R8                        SET UP ADDRESSABILITY
         LA    R0,J1DWA                        START OF DYNAMIC AREA
         LA    R1,(J1DCLRE-J1DWA)              LENGTH OF DYNAMIC AREA
         LA    R14,J2SWA                       START OF STATIC DATA
         LA    R15,J2SMDL                      LENGTH OF MODEL DATA
         MVCL  R0,R14                          MOVE TO BE RENT,REUS
         MVC   J1DBFCL(8),0(R3)                MOVE PRT NAME TO P1G.
         MVC   J1DHPRT(8),0(R3)                MOVE PRT NAME TO P1G.
         MVC   J1DFLSMS(8),0(R3)                MOVE PRT NAME TO P1G.
         MVC   J1DWERRP(8),0(R3)                MOVE PRT NAME TO P1G.
         MVC   J1DCOMCH(1),COMMCHR          SAVE COMMAND CHAR      *SKV
         MVC   J1DAPPLN(1),P1SAPPLN         SAVE ACB NAME LEN      *SKV
         MVC   J1DAPPNM(4),P1SAPPNM         SAVE ACB NAME          *SKV
         MVC   J1DJRP4A(4),JRP400EP         SAVE EPA OF JRP400     *SKV
         MVC   J1DJRP4D(4),JRP400SX         SAVE DATA ADDR FOR JRP4*SKV
         MVC   J1DSMFID(1),SMFIDHLD         SAVE SMF RECORD NUMBR  *SKV
         DROP  R8
*
P1DSETLK DS    0H
         NI    P1DPFLG,255-J1PCNTF              TURN OFF CONT. BIT
         CLI   71(R3),C' '                      IS THIS CARD CONTINUED
         BE    P1DNOCNT                         NO.
         OI    P1DPFLG,J1PCNTF                  YEH. TURN ON CONT FLAG
P1DNOCNT LA    R9,P1DPFLG
         LINK  EP=JRP110,PARAM=((R9),(R10),(R11))     LINK TO -
*                                              P110 MODIFY ROUTINE
         TM    P1DPFLG,J1PINVF                  INVALID ON INIT
         BO    JRPRTRN                          YES END
         TM    P1DPFLG,J1PCNTF                  CONTINUATION CARD??
         BO    P1GLOOP                          YEH. GET NEXT CARD
         LA    R10,12(R10)                      BUMP TO NEXT PWT ENTRY
         B     P1GLOOP
*
**       CLOSE INPUT DCB AND FREE BUFFER POOL
*
INTCLOSE LA    R2,P1DDCBI                      GET DCB ADDRESS
         CLOSE ((R2),DISP),MF=(E,P1DOPENX)
         FREEPOOL P1DDCBI                       FREE BUFFER POOL
         MVC   0(4,R10),=C'PEND'                END OF PWT
         MVI   P1DPFLG,X'00'                    END OF INITIALIZATION
**       SET UP ACB AND OPEN
*
OPNACB   DS    0H
         OPEN  P1DACB
         LTR   15,15
*        BZ    AT120
         BZ    P1CMJES
         MVC   P1DWTO1,=CL4'E100'
         MVC   P1DWTO2,=CL30'ACB NOT AVAILIBLE        '
         LA    R15,8
         B     P1FATALF
         DS    0F
         DS    0F
P1DACB   ACB   AM=VTAM,APPLID=P1SAPPLN,MACRF=LOGON
*
**       ATTACH JES SCAN MODULE
*
P1CMJES  DS    0H
         LA    R9,P1DJFLG
         LA    R10,PWT
         LA    R11,P1DACB
         LA    R5,P1DJECB
         ATTACH  EP=JRP200,PARAM=((R9),(R10),(R11)),ECB=(R5),          X
                SF=(E,P1DATACH)
         ST    R1,P1DJTCB
*
**       ATTACH CONTROL TERMINAL MODULE
*
AT120    LA    R5,P1DTECB
         LA    R6,TWT
         LA    R7,P1DPFLG
         LA    R8,PWT
         L     R2,JRP400SX              GET STORAGE POINTER -JRP400*SKV
*        LINK    EP=JRP120,PARAM=((R7),(R8)) ECB=(R5),                X
         ATTACH  EP=JRP120,PARAM=((R7),(R8),(R6),(R2)),ECB=(R5),   *SKVX
                SF=(E,P1DATACH)
         ST     R1,P1DTTCB
*
**     SEND OPERATOR INITIALIZATION COMPLETED MESSAGE AND SETUP    *SKV
**     FOR JRP400 RTN.                                             *SKV
*
         USING JRP4DS,R2                MAP THE JRP400 DSECT       *SKV
         L     R2,JRP400SX              GET STORAGE POINTER        *SKV
         MVC   JRP4DSMF(1),SMFIDHLD     SAVE SMF RECORD NUMBER     *SKV
         MVC   JRP4DAID(4),P1SAPPNM     SAVE APPLICATION NAME      *SKV
         MVC   JRP4DCID(4),P1SACPNM     SAVE APPLICATION NAME      *SKV
*        MVC   J1DJRP4D(4),JRP400SX     SAVE DATA AREA ADDRESS     *SKV
         DROP  R2                                                  *SKV
*   CALL JRP400 TO RECORD JRP START                                *SKV
         USING JRP4DS,R2                MAP THE JRP400 DSECT       *SKV
         L     R2,JRP400SX              GET STORAGE POINTER        *SKV
         MVI   JRP4STAT,C'R'            SET JRP400 TO RUN MODE     *SKV
         MVI   JRP4DSRN,X'00'           SET INIT OPTION FOR JRP400 *SKV
         MVC   JRP4DATA+9(31),INFIDSN   RECORD DSNAME              *SKV
         MVC   JRP4DATA+0(8),INFIDSNM   RECORD DSNAME MEMBERNAME   *SKV
         LR    R1,R2                    SET PARM ADDRESS           *SKV
         L     R15,JRP400EP             GET JRP400 EPA             *SKV
         BALR  R14,R15                  RUN JRP400                 *SKV
*        WTO   'JRP100 RETURNING FROM JRP400 - R '                 *SKV
         DROP  R2                                                  *SKV
*                                                                  *SKV
         MVC   P1DWTO1,=CL4'I101'        MESSAGE IDENTIFIER
         MVC   P1DWTO2,=CL30'INITIALIZATION COMPLETE'
         LA    R1,P1DWTO                 WTO LIST
         SVC   35
         USING CSCL,R10                                            *SKV
         USING CIB,R11                                             *SKV
WTOR1    EQU   *                                                   *SKV
         CLI   WTORNO,C'Y'          USE WTOR OR MODIFY / STOP?     *SKV
         BE    WTOR                  USE WTOR, SO GO DOIT          *SKV
         WTO   'JRP904I - JRP WAITING FOR COMMAND.'                *SKV
         EXTRACT CSCLADDR,FIELDS=COMM   GET ADDRESS OF CSCL        *SKV
         L     R10,CSCLADDR        LOAD ADDRESS OF CSCL            *SKV
         CLC   COMCIBPT,=4XL1'00'  IS CIB PRESENT?                 *SKV
         BE    WTOR                 NO - DO WTOR                   *SKV
         BAL   R14,DELPCIB         GO DELETE PROCESSED CIB         *SKV
         L     R10,CSCLADDR        LOAD ADDRESS OF CSCL            *SKV
         LA    R11,COMCIBPT        LOAD ADDR OF PTR OF CIB CHAIN   *SKV
         QEDIT ORIGIN=(R11),CIBCTR=1 SET CIB CHAIN COUNT           *SKV
WTOR2    EQU   *                                                   *SKV
         STIMER WAIT,DINTVL=WAITTIME WAIT A BIT                    *SKV
         L     R10,CSCLADDR        LOAD ADDR OF CSCL               *SKV
         L     R11,COMECBPT        LOAD ADDR OF PTR OF CIB CHAIN   *SKV
         TM    0(R11),X'40'        HAS ECB BEEN POSTED?            *SKV
         BNO   WTOR2                NO - GO BACK AND WAIT MORE     *SKV
         L     R10,CSCLADDR        LOAD ADDR OF CSCL               *SKV
         L     R11,COMCIBPT        LOAD ADDR OF CIB                *SKV
         CLI   CIBVERB,CIBMODFY    A MODIFY COMMAND RECEIVED?      *SKV
         BE    MODIFY                                              *SKV
         CLI   CIBVERB,CIBSTOP     A STOP COMMAND RECEIVED?        *SKV
         BE    STOP                                                *SKV
         ABEND 100,DUMP            *SHOULDN'T GET HERE             *SKV
MODIFY   EQU   *                                                   *SKV
         WTO   'JRP903I RECEIVED YOUR MODIFY REQUEST'              *SKV
         LH    R15,CIBDATLN        LOAD LEN OF MODIFY DATA         *SKV
         BCTR  R15,0               REDUCE LEN FOR EX INSTRUCTION   *SKV
         LA    R2,CIBDATA          LOAD ADDR OF MODIFY DATA        *SKV
         EX    R15,MVCBDATA        COPY COMMAND TO USER AREA       *SKV
         BAL   R14,DELPCIB         GO DELETE PROCESSED CIB         *SKV
         CLC   MDFYDATA(6),=C'ICLOSE'  SHUTDOWN REQUESTED?         *SKV
         BE    ICLOSE2                 YES - THEN GO SHUTDOWN      *SKV
         CLC   MDFYDATA(4),=C'SHUT'    SHUTDOWN REQUESTED?         *SKV
         BE    ICLOSE2                 YES - THEN GO SHUTDOWN      *SKV
*   CALL JRP400 TO RECORD JRP INV OPER COMMAND                     *SKV
         USING JRP4DS,R2                MAP THE JRP400 DSECT       *SKV
         L     R2,JRP400SX              GET STORAGE POINTER        *SKV
         MVI   JRP4DATA,X'40'                                      *SKV
         MVC   JRP4DATA+1(39),JRP4DATA                             *SKV
         MVI   JRP4STAT,C'R'            SET JRP400 TO RUN MODE     *SKV
         MVI   JRP4DSRN,X'04'           SET INIT OPTION FOR JRP400 *SKV
         MVC   JRP4DATA(40),MDFYDATA    STORE COMMAND              *SKV
         LR    R1,R2                    SET PARM ADDRESS           *SKV
         L     R15,JRP400EP             GET JRP400 EPA             *SKV
         BALR  R14,R15                  RUN JRP400                 *SKV
*        WTO   'JRP100 RETURNING FROM JRP400 - R '                 *SKV
         DROP  R2                                                  *SKV
         XC    MDFYDATA,MDFYDATA      CLEAR INPUT AREA             *SKV
         WTO   'JRP904I UNKNOWN COMMAND RECEIVED'                  *SKV
         B     WTOR2               GO BACK AND WAIT FOR NEXT CMD   *SKV
MVCBDATA MVC   MDFYDATA(0),0(R2)   MOVE USER DATA                  *SKV
STOP     EQU   *                                                   *SKV
*   CALL JRP400 TO RECORD JRP STOP OPER COMMAND                    *SKV
         USING JRP4DS,R2                MAP THE JRP400 DSECT       *SKV
         L     R2,JRP400SX              GET STORAGE POINTER        *SKV
         MVI   JRP4DATA,X'40'                                      *SKV
         MVC   JRP4DATA+1(39),JRP4DATA                             *SKV
         MVI   JRP4STAT,C'R'            SET JRP400 TO RUN MODE     *SKV
         MVI   JRP4DSRN,X'03'           SET INIT OPTION FOR JRP400 *SKV
         MVC   JRP4DATA(01),=C'P'       STORE COMMAND              *SKV
         LR    R1,R2                    SET PARM ADDRESS           *SKV
         L     R15,JRP400EP             GET JRP400 EPA             *SKV
         BALR  R14,R15                  RUN JRP400                 *SKV
*        WTO   'JRP100 RETURNING FROM JRP400 - R '                 *SKV
         DROP  R2                                                  *SKV
         B     ICLOSE3             GO SHUTDOWN                     *SKV
WTOR     EQU   *                                                   *SKV
         SR    R6,R6
         SR    R7,R7
         WTOR  'JRP100I - ENTER ICLOSE TO SHUT DOWN',ANSWER,20,WTORECB,X
               ROUTCDE=(1,11),DESC=5
*
**     WAIT FOR WTOR, JRPJES OR JRP CTRTERM ECB"S TO BE POSTED
*
WAIT     DS    0H
         LA    R1,P1DJECB
         ST    R1,P1DWECB
         LA    R1,P1DTECB
         ST    R1,P1DWECB+4
         LA    R1,WTORECB
         ST    R1,P1DCOMM
         MVI   P1DTFLG,C' '
         MVI   P1DCOMM,X'80'     ** TEST  LAST IN WAIT LIST
         LA    R1,P1DWECB                 LOAD ECB LIST
         WAIT  1,ECBLIST=(1)              WAIT FOR WORK OR COMMAND ECB
P1CMCK   DS    0H
         LA    R1,P1DTECB
         TM    0(R1),X'40'
         BNO   P1CKCOMM
         MVI   P1DTFLG,C'T'
         B     P1CMCONE
P1CKCOMM L     R1,P1DCOMM                 GET COMM PTRS
         TM    0(R1),X'40'                POSTED?
         BNO   JESABEND                   NO - GO TO CLOSE CODE
         XC    WTORECB,WTORECB
         OC    ANSWER(6),BLANKS
         CLC   ANSWER(4),=C'SHUT'                                *SKV*
         BE    ICLOSE2                                           *SKV*
         CLC   ANSWER(6),=C'ICLOSE'
         BE    ICLOSE2                                           *SKV*
ICLOSE1  CLC   ANSWER(8),=C'SHUTDOWN'                            *SKV*
         BNE   WTOR
ICLOSE2  DS    0H                                                  *SKV
*   CALL JRP400 TO RECORD JRP MODIFY STOP OPER COMMAND             *SKV
         USING JRP4DS,R2                MAP THE JRP400 DSECT       *SKV
         L     R2,JRP400SX              GET STORAGE POINTER        *SKV
         MVI   JRP4DATA,X'40'                                      *SKV
         MVC   JRP4DATA+1(39),JRP4DATA                             *SKV
         MVI   JRP4STAT,C'R'            SET JRP400 TO RUN MODE     *SKV
         MVI   JRP4DSRN,X'02'           SET INIT OPTION FOR JRP400 *SKV
         MVC   JRP4DATA(40),MDFYDATA    STORE COMMAND              *SKV
         LR    R1,R2                    SET PARM ADDRESS           *SKV
         L     R15,JRP400EP             GET JRP400 EPA             *SKV
         BALR  R14,R15                  RUN JRP400                 *SKV
*        WTO   'JRP100 RETURNING FROM JRP400 - R '                 *SKV
         DROP  R2                                                  *SKV
ICLOSE3  OI    P1DPFLG,J1DICLSF                                    *SKV
P1CMCONE EQU   *
         TM    P1DPFLG,J1DICLSF ICLOSE ???
         BNO   P1MBCLS          NO GO BACK AND WAIT
WTOREND  EQU   *
         MVC   P1DWTO1,=CL4'I102 '
         MVC   P1DWTO2,=CL17'JRP SHUTTING DOWN'
         OI    P1DJFLG,J1DICLSF
         LA    R1,P1DWTO
         SVC   35
         B     JESWAIT
P1MBCLS  EQU    *
*1MBCLS  TM     P1DPFLG,J1DNCLSF CLOSE ???
*        BO     P1SNDCL          NO GO BACK AND WAIT
*        CLI    P1DTFLG,C'T'     FROM JRP120
*        BNE    P1CMDEL
P1SNDCL  DS     0H
         MVC    P1DWTO1,=CL4'I103 '
         MVC    P1DWTO2,=CL30'CLOSE IN PROGRESS'
         OI     P1DJFLG,J1DNCLSF
         LA    R1,P1DWTO
         SVC   35
JESWAIT  LA     R1,P1DJECB
         WAIT   ECB=(R1)
         B      JESSTOP
JESABEND MVC   P1DWTO1,=CL4'E101'
         MVC   P1DWTO2,=CL30'JRP200 ERROR ENCOUNTERED'
         LA    R1,P1DWTO
         SVC   35
JESSTOP  STATUS STOP,TCB=P1DJTCB
         DETACH P1DJTCB
CONSCLOS STATUS STOP,TCB=P1DTTCB
         DETACH P1DTTCB
C2       DS    0H                                                  *SKV
*        WTO   'JRP100 GOING TO JRP400 FOR STOP  '                 *SKV
         USING JRP4DS,R2                MAP THE JRP400 DSECT       *SKV
         L     R2,JRP400SX              SAVE STORAGE POINTER       *SKV
         MVI   JRP4STAT,C'R'            SET JRP400 TO STOP MODE    *SKV
         MVI   JRP4DSRN,X'01'           SET INIT OPTION FOR JRP400 *SKV
         MVC   JRP4DATA(40),=40C'TERM'  CLEAR DATA AREA            *SKV
         LR    R1,R2                    SET PARM ADDRESS           *SKV
         L     R15,JRP400EP             GET JRP400 EPA             *SKV
         BALR  R14,R15                  RUN JRP400                 *SKV
*        WTO   'JRP100 RETURNING FROM JRP400 - S '                 *SKV
         DROP  R2                       MAP THE JRP400 DSECT       *SKV
         DELETE EP=JRP400               DELETE THE JRP400 PGM      *SKV
         MVC   P1DWTO1,=CL4'I104'
         MVC   P1DWTO2,=CL30'SHUTDOWN COMPLETE, ENDING'
         LA    R1,P1DWTO
         SVC   35
*        RETURN TO CALLER
JRPRTRN DS    0H
         CLOSE   P1DACB
         WTO   'JRP100I AFTER ACB CLOSE'
         CLC   JRP400SA(4),=F'0'         STORAGE GOTTEN FOR JRP400?*SKV
         BE    JRPRTRN1                   NO - BYPASS FREEMAIN     *SKV
         FREEMAIN EC,LV=2000,A=JRP400SA  FREE STRG FOR JRP400      *SKV
JRPRTRN1 DS    0H                                                  *SKV
         JRPRTRN
DELPCIB  DS    0H
         L     R10,CSCLADDR         LOAD ADDRESS OF CSCL
         LA    R11,COMCIBPT         LOAD ADDRESS OF PTR TO CIB CHAIN
         L     R2,0(0,R11)          LOAD ADDR OF CIB TO BE FREED
         QEDIT ORIGIN=(R11),BLOCK=(R2) FREE PROCESSED CIB
         BR    R14                  RETURN TO CALLER
*
         TITLE 'F A T A L  E R R O R  M E S S A G E S'
P1FATALF DS    0H
         CVD   R15,P1DWORK         CONVERT CALLERS RETURN CODE IN R15
         STM   R14,R12,12(R13)     SAVE MY JRPREGS
         LA    R1,P1DWTO           GET MF=L FORM OF WTO
         SVC   35                  ISSUE WTO
         CLOSE P1DACB
         ABEND  5,DUMP
         TITLE 'LITERAL POOL'
         LTORG
         TITLE 'STATIC STORAGE'
P1GEOD   EQU   *
J2SYN    EQU   *
P1SWA    DS    0D
**********************************************************************
****NOTE NOTE NOTE  MUST KEEP STATIC AND DYNAMIC AREAS IN SYNC***
****DYNAMIC IS GETMAINED AND STATIC IS USED TO INITIALIZE IT.
**********************************************************************
         PUSH  PRINT
         PRINT GEN,ON
*DEFINE DCB AND OPEN AND CLOSE. (EODAD,SYNAD,OPEN AND CLOSE)
P1SDCBIS EQU   *
P1SDCBI  DCB   DSORG=PS,MACRF=GL,DDNAME=JRPDATA,EODAD=INTCLOSE,        *
               EXLST=PTRJFCB                                        SKV
P1SDCBIE EQU   *
P1SATACH ATTACH EP=JRP200,SF=L
P1SWTO   WTO   'JRPE999 ....X....X....X....X....X....X',MF=L
P1SMDL   EQU   *-P1SWA
***********************************************************************
*  EVERYTHING FROM HERE ON IS NOT COPIED TO DYNAMIC AREA              *
***********************************************************************
P1SAPPLN DC    XL1'04'
P1SAPPNM DC    CL4'JRP '
P1SACPLN DC    XL1'04'
P1SACPNM DC    CL4'CJRP'
JRP400T  DC    F'0'                JRP400 TCB ADDRESS              *SKV
JRP400EP DS    F                   JRP400 EP ADDRESS               *SKV
JRP400SA DS    F'0'                JRP400 SAVE/PARM AREA           *SKV
JRP4ECB  DS    F'0'                JRP400 ECB                      *SKV
WAITTIME DC    C'00000300'         WAIT THREE SECONDS              *SKV
CSCLADDR DS    F                                                   *SKV
MDFYDATA DC    100XL1'00'                                          *SKV
WTORNO   DC    C'Y'                WTOR FLAG = YES OR NO           *SKV
LOGGING  DC    C'0'                JRP LOGGING 0 = NONE            *SKV
*                1 = LOG TO JRPLOG DD
*                2 = LOG TO CONSOLE
*                3 = LOG TO SYSLOG
COMMCHR  DC    C'$'                JRP COMMAND CHAR DEFAULT = $    *SKV
DPFLD    DS    D                                                   *SKV
FWFLD    DS    F                                                   *SKV
SMFIDHLD DC    X'FF'               SMF ID - DEFAULT TO 255         *SKV
         DS    0F
WTORECB  DS    A                   PTR TO COMM PTRS
BLANKS   DC    CL16' '
ANSWER   DS    CL20
         DS    0F                                                   SKV
PTRJFCB  DC    X'87'                                                SKV
         DC    AL3(JFCB)                                            SKV
JFCB     DS    CL176                                                SKV
INFIDSN  DC    44CL1' '                                             SKV
INFIDSNM DC    8CL1' '                                              SKV
         JRPSMAC         MAPPING FOR STATIC AREA
         LTORG
         TITLE 'DYNAMIC - GOTTEN AREA'
DSA      DSECT
         DS    9D                  O/S SAVE
****NOTE NOTE NOTE  MUST KEEP STATIC AND DYNAMIC AREAS IN SYNC***
****DYNAMIC IS GETMAINED AND STATIC IS USED TO INITIALIZE IT.
P1DWA    DS    0D
P1DDCBIS EQU   *
P1DDCBI  DCB   DSORG=PS,MACRF=GL,DDNAME=JRPDATA,EODAD=INTCLOSE,        *
               EXLST=PTRJFCB                                        SKV
P1DATACH ATTACH EP=JRP200,SF=L
P1DWTO   WTO   'JRPE999 ....X....X....X....X....X....X',MF=L
P1DWTO1  EQU   P1DWTO+4+3,4      ERROR MESSAGE NUMBER PORTION
P1DWTO2  EQU   P1DWTO+4+3+5,30    MESSAGE PORTION
***********************************************************************
*  EVERYTHING FROM HERE ON IS SET TO LOW VALUES AT INIT TIME          *
***********************************************************************
         DS    0F
P1DJTCB  DS    F     JRP JES TCB
P1DTTCB  DS    F     JRP CTRCONS TCB
P1DJECB  DS    F     JRP JES ATTACH ECB
P1DTECB  DS    F     JRP CTRCONS ATTACH ECB
P1DWECB  DS    A     JES ATTACH ECB ADDCON
         DS    A     CTRTERM ATTACH ECB ADDCON
P1DCOMM  DS    A     WTOR ECB ADDCON
P1DOPENX DS    F
P1DPFLG  DS    C
P1DJFLG  DS    C
P1DTFLG  DS    C
P1DWORK  DS    D                   WORK AREA
PWT      DS    101CL12  MAX OF 100 PRINTERS
TWT      DS    25CL4    MAX OF 25  CONTROL TERMINALS
P1DCLRE  EQU   *                   END OF DYNAMIC AREA
P1DSASIZ EQU   *-P1DWA             SIZE OF DYNAMIC AREA
         TITLE 'MISC EQUATES AND DSECTS'
         PRINT ON,GEN
         JRPDMAC         MAPPING FOR DYNAMIC AREA
         JRPREGS
         PUSH  PRINT
         DCBD  DSORG=PS
         IEZDEB
         IFGACB                     ACB
         CVT     DSECT=YES          CVT
         IHAPSA  DSECT=YES          PSA
         IKJTCB  DSECT=YES          TCB
         IEZJSCB                   JSCB
CSCL     DSECT
         IEZCOM
CIB      DSECT
         IEZCIB
         JRP4DS
         PRINT ON,GEN                                               SKV
         DSECT                                                      SKV
         IEFJFCBN LIST=NO                                           SKV
         END
