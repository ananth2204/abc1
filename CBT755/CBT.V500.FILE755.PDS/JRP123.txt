JRP123 TITLE 'JRP SECURITY EXIT'
***********************************************************************
*                                                                     *
*             MODULE NAME = JRP123                                    *
*                                                                     *
*             DESCRIPTIVE NAME =  SECURITY EXIT FOR "JRP"             *
*                                                                     *
*             AUTHOR =                                                *
*                                                                     *
*             COPYRIGHT =  1/8/84                                     *
*                                                                     *
*              MODIFIED =                                             *
*                                                                     *
*             STATUS = ENTRY AND EXIT ONLY. TO BE USER CODED          *
*                                                                     *
*             FUNCTION =                                              *
*                PROVIDES EXIT FOR SECURITY PACKAGE(ACF2, RACF)       *
*                INTERFACE BY USERID AND PASSWORD INFORMATION.        *
*                                                                     *
*                OPERATION =                                          *
*                   INTERFACES WITH SECURITY PACKAGE FOR JRP          *
*                   CONTROL TERMINAL OPERATOR.                        *
*                                                                     *
*              NOTES =                                                *
*                                                                     *
*                 DEPENDENCIES = MUST BE REENTRANT AND REUSABLE.      *
*                                                                     *
*                                                                     *
*                 REGISTER CONVENTIONS = STANDARD CONVENTIONS.        *
*                    REGISTERS 11      = TERMINAL DYNAMIC AREA        *
*                                        POINTER USED WITH TERMINAL   *
*                                        DYNAMIC DSECT INCLUDES       *
*                                                                     *
*                    REGISTERS 12      = BASE REGISTER                *
*                                                                     *
*                    REGISTERS 13      = SAVE AREA AND GETMAINED DSECT*
*                                                                     *
*                PROCESSOR = ASM                                      *
*                                                                     *
*                MODULE SIZE =                                        *
*                                                                     *
*                ATTRIBUTES = SCHEDULER KEY 8, PROBLEM STATE          *
*                                                                     *
*                                                                     *
*             ENTRY POINTS = JRP123 (ONLY ENTRY POINT)                *
*                                                                     *
*                LINKAGE =                                            *
*                   FROM JRP122 AS A SUBROUTINE                       *
*                                                                     *
*                                                                     *
*             OUTPUT = ON EXIT R15 SHOULD BE SET TO :                 *
*                      0 = USERID AND PASSWORD O.K.                   *
*                      4 = PASSWORD EXPIRED                           *
*                      8 = INVALID PASSWORD                           *
*                     12 = INVALID USERID                             *
*                     16 = USER NOT AUTHORIZED                        *
*                     20 = UNKNOWN CONDITION.                         *
*                                                                     *
*             EXIT - NORMAL = AT PROGRAM END VIA BRANCH REGISTER 14   *
*                                                                     *
*                                                                     *
*             EXTERNAL REFERENCES =                                   *
*                                                                     *
*                ROUTINES =                                           *
*                                                                     *
*                DATA AREAS =                                         *
*                                                                     *
*                CONTROL BLOCKS = CTCB(CONTROL TERMINAL CONTROL BLOCK)*
*                                                                     *
*                                                                     *
*             TABLES =                                                *
*                                                                     *
*             MACROS =                                                *
*                                                                     *
*             CHANGE ACTIVITY =                                       *
*                                                                     *
*             MESSAGES =                                              *
*                                                                     *
*                                                                     *
*             ABEND CODES =                                           *
*                                                                     *
*     *SKV*  PUT IN PLACE SOME SAMPLE CODE.  TO NOT USE THIS          *
*            UNCOMMENT THE BRANCH.                                    *
*  ***                                                                *
*  ***    IT'S WEIRD, BUT THE MAXIMUM USERID LENGTH IS 7 AND THE      *
*  ***    AND THE MAX PASSWORD IS 5.  GO FIGURE...                    *
*  ***                                                                *
*     *SKV*  CHANGED A COMMENT TO REFLECT ACCURACY.                   *
*     *SKV*  COMMENT:  A GOOD PLACE TO PUT IN A CALL TO RACF/SAF OR   *
*            ACF2.                                                    *
*                                                                     *
***********************************************************************
*                                                                   SKV
         COPY  JRPGLBL                                              SKV
*                                                                   SKV
ENTRYLEN EQU   19                                                   SKV
*                                                                   SKV
JRP123 CSECT
         JRPSAVE REG=12,ID=*,LV=SAVESIZE ;GET SAVE AREA
         USING TERMSAVE,R13              ;MAKE ADDRESSABLE
         L     R11,0(R1)                 ;GET CTCB AREA
         USING J1TWA,R11                 ;MAKE ADDRESSABLE
***********************************************************************
* SECURITY INTERFACE CODE GOES HERE....MUST BE REENTRANT REUSABLE
* DATA AREAS OF CONCERN TO THIS EXIT ARE LISTED BELOW AND DEFINED IN
* CTCB DSECT.
*
*            J1TLUID  8 BYTE LUID FIELD
*            J1DUSER  7 BYTE USERID FIELD
*            J1DPSWD  8 BYTE PASSWORD FIELD
***********************************************************************
*
*   UNCOMMENT THE CODE BELOW TO BYPASS THE SECURITY CHECKING       *SKV
*
*        B     RTNOK                     *** GO APPROVE & EXIT     *SKV
*
*     GO CHANGE INPUT FIELD TO TO REMOVE BIN ZEROS
*
         L     R0,=F'7'                  SET FIELD LENGTH
         LA    R1,J1DUSER                POINT TO THE FIELD TO PROCESS
         BAL   R6,FLDPREPI               GO PREP FIELD
         L     R0,=F'8'                  SET FIELD LENGTH
         LA    R1,J1DPSWD                POINT TO THE FIELD TO PROCESS
         BAL   R6,FLDPREPI               GO PREP FIELD
*
         LA    R1,LOGIDPWT               POINT TO THE START OF TABLE
         LA    R2,LOGIDPWE               POINT TO THE END OF TABLE
UIDLOOP  EQU   *
         CR    R1,R2                     AT END OF TABLE?
         BNL   RTNBUSR                    YES - GO TO BAD USER RTN
         CLC   0(7,R1),J1DUSER           USERID MATCH?
         BNE   UIDLOOPN                   NO = GO CHECK NEXT
         CLC   7(8,R1),J1DPSWD           PASSWORD MATCH?
         BE    RTNOK                      YES - GO APPROVE & EXIT
*                   IF YOU WANT TO CHECK TERMINAL ID, PUT CODE HERE...
         B     RTNBPWD                    NO  - GO EXIT WITH BAD PWD
UIDLOOPN EQU   *
         LA    R1,ENTRYLEN(R1)           POINT TO THE NEXT ENTRY    SKV
         B     UIDLOOP                   GO CHECK NEXT
*
*   BAD USERID
*
RTNBUSR  EQU   *
         L     R15,RC12                  ;SET BAD USER (12)
         XC    J1DPSWD(8),J1DPSWD        ;CLEAR PASSWORD
         B     JRPRTRN                    RETURN TO CALLER
*
*   BAD PASSWORD
*
RTNBPWD  EQU   *
         L     R15,RC8                   ;SET BAD PASSWORD (8)
         XC    J1DPSWD(8),J1DPSWD        ;CLEAR PASSWORD
         B     JRPRTRN                    RETURN TO CALLER
*
*   ALL OK
*
RTNOK    XR    R15,R15                   ;ZERO REG 15
         XC    J1DPSWD(8),J1DPSWD        ;CLEAR PASSWORD
JRPRTRN  DS    0H
         JRPRTRN                         ;RETURN TO JRP122
*
*   GO REMOVE BINARY ZEROS FROM FIELD
*
FLDPREPI EQU   *
         LA    R5,0(R0,R1)               POINT TO THE ENDING POSITION
         B     FLDPREPL                  GO DO THE CHECK AND REPLACE
FLDPREPX EQU   *
         BR    R6                        RETURN TO CALLER
*
FLDPREPL EQU   *
         CR    R1,R5                     ARE WE AT THE LAST POSITION?
         BH    FLDPREPX                   YES - GO EXIT LOOP
         CLI   0(R1),X'00'               BINARY ZERO?
         BNE   FLDPREP1                   NO - GO CONTINUE
         MVI   0(R1),X'40'               CHANGE THE BIN ZERO TO SPACE
FLDPREP1 EQU   *
         LA    R1,1(R1)                  POINT TO THE NEXT POSITION
         BR    R6                        RETURN TO CALLER
*
*    USERID & PASSWORD TABLE
*
*  ENTRY CONSISTS OF (7) USERNAME
*                    (8) PASSWORD
*                    (2) COMMAND AUTHORITY ENTRY(IN DEVELOPMENT)
*                    (2) DEVICE AUTHORITY ENTRY   (IN DEVELOPMENT)
*
LOGIDPWT DC    CL7'SCOTT  ',CL8'SCOTT   ',XL2'FF',XL2'01' 1ST ENTRY SKV
         DC    CL7'SCOTT1 ',CL8'SCOTT   ',XL2'FF',XL2'02' 2ND       SKV
         DC    CL7'SCOTT2 ',CL8'SCOTT   ',XL2'FF',XL2'03' 3RD       SKV
         DC    CL7'SCOTT3 ',CL8'SCOTT   ',XL2'FF',XL2'04' 4TH       SKV
LOGIDPWE DC    CL7'*******',CL8'*****   ',XL2'00',XL2'00' END       SKV
*
*  FORMAT OF AUTHORITY LIST
*
AUTHLST  DS    0F
         DC    XL1'01',XL1'00'   UNSOL/NOUNSOL
         DC    XL1'02',XL1'10'   DISPLAY ONLY
         DC    XL1'03',XL1'20'   RESTRICTED
         DC    XL1'03',XL1'A0'   SUBJECT TO USERMOD EXIT INTERROGATION
         DC    XL1'04',XL1'FF'   ALL COMMANDS
AUTHLSTX DC    XL1'FF',XL1'00'   (END OF ENTRY)
*
*
*  FORMAT OF DEVICE LIST
*     XL1 - SEQUENCE NUMBER
*     XL1 - NUMBER OF ENTRIES
*     CL8 - (OPTIONAL) DEVICE LIST (MIN - 0, MAX 255)
*
DEVLST   DS    0F
         DC    XL1'01',XL1'02',CL8'JPRT1',CL8'JPRT2'
         DC    XL1'02',XL1'01',CL8'JPRT1'
         DC    XL1'03',XL1'01',CL8'JPRT2'
         DC    XL1'04',XL1'02',CL8'JPRT1',CL8'JPRT2'
DEVLSTX  DC    XL1'FF',XL1'00'
*
*
RC0      DC    F'0'      = USERID AND PASSWORD O.K.
RC4      DC    F'4'      = PASSWORD EXPIRED
RC8      DC    F'8'      = INVALID PASSWORD
RC12     DC    F'12'     = INVALID USERID
RC16     DC    F'16'     = USER NOT AUTHORIZED
RC20     DC    F'20'     = UNKNOWN CONDITION.
*
         LTORG
*
** JRP123 SAVE AREA DSECT POINTED TO BY R13
*
TERMSAVE DSECT
         DS    9D                       ;O/S SAVE
SAVESIZE EQU   *-TERMSAVE               ;SIZE OF DYNAMIC AREA
         JRPTMAC                        ;TERMINAL DYNAMIC DSECT
         JRPREGS
         END
