/*  REXX                                                              */
/**********************************************************************/
/*  MODULE  = REORGVS6   (Recreate a VSAM Cluster - Used with the     */
/*                        REORGVS exec).                              */
/*                                                                    */
/*  #¢# AUTHOR             TKMILLE                                    */
/*  #¢# CREATED            TKMILLE 01/17/2006                         */
/*  #¢# LAST UPDATE        TKMILLE 09/16/2008                         */
/*                                                                    */
/*  PURPOSE = This exec will input an IDCAMS EXPORT SYSPRINT file and */
/*            will parse out the return codes and messages to         */
/*            determine the result of the EXPORT FUNCTION.            */
/*                                                                    */
/*            Input File SYSPRINT must be preallocated prior to the   */
/*            execution of this exec.                                 */
/*                                                                    */
/*            If the IDCAMS EXPORT Function completed unsuccessfully  */
/*            with a return code 12 and Feedback code of 160, return  */
/*            a return code 0 (the VSAM Cluster was empty) instead of */
/*            a return code of 12.                                    */
/*                                                                    */
/*            For all other results, return the return code which was */
/*            returned from the IDCAMS EXPORT function in the         */
/*            SYSPRINT report.                                        */
/*                                                                    */
/*  PARMS:                                                            */
/*  (Required Parms):                                                 */
/*                                                                    */
/*          None                                                      */
/*                                                                    */
/*  (Optional Parms):                                                 */
/*                                                                    */
/*          DEBUG  =      The parameter which displays the statements */
/*                        as they are executed.                       */
/*                                                                    */
/*  Modifications/Tailoring:                                          */
/*                                                                    */
/*  Make the following modification before storing this Rexx exec.    */
/*                                                                    */
/*  1)        Change the length of "sysid" to your specification.     */
/*            The default is 3 characters if this is not              */
/*            modified.                                               */
/*                                                                    */
/*  Return Codes:                                                     */
/*                                                                    */
/*    0   - Export function was successful.                           */
/*    2   - The VSAM Cluster input the IDCAMS EXPORT Function was     */
/*    4   - Export function was successful with warnings.             */
/*    8   - Export function has syntax error on SYSIN command stream. */
/*   12   - Export function was unsuccessful.                         */
/*                                                                    */
/**********************************************************************/
version_level = "V01.01.08"
Arg parms
Trace "O"

debug    = ""
Do i = 1 to words(parms)
         If ABBREV("DEBUG",word(parms,i),3) = 1  then Do
            debug = "DEBUG"
            parms = DELWORD(parms,i,1)
            i = i - 1
            iterate
         End
End
If debug = "DEBUG"  then Do
   Trace "I"
End

userid        = Userid()
rtcd          = 12

sysid         = Left(MVSVAR(sysname),3," ")
input_count   = 0

Read_SYSPRINT_Loop:

input_count = input_count + 1

"EXECIO 1 DISKR SYSPRINT (STEM sysprint. "
execiorc     = rc
If execiorc  = 2   Then ,        /* On end of file - Finish up  */
   Signal End_Up
If execiorc <> 0   Then Do
   Say ;
   Say "Error executing the EXECIO DISKR Rexx command for file",
       "SYSPRINT !!!"
   Say "File SYSPRINT was not properly preallocated or is invalid"
   Say "Input I/O count is " input_count
   Say "EXECIO Return Code is "execiorc" !!!"
   Say "Exec REORGVS6 is Terminated Prematurely     ..."
   rtcd = 12
   Signal End_up
End

p1  = POS("IDC3351I",sysprint.1)
If p1  > 0                                            Then Do
   p2  = POS("OPEN RETURN CODE IS 160",sysprint.1)
   If p2  > 0                                         Then Do
      rtcd    = 0
      Signal End_Up
   End
End

record = sysprint.1
Parse var record "HIGHEST CONDITION CODE WAS" +26 parse_value +4
If parse_value <> ""                                  Then Do
   rtcd    = STRIP(parse_value)
   Signal End_Up
End

Signal Read_SYSPRINT_Loop

End_Up:

"EXECIO 0 DISKR SYSPRINT (FINIS"

Say ;
Say "REORGVS6 Exec - Get EXPORT Return Code ..."
Say ;
Say "version_level                =>" version_level
Say ;
Say "REORGVS6 exec return code is   " rtcd

Return rtcd
