/*  REXX                                                              */
/**********************************************************************/
/*  MODULE  = REORGVS5   (Recreate a VSAM Cluster - Used with the     */
/*                        REORGVS exec).                              */
/*                                                                    */
/*  #¢# AUTHOR             TKMILLE                                    */
/*  #¢# CREATED            TKMILLE 01/16/2006                         */
/*  #¢# LAST UPDATE        TKMILLE 09/16/2008                         */
/*                                                                    */
/*  PURPOSE = This exec will update the log file (LOGFILE) with the   */
/*            VSAM cluster dataset name and EXPORT image dataset      */
/*            name and the current julian date for each VSAM file     */
/*            which was successfully reorganized.                     */
/*                                                                    */
/*            The VSAM cluster dataset which is being reorganized     */
/*            is input on file LIST.  File LIST must be preallocated  */
/*            prior to the execution of this exec.                    */
/*                                                                    */
/*            Input File EXPORT is input to retrieve the EXPORT       */
/*            dataset name used to hold the backup image of the       */
/*            VSAM cluster being reorganized.                         */
/*            File EXPORT must be preallocated prior to the           */
/*            execution of this exec.                                 */
/*                                                                    */
/*            Output File LOGFILE must be preallocated prior to the   */
/*            execution of this exec.                                 */
/*                                                                    */
/*  Modifications/Tailoring:                                          */
/*                                                                    */
/*  Make the following modifications before storing this Rexx exec.   */
/*                                                                    */
/*                                                                    */
/*  1)        Change the length of "sysid" to your specification.     */
/*            The default is 3 characters if this is not              */
/*            modified.                                               */
/*                                                                    */
/*  2)        Change the HLQ of the log dataset to your               */
/*            specification.  The default is HLQ is "SYS3".           */
/*                                                                    */
/*  PARMS:                                                            */
/*  (Required Parms):                                                 */
/*                                                                    */
/*          None                                                      */
/*                                                                    */
/*  (Optional Parms):                                                 */
/*                                                                    */
/*          DEBUG  =      The parameter which displays the statements */
/*                        as they are executed.                       */
/*                                                                    */
/*  Return Codes:                                                     */
/*                                                                    */
/*    0   - Successful execution of the exec                          */
/*   12   - Invalid input or output files or the files were not       */
/*          preallocated properly.                                    */
/*                                                                    */
/**********************************************************************/
version_level = "V01.01.08"
Arg parms
Trace "O"

reorg_status = ""
debug        = ""
Do i = 1 to words(parms)
         If ABBREV("DEBUG",word(parms,i),3) = 1  then Do
            debug = "DEBUG"
            parms = DELWORD(parms,i,1)
            i = i - 1
            iterate
         End
         If ABBREV("GOOD",word(parms,i),4) = 1  then Do
            reorg_status = "GOOD"
            parms = DELWORD(parms,i,1)
            i = i - 1
            iterate
         End
         If ABBREV("BAD",word(parms,i),3) = 1  then Do
            reorg_status = "BAD"
            parms = DELWORD(parms,i,1)
            i = i - 1
            iterate
         End
End
If debug = "DEBUG"  then Do
   Trace "I"
End

userid        = Userid()
rtcd          = 0
sysid         = Left(MVSVAR(sysname),3," ")
logfile_dsn   = "SYS3.REORGVS.LOGFILE"
input_count   = 0

Get_Input_Datasets:

"EXECIO 1 DISKR LIST     (STEM list. "
execiorc     = rc
If execiorc  = 2   Then ,        /* On end of file - Finish up  */
   Signal End_Up
If execiorc <> 0   Then Do
   Say ;
   Say "Error executing the EXECIO DISKR Rexx command for file",
       "LIST !!!"
   Say "File LIST was not properly preallocated or is invalid"
   Say "Input I/O count is " input_count
   Say "EXECIO Return Code is "execiorc" !!!"
   Say "Exec REORGVS5 is Terminated Prematurely     ..."
   rtcd = 12
   Signal End_up
End

input_count = input_count + 1

cluster_dsn = STRIP(WORD(list.1,1))

Get_Export_File_Dsn:

listdsi_rc = LISTDSI(EXPORT FILE)

If listdsi_rc <> 0   then Do
   Say "Error calling LISTDSI function to obtain EXPORT dsname !"
   Say "Return Code =" listdsi_rc
   rtcd = listdsi_rc
   Signal End_Up
End

export_dsn = sysdsname

Say ;
Say "REORGVS5 Exec - Log Reorganization Results ..."
Say ;
Say "version_level                   =>" version_level
Say ;
Say "Vsam Cluster Dataset Name       =>" cluster_dsn
Say "Export Image Dataset Name       =>" export_dsn
Say "Reorganization status           =>" reorg_status "reorganization"

Output_Log_Entry:

"%DYNALC DD(LOGFILE) DA('"logfile_dsn"') DISP(MOD)",
     "ATTEMPTS(1800) INTERVAL(3)"
dynalc_rc = rc

If dynalc_rc <> 0 Then Do
   Say ;
   Say "Error executing the DYNALC exec to allocate logfile dataset"
   Say "    """logfile_dsn""" !"
   Say "DYNALC exec return code is =>" dynalc_rc
   Say "Exec REORGVS5 is Terminated Prematurely     ..."
   rtcd = 12
   Signal End_up
End

datej       = Substr(Date(S),1,4)"/"Substr(Date(J),3,3)
timex       = Time()

logfile.1   = Copies(" ",150)      /* Initialize the LOGFILE record */
logfile.1   = OVERLAY(STRIP(cluster_dsn),logfile.1,1,44)
logfile.1   = OVERLAY(STRIP(reorg_status),logfile.1,46,4)
logfile.1   = OVERLAY(STRIP(export_dsn),logfile.1,51,44)
logfile.1   = OVERLAY(STRIP(datej),logfile.1,96,8)
logfile.1   = OVERLAY(STRIP(timex),logfile.1,105,8)

"EXECIO 1 DISKW LOGFILE  (STEM logfile. "
execiorc     = rc
If execiorc <> 0   Then Do
   Say ;
   Say "Error executing the EXECIO DISKW Rexx command for file",
       "LOGFILE !!!"
   Say "File LOGFILE was not properly allocated or invalid",
       "or an I/O error was encountered"
   Say "EXECIO Return Code is "execiorc" !!!"
   Say "Exec REORGVS5 is Terminated Prematurely     ..."
   rtcd = 12
   Signal End_up
End

Say "Reorganization Result Logged In =>" logfile_dsn

Signal Get_Input_Datasets

End_Up:

"EXECIO 0 DISKR LIST     (FINIS"
"EXECIO 0 DISKR EXPORT   (FINIS"
"EXECIO 0 DISKW LOGFILE  (FINIS"

Say ;
Say "Dataset Input Count is         " input_count
Say ;
Say "REORGVS5 exec return code is   " rtcd
Say ;

Return rtcd
