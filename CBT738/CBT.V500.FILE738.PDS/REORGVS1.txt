/*  REXX                                                              */
/**********************************************************************/
/*  MODULE  = REORGVS1   (Recreate a VSAM Cluster - Used with the     */
/*                        REORGVS exec).                              */
/*                                                                    */
/*  #¢# AUTHOR             TKMILLE                                    */
/*  #¢# CREATED            TKMILLE 01/04/2006                         */
/*  #¢# LAST UPDATE        TKMILLE 09/16/2008                         */
/*                                                                    */
/*  PURPOSE = This exec will build IDCAMS commands to recreate a      */
/*            VSAM cluster.  It will build the "LISTC ALL ENT"        */
/*            commands to generate an IDCAMS catalog listing          */
/*            SYSPRINT report and it will also build IDCAMS           */
/*            "EXPORT" commands to export and reimport the cluster    */
/*            data.                                                   */
/*                                                                    */
/*            Input File LIST must be preallocated prior to the       */
/*            execution of this exec.  This file contains the         */
/*            dataset names of the VSAM clusters to be reallocated.   */
/*                                                                    */
/*            File EXPORT is the IDCAMS Export image dataset name.    */
/*                                                                    */
/*            File COMMANDS contains the IDCAMS commands generated by */
/*            this exec.                                              */
/*                                                                    */
/*            Output Files EXPORT, COMMANDS, and IMPORT               */
/*            must be preallocated prior to the execution of this     */
/*            exec.                                                   */
/*                                                                    */
/*  PARMS:                                                            */
/*  (Required Parms):                                                 */
/*                                                                    */
/*          None                                                      */
/*                                                                    */
/*  (Optional Parms):                                                 */
/*                                                                    */
/*          DEBUG  =      The parameter which displays the statements */
/*                        as they are executed.                       */
/*                                                                    */
/*  Modifications/Tailoring:                                          */
/*                                                                    */
/*  Make the following modification before storing this Rexx exec.    */
/*                                                                    */
/*  1)        Change the length of "sysid" to your specification.     */
/*            The default is 3 characters if this is not              */
/*            modified.                                               */
/*                                                                    */
/*                                                                    */
/*  Return Codes:                                                     */
/*                                                                    */
/*    0   - Successful execution of the exec                          */
/*   12   - Invalid input or output files or the files were not       */
/*          preallocated properly.                                    */
/*                                                                    */
/**********************************************************************/
version_level = "V01.01.08"
Arg parms
Trace "O"

debug    = ""
Do i = 1 to words(parms)
         If ABBREV("DEBUG",word(parms,i),3) = 1  then Do
            debug = "DEBUG"
            parms = DELWORD(parms,i,1)
            i = i - 1
            iterate
         End
End
If debug = "DEBUG"  then Do
   Trace "I"
End

userid        = Userid()
rtcd          = 0
sysid         = Left(MVSVAR(sysname),3," ")
input_count   = 0

Get_Input_Datasets:

"EXECIO 1 DISKR LIST     (STEM list. "
execiorc     = rc
If execiorc  = 2   Then ,        /* On end of file - Finish up  */
   Signal End_Up
If execiorc <> 0   Then Do
   Say ;
   Say "Error executing the EXECIO DISKR Rexx command for file",
       "LIST !!!"
   Say "File LIST was not properly preallocated or is invalid"
   Say "Input I/O count is " input_count
   Say "EXECIO Return Code is "execiorc" !!!"
   Say "Exec REORGVS1 is Terminated Prematurely     ..."
   rtcd = 12
   Signal End_up
End

input_count = input_count + 1

dsn     = STRIP(WORD(list.1,1))

Output_EXPORT_Out_Record:

export.1    = "  EXPORT '"||STRIP(dsn)||"'   +"
export.2    = "          OUTFILE(IMAGE)  TEMPORARY"
export.3    = "                                   "
export.4    = "  SET MAXCC=0"

"EXECIO 4 DISKW EXPORT (STEM export. "
execiorc     = rc
If execiorc <> 0   Then Do
   Say ;
   Say "Error executing the EXECIO DISKW Rexx command for file",
       "EXPORT !!!"
   Say "File EXPORT was not properly preallocated or invalid",
       "or an I/O error was encountered"
   Say "EXECIO Return Code is "execiorc" !!!"
   Say "Exec REORGVS1 is Terminated Prematurely     ..."
   rtcd = 12
   Signal End_up
End


Output_LISTC_Record:

commands.1  = "  LISTC ALL ENT('"||STRIP(dsn)||"')"

"EXECIO 1 DISKW COMMANDS (STEM commands. "
execiorc     = rc
If execiorc <> 0   Then Do
   Say ;
   Say "Error executing the EXECIO DISKW Rexx command for file",
       "COMMANDS !!!"
   Say "File COMMANDS was not properly preallocated or invalid",
       "or an I/O error was encountered"
   Say "EXECIO Return Code is "execiorc" !!!"
   Say "Exec REORGVS1 is Terminated Prematurely     ..."
   rtcd = 12
   Signal End_up
End


Output_IMPORT_In_Record:

import.1    = "  IMPORT INFILE(IMAGE)  INTOEMPTY   +"
import.2    = "     ODS('"||STRIP(dsn)||"')"

"EXECIO 2 DISKW IMPORT (STEM import. "
execiorc     = rc
If execiorc <> 0   Then Do
   Say ;
   Say "Error executing the EXECIO DISKW Rexx command for file",
       "IMPORT !!!"
   Say "File IMPORT was not properly preallocated or invalid",
       "or an I/O error was encountered"
   Say "EXECIO Return Code is "execiorc" !!!"
   Say "Exec REORGVS1 is Terminated Prematurely     ..."
   rtcd = 12
   Signal End_up
End

Signal Get_Input_Datasets

End_Up:

"EXECIO 0 DISKR LIST      (FINIS"
export.1     = "  "
"EXECIO 1 DISKW EXPORT    (STEM export."
"EXECIO 0 DISKW EXPORT    (FINIS"
commands.1   = "  "
"EXECIO 1 DISKW COMMANDS  (STEM commands."
"EXECIO 0 DISKW COMMANDS  (FINIS"
import.1     = "  "
"EXECIO 1 DISKW IMPORT    (STEM import."
"EXECIO 0 DISKW IMPORT    (FINIS"

Say ;
Say "REORGVS1 Exec - Build IDCAMS Commands ..."
Say ;
Say "version_level                =>" version_level
Say ;
Say "Dataset Input Count is         " input_count
Say ;
Say "REORGVS1 exec return code is   " rtcd
Say ;

Return rtcd
