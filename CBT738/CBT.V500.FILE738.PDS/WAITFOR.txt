*******************************************************************
**    MODULE NAME : WAITFOR                                      **
**    TYPE        : PROGRAM                                      **
**    PARAMETERS  : PARM1 ( WAIT TIME AS HHMMSSth FORMAT )       **
**                                                               **
**    IT GET'S A PARAMETER. FORMAT OF THIS PARAMETER IS HHMMSSth.**
**    IT WAITS FOR A TIME THAT IS GIVEN TO THIS PROGRAM AS A     **
**    PARAMETER. IT EXECUTES STIMER MACRO. AT THE END OF THE     **
**    WAIT TIME IT RETURNS TO CALLER.                            **
**                                                               **
**    THIS PROGRAM WAS FOUND ON THE MAINFRAME WEEKLY WEB SITE:   **
**    WWW.MAINFRAMEWEEK.COM/CODE/SHOWCODE.PHP/0036/MW36MVS2.TXT  **
**    AND TAILORED.                                              **
**                                                               **
**    REGISTER USAGE:                                            **
**           R2   WORK REGISTER                                  **
**           R3   WORK REGISTER                                  **
**           R4   WORK REGISTER                                  **
**           R5   UNUSED                                         **
**           R6   SAVE AREA BACKWARD LINK                        **
**           R7   PARAMETER LIST ADDRESS                         **
**        R8-R10  UNUSED                                         **
**           R11  UNUSED                                         **
**           R12  BASE REGISTER                                  **
**           R13  SAVE AREA                                      **
*******************************************************************
WAITFOR  CSECT
WAITFOR  AMODE 31
WAITFOR  RMODE 24
***********************************************************************
         SAVE  (14,12),,WAITFOR_ASSEMBLED_ON_&SYSDATE._&SYSTIME
         LR    R12,R15              Load entry address
         USING WAITFOR,R12          Declare entry base
         LR    R7,R1                Preserve passed parmlist address
         LR    R6,R13               Store SAVEAREA Backward link
         L     R3,=A(STORSIZE)      Size of storage to get and clear
         STORAGE OBTAIN,LENGTH=(R3),LOC=(RES)
         ST    R1,8(,R13)           Set SAVEAREA Forward link
         LR    R13,R1               Address of obtained area
         USING STORAREA,R13         Addressability to obtained area
         ST    R6,4(,R13)           Set SAVEAREA Backward link
**********************************************************************
*   Pick up the passed stimer wait time (HHMMSSth) - 8 bytes EBCDIC
**********************************************************************
         MVC   WAITTIME,WAITINIT     Initialize the Waittime - 0 Secs
**********************************************************************
*        This program can be called from a Rexx exec (or via jcl
*        directly) or from another program as a subroutine call.
*        Determine the caller's method of calling WAITFOR.
*        Macro HOWCALLD will determine how this program was called.
**********************************************************************
         L     R2,0(,R7)
         LR    R1,R7                Restore R1
         HOWCALLD PARMCALL=PARMCALL
         B     GOWAIT
PARMCALL DS    0H
         CLC   0(2,R2),=H'1'        WaitTime must be at least 1 byte
         BL    GOWAIT               Invalid parm passed Use 0 Seconds
         CLC   0(2,R2),=H'8'        Must not be longer than 8 bytes
         BH    GOWAIT               Member Name Parm too long
         LH    R4,0(,R2)            Pick up the WaitTime length
         LA    R2,2(,R2)            Pick up the WaitTime Address
         B     GOWAIT
GOWAIT   DS    0H
         MVC   WAITTIME,0(R2)        Move wait time into WAITTIME
         STIMER WAIT,DINTVL=WAITTIME Issue STIMER WAIT Macro (HHMMSSth)
RETURN   DS    0H
         LR    R2,R13               Pointer to storage area
         L     R3,=A(STORSIZE)      Work area size
         L     R13,4(,R13)          Restore Caller's SAVEAREA address
         STORAGE RELEASE,LENGTH=(R3),ADDR=(R2)
         L     R14,12(,R13)         Restore R14
         XR    R15,R15              Zero Return Code
         LM    R0,R12,20(R13)       Restore caller's registers
         BSM   0,R14                Return to caller in caller's Amode
**********************************************************************
*    STATIC  STORAGE FOLLOWS
**********************************************************************
           LTORG
WAITINIT   DC   CL8'00000000'       Default Value for Invalid Parms
**********************************************************************
*    DYNAMIC STORAGE FOLLOWS
**********************************************************************
STORAREA   DSECT ,                  Reentrant storage
SAVEAREA   DS   20F
           DS   0D                  WAITTIME must be on a DW boundary
WAITTIME   DS   CL8                 Wait Time as HHMMSSth - EBCDIC
STORSIZE   EQU  *-STORAREA
           YREGS
           END
