OPMIUE01 TITLE 'VPS USER EXIT-01:   PRINT LINE'                   OPM3
*---------------------------------------------------------------------*
*                                                                     *
* I CALLED LRS WITH A PROBLEM WHICH WAS NOT REALLY A PROBLEM.     OPM2*
* A CUSTOMER OF MINE WAS CONVERTING A FEW DOT-MATRIX PRINTERS     OPM2*
* TO LASERJETS. THERE IS AN APPLICATION WHICH USES THE "+" ASA    OPM2*
* CARRIAGE CONTROL CHARACTER TO OVERPRINT A WORD MAKING IT APPEAR OPM2*
* AS BOLD. THIS IS LOST WHEN THEY MOVED TO LASERJETS. I GOT A     OPM2*
* RESPONSE FROM ONE OF THE SUPPORT STAFF TELLING ME OF AN OLD     OPM2*
* USER EXIT01 AT THE V6R2 LEVEL WHICH WILL DO IT. HE SENT ME THE  OPM2*
* CODE AND I PLANNED TO CONVERT IT. WELL, IN THE AFTERNOON HE     OPM2*
* SENT ME THE CODE ALREADY CONVERTED TO THE V8 LEVEL. PLUS HE     OPM2*
* TESTED IT. THIS IS STELLAR SUPPORT!!!!                          OPM2*
*                                                                 OPM2*
* OPM PLANS TO IMPLEMENT THIS EXIT IN A VERY LIMITED WAY. SINCE   OPM2*
* THE IMPLEMENTATION OF EXIT01 CAUSES THE EXIT TO BE CALLED FOR   OPM2*
* EVERY PRINT LINE WE PLAN TO BRING UP A MINI-VPS STC TO SERVICE  OPM2*
* ONLY THOSE PRINTERS NEEDING THIS SERVICE. THERE WOULD BE SEVERE OPM2*
* PERFORMANCE ISSUES EVEN IF THE EXIT WAS ENTERED, CHECKED FOR    OPM2*
* "HPL" AND EXITED IMMEDIATELY.                                   OPM2*
*                                                                 OPM2*
*                            JIM MARSHALL, SOFTWARE "JANITOR"     OPM2*
*                            US OFFICE OF PERSONNEL MANAGEMENT    OPM2*
*                            CENTER FOR INFORMATION SERVICES/     OPM2*
*                                DATA CENTER GROUP                OPM2*
*                            1900 E STREET NW - ROOM BH04         OPM2*
*                            WASHINGTON DC  20415                 OPM2*
*                            (W) 202-606-1261 (0700-1530ET)       OPM2*
*                            JIM.MARSHALL@OPM.GOV                 OPM2*
*                                                                 OPM2*
* CHANGE LOG:                                                     OPM2*
*                                                                 OPM2*
*  OPM1 - 11/22/2003 - LRS HAD MARKED THE CODE IT CHANGED WITH    OPM2*
*         "DC0063" OUT IN 73-80. LEFT THIS CODE AND CHANGED ALL   OPM2*
*         REFERENCES TO OPM                                       OPM2*
*                                                                 OPM2*
*  OPM2 - 11/22/2003 - ADDED ALL THE DOCUMENTATION.               OPM2*
*                                                                 OPM2*
*  OPM3 - 11/22/2003 - TOOK THE LRS CODE AND CHANGED THE MODULE   OPM2*
*         NAME TO OPMIUE01 AND ALL REFERENCES.                    OPM2*
*                                                                 OPM2*
*  OPM4 - 11/22/2003 - CHANGED THE HPL CODE CHECK TO "HPL". THIS  OPM2*
*         WILL ENSURE THERE IS NO MIXUP WITH OTHER HPL PRINTERS.  OPM2*
*                                                                 OPM2*
*  OPM5 - 12/01/2003 - THE HPL PART OF CODE USES GETMAIN/FREEMAIN OPM2*
*         CODE VERSUS $$VPSERV. THE CODE WILL WORK BUT SHOULD BE  OPM2*
*         FIXED.                                                  OPM2*
*                                                                 OPM2*
*  OPM6 - 01/21/2004 - THE OPM SEPINFO VALUE IS "HPL" VS "HP+".   OPM2*
*         CHANGED TRIGGER STRING FROM "HP+" TO "HPL" IAW OPM      OPM2*
*         STANDARDS.                                              OPM2*
*                                                                 OPM2*
*-----------------------------------------------------------------OPM2*
*                                                                     *
* THIS IS A SPECIAL VERSION OF VPS USER EXIT 01 THAT LOOKS FOR        *
* OVERPRINT CARRIAGE CONTROL CHARACTERS ON PRINT LINES.  WHEN AN      *
* OVERPRINT IS FOUND, THE PCL COMMAND TO TURN BOLD PRINTING ON IS     *
* INSERTED BEFORE EACH WORD IN THE PRINT LINE, THE PCL COMMAND TO     *
* TURN BOLD PRINTING OFF IS INSERTED AFTER EACH WORD IN THE PRINT     *
* LINE, AND FLAGS ARE SET TO PREVENT VPS FROM TRANSLATING DATA IN     *
* THE PRINT LINE.  THESE ACTIONS WILL NOT BE PERFORMED FOR ANY PRINT  *
* LINE OVER 384 CHARACTERS IN LENGTH.                                 *
*                                                                     *
* THIS EXIT ALSO LOOKS FOR BACKSPACE CHARACTERS (X'16') THAT ARE USED *
* WITHIN PRINT LINES TO INDICATE THAT THE WORD SHOULD BE PRINTED IN   *
* BOLD TYPE.  FOR PRINTERS WITH SEPINFO=HPL, "BOLD ON" AND "BOLD  OPM4*
* OFF" COMMANDS ARE INSERTED AROUND THE WORD THAT NEEDS TO BE         *
* HIGHLIGHTED, AND THE BACKSPACES AND SECOND APPEARANCE OF THE WORD   *
* ARE REMOVED.  THESE ACTIONS WILL NOT BE PERFORMED FOR ANY PRINT     *
* LINE OVER 1024 BYTES IN LENGTH.                                     *
*                                                                     *
* THESE FUNCTIONS ARE PERFORMED ONLY FOR TCP/IP PRINTERS WITH         *
* SEPINFO=HPL IN THEIR PRINTER DEFINITION.                        OPM4*
*                                                                     *
* ALL DATA OUTSIDE THE PCL COMMANDS IS TRANSLATED BY USING THE        *
* TRANSLATE TABLE SPECIFIED IN THE PRTXLATE KEYWORD.                  *
*                                                                     *
* BOTH CONDITIONS ARE NOT HANDLED FOR THE SAME PRINT LINE, I.E., A    *
* PRINT LINE WITH AN OVERPRINT CARRIAGE CONTROL WILL NOT BE CHECKED   *
* FOR BACKSPACE CHARACTERS.                                           *
*                                                                     *
* PRTROPTS=40000000 SHOULD BE USED FOR PRINTERS THAT NEED THESE       *
* SPECIAL FUNCTIONS, TO GUARANTEE THAT THE LINE WORK AREA IS AT       *
* LEAST 500 BYTES LONG.  HOWEVER, IT IS STILL POSSIBLE THAT THE       *
* REVISED TEXT WOULD EXCEED THE AVAILABLE LINE WORK AREA SIZE.  IF    *
* THAT SITUATION OCCURS, THE EXIT WILL NOT MODIFY THE PRINT LINE      *
* AT ALL.                                                             *
*                                                                     *
* THE BASE SOURCE CODE FOR THIS EXIT WAS COPIED FROM                  *
* DIST.VPS.V1R80 ON NOV 20, 2003. CHANGES CAN BE FOUND BY SEARCHING   *
* FOR "DC0063" IN COLUMNS 73 - 80. THE CHANGES ADDED TO THIS VERSION  *
* OF EXIT01 WERE COPIED FROM VPS.R62.SPECIAL(AS595E01).               *
*---------------------------------------------------------------------*
         SPACE 1
*---------------------------------------------------------------------*
*                                                                     *
*        VTAM PRINTER SUPPORT SYSTEM - VERSION 1, RELEASE 8.0         *
*                                                                     *
*        (C) COPYRIGHT - 1999, 2000                                   *
*                        LEVI, RAY & SHOUP, INC.                      *
*                        2401 WEST MONROE STREET                      *
*                        SPRINGFIELD, IL 62704                        *
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
*                                                                     *
*   MODULE NAME:    OPMIUE01 (US OFFICE OF PERSONNEL MANAGEMENT)  OPM3*
*                                                                     *
*                                                                     *
*   EXIT NAME:      PRINT LINE                                        *
*                                                                     *
*                                                                     *
*   EXIT TYPE:      PRINTER                                           *
*                                                                     *
*                                                                     *
*   EXIT TCB:       PRINTER                                           *
*                                                                     *
*                                                                     *
*   RECOVERY:       EXITNN= KEYWORD DETERMINES RECOVERY ON/OFF        *
*                                                                     *
*                   RECOVERY ON:  EXIT WILL BE DISABLED               *
*                                                                     *
*                   RECOVERY OFF: PRINTER WILL BE 'EDRAINED'          *
*                                                                     *
*                                                                     *
*   ATTRIBUTES:     REENTRANT, REUSEABLE, REFRESHABLE                 *
*                                                                     *
*                   PROBLEM STATE, KEY 8                              *
*                                                                     *
*                   AUTHORIZED                                        *
*                                                                     *
*                   AMODE=31 RMODE=31                                 *
*                                                                     *
*                                                                     *
*   FUNCTION:       THIS EXIT IS CALLED AS EACH LINE OF A DATASET     *
*                   IS ABOUT TO BE PRINTED.  IT IS CALLED PRIOR TO    *
*                   TRANSLATION, TRUNCATION OR CARRIAGE CONTROL       *
*                   VALIDATION.  IT CAN MODIFY THE CONTENTS OF A      *
*                   PRINT LINE, OR NOTIFY VPS THAT THE PRINT LINE     *
*                   IS NOT TO BE PROCESSED.                           *
*                                                                     *
*                                                                     *
*=====================================================================*
         SPACE
*---------------------------------------------------------------------*
*                                                                     *
*   FUNCTION:                                                         *
*                                                                     *
*   THE SAMPLE EXIT, SUPPLIED BY LRS, PROVIDES THE FOLLOWING:         *
*                                                                     *
*   - SUPPORT FOR THE XEROX TRANSPARENCY FEATURE OF THE XEROX LASER   *
*     PRINTERS.                                                       *
*                                                                     *
*   EACH FUNCTION IS SUPPORTED BY A SEPARATE SECTION OF CODE.  IT IS  *
*   ASSUMED THAT A PRINT LINE WILL QUALIFY FOR ONLY A SINGLE SECTION. *
*   IF THE PRINT LINE FAILS TO QUALIFY FOR EITHER SECTION, CONTROL IS *
*   RETURNED TO VPS WITH RC=00.                                       *
*                                                                     *
*                                                                     *
*   NOTES:                                                            *
*                                                                     *
*   - THIS EXIT IS CALLED FOR EVERY LINE OF A DATASET.                *
*                                                                     *
*   - THIS EXIT IS NOT CALLED FOR SEPARATOR LINES.                    *
*                                                                     *
*   - THIS EXIT CALLED PRIOR TO TRANSLATION, TRUNCATION, AND CARRIAGE *
*     CONTROL VALIDATION.                                             *
*                                                                     *
*   - THE DATA LENGTH AND CONTENT MAY BE ALTERED.                     *
*                                                                     *
*   - THE CARRIAGE CONTROL CHARACTER MAY BE ALTERED TO ANY VALID      *
*     CHARACTER FOR THE DATASET TYPE (IE. ASA TO ASA OR MACHINE TO    *
*     MACHINE).                                                       *
*                                                                     *
*   - THE LENGTH CANNOT BE SET TO 00.                                 *
*                                                                     *
*   - THE LENGTH CANNOT EXCEED THE VALUE IN FIELD 'LINELNLM'.         *
*                                                                     *
*   - 'LINELNLM' CANNOT BE MODIFIED.                                  *
*                                                                     *
*   - RESERVED AREAS WITHIN THE PRINT LINE DSECT CANNOT BE MODIFIED.  *
*                                                                     *
*   - MULTIPLE PRINT LINES MAY BE GENERATED FROM ONE INPUT PRINT      *
*     LINE USING RETURN CODE 8.  PLEASE NOTE THAT THE INPUT PRINT     *
*     LINE WILL BE AVAILABLE ONLY ON THE FIRST CALL. CALLS TO THE     *
*     EXIT GENERATED BY SPECIFYING RC08 WILL BE PASSED UNPREDICTABLE  *
*     DATA IN THE PRINT LINE.  IF THE ORIGINAL PRINT LINE MUST BE     *
*     REFERENCED IN SUBSEQUENT CALLS, THEN IT WILL BE THE USER'S      *
*     RESPONSIBILTY TO SAVE IT.  MULTIPLE CALLS GENERATED BY RC08     *
*     SHOULD BE TERMINATED BY SPECIFYING RC00.                        *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
*---------------------------------------------------------------------*
*                                                                     *
*   REGISTER USAGE:                                                   *
*                                                                     *
*      ON ENTRY:                                                      *
*         R0 ==>  REQUEST CODE (ALWAYS 00).                           *
*         R1 ==>  ADDRESS OF PARAMETER LIST.                          *
*        R13 ==>  ADDRESS OF CALLERS SAVEAREA.                        *
*        R14 ==>  RETURN ADDRESS.                                     *
*        R15 ==>  ENTRY ADDRESS.                                      *
*                                                                     *
*      DURING EXECUTION:                                              *
*      R0-R3 ==>  VOLATILE.                                           *
*         R4 ==>  ADDRESS OF SYSTEM ATTRIBUTES.                       *
*         R5 ==>  ADDRESS OF USER WORKAREA (2 WORDS).                 *
*         R6 ==>  ADDRESS OF PRINTER ATTRIBUTES.                      *
*         R7 ==>  ADDRESS OF DATASET ATTRIBUTES.                      *
*         R8 ==>  ADDRESS OF LINE ATTRIBUTES.                         *
*     R9-R11 ==>  VOLATILE.                                           *
*        R12 ==>  VPSSUE01 BASE REGISTER.                             *
*        R13 ==>  ADDRESS OF CALLERS SAVEAREA.                        *
*    R14-R15 ==>  VOLATILE.                                           *
*                                                                     *
*      ON EXIT:                                                       *
*         ALL REGISTERS WILL BE RESTORED TO ENTRY CONDITIONS, EXCEPT  *
*      R15, WHICH WILL CONTAIN THE RETURN CODE.                       *
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
*   PARAMETER LIST FORMAT:                                            *
*                                                                     *
*      A(SYSTEM ATTRIBUTES)  - MAPPING MACRO: VPSSSYAT                *
*      A(USER WORKAREA)      - MAPPING MACRO: N/A                     *
*      A(PRINTER ATTRIBUTES) - MAPPING MACRO: VPSSPRAT                *
*      A(DATASET ATTRIBUTES) - MAPPING MACRO: VPSSDSAT                *
*      A(PRINT LINE)         - MAPPING MACRO: VPSSLINE                *
*      A(XMEMORY ATTRIBUTES) - MAPPING MACRO: VPSSXMAT                *
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
*   RETURN CODES:                                                     *
*                                                                     *
*      00 - PROCESS THE LINE AND DON'T CALL AGAIN.                    *
*      04 - DELETE  THE LINE.                                         *
*      08 - PROCESS THE LINE AND CALL AGAIN.                          *
*                                                                     *
*---------------------------------------------------------------------*
         EJECT
*---------------------------------------------------------------------*
*        O P M I U E 0 1   E N T R Y                              OPM3*
*---------------------------------------------------------------------*
OPMIUE01 CSECT                                                    OPM3
OPMIUE01 AMODE ANY                                                OPM3
OPMIUE01 RMODE ANY                                                OPM3
         B     28(,R15)                BRANCH AROUND CONSTANTS.
         DC    CL8'OPMIUE01',CL8'&SYSDATE',CL8'&SYSTIME'          OPM3
         BAKR  R14,R0                  SAVE REGISTERS.                  VPS80
         LR    R12,R15                 LOAD PROGRAM BASE REGISTER.
         USING OPMIUE01,R12            ESTABLISH PROGRAM BASE REG OPM3
         SPACE
         LM    R4,R8,0(R1)             LOAD PARAMETER REGISTERS.
         USING SYADSECT,R4             ESTABLISH SYAT ADDRESSABILITY.
         USING PRADSECT,R6             ESTABLISH PRAT ADDRESSABILITY.
         USING DSADSECT,R7             ESTABLISH DSAT ADDRESSABILITY.
         USING LINEDSCT,R8             ESTABLISH LINE ADDRESSABILITY.
         SPACE
         LTR   R0,R0                   NORMAL PRINT LINE REQUEST?
         BNZ   UXITRC00                NO, RETURN W/ RC=00.
         SPACE
*---------------------------------------------------------------------*
*        INVOKE XEROX LASER CONTROL SECTION                           *
*---------------------------------------------------------------------*
         SPACE
         BAS   R14,XRXLASER            PROCESS XEROX LASER.
         B     UXIT010(R15)            TAKE APPROPRIATE ACTION.
UXIT010  DS    0H
         B     UXITRC00                00 - RETURN W/ RC=00.
         B     UXITRC04                04 - RETURN W/ RC=04.
*                                      08 - INVOKE NEXT SECTION.
         SPACE
*        B     UXITRC00                RETURN W/ RC=00.
         EJECT
*-----------------------------------------------------------------OPM1* DC0063
*        INVOKE HPL OVERPRINT SECTION                             OPM1* DC0063
*-----------------------------------------------------------------OPM1* DC0063
         BAL   R14,HPLLASER            PROCESS HPL OVERPRINT.     OPM1  DC0063
         B     UXIT020(R15)            TAKE APPROPRIATE ACTION.   OPM1  DC0063
UXIT020  DS    0H                                                 OPM1  DC0063
         B     UXITRC00                00 - RETURN W/ RC=00.      OPM1  DC0063
         B     UXITRC04                04 - RETURN W/ RC=04.      OPM1  DC0063
*                                      08 - INVOKE NEXT SECTION.  OPM1  DC0063
         SPACE
         B     UXITRC00                RETURN W/ RC=00.
         SPACE
*---------------------------------------------------------------------*
*        O P M I U E 0 1   E X I T                                OPM3*
*---------------------------------------------------------------------*
         SPACE
UXITRC00 DS    0H
         LA    R15,RC00                SET RETURN CODE = 00.
         B     UXIT999                 RETURN W/ RC=00.
         SPACE
UXITRC04 DS    0H
         LA    R15,RC04                SET RETURN CODE = 04.
         B     UXIT999                 RETURN W/ RC=00.
         SPACE
UXIT999  DS    0H
         EREG  R0,R1                   RESTORE R0 AND R1.               VPS80
         PR    ,                       RESTORE STATUS AND RETURN.       VPS80
         TITLE 'XEROX LASER CONTROLS'
*---------------------------------------------------------------------*
*                                                                     *
*   FUNCTION:                                                         *
*                                                                     *
*   THIS SECTION SUPPORTS THE XEROX TRANSPARENCY FEATURE OF THE       *
*   XEROX LASER PRINTERS.  IT ONLY PROCESSES DATASETS WITH THE        *
*   FOLLOWING ATTRIBUTES:                                             *
*                                                                     *
*   - SEPINFO MUST = XRX IN THE 1ST 3 BYTES                           *
*   - LUTYPE MUST = 1 OR 6                                            * VPS61008
*   - DATASET CLASS MUST = 6                                          *
*   - LINE MUST NOT HAVE CARRIAGE CONTROL                             *
*   - LINE LENGTH MUST BE LESS THAN OR EQUAL TO 80                    *
*                                                                     *
*   AFTER THE ABOVE CONDITIONS HAVE BEEN SATISFIED, THE FOLLOWING     *
*   ACTIONS ARE PERFORMED:                                            *
*                                                                     *
*   - THE LINE IS PADDED WITH BLANKS TO 80 BYTES.                     *
*   - XEROX TRANSPARENCY CHARACTER (X'36') AND LENGTH BYTE (X'50')    *
*     ARE INSERTED AT THE BEGINNING OF THE LINE.                      *
*   - THE LINE LENGTH FIELD IS UPDATED ('LINELNL').                   *
*   - THE LINE FLAGS ARE SET TO INFORM VPS OF THE FOLLOWING:          *
*     - BYPASS CC PROCESSING                                          *
*     - BYPASS TRANSLATION                                            *
*     - BYPASS TRUNCATION OF TRAILING BLANKS                          *
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
*   REGISTER USAGE:                                                   *
*                                                                     *
*      ON ENTRY:                                                      *
*      R0-R3 ==>  UNPREDICTABLE.                                      *
*         R4 ==>  ADDRESS OF SYSTEM ATTRIBUTES.                       *
*         R5 ==>  ADDRESS OF USER WORKAREA (2 WORDS).                 *
*         R6 ==>  ADDRESS OF PRINTER ATTRIBUTES.                      *
*         R7 ==>  ADDRESS OF DATASET ATTRIBUTES.                      *
*         R8 ==>  ADDRESS OF LINE ATTRIBUTES.                         *
*     R9-R11 ==>  UNPREDICTABLE.                                      *
*        R12 ==>  VPSSUE01 BASE REGISTER.                             *
*        R13 ==>  ADDRESS OF CALLERS SAVEAREA.                        *
*        R14 ==>  RETURN ADDRESS.                                     *
*        R15 ==>  UNPREDICTABLE.                                      *
*                                                                     *
*      ON EXIT:                                                       *
*         ALL REGISTERS WILL BE RESTORED TO ENTRY CONDITIONS, EXCEPT  *
*      R15, WHICH WILL CONTAIN THE RETURN CODE.                       *
*                                                                     *
*---------------------------------------------------------------------*
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*   RETURN CODES:                                                     *
*                                                                     *
*      00 - LINE HAS BEEN PROCESSED (RETURN TO VPS W/ RC=00).         *
*      04 - LINE HAS BEEN PROCESSED (RETURN TO VPS W/ RC=04).         *
*      08 - LINE HAS NOT BEEN PROCESSED (INVOKE OTHER SECTIONS).      *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
*---------------------------------------------------------------------*
*        XEROX LASER CONTROLS                                         *
*---------------------------------------------------------------------*
         SPACE
XRXLASER DS    0H
         BAKR  R14,R0                  SAVE REGISTERS.                  VPS80
         SPACE
         XR    R10,R10                 RESET WORKAREA BASE REGISTER.
         SPACE
         CLC   =CL3'XRX',PRASEPI       IS THIS A XEROX LASER PRINTER?
         BNE   XRXRC08                 NO, RETURN W/ RC=08.
         SPACE
         CLI   PRALUTYP,$PRALU1        LU-1 DEVICE?
         BE    XRX005                  YES, CONTINUE.                   VPS61008
         CLI   PRALUTYP,$PRALU6        LU-6 DEVICE?                     VPS61008
         BNE   XRXRC08                 NO, RETURN W/ RC=08.             VPS61008
XRX005   DS    0H                                                       VPS61008
         CLI   DSACLS,C'6'             CLASS = 6?
         BNE   XRXRC08                 NO, RETURN W/ RC=08.
         SPACE
         TM    LINELNF,$LINEHCC        IS CC PRESENT?
         BO    XRXRC08                 YES, RETURN W/ RC=08.
         SPACE
         LA    R14,LINELIN             POINT TO DATA LINE.
         LH    R1,LINELNL              GET ORIGINAL LENGTH.
         CH    R1,=AL2(XRXMAXLN)       VALID LENGTH?
         BH    XRXRC08                 NO,  RETURN W/ RC=08.
         BE    XRX015                  YES, FULL RECORD.
         SPACE
         LH    R0,=AL2(XRXMAXLN)       ESTABLISH...
         SR    R0,R1                      LOOP COUNTER.
         LA    R14,0(R1,R14)           OFFSET OF 1ST TRAILING BLANK.
XRX010   DS    0H
         MVI   0(R14),C' '             ADD TRAILING BLANK.
         LA    R14,1(,R14)             BUMP TO NEXT BYTE.
         BCT   R0,XRX010               LOOP UNTIL COMPLETE.
         SPACE
XRX015   DS    0H
         LH    R1,=AL2(XRXMAXLN)       SET...                           VPS61087
         LA    R1,L'XRXSCS(,R1)           LENGTH...
         STH   R1,LINELNL                    OF NEW LINE.
         SPACE
*---------------------------------------------------------------------*
*                                                                     *
*        INSERT XEROX TRANSPARENCY CHARACTER                          *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
         $$VPSERV REQUEST=GETMAIN,                                     XVPS80
               LV=XRXLEN,                                              XVPS80
               SYAT=YES,                                               XVPS80
               MF=S                                                     VPS80
         LR    R10,R1                  LOAD XRX BASE REGISTER.
         USING XRXDSECT,R10            ESTABLISH XRX ADDRESSABILITY.
         SPACE
         MVC   XRXWORK,LINELIN              COPY DATA LINE TO WORKAREA.
         MVC   LINELIN(L'XRXSCS),XRXSCS     INSERT XEROX TRANSPARENCY.
         MVC   LINELIN+L'XRXSCS(80),XRXWORK APPEND FONT DATA.
         SPACE
         OI    LINELNF,$LINECCP            BYPASS CC PROCESSING.
         OI    LINELNF2,$LINEBXL+$LINEBTB  BYPASS XLATE AND TRUNCATION.
         DROP  R10                     KILL XEROX ADDRESSABILITY.
         SPACE
*---------------------------------------------------------------------*
*        FREE XEROX WORKAREA                                          *
*---------------------------------------------------------------------*
         SPACE
         LTR   R10,R10                 XRX WORKAREA ALLOCATED?
         BZ    XRX020                  NO, BYPASS FREEMAIN.
         $$VPSERV REQUEST=FREEMAIN,                                    XVPS80
               A=(R10),                                                XVPS80
               LV=XRXLEN,                                              XVPS80
               SYAT=YES,                                               XVPS80
               MF=S                                                     VPS80
         XR    R10,R10                 RESET WORKAREA BASE REGISTER.
XRX020   DS    0H
         B     XRXRC00                 RETURN W/ RC=00.
         SPACE
*---------------------------------------------------------------------*
*        RC=00 (RETURN TO VPS AND PROCESS THE LINE)                   *
*---------------------------------------------------------------------*
         SPACE
XRXRC00  DS    0H
         LA    R15,RC00                SET RETURN CODE = 00.
         B     XRX999                  RETURN W/ RC=00.
         SPACE
*---------------------------------------------------------------------*
*        RC=04 (RETURN TO VPS AND DISCARD THE LINE)                   *
*---------------------------------------------------------------------*
         SPACE
XRXRC04  DS    0H
         LA    R15,RC04                SET RETURN CODE = 04.
         B     XRX999                  RETURN W/ RC=04.
         SPACE
*---------------------------------------------------------------------*
*        RC=08 (INVOKE NEXT SECTION)                                  *
*---------------------------------------------------------------------*
         SPACE
XRXRC08  DS    0H
         LA    R15,RC08                SET RETURN CODE = 08.
         B     XRX999                  RETURN W/ RC=08.
         SPACE
*---------------------------------------------------------------------*
*        RETURN TO CALLER                                             *
*---------------------------------------------------------------------*
         SPACE
XRX999   DS    0H
         EREG  R0,R1                   RESTORE R0 AND R1.               VPS80
         PR    ,                       RESTORE STATUS AND RETURN.       VPS80
         TITLE 'HPL OVERPRINT CONTROL'                            OPM1  DC0063
*-----------------------------------------------------------------OPM1* DC0063
*   RETURN CODES:                                                 OPM1* DC0063
*                                                                 OPM1* DC0063
*      00 - LINE HAS BEEN PROCESSED (RETURN TO VPS W/ RC=00).     OPM1* DC0063
*      04 - LINE HAS BEEN PROCESSED (RETURN TO VPS W/ RC=04).     OPM1* DC0063
*      08 - LINE HAS NOT BEEN PROCESSED (INVOKE OTHER SECTIONS).  OPM1* DC0063
*-----------------------------------------------------------------OPM1* DC0063
         SPACE 1                                                  OPM1  DC0063
HPLLASER DS    0H                                                 OPM1  DC0063
         LR    R11,R14                 SAVE RETURN ADDRESS.       OPM1  DC0063
         XR    R10,R10                 RESET WORKAREA BASE REG    OPM1  DC0063
         SPACE 1                                                  OPM1  DC0063
         CLI   PRALUTYP,$PRALUT        TCP/IP DEVICE?             OPM1  DC0063
         BNE   HPLRC08                 NO, RETURN W/ RC=08.       OPM1  DC0063
         SPACE 1                                                  OPM1  DC0063
         CLC   =CL3'HPL',PRASEPI       IS THIS AN HP PRINTER?     OPM4  DC0063
         BNE   HPLRC08                 NO, RETURN W/ RC=08.       OPM1  DC0063
         SPACE 1                                                  OPM1  DC0063
         TM    LINELNF,$LINEHCC        IS CC PRESENT?             OPM1  DC0063
         BZ    HPB000                  NO, GO CHECK FOR BACKSPACESOPM1  DC0063
         SPACE 1                                                  OPM1  DC0063
         CLI   LINELNC,C'+'            IS CC ASA OVERPRINT?       OPM1  DC0063
         BE    HPL0010                 YES, GO CHECK LINE LENGTH. OPM1  DC0063
         SPACE 1                                                  OPM1  DC0063
         CLI   LINELNC,X'01'           IS CC MACHINE OVERPRINT?   OPM1  DC0063
         BNE   HPB000                  NO, GO CHECK FOR BACKSPACESOPM1  DC0063
         SPACE 1                                                  OPM1  DC0063
HPL0010  DS    0H                                                 OPM1  DC0063
         LH    R15,LINELNL             LOAD R15 W/CURRENT LNE LEN OPM1  DC0063
         BCTR  R15,0                   DECREMENT BY 1 FOR CCTL.   OPM1  DC0063
         CH    R15,=H'384'             IS LINE DATA > 384 BYTES?  OPM1  DC0063
         BH    HPLRC00                 YES, RETURN W/ RC=00.      OPM1  DC0063
         SPACE 1                                                  OPM1  DC0063
         CH    R15,=H'1'               IS LINE DATA < 1 BYTE?     OPM1  DC0063
         BL    HPLRC00                 YES, RETURN W/ RC=00.      OPM1  DC0063
         SPACE 1                                                  OPM1  DC0063
         $$VPSERV REQUEST=GETMAIN,                                OPM5 XVPS80
               LV=HPLLEN,                                         OPM5 XVPS80
               SYAT=YES,                                          OPM5 XVPS80
               MF=S                                               OPM5  VPS80
         LR    R10,R1                  LOAD HPL BASE REGISTER.    OPM1  DC0063
         USING HPLDSECT,R10            ESTABLISH ADDRESSABILITY.  OPM1  DC0063
         SPACE 1                                                  OPM1  DC0063
         LA    R0,HPLWORK              POINT R0 TO LINE SAVE AREA.OPM1  DC0063
         LH    R1,LINELNL              LOAD R1 W/LENGTH OF LINE.  OPM1  DC0063
         BCTR  R1,0                    DECREMENT BY 1 FOR CCTL.   OPM1  DC0063
         LA    R14,LINELND             POINT R14 TO LINE DATA.    OPM1  DC0063
         LR    R15,R1                  LOAD R15 W/LEN OF LINE DATAOPM1  DC0063
         MVCL  R0,R14                  MOVE LINE DATA TO SAVE AREAOPM1  DC0063
         SPACE 1                                                  OPM1  DC0063
         LH    R1,LINELNL              LOAD R1 W/LENGTH OF LINE.  OPM1  DC0063
         STH   R1,HPLLNL               SAVE ORIGINAL LENGTH.      OPM1  DC0063
         BCTR  R1,0                    DECREMENT BY 1 FOR CCTL.   OPM1  DC0063
         LA    R2,HPLWORK              POINT R2 TO ORIGINAL DATA. OPM1  DC0063
         LA    R3,HPLNEW               POINT R3 TO REVISED DATA.  OPM1  DC0063
         SPACE 1                                                  OPM1  DC0063
         MVI   HPLFLAG,X'00'           INITIALIZE CMD FLAG.       OPM1  DC0063
         SPACE 1                                                  OPM1  DC0063
HPL0012  DS    0H                                                 OPM1  DC0063
         CLI   0(R2),X'40'             IS ORIGINAL BYTE A SPACE?  OPM1  DC0063
         BNE   HPL0020                 NO, GO INSERT "BOLD ON" CMDOPM1  DC0063
         SPACE 1                                                  OPM1  DC0063
HPL0014  DS    0H                                                 OPM1  DC0063
         MVI   0(R3),X'40'             MOVE BLANK TO REVISED DATA.OPM1  DC0063
         ICM   R9,B'1111',PRATXLAT     POINT R3 TO TRANSLATE TABLEOPM1  DC0063
         BZ    HPL0016                 NO TRANSLATE TABLE.        OPM1  DC0063
         TR    0(1,R3),0(R9)           TRANSLATE BYTE.            OPM1  DC0063
HPL0016  DS    0H                                                 OPM1  DC0063
         LA    R2,1(,R2)               BUMP TO NEXT INPUT BYTE.   OPM1  DC0063
         LA    R3,1(,R3)               BUMP TO NEXT OUTPUT BYTE.  OPM1  DC0063
         BCT   R1,HPL0012              GO CHECK NEXT BYTE.        OPM1  DC0063
         SPACE 1                                                  OPM1  DC0063
         B     HPL0090                 GO FREEMAIN STORAGE, ETC.  OPM1  DC0063
         SPACE 1                                                  OPM1  DC0063
HPL0020  DS    0H                                                 OPM1  DC0063
         MVC   0(L'BOLDON,R3),BOLDON   MOVE "BOLD ON" TO REVISED  OPM1  DC0063
*                                        DATA.                    OPM1  DC0063
         LH    R15,HPLLNL              GET CURRENT LINE LENGTH.   OPM1  DC0063
         LA    R15,5(,R15)             ADD 5 TO LENGTH.           OPM1  DC0063
         STH   R15,HPLLNL              SAVE MODIFIED LENGTH.      OPM1  DC0063
         LA    R3,5(,R3)               BUMP OUTPUT PTER PAST CMD. OPM1  DC0063
         MVI   HPLFLAG,X'01'           SET PCL CMD FLAG ON.       OPM1  DC0063
         SPACE 1                                                  OPM1  DC0063
HPL0022  DS    0H                                                 OPM1  DC0063
         MVC   0(1,R3),0(R2)           MOVE NON-BLANK TO REVISED  OPM1  DC0063
*                                        DATA.                    OPM1  DC0063
         ICM   R9,B'1111',PRATXLAT     POINT R3 TO TRANSLATE TABLEOPM1  DC0063
         BZ    HPL0024                 NO TRANSLATE TABLE.        OPM1  DC0063
         TR    0(1,R3),0(R9)           TRANSLATE THIS BYTE.       OPM1  DC0063
         SPACE 1                                                  OPM1  DC0063
HPL0024  DS    0H                                                 OPM1  DC0063
         LA    R2,1(,R2)               BUMP TO NEXT INPUT BYTE.   OPM1  DC0063
         LA    R3,1(,R3)               BUMP TO NEXT OUTPUT BYTE.  OPM1  DC0063
         BCT   R1,HPL0030              GO CHECK NEXT BYTE.        OPM1  DC0063
         SPACE 1                                                  OPM1  DC0063
         B     HPL0090                 GO FREEMAIN STORAGE, ETC.  OPM1  DC0063
         SPACE 1                                                  OPM1  DC0063
HPL0030  DS    0H                                                 OPM1  DC0063
         CLI   0(R2),X'40'             IS ORIGINAL BYTE A SPACE?  OPM1  DC0063
         BNE   HPL0022                 NO, MOVE IT TO REVISED AREAOPM1  DC0063
         SPACE 1                                                  OPM1  DC0063
         MVC   0(L'BOLDOFF,R3),BOLDOFF MOVE "BOLD OFF" TO REVISED OPM1  DC0063
*                                         DATA                    OPM1  DC0063
         LH    R15,HPLLNL              GET CURRENT LINE LENGTH.   OPM1  DC0063
         LA    R15,5(,R15)             ADD 5 TO LENGTH.           OPM1  DC0063
         STH   R15,HPLLNL              SAVE MODIFIED LENGTH.      OPM1  DC0063
         LA    R3,5(,R3)               BUMP OUTPUT PTER PAST CMD. OPM1  DC0063
         MVI   HPLFLAG,X'00'           SET PCL CMD FLAG OFF.      OPM1  DC0063
         B     HPL0014                 GO MOVE BLANK BYTE.        OPM1  DC0063
         SPACE 1                                                  OPM1  DC0063
HPL0090  DS    0H                                                 OPM1  DC0063
         CLI   HPLFLAG,X'01'           "BOLD ON" W/OUT "BOLD OFF"?OPM1  DC0063
         BNE   HPL0092                 NO, CONTINUE AS IS.        OPM1  DC0063
         SPACE 1                                                  OPM1  DC0063
         MVC   0(L'BOLDOFF,R3),BOLDOFF MOVE "BOLD OFF" TO REVISED OPM1  DC0063
*                                         DATA                    OPM1  DC0063
         LH    R15,HPLLNL              GET CURRENT LINE LENGTH.   OPM1  DC0063
         LA    R15,5(,R15)             ADD 5 TO LENGTH.           OPM1  DC0063
         STH   R15,HPLLNL              SAVE MODIFIED LENGTH.      OPM1  DC0063
         SPACE 1                                                  OPM1  DC0063
HPL0092  DS    0H                                                 OPM1  DC0063
         CLC   HPLLNL,LINELNLM         IS REVISED LINE TOO LARGE? OPM1  DC0063
         BH    HPL0094                 YES, EXIT WITHOUT CHANGE.  OPM1  DC0063
         SPACE 1                                                  OPM1  DC0063
         LA    R0,LINELND              POINT R0 TO LINE DATA AREA.OPM1  DC0063
         LH    R1,HPLLNL               PUT NEW LINE LEN INTO R1   OPM1  DC0063
         BCTR  R1,0                    DECREMENT FOR CARRIAGE CNTLOPM1  DC0063
         LA    R14,HPLNEW              PT R14 TO REVISED LINE DATAOPM1  DC0063
         LR    R15,R1                  PUT NEW LINE LEN INTO R15. OPM1  DC0063
         MVCL  R0,R14                  MOVE LINE DATA TO SAVE AREAOPM1  DC0063
         SPACE 1                                                  OPM1  DC0063
         MVC   LINELNL,HPLLNL          RESTORE REVISED LINE LEN   OPM1  DC0063
         OI    LINELNF2,$LINEBXL+$LINEBTB  BYPASS XLATE AND TRUNC OPM1  DC0063
         SPACE 1                                                  OPM1  DC0063
HPL0094  DS    0H                                                 OPM1  DC0063
         $$VPSERV REQUEST=FREEMAIN,                               OPM5 XVPS80
               A=(R10),                                           OPM5 XVPS80
               LV=HPLLEN,                                         OPM5 XVPS80
               SYAT=YES,                                          OPM5 XVPS80
               MF=S                                               OPM5  VPS80
         XR    R10,R10                 RESET WORKAREA BASE REG    OPM1  DC0063
         B     HPLRC00                 RETURN W/ RC=00.           OPM1  DC0063
         SPACE 1                                                  OPM1  DC0063
         DROP  R10                     KILL HP ADDRESSABILITY.    OPM1  DC0063
         SPACE 1                                                  OPM1  DC0063
HPB000   DS    0H                                                 OPM1  DC0063
*-----------------------------------------------------------------OPM1* DC0063
*        FIND ANY BACKSPACE CHARACTERS (X'16') IN THE PRINT LINE. OPM1* DC0063
*-----------------------------------------------------------------OPM1* DC0063
         SPACE 1                                                  OPM1  DC0063
         XR    R15,R15                 CLEAR R15.                 OPM1  DC0063
         ICM   R15,B'0011',LINELNL     LOAD R15 W/LENGTH OF LINE. OPM1  DC0063
         C     R15,=F'1024'            IS LINE > 1024 BYTES LONG? OPM1  DC0063
         BH    HPBRC00                 YES, GO RETURN W/ RC=00.   OPM1  DC0063
         SPACE 1                                                  OPM1  DC0063
         LA    R9,LINELIN              LOAD R9 W/ADDRESS OF LINE. OPM1  DC0063
         TM    LINELNF,$LINEHCC        LINE HAVE CARRIAGE CONTROL?OPM1  DC0063
         BZ    HPB010                  NO, CONTINUE.              OPM1  DC0063
         LA    R9,1(,R9)               BUMP R9 PAST CARRIAGE CONTROPM1  DC0063
         BCTR  R15,0                   DECREMENT LEN OF DATA BY 1.OPM1  DC0063
HPB010   DS    0H                                                 OPM1  DC0063
         C     R15,=F'256'             OVER 256 BYTES TO CHECK?   OPM1  DC0063
         BNH   HPB020                  NO, CHECK REMAINING BYTES. OPM1  DC0063
         TRT   0(256,R9),BKSPTAB       CHECK FOR X'16'.           OPM1  DC0063
         BNZ   HPB030                  FOUND AT LEAST ONE.        OPM1  DC0063
         LA    R9,256(,R9)             BUMP DATA POINTER BY 256.  OPM1  DC0063
         S     R9,=F'256'              DECREMENT LENGTH BY 256.   OPM1  DC0063
         B     HPB010                  GO CHECK WHAT'S LEFT.      OPM1  DC0063
HPB020   DS    0H                                                 OPM1  DC0063
         C     R15,=F'4'               POSSIBLE TO HAVE BACKSPACE?OPM1  DC0063
         BL    HPBRC00                 NO, GO RETURN W/ RC=00.    OPM1  DC0063
         BCTR  R9,0                    DECREMENT BY 1 FOR CHECK.  OPM1  DC0063
         EX    R9,HPBTRT               CHECK FOR X'16'.           OPM1  DC0063
         BZ    HPBRC00                 NONE FOUND, RET W/ RC=00.  OPM1  DC0063
         SPACE 1                                                  OPM1  DC0063
*-----------------------------------------------------------------OPM1* DC0063
*        GETMAIN A WORK AREA                                      OPM1* DC0063
*-----------------------------------------------------------------OPM1* DC0063
HPB030   DS    0H                                                 OPM1  DC0063
         $$VPSERV REQUEST=GETMAIN,                                OPM5 XVPS80
               LV=HPLLEN,                                         OPM5 XVPS80
               SYAT=YES,                                          OPM5 XVPS80
               MF=S                                               OPM5  VPS80
         SPACE 1                                                  OPM1  DC0063
         LR    R10,R1                  LOAD HPL BASE REGISTER.    OPM1  DC0063
         USING HPLDSECT,R10            ESTABLISH HPL ADDR         OPM1  DC0063
         MVI   HPBCHG,X'00'            CLEAR CHANGE FLAG.         OPM1  DC0063
         LA    R9,LINELIN              LOAD R9 W/ADDRESS OF LINE. OPM1  DC0063
         XR    R15,R15                 CLEAR R15.                 OPM1  DC0063
         ICM   R15,B'0011',LINELNL     LOAD R15 W/LENGTH OF LINE. OPM1  DC0063
         LR    R1,R9                   LOAD R1 W/ADDRESS OF LINE. OPM1  DC0063
         AR    R1,R15                  BUMP BY LENGTH.            OPM1  DC0063
         ST    R1,HPBLINEE             SAVE END OF LINE ADDRESS.  OPM1  DC0063
         LA    R2,HPLNEW               LOAD R2 W/ADDR OF WORK AREAOPM1  DC0063
         SPACE 1                                                  OPM1  DC0063
         TM    LINELNF,$LINEHCC        LINE HAVE CARRIAGE CONTROL?OPM1  DC0063
         BZ    HPB040                  NO, CONTINUE.              OPM1  DC0063
         MVC   0(1,R2),0(R9)           MOVE CARR CTL TO WORK AREA.OPM1  DC0063
         LA    R2,1(,R2)               BUMP R2 PAST CARR CONTROL. OPM1  DC0063
         LA    R9,1(,R9)               BUMP R9 PAST CARR CONTROL. OPM1  DC0063
         BCTR  R15,0                   DECREMENT LEN OF DATA BY 1.OPM1  DC0063
         SPACE 1                                                  OPM1  DC0063
HPB040   DS    0H                                                 OPM1  DC0063
         ST    R9,HPBLINES             SAVE STARTING ADDR OF DATA.OPM1  DC0063
*-----------------------------------------------------------------OPM1* DC0063
*        SCAN FOR FIRST BACKSPACE                                 OPM1* DC0063
*-----------------------------------------------------------------OPM1* DC0063
         SPACE 1                                                  OPM1  DC0063
HPB050   DS    0H                                                 OPM1  DC0063
         C     R15,=F'0'               ANY MORE BYTES TO CHECK?   OPM1  DC0063
         BNH   HPBRC00                 NO, GO EXIT W/ RC=00.      OPM1  DC0063
         CLI   0(R9),X'16'             IS THIS A BACKSPACE?       OPM1  DC0063
         BE    HPB060                  YES, GO SAVE STARTING PT.  OPM1  DC0063
         SPACE 1                                                  OPM1  DC0063
         MVC   0(1,R2),0(R9)           MOVE BYTE TO WORK AREA.    OPM1  DC0063
         ICM   R3,B'1111',PRATXLAT     PT R3 TO TRANSLATE TABLE.  OPM1  DC0063
         BZ    HPB052                  NO TRANSLATE TABLE.        OPM1  DC0063
         TR    0(1,R2),0(R3)           TRANSLATE BYTE.            OPM1  DC0063
HPB052   DS    0H                                                 OPM1  DC0063
         LA    R2,1(,R2)               BUMP R2 TO NEXT BYTE.      OPM1  DC0063
         LA    R9,1(,R9)               BUMP R9 TO NEXT BYTE.      OPM1  DC0063
         BCTR  R15,0                   DECREMENT LENGTH BY 1.     OPM1  DC0063
         B     HPB050                  GO CHECK NEXT BYTE.        OPM1  DC0063
         SPACE 1                                                  OPM1  DC0063
HPB060   DS    0H                                                 OPM1  DC0063
         ST    R9,HPBKSP1              SAVE ADDR OF 1ST BACKSPACE.OPM1  DC0063
*-----------------------------------------------------------------OPM1* DC0063
*        FIND CONSECUTIVE BACKSPACE CHARACTERS                    OPM1* DC0063
*-----------------------------------------------------------------OPM1* DC0063
         SPACE 1                                                  OPM1  DC0063
HPB070   DS    0H                                                 OPM1  DC0063
         LA    R9,1(,R9)               BUMP R9 TO NEXT BYTE.      OPM1  DC0063
         BCTR  R15,0                   DECREMENT LENGTH BY 1.     OPM1  DC0063
         C     R15,=F'0'               ANY MORE BYTES TO CHECK?   OPM1  DC0063
         BNH   HPBRC00                 NO, GO EXIT W/ RC=00.      OPM1  DC0063
         CLI   0(R9),X'16'             IS THIS A BACKSPACE?       OPM1  DC0063
         BE    HPB070                  YES, GO CHECK NEXT BYTE.   OPM1  DC0063
         L     R14,HPBKSP1             LOAD R14 W/ADDR  1ST X'16' OPM1  DC0063
         LR    R1,R9                   LOAD R1 W/ADDR CURR BYTE.  OPM1  DC0063
         SR    R1,R14                  FIND NUMBER OF BACKSPACES. OPM1  DC0063
         SPACE 1                                                  OPM1  DC0063
         SR    R14,R1                  FIND START OF STRING.      OPM1  DC0063
         C     R14,HPBLINES            WE PRIOR TO START OF DATA? OPM1  DC0063
         BL    HPB090                  YES, GO CLEAR CHANGE SW    OPM1  DC0063
         SPACE 1                                                  OPM1  DC0063
         BCTR  R1,0                    DECREMENT LEN FOR COMPARE. OPM1  DC0063
         EX    R1,HPBCLC               IS WORD REPEATED?          OPM1  DC0063
         BNE   HPB090                  NO, GO CLEAR CHANGE SW     OPM1  DC0063
         SPACE 1                                                  OPM1  DC0063
         LA    R1,1(,R1)               RESTORE DECREMENTED BYTE.  OPM1  DC0063
         LR    R14,R9                  LOAD R14 W/CURRENT ADDRESS.OPM1  DC0063
         AR    R14,R1                  BUMP BY LENGTH OF DATA.    OPM1  DC0063
         C     R14,HPBLINEE            PAST THE END OF THE LINE?  OPM1  DC0063
         BH    HPB090                  YES, GO CLEAR CHANGE SW    OPM1  DC0063
         SPACE 1                                                  OPM1  DC0063
*-----------------------------------------------------------------OPM1* DC0063
*        MOVE IN "BOLD" COMMAND                                   OPM1* DC0063
*-----------------------------------------------------------------OPM1* DC0063
         SPACE 1                                                  OPM1  DC0063
         SR    R2,R1                   MAKE ROOM FOR BOLD COMMAND.OPM1  DC0063
         MVC   0(5,R2),BOLDON          MOVE IN BOLD COMMAND.      OPM1  DC0063
         LA    R2,5(,R2)               BUMP R2 PAST BOLD COMMAND. OPM1  DC0063
         SPACE 1                                                  OPM1  DC0063
*-----------------------------------------------------------------OPM1* DC0063
*        MOVE IN WORD(S) THAT ARE TO BE BOLD                      OPM1* DC0063
*-----------------------------------------------------------------OPM1* DC0063
         SPACE 1                                                  OPM1  DC0063
HPB080   DS    0H                                                 OPM1  DC0063
         MVC   0(1,R2),0(R9)           MOVE ONE BYTE OF BOLD WORD.OPM1  DC0063
         ICM   R3,B'1111',PRATXLAT     POINT R3 TO TRANS TABLE.   OPM1  DC0063
         BZ    HPB082                  NO TRANSLATE TABLE.        OPM1  DC0063
         TR    0(1,R2),0(R3)           TRANSLATE BYTE TO ASCII.   OPM1  DC0063
HPB082   DS    0H                                                 OPM1  DC0063
         LA    R2,1(,R2)               BUMP OUTPUT PTER BY 1 BYTE.OPM1  DC0063
         LA    R9,1(,R9)               BUMP INPUT PTER BY 1 BYTE. OPM1  DC0063
         BCT   R1,HPB080               CONT UNTIL ALL ARE MOVED.  OPM1  DC0063
         SPACE 1                                                  OPM1  DC0063
*-----------------------------------------------------------------OPM1* DC0063
*        MOVE IN "NORMAL" COMMAND                                 OPM1* DC0063
*-----------------------------------------------------------------OPM1* DC0063
         SPACE 1                                                  OPM1  DC0063
         MVC   0(5,R2),BOLDOFF         MOVE IN MEDIUM STROKE CMD  OPM1  DC0063
         LA    R2,5(,R2)               BUMP R2 PAST MEDIUM CMD    OPM1  DC0063
         MVI   HPBCHG,X'01'            INDICATE CHANGE WAS MADE.  OPM1  DC0063
         SPACE 1                                                  OPM1  DC0063
*-----------------------------------------------------------------OPM1* DC0063
*        FIND NUMBER OF BYTES REMAINING IN LINE AND CHECK THEM    OPM1* DC0063
*-----------------------------------------------------------------OPM1* DC0063
         SPACE 1                                                  OPM1  DC0063
         XR    R15,R15                 CLEAR R15.                 OPM1  DC0063
         ICM   R15,B'0011',LINELNL     MOVE IN ORIGINAL LINE LEN  OPM1  DC0063
         LR    R1,R9                   PUT CURRENT POS INTO R1.   OPM1  DC0063
         LA    R0,LINELIN              POINT R0 TO START OF LINE. OPM1  DC0063
         SR    R1,R0                   PUT LEN PROCESSED INTO R1. OPM1  DC0063
         SR    R15,R1                  CALCULATE AMT REMAINING.   OPM1  DC0063
         B     HPB050                  GO CHK FOR MORE BACKSPACES.OPM1  DC0063
         SPACE 1                                                  OPM1  DC0063
HPB090   DS    0H                                                 OPM1  DC0063
         MVI   HPBCHG,X'00'            CLEAR CHANGE FLAG.         OPM1  DC0063
         B     HPBRC00                 GO EXIT W/ RC=00.          OPM1  DC0063
         SPACE 1                                                  OPM1  DC0063
*-----------------------------------------------------------------OPM1* DC0063
*        RC=00 (PRINT LINE)                                       OPM1* DC0063
*-----------------------------------------------------------------OPM1* DC0063
         SPACE 1                                                  OPM1  DC0063
HPBRC00  DS    0H                                                 OPM1  DC0063
         CLI   HPBCHG,X'01'            ANY CHANGES TO LINE?       OPM1  DC0063
         BNE   HPBRC001                NO, GO EXIT W/ RC=00.      OPM1  DC0063
         LA    R3,HPLNEW               LOAD R3 W/ADDR OF WORK AREAOPM1  DC0063
         SR    R2,R3                   FIND LEN OF MODIFIED REC   OPM1  DC0063
         CH    R2,LINELNLM             IS NEW RECORD TOO LARGE?   OPM1  DC0063
         BH    HPBRC001                YES, LEAVE RECORD AS IS.   OPM1  DC0063
         SPACE 1                                                  OPM1  DC0063
         LA    R0,LINELIN              POINT R0 TO OUTPUT AREA.   OPM1  DC0063
         LA    R14,HPLNEW              POINT R14 TO WORK AREA.    OPM1  DC0063
         LR    R1,R2                   LOAD R1 WITH LENGTH.       OPM1  DC0063
         LR    R15,R1                  LOAD R15 WITH LENGTH.      OPM1  DC0063
         MVCL  R0,R14                  MOVE WK AREA TO OUTPUT AREAOPM1  DC0063
         SPACE 1                                                  OPM1  DC0063
         STCM  R2,B'0011',LINELNL      RESET LENGTH OF LINE.      OPM1  DC0063
         OI    LINELNF2,$LINEBXL       BYPASS TRANSLATION.        OPM1  DC0063
HPBRC001 DS    0H                                                 OPM1  DC0063
         LA    R15,RC00                SET RETURN CODE = 00.      OPM1  DC0063
         B     HPB999                  RETURN W/ RC=00.           OPM1  DC0063
         SPACE 1                                                  OPM1  DC0063
*-----------------------------------------------------------------OPM1* DC0063
*        RC=04 (PRINT LINE)                                       OPM1* DC0063
*-----------------------------------------------------------------OPM1* DC0063
         SPACE 1                                                  OPM1  DC0063
HPBRC04  DS    0H                                                 OPM1  DC0063
         LA    R15,RC04                SET RETURN CODE = 04.      OPM1  DC0063
         B     HPB999                  RETURN W/ RC=04.           OPM1  DC0063
         SPACE 1                                                  OPM1  DC0063
*-----------------------------------------------------------------OPM1* DC0063
*        RC=08 (INVOKE NEXT SECTION)                              OPM1* DC0063
*-----------------------------------------------------------------OPM1* DC0063
         SPACE 1                                                  OPM1  DC0063
HPBRC08  DS    0H                                                 OPM1  DC0063
         LA    R15,RC08                SET RETURN CODE = 08.      OPM1  DC0063
         B     HPB999                  RETURN W/ RC=08.           OPM1  DC0063
         SPACE 1                                                  OPM1  DC0063
*-----------------------------------------------------------------OPM1* DC0063
*        RETURN TO CALLER                                         OPM1* DC0063
*-----------------------------------------------------------------OPM1* DC0063
         SPACE 1                                                  OPM1  DC0063
HPB999   DS    0H                                                 OPM1  DC0063
         LR    R2,R15                  SAVE RETURN CODE.          OPM1  DC0063
         LTR   R10,R10                 HPL WORKAREA ALLOCATED?    OPM1  DC0063
         BZ    HPB999A                 NO, BYPASS FREEMAIN.       OPM1  DC0063
         $$VPSERV REQUEST=FREEMAIN,                               OPM5 XVPS80
               A=(R10),                                           OPM5 XVPS80
               LV=HPLLEN,                                         OPM5 XVPS80
               SYAT=YES,                                          OPM5 XVPS80
               MF=S                                               OPM5  VPS80
         XR    R10,R10                 RESET WORKAREA BASE REG    OPM1  DC0063
HPB999A  DS    0H                                                 OPM1  DC0063
         LR    R15,R2                  RESTORE RETURN CODE.       OPM1  DC0063
         LR    R14,R11                 RESTORE RETURN ADDRESS.    OPM1  DC0063
         BR    R14                     RETURN TO CALLER.          OPM1  DC0063
         SPACE 1                                                  OPM1  DC0063
HPBTRT   TRT   0(0,R9),BKSPTAB         *** EXECUTED ***           OPM1  DC0063
HPBCLC   CLC   0(0,R14),0(R9)          *** EXECUTED ***           OPM1  DC0063
BKSPTAB  DC    22X'00',X'FF',233X'00'  TRANS TABLE TO FIND X'16'. OPM1  DC0063
         SPACE 1                                                  OPM1  DC0063
*-----------------------------------------------------------------OPM1* DC0063
*        RC=00 (RETURN TO VPS AND PROCESS THE LINE)               OPM1* DC0063
*-----------------------------------------------------------------OPM1* DC0063
         SPACE 1                                                  OPM1  DC0063
HPLRC00  DS    0H                                                 OPM1  DC0063
         LA    R15,RC00                SET RETURN CODE = 00.      OPM1  DC0063
         B     HPL999                  RETURN W/ RC=00.           OPM1  DC0063
         SPACE 1                                                  OPM1  DC0063
*-----------------------------------------------------------------OPM1* DC0063
*        RC=04 (RETURN TO VPS AND DISCARD THE LINE)               OPM1* DC0063
*-----------------------------------------------------------------OPM1* DC0063
         SPACE 1                                                  OPM1  DC0063
HPLRC04  DS    0H                                                 OPM1  DC0063
         LA    R15,RC04                SET RETURN CODE = 04.      OPM1  DC0063
         B     HPL999                  RETURN W/ RC=04.           OPM1  DC0063
         SPACE 1                                                  OPM1  DC0063
*-----------------------------------------------------------------OPM1* DC0063
*        RC=08 (INVOKE NEXT SECTION)                              OPM1* DC0063
*-----------------------------------------------------------------OPM1* DC0063
         SPACE 1                                                  OPM1  DC0063
HPLRC08  DS    0H                                                 OPM1  DC0063
         LA    R15,RC08                SET RETURN CODE = 08.      OPM1  DC0063
         B     HPL999                  RETURN W/ RC=08.           OPM1  DC0063
         SPACE 1                                                  OPM1  DC0063
*-----------------------------------------------------------------OPM1* DC0063
*        RETURN TO CALLER                                         OPM1* DC0063
*-----------------------------------------------------------------OPM1* DC0063
         SPACE 1                                                  OPM1  DC0063
HPL999   DS    0H                                                 OPM1  DC0063
         LR    R14,R11                 RESTORE RETURN ADDRESS.    OPM1  DC0063
         BR    R14                     RETURN TO CALLER.          OPM1  DC0063
         SPACE 1                                                  OPM1  DC0063
*---------------------------------------------------------------------*
*        REGISTER EQUATES                                             *
*---------------------------------------------------------------------*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         TITLE 'LITERALS && CONSTANTS'
*---------------------------------------------------------------------*
*        LITERALS & CONSTANTS                                         *
*---------------------------------------------------------------------*
         LTORG
         SPACE
RC00     EQU   0
RC04     EQU   4
RC08     EQU   8
SPACE    EQU   X'40'
XRXMAXLN EQU   80                      MAXIMUM LINE SIZE.
         SPACE
XRXSCS   DC    XL2'3650'               XEROX SCS TRANSPARENCY CODES.
BOLDON   DC    X'1B28733342'           PCL "BOLD STROKEWEIGHT"    OPM1  DC0063
*                                         COMMAND                 OPM1  DC0063
BOLDOFF  DC    X'1B28733042'           PCL "NORMAL STROKEWEIGHT"  OPM1  DC0063
*                                         COMMAND                 OPM1  DC0063
         SPACE
         TITLE 'XEROX LASER WORKAREA DSECT'
*---------------------------------------------------------------------*
*        XEROX LASER WORKAREA DSECT                                   *
*---------------------------------------------------------------------*
         SPACE
XRXDSECT DSECT
XRXWORK  DC    CL80' '                 PRINT LINE WORKAREA.
         DS    XL16'00'                RESERVE FOR EXPANSION.           VPS80
         DS    0D                      ALIGN ON A DOUBLEWORD BOUNDARY.
XRXLEN   EQU   *-XRXDSECT              LENGTH OF DSECT.
         TITLE 'HPL WORKAREA DSECT'                               OPM1  DC0063
*-----------------------------------------------------------------OPM1* DC0063
*        HPL WORKAREA DSECT                                       OPM1* DC0063
*-----------------------------------------------------------------OPM1* DC0063
         SPACE 1                                                  OPM1
HPLDSECT DSECT                                                    OPM1  DC0063
HPLLNL   DS    H                       LINE LENGTH.               OPM1  DC0063
HPLFLAG  DS    XL1                     PCL BOLD ON/OFF FLAG.      OPM1  DC0063
HPLWORK  DS    CL384                   PRINT LINE WORKAREA.       OPM1  DC0063
HPBLINES DC    F'0'                    ADDR OF BEGINNING OF DATA. OPM1  DC0063
HPBLINEE DC    F'0'                    ADDR OF END       OF DATA. OPM1  DC0063
HPBKSP1  DC    F'0'                    ADDRESS OF FIRST BACKSPACE OPM1  DC0063
HPBCHG   DC    X'00'                   CHANGE FLAG.               OPM1  DC0063
HPLNEW   DS    CL4096                  REVISED PRT LINE WORKAREA. OPM1  DC0063
         SPACE 1                                                  OPM1  DC0063
         DS    0D                      ALIGN ON A DBLEWD BOUNDARY.OPM1  DC0063
HPLLEN   EQU   *-HPLDSECT              LENGTH OF DSECT.           OPM1  DC0063
         SPACE
         VPSSSYAT
         VPSSPRAT
         VPSSDSAT
         VPSSLINE
         END   OPMIUE01                                           OPM3
