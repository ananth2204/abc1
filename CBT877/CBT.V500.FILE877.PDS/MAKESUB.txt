/*REXX****************************************************************/
/*                                                                   */
/* MAKESUB -- Submit a make file.                                    */
/*                                                                   */
/*  When you Edit/View a make file, type MAKESUB in the command line */
/*  to submit a MAKEnnn batch job which will execute the file.       */
/*                                                                   */
/* Format:                                                           */
/*   MAKESUB Ýenv Ýssid¨¨    i.e ÝQMF|SQL|SPUFI|TSO|DSN... ÝDB2A¨¨   */
/*                                                                   */
/* Note. 1. Excluded lines with X/XX prefix command(s) are bypassed. */
/*       2. MAKESUB 'dsn' --  format to run as a TSO command.        */
/****************************************Vladimir Mestovski 2012******/

trace 'O'
 Parse UPPER SOURCE . cmdtype .
 datedsn = '.J'Date('J')
 timedsn = '.S'Time('S')
 makejob = 'MAKE'Time('M')
 makecmd = Userid()||'.TEMP.MAKE.CMD'||datedsn||timedsn
 makejcl = Userid()||'.TEMP.MAKE.JCL'||datedsn||timedsn

 Address "TSO"
 "MAKEINI"
 mkSSID=''
 "ISPEXEC VGET (mkSSID)"
 if mkSSID='' then do;
  say "*ERROR* MAKEINI member probably has been corrupted.."
  exit 8;
 end;

 if arg() > 0 & substr(arg(1),1,1)="'" then do;
/*------------------ as a TSO command ------------------------------*/
/* MAKESUB 'dsn'        <-- your make file                          */
/*------------------------------------------------------------------*/
  parse upper arg "'" makecmd "'"
  x = Outtrap('o.')
 "DELETE '"makejcl"'"
 'FREE FI($MAKEJCL)'
 "ALLOCATE FI($MAKEJCL) DSNAME('"makejcl"')",
      ' NEW DSORG(PS) RECFM(F,B) LRECL(80) ',
      ' SPACE(1,1) TRACKS    RELEASE UNIT(SYSDA) CATALOG'
  x = Outtrap('OFF')
  Call filljob;
 'EXECIO * DISKW $MAKEJCL (FINIS STEM jcl.'
  If rc<> 0 then exit 12
 'FREE FI($MAKEJCL)'
 "SUBMIT '"makejcl"'"
  x = Outtrap('o.')
 "DELETE '"makejcl"'"
  x = Outtrap('OFF')
  Exit 0
end

/*--------------------- inside View/Edit session -------------------*/
/* MAKESUB Ýenv Ýssid¨¨    i.e ÝQMF|SQL|SPUFI|TSO..  ÝDB2A¨¨        */
/*------------------------------------------------------------------*/
 if cmdtype='SUBROUTINE' then OPT1=arg(1);
 if cmdtype='COMMAND' then
 "ISREDIT" 'MACRO (OPT1) PROCESS';
 "ISPEXEC" 'CONTROL ERRORS RETURN'
 'ISREDIT (user) = USER_STATE'
 'ISREDIT (hrow,hcol) = CURSOR'
 'ISREDIT (scan) = SCAN'
 'ISREDIT SCAN OFF'
 'ISREDIT (LAST) = LINENUM .ZLAST'
 'ISREDIT (LRECL) = LRECL'

 if datatype(LAST)='CHAR' then do;
  say "*ERROR* YOU should remove pending prefix commands first.."
  exit 8;
 end;

 env = ''
 this_ssid = ''
 if opt1<>'' then do;
    parse upper var opt1 env this_ssid .
    env=env||' ';
 end;
 if this_ssid<>'' then mkSSID=this_ssid;
 if env='SCAN'    then do; mkSSID=mkSSID 'YES'; env=''; end;
 if env='PREPARE' then do; env=''; delete='KEEP'; end;

 k=0
 do i = 1 to LAST
  "ISREDIT" '(statusx) = XSTATUS' i
  if statusx = 'X' then iterate
  "ISREDIT" '(DATA) = LINE' i
  k=k+1
  lines.k = env||DATA
 end
 lines.0=k

 x = Outtrap('o.')
 "DELETE '"makecmd"'"
 "DELETE '"makejcl"'"
 'FREE FI($MAKECMD)'
 'FREE FI($MAKEJCL)'
 "ALLOCATE FI($MAKECMD) DSNAME('"makecmd"')",
     ' NEW DSORG(PS) RECFM(V,B) LRECL(256) ',
     ' SPACE(1,1) CYLINDERS RELEASE UNIT(SYSDA) CATALOG'
 "ALLOCATE FI($MAKEJCL) DSNAME('"makejcl"')",
     ' NEW DSORG(PS) RECFM(F,B) LRECL(80) ',
     ' SPACE(1,1) TRACKS    RELEASE UNIT(SYSDA) CATALOG'
 x = Outtrap('OFF')

 'EXECIO * DISKW $MAKECMD (FINIS STEM lines.'
 If rc>1   then exit (restoreState(rc))
 'FREE FI($MAKECMD)'

 Call filljob;

 'EXECIO * DISKW $MAKEJCL (FINIS STEM jcl.'
 If rc>1   then exit (restoreState(rc))
 'FREE FI($MAKEJCL)'

 if cmdtype='COMMAND' | OPT1='SCAN' then do;
  "SUBMIT '"makejcl"'"
  x = Outtrap('o.')
  "DELETE '"makejcl"'"
  x = Outtrap('OFF')
 End;
 Else "ISPEXEC VPUT (makejcl)"

 exit (restoreState(0))

/*--------------------------------------------------------------------*/
restoreState: parse arg src
  'ISREDIT USER_STATE = (user)'
  'ISREDIT SCAN = 'scan
  Select;
   When hcol=0 then src=1
   When hrow>last then 'ISREDIT LOCATE .ZL'
   Otherwise 'ISREDIT CURSOR = (hrow,hcol)'
  End
 if cmdtype='COMMAND' then "ISREDIT MEND"
Return src

/*--------------------------------------------------------------------*/
filljob:
 trace 'O'
 mkJCL0=0;
 Address "ISPEXEC"
 'VGET (mkJCL0)'
 c.0=mkJCL0
 do i=1 to mkJCL0
  'VGET (mkJCL'i')';
  c.i=value('mkJCL'i)
 end;
 'VGET (mkJCLLIB)'
 Do i=1 to c.0
 jcl.i=c.i
 x=SetVar('_mkJOB',makejob)
 x=SetVar('_mkSSID',mkSSID)
 x=SetVar('_mkJCLLIB',mkJCLLIB)
 x=SetVar('_mkCMD',makecmd)
 x=SetVar('_mkDELETE',delete)
 x=SetVar('_sysuid',Userid())
 End;
 jcl.0=c.0
 return;

SetVar: parse arg what,towhat
  p=index(jcl.i,what);
  if p>0 then
  jcl.i=substr(jcl.i,1,p-1)||towhat||substr(jcl.i,p+length(what))
  return 0;
