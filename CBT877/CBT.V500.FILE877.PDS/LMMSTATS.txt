/*REXX*****************************************************************/
/* LMMSTATS changes ISPF statistic of a member in PDS directory       */
/* Format:                                                            */
/*  LMMSTATS 'MDCT.PROD.C(A1)' +                                      */
/*  USER(id) CREATED(11/05/26) MODDATE(11/06/20) MODTIME(07:37)       */
/******************************************************************VM**/
trace 'O'
signal on Error
parse arg dsn_member opt
parse var dsn_member "'" DSN "(" MBR ")'"
If DSN = '' | MBR = '' then
do; say '*ERROR* Invalid syntax'; exit 8; end
parse var opt 'USER(' id ')' .
parse var opt 'CREATED(' created ')' .
parse var opt 'MODDATE(' moddate ')' .
parse var opt 'MODTIME(' modtime ')' .

Address ISPEXEC
"CONTROL ERRORS RETURN"

signal on  Error
say "Allocating output dataset '"DSN"'..."
"LMINIT DATAID(DD)   DATASET('"DSN"')   ENQ(SHR)"
"LMOPEN DATAID("DD") OPTION(INPUT)"
"LMMFIND DATAID("DD") MEMBER("MBR") STATS(YES)"
say "*** Old: USER("ZLUSER") CREATED("ZLCDATE") MODDATE("ZLMDATE")",
         " MODTIME("ZLMTIME")";
  if id<>''      then ZLUSER=id
  if created<>'' then ZLCDATE=created
  if moddate<>'' then ZLMDATE=moddate
  if modtime<>'' then ZLMTIME=modtime
"LMMSTATS DATAID("DD") MEMBER("MBR") USER("ZLUSER") ",
         "CREATED("ZLCDATE") MODDATE("ZLMDATE") MODTIME("ZLMTIME")"
"LMMFIND DATAID("DD") MEMBER("MBR") STATS(YES)"
say "*** New: USER("ZLUSER") CREATED("ZLCDATE") MODDATE("ZLMDATE")",
         " MODTIME("ZLMTIME")";
"LMFREE DATAID("DD")"
Exit 0;

Error:
 say '*ERROR*' strip(ZERRLM)
 "LMFREE DATAID("DD")"
 Exit 12
