/*REXX****************************************************************/
/*                                                                   */
/* MACRO -- execute ISPF Edit commands from a file                   */
/*                                                                   */
/*********************************************************************/
trace 'O'
signal on Error

"ISREDIT" 'MACRO (CLONEMBR) PROCESS'
"ISPEXEC" 'CONTROL ERRORS RETURN'

CLONETYP='';
if CLONEMBR = '' then
"ISPEXEC VGET (CLONEMBR)"
if CLONEMBR = '' then do;
 say "*WARNING* SET CLONEMBR was not specicied"
 Signal MEND
end

Address TSO "ALLOC FI(MACRO) DA('"CLONEMBR"') SHR REUSE"
signal off Error
"EXECIO * DISKR MACRO (stem file."
 if rc<>0 then do;
  say '*ERROR* Read Error for' CLONEMBR '-- probably not found'
  signal Error2
 end

signal on  Error
do i=1 to file.0

 file.i=strip(file.i)
 if file.i='' then iterate

 if substr(file.i,1,2) = '--' then iterate
 if substr(file.i,1,2) = '/*' then iterate
 if substr(file.i,1,1) = '*'  then iterate

 Parse Upper Var file.i cmd w1 w2 w3
 if cmd = 'END'    then leave
 if cmd = 'CANCEL' then Signal Error
 if cmd = 'C' | cmd = 'CHANGE' then Do;
  /*if CLONETYP = 'BACK' then do; w=w2; w2=w1; w1=w; end;*/
  /*if w3 = '' then w3 = 'ALL' */
    file.i=file.i 'ALL'
 End

signal off Error
 "ISREDIT" file.i

 if rc>4 then Signal Error

  'ISREDIT (c,nc) = CHANGE_COUNTS'
  if c=0 then Iterate
  say strip(file.i) ' -- 'c+0 'string(s) changed'
  if nc/=0 then do;
   say '*ERROR* 'nc+0 'string(s) that could not be changed'
   Signal Error;
  end;

end
MEND:
 "ISREDIT END"
 "ISREDIT MEND"
Exit 0
Error:
 say file.i
Error2:
 "ISREDIT CANCEL"
 "ISREDIT MEND"
 Exit 12
