/*REXX****************************************************************/
/* LMCLONE clones a member of a PDS                                  */
/*                                                                   */
/*  Format: SET CLONEMBR='DSN(MBRclone)'  <-- define before LMCLONEs */
/*                                                                   */
/*          LMCLONE 'DSNfrom(MBRfrom)' 'DSNto(MBRto)' ÝREPLACE¨      */
/*                                                                   */
/*****************************************************************VM**/
trace 'O'
signal on Error
parse arg dsn1 dsn2 opt
parse var dsn1 "'" DSNfrom "(" MBRfrom ")'"
parse var dsn2 "'" DSNto   "(" MBRto   ")'"
If DSNfrom = '' | MBRfrom = '' | DSNto = '' | MBRto = '',
 | (opt<>'REPLACE' & opt<>'') then
do; say '*ERROR* Invalid syntax'; exit 8; end

Address ISPEXEC
"CONTROL ERRORS RETURN"

say "Allocating input dataset '"DSNfrom"'..."
"LMINIT DATAID(DDFROM) DATASET('"DSNfrom"') ENQ(SHR)"
"LMOPEN  DATAID("DDFROM") OPTION(INPUT)"
"LMMFIND DATAID("DDFROM") MEMBER("MBRfrom") STATS(YES)"

say "Allocating output dataset '"DSNto"'..."
"LMINIT DATAID(DDTO)   DATASET('"DSNto"')   ENQ(SHR)"

say 'Copying member' MBRfrom 'to ' MBRto '...'
"LMCOPY FROMID("DDFROM") FROMMEM("MBRfrom") ",
             "TODATAID("DDTO"  )   TOMEM("MBRto")" opt
signal off Error

say 'Editing 'dsn2' with commands taken from CLONEMBR member'
"ISPEXEC EDIT DATASET("dsn2") MACRO(MACRO)"
signal on  Error
"LMMSTATS DATAID("DDTO") MEMBER("MBRto") USER("ZLUSER") ",
         "CREATED("ZLCDATE") MODDATE("ZLMDATE") MODTIME("ZLMTIME")"
"LMFREE DATAID("DDFROM")"
"LMFREE DATAID("DDTO")"
Exit 0;

Error:
 say '*ERROR*' strip(ZERRLM)
 "LMFREE DATAID("DDFROM")"
 "LMFREE DATAID("DDTO")"
 Exit 12
