/*REXX****************************************************************/
/* LMMOVE move a member to other lib                                 */
/*  Format: LMMOVE 'DSNfrom(MBRfrom)' 'DSNto(MBRto)' ÝREPLACE¨       */
/*****************************************************************VM**/
trace 'O'
signal on Error
parse arg dsn1 dsn2 opt
parse var dsn1 "'" DSNfrom "(" MBRfrom ")'"
parse var dsn2 "'" DSNto   "(" MBRto   ")'"
If DSNfrom = '' | MBRfrom = '' | DSNto = '' | MBRto = '',
 | (opt<>'REPLACE' & opt<>'') then
do; say '*ERROR* Invalid syntax'; exit 8; end

Address ISPEXEC
"CONTROL ERRORS RETURN"
say "Allocating input dataset '"DSNfrom"'..."
"LMINIT DATAID(DDFROM) DATASET('"DSNfrom"') ENQ(SHR)"
say "Allocating output dataset '"DSNto"'..."
"LMINIT DATAID(DDTO)   DATASET('"DSNto"')   ENQ(SHR)"
say 'Moving member' MBRfrom 'to ' MBRto '...'
"LMMOVE FROMID("DDFROM") FROMMEM("MBRfrom") ",
             "TODATAID("DDTO"  )   TOMEM("MBRto")" opt
"LMFREE DATAID("DDFROM")"
"LMFREE DATAID("DDTO")"
Exit 0;

Error:
 say '*ERROR*' strip(ZERRLM)
 "LMFREE DATAID("DDFROM")"
 "LMFREE DATAID("DDTO")"
 Exit 12
