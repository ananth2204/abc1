/*REXX****************************************************************/
/* SUBMIT a job and wait its end                                     */
/*****************************************************************VM**/
 trace 'O'
 Signal On Error;
 arg job 'MAXWAIT=' wtime .
 if wtime = '' then wtime=3600
 itime=10
 if wtime<itime then wtime=itime
 Address TSO
 x=outtrap('out.')
 'SUBMIT' job
 x=outtrap('OFF')
 say out.1
 parse var out.1 . jobid msg
 if msg <> 'SUBMITTED' then Signal Error
 x=outtrap('out.')
 'STATUS' jobid
 x=outtrap('OFF')
 parse var out.1 . . msg
 say msg
 if msg = 'ON OUTPUT QUEUE' then do;Call CheckOutput; exit 0; end;
 holdmsg=msg
 do wtime/itime while msg = 'WAITING FOR EXECUTION' | msg = 'EXECUTING'
   'SLEEP' itime
    x=outtrap('out.')
   'STATUS' jobid
    x=outtrap('OFF')
    parse var out.1 . . msg
    if holdmsg<>msg then say msg
    holdmsg=msg
 end
 if msg = 'ON OUTPUT QUEUE'
 then do; Call CheckOutput; exit 0; end;
 if msg = 'EXECUTING'
 then do;
    say '*ERROR* MAXWAIT='wtime 'is not enough to wait the end of job'
    Signal Error
 end
 exit 8
CheckOutput:
  trace 'O'
  Signal Off Error
    x=outtrap('o.')
  "DELETE TEMP.OUTLIST"
  "FREE FI(OUTLIST)"
    x=outtrap('OFF')
  Signal On  Error
  Address TSO "ALLOCATE FI(OUTLIST) DSNAME(TEMP.OUTLIST) ",
              " NEW DSORG(PS) RECFM(F,B) LRECL(132) ",
              " SPACE(10,10) CYLINDERS RELEASE ",
              " UNIT(SYSDA) MAXVOL(10) CATALOG REUSE"
  'OUTPUT' jobid 'HOLD KEEP PRINT(TEMP)'
  'EXECIO * DISKR OUTLIST (stem o. FINIS'
  Do j=1 to o.0
  if index(o.j,'JOB NOT RUN - JCL ERROR') > 0
   then do; say strip(o.j); Signal Error; end
  End;
  Do j=1 to o.0
  if index(o.j,'JOB FAILED - JCL ERROR') > 0
   then do; say strip(o.j); Signal Error; end
  End;
  Do j=1 to o.0
  if index(o.j,'IEA995I SYMPTOM DUMP OUTPUT') > 0
   then do; say strip(o.j); Signal Error; end
  End;
  Do j=1 to o.0
  if index(o.j,'ABEND=SB37 U0000') > 0
   then do; say strip(o.j); Signal Error; end
  End;

  Parse var jobid job '(' id ')'
  Do j=1 to o.0
  if index(o.j,id||'  GSDMV2') > 0
    then say strip(o.j);
  End;

  return;
Error:
 Exit 12
