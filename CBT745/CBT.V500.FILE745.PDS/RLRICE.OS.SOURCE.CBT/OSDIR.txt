* ------------------------------------------------------------------- *
*                                                                     *
*  Module name: OSDIR                                                 *
*                                                                     *
*  Build main body for non-load module PDS or PDSE directories.       *
*                                                                     *
*  This thing has a lot of functions so it is large.                  *
*  It is already close to needing a 3rd base register.                *
*  I already 'bit' the bullet and establish three base registers.     *
*                                                                     *
* ------------------------------------------------------------------- *
OSDIR    CSECT
OSDIR    AMODE 31
OSDIR    RMODE ANY
         USING OSCOMM,R12
         USING SESSION,R11
         USING OSDIR,R15
         USING VDATA,R7
         USING IHADCB,R4
         B     INIT0000
MODID    DC    CL8'OSDIR'
         DC    CL8'&SYSDATE'
         DC    CL8'&SYSTIME'
         DC    A(OSDIREND-OSDIR)
INIT0000 DS    0H
         USING OSCOMM,R12                     DEFINE COMMON AREA BASE
         STM   R14,R12,12(R13)                SAVE REGS
         LR    R10,R15                        COPY ENTRY POINT
         LA    R9,2048(R10)                   SET 2ND AND 3RD BASE
         LA    R9,2048(R9)                     .. DO IT STRAIGHT
         LA    R8,2048(R9)                     .. FORWARD.. NOTHING
         LA    R8,2048(R8)                     .. 'CUTE'
         DROP  R15
         USING OSDIR,R10,R9,R8                DEFINE BASE
         L     R5,COMM_OSSPFD                 SPF DATA
         USING OSSPFD,R5                      DEFINE BASE
         CLI   SESS_FORMAT_FLAGS,$FORMAT_INITIALIZE
         BNE   INIT0010
* ------------------------------------------------------------------- *
*        Perform initialization                                       *
* ------------------------------------------------------------------- *
         OC    SESS_FORMATTER_DXD_ADDR,SESS_FORMATTER_DXD_ADDR
         BNZ   ERR0150
         LA    R0,OSDIR_DXD_L
         ST    R0,SESS_FORMATTER_DXD_LENGTH
         GETMAIN RU,                          GETMAIN WORK AREA        +
               LV=OSDIR_DXD_L,                                         +
               LOC=BELOW
         ST    R1,SESS_FORMATTER_DXD_ADDR     SAVE WORK AREA ADDRESS
         LR    R2,R1
         LR    R0,R1
         LA    R1,OSDIR_DXD_L
         SR    R14,R14
         SR    R15,R15
         MVCL  R0,R14
         ST    R13,4(R2)                      SAVE R13
         ST    R2,8(R13)                      CHAIN SAVE AREA
         LR    R13,R2                         COPY WORK AREA ADDRESS
         USING OSDIR_WORK,R13                 DEFINE WORK AREA BASE
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY1
         XC    DXD_RC,DXD_RC                  INITIALIZE RC
         LA    R1,DIR_RECORD                  I/O AREA ADDRESS
         ST    R1,DSPCREQ_RECORD_ADDR
         ITRACE ID=I_O_AREA,                                           +
               RDATA1=R1
         MVC   DXD_OPEN(OPEN_L),OPEN_I        INITIALIZE OPEN
         MVC   DXD_CLOSE(CLOSE_L),CLOSE_I     INITIALIZE CLOSE
         MVI   LOADREQ_FUNC,$LOADREQ_LOAD     SET OSLOAD FUNCTION
         LA    R0,DATASPACE_1                 USE DATA SPACE 1
         ST    R0,DSPCREQ_DATASPACE
         ITRACE ID=VDATA
         MVC   SESS_DISP_PANEL,DIR_PANEL      SET PANEL NAME
         LA    R1,SESS_DISP_PANEL
         MVI   COMM_VDATA_FUNC,$VDATA_GETMAIN
         L     R15,COMM_V_OSVDATA
         BALR  R14,R15
         ST    R0,DXD_VDATA_LENGTH            SAVE VDATA LENGTH
         ST    R1,DXD_VDATA_ADDR              SAVE VDATA ADDR
         MVC   DXD_VWIDTH,SPF_VWIDTH          SAVE WIDTH
         MVC   DXD_VDEPTH,SPF_VDEPTH          SAVE DEPTH
         ICM   R4,15,SESS_DCB_ADDR            DCB ALREADY PRESENT?
         BNZ   EXIT0000                       YES
         LA    R0,PDSDCB_L                    DCB LENGTH
         ST    R0,SESS_DCB_LENGTH
         GETMAIN RU,                          GET STORAGE FOR DCB      +
               LV=(0),                                                 +
               LOC=BELOW
         LR    R4,R1                          SAVE DCB ADDRESS
         ST    R4,SESS_DCB_ADDR
         L     R1,=A(PDSDCB_I)
         MVC   0(PDSDCB_L,R4),0(R1)           INITIALIZE DCB
         MVC   DCBDDNAM,SESS_DD               SET DD NAME
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
INIT0010 DS    0H
         ICM   R15,15,SESS_FORMATTER_DXD_ADDR
         BZ    ERR0200
         ST    R13,4(R15)
         ST    R15,8(R13)
         LR    R13,R15
         ITRACE ID=ENTRY2,                                             +
               DATA1=(SESS_FORMAT_FLAGS,1),                            +
               DATA2=(COMM_SESS_FUNC,1)
         XC    DXD_RC,DXD_RC
         CLI   SESS_FORMAT_FLAGS,$FORMAT_CONTROL
         BE    EXIT0000
         CLI   SESS_FORMAT_FLAGS,$FORMAT_CLEANUP
         BE    CLEAN000
         L     R4,SESS_DCB_ADDR               DCB ADDRESS
         LA    R1,RTRY0000                    RETRY ADDRESS
         ST    R1,SESS_RETRY
         CLI   COMM_SESS_FUNC,$SESS_SWITCH    SESSION SWITCH?
         BNE   MAIN0000                       NO
         TM    DXD_FLAGS,$DXD_INIT            EVER BUILT DATA?
         BNO   MAIN0000                       NO
         L     R6,DXD_LINES_REMAINING         LINES REMAINING
         L     R7,DXD_CURRENT_VDATA           POSITION IN VDATA
         ITRACE ID=RESUME,                                             +
               RDATA1=R6,                                              +
               RDATA2=R7
         LTR   R6,R6                          RESUMING LINE CMDS?
         BNZ   MAIN0210                       YES
         B     MAIN0180                       RESUME AFTER PRIMARY CMD
* ------------------------------------------------------------------- *
*          (re)build display                                          *
* ------------------------------------------------------------------- *
MAIN0000 DS    0H
         OI    DXD_FLAGS,$DXD_INIT            SET FLAG
         MVC   DSPCREQ_RECORD_NBR,DATASPACE_1_DISPLAY_FIRST_RECORD
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0020 DS    0H
         XC    DXD_LINES,DXD_LINES            LINES BUILT
         L     R0,DXD_VDATA_ADDR              VDATA ADDRESS
         L     R1,DXD_VDATA_LENGTH            VDATA LENGTH
         SR    R14,R14                        CLEAR R14
         SR    R15,R15                        CLEAR R15
         MVCL  R0,R14                         INITIALIZE VDATA BUFFER
         CLI   SESS_DATA_TYPE,$DATA_TYPE_PDS_DIR DIRECTORY DATA?
         BNE   ERR0220                        NO
         L     R6,DXD_VDEPTH                  LINES AVAILABLE
         L     R7,DXD_VDATA_ADDR              BUFFER ADDRESS
* ------------------------------------------------------------------- *
*        Add heading to body                                          *
* ------------------------------------------------------------------- *
         ITRACE ID=HEADING,                                            +
               RDATA1=R6,                                              +
               RDATA2=R7
         LA    R2,HEADING_01_L                HEADING LENGTH
         C     R2,DXD_VWIDTH                  GREATER THAN SCREEN WIDTH
         BNH   MAIN0030                       NO
         L     R2,DXD_VWIDTH                  LIMIT TO SCREEN WIDTH
MAIN0030 DS    0H
         BCTR  R2,0                           INSTRUCTION LENGTH
         EX    R2,MVC_01                      COPY HEADING
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         XC    DXD_CURSOR_POSITION,DXD_CURSOR_POSITION
         B     MAIN0160
* ------------------------------------------------------------------- *
*        "Retreive" data and build body of SPF panel                  *
* ------------------------------------------------------------------- *
MAIN0040 DS    0H
         ITRACE ID=RETREIVE,                                           +
               DATA1=(DSPCREQ_RECORD_NBR,4),                           +
               DATA2=(DATASPACE_1_LAST_RECORD,4)
         CLC   DSPCREQ_RECORD_NBR,DATASPACE_1_LAST_RECORD
         BH    MAIN0180
         MVI   DSPCREQ_FUNC,$DSPCREQ_RETREIVE
         ITRACE ID=GET_RECD,                                           +
               DATA1=(DSPCREQ_RECORD_NBR,4),                           +
               DATA2=(DSPCREQ_RECORD_ADDR,4)
         MVI   DSPCREQ_FUNC,$DSPCREQ_RETREIVE
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE
         BALR  R14,R15
         CLI   DSPCREQ_RC,$DSPCREQ_OK
         BNE   ERR0010
         TM    SESS_USER_OPTIONS,$OPTIONS_SHOW_TAGGED
         BO    MAIN0050
         TM    SESS_USER_OPTIONS,$OPTIONS_SHOW_NOT_TAGGED
         BO    MAIN0060
         B     MAIN0070
MAIN0050 DS    0H
         TM    DIR_FLAGS,$DIR_TAGGED
         BNO   MAIN0170
         B     MAIN0070
MAIN0060 DS    0H
         TM    DIR_FLAGS,$DIR_TAGGED
         BO    MAIN0170
MAIN0070 DS    0H
         MVC   VDATA,COMM_BLANKS
         MVI   VDATA_ATTR_1,$SCREEN_ATTR_INPUT
         MVC   VDATA_MEMBER,DIR_NAME
         TM    DIR_FLAGS,$DIR_TAGGED
         BO    MAIN0080
         MVI   VDATA_ATTR_2,$SCREEN_ATTR_NORMAL
         B     MAIN0090
MAIN0080 DS    0H
         MVI   VDATA_ATTR_2,$SCREEN_ATTR_HIGH
MAIN0090 DS    0H
         MVI   VDATA_ATTR_3,$SCREEN_ATTR_NORMAL
         UNPK  DXD_WORK_1(7),DIR_TTR(4)
         TR    DXD_WORK_1(6),HEXCHAR
         MVC   VDATA_TTR,DXD_WORK_1
         TM    DIR_FLAGS,$DIR_DELETED
         BO    MAIN0120
         TM    DIR_FLAGS,$DIR_RENAMED
         BO    MAIN0130
         TM    DIR_FLAGS,$DIR_SPF
         BNO   MAIN0140
* ----------       FORMAT SPF STATISTICS            ----------------- *
         SR    R1,R1
         IC    R1,DIR_SPF_VERSION
         CVD   R1,COMM_DWORD
         MVC   DXD_WORK_1(L'EDIT_WORD_1),EDIT_WORD_1
         ED    DXD_WORK_1(L'EDIT_WORD_1),COMM_DWORD+4
         MVC   VDATA_VERSION,DXD_WORK_1+6
         OI    VDATA_VERSION,C'0'
         MVI   VDATA_DOT,C'.'
         IC    R1,DIR_SPF_LEVEL
         CVD   R1,COMM_DWORD
         MVC   DXD_WORK_1(L'EDIT_WORD_1),EDIT_WORD_1
         ED    DXD_WORK_1(L'EDIT_WORD_1),COMM_DWORD+4
         MVC   VDATA_LEVEL,DXD_WORK_1+6
         OI    VDATA_LEVEL,C'0'
         ICM   R1,3,DIR_SPF_SIZE
         CVD   R1,COMM_DWORD
         MVC   DXD_WORK_1(L'EDIT_WORD_1),EDIT_WORD_1
         ED    DXD_WORK_1(L'EDIT_WORD_1),COMM_DWORD+4
         MVC   VDATA_SIZE,DXD_WORK_1+1
         ICM   R1,3,DIR_SPF_INIT
         CVD   R1,COMM_DWORD
         MVC   DXD_WORK_1(L'EDIT_WORD_1),EDIT_WORD_1
         ED    DXD_WORK_1(L'EDIT_WORD_1),COMM_DWORD+4
         MVC   VDATA_INIT,DXD_WORK_1+1
AP1      AP    DIR_SPF_C_DATE,P1900000
         MVC   DXD_WORK_1(L'EDIT_WORD_2),EDIT_WORD_2
         ED    DXD_WORK_1(L'EDIT_WORD_2),DIR_SPF_C_DATE
         MVC   VDATA_CREATE_DATE,DXD_WORK_1+1
MAIN0100 DS    0H
AP2      AP    DIR_SPF_M_DATE,P1900000
         MVC   DXD_WORK_1(L'EDIT_WORD_2),EDIT_WORD_2
         ED    DXD_WORK_1(L'EDIT_WORD_2),DIR_SPF_M_DATE
         MVC   VDATA_MOD_DATE,DXD_WORK_1+1
MAIN0110 DS    0H
         UNPK  DXD_WORK_1(7),DIR_SPF_M_TIME(4)
         TR    DXD_WORK_1(6),HEXCHAR
         MVC   VDATA_MOD_TIME+0(2),DXD_WORK_1+0
         MVI   VDATA_MOD_TIME+2,C':'
         MVC   VDATA_MOD_TIME+3(2),DXD_WORK_1+2
         MVI   VDATA_MOD_TIME+5,C':'
         MVC   VDATA_MOD_TIME+6(2),DXD_WORK_1+4
         MVC   VDATA_USER,DIR_SPF_USER
         B     MAIN0140
MAIN0120 DS    0H
         MVI   VDATA_ATTR_2,$SCREEN_ATTR_HIGH_TURQUOISE
         MVC   VDATA_STATISTICS(DELETED_MSG_L),DELETED_MSG
         B     MAIN0140
MAIN0130 DS    0H
         MVI   VDATA_ATTR_2,$SCREEN_ATTR_HIGH_TURQUOISE
         MVC   VDATA_STATISTICS(RENAMED_MSG_L),RENAMED_MSG
MAIN0140 DS    0H
         L     R1,DSPCREQ_RECORD_NBR          CURRENT RECORD NUMBER
         A     R1,F1                          ADD 1
         ST    R1,DSPCREQ_RECORD_NBR          SAVE NEW RECORD NUMBER
         LH    R1,DXD_LINES                   LINES DISPLAYED
         A     R1,F1                          ADD 1
         STH   R1,DXD_LINES                   SAVE LINES DISPLAYED
MAIN0160 DS    0H
         ITRACE ID=LOOP,                                               +
               RDATA1=R6,                                              +
               RDATA2=R7
         A     R7,DXD_VWIDTH                  NEXT LINE
         BCT   R6,MAIN0040                    MINUS 1 LINE
         B     MAIN0180
MAIN0170 DS    0H
         L     R1,DSPCREQ_RECORD_NBR          CURRENT RECORD NUMBER
         A     R1,F1                          ADD 1
         ST    R1,DSPCREQ_RECORD_NBR          SAVE NEW RECORD NUMBER
         B     MAIN0040
* ------------------------------------------------------------------- *
*        Build the DSCB info and headings                             *
* ------------------------------------------------------------------- *
MAIN0180 DS    0H
         ITRACE ID=OSDSCB
         MVC   DATASPACE_1_DISPLAY_LAST_RECORD,DSPCREQ_RECORD_NBR
         MVC   SPF_COLUMN_FROM,H1             INITIALIZE COLUMN
         L     R15,COMM_V_OSDSCB              OSDSCB ENTRY POINT
         BALR  R14,R15                        FORMAT THE DSCB INFO
* ------------------------------------------------------------------- *
*        Display the panel.                                           *
* ------------------------------------------------------------------- *
MAIN0185 DS    0H
         ITRACE ID=DISPLAY
         MVC   SESS_DISP_PANEL,DIR_PANEL      SET PANEL NAME
         MVC   SESS_DISP_VDATA_ADDR,DXD_VDATA_ADDR
         MVC   SESS_DISP_VDATA_LENGTH,DXD_VDATA_LENGTH
         MVC   SESS_DISP_CURSOR,DXD_CURSOR_POSITION
         L     R15,COMM_V_OSDISP
         BALR  R14,R15                        LINK TO OSDISP
         ITRACE ID=DISP_RC,                                            +
               DATA1=(SESS_RC,2),                                      +
               DATA2=(SESS_STATUS,1)
         CLC   COMM_NEW_FORMAT,COMM_BLANKS    NEW FORMAT WANTED?
         BNE   EXIT0000                       YES
         CLI   COMM_SESS_FUNC,0               SESSION FUNCTION?
         BNE   MAIN0190                       YES
         OC    SESS_RC,SESS_RC                DISPLAY RC=0?
         BZ    MAIN0200                       YES
         ITRACE ID=ENDING
         MVI   COMM_SESS_FUNC,$SESS_REMOVE
         B     EXIT0000
* ------------------------------------------------------------------- *
*        Save VDATA address and lines remaining                       *
* ------------------------------------------------------------------- *
MAIN0190 DS    0H
         ITRACE ID=SAVE_6_7,                                           +
               RDATA1=R6,                                              +
               RDATA2=R7
         ST    R6,DXD_LINES_REMAINING         SAVE LIENS REMAINING
         ST    R7,DXD_CURRENT_VDATA           SAVE ADDR IN VDATA
         B     EXIT0000
* ------------------------------------------------------------------- *
*        Process any line command(s)                                  *
* ------------------------------------------------------------------- *
MAIN0200 DS    0H
         NI    DXD_FLAGS,255-$LINE_CMD        RESET INDICATOR
         MVC   DSPCREQ_RECORD_NBR,DATASPACE_1_DISPLAY_FIRST_RECORD
         SR    R6,R6
         ICM   R6,3,DXD_LINES                 LINES OF DISPLAY BUILT
         BZ    MAIN0250                       NONE
         L     R7,DXD_VDATA_ADDR              SPF DISPLAY BUFFER ADDR
         ITRACE ID=CMDS,                                               +
               RDATA1=R7,                                              +
               RDATA2=R6
         A     R7,DXD_VWIDTH                  SKIP HEADING LINE
         L     R1,DXD_VWIDTH                  SCREEN WIDTH
         AH    R1,=Y(VDATA_SELECTION-VDATA+1) PLUS DISP TO SELECTION
         ST    R1,DXD_CURSOR_POSITION         INIT CURSOR POSITION
MAIN0210 DS    0H
         ITRACE ID=LINE_CMD,                                           +
               DATA1=(VDATA_SELECTION,1),                              +
               DATA2=VDATA_MEMBER
         OI    VDATA_SELECTION,C' '           'TRANSLATE' TO UPPER CASE
         CLI   VDATA_SELECTION,C' '           BLANK?
         BE    MAIN0230                       YES
         CLC   DELETED_MSG(DELETED_MSG_L),VDATA_STATISTICS
         BE    ERR0130
         CLC   RENAMED_MSG(RENAMED_MSG_L),VDATA_STATISTICS
         BE    ERR0130
         MVI   SESS_COMMAND_FLAGS,$SESS_COMMAND_SEARCH
         LA    R1,VDATA_SELECTION             COMMAND ADDRESS
         ST    R1,SESS_COMMAND_ADDRESS        SET ADDRESS
         MVC   SESS_COMMAND_LENGTH,H1         SET LENGTH
         LA    R1,LINE_TABLE                  LIST OF COMMANDS
         L     R15,COMM_V_OSCMD               OSCMD ENTRY POINT
         BALR  R14,R15                        SEARCH COMMANDS
         TM    SESS_COMMAND_FLAGS,$SESS_COMMAND_FOUND
         BOR   R15
         MVC   COMM_INFO_01,VDATA_SELECTION
         MVC   COMM_MSG_ID,H1                 SET MESSAGE ID
         BAL   R2,MSG0000
         MVC   SPF_MSG_1,COMM_MSG_1           COPY MESSAGE
         B     MAIN0180                       DISPLAY WITH MESSAGE
MAIN0220 DS    0H
         OI    DXD_FLAGS,$LINE_CMD            LINE CMD WAS FOUND
         MVI   VDATA_SELECTION,C' '           RESET CMD
MAIN0230 DS    0H
         L     R1,DSPCREQ_RECORD_NBR          RECORD NUMBER
         A     R1,F1                          PLUS 1
         ST    R1,DSPCREQ_RECORD_NBR          SAVE RECORD NUMBER
MAIN0240 DS    0H
         L     R1,DXD_CURSOR_POSITION         CURSOR POSITION
         A     R1,DXD_VWIDTH                  NEXT LINE
         ST    R1,DXD_CURSOR_POSITION         UPDATE CURSOR POSITION
         A     R7,DXD_VWIDTH                  NEXT LINE
         BCT   R6,MAIN0210                    LOOP
         XC    DXD_CURSOR_POSITION,DXD_CURSOR_POSITION
         TM    DXD_FLAGS,$LINE_CMD            LINE COMMAND FOUND?
         BO    MAIN0180                       YES
* ------------------------------------------------------------------- *
*                                                                     *
*       No line command(s) were found.                                *
*                                                                     *
*       Process any primary command.                                  *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0250 DS    0H
         CLC   SPF_ZCMD,COMM_BLANKS           ANY PRIMARY COMMAND?
         BE    MAIN0330                       NO
         MVC   SESS_COMMAND_ADDRESS,OPERAND_01_ADDRESS
         MVC   SESS_COMMAND_LENGTH,OPERAND_01_LENGTH
         MVI   SESS_COMMAND_FLAGS,$SESS_COMMAND_SEARCH
         LA    R1,COMMAND_TABLE
         L     R15,COMM_V_OSCMD
         BALR  R14,R15                        SEARCH COMMAND
         TM    SESS_COMMAND_FLAGS,$SESS_COMMAND_FOUND
         BNO   ERR0080                        NOT FOUND
         LH    R3,COMM_OPERANDS_NBR           NBR OF OPERANDS
         BR    R15                            PROCESS COMMAND
* ------------------------------------------------------------------- *
*                                                                     *
*        Check for a scroll command.                                  *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0330 DS    0H
         ICM   R1,15,SPF_ZSCROLLN             NUMBER OF LINES TO SCROLL
         BZ    MAIN0340                       ZERO..
         BCTR  R1,0                           MINUS 1 (HEADING LINE)
MAIN0340 DS    0H
         ST    R1,SESS_VERTICAL_SCROLL
         MVC   SESS_HORIZONTAL_SCROLL,SPF_ZSCROLLN
         ITRACE ID=ZVERB,                                              +
               DATA1=SPF_ZVERB,                                        +
               DATA2=(SPF_ZSCROLLN,4)
         CLI   SPF_ZVERB,C'B'                 BOTTOM?
         BE    MAIN0400                       YES
         CLI   SPF_ZVERB,C'D'                 SCROLL DOWN?
         BE    MAIN0370                       YES
         CLI   SPF_ZVERB,C'L'                 SCROLL LEFT?
         BE    MAIN0180                       YES
         CLI   SPF_ZVERB,C'R'                 SCROLL RIGHT?
         BE    MAIN0180                       YES
         CLI   SPF_ZVERB,C'T'                 TOP?
         BE    MAIN0390                       YES
         CLI   SPF_ZVERB,C'U'                 SCROLL UP?
         BE    MAIN0350                       YES
         B     MAIN0180                       DISPLAY SAME DATA AGAIN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0350 DS    0H
         ITRACE ID=UP
         MVC   SPF_ZCMD,COMM_BLANKS                 CLEAR ZCMD
         CLI   SPF_ZSCROLLA,C'M'                    MAX?
         BE    MAIN0390                             YES
         L     R1,SESS_VERTICAL_SCROLL              NUMBER TO SCROLL
         L     R2,DATASPACE_1_DISPLAY_FIRST_RECORD  1ST ON DISPLAY
         SR    R2,R1                                MINUS SCROLL
         C     R2,DATASPACE_1_FIRST_RECORD          LESS THAN FIRST?
         BNL   MAIN0360                             NO
         L     R2,DATASPACE_1_FIRST_RECORD          FORCE TO FIRST
MAIN0360 DS    0H
         ST    R2,DATASPACE_1_DISPLAY_FIRST_RECORD  FIRST TO DISPLAY
         B     MAIN0000                             REBUILD EVERYTHING
* ------------------------------------------------------------------- *
*        SCROLL DOWN                                                  *
* ------------------------------------------------------------------- *
MAIN0370 DS    0H
         ITRACE ID=DOWN
         MVC   SPF_ZCMD,COMM_BLANKS                 CLEAR ZCMD
         CLI   SPF_ZSCROLLA,C'M'                    MAX?
         BE    MAIN0400                             YES
         L     R1,SESS_VERTICAL_SCROLL              NUMBER TO SCROLL
         L     R2,DATASPACE_1_DISPLAY_FIRST_RECORD  RECORD DISPLAYED
         AR    R2,R1                                PLUS SCROLL
         C     R2,DATASPACE_1_LAST_RECORD
         BH    MAIN0380                             WOULD GO BEYOND END
         ST    R2,DATASPACE_1_DISPLAY_FIRST_RECORD  FIRST RECORD NBR
         B     MAIN0000                             REBUILD EVERYTHING
MAIN0380 DS    0H
         L     R2,DATASPACE_1_LAST_RECORD           HIGHEST STORED
         S     R2,DATASPACE_1_DISPLAY_FIRST_RECORD  FIRST DISPLAYED
         C     R2,DXD_VDEPTH                        MORE THAN ONE SCRN?
         BNH   MAIN0180                             NO.. STAY PUT
         L     R2,DATASPACE_1_LAST_RECORD           HIGHEST STORED
         S     R2,DXD_VDEPTH                        MINUS 1 SCREEN
         A     R2,F2                                PLUS 2
         ST    R2,DATASPACE_1_DISPLAY_FIRST_RECORD  FIRST TO DISPLAY
         B     MAIN0000                             REBUILD EVERYTHING
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0390 DS    0H
         ITRACE ID=TOP
         MVC   SPF_ZCMD,COMM_BLANKS           CLEAR ZCMD
        MVC   DATASPACE_1_DISPLAY_FIRST_RECORD,DATASPACE_1_FIRST_RECORD
         XC    SESS_LAST_FOUND,SESS_LAST_FOUND
         B     MAIN0000                       REBUILD EVERYTHING
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0400 DS    0H
         ITRACE ID=BOTTOM
         MVC   SPF_ZCMD,COMM_BLANKS           CLEAR ZCMD
         L     R2,DATASPACE_1_LAST_RECORD     HIGHEST RECORD AVAILABLE
         S     R2,DXD_VDEPTH                  MINUS LINES ON DISPLAY
         A     R2,F2                          PLUS 2
         CH    R2,H1                          STILL HAVE AT LEAST 1?
         BNL   MAIN0410                       YES
         LH    R2,H1                          FORCE RECORD 1
MAIN0410 DS    0H
         ST    R2,DATASPACE_1_DISPLAY_FIRST_RECORD   FIRST RECORD NBR
         B     MAIN0000                       REBUILD EVERYTHING
* ------------------------------------------------------------------- *
*        APPEND (add to end of an existing file)                      *
* ------------------------------------------------------------------- *
MAIN0420 DS    0H
         ITRACE ID=APPEND1
         MVI   VDATA_SELECTION,C' '
         MVC   LOADREQ_MODULE,MODULE_OSAPPEND
         LA    R1,LOADREQ
         L     R15,COMM_V_OSLOAD
         BALR  R14,R15                        LOAD OSAPPEND
         MVC   SPF_MEMBER,VDATA_MEMBER        COPY MEMBER NAME
         MVC   DXD_MEMBER,VDATA_MEMBER        COPY MEMBER NAME
         MVI   APPNDREQ_FUNC,$APPNDREQ_OPEN
         LA    R1,APPNDREQ
         ICM   R15,15,LOADREQ_EP              COPY ENTRY POINT
         BALR  R14,R15                        LINK TO OSAPPEND
         ITRACE ID=APPND_RC,                                           +
               RDATA1=R15
         MVC   SPF_MEMBER,SESS_MEMBER
         CH    R15,H4                         PROBLEM?
         BE    MAIN0440                       YES
         CH    R15,H8                         EXIT?
         BE    MAIN0440                       YES
         MVC   APPNDREQ_MEMBER,DXD_MEMBER
         MVI   APPNDREQ_FUNC,$APPNDREQ_WRITE
         ICM   R15,15,LOADREQ_EP              OSAPPEND ENTRY POINT
         BALR  R14,R15                        LINK TO OSAPPEND
         MVI   APPNDREQ_FUNC,$APPNDREQ_CLOSE
         ICM   R15,15,LOADREQ_EP              OSAPPEND ENTRY POINT
         BALR  R14,R15                        LINK TO OSAPPEND
         MVC   VDATA_STATISTICS,COMM_BLANKS
         MVC   VDATA_STATISTICS(APPND_MSG_L),APPND_MSG
         B     MAIN0220                       PROCESS NEXT SELECTION
MAIN0440 DS    0H
         MVI   VDATA_ATTR_3,$SCREEN_ATTR_HIGH
         MVC   VDATA_STATISTICS(L'COMM_MSG_1),COMM_MSG_1
         B     MAIN0220
* ------------------------------------------------------------------- *
*        LOCATE (make a member the 1st shown on display)              *
* ------------------------------------------------------------------- *
MAIN0450 DS    0H
         ITRACE ID=LOCATE,                                             +
               RDATA1=R3
         CH    R3,H2                          TWO PARAMETERS?
         BL    MAIN0470                       LESS
         BH    ERR0060                        TOO MANY
         CLC   OPERAND_02_LENGTH,H8           MEMBER NAME TOO LONG?
         BH    ERR0070                        YES
         CLC   SESS_SEARCH_ARG_LENGTH,OPERAND_02_LENGTH
         BNE   MAIN0460                       DIFFERENT LENGTH
         CLC   SESS_SEARCH_ARG,0(R2)          SAME STRING?
         BE    MAIN0470                       YES
MAIN0460 DS    0H
         MVC   DSPCREQ_RECORD_NBR,DATASPACE_1_FIRST_RECORD
         ICM   R2,15,OPERAND_02_ADDRESS
         MVC   DSPCREQ_MEMBER,0(R2)           COPY MEMBER NAME
         MVC   SESS_SEARCH_ARG_LENGTH,OPERAND_02_LENGTH
         MVC   SESS_SEARCH_ARG,COMM_BLANKS
         MVC   SESS_SEARCH_ARG(8),0(R2)
         B     MAIN0480
MAIN0470 DS    0H
         MVC   SPF_ZCMD,COMM_BLANKS           CLEAR THE COMMAND
         OC    SESS_SEARCH_ARG_LENGTH,SESS_SEARCH_ARG_LENGTH
         BZ    ERR0170
         MVC   DSPCREQ_MEMBER,SESS_SEARCH_ARG
         ICM   R1,15,SESS_SEARCH_RECORD
         LA    R1,1(,R1)
         STCM  R1,15,DSPCREQ_RECORD_NBR
MAIN0480 DS    0H
         MVI   DSPCREQ_FUNC,$DSPCREQ_FIND_MEMBER_G
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE
         BALR  R14,R15
         CLI   DSPCREQ_RC,$DSPCREQ_OK
         BNE   MAIN0490                       NOT FOUND
         MVC   DATASPACE_1_DISPLAY_FIRST_RECORD,DSPCREQ_RECORD_NBR
         MVC   SESS_SEARCH_RECORD,DSPCREQ_RECORD_NBR
         MVC   SPF_ZCMD,COMM_BLANKS           CLEAR THE COMMAND
         B     MAIN0000
MAIN0490 DS    0H
         OC    SESS_LAST_FOUND,SESS_LAST_FOUND
         BZ    ERR0020                        NEVER FOUND
         B     ERR0180                        BOTTOM OF DATA REACHED
* ------------------------------------------------------------------- *
*        OFFLOAD member(s) to a sequential file                       *
* ------------------------------------------------------------------- *
MAIN0510 DS    0H
         ITRACE ID=OFFLOAD
         MVI   VDATA_SELECTION,C' '           RESET THE SELECTION
         MVC   SPF_MEMBER,VDATA_MEMBER        COPY MEMBER NAME
         MVC   DXD_MEMBER,VDATA_MEMBER        COPY MEMBER NAME
         MVC   LOADREQ_MODULE,MODULE_OSOFFLD
         LA    R1,LOADREQ
         L     R15,COMM_V_OSLOAD
         BALR  R14,R15
         ITRACE ID=OSOFFLD,                                            +
               RDATA1=R15
         MVI   OFFLDREQ_FUNC,$OFFLDREQ_OPEN
         LA    R1,OFFLDREQ                    PARMS FOR OSOFFLD
         ICM   R15,15,LOADREQ_EP              COPY ENTRY POINT
         BALR  R14,R15                        LINK TO OSOFFLD
         LTR   R15,R15                        SUCCESSFUL?
         BNZ   MAIN0530                       NO
         MVC   SPF_MEMBER,SESS_MEMBER
         CH    R15,H4                         PROBLEM?
         BE    MAIN0520                       YES
         CH    R15,H8                         EXIT?
         BE    MAIN0520                       YES
         MVC   OFFLDREQ_MEMBER,DXD_MEMBER
         MVI   OFFLDREQ_FUNC,$OFFLDREQ_WRITE
         ICM   R15,15,LOADREQ_EP              OSOFFLD ENTRY POINT
         BALR  R14,R15                        LINK TO OSOFFLD
         LTR   R15,R15                        SUCCESSFUL?
         BNZ   MAIN0530                       NO
         MVI   OFFLDREQ_FUNC,$OFFLDREQ_CLOSE
         ICM   R15,15,LOADREQ_EP              OSOFFLD ENTRY POINT
         BALR  R14,R15                        LINK TO OSOFFLD
         LTR   R15,R15                        SUCCESSFUL?
         BNZ   MAIN0530                       NO
         MVC   VDATA_STATISTICS,COMM_BLANKS
         MVC   VDATA_STATISTICS(OFFLD_MSG_L),OFFLD_MSG
         B     MAIN0220                       PROCESS NEXT SELECTION
MAIN0520 DS    0H
         MVI   VDATA_ATTR_3,$SCREEN_ATTR_HIGH
         MVC   VDATA_STATISTICS(L'COMM_MSG_1),COMM_MSG_1
         B     MAIN0220
MAIN0530 DS    0H
         ITRACE ID=MAIN0530,                                           +
               RDATA1=R15
         MVC   SPF_MSG_1,COMM_MSG_1
         MVC   SPF_MSG_2,COMM_MSG_2
         MVC   SPF_MSG_3,COMM_MSG_3
         MVC   SPF_MSG_4,COMM_MSG_4
         MVC   SPF_MSG_5,COMM_MSG_5
         B     MAIN0180
* ------------------------------------------------------------------- *
*        Reset TAG indicator (LINE COMMAND)                           *
* ------------------------------------------------------------------- *
MAIN0540 DS    0H
         ITRACE ID=LINE_RST
         MVI   VDATA_SELECTION,C' '           RESET THE SELECTION
         MVI   DSPCREQ_FUNC,$DSPCREQ_RETREIVE
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE
         BALR  R14,R15
         NI    DIR_FLAGS,255-$DIR_TAGGED
         MVI   DSPCREQ_FUNC,$DSPCREQ_UPDATE
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE
         BALR  R14,R15
         MVI   VDATA_ATTR_2,$SCREEN_ATTR_NORMAL
         MVC   VDATA_STATISTICS(RESET_MSG_L),RESET_MSG
         B     MAIN0220                       PROCESS NEXT SELECTION
* ------------------------------------------------------------------- *
*        Reset tag indicator (primary command)                        *
* ------------------------------------------------------------------- *
MAIN0550 DS    0H
         ITRACE ID=PRIM_RST
         CH    R3,H2                             PROPER PARAMETERS?
         BL    ERR0050                           TOO FEW
         BH    ERR0060                           TOO MANY
         CLC   OPERAND_02_LENGTH,H8              MEMBER NAME TOO LONG?
         BH    ERR0070                           YES
         ICM   R2,15,OPERAND_02_ADDRESS
         CLC   ALL,0(R2)                         RESET ALL MEMBERS?
         BE    MAIN0560                          YES
         MVC   DXD_MEMBER,0(R2)                  COPY MEMBER NAME
         MVC   DSPCREQ_RECORD_NBR,F1             START WITH 1ST MEMBER
         MVI   DSPCREQ_FUNC,$DSPCREQ_FIND_MEMBER_S
         MVC   DSPCREQ_MEMBER,DXD_MEMBER
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE
         BALR  R14,R15
         CLI   DSPCREQ_RC,$DSPCREQ_OK
         BNE   ERR0020                           NOT FOUND
         TM    DIR_FLAGS,$DIR_DELETED+$DIR_RENAMED
         BNZ   ERR0130
         NI    DIR_FLAGS,255-$DIR_TAGGED
         MVI   DSPCREQ_FUNC,$DSPCREQ_UPDATE
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE
         BALR  R14,R15
         MVC   SPF_ZCMD,COMM_BLANKS              CLEAR COMMAND
         B     MAIN0000                          REBUILD DISPLAY
MAIN0560 DS    0H
         ITRACE ID=RESETALL
         MVC   DSPCREQ_RECORD_NBR,DATASPACE_1_FIRST_RECORD
         SR    R2,R2                             CLEAR FOR COUNTER
MAIN0570 DS    0H
         MVI   DSPCREQ_FUNC,$DSPCREQ_RETREIVE
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE
         BALR  R14,R15
         TM    DIR_FLAGS,$DIR_TAGGED             MEMBER TAGGED?
         BNO   MAIN0580                          NO
         A     R2,F1                             PLUS 1
         NI    DIR_FLAGS,255-$DIR_TAGGED
         MVI   DSPCREQ_FUNC,$DSPCREQ_UPDATE
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE
         BALR  R14,R15
MAIN0580 DS    0H
         L     R1,DSPCREQ_RECORD_NBR             CURRENT RECORD NUMBER
         C     R1,DATASPACE_1_LAST_RECORD        AT LAST RECORD
         BE    MAIN0590                          YES
         A     R1,F1                             PLUS 1
         ST    R1,DSPCREQ_RECORD_NBR             SET UPDATED NUMBER
         B     MAIN0570
MAIN0590 DS    0H
         MVC   SPF_ZCMD,COMM_BLANKS              CLEAR COMMAND
         ST    R2,COMM_DWORD
         MVI   COMM_MSG_ID+1,14                  SET MESSAGE NUMBER
         BAL   R2,MSG0000                        BUILD MESSAGE
         MVC   SPF_MSG_1,COMM_MSG_1
         B     MAIN0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0600 DS    0H
         ITRACE ID=LINE_SEL,                                           +
               DATA1=VDATA_MEMBER
         NI    DXD_FLAGS,255-$EMPTY_MEMBER       RESET INDICATOR
         MVC   DXD_MEMBER,VDATA_MEMBER           COPY MEMBER NAME
         MVC   DXD_SELECTION,VDATA_SELECTION     COPY LINE CMD
         B     MAIN0640
MAIN0610 DS    0H
         ITRACE ID=PRIM_SEL,                                           +
               DATA1=(0(R2),1)
         SR    R7,R7                             NOT FROM LINE CMD
         MVC   DXD_SELECTION,0(R2)               SAVE 1ST CHAR OF CMD
         CH    R3,H2                             PROPER PARAMETERS?
         BL    ERR0050                           TOO FEW
         BH    ERR0060                           TOO MANY
         CLC   OPERAND_02_LENGTH,H8              MEMBER NAME TOO LONG?
         BH    ERR0070                           YES
         MVC   DXD_MEMBER,COMM_BLANKS            INITIALIZE MEMBER NAME
         SR    R1,R1                             CLEAR R1
         ICM   R1,3,OPERAND_02_LENGTH            NAME LENGTH
         BCTR  R1,0                              FOR EX INSTRUCTION
         ICM   R2,15,OPERAND_02_ADDRESS          MEMBER NAME ADDRESS
         EX    R1,MVC_02                         COPY MEMBER NAME
         MVC   DSPCREQ_RECORD_NBR,DATASPACE_1_FIRST_RECORD
         NI    DXD_FLAGS,255-$EMPTY_MEMBER       RESET INDICATOR
         MVI   DSPCREQ_FUNC,$DSPCREQ_FIND_MEMBER_S
         MVC   DSPCREQ_RECORD_NBR,F1             START WITH 1ST MEMBER
         MVC   DSPCREQ_MEMBER,DXD_MEMBER
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE
         BALR  R14,R15
         CLI   DSPCREQ_RC,$DSPCREQ_OK            MEMBER LOCATED?
         BE    MAIN0620                          YES
         TM    SESS_EDIT_FLAGS,$SESS_EDIT        EDIT SESSION?
         BNO   ERR0020                           NO
         OI    DXD_FLAGS,$EMPTY_MEMBER           NEW MEMBER (EMPTY)
         B     MAIN0630
MAIN0620 DS    0H
         TM    DIR_FLAGS,$DIR_DELETED+$DIR_RENAMED
         BNZ   ERR0130
MAIN0630 DS    0H
         MVC   SPF_ZCMD,COMM_BLANKS              RESET THE COMMAND
MAIN0640 DS    0H
         ITRACE ID=BLD_NEW
         GETMAIN RU,                                                   +
               LV=SESSION_L,                                           +
               LOC=ANY
         LR    R3,R1                            SAVE ADDRESS
NEW      USING SESSION,R3
         ST    R3,COMM_NEW_SESSION              SET NEW BLOCK ADDR
         LR    R0,R3
         LA    R1,SESSION_L
         LR    R14,R11                          CURRENT SESSION ADDR
         LR    R15,R1                           COPY LENGTH
         MVCL  R0,R14                           COPY SESSION BLOCK
         L     R1,SESS_NEXT                     NEXT ON CHAIN
NEXT     USING SESSION,R1
         ST    R1,NEW.SESS_NEXT                 SET NEXT IN NEW BLOCK
         ST    R11,NEW.SESS_PREV                SET PREV IN NEW BLOCK
         ST    R3,SESS_NEXT                     SET NEXT IN CURRENT
         LTR   R1,R1                            'NEXT' BLOCK?
         BZ    MAIN0645                         NO
         ST    R3,NEXT.SESS_PREV                SET PREV IN NEXT BLOCK
         DROP  NEXT
MAIN0645 DS    0H
         MVI   NEW.SESS_FORMAT_FLAGS,0
         MVI   NEW.DATASPACE_1_ALET,X'FF'
         MVC   DATASPACE_1_FIRST_RECORD,F1
         MVC   DATASPACE_1_DISPLAY_FIRST_RECORD,F1
         MVC   DATASPACE_1_DISPLAY_FIRST_COLUMN,F1
         XC    SESS_IO_AREA,SESS_IO_AREA
         XC    NEW.SESS_FORMATTER_DXD_ADDR,NEW.SESS_FORMATTER_DXD_ADDR
         XC    NEW.SESS_FORMATTER_DXD_LENGTH,NEW.SESS_FORMATTER_DXD_LEN+
               GTH
         XC    NEW.SESS_LAST_FOUND,NEW.SESS_LAST_FOUND
         XC    NEW.SESS_SEARCH_ARG_LENGTH,NEW.SESS_SEARCH_ARG_LENGTH
         MVC   NEW.SESS_SEARCH_ARG,COMM_BLANKS
         MVC   NEW.SESS_MEMBER,DXD_MEMBER
         MVC   NEW.SESS_PATH,SESS_PATH
         XC    NEW.SESS_DISP_VDATA_ADDR,NEW.SESS_DISP_VDATA_ADDR
         XC    NEW.SESS_DISP_VDATA_LENGTH,NEW.SESS_DISP_VDATA_ADDR
         XC    NEW.SESS_DCB_ADDR,NEW.SESS_DCB_ADDR
         XC    NEW.SESS_DCB_LENGTH,NEW.SESS_DCB_LENGTH
         MVI   NEW.SESS_KEYWORD_FLAGS,$KEYWORD_DSN+$KEYWORD_MEMBER+$KEY+
               WORD_VOL
         TM    DXD_FLAGS,$EMPTY_MEMBER           EMPTY MEMBER?
         BNO   MAIN0650                          NO
         OI    NEW.SESS_EDIT_FLAGS,$SESS_EDIT_EMPTY
MAIN0650 DS    0H
         L     R0,COMM_IO_SIZE                  I/O AREA SIZE
         GETMAIN RU,                                                   +
               LV=(0),                                                 +
               LOC=BELOW
         ST    R1,SESS_IO_AREA                  SAVE ADDRESS
         ITRACE ID=IO_AREA,                                            +
               RDATA1=R1
         ITRACE ID=CRE_DSPC                     CREATING A DATA SPACE
         LA    R0,NEW.DATASPACE_1
         ST    R0,DSPCREQ_DATASPACE
         MVI   DSPCREQ_FUNC,$DSPCREQ_CREATE
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE
         BALR  R14,R15                          CREATE DATASPACE
         ITRACE ID=DSPC_RC,                                            +
               DATA1=(DSPCREQ_RC,1)
         CLI   DSPCREQ_RC,$DSPCREQ_OK
         BNE   ERR0080
         ITRACE ID=XREF,                                               +
               DATA1=NEW.SESS_DD,                                      +
               DATA2=NEW.DATASPACE_1_NAME
         LA    R0,DATASPACE_1                    OUR DATA SPACE INFO
         ST    R0,DSPCREQ_DATASPACE              SET ADDRESS
         ST    R6,DXD_LINES_REMAINING            SAVE LINES REMAINING
         ST    R7,DXD_CURRENT_VDATA              SAVE VDATA ADDR
         MVI   COMM_SESS_FUNC,$SESS_SWITCH       SET SWITCH INDICATOR
         CLI   DXD_SELECTION,C'B'                BROWSE?
         BE    MAIN0660                          YES
         CLI   DXD_SELECTION,C'E'                EDIT?
         BE    MAIN0670                          YES
         LTR   R7,R7                             LINE COMMAND?
         BZ    EXIT0000                          NO
         MVI   VDATA_SELECTION,C' '              RESET THE SELECTION
         B     EXIT0000                          EXIT
MAIN0660 DS    0H
         ITRACE ID=BROWSE,DATA1=(COMM_SESS_FUNC,1)
         MVI   NEW.SESS_EDIT_FLAGS,0             TURN OFF EDIT MODE
         LTR   R7,R7                             LINE COMMAND?
         BZ    EXIT0000                          NO
         MVI   VDATA_SELECTION,C' '              RESET THE SELECTION
         B     EXIT0000
MAIN0670 DS    0H
         ITRACE ID=EDIT,DATA1=(COMM_SESS_FUNC,1)
         MVI   NEW.SESS_EDIT_FLAGS,$SESS_EDIT    TURN ON EDIT MODE
         LTR   R7,R7                             LINE COMMAND?
         BZ    EXIT0000                          NO
         MVI   VDATA_SELECTION,C' '              RESET THE SELECTION
         B     EXIT0000
         DROP  NEW
* ------------------------------------------------------------------- *
*        Tag member (line command)                                    *
* ------------------------------------------------------------------- *
MAIN0680 DS    0H
         ITRACE ID=LINETAG
         MVI   VDATA_SELECTION,C' '                RESET THE SELECTION
         MVI   DSPCREQ_FUNC,$DSPCREQ_RETREIVE
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE
         BALR  R14,R15
         OI    DIR_FLAGS,$DIR_TAGGED             SET THE TAGGED FLAG
         MVI   DSPCREQ_FUNC,$DSPCREQ_UPDATE
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE
         BALR  R14,R15
         MVI   VDATA_ATTR_2,$SCREEN_ATTR_HIGH
         MVC   VDATA_STATISTICS(TAGGED_MSG_L),TAGGED_MSG
         B     MAIN0220                          PROCESS NEXT SELECTION
* ------------------------------------------------------------------- *
*        Tag member (primary command)                                 *
* ------------------------------------------------------------------- *
MAIN0690 DS    0H
         ITRACE ID=PRIMTAG
         CH    R3,H2                             PROPER PARAMETERS?
         BL    ERR0050                           TOO FEW
         BH    ERR0060                           TOO MANY
         CLC   OPERAND_02_LENGTH,H8              MEMBER NAME TOO LONG?
         BH    ERR0070                           YES
         ICM   R2,15,OPERAND_02_ADDRESS
         MVI   DSPCREQ_FUNC,$DSPCREQ_FIND_MEMBER_S
         MVC   DSPCREQ_MEMBER,0(R2)              MEMBER NAME
         MVC   DSPCREQ_RECORD_NBR,F1             START WITH 1ST MEMBER
         LA    R1,DSPCREQ                        DATASPACE PARMS
         L     R15,COMM_OSDSPACE
         BALR  R14,R15                           FIND MEMBER
         CLI   DSPCREQ_RC,$DSPCREQ_OK
         BNE   ERR0020                           NOT FOUND
         OI    DIR_FLAGS,$DIR_TAGGED             SET TAGGED FLAG
         MVI   DSPCREQ_FUNC,$DSPCREQ_UPDATE      SET FUNCTION (UPDATE)
         LA    R1,DSPCREQ                        DATASPACE PARMS
         L     R15,COMM_OSDSPACE
         BALR  R14,R15                           UPDATE THE MEMBER
         MVC   SPF_ZCMD,COMM_BLANKS              CLEAR THE COMMAND
         B     MAIN0000                          REBUILD DISPLAY
* ------------------------------------------------------------------- *
*        APPEND members to a sequential file                          *
* ------------------------------------------------------------------- *
MAIN0700 DS    0H
         ITRACE ID=APPEND2
         CH    R3,H2                             TWO PARAMETERS?
         BH    ERR0060                           TOO MANY
         BL    MAIN0710                          ONLY 1
         CLC   OPERAND_02_LENGTH,H8              MEMBER NAME TOO LONG?
         BH    ERR0070                           YES
         ICM   R2,15,OPERAND_02_ADDRESS
         MVC   DXD_MEMBER,0(R2)                  COPY MEMBER NAME
         MVC   APPNDREQ_MEMBER,0(R2)             COPY MEMBER NAME
         MVI   DSPCREQ_FUNC,$DSPCREQ_FIND_MEMBER_S
         MVC   DSPCREQ_RECORD_NBR,F1             START WITH 1ST MEMBER
         MVC   DSPCREQ_MEMBER,APPNDREQ_MEMBER    MEMBER NAME
         LA    R1,DSPCREQ                        DATASPACE PARMS
         L     R15,COMM_OSDSPACE
         BALR  R14,R15                           FIND MEMBER
         CLI   DSPCREQ_RC,$DSPCREQ_OK            LOCATED?
         BNE   ERR0020                           NOT FOUND
         TM    DIR_FLAGS,$DIR_DELETED+$DIR_RENAMED
         BNZ   ERR0130
MAIN0710 DS    0H
         MVC   SPF_ZCMD,COMM_BLANKS              RESET COMMAND
         MVC   LOADREQ_MODULE,MODULE_OSAPPEND    MODULE TO LOAD
         LA    R1,LOADREQ                        OSLOAD PARMS
         L     R15,COMM_V_OSLOAD
         BALR  R14,R15                           LOAD OSAPPEND
         ICM   R15,15,LOADREQ_EP                 COPY ENTRY POINT
         MVI   APPNDREQ_FUNC,$APPNDREQ_OPEN      OSAPPEND FUNCTION
         LA    R1,APPNDREQ                       OSAPPEND PARMS
         BALR  R14,R15                           LINK TO OSAPPEND
         ITRACE ID=APPND_RC,                                           +
               RDATA1=R15
         CH    R15,H4                            PROBLEM?
         BE    MAIN0770                          YES
         CH    R15,H8                            EXIT?
         BE    MAIN0180                          YES
         TM    OPERAND_02_TYPE,$OPERAND_PRESENT  OPERAND 2 PRESENT?
         BO    MAIN0740                          YES
         MVC   DSPCREQ_RECORD_NBR,F1             FIND 'TAGGED' MEMBERS
         LA    R1,DIR_RECORD                     I/O AREA ADDR
         ST    R1,DSPCREQ_RECORD_ADDR            SET IN DSPC PARMS
MAIN0730 DS    0H
         CLC   DSPCREQ_RECORD_NBR,DATASPACE_1_LAST_RECORD
         BH    MAIN0760
         MVI   DSPCREQ_FUNC,$DSPCREQ_RETREIVE    SET FUNCTION
         LA    R1,DSPCREQ                        DATA SPACE PARMS
         L     R15,COMM_OSDSPACE
         BALR  R14,R15                           RETREIVE RECORD
         CLI   DSPCREQ_RC,$DSPCREQ_NOT_STORED    THIS RECORD STORED?
         BE    MAIN0750                          NO
         CLI   DSPCREQ_RC,$DSPCREQ_OK            SUCCESSFUL?
         BNE   ERR0010                           NO
         TM    DIR_FLAGS,$DIR_TAGGED             TAGGED?
         BNO   MAIN0750                          NO
         MVC   SPF_MEMBER,DIR_NAME               SET MEMBER NAME
         MVC   APPNDREQ_MEMBER,DIR_NAME          SET MEMBER NAME
MAIN0740 DS    0H
         MVI   APPNDREQ_FUNC,$APPNDREQ_WRITE     SET APPEND FUNCTION
         LA    R1,APPNDREQ                       OSAPPEND PARMS
         ICM   R15,15,LOADREQ_EP                 COPY ENTRY POINT
         BALR  R14,R15                           LINK TO OSAPPEND
         TM    OPERAND_02_TYPE,$OPERAND_PRESENT  OPERAND 2 PRESENT?
         BO    MAIN0760                          YES
MAIN0750 DS    0H
         L     R1,DSPCREQ_RECORD_NBR             CURRENT RECORD NUMBER
         A     R1,F1                             PLUS 1
         ST    R1,DSPCREQ_RECORD_NBR             SAVE NEW RECORD NUMBER
         B     MAIN0730                          EXAMINE THIS MEMBER
MAIN0760 DS    0H
         MVI   APPNDREQ_FUNC,$APPNDREQ_CLOSE     SET APPEND FUNCTION
         LA    R1,APPNDREQ                       OSAPPEND PARMS
         ICM   R15,15,LOADREQ_EP                 COPY ENTRY POINT
         BALR  R14,R15                           LINK TO OSAPPEND
         MVC   COMM_INFO_01,APPNDREQ_COUNT       MEMBERS APPENDED
         MVC   COMM_INFO_02,APPNDREQ_BYTES
         MVC   COMM_MSG_ID,H3                    SET MESSAGE NUMBER
         BAL   R2,MSG0000                        BUILD MESSAGE
         MVC   SPF_MSG_1,COMM_MSG_1
         MVC   SPF_ZCMD,COMM_BLANKS              CLEAR COMMAND
         B     MAIN0180                          EXIT WITH RC=0
MAIN0770 DS    0H
         MVC   SPF_MSG_1,COMM_MSG_1
         MVC   SPF_MSG_2,COMM_MSG_2
         MVC   SPF_MSG_3,COMM_MSG_3
         MVC   SPF_MSG_4,COMM_MSG_4
         MVC   SPF_MSG_5,COMM_MSG_5
         B     MAIN0180                          EXIT WITH RC=0
* ------------------------------------------------------------------- *
*                                                                     *
*        OFFLOAD members to a sequential file                         *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0800 DS    0H
         ITRACE ID=OFFLOAD2
         CH    R3,H2                             TWO PARAMETERS?
         BH    ERR0060                           TOO MANY
         BL    MAIN0810                          ONLY 1
         CLC   OPERAND_02_LENGTH,H8              MEMBER NAME TOO LONG?
         BH    ERR0070                           YES
         ICM   R2,15,OPERAND_02_ADDRESS
         MVC   OFFLDREQ_MEMBER,0(R2)             COPY MEMBER NAME
         MVC   SPF_MEMBER,0(R2)                  COPY MEMBER NAME
         MVC   DXD_MEMBER,0(R2)                  COPY MEMBER NAME
         MVI   DSPCREQ_FUNC,$DSPCREQ_FIND_MEMBER_S
         MVC   DSPCREQ_RECORD_NBR,F1             START WITH 1ST MEMBER
         MVC   DSPCREQ_MEMBER,DXD_MEMBER         SET MEMBER NAME
         LA    R1,DSPCREQ                        DATASPACE PARMS
         L     R15,COMM_OSDSPACE
         BALR  R14,R15                           FIND MEMBER
         CLI   DSPCREQ_RC,$DSPCREQ_OK            SUCCESSFUL?
         BNE   ERR0020                           NO
         TM    DIR_FLAGS,$DIR_DELETED+$DIR_RENAMED
         BNZ   ERR0130
MAIN0810 DS    0H
         MVC   SPF_ZCMD,COMM_BLANKS              RESET COMMAND
         MVC   LOADREQ_MODULE,MODULE_OSOFFLD     MODULE TO LOAD
         LA    R1,LOADREQ                        LOAD PARMS
         L     R15,COMM_V_OSLOAD
         BALR  R14,R15                           LOAD OSOFFLD
         ICM   R15,15,LOADREQ_EP                 OSOFFLD ENTRY POINT
         ITRACE ID=OPEN
         MVI   OFFLDREQ_FUNC,$OFFLDREQ_OPEN      SET OFFLOAD FUNCTION
         LA    R1,OFFLDREQ                       OSOFFLD PARMS
         BALR  R14,R15                           LINK TO OSOFFLD
         ITRACE ID=OPEN_RC,                                            +
               RDATA1=R15
         CH    R15,H4                            OPEN SUCCESSFUL?
         BE    MAIN0870                          NO
         CH    R15,H8                            EXIT?
         BE    MAIN0180                          YES
         TM    OPERAND_02_TYPE,$OPERAND_PRESENT  OPERAND 2 PRESENT?
         BO    MAIN0840                          YES
         MVC   DSPCREQ_RECORD_NBR,F1             FIRST RECORD
         LA    R1,DIR_RECORD                     I/O AREA
         ST    R1,DSPCREQ_RECORD_ADDR            SET ADDR IN PARMS
MAIN0830 DS    0H
         CLC   DSPCREQ_RECORD_NBR,DATASPACE_1_LAST_RECORD
         BH    MAIN0860                          END OF MEMBERS
         MVI   DSPCREQ_FUNC,$DSPCREQ_RETREIVE    SET FUNCTION
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE
         BALR  R14,R15                           RETREIVE MEMBER
         CLI   DSPCREQ_RC,$DSPCREQ_NOT_STORED    STORED?
         BE    MAIN0850                          NO
         CLI   DSPCREQ_RC,$DSPCREQ_OK            SUCCESSFUL?
         BNE   ERR0010                           NO
         TM    DIR_FLAGS,$DIR_TAGGED             TAGGED?
         BNO   MAIN0850                          NO
         MVC   SPF_MEMBER,DIR_NAME               SET MEMBER NAME
         MVC   OFFLDREQ_MEMBER,DIR_NAME          SET MEMBER NAME
MAIN0840 DS    0H
         ITRACE ID=WRITE,                                              +
               DATA1=OFFLDREQ_MEMBER
         MVI   OFFLDREQ_FUNC,$OFFLDREQ_WRITE     SET OFFLOAD FUNCTION
         LA    R1,OFFLDREQ                       OSOFFLD PARMS
         ICM   R15,15,LOADREQ_EP                 OSOFFLD ENTRY POINT
         BALR  R14,R15                           LINK TO OSOFFLD
         TM    OPERAND_02_TYPE,$OPERAND_PRESENT  OPERAND 2 PRESENT?
         BO    MAIN0860                          YES
MAIN0850 DS    0H
         L     R1,DSPCREQ_RECORD_NBR             CURRENT RECORD NUMBER
         A     R1,F1                             PLUS 1
         ST    R1,DSPCREQ_RECORD_NBR             SAVE NEW RECORD NUMBER
         B     MAIN0830                          CHECK THIS MEMBER
MAIN0860 DS    0H
         ITRACE ID=CLOSE
         MVI   OFFLDREQ_FUNC,$OFFLDREQ_CLOSE
         LA    R1,OFFLDREQ                       OSOFFLD PARMS
         ICM   R15,15,LOADREQ_EP                 OSOFFLD ENTRY POINT
         BALR  R14,R15                           LINK TO OSOFFLD
         MVC   COMM_DWORD,OFFLDREQ_COUNT
         MVC   COMM_MSG_ID,H4                    SET MESSAGE NUMBER
         BAL   R2,MSG0000                        BUILD MESSAGE
         MVC   SPF_MSG_1,COMM_MSG_1              COPY MESSAGE
         MVC   SPF_ZCMD,COMM_BLANKS              CLEAR COMMAND
         B     MAIN0180                          EXIT WITH RC=0
MAIN0870 DS    0H
         ITRACE ID=OPEN_ERR
         MVC   SPF_MSG_1,COMM_MSG_1
         MVC   SPF_MSG_2,COMM_MSG_2
         MVC   SPF_MSG_3,COMM_MSG_3
         MVC   SPF_MSG_4,COMM_MSG_4
         MVC   SPF_MSG_5,COMM_MSG_5
         B     MAIN0180
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0900 DS    0H
         ITRACE ID=SORT
         CH    R3,H2                             TWO OPERANDS?
         BL    MAIN0910                          DEFAULT TO 'BY NAME'
         BH    ERR0060                           TOO MANY
         ICM   R2,15,OPERAND_02_ADDRESS          2ND OPERAND'S ADDRESS
         CLC   NAME,0(R2)                        SORT BY NAME?
         BE    MAIN0910
         CLC   TTR,0(R2)                         SORT BY TTR?
         BE    MAIN0920
         CLC   CDATE,0(R2)                       SORT BY CDATE?
         BE    MAIN0930
         CLC   MDATE,0(R2)                       SORT BY MDATE/TIME?
         BE    MAIN0940
         CLC   SIZE,0(R2)                        SORT BY SIZE?
         BE    MAIN0950
         CLC   USER,0(R2)                        SORT BY USER?
         BE    MAIN0960
         MVI   COMM_MSG_ID+1,5
         B     ERR1000                           EXIT
MAIN0910 DS    0H
         ITRACE ID=BY_NAME
         MVI   DSPCREQ_FUNC,$DSPCREQ_SORT_NAME
         B     MAIN0970
MAIN0920 DS    0H
         ITRACE ID=BY_TTR
         MVI   DSPCREQ_FUNC,$DSPCREQ_SORT_TTR
         B     MAIN0970
MAIN0930 DS    0H
         ITRACE ID=BY_CDATE
         MVI   DSPCREQ_FUNC,$DSPCREQ_SORT_CDATE
         B     MAIN0970
MAIN0940 DS    0H
         ITRACE ID=BY_MDATE
         MVI   DSPCREQ_FUNC,$DSPCREQ_SORT_MDATE
         B     MAIN0970
MAIN0950 DS    0H
         ITRACE ID=BY_SIZE
         MVI   DSPCREQ_FUNC,$DSPCREQ_SORT_SIZE
         B     MAIN0970
MAIN0960 DS    0H
         ITRACE ID=BY_USER
         MVI   DSPCREQ_FUNC,$DSPCREQ_SORT_USER
MAIN0970 DS    0H
         ITRACE ID=SORTDSPC
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE
         BALR  R14,R15                           SORT THE DATA SPACE
         MVC   SPF_ZCMD,COMM_BLANKS              CLEAR COMMAND
         B     MAIN0000
* ------------------------------------------------------------------- *
*        SUBMIT                                                       *
* ------------------------------------------------------------------- *
MAIN1000 DS    0H
         ITRACE ID=SUBMIT
         CH    R3,H2                             PROPER NUMBER?
         BL    ERR0050                           TOO FEW
         BH    ERR0060                           TOO MANY
         CLC   OPERAND_02_LENGTH,H8              OPERAND 2 TOO LONG?
         BH    ERR0070                           YES
         ICM   R2,15,OPERAND_02_ADDRESS          ADDR OF MEMBER'S NAME
         MVC   DXD_MEMBER,0(R2)                  COPY MEMBER NAME
         MVI   DSPCREQ_FUNC,$DSPCREQ_FIND_MEMBER_S
         MVC   DSPCREQ_RECORD_NBR,F1             START WIT RECORD 1
         MVC   DSPCREQ_MEMBER,DXD_MEMBER         SET MEMBER NAME
         LA    R1,DSPCREQ                        DATA SPACE PARMS
         L     R15,COMM_OSDSPACE
         BALR  R14,R15                           LOCATE MEMBER
         CLI   DSPCREQ_RC,$DSPCREQ_OK            MEMBER FOUND?
         BNE   ERR0210                           NO
         TM    DIR_FLAGS,$DIR_DELETED+$DIR_RENAMED
         BNZ   ERR0130                           DELETED OR RENAMED..
         MVC   LOADREQ_MODULE,MODULE_OSSUBMIT    MODULE TO LOAD
         LA    R1,LOADREQ                        LOAD PARMS
         L     R15,COMM_V_OSLOAD
         BALR  R14,R15                           LOAD OSSUBMIT
         MVC   SUBREQ_DSN,SESS_DSN               COPY DATASET NAME
         MVC   SUBREQ_MEMBER,0(R2)               COPY MEMBER NAME
         LA    R1,SUBREQ                         SUBMIT PARMS
         ITRACE ID=CALL_SUB,                                           +
               RDATA1=R1
         ICM   R15,15,LOADREQ_EP                 OSSUBMIT ENTRY POINT
         BALR  R14,R15                           CALL OSSUBMIT
         MVC   SPF_ZCMD,COMM_BLANKS
         MVC   SPF_MSG_1,COMM_MSG_1
         MVC   SPF_MSG_2,COMM_MSG_2
         MVC   SPF_MSG_3,COMM_MSG_3
         MVC   SPF_MSG_4,COMM_MSG_4
         MVC   SPF_MSG_5,COMM_MSG_5
         B     MAIN0180                          EXIT
MAIN1010 DS    0H
         ITRACE ID=SUBMIT
         MVI   DSPCREQ_FUNC,$DSPCREQ_FIND_MEMBER_S
         MVC   DSPCREQ_MEMBER,VDATA_MEMBER       SET MEMBER NAME
         LA    R1,DSPCREQ                        DATA SPACE PARMS
         L     R15,COMM_OSDSPACE
         BALR  R14,R15                           LOCATE MEMBER
         CLI   DSPCREQ_RC,$DSPCREQ_OK            MEMBER FOUND?
         BNE   ERR0210                           NO
         TM    DIR_FLAGS,$DIR_DELETED+$DIR_RENAMED
         BNZ   ERR0130                           DELETED OR RENAMED..
         MVC   LOADREQ_MODULE,MODULE_OSSUBMIT    MODULE TO LOAD
         LA    R1,LOADREQ                        LOAD PARMS
         L     R15,COMM_V_OSLOAD
         BALR  R14,R15                           LOAD OSSUBMIT
         MVC   SUBREQ_DSN,SESS_DSN               COPY DATASET NAME
         MVC   SUBREQ_MEMBER,VDATA_MEMBER        COPY MEMBER NAME
         LA    R1,SUBREQ                         SUBMIT PARMS
         ITRACE ID=CALL_SUB,                                           +
               RDATA1=R1
         ICM   R15,15,LOADREQ_EP                 OSSUBMIT ENTRY POINT
         BALR  R14,R15                           CALL OSSUBMIT
         MVC   DXD_MSG,COMM_MSG_2
         LA    R2,DXD_MSG
         LA    R3,L'DXD_MSG
         TPUT  (R2),(R3),R
         B     MAIN0220
* ------------------------------------------------------------------- *
*        Delete a member                                              *
* ------------------------------------------------------------------- *
MAIN1100 DS    0H
         ITRACE ID=LINE_DEL,                                           +
               DATA1=VDATA_MEMBER
         MVI   VDATA_SELECTION,C' '              RESET THE SELECTION
         MVC   SPF_MEMBER,VDATA_MEMBER
         MVC   DXD_MEMBER,VDATA_MEMBER
         MVC   SESS_DISP_PANEL,OS0006
         L     R15,COMM_V_OSCNFRM
         BALR  R14,R15                           REQUEST CONFIRMATION
         CH    R15,H4                            RC = 4?
         BE    MAIN1150                          YES, ABORT
         CH    R15,H8                            RC = 4?
         BE    MAIN1150                          YES, ABORT
         OC    SPF_YES_OR_NO,COMM_BLANKS
         CLI   SPF_YES_OR_NO,C'Y'                DELETE CONFIRMED?
         BE    MAIN1120                          YES
         CLI   SPF_YES_OR_NO,C'N'                ANSWER 'NO'?
         BE    MAIN1150                          YES
MAIN1120 DS    0H
         BAL   R2,OPEN0000                       MAKE SURE DCB IS OPEN
         LA    R3,DXD_MEMBER
         ITRACE ID=STOW,                                               +
               RDATA1=R4,                                              +
               RDATA2=R3
         STOW  (R4),(R3),D                       DELETE THE MEMBER
         ITRACE ID=STOW_RC,                                            +
               RDATA1=R15
         LTR   R15,R15                           SUCCESSFUL?
         BNZ   ERR0100                           NO
         MVI   DSPCREQ_FUNC,$DSPCREQ_RETREIVE
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE
         BALR  R14,R15
         OI    DIR_FLAGS,$DIR_DELETED
         MVI   DSPCREQ_FUNC,$DSPCREQ_UPDATE
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE
         BALR  R14,R15
         MVI   VDATA_ATTR_2,$SCREEN_ATTR_HIGH
         MVC   VDATA_STATISTICS(DELETED_MSG_L),DELETED_MSG
         B     MAIN0220                          PROCESS NEXT SELECTION
MAIN1130 DS    0H
         ITRACE ID=PRIM_DEL
         CH    R3,H2                             TWO PARAMETERS?
         BL    MAIN1200                          JUST ONE
         BH    ERR0060                           TOO MANY
         CLC   OPERAND_02_LENGTH,H8              MEMBER NAME TOO LONG?
         BH    ERR0070                           YES
         ICM   R2,15,OPERAND_02_ADDRESS
         MVC   DSPCREQ_MEMBER,0(R2)              COPY MEMBER NAME
         MVC   DXD_MEMBER,0(R2)                  COPY MEMBER NAME
         MVI   DSPCREQ_FUNC,$DSPCREQ_FIND_MEMBER_S
         MVC   DSPCREQ_RECORD_NBR,F1             START WITH RECORD 1
         LA    R1,DSPCREQ                        PARM BLOCK ADDRESS
         L     R15,COMM_OSDSPACE                 OSDSPACE ENTRY POINT
         BALR  R14,R15                           FIND THE MEMBER
         CLI   DSPCREQ_RC,$DSPCREQ_OK
         BNE   ERR0020                           NOT FOUND
         TM    DIR_FLAGS,$DIR_DELETED+$DIR_RENAMED
         BNZ   ERR0130
         MVC   SPF_MEMBER,DSPCREQ_MEMBER
         MVC   SPF_ZCMD,COMM_BLANKS
         MVC   SESS_DISP_PANEL,OS0006
         L     R15,COMM_V_OSCNFRM
         BALR  R14,R15                           LINK TO SPF
         CH    R15,H4                            RC = 4?
         BE    MAIN1150                          YES, ABORT
         CH    R15,H8                            RC = 4?
         BE    MAIN1150                          YES, ABORT
         LTR   R15,R15                           SUCCESSFUL?
         BNZ   ERR0090                           NO
         CLI   SPF_YES_OR_NO,C'Y'                DELETE CONFIRMED?
         BE    MAIN1140                          YES
         CLI   SPF_YES_OR_NO,C'N'                ANSWER 'NO'?
         BE    MAIN1150                          YES
MAIN1140 DS    0H
         BAL   R2,OPEN0000                       MAKE SURE DCB IS OPEN
         LA    R3,DSPCREQ_MEMBER                 MEMBER NAME
         ITRACE ID=STOW,                                               +
               RDATA1=R4,                                              +
               RDATA2=R3
         STOW  (R4),(R3),D                       DELETE THE MEMBER
         ITRACE ID=STOW_RC,                                            +
               RDATA1=R15
         LTR   R15,R15                           SUCCESSFUL?
         BNZ   ERR0100                           NO
         MVI   DSPCREQ_FUNC,$DSPCREQ_RETREIVE
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE                 OSDSPACE ENTRY POINT
         BALR  R14,R15                           RETREIVE THE RECORD
         OI    DIR_FLAGS,$DIR_DELETED
         MVI   DSPCREQ_FUNC,$DSPCREQ_UPDATE
         L     R15,COMM_OSDSPACE                 OSDSPACE ENTRY POINT
         BALR  R14,R15                           UPDATE THE RECORD
         B     MAIN0000                          REBUILD DISPLAY
MAIN1150 DS    0H
         MVC   SPF_ZCMD,COMM_BLANKS              CLEAR THE COMMAND
         MVC   SPF_MSG_1(MSG02_L),MSG02
         B     MAIN0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN1200 DS    0H
         ITRACE ID=DELFLAGD
         MVC   DSPCREQ_RECORD_NBR,F1             FIRST RECORD
         LA    R1,DIR_RECORD
         ST    R1,DSPCREQ_RECORD_ADDR
MAIN1210 DS    0H
         CLC   DSPCREQ_RECORD_NBR,DATASPACE_1_LAST_RECORD
         BH    MAIN0000
         MVI   DSPCREQ_FUNC,$DSPCREQ_RETREIVE
         LA    R1,DSPCREQ                        DSPCREQ PARMS
         L     R15,COMM_OSDSPACE                 OSDSPACE ENTRY POINT
         BALR  R14,R15                           RETREIVE THE RECORD
         CLI   DSPCREQ_RC,$DSPCREQ_NOT_STORED
         BE    MAIN0000                          NOT STORED
         CLI   DSPCREQ_RC,$DSPCREQ_OK
         BNE   ERR0010                           ERROR..
         TM    DIR_FLAGS,$DIR_DELETED
         BO    MAIN1240                          MEMBER DELETED
         TM    DIR_FLAGS,$DIR_TAGGED             TAGGED?
         BNO   MAIN1240                          NO
MAIN1220 DS    0H
         ITRACE ID=MAIN1220,                                           +
               DATA1=DIR_NAME
         MVC   SPF_MEMBER,DIR_NAME               COPY MEMBER NAME
         MVC   SPF_ZCMD,COMM_BLANKS
         MVC   SESS_DISP_PANEL,OS0006
         L     R15,COMM_V_OSCNFRM
         BALR  R14,R15                           LINK TO OSCNFRM
         CH    R15,H4                            RC = 4?
         BE    MAIN0000                          YES, ABORT
         CH    R15,H8                            RC = 4?
         BE    MAIN0000                          YES, ABORT
         LTR   R15,R15                           SUCCESSFUL?
         BNZ   ERR0090                           NO
         CLI   SPF_YES_OR_NO,C'Y'                DELETE CONFIRMED?
         BE    MAIN1230                          YES
         CLI   SPF_YES_OR_NO,C'N'                ANSWER 'NO'?
         BE    MAIN1240                          YES
MAIN1230 DS    0H
         BAL   R2,OPEN0000                       MAKE SURE DCB IS OPEN
         LA    R3,DIR_NAME                       MEMBER NAME
         ITRACE ID=DELETE,                                             +
               RDATA1=R4,                                              +
               RDATA2=R3
         STOW  (R4),(R3),D                       DELETE THE MEMBER
         ITRACE ID=STOW_RC,                                            +
               RDATA1=R15
         LTR   R15,R15                           SUCCESSFUL?
         BNZ   ERR0100                           NO
         OI    DIR_FLAGS,$DIR_DELETED
         MVI   DSPCREQ_FUNC,$DSPCREQ_UPDATE
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE
         BALR  R14,R15                           UPDATE INFO
MAIN1240 DS    0H
         ITRACE ID=MAIN1240
         L     R1,DSPCREQ_RECORD_NBR
         A     R1,F1                             PLUS 1
         ST    R1,DSPCREQ_RECORD_NBR             SAVE RECORD NUMBER
         B     MAIN1210
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN1400 DS    0H
         ITRACE ID=PRIM_REN
         CH    R3,H2                             PROPER PARAMETERS?
         BL    ERR0050                           TOO FEW
         BH    ERR0060                           TOO MANY
         CLC   OPERAND_02_LENGTH,H8              MEMBER NAME TOO LONG?
         BH    ERR0070                           YES
         ICM   R2,15,OPERAND_02_ADDRESS
         MVC   SPF_MEMBER,0(R2)                  COPY MEMBER NAME
         MVC   DSPCREQ_MEMBER,SPF_MEMBER         COPY MEMBER NAME
         MVI   DSPCREQ_FUNC,$DSPCREQ_FIND_MEMBER_S
         MVC   DSPCREQ_RECORD_NBR,F1             START WITH RECORD 1
         LA    R1,DSPCREQ                        DSPCREQ PARMS
         L     R15,COMM_OSDSPACE                 OSDSPACE ENTRY POINT
         BALR  R14,R15                           LINK TO OSDSPACE
         CLI   DSPCREQ_RC,$DSPCREQ_OK            MEMBER EXIST?
         BNE   ERR0120                           NO
         MVC   SPF_ZCMD,COMM_BLANKS              CLEAR THE COMMAND
         MVC   SPF_NEW_MEMBER,COMM_BLANKS
MAIN1410 DS    0H
         LA    R1,DISPLAY                        SPF FUNCTION
         ST    R1,DXD_SPF_1                      SET ADDRESS
         LA    R1,OS0007                         PANEL NAME
         ST    R1,DXD_SPF_2                      SET ADDRESS
         OI    DXD_SPF_2,X'80'                   SET END-OF-LIST
         LA    R1,DXD_SPF_PARMS                  PARM LIST ADDRESS
         L     R15,COMM_ISPLINK                  SPF ENTRY POINT
         BALR  R14,R15                           LINK TO SPF
         CH    R15,H4                            RC = 4?
         BE    MAIN1450                          YES, ABORT
         CH    R15,H8                            RC = 4?
         BE    MAIN1450                          YES, ABORT
         LTR   R15,R15                           SUCCESSFUL?
         BNZ   ERR0090                           NO
         OC    SPF_NEW_MEMBER,COMM_BLANKS
         CLC   SPF_NEW_MEMBER,COMM_BLANKS
         BE    MAIN1410                          DISPLAY AGAIN
         MVI   DSPCREQ_FUNC,$DSPCREQ_FIND_MEMBER_S
         MVC   DSPCREQ_RECORD_NBR,F1             START WITH RECORD 1
         MVC   DSPCREQ_MEMBER,SPF_NEW_MEMBER
         LA    R1,DSPCREQ                        DSPCREQ PARMS
         L     R15,COMM_OSDSPACE                 OSDSPACE ENTRY POINT
         BALR  R14,R15                           LINK TO OSDSPACE
         CLI   DSPCREQ_RC,$DSPCREQ_OK            NAME ALREADY EXIST?
         BE    ERR0110                           YES
         MVC   DSPCREQ_MEMBER,SPF_MEMBER
         LA    R1,DSPCREQ                        DSPCREQ PARMS
         L     R15,COMM_OSDSPACE                 OSDSPACE ENTRY POINT
         BALR  R14,R15                           LINK TO OSDSPACE
         CLI   DSPCREQ_RC,$DSPCREQ_OK            MEMBER FOUND?
         BNE   ERR0120                           NO
         MVC   DXD_MEMBER,DIR_NAME               COPY MEMBER NAME
         TM    DIR_FLAGS,$DIR_DELETED+$DIR_RENAMED
         BNZ   ERR0130
MAIN1420 DS    0H
         MVC   DXD_RENAME_OLD,SPF_MEMBER
         MVC   DXD_RENAME_NEW,SPF_NEW_MEMBER
         BAL   R2,OPEN0000                       MAKE SURE DCB IS OPEN
         LA    R3,DXD_RENAME_OLD
         ITRACE ID=STOW,                                               +
               DATA1=0(R3),                                            +
               DATA2=8(R3)
         STOW  (R4),(R3),C                       RENAME THE MEMBER
         ITRACE ID=STOW_RC,                                            +
               RDATA1=R15
         LTR   R15,R15                           SUCCESSFUL?
         BNZ   ERR0140                           NO
         MVC   DIR_NAME,SPF_NEW_MEMBER           COPY NEW NAME
         MVI   DSPCREQ_FUNC,$DSPCREQ_STORE
         L     R1,DATASPACE_1_LAST_RECORD        HIGHEST RECORD SO FAR
         A     R1,F1                             PLUS 1
         ST    R1,DSPCREQ_RECORD_NBR             SET RECORD NUMBER
         LA    R1,DSPCREQ                        OSDSPACE PARMS
         L     R15,COMM_OSDSPACE                 OSDSPACE ENTRY POINT
         BALR  R14,R15                           STORE THE NEW RECORD
         MVC   DSPCREQ_MEMBER,SPF_MEMBER
         MVI   DSPCREQ_FUNC,$DSPCREQ_FIND_MEMBER_S
         MVC   DSPCREQ_RECORD_NBR,F1             START WITH RECORD 1
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE                 OSDSPACE ENTRY POINT
         BALR  R14,R15                           RETREIVE THE RECORD
         OI    DIR_FLAGS,$DIR_RENAMED
         MVC   DIR_DATA(DIR_SPF_L),COMM_BLANKS
         MVC   DIR_DATA(L'SPF_NEW_MEMBER),SPF_NEW_MEMBER
         MVI   DSPCREQ_FUNC,$DSPCREQ_UPDATE
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE                 OSDSPACE ENTRY POINT
         BALR  R14,R15                           UPDATE THE RECORD
         MVI   SESS_SORT,0                       RESET SORT INDICATORS
         B     MAIN0000                          REBUILD DISPLAY
MAIN1430 DS    0H
         ITRACE ID=LINE_REN
         MVI   VDATA_SELECTION,C' '              RESET THE SELECTION
         MVC   SPF_MEMBER,VDATA_MEMBER           COPY MEMBER
         MVC   DXD_MEMBER,VDATA_MEMBER           COPY MEMBER
         MVC   SPF_NEW_MEMBER,COMM_BLANKS
         LA    R1,DISPLAY                        SPF FUNCTION
         ST    R1,DXD_SPF_1                      SET ADDRESS
         LA    R1,OS0007                         PANEL NAME
         ST    R1,DXD_SPF_2                      SET ADDRESS
         OI    DXD_SPF_2,X'80'                   SET END-OF-LIST
         LA    R1,DXD_SPF_PARMS                  PARM LIST ADDRESS
         L     R15,COMM_ISPLINK                  SPF ENTRY POINT
         BALR  R14,R15                           LINK TO SPF
         CH    R15,H4                            RC = 4?
         BE    MAIN1450                          YES, ABORT
         CH    R15,H8                            RC = 8?
         BE    MAIN1450                          YES, ABORT
         LTR   R15,R15                           SUCCESSFUL?
         BNZ   ERR0090                           NO
         MVC   DSPCREQ_MEMBER,SPF_NEW_MEMBER
         MVI   DSPCREQ_FUNC,$DSPCREQ_FIND_MEMBER_S
         MVC   DSPCREQ_RECORD_NBR,F1             START WITH RECORD 1
         ITRACE ID=FIND_NEW,                                           +
               DATA1=DSPCREQ_MEMBER
         LA    R1,DSPCREQ                        PARM BLOCK ADDRESS
         L     R15,COMM_OSDSPACE                 OSDSPACE ENTRY POINT
         BALR  R14,R15                           FIND THE MEMBER
         CLI   DSPCREQ_RC,$DSPCREQ_OK
         BE    ERR0110                           MEMBER ALREADY EXISTS
         MVC   DSPCREQ_MEMBER,SPF_MEMBER
         MVC   DSPCREQ_RECORD_NBR,F1             START WITH RECORD 1
         ITRACE ID=FIND_OLD,                                           +
               DATA1=SPF_MEMBER
         LA    R1,DSPCREQ                        PARM BLOCK ADDRESS
         L     R15,COMM_OSDSPACE                 OSDSPACE ENTRY POINT
         BALR  R14,R15                           FIND THE MEMBER
         CLI   DSPCREQ_RC,$DSPCREQ_OK
         BNE   ERR0120                           MEMBER NOT FOUND
         MVC   DXD_RENAME_OLD,SPF_MEMBER
         MVC   DXD_RENAME_NEW,SPF_NEW_MEMBER
         BAL   R2,OPEN0000                       MAKE SURE DCB IS OPEN
         LA    R3,DXD_RENAME_OLD
         ITRACE ID=STOW,                                               +
               DATA1=0(R3),                                            +
               DATA2=8(R3)
         STOW  (R4),(R3),C                       RENAME THE MEMBER
         ITRACE ID=STOW_RC,                                            +
               RDATA1=R15
         LTR   R15,R15                           SUCCESSFUL?
         BNZ   ERR0140                           NO
         MVC   DIR_NAME,SPF_NEW_MEMBER           COPY NEW NAME
         MVI   DSPCREQ_FUNC,$DSPCREQ_STORE
         L     R1,DATASPACE_1_LAST_RECORD        HIGHEST RECORD SO FAR
         A     R1,F1                             PLUS 1
         ST    R1,DSPCREQ_RECORD_NBR             SET RECORD NUMBER
         LA    R1,DSPCREQ                        OSDSPACE PARMS
         L     R15,COMM_OSDSPACE                 OSDSPACE ENTRY POINT
         BALR  R14,R15                           STORE THE NEW RECORD
         MVC   DSPCREQ_MEMBER,SPF_MEMBER
         MVI   DSPCREQ_FUNC,$DSPCREQ_FIND_MEMBER_S
         MVC   DSPCREQ_RECORD_NBR,F1             START WITH RECORD 1
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE                 OSDSPACE ENTRY POINT
         BALR  R14,R15                           RETREIVE THE RECORD
         OI    DIR_FLAGS,$DIR_RENAMED
         MVC   DIR_DATA(DIR_SPF_L),COMM_BLANKS
         MVC   DIR_DATA(L'SPF_NEW_MEMBER),SPF_NEW_MEMBER
         MVI   DSPCREQ_FUNC,$DSPCREQ_UPDATE
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE                 OSDSPACE ENTRY POINT
         BALR  R14,R15                           UPDATE THE RECORD
         MVI   VDATA_ATTR_2,$SCREEN_ATTR_HIGH
         MVC   VDATA_STATISTICS(RENAMED_MSG_L),RENAMED_MSG
         MVI   SESS_SORT,0                       RESET SORT INDICATORS
         B     MAIN0220                          PROCESS NEXT SELECTION
MAIN1450 DS    0H
         MVC   SPF_ZCMD,COMM_BLANKS              CLEAR THE COMMAND
         MVC   SPF_MSG_1(MSG03_L),MSG03
         B     MAIN0000                          REBUILD DISPLAY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN2000 DS    0H
         ITRACE ID=SHOW
         SR    R15,R15
         CLC   COMM_OPERANDS_NBR,H1              ONE OPERAND?
         BE    MAIN2040                          YES.. SHOW ALL
         CLC   COMM_OPERANDS_NBR,H3              TOO MANY?
         BH    ERR0060                           YES
         BNE   MAIN2010                          OTHERWISE TWO OPERANDS
         ICM   R1,15,OPERAND_02_ADDRESS
         CLC   OPERAND_02_LENGTH,H3              LENGTH = 3?
         BNE   ERR0190                           NO.. CANNOT BE 'NOT'
         CLC   NOT,0(R1)                         'NOT' KEYWORD?
         BNE   ERR0190
         ICM   R1,15,OPERAND_03_ADDRESS
         ICM   R15,3,OPERAND_03_LENGTH
         B     MAIN2020
MAIN2010 DS    0H
         ICM   R1,15,OPERAND_02_ADDRESS
         ICM   R15,3,OPERAND_02_LENGTH
         CLC   ALL,0(R1)                         ALL?
         BE    MAIN2040                          YES
MAIN2020 DS    0H
         BCTR  R15,0
         EX    R15,SHOW_CLC                      'TAGGED' KEYWORD?
         BNE   ERR0190
         CLC   COMM_OPERANDS_NBR,H2              TWO OPERANDS?
         BNE   MAIN2030
         ITRACE ID=SHOW_TAG
         MVC   SPF_TAGGED,TAGGED
         OI    SESS_USER_OPTIONS,$OPTIONS_SHOW_TAGGED
         NI    SESS_USER_OPTIONS,255-$OPTIONS_SHOW_NOT_TAGGED
         B     MAIN2050
MAIN2030 DS    0H
         ITRACE ID=SHOW_NOT
         OI    SESS_USER_OPTIONS,$OPTIONS_SHOW_NOT_TAGGED
         NI    SESS_USER_OPTIONS,255-$OPTIONS_SHOW_TAGGED
         MVC   SPF_TAGGED,NOT_TAGGED
         B     MAIN2050
MAIN2040 DS    0H
         ITRACE ID=SHOW_ALL
         NI    SESS_USER_OPTIONS,255-$OPTIONS_SHOW_TAGGED
         NI    SESS_USER_OPTIONS,255-$OPTIONS_SHOW_NOT_TAGGED
         MVC   SPF_TAGGED,COMM_BLANKS
MAIN2050 DS    0H
         MVC   SPF_ZCMD,COMM_BLANKS
         B     MAIN0000
SHOW_CLC CLC   0(0,R1),TAGGED
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN2100 DS    0H
         ITRACE ID=REFRESH
         MVC   SPF_ZCMD,COMM_BLANKS              RESET COMMAND
         MVI   DSPCREQ_FUNC,$DSPCREQ_CLEAR       DATA SPACE FUNCTION
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE
         BALR  R14,R15                           CLEAR DATA SPACE
         MVC   LOADREQ_MODULE,MODULE_OSDESERV    MODULE NAME
         LA    R1,LOADREQ
         L     R15,COMM_V_OSLOAD
         BALR  R14,R15                           LOAD OSDESERV
         ICM   R15,15,LOADREQ_EP                 OSDESERV ENTRY POINT
         BALR  R14,R15                           LINK TO OSDESERV
         ITRACE ID=DESERVRC,                                           +
               RDATA1=R15
         CLC   DATASPACE_1_DISPLAY_FIRST_RECORD,DATASPACE_1_LAST_RECORD
         BNH   MAIN0000                          DISPLAY
         B     MAIN0400                          GO TO BOTTOM
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN2200 DS    0H
         ITRACE ID=LINECMDS
         MVI   SESS_COMMAND_FLAGS,$SESS_COMMAND_DISPLAY
         LA    R1,LINE_TABLE
         L     R15,COMM_V_OSCMD
         BALR  R14,R15
         B     MAIN0220
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN3000 DS    0H
         ITRACE ID=DUMP,                                               +
               DATA1=DATASPACE_1_NAME,                                 +
               DATA2=SESS_DD
         MVI   DAIR_FUNC,$DAIR_ALLOC             SET FUNCTION
         MVC   DAIR_DD,OSDUMP_DD                 COPY DDNAME
         MVC   DAIR_SYSOUT,COMM_SYSOUT
         LA    R1,DAIRREQ
         L     R15,COMM_V_OSDAIR
         BALR  R14,R15
         OC    DAIR_R15,DAIR_R15                 SUCCESSFUL?
         BNZ   ERR0020                           NO
         L     R1,=A(DUMP_DCB_I)
         MVC   DUMP_DCB(DUMP_DCB_L),0(R1)
         LA    R2,DUMP_DCB                       DCB ADDRESS
         ST    R2,DSPCREQ_DCB                    PASS IT TO OSDSPACE
         ITRACE ID=OPENDUMP,                                           +
               RDATA1=R2
         OPEN  ((R2),OUTPUT),                    OPEN OUTPUT DCB       +
               MODE=31,                                                +
               MF=(E,DXD_OPEN)
         LA    R1,DUMP_IO_AREA                   I/O AREA ADDRESS
         ST    R1,DSPCREQ_RECORD_ADDR            PASS ADDRESS
         MVI   DSPCREQ_FUNC,$DSPCREQ_DUMP
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE
         BALR  R14,R15
         ITRACE ID=CLS_DUMP,                                           +
               RDATA1=R2
         CLOSE ((R2)),                           CLOSE DCB             +
               MODE=31,                                                +
               MF=(E,DXD_CLOSE)
         FREEPOOL (R2)                           FREE BUFFERS
         MVC   SPF_MSG_1(MSG01_L),MSG01
         B     MAIN0180
MSG01    DC    C'DATASPACE dump complete'
MSG01_L  EQU   *-MSG01
MSG02    DC    C'Member deletion aborted'
MSG02_L  EQU   *-MSG02
MSG03    DC    C'Member rename ABORTED'
MSG03_L  EQU   *-MSG03

* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN3200 DS    0H
         ITRACE ID=DISP_CMD
         MVI   SESS_COMMAND_FLAGS,$SESS_COMMAND_DISPLAY
         LA    R1,COMMAND_TABLE
         L     R15,COMM_V_OSCMD
         BALR  R14,R15                           DISPLAY COMMANDS
         B     MAIN0185                          DISPLAY SAME DATA
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
OPEN0000 DS    0H
         TM    SESS_DCB_FLAGS,$SESS_DCB_OPEN     DCB OPEN?
         BOR   R2                                YES
         ITRACE ID=OPEN,                                               +
               RDATA1=R4
         OPEN  ((R4),OUTPUT),                                          +
               MODE=31,                          OPEN DCB              +
               MF=(E,DXD_OPEN)
         OI    SESS_DCB_FLAGS,$SESS_DCB_OPEN     REMEMBER DCB IS OPEN
         BR    R2
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
RTRY0000 DS    0H
         ITRACE ID=ABEND,                                              +
               DATA1=(SESS_SYSTEM_CODE,6)
         CLC   SESS_ABEND_DISP,AP1_DISP          ABEND ON CREATE DATE?
         BE    RTRY0010                          YES
         CLC   SESS_ABEND_DISP,AP2_DISP          MODIFICATION DATE?
         BE    RTRY0020                          YES
         L     R15,COMM_V_OSABEND
         BALR  R14,R15                           LINK TO OSABEND
         NI    SESS_ABEND_FLAG,255-$ABENDED
         B     EXIT0000
RTRY0010 DS    0H
         ITRACE ID=BAD_CREDT                     CREATE DATE NOT VALID
         MVC   VDATA_CREATE_DATE,=C'--------'
         B     MAIN0100                          RESUME
RTRY0020 DS    0H
         ITRACE ID=BAD_MODDT
         MVC   VDATA_MOD_DATE,=C'--------'
         B     MAIN0110                          RESUME
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0010  DS    0H
         DC    H'1',C'ERROR FROM OSDSPACE'
ERR0020  DS    0H
         ITRACE ID=DSPC_ERR
         MVC   SPF_MSG_1,COMM_MSG_1              COPY MESSAGE
         MVC   SPF_MSG_2,COMM_MSG_2              COPY MESSAGE
         MVC   SPF_MSG_3,COMM_MSG_3              COPY MESSAGE
         MVC   SPF_MSG_4,COMM_MSG_4              COPY MESSAGE
         MVC   SPF_MSG_5,COMM_MSG_5              COPY MESSAGE
         B     MAIN0180                          EXIT
ERR0050  DS    0H
         MVI   COMM_MSG_ID+1,6                   TOO FEW OPERANDS
         B     ERR1000
ERR0060  DS    0H
         MVI   COMM_MSG_ID+1,7                   TOO MANY OPERANDS
         B     ERR1000
ERR0070  DS    0H
         MVI   COMM_MSG_ID+1,8                   OPERAND TOO LONG
         B     ERR1000
ERR0080  DS    0H
         MVI   COMM_MSG_ID+1,2                   INVALID COMMAND
         MVC   COMM_INFO_01,SPF_ZCMD             COPY COMMAND
         B     ERR1000
ERR0090  DS    0H
         DC    H'9'
         DC    C'DISPLAY FAILED'
ERR0100  DS    0H
         ST    R15,COMM_DWORD                    SET RETURN CODE
         MVI   COMM_MSG_ID+1,9                   STOW (DELETE) FAILED
         B     ERR1000
ERR0110  DS    0H
         MVC   COMM_INFO_01,DSPCREQ_MEMBER
         MVI   COMM_MSG_ID+1,10                  MEMBER ALREADY EXISTS
         B     ERR1000
ERR0120  DS    0H
         MVI   COMM_MSG_ID+1,11                  MEMBER NOT FOUND
         MVC   COMM_INFO_01,SPF_MEMBER
         B     ERR1000
ERR0130  DS    0H
         MVI   COMM_MSG_ID+1,12                  DELETED OR RENAMED
         MVC   COMM_INFO_01,DXD_MEMBER
         B     ERR1000
ERR0140  DS    0H
         MVI   COMM_MSG_ID+1,13                  STOW (RENAME) FAILED
         ST    R15,COMM_DWORD                    SET RETURN CODE
         B     ERR1000
ERR0150  DS    0H
         DC    H'0'
ERR0160  DS    0H
         DC    H'0'
ERR0170  DS    0H
         MVI   COMM_MSG_ID+1,15                  NO SEARCH ARG
         B     ERR1000
ERR0180  DS    0H
         MVI   COMM_MSG_ID+1,16                  BOTTOM OF DATA REACHED
         B     ERR1000
ERR0190  DS    0H
         MVI   COMM_MSG_ID+1,17                  INVALID SHOW OPTION
         B     ERR1000
ERR0200  DS    0H
         CLI   SESS_FORMAT_FLAGS,$FORMAT_CLEANUP
         BE    EXIT0000
         DC    H'0'
ERR0210  DS    0H
         MVI   COMM_MSG_ID+1,18                  MEMBER NOT FOUND
         MVC   COMM_INFO_01(L'DXD_MEMBER),DXD_MEMBER MEMBER NAME
         B     ERR1000
ERR0220  DS    0H
         MVI   COMM_MSG_ID+1,19
ERR1000  DS    0H
         BAL   R2,MSG0000                        BUILD THE MESSAGE
         MVC   SPF_MSG_1,COMM_MSG_1              COPY MESSAGE
         MVC   SPF_MSG_2,COMM_MSG_2              COPY MESSAGE
         MVC   SPF_MSG_3,COMM_MSG_3              COPY MESSAGE
         MVC   SPF_MSG_4,COMM_MSG_4              COPY MESSAGE
         MVC   SPF_MSG_5,COMM_MSG_5              COPY MESSAGE
         B     MAIN0180
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MSG0000  DS    0H
         MVC   COMM_MSG_CSECT,MODID              CSECT FOR MESSAGES
         L     R15,COMM_V_OSMSG                  OSMSG ENTRY POINT
         BALR  R14,R15                           BUILD THE MESSAGE
         BR    R2
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DEBUG000 DS    0H
         ITRACE ID=DEBUG
         L     R15,COMM_V_OSDEBUG
         BALR  R14,R15
         B     MAIN0180
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
CLEAN000 DS    0H
         ITRACE ID=CLEAN_UP,                                           +
               DATA1=(DXD_VDATA_ADDR,4),                               +
               DATA2=(DXD_VDATA_LENGTH,4)
         L     R0,DXD_VDATA_LENGTH
         L     R1,DXD_VDATA_ADDR
         MVI   COMM_VDATA_FUNC,$VDATA_FREEMAIN   SET FUNCTION
         L     R15,COMM_V_OSVDATA
         BALR  R14,R15                           FREE VDATA
         TM    SESS_DCB_FLAGS,$SESS_DCB_OPEN     DCB OPEN?
         BNO   CLEAN010                          NO
         ITRACE ID=CLOSE,                                              +
               RDATA1=R4
         CLOSE ((R4)),                           CLOSE DCB             +
               MODE=31,                                                +
               MF=(E,DXD_CLOSE)
         NI    SESS_DCB_FLAGS,255-$SESS_DCB_OPEN DCB IS CLOSED
CLEAN010 DS    0H
         ICM   R4,15,SESS_DCB_ADDR               DCB PRESENT?
         BZ    CLEAN020                          NO
         ITRACE ID=FREEPOOL,                                           +
               RDATA1=R4
         FREEPOOL (R4)                           FREE BUFFERS
         ITRACE ID=FREE_DCB,                                           +
               RDATA1=R4
         L     R0,SESS_DCB_LENGTH
         FREEMAIN RU,                            FREE DCB              +
               A=(R4),                                                 +
               LV=(0)
         XC    SESS_DCB_ADDR,SESS_DCB_ADDR
         XC    SESS_DCB_LENGTH,SESS_DCB_LENGTH
         NI    SESS_DCB_FLAGS,255-$SESS_DCB_OPEN
CLEAN020 DS    0H
         LA    R0,OSDIR_DXD_L                    WORK AREA SIZE
         LR    R1,R13                            COPY WORK AREA ADDRESS
         L     R13,4(R13)                        RESTORE R13
         FREEMAIN RU,                            FREE WORK AREA        +
               A=(1),                                                  +
               LV=(0)
         XC    SESS_FORMATTER_DXD_ADDR,SESS_FORMATTER_DXD_ADDR
         XC    SESS_FORMATTER_DXD_LENGTH,SESS_FORMATTER_DXD_LENGTH
         SR    R15,R15
         B     EXIT0010
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         LH    R15,DXD_RC                        RETURN CODE
         ITRACE ID=EXIT,                                               +
               RDATA1=R15,                                             +
               DATA2=(12(R13),4)
         L     R13,4(R13)                        RESTORE R13
EXIT0010 DS    0H
         L     R14,12(,R13)                      RESTORE RETURN POINT
         LM    R0,R12,20(R13)                    RESTORE REGISTERS
         BR    R14                               RETURN
* ------------------------------------------------------------------- *
MVC_01   MVC   0(0,R7),HEADING_01                COPY HEADING
MVC_02   MVC   DXD_MEMBER(0),0(R2)               COPY MEMBER NAME
* ------------------------------------------------------------------- *
*        CONSTANTS                                                    *
* ------------------------------------------------------------------- *
F1              DC    F'1'
F2              DC    F'2'
H1              DC    H'1'
H2              DC    H'2'
H3              DC    H'3'
H4              DC    H'4'
H8              DC    H'8'

PDSDCB_I DCB   DSORG=PO,                                               +
               DDNAME=ANYDD,                                           +
               MACRF=W
PDSDCB_L       EQU   *-PDSDCB_I

DUMP_DCB_I     DCB DSORG=PS,                                           +
               DDNAME=OSDUMP,                                          +
               RECFM=FBA,                                              +
               LRECL=133,                                              +
               BLKSIZE=13300,                                          +
               MACRF=PM
DUMP_DCB_L     EQU      *-DUMP_DCB_I

OPEN_I         OPEN     (*,OUTPUT),                                    +
               MODE=31,                                                +
               MF=L
OPEN_L         EQU      *-OPEN_I
CLOSE_I        CLOSE    (*),                                           +
               MODE=31,                                                +
               MF=L
CLOSE_L        EQU      *-CLOSE_I

AP1_DISP        DC    Y((AP1-OSDIR)+4)
AP2_DISP        DC    Y((AP2-OSDIR)+4)


P1900000        DC    P'1900000'

EDIT_WORD_1     DC    X'4020202020202120'
EDIT_WORD_2     DC    X'F0212020204B202021'
EDIT_WORD_3     DC    X'40202020202020202120'

MODULE_OSAPPEND DC    CL8'OSAPPEND'
MODULE_OSDESERV DC    CL8'OSDESERV'
MODULE_OSOFFLD  DC    CL8'OSOFFLD '
MODULE_OSSUBMIT DC    CL8'OSSUBMIT'
ALL             DC    C'ALL '
CDATE           DC    C'CDATE'
DISPLAY         DC    C'DISPLAY'
OSABEND         DC    CL8'OSABEND'
OSDUMP_DD       DC    CL8'OSDUMP'
DIR_PANEL       DC    CL8'OSDIR'
OS0006          DC    CL8'OS0006'
OS0007          DC    CL8'OS0007'
MDATE           DC    C'MDATE'
NAME            DC    C'NAME'
NOT             DC    C'NOT'
NOT_TAGGED      DC    C'NOT TAGGED'
SIZE            DC    C'SIZE'
TAGGED          DC    C'TAGGED    '
TTR             DC    C'TTR'
USER            DC    C'USER'

HEXCHAR         EQU   *-C'0'
                DC    C'0123456789ABCDEF'

APPND_MSG       EQU   *
                DC    AL1($SCREEN_ATTR_HIGH)
                DC    C'<-------- Appended '
                DC    AL1($SCREEN_ATTR_NORMAL)
APPND_MSG_L     EQU   *-APPND_MSG

DELETED_MSG     EQU   *
                DC    AL1($SCREEN_ATTR_HIGH)
                DC    C'<-------- Deleted  '
                DC    AL1($SCREEN_ATTR_NORMAL)
DELETED_MSG_L   EQU   *-DELETED_MSG

RENAMED_MSG     EQU   *
                DC    AL1($SCREEN_ATTR_HIGH)
                DC    C'<-------- Renamed  '
                DC    AL1($SCREEN_ATTR_NORMAL)
RENAMED_MSG_L   EQU   *-RENAMED_MSG

OFFLD_MSG       EQU   *
                DC    AL1($SCREEN_ATTR_HIGH)
                DC    C'<-------- Offloaded'
                DC    AL1($SCREEN_ATTR_NORMAL)
OFFLD_MSG_L     EQU   *-OFFLD_MSG

RESET_MSG       EQU   *
                DC    AL1($SCREEN_ATTR_HIGH)
                DC    C'<-------- Reset    '
                DC    AL1($SCREEN_ATTR_NORMAL)
RESET_MSG_L     EQU   *-RESET_MSG

TAGGED_MSG      EQU   *
                DC    AL1($SCREEN_ATTR_HIGH)
                DC    C'<-------- Tagged   '
                DC    AL1($SCREEN_ATTR_NORMAL)
TAGGED_MSG_L    EQU   *-TAGGED_MSG


HEADING_01      DS    0C
                DC    AL1($SCREEN_ATTR_HIGH_YELLOW)
                DC    CL3' '
                DC    Cl8'Name    '
                DC    CL3' '
                DC    CL6' TTR  '
                DC    CL2' '
                DC    CL5'VV.MM'
                DC    CL1' '
                DC    CL7'   Size'
                DC    CL1' '
                DC    CL7'   Init'
                DC    CL1' '
                DC    CL8'Created'
                DC    CL1' '
                DC    CL17'____Modified_____'
                DC    CL1' '
                DC    CL8'User'
HEADING_01_L    EQU   *-HEADING_01

                LTORG

COMMAND_TABLE   DS    0X
                CMD   A,MAIN0700,'Append'
                CMD   B,MAIN0610,'Browse'
                CMD   D,MAIN1130,'Delete'
                CMD   E,MAIN0610,'Edit'
                CMD   F,MAIN0450,'Find'
                CMD   J,MAIN1000,'Submit as batch job'
                CMD   L,MAIN0450,'Locate'
                CMD   O,MAIN0800,'Offload'
                CMD   R,MAIN1400,'Rename'
                CMD   S,MAIN0610,'Select (browse)'
                CMD   T,MAIN0690,'Tag'
                CMD   X,MAIN0550,'Reset tag'
                CMD   BR,MAIN0610,'BRowse'
                CMD   ED,MAIN0610,'EDit'
                CMD   FI,MAIN0450,'FInd'
                CMD   FL,0,'List available formats'
                CMD   SH,MAIN2000,'Set SHOW (tagged or all)'
                CMD   DEL,MAIN1130,'DELete'
                CMD   JOB,MAIN1000,'Subit as batch job'
                CMD   LOC,MAIN0450,'LOCate'
                CMD   OFF,MAIN0800,'OFFload'
                CMD   REN,MAIN1400,'REName'
                CMD   RES,MAIN0550,'RESet tag'
                CMD   SEL,MAIN0610,'SELect (browse)'
                CMD   SUB,MAIN1000,'SUBmit as batch job'
                CMD   TAG,MAIN0690,'TAG'
                CMD   EDIT,MAIN0610,'EDIT'
                CMD   FIND,MAIN0450,'FIND'
                CMD   FLAG,MAIN0690,'FLAG (tag)'
                CMD   LIST,0,'List available formats'
                CMD   SHOW,MAIN2000,'Set SHOW (tagged or all)'
                CMD   SORT,MAIN0900,'SORT'
                CMD   VIEW,MAIN0610,'VIEW (browse)'
                CMD   DEBUG,DEBUG000,'Display internal data'
                CMD   FLIST,0,'List available formats'
                CMD   RESET,MAIN0550,'RESET tag'
                CMD   RFIND,MAIN0470,'Repeat previous FIND'
                CMD   APPEND,MAIN0700,'APPEND'
                CMD   BROWSE,MAIN0610,'BROWSE'
                CMD   DELETE,MAIN1130,'DELETE'
                CMD   LOCATE,MAIN0450,'LOCATE'
                CMD   RENAME,MAIN1400,'RENAME'
                CMD   SELECT,MAIN0610,'SELECT'
                CMD   SUBMIT,MAIN1000,'SUBMIT as batch job'
                CMD   OFFLOAD,MAIN0800,'OFFLOAD'
                CMD   REFRESH,MAIN2100,'REFRESH'
                CMD   DUMPDATA,MAIN3000,'Dump dataspace'
                CMD   ??,MAIN3200,'Display available commands'
                DC    X'FF'


LINE_TABLE      DS    0C
                CMD   A,MAIN0420,'Append'
                CMD   B,MAIN0600,'Browse'
                CMD   D,MAIN1100,'Delete'
                CMD   E,MAIN0600,'Edit'
                CMD   J,MAIN1010,'Submit as batch job'
                CMD   O,MAIN0510,'Offload'
                CMD   R,MAIN1430,'Rename'
                CMD   S,MAIN0600,'Select (browse)'
                CMD   T,MAIN0680,'Tag'
                CMD   V,MAIN0600,'View (browse)'
                CMD   X,MAIN0540,'Reset tag'
                CMD   ?,MAIN2200,'Display available line commands'
                DC    X'FF'
OSDIREND       EQU   *
* ------------------------------------------------------------------- *
*        WORK AREA                                                    *
* ------------------------------------------------------------------- *
OSDIR_WORK       DSECT
                 COPY   DXDPREF

DXD_OPEN         DS     0F,(OPEN_L)X
DXD_CLOSE        DS     0F,(CLOSE_L)X

DXD_SPF_PARMS    DS     0F
DXD_SPF_1        DS     F
DXD_SPF_2        DS     F
DXD_SPF_3        DS     F

DXD_VWIDTH       DS     F
DXD_VDEPTH       DS     F

DXD_VDATA_ADDR      DS  A
DXD_VDATA_LENGTH    DS  F
DXD_CURSOR_POSITION DS  F

DXD_LINES_REMAINING DS  F
DXD_CURRENT_VDATA   DS  A

DXD_RC           DS     H

DXD_LINES        DS     H

DXD_FLAGS        DS     X
$DXD_INIT        EQU    X'80'
$LINE_CMD        EQU    X'40'
$EMPTY_MEMBER    EQU    X'20'

DXD_WORK_1       DS     CL10

DXD_RENAME_OLD   DS     CL8
DXD_RENAME_NEW   DS     CL8

DXD_SELECTION    DS     C
DXD_MEMBER       DS     CL8

                 COPY   DIR

DUMP_CONTROL     DS     0F
DUMP_DCB         DS     (DUMP_DCB_L)X
DUMP_IO_AREA     DS     CL133

DXD_MSG          DS     CL80

                 DAIRREQ  DSECT=NO
                 DSPCREQ  DSECT=NO
                 APPNDREQ DSECT=NO
                 LOADREQ  DSECT=NO
                 OFFLDREQ DSECT=NO
                 SUBREQ   DSECT=NO
OSDIR_WORK       DSECT
OSDIR_DXD_L      EQU      *-OSDIR_WORK
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
VDATA             DSECT
VDATA_ATTR_1      DS    X
VDATA_SELECTION   DS    X
VDATA_ATTR_2      DS    X
                  DS    C
VDATA_MEMBER      DS    CL8
VDATA_ATTR_3      DS    X
                  DS    CL2
VDATA_STATISTICS  DS    0CL65
VDATA_TTR         DS    CL6
                  DS    CL2
VDATA_VERSION     DS    CL2
VDATA_DOT         DS    C
VDATA_LEVEL       DS    CL2
                  DS    C
VDATA_SIZE        DS    CL7
                  DS    C
VDATA_INIT        DS    CL7
                  DS    C
VDATA_CREATE_DATE DS    CL8
                  DS    C
VDATA_MOD_DATE    DS    CL8
                  DS    C
VDATA_MOD_TIME    DS    CL8
                  DS    C
VDATA_USER        DS    CL8
VDATA_L           EQU   *-VDATA

* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                  COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                  SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                  COPY     OPERAND
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                  BPXYSTAT DSECT=YES,LIST=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                  COPY     COMMAND
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                  OSSPFD  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                  IBMMAC  DCBD=PO
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                  COPY    ATTRS
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                  COPY    TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                  COPY    REGEQU
                  END     OSDIR
