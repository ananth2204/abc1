* ------------------------------------------------------------------- *
*                                                                     *
*  MODULE NAME: OSVARMGR                                              *
*                                                                     *
*  Define or delete SPF variables.                                    *
*                                                                     *
* ------------------------------------------------------------------- *
OSVARMGR CSECT
OSVARMGR AMODE  31
OSVARMGR RMODE  ANY
         USING  OSVARMGR,R15
         USING  OSCOMM,R12
         USING  SESSION,R11
         B      INIT0000
MODID    DC     CL8'OSVARMGR'
         DC     CL8'&SYSDATE'
         DC     CL8'&SYSTIME'
         DC     A(VARMGREND-OSVARMGR)
INIT0000 DS     0H
         STM    R14,R12,12(R13)               SAVE REGISTERS
         LR     R10,R15                       COPY BASE ADDRESS
         DROP   R15
         USING  OSVARMGR,R10
         L      R15,COMM_DXD                  DXD AREA BASE ADDRESS
         A      R15,DXD_START                 PLUS OFFSET
         ST     R13,4(R15)
         ST     R15,8(R13)
         LR     R13,R15                       COPY DXD AREA ADDRESS
         USING  DXDVMGR,R13                   DEFINE WORK AREA BASE
         MVC    DXD_CSECT,MODID
* ------------------------------------------------------------------- *
         LR    R9,R1
         USING VARREQ,R9                      DEFINE BASE
         ITRACE ID=ENTRY,                                              +
               RDATA1=R9,                                              +
               DATA2=VARCMD
         L     R8,COMM_OSSPFD                 SPF DATA
         USING OSSPFD,R8                      DEFINE BASE
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
VAR0010  DS    0H
         CLI   VARCMD,$VARDEF                 DEFINE VARIABLES?
         BE    VAR0100                        YES
         CLI   VARCMD,$VARDEL                 DELETE VARIABLE?
         BE    VAR0200                        YES
         SR    R5,R5
         ICM   R5,1,VARCMD
         ITRACE ID=BADCMD
         ABEND 1,DUMP,,USER                   BAD COMMAND
* ------------------------------------------------------------------- *
*        PROCESS DEFINE COMMANDS                                      *
* ------------------------------------------------------------------- *
VAR0100  DS    0H
         ITRACE ID=DEFINE,                    DEFINE VARIABLES         +
               DATA1=VARTBLE,                                          +
               DATA2=VARAREA
         L     R6,VARTBLE                     FIRST VARIABLE
VAR0110  DS    0H
         USING VARDSECT,R6                    DEFINE BASE
         TM    VARFLAG,$VDEFINE               VARIABLE ALREADY DEFINED?
         BO    VAR0120                        YES
         TM    VARFLAG,$VNEVER                NEVER DEFINE?
         BO    VAR0120                        YES
         LA    R1,VDEFINE                     COMMAND'S ADDRESS
         ST    R1,DXD_SPF1                    SET ADDRESS IN PARM LIST
         LA    R1,VARNAME                     VARIABLE'S NAME
         ST    R1,DXD_SPF2                    SET NAME'S ADDRESS
         SR    R1,R1                          CLEAR REGISTER
         ICM   R1,3,VARDISP                   DISPLACEMENT TO DATA
         A     R1,VARAREA                     PLUS BASE ADDRESS
         ST    R1,DXD_SPF3                    SET ADDRESS IN PARM LIST
         LA    R1,VARFORM                     DATA FORMAT
         ST    R1,DXD_SPF4                    SET ADDRESS IN PARM LIST
         LA    R1,VARLEN                      VARIABLE LENGTH
         ST    R1,DXD_SPF5                    SET ADDRESS IN PARM LIST
         OI    DXD_SPF5,X'80'                 SET END-OF-LIST
         ITRACE ID=VDEF,                      DEFINING A VARIABLE      +
               DATA1=VARNAME,                 .. VARIABLE'S NAME       +
               DATA2=DXD_SPF3                 .. ADDRESS OF DATA
         LA    R1,DXD_SPFP                    SPF PARM LIST ADDRESS
         L     R15,COMM_ISPLINK               SPFLINK ENTRY POINT
         BALR  R14,R15                        LINK TO SPF
         LTR   R15,R15                        VDEFINE SUCCESSFUL?
         BNZ   ERR0010                        NO
         OI    VARFLAG,$VDEFINE               VARIABLE HAS BEEN DEFINED
         TM    VARFLAG,$VGET                  VGET NOW?
         BNO   VAR0120                        NO
         ITRACE ID=VGET,                                               +
               DATA1=VARNAME
         MVC   DXD_VARNAME,VARNAME            COPY VARNAME
         LA    R1,VGET                        VGET COMMAND
         ST    R1,DXD_SPF1                    SET ADDRESS
         LA    R1,DXD_VARNAMES          < --  DON'T LIKE THE CLOSE NAME
         ST    R1,DXD_SPF2                    SET NAME
         LA    R1,SHARED                      VARIABLE POOL NAME
         ST    R1,DXD_SPF3                    SET ADDRESS
         OI    DXD_SPF3,X'80'                 FLAG END-OF-LIST
         LA    R1,DXD_SPF1
         L     R15,COMM_ISPLINK               SPF ENTRY POINT
         BALR  R14,R15
         ITRACE ID=VGET_RC,                                            +
               RDATA1=R15
VAR0120  DS    0H
         LA    R6,VARL(R6)                    NEXT VARIABLE
         CLI   0(R6),X'FF'                    END OF TABLE?
         BNE   VAR0110                        NO
         MVC   SPF_RFIND,PASSTHRU             SET RFIND OPTION
         B     EXIT0000                       ALL PROCESSED
* ------------------------------------------------------------------- *
*        PROCESS VARIABLE DELETION                                    *
* ------------------------------------------------------------------- *
VAR0200  DS    0H
         ITRACE ID=DELETE                     DELETING ALL VARIABLES
         LA    R1,VDELETE                     COMMAND'S ADDRESS
         ST    R1,DXD_SPF1                    SET ADDRESS IN PARM LIST
         L     R6,VARTBLE                     START WITH FIRST VARIABLE
VAR0210  DS    0H
         CLI   0(R6),X'FF'                    END OF TABLE?
         BE    EXIT0000                       YES
         TM    VARFLAG,$VDEFINE               VARIABLE DEFINED?
         BNO   VAR0220                        NO
         ST    R6,DXD_SPF2                    SET VARIABLE NAME ADDRESS
         OI    DXD_SPF2,X'80'                 FLAG END-OF-LIST
         ITRACE ID=VDEL,                      DELETING A VARIABLE      +
               DATA1=VARNAME                  .. VARIABLE NAME
         LA    R1,DXD_SPFP                    PARM LIST'S ADDRESS
         L     R15,COMM_ISPLINK               SPFLINK ENTRY POINT
         BALR  R14,R15                        LINK TO SPF
         LTR   R15,R15                        VDELETE SUCCESSFUL?
         BNZ   ERR0020                        NO
VAR0220  DS    0H
         LA    R6,VARL(R6)                    NEXT VARIABLE
         B     VAR0210                        PROCESS ALL VARIABLES
* ------------------------------------------------------------------- *
*        ERROR ROUTINES                                               *
* ------------------------------------------------------------------- *
ERR0010  DS    0H
         ITRACE ID=VDEFFAIL,                                           +
               RDATA1=R15
         DC    H'12'
ERR0020  DS    0H
         ITRACE ID=VDELFAIL,                                           +
               RDATA1=R15
         DC    H'13'
EXIT0000 DS    0H
         ITRACE ID=EXIT
         L     R13,4(R13)                     RESTORE SAVE AREA ADDRESS
         LM    R14,R12,12(R13)                RESTORE REGISTERS
         BR    R14                            RETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXD_START DC    Q(DXDVMGR)
VARNAME_I DC    F'1'
          DC    F'0'
          DC    CL8' '
VARNAME_L EQU   *-VARNAME_I
PASSTHRU  DC    CL8'PASSTHRU'
SHARED    DC    CL8'SHARED'
VDEFINE   DC    CL8'VDEFINE'
VDELETE   DC    CL8'VDELETE'
VGET      DC    CL8'VGET'
          LTORG
VARMGREND EQU   *
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDVMGR      DSECT
             COPY  DXDPREF
DXD_SPFP     DS    0A                    ISPF PARM LIST
DXD_SPF1     DS    A                     PARM 1 ADDRESS
DXD_SPF2     DS    A                     PARM 2 ADDRESS
DXD_SPF3     DS    A                     PARM 3 ADDRESS
DXD_SPF4     DS    A                     PARM 4 ADDRESS
DXD_SPF5     DS    A                     PARM 5 ADDRESS

DXD_VARNAMES DS   0A
             DS    F
             DS    F
DXD_VARNAME  DS    CL8
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         BPXYSTAT DSECT=YES,LIST=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY     VARDSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         VARREQ   DSECT=YES
         OSSPFD   TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY     REGEQU
         END      OSVARMGR
