* ------------------------------------------------------------------- *
*                                                                     *
*        ESTAE exit                                                   *
*                                                                     *
* ------------------------------------------------------------------- *
OSESTAE  CSECT
OSESTAE  AMODE 31
OSESTAE  RMODE ANY
         USING OSESTAE,R15
         B     STAE0000
MODID    DC    CL8'OSESTAE'
         DC    CL8'&SYSDATE'
         DC    CL8'&SYSTIME'
         DC    A(ESTAEEND-OSESTAE)
STAE0000 DS    0H
         CH    R0,H12                         SDWA PRESENT?
         BE    STAE0040                       NO
         STM   R14,R12,12(R13)                SAVE REGISTERS
         LR    R10,R15                        COPY BASE ADDRESS
         DROP  R15
         USING OSESTAE,R10                    DEFINE BASE
         SAC   0                              MAKE SURE AR MODE IS OFF
         LR    R9,R1                          COPY SDWA ADDRESS
         USING SDWA,R9                        DEFINE BASE
         L     R12,SDWAPARM                   PARM LIST ADDRESS
         USING OSCOMM,R12                     DEFINE BASE
         L     R11,COMM_ACTIVE_SESSION        CURRENTLY ACTIVE SESSION
         USING SESSION,R11
         TM    SESS_ABEND_FLAG,$ABEND_NO_STAE
         BO    STAE0040                       DON'T ATTEMPT RETRY
         TM    SESS_ABEND_FLAG,$ABENDED       ALREADY ABENDED?
         BO    STAE0040                       YES
         L     R15,COMM_DXD                   WORK AREA BASE
         A     R15,DXD_START                  PLUS DISP TO OUR SECTION
         ST    R13,4(,R15)                    SAVE CALLER'S REG 13
         ST    R15,8(,R13)                    CHAIN TO CALLER
         LR    R13,R15
         USING WKESTAE,R13
         MVC   DXD_CSECT,MODID
         SR    R15,R15                        CLEAR REGISTER
         ICM   R15,3,SDWACMPC                 SYSTEM COMPLETION CODE
         SRL   R15,4                          SHIFT OUT USER BIT(S)
         STH   R15,SESS_SYSTEM_CODE           SAVE SYSTEM CODE
         MVC   SESS_USER_CODE,SDWACMPC+1      COPY USER CODE
         NI    SESS_USER_CODE,X'0F'           TURN OFF SYSTEM BIT(S)
         MVC   SESS_ABEND_R15,SDWAGR15+2      SAVE R15
         ITRACE ID=ABEND,                     TRACE ABEND              +
               DATA1=(SESS_SYSTEM_CODE,6),        CAPTURE ABEND CODE(S)+
               DATA2=SDWAEC1                      .. CAPTURE PSW
         OI    SESS_ABEND_FLAG,$ABENDED       SET ABEND INDICATOR
         MVC   SESS_ABEND_CSECT,=C'????????'
         L     R2,SDWAGR10                    R10 AT TIME OF ABEND
BOMBBASE USING OSESTAE,R2
         CLC   =C'OS',BOMBBASE.MODID          FIRST TWO CHARS 'OS'?
         BNE   STAE0030                       NO
         CLC   =C'OSDSPACE',BOMBBASE.MODID    OSDSPACE?
         BE    STAE0040                       YES
         LA    R3,BOMBBASE.MODID+2
         LA    R4,L'MODID-2
STAE0010 DS    0H
         CLI   0(R3),C' '                     BLANK?
         BE    STAE0020                       YES
         CLI   0(R3),C'A'                     LESS THAN C'A'?
         BL    STAE0030                       YES
         CLI   0(R3),C'Z'                     C'Z' OR LESS?
         BL    STAE0020                       YES
         CLI   0(R3),C'0'                     LESS THAN C'0'
         BL    STAE0030                       YES
         CLI   0(R3),C'9'                     GREATER THAN C'9'?
         BH    STAE0030                       YES
STAE0020 DS    0H
         LA    R3,1(,R3)                      NEXT
         BCT   R4,STAE0010
         MVC   SESS_ABEND_CSECT,BOMBBASE.MODID
         L     R3,SDWANXT1                    INSTRUCTION AT ABEND
         LA    R2,0(,R2)                      DROP AMODE BIT(S)
         LA    R3,0(,R3)                      DROP AMODE BIT(S)
         SR    R3,R2                          COMPUTE DISPLACEMENT
         STCM  R3,3,SESS_ABEND_DISP           SAVE DISPLACEMENT
         DROP  BOMBBASE
         ITRACE ID=MOD+DISP,                                           +
               DATA1=SESS_ABEND_CSECT,                                 +
               DATA2=(SESS_ABEND_DISP,2)
         ITRACE ID=R0_R3,                                              +
               DATA1=SDWAGR00,                                         +
               DATA2=SDWAGR02
         ITRACE ID=R4_R7,                                              +
               DATA1=SDWAGR04,                                         +
               DATA2=SDWAGR06
         ITRACE ID=R8_R11,                                             +
               DATA1=SDWAGR08,                                         +
               DATA2=SDWAGR10
         ITRACE ID=R12_R15,                                            +
               DATA1=SDWAGR12,                                         +
               DATA2=SDWAGR14
STAE0030 DS    0H
         L     R2,SESS_RETRY                  RETRY POINT
         SETRP RETADDR=(R2),                  SET RECOVERY PARAMETERS  +
               FRESDWA=YES,                   .. DON'T CREATE A DUMP   +
               DUMP=NO,                       .. FREE THE SDWA         +
               RC=4                           .. RETRY
*
*        This should never be reached
*
         L     R13,4(,R13)                    RESTORE R13
         LM    R14,R12,12(R13)                RESTORE REGISTERS
         BR    R14
STAE0040 DS    0H
         SETRP RC=0                           DON'T TRY TO RECOVER
*
*        This should never be reached either
*
         BR    R14
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXD_START DC    Q(WKESTAE)
H12       DC    H'12'
          LTORG
ESTAEEND  EQU   *
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COMMON TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         BPXYSTAT DSECT=YES,LIST=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY   TRENTRY
* ------------------------------------------------------------------- *
WKESTAE  DSECT
         COPY   DXDPREF
* ------------------------------------------------------------------- *
         IBMMAC SDWA=YES
* ------------------------------------------------------------------- *
         COPY   REGEQU
         END    OSESTAE
