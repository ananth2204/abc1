*---------------------------------------------------------------------*
*                                                                     *
*         Format Page data set activity (SMF type 75)                 *
*         ICF define activity                                         *
*                                                                     *
*   At entry                                                          *
*       R15    program base address                                   *
*       R14    return address                                         *
*       R9     OSSPFD ADDRESS                                         *
*       R8     VDATA buffer address                                   *
*       R7     SMF RECORD ADDRESS +4 (DOES NOT INCLUDE LENGTH)        *
*                                                                     *
* ------------------------------------------------------------------- *
SMF0075  CSECT
SMF0075  AMODE 31
SMF0075  RMODE ANY
         USING OSCOMM,R12
         USING SESSION,R11
         USING SMF0075,R15
         B     INIT0000
MODID    DC    CL8'SMF0075'
         DC    CL8'&SYSDATE'
         DC    CL7'&SYSTIME'
INIT0000 DS    0H
         USING OSCOMM,R12                     DEFINE COMMON AREA BASE
         STM   R14,R12,12(R13)                SAVE REGS
         LR    R10,R15                        COPY ENTRY POINT
         USING SMF0075,R10                    DEFINE BASE
         DROP  R15
         L     R1,SESS_FORMATTER_DXD_ADDR
         A     R1,DXDSTART
         ST    R13,4(R1)                      SAVE R13
         ST    R1,8(R13)                      CHAIN SAVE AREA
         LR    R13,R1                         COPY WORK AREA ADDRESS
         USING WK0075,R13                     DEFINE BASE
         MVC   DXD_CSECT,MODID
         USING OSSPFD,R9                      DEFINE BASE
         USING SMF75FLG,R7                    DEFINE BASE
         L     R6,SPF_VDEPTH
         L     R5,SMF75PSS                    OFFSET TO PAGE INFO
         AR    R5,R7                          PLUS RECORD BASE
         SH    R5,H4
         USING SMF75PSD,R5                    DEFINE BSE
         LH    R4,SMF75PSN                    NBR OF SECTIONS
         MVC   0(PAGE_HEADING_L,R8),PAGE_HEADING
         A     R8,SPF_VWIDTH
         BCTR  R6,0
         A     R8,SPF_VWIDTH
         BCTR  R6,0
MAIN0010 DS    0H
         MVC   0(MSG_01_L,R8),MSG_01          INITIALIZE MESSAGE
WORK     USING MSG_01,R8
         MVC   WORK.MSG_01_DSN,SMF75DSN       COPY DATA SET NAME
         MVC   WORK.MSG_01_VOL,SMF75VOL       COPY VOLSER
         TM    SMF75PST,SMF75LPA              PLPA?
         BO    MAIN0020
         TM    SMF75PST,SMF75COM              COMMON?
         BO    MAIN0030
         TM    SMF75PST,SMF75LOC              LOCAL?
         BO    MAIN0040
         B     MAIN0050
MAIN0020 DS    0H
         MVC   WORK.MSG_01_TYPE,TYPE_PLPA
         B     MAIN0050
MAIN0030 DS    0H
         MVC   WORK.MSG_01_TYPE,TYPE_COMMON
         B     MAIN0050
MAIN0040 DS    0H
         MVC   WORK.MSG_01_TYPE,TYPE_LOCAL
         DROP  WORK
MAIN0050 DS    0H
         A     R8,SPF_VWIDTH
         BCT   R6,MAIN0060
         B     EXIT0000
MAIN0060 DS    0H
         TM    SMF75PST,SMF75DSB+SMF75ONL+SMF75OFL
         BNO   MAIN0100
         MVC   0(MSG_02_L,R8),MSG_02          INITIALIZE MESSAGE
WORK     USING MSG_02,R8
         TM    SMF75PST,SMF75DSB              BAD?
         BNO   MAIN0070                       NO
         MVC   WORK.MSG_02_BAD,YES
MAIN0070 DS    0H
         TM    SMF75PST,SMF75ONL              VARIED ONLINE?
         BNO   MAIN0080                       NO
         MVC   WORK.MSG_02_ONLINE,YES
MAIN0080 DS    0H
         TM    SMF75PST,SMF75OFL              VARIED OFFLINE
         BNO   MAIN0090
         MVC   WORK.MSG_02_OFFLINE,YES
         DROP  WORK
MAIN0090 DS    0H
         A     R8,SPF_VWIDTH                  NEXT LINE
         BCT   R6,MAIN0100
MAIN0100 DS    0H
         MVC   0(MSG_03_L,R8),MSG_03          INITIALIZE MESSAGE
WORK     USING MSG_03,R8
         L     R0,SMF75SLA                    TOTAL SLOTS
         CVD   R0,COMM_DWORD                  CONVERT TO DECIMAL
         ED    WORK.MSG_03_TOTAL,COMM_DWORD+4
         L     R0,SMF75BDS                    BAD SLOTS
         CVD   R0,COMM_DWORD                  CONVERT TO DECIMAL
         ED    WORK.MSG_03_BAD,COMM_DWORD+4
         L     R0,SMF75MXU                    MAXIMUM IN USE
         CVD   R0,COMM_DWORD
         ED    WORK.MSG_03_MAX,COMM_DWORD+4
         L     R0,SMF75MNU                    MINIMUM IN USE
         CVD   R0,COMM_DWORD                  CONVERT TO DECIMAL
         ED    WORK.MSG_03_MIN,COMM_DWORD+4
         L     R0,SMF75AVU                    AVERAGE NUMBER IN USE
         CVD   R0,COMM_DWORD
         ED    WORK.MSG_03_AVERAGE,COMM_DWORD+4
         DROP  WORK
         A     R8,SPF_VWIDTH                  NEXT LINE
         BCT   R6,MAIN0110
         B     EXIT0000
MAIN0110 DS    0H
         MVC   0(MSG_04_L,R8),MSG_04          INITIALIZE MESSAGE
WORK     USING MSG_04,R8
         L     R0,SMF75USE                    NBR OF TIMES USED
         CVD   R0,COMM_DWORD                  CONVERT TO DECIMAL
         ED    WORK.MSG_04_USE,COMM_DWORD+4
         L     R0,SMF75SIO                    NUMBER OF SIO'S
         CVD   R0,COMM_DWORD                  CONVERT TO DECIMAL
         ED    WORK.MSG_04_SIOS,COMM_DWORD+4
         L     R0,SMF75PGX                    PAGES TRANSFERRED
         CVD   R0,COMM_DWORD                  CONVERT TO DECIMAL
         ED    WORK.MSG_04_PAGES,COMM_DWORD+4
         A     R8,SPF_VWIDTH                  NEXT LINE
         BCT   R6,MAIN0120
         B     EXIT0000
         DROP  WORK
MAIN0120 DS    0H
         A     R8,SPF_VWIDTH                  NEXT LINE
         BCT   R6,MAIN0130
         B     EXIT0000
MAIN0130 DS    0H
         AH    R5,SMF75PSL                    NEXT PAGE DATA SET
         BCT   R4,MAIN0010
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
EXIT0000 DS    0H
         ITRACE ID=EXIT
         L     R13,4(R13)                     RESTORE SAVE AREA ADDRESS
         L     R14,12(,R13)                   RESTORE R14
         LM    R14,R12,12(R13)                RESTORE REGISTERS
         SR    R15,R15
         BR    R14                            RETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDSTART       DC    Q(WK0075)
H4             DC    H'4'

TYPE_PLPA      DC    CL10'PLPA'
TYPE_COMMON    DC    CL10'COMMON'
TYPE_LOCAL     DC    CL10'LOCAL'
YES            DC    CL3'Yes'

PAGE_HEADING   DS    0C
               DC    20C' '
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'Page dataset(s)'
               DC    AL1($SCREEN_ATTR_NORMAL)
PAGE_HEADING_L EQU   *-PAGE_HEADING

MSG_01         DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
MSG_01_DSN     DC    CL44' '
               DC    AL1($SCREEN_ATTR_NORMAL)
               DC    C'    '
MSG_01_VOL     DS    CL6' '
               DC    C'    '
MSG_01_TYPE    DC    CL10' '
MSG_01_L       EQU   *-MSG_01

MSG_02         DS    0C
               DC    C'Data set bad? '
MSG_02_BAD     DS    CL3' '
               DC    C'    Varied online?'
MSG_02_ONLINE  DS    CL3' '
               DC    C'    Varied offline?'
MSG_02_OFFLINE DS    CL3' '
MSG_02_L       EQU   *-MSG_02

MSG_03         DS    0C
               DC    C' Slots Total'
MSG_03_TOTAL   DC    X'4020202020202120'
               DC    C'  Bad'
MSG_03_BAD     DC    X'4020202020202120'
               DC    C'   Use MAX'
MSG_03_MAX     DC    X'4020202020202120'
               DC    C'  Min'
MSG_03_MIN     DC    X'4020202020202120'
               DC    C'  Avg'
MSG_03_AVERAGE DC    X'4020202020202120'
MSG_03_L       EQU   *-MSG_03

MSG_04         DS    0C
               DC    C' Times in use by ASM     '
MSG_04_USE     DC    X'4020202020202120'
               DC    C'  SIOs'
MSG_04_SIOS    DC    X'4020202020202120'
               DC    C'  Pages transferred'
MSG_04_PAGES   DC    X'4020202020202120'
MSG_04_L       EQU   *-MSG_04

* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
WK0075         DSECT
               COPY      DXDPREF
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               SESSION
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               BPXYSTAT  DSECT=YES,LIST=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               OSSPFD    TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
SMF_RECORD     DSECT
               ERBSMF75
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY      ATTRS
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY      TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY     REGEQU
               END      SMF0075
