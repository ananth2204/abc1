* ------------------------------------------------------------------- *
*                                                                     *
*  MODULE NAME: UPDATE                                                *
*                                                                     *
*  UPDATE a record                                                    *
*                                                                     *
* ------------------------------------------------------------------- *
UPDATE   CSECT
UPDATE   AMODE 31
UPDATE   RMODE ANY
         USING OSCOMM,R12
         USING SESSION,R11
         USING UPDATE,R15
         B     INIT0000
MODID    DC    CL8'UPDATE'
         DC    CL8'&SYSDATE'
         DC    CL7'&SYSTIME'
INIT0000 DS    0H
         STM   R14,R12,12(R13)                   SAVE REGS
         LR    R10,R15                           COPY ENTRY POINT ADDR
         DROP  R15
         USING UPDATE,R10                        DEFINE BASE
         USING DSPCREQ,R9                        DEFINE BASE
         L     R8,DSPCREQ_DATASPACE              DATASPACE INFO ADDRESS
         USING SCONTROL_DATASPACE,R8
         L     R15,COMM_OSDSPACE_DXD             WORK AREA BASE
         A     R15,DXD_START                     PLUS DISPLACEMENT
         ST    R13,4(R15)                        SAVE R13
         ST    R15,8(R13)                        CHAIN SAVE AREA
         LR    R13,R15                           COPY WORK AREA ADDRESS
         USING DXDUPDT,R13
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         ITRACE ID=UPDATE,                                             +
               DATA1=SCONTROL_DATASPACE_NAME,                          +
               DATA2=(DSPCREQ_RECORD_NBR,4)
         OC    DSPCREQ_RECORD_LENGTH(2),DSPCREQ_RECORD_LENGTH
         BNZ   ERR0080
         SR    R6,R6                              ADDR OF CONTROL INFO
         LAM   R6,R6,SCONTROL_DATASPACE_ALET      SET THE ALET
         SAC   512                                TURN ON AR MODE
         L     R5,DSPCREQ_RECORD_NBR              RECORD NUMBER
         STCM  R5,15,COMM_INFO_01
         C     R5,SCONTROL_DATASPACE_FIRST_RECORD TOO LOW?
         BL    ERR0040                            YES
         C     R5,SCONTROL_DATASPACE_LAST_RECORD  TOO HIGH?
         BH    ERR0050                            YES
         S     R5,SCONTROL_DATASPACE_FIRST_RECORD CONVERT TO RELATIVE
         MH    R5,=Y(DATASPACE_INDEX_L)           MULTIPLY BY SIZE EACH
         LAM   R5,R5,SCONTROL_DATASPACE_ALET      SET AR
         USING DATASPACE_INDEX,R5
         ICM   R2,15,DATASPACE_INDEX_ADDR
         BZ    ERR0060                            RECORD NOT STORED
         L     R3,DSPCREQ_RECORD_LENGTH           LENGTH
         C     R3,DATASPACE_INDEX_LENGTH          SAME LENGTH?
         BNE   ERR0110                            NO
         LAM   R2,R2,SCONTROL_DATASPACE_ALET      SET AR
         L     R14,DSPCREQ_RECORD_ADDR            NEW RECORD'S ADDRESS
         LR    R15,R3                             COPY LENGTH
         MVCL  R2,R14                             COPY TO DATA SPACE
         B     EXIT0000                           DONE
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0040  DS    0H
         MVI   COMM_MSG_ID+1,4
         MVC   COMM_INFO_02,SCONTROL_DATASPACE_NAME
         B     ERR1000
ERR0050  DS    0H
         MVI   COMM_MSG_ID+1,5
         MVC   COMM_INFO_02,SCONTROL_DATASPACE_NAME
         B     ERR1000
ERR0060  DS    0H
         MVI   COMM_MSG_ID+1,6
         MVC   COMM_INFO_02,SCONTROL_DATASPACE_NAME
         B     ERR1000
ERR0080  DS    0H
         MVI   COMM_MSG_ID+1,8
         MVC   COMM_INFO_02,SCONTROL_DATASPACE_NAME
         B     ERR1000
ERR0110  DS    0H
         MVI   COMM_MSG_ID+1,11
         STCM  R3,15,COMM_INFO_02
         MVC   COMM_INFO_02(4),DATASPACE_INDEX_LENGTH
         MVC   COMM_INFO_03(4),DSPCREQ_RECORD_LENGTH
ERR1000  DS    0H
         SAC   0
         L     R15,COMM_V_OSMSG
         BALR  R14,R15
         MVI   DSPCREQ_RC,$DSPCREQ_ERROR
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         SAC   0
         LAM   R2,R2,F0
         LAM   R5,R5,F0
         LAM   R6,R6,F0
         ITRACE ID=EXIT,                                               +
               DATA1=(DSPCREQ_RC,1)
         L     R13,4(R13)                     RESTORE SAVE AREA
         LM    R14,R12,12(R13)                RESTORE REGISTERS
         BR    R14                            RETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXD_START   DC    Q(DXDUPDT)
F0          DC    F'0'

            LTORG
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDUPDT     DSECT
            COPY      DXDPREF
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
            COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
            SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
            BPXYSTAT DSECT=YES,LIST=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
            DSPCREQ  DSECT=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
SCONTROL    DSECT
            SCONTROL SCONTROL
SCONTROL_L  EQU      *-SCONTROL
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
            COPY     SINDEX
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
            COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
            COPY     REGEQU
            END      UPDATE
