* ------------------------------------------------------------------- *
*                                                                     *
*  MODULE NAME: FINDDSN                                               *
*                                                                     *
*  FINDDSN A DATA SET                                                 *
*                                                                     *
* ------------------------------------------------------------------- *
FINDDSN  CSECT
FINDDSN  AMODE 31
FINDDSN  RMODE ANY
         USING OSCOMM,R12
         USING SESSION,R11
         USING FINDDSN,R15
         B     INIT0000
MODID    DC    CL8'FINDDSN'
         DC    CL8'&SYSDATE'
         DC    CL7'&SYSTIME'
INIT0000 DS    0H
         STM   R14,R12,12(R13)              SAVE REGS
         LR    R10,R15                      COPY ENTRY POINT ADDR
         DROP  R15
         USING FINDDSN,R10                  DEFINE BASE
         USING DSPCREQ,R9                   DEFINE BASE
         L     R8,DSPCREQ_DATASPACE         DATASPACE INFO ADDRESS
         USING SCONTROL_DATASPACE,R8
         L     R15,COMM_OSDSPACE_DXD        WORK AREA BASE
         A     R15,DXD_START                PLUS DISPLACEMENT
         ST    R13,4(R15)                   SAVE R13
         ST    R15,8(R13)                   CHAIN SAVE AREA
         LR    R13,R15                      COPY WORK AREA ADDRESS
         USING DXDDSN,R13
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         CLI   SESS_DATA_TYPE,$DATA_TYPE_VTOC
         BNE   ERR0010
         ITRACE ID=FIND_DSN,                                           +
               DATA1=SCONTROL_DATASPACE_NAME,                          +
               DATA2=DSPCREQ_DSN
         CLI   DSPCREQ_FUNC,$DSPCREQ_FIND_DSN_S
         BE    FIND0020                     USE FULL LENGTH
         LA    R1,DSPCREQ_DSN+(L'DSPCREQ_DSN-1)
         LA    R15,L'DSPCREQ_DSN-1          LENGTH OF NAME
FIND0010 DS    0H
         CLI   0(R1),C' '                   BLANK?
         BNE   FIND0020                     NO
         BCTR  R1,0
         BCT   R15,FIND0010                 LOOP
         MVC   COMM_MSG_1(MSG_01_L),MSG_01
         MVI   DSPCREQ_RC,$DSPCREQ_ERROR
         B     EXIT0000
FIND0020 DS    0H
         ICM   R2,15,SESS_FORMAT1S          HIGHEST FORMAT_1 STORED
         BZ    FIND0070
         L     R3,DSPCREQ_RECORD_NBR        STARTING RECORD NBR
         LR    R7,R3                        COPY
         MH    R7,=Y(DATASPACE_INDEX_L)     MULTIPLY BY LENGTH EACH
         USING DATASPACE_INDEX,R7           DEFINE BASE
         A     R3,F1                        PLUS 1
         SR    R2,R3                        MINUS FIRST
         A     R2,F1                        PLUS 1
         LAM   R7,R7,SCONTROL_DATASPACE_ALET SET AR
         LAM   R4,R4,SCONTROL_DATASPACE_ALET SET AR
         SAC   512                          TURN ON AR MODE
FIND0030 DS    0H
         ICM   R4,15,DATASPACE_INDEX_ADDR   ADDRESS OF STORED RECORD
         BZ    FIND0060                     NOT STORED
         LA    R4,1(,R4)                    SKIP SORT SEQUENCER
         CLI   DSPCREQ_FUNC,$DSPCREQ_FIND_DSN_G
         BE    FIND0040                     DO GENERIC SEARCH
         CLC   DSPCREQ_DSN,0(R4)            DSN MATCH?
         BE    FIND0080                     YES
         B     FIND0060
FIND0040 DS    0H
         LA    R14,L'DSPCREQ_DSN
         SR    R14,R15                      NUMBER OF LOOPS
FIND0050 DS    0H
         EX    R15,DSN_CLC                  MATCH?
         BE    FIND0080                     YES
         LA    R4,1(,R4)                    PLUS 1
         BCT   R14,FIND0050                 LOOP
FIND0060 DS    0H
         LA    R7,DATASPACE_INDEX_L(,R7)    NEXT INDEX ENTRY
         A     R3,F1                        ADD 1 TO RECORD NUMBER
         BCT   R2,FIND0030                  CHECK THIS ENTRY
         SAC   0                            TURN OFF AR MODE
FIND0070 DS    0H
         MVC   COMM_MSG_1(MSG_02_L),MSG_02
         MVI   DSPCREQ_RC,$DSPCREQ_ERROR    SET ERROR INDICATOR
         B     EXIT0000
FIND0080 DS    0H
         L     R0,DSPCREQ_RECORD_ADDR       CALLER'S I/O AREA ADDRESS
         L     R1,DATASPACE_INDEX_LENGTH    RECORD LENGTH
         ST    R1,DSPCREQ_RECORD_LENGTH     PASS LENGTH TO CALLER
         L     R4,DATASPACE_INDEX_ADDR      RECORD ADDRESS
         LR    R5,R1                        COPY LENGTH
         MVCL  R0,R4                        COPY TO CALLER
         SAC   0                            TURN OFF AR MODE
         ITRACE ID=FOUND,                                              +
               RDATA1=R3
         ST    R3,DSPCREQ_RECORD_NBR        RETURN RECORD NUMBER
         MVI   DSPCREQ_RC,$DSPCREQ_OK       SET RC
         B     EXIT0000                     EXIT
DSN_CLC  CLC   DSPCREQ_DSN(0),0(R4)
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0010  DS    0H
         MVI   COMM_MSG_ID+1,15             SET MESSAGE ID
         L     R14,COMM_V_OSMSG
         BALR  R14,R15                      BUILD MESSAGE
         MVI   DSPCREQ_RC,$DSPCREQ_ERROR
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=EXIT,                                               +
               DATA1=(DSPCREQ_RC,1)
         LAM   R7,R7,F0                     RESET AR
         LAM   R4,R4,F0                     RESET AR
         L     R13,4(R13)                   RESTORE SAVE AREA ADDRESS
         LM    R14,R12,12(R13)              RESTORE REMAINING REGISTERS
         BR    R14                          RETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXD_START     DC    Q(DXDDSN)
F0            DC    F'0'
F1            DC    F'1'

MSG_01        DC    C'DSN IS ALL BLANKS'
MSG_01_L      EQU   *-MSG_01
MSG_02        DC    C'NOT FOUND'
MSG_02_L      EQU   *-MSG_02
              LTORG
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDDSN        DSECT
              COPY      DXDPREF
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              BPXYSTAT DSECT=YES,LIST=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              DSPCREQ  DSECT=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
SCONTROL      DSECT
              SCONTROL SCONTROL
SCONTROL_L    EQU      *-SCONTROL
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              COPY     SINDEX
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              COPY     REGEQU
              END      FINDDSN
