* ------------------------------------------------------------------- *
*                                                                     *
*  MODULE NAME: Find a VTOC record by CCHHR                           *
*                                                                     *
* ------------------------------------------------------------------- *
FINDCCHH CSECT
FINDCCHH AMODE 31
FINDCCHH RMODE ANY
         USING OSCOMM,R12
         USING SESSION,R11
         USING FINDCCHH,R15
         B     INIT0000
MODID    DC    CL8'FINDCCHH'
         DC    CL8'&SYSDATE'
         DC    CL7'&SYSTIME'
INIT0000 DS    0H
         STM   R14,R12,12(R13)                SAVE REGS
         LR    R10,R15                        COPY ENTRY POINT ADDR
         DROP  R15
         USING FINDCCHH,R10                   DEFINE BASE
         USING DSPCREQ,R9                     DEFINE BASE
         L     R8,DSPCREQ_DATASPACE           DATASPACE INFO ADDRESS
         USING SCONTROL_DATASPACE,R8
         L     R15,COMM_OSDSPACE_DXD          WORK AREA BASE
         A     R15,DXD_START                  PLUS DISPLACEMENT
         ST    R13,4(R15)                     SAVE R13
         ST    R15,8(R13)                     CHAIN SAVE AREA
         LR    R13,R15                        COPY WORK AREA ADDRESS
         USING DXDCCHH,R13
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY,                                              +
               DATA1=(DSPCREQ_RECORD_ADDR,4)
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         ITRACE ID=FINDCCHH,                                           +
               DATA1=SCONTROL_DATASPACE_NAME,                          +
               DATA2=(DSPCREQ_CCHHR,5)
         CLI   SESS_DATA_TYPE,$DATA_TYPE_VTOC
         BNE   ERR0010                        NOT VTOC
         SR    R1,R1                          START WITH FIRST RECORD
         SR    R7,R7                          FIRST INDEX RECORD
         USING DATASPACE_INDEX,R7             DEFINE BASE
         LAM   R7,R7,SCONTROL_DATASPACE_ALET  SET AR
         SAC   512                            TURN ON AR MODE
FIND0020 DS    0H
         CLC   DSPCREQ_CCHHR,DATASPACE_INDEX_CCHHR
         BE    FIND0030                       FOUND IT
         LA    R7,DATASPACE_INDEX_L(,R7)      NEXT RECORD'S INDEX
         A     R1,F1                          ADD 1 TO RECORD NUMBER
         C     R1,SCONTROL_DATASPACE_LAST_RECORD AT LAST RECORD?
         BNH   FIND0020                       NO, KEEP LOOKING
         SAC   0
         ITRACE ID=NOT_FND
         MVI   DSPCREQ_RC,$DSPCREQ_ERROR      NOT FOUND
         MVC   COMM_MSG_1(MSG_02_L),MSG_02
         B     EXIT0000
FIND0030 DS    0H
         ST    R1,DSPCREQ_RECORD_NBR          RETURN RECORD NBR
         L     R14,DATASPACE_INDEX_ADDR       RECORD'S ADDRESS
         LAM   R14,R14,SCONTROL_DATASPACE_ALET
         L     R15,DATASPACE_INDEX_LENGTH
         STCM  R15,15,DSPCREQ_RECORD_LENGTH
         L     R0,DSPCREQ_RECORD_ADDR         CALLER'S I/O AREA ADDRESS
         LR    R1,R15                         COPY TO 'LENGTH 2'
         MVCL  R0,R14                         COPY RECORD TO CALLER
         SAC   0
         ITRACE ID=FOUND
         MVI   DSPCREQ_RC,$DSPCREQ_OK         SET RC
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0010  DS    0H
         MVI   COMM_MSG_ID+1,15               SET MESSAGE ID
         L     R14,COMM_V_OSMSG
         BALR  R14,R15                        BUILD MESSAGE
         MVI   DSPCREQ_RC,$DSPCREQ_ERROR
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=EXIT,                                               +
               DATA1=(DSPCREQ_RC,1)
         LAM   R7,R7,F0
         LAM   R14,R14,F0
         L     R13,4(R13)                     RESTORE ADDRESS
         LM    R14,R12,12(R13)                RESTORE REGISTERS
         BR    R14                            RETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXD_START     DC    Q(DXDCCHH)
F0            DC    F'0'
F1            DC    F'1'

MSG_02        DC    C'NOT FOUND'
MSG_02_L      EQU   *-MSG_02

              LTORG
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDCCHH       DSECT
              COPY      DXDPREF
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              SESSION   TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              BPXYSTAT  DSECT=YES,LIST=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              DSPCREQ   DSECT=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
SCONTROL      DSECT
              SCONTROL  SCONTROL
SCONTROL_L    EQU       *-SCONTROL
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              COPY      SINDEX
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              COPY      TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              COPY      REGEQU
              END       FINDCCHH
