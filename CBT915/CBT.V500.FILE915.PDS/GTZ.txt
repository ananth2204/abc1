/* --------- REXX Generic Tracker Facility Post Processor ---------- */
/*                                                                   */
/* An ISPF dialog to generate JCL to execute the Generic Tracker     */
/* Facility batch reporting program, GTZPRINT.  Note that by default */
/* GTZPRINT is a RACF-protected utility.  See the z/OS MVS Diagnosis */
/* manual for creating the appropriate facilty class defintion.      */
/*                                                                   */
/* ----------------------------------------------------------------- */
/*                                                                   */
/* Updated 11Aug14, William J. Smith, IBM GTS/CTG/Datamatics         */
/* . Added CONTROL DISPLAY SAVE/RESTORE to fix termination error if  */
/*   EDIT is displayed and immediately terminated with CANCEL as the */
/*   re-display would have an invalid cursor location                */
/*                                                                   */
/* Updated 30Jul14, William J. Smith, IBM GTS/CTG/Datamatics         */
/* . Added support for a fifth JOB statement to accommodate complex  */
/*   routing across multiple NJE connections in a parallel sysplex   */
/*                                                                   */
/* Updated 29Jul14, William J. Smith, IBM GTS/CTG/Datamatics         */
/* . Added SCHENV parameter for WLM parallel sysplex environment     */
/* . Updated GTZPRINT skeleton to document program requirement that  */
/*   mandates DCB=BLKSIZE=80 on the SYSOUT DD statement              */
/* . Corrected panel redisplay error (incorrect coding syntax)       */
/*                                                                   */
/* Written 23Jul14, William J. Smith, IBM GTS/CTG/Datamatics         */
/* . Extracts content from the z/OS 2.1 or later Generic Tracker     */
/*   Facility diagnostic records using the GTZPRINT utility          */
/* . Ref:  "z/OS MVS Diagnosis: Tools and Service Aids", GA32-0905   */
/*                                                                   */
/* Utility Programs:  GTZPRINT - report tracking facility findings   */
/* Ref:  SYS1.SAMPLIB(GTZPRNTJ)                                      */
/*                                                                   */
/* ----------------------------------------------------------------- */

/* Trace ?i                                                          */

/* ----------------------------------------------------------------- */
/*                                                                   */
/* Build a valid JOB statement for batch execution of GTZPRINT       */
/*                                                                   */
/* ----------------------------------------------------------------- */
/*                                                                   */

ADDRESS ISPEXEC
"VGET (ZACCTNUM,ZTEMPF) ASIS"

tsoid = sysvar("sysuid")
acct  = substr(zacctnum,1,1)

jobcard_jcl = "(jobcard1 jobcard2 jobcard3 jobcard4 jobcard5)"
"VGET" jobcard_jcl "SHARED"

If jobcard1 = "" then
  jobcard1 = ,
  "//"tsoid"GTZP JOB ("acct"),'GTZPRINT Track Data',CLASS=K,  "

If jobcard2 = "" then
  jobcard2 = ,
  "//     MSGCLASS=1,MSGLEVEL=(1,1),NOTIFY=&SYSUID,REGION=0K ,"

If jobcard3 = "" then
  jobcard3 = ,
  "//*    SCHENV=                                             "

If jobcard4 = "" then
  jobcard4 = "//*"

If jobcard5 = "" then
  jobcard5 = "//*"

/* ----------------------------------------------------------------- */
/*                                                                   */
/* Display the GTZPRINT batch submission panel                       */
/*                                                                   */
/* ----------------------------------------------------------------- */
/*                                                                   */

"DISPLAY PANEL(GTZPRINT)"
If RC > 0 then
  Do
    "SETMSG MSG(GTZP001)"
    Exit
  End

Do while RC = 0
/* ----------------------------------------------------------------- */
/*                                                                   */
/* Initiate file tailoring to construct JCL to execute GTZPRINT      */
/*                                                                   */
/* ----------------------------------------------------------------- */
/*                                                                   */

  "FTOPEN TEMP"

/* ----------------------------------------------------------------- */
/*                                                                   */
/* Construct JOB statement JCL and JECL                              */
/*                                                                   */
/* ----------------------------------------------------------------- */
/*                                                                   */

  "FTINCL GTZPSKEL"
  ftrc = RC
  If ftrc > 0 then
    Do
      "SETMSG MSG(GTZP002)"
      Exit
    End

/* ----------------------------------------------------------------- */
/*                                                                   */
/* Close the temporary file tailoring data set and pass the file     */
/* tailoring temporary data set name to the PDF editor using the     */
/* ZTEMPF system variable.                                           */
/*                                                                   */
/* ----------------------------------------------------------------- */
/*                                                                   */

  "FTCLOSE"

  "CONTROL DISPLAY SAVE"

  "EDIT DATASET('"ZTEMPF"')"

/* ----------------------------------------------------------------- */
/*                                                                   */
/* VGET the name of the last command entered during EDIT processing. */
/* Allow the user to CANCEL the PDF EDIT session and bypass          */
/* submission of the generated jobstream for background (batch)      */
/* processing.                                                       */
/*                                                                   */
/* SUBMIT generates 2 possible return codes:                         */
/*                                                                   */
/*   0 - Submit processing was successful.                           */
/*  12 - Submit processing was unsuccessful and an error message has */
/*       been issued.  (Possible error cause:  submission of an      */
/*       empty dat set.)                                             */
/* Force all decision points to exit at the same EXIT statement.     */
/*                                                                   */
/* ----------------------------------------------------------------- */

  "VGET (ZVERB)"
  If ,
(ZVERB = "END" | ZVERB = "RETURN" | ZVERB = "SUBMIT" | ZVERB = "EXIT"),
  then
    Do
      Address TSO
      "SUBMIT '"ZTEMPF"'"
      subrc = RC
      If subrc = 0 then
        Do
          Address ISPEXEC
          "SETMSG MSG(GTZP005)"
        End
      Else
        Do
          Address ISPEXEC
          "SETMSG MSG(GTZP003)"
          Exit
        End
    End
  Else
    "SETMSG MSG(GTZP004)"

  "CONTROL DISPLAY RESTORE"
  "DISPLAY CURSOR(STATUS)"

End
