./ ADD NAME=$NOTE    0100-05112-05112-1702-00030-00030-00000-SBGOLOB
Installation instructions for SENDX.

    (I renamed the original CLIST from SEND to SENDX so it
    doesn't interfere with the TSO SEND command.  I didn't
    want to bother with typing %SEND and I thought that SENDX
    was easier.)

1.  Copy SENDX to a library in your SYSPROC concatenation on TSO.

2.  Copy SENDP to a panel library in your ISPPLIB concatenation.

Type SENDX and you're ready to go.

  Sam Golob
  sbgolob@att.net         or sbgolob@cbttape.org
   ---------------------------------------------------------
Note from John Kalinich, who sent this to me.

Sam,

A former co-worker of mine wrote the SEND clist.  His name
is Mark Reschke and he now lives in Caribou, Maine.

  mark.reschke@noaa.gov.
  jkalinic@csc.com

Regards,
John K


./ ADD NAME=SENDP    0102-91191-92233-0645-00035-00033-00000-TRIDJK
)ATTR DEFAULT(%+_)
       %   TYPE(TEXT) INTENS(HIGH)
       +   TYPE(TEXT) INTENS(LOW)
       _   TYPE(INPUT) INTENS(HIGH) CAPS(OFF) JUST(LEFT)
       ¬   TYPE(INPUT) INTENS(HIGH) CAPS(ON)  JUST(LEFT)
)BODY
%-------------------------- SEND COMMAND ENTRY PANEL ---------------------------
%ENTER PARAMETERS BELOW:_ZCMD                                                  +
+
+   MESSAGE TO BE SENT.  SPECIFY TWO SINGLE QUOTES ('')
+   WHEN ONE (') IS TO BE SENT.
%   ===>_MSG1                                                                 ++
+       _MSG2                                                                 ++
+       _MSG3                                                                 ++
+
+   ENTER A%LIST OF USERS+TO RECEIVE MESSAGE.  SEPARATE THEM BY COMMAS (,).
+     OR
+   ENTER%"OPER"+TO SEND TO THE OPERATOR.
+     OR
+   ENTER%"BRDCST"+TO SEND MESSAGE TO EVERYBODY AND PLACE IT IN THE BROADCAST
          DATASET FOR LATER SENDING TO USERS WHO LOGON.
+     OR
+   ENTER%"DELETE"+WITH A MESSAGE NUMBER IN ONE OR MORE LINES ABOVE TO DELETE
          A MESSAGE FROM THE BROADCAST DATASET.
+     OR
+   ENTER%"LIST"+TO LIST THE BROADCAST DATASET AND ASSOCIATED MESSAGE NUMBERS.
%   ===>¬ULIST                                                                 +
)INIT
  IF (&ULIST = BRDCST,DELETE)
    &ULIST = &Z
)PROC
  IF (&ULIST ¬= LIST)
    VER(&MSG1,NONBLANK)
    VER(&ULIST,NONBLANK)
)END
./ ADD NAME=SENDX    0108-91191-05112-1121-00100-00100-00000-TRIDJK
PROC 0 DEBUG
IF &DEBUG = &STR(DEBUG) THEN -
  CONTROL MSG NOFLUSH LIST CONLIST SYMLIST PROMPT ASIS
ELSE -
  CONTROL NOMSG NOFLUSH NOLIST NOCONLIST NOSYMLIST ASIS
DISP: +
ISPEXEC DISPLAY PANEL(SENDP)
IF &LASTCC = 8 THEN EXIT
SET BLANKS = &STR(                                        )
SET BLANKS = &STR(&BLANKS)&STR(                                )
SET MSG1 = &STR(&MSG1)&SUBSTR(1:&EVAL(70-&LENGTH(&STR(&MSG1))),&BLANKS)
IF &STR(&ULIST) = OPER -
  THEN -
    DO
      SEND '&STR(&MSG1)'
      IF &LENGTH(&STR(&MSG2)) > 0 -
        THEN -
          DO
            SET MSG2 = &STR(&MSG2)+
              &SUBSTR(1:&EVAL(70-&LENGTH(&STR(&MSG2))),&BLANKS)
            SEND '&STR(&MSG2)'
          END
      IF &LENGTH(&STR(&MSG3)) > 0 -
        THEN -
          DO
            SET MSG3 = &STR(&MSG3)+
              &SUBSTR(1:&EVAL(70-&LENGTH(&STR(&MSG3))),&BLANKS)
            SEND '&STR(&MSG3)'
          END
    END
  ELSE -
    IF &STR(&ULIST) = BRDCST -
      THEN -
        DO
          OPER
          SEND '&STR(&MSG1)',ALL,LOGON
          IF &LENGTH(&STR(&MSG2)) > 0 -
            THEN -
              SEND '&STR(&MSG2)',ALL,LOGON
          IF &LENGTH(&STR(&MSG3)) > 0 -
            THEN -
              SEND '&STR(&MSG3)',ALL,LOGON
          DATA
          END
          ENDDATA
        END
      ELSE -
        IF &STR(&ULIST) = DELETE -
          THEN -
            DO
              SET MSG1 = &MSG1
              SET MSG2 = &MSG2
              SET MSG3 = &MSG3
              OPER
              SEND &MSG1,DELETE
              IF &LENGTH(&STR(&MSG2)) > 0 -
                THEN -
                    SEND &MSG2,DELETE
              IF &LENGTH(&STR(&MSG3)) > 0 -
                THEN -
                    SEND &MSG3,DELETE
              DATA
              END
              ENDDATA
            END
          ELSE -
            IF &STR(&ULIST) = LIST -
              THEN -
                DO
                  ISPEXEC CONTROL DISPLAY LINE START(1)
                  OPER
                  SEND LIST
                  DATA
                  END
                  ENDDATA
                END
              ELSE -
                DO
                  SEND '&STR(&MSG1)' USER(&ULIST) LOGON
                  IF &LENGTH(&STR(&MSG2)) > 0 -
                    THEN -
                      DO
                        SET MSG2 = &STR(&MSG2)+
                          &SUBSTR(1:&EVAL(70-&LENGTH(&STR(&MSG2))),+
                          &BLANKS)
                        SEND '&STR(&MSG2)' USER(&ULIST) LOGON
                      END
                  IF &LENGTH(&STR(&MSG3)) > 0 -
                    THEN -
                      DO
                        SET MSG3 = &STR(&MSG3)+
                          &SUBSTR(1:&EVAL(70-&LENGTH(&STR(&MSG3))),+
                          &BLANKS)
                        SEND '&STR(&MSG3)' USER(&ULIST) LOGON
                      END
                END
GOTO DISP
END
/* J. KALINICH, 314-552-3232 */
/* SENDS MESSAGES TO OPERATOR OR TSO USERS */
