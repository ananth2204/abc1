* ------------------------------------------------------------------- *
*                                                                     *
*  Module name: OSDSCB                                                *
*                                                                     *
*  Many functions display general information about data sets         *
*  like the RECFM, LRECL, BLKSIZE.                                    *
*                                                                     *
*  This module processes this information putting the info into       *
*  SPF variables:                                                     *
*                                                                     *
*                                                                     *
*      SPF_DD                DD name                                  *
*      SPF_DSN               Data set name                            *
*      SPF_MEMBER            PDS or PDS/E member name                 *
*      SPF_CSECT             CSECT name (LMOD functions)              *
*      SPF_VOLSER            VOLSER                                   *
*      SPF_FORMAT            Format name                              *
*      SPF_PATH              HFS path name                            *
*      SPF_PATH_LENGTH       Length of path name                      *
*      SPF_REAL_PATH         Real path name                           *
*      SPF_REAL_PATH_LENGTH  Length of real path name                 *
*      SPF_RECORD_FROM       First record on display                  *
*      SPF_RECORD_TO         Last record on display                   *
*      SPF_COLUMN_FROM       First column on display                  *
*      SPF_COLUMN_TO         Last column on display                   *
*      SPF_DSORG             Data set organization                    *
*      SPF_RECFM             Record format                            *
*      SPF_LRECL             Logical record length                    *
*      SPF_BLKSIZE           Block size                               *
*      SPF_RKP               Relative key position                    *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
OSDSCB   CSECT
OSDSCB   AMODE 31
OSDSCB   RMODE ANY
         USING OSDSCB,R15
         USING OSCOMM,R12
         USING SESSION,R11
         B     INIT0000
MODID    DC    CL8'OSDSCB'
         DC    CL8'&SYSDATE'
         DC    CL8'&SYSTIME'
         DC    A(DSCBEND-OSDSCB)
INIT0000 DS    0H
         STM   R14,R12,12(R13)                SAVE REGISTERS
         LR    R10,R15                        COPY BASE ADDRESS
         DROP  R15
         USING OSDSCB,R10
         L     R15,COMM_DXD                   DXD AREA BASE ADDRESS
         A     R15,DXD_START                  PLUS OFFSET
         ST    R13,4(,R15)
         ST    R15,8(,R13)
         LR    R13,R15                        COPY DXD AREA ADDRESS
         USING DXDDSCB,R13                    DEFINE WORK AREA BASE
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY
         USING DS1FMTID,SESS_DSCB
         L     R9,COMM_OSSPFD
         USING OSSPFD,R9
* ------------------------------------------------------------------- *
*        Copy/build common panel heading data                         *
* ------------------------------------------------------------------- *
DSCB0000 DS    0H
         TM    SESS_KEYWORD_FLAGS,$KEYWORD_PATH
         BO    DSCB0010
         MVC   SPF_DD,SESS_DD                 COPY DD NAME
         MVC   SPF_DSN,SESS_DSN               COPY DSN
         MVC   SPF_MEMBER,SESS_MEMBER
         MVC   SPF_CSECT,SESS_DIALOG_SECTION
         MVC   SPF_FORMAT,SESS_FORMAT
         MVC   SPF_VOLSER,SESS_VOLSER
         B     DSCB0020
DSCB0010 DS    0H
         MVC   SPF_PATH,SESS_PATH
         MVC   SPF_REAL_PATH,SESS_REAL_PATH
         L     R1,SESS_PATH_LENGTH
         CVD   R1,COMM_DWORD
         MVC   WORK_WORK_1(L'EDIT_WORD_2),EDIT_WORD_2
         ED    WORK_WORK_1(L'EDIT_WORD_2),COMM_DWORD+5
         MVC   SPF_PATH_LENGTH,WORK_WORK_1+1
         L     R1,SESS_REAL_PATH_LENGTH
         CVD   R1,COMM_DWORD
         MVC   WORK_WORK_1(L'EDIT_WORD_2),EDIT_WORD_2
         ED    WORK_WORK_1(L'EDIT_WORD_2),COMM_DWORD+5
         MVC   SPF_REAL_PATH_LENGTH,WORK_WORK_1+1
DSCB0020 DS    0H
         TM    SESS_USER_OPTIONS,$OPTIONS_SHOW_TAGGED+$OPTIONS_SHOW_NOT+
               _TAGGED
         BZ    DSCB0030
         MVC   SPF_RECORD_FROM,N_A
         MVC   SPF_RECORD_TO,N_A
         B     DSCB0050
DSCB0030 DS    0H
         ICM   R1,15,DATASPACE_1_DISPLAY_FIRST_RECORD
         BNZ   DSCB0040
         LA    R1,1                           START WITH RECORD 1
DSCB0040 DS    0H
         CVD   R1,COMM_DWORD                  CONVERT TO DECIMAL
         MVC   WORK_WORK_1(L'EDIT_WORD_1),EDIT_WORD_1
         ED    WORK_WORK_1(L'EDIT_WORD_1),COMM_DWORD+3
         MVC   SPF_RECORD_FROM,WORK_WORK_1+1
         L     R1,DATASPACE_1_DISPLAY_LAST_RECORD
         CVD   R1,COMM_DWORD
         MVC   WORK_WORK_1(L'EDIT_WORD_1),EDIT_WORD_1
         ED    WORK_WORK_1(L'EDIT_WORD_1),COMM_DWORD+3
         MVC   SPF_RECORD_TO,WORK_WORK_1+1
DSCB0050 DS    0H
         TM    DS1SMSFG,DS1PDSEX              HFS?
         BO    DSCB0070
         TM    DS1SMSFG,DS1PDSE               PDS/E
         BO    DSCB0080
         TM    DS1DSORG+1,DS1ORGAM            VSAM?
         BO    DSCB0060
         TM    DS1DSORG,DS1DSGPS              SEQUENTIAL?
         BO    DSCB0090                       YES
         TM    DS1DSORG,DS1DSGDA              DIRECT ACCESS?
         BO    DSCB0100
         TM    DS1DSORG,DS1DSGPO              PDS
         BO    DSCB0110
         MVC   SPF_DSORG,=C'UNKN'             UNKNOWN
         B     DSCB0120
DSCB0060 DS    0H
         MVC   SPF_DSORG,=C'VSAM'
         B     DSCB0120
DSCB0070 DS    0H
         MVC   SPF_DSORG,=C'HFS '
         B     DSCB0120
DSCB0080 DS    0H
         MVC   SPF_DSORG,=C'PDSE'
         B     DSCB0120
DSCB0090 DS    0H
         MVC   SPF_DSORG,=C'PS  '
         TM    DS1DSORG,DS1DSGU               UNMOVABLE?
         BNO   DSCB0120                       NO
         MVI   SPF_DSORG+2,C'U'               SET 'UNMOVABLE'
         B     DSCB0120
DSCB0100 DS    0H
         MVC   SPF_DSORG,=C'DA  '
         TM    DS1DSORG,DS1DSGU               UNMOVABLE?
         BNO   DSCB0120                       NO
         MVI   SPF_DSORG+2,C'U'               SET 'UNMOVABLE'
         B     DSCB0120
DSCB0110 DS    0H
         MVC   SPF_DSORG,=C'PO  '
         TM    DS1DSORG,DS1DSGU               UNMOVABLE?
         BNO   DSCB0120                       NO
         MVI   SPF_DSORG+2,C'U'               SET 'UNMOVABLE'
         B     DSCB0120
DSCB0120 DS    0H
         LA    R8,SPF_RECFM
         MVC   SPF_RECFM,COMM_BLANKS
         TM    DS1RECFM,DS1RECFU              UNDEFINED?
         BO    DSCB0130
         TM    DS1RECFM,DS1RECFV              VARAIBLE?
         BO    DSCB0140
         TM    DS1RECFM,DS1RECFF              FIXED?
         BO    DSCB0150
         MVC   SPF_RECFM,=C'???'              UNKNOWN
         B     DSCB0190
DSCB0130 DS    0H
         MVI   0(R8),C'U'
         LA    R8,1(,R8)
         B     DSCB0170
DSCB0140 DS    0H
         MVI   0(R8),C'V'
         LA    R8,1(,R8)
         B     DSCB0160
DSCB0150 DS    0H
         MVI   0(R8),C'F'
         LA    R8,1(,R8)
DSCB0160 DS    0H
         TM    DS1RECFM,DS1RECFB              BLOCKED?
         BNO   DSCB0170                       NO
         MVI   0(R8),C'B'
         LA    R8,1(,R8)
DSCB0170 DS    0H
         TM    DS1RECFM,DS1RECFA              ASA CONTROL?
         BO    DSCB0180
         TM    DS1RECFM,DS1RECMC              MACHINE cONTROL?
         BNO   DSCB0190
         MVI   0(R8),C'A'
         LA    R8,1(,R8)
         B     DSCB0190
DSCB0180 DS    0H
         MVI   0(R8),C'M'
         LA    R8,1(,R8)
DSCB0190 DS    0H
         TM    DS1RECFM,DS1RECFS              SPANNED?
         BNO   DSCB0200                       NO
         MVI   0(R8),C'S'
DSCB0200 DS    0H
         LH    R1,DS1LRECL                    LRECL
         CVD   R1,COMM_DWORD
         MVC   WORK_WORK_1(L'EDIT_WORD_2),EDIT_WORD_2
         ED    WORK_WORK_1(L'EDIT_WORD_2),COMM_DWORD+5
         MVC   SPF_LRECL,WORK_WORK_1+1
         LH    R1,DS1BLKL                     BLOCK SIZE
         CVD   R1,COMM_DWORD
         MVC   WORK_WORK_1(L'EDIT_WORD_2),EDIT_WORD_2
         ED    WORK_WORK_1(L'EDIT_WORD_2),COMM_DWORD+5
         MVC   SPF_BLOCK_SIZE,WORK_WORK_1+1
         SR    R1,R1
         IC    R1,DS1KEYL                     KEY LENGTH
         CVD   R1,COMM_DWORD
         MVC   WORK_WORK_1(L'EDIT_WORD_2),EDIT_WORD_2
         ED    WORK_WORK_1(L'EDIT_WORD_2),COMM_DWORD+5
         MVC   SPF_KEY_LENGTH,WORK_WORK_1+1
         L     R1,DATASPACE_1_DISPLAY_FIRST_COLUMN
         CVD   R1,COMM_DWORD
         MVC   WORK_WORK_1(L'EDIT_WORD_2),EDIT_WORD_2
         ED    WORK_WORK_1(L'EDIT_WORD_2),COMM_DWORD+5
         MVC   SPF_COLUMN_FROM,WORK_WORK_1+1
         L     R1,DATASPACE_1_DISPLAY_LAST_COLUMN
         CVD   R1,COMM_DWORD
         MVC   WORK_WORK_1(L'EDIT_WORD_2),EDIT_WORD_2
         ED    WORK_WORK_1(L'EDIT_WORD_2),COMM_DWORD+5
         MVC   SPF_COLUMN_TO,WORK_WORK_1+1
         TM    SESS_DATA_TYPE,$DATA_TYPE_HFS  HFS?
         BNO   DSCB0210                       NO
         L     R1,DATASPACE_1_LONGEST_RECORD
         CVD   R1,COMM_DWORD
         MVC   WORK_WORK_1(L'EDIT_WORD_2),EDIT_WORD_2
         ED    WORK_WORK_1(L'EDIT_WORD_2),COMM_DWORD+5
         MVC   SPF_COLUMN_OF,WORK_WORK_1+1
DSCB0210 DS    0H
         MVC   SPF_ASCII,COMM_BLANKS
         TM    SESS_USER_OPTIONS,$OPTIONS_ASCII ASCII ON?
         BNO   DSCB0220                         NO
         MVC   SPF_ASCII,ASCII                  ACSII IS ON
DSCB0220 DS    0H
         TM    SESS_DATA_TYPE,$DATA_TYPE_VSAM VSAM?
         BNO   EXIT0000                       NO
* ------------------------------------------------------------------- *
*        EDIT VSAM LRECL, KEY LENGTH, RKP                             *
* ------------------------------------------------------------------- *
         ITRACE ID=VSAM
         L     R1,SESS_VSAM_LRECL
         CVD   R1,COMM_DWORD
         MVC   WORK_WORK_1(L'EDIT_WORD_2),EDIT_WORD_2
         ED    WORK_WORK_1(L'EDIT_WORD_2),COMM_DWORD+5
         MVC   SPF_LRECL,WORK_WORK_1+1
         L     R1,SESS_VSAM_KEY_LENGTH
         CVD   R1,COMM_DWORD
         MVC   WORK_WORK_1(L'EDIT_WORD_2),EDIT_WORD_2
         ED    WORK_WORK_1(L'EDIT_WORD_2),COMM_DWORD+5
         MVC   SPF_KEY_LENGTH,WORK_WORK_1+1
         L     R1,SESS_VSAM_RKP
         CVD   R1,COMM_DWORD
         MVC   WORK_WORK_1(L'EDIT_WORD_2),EDIT_WORD_2
         ED    WORK_WORK_1(L'EDIT_WORD_2),COMM_DWORD+5
         MVC   SPF_RKP,WORK_WORK_1+1
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=EXIT
         L     R13,4(,R13)                    RESTORE SAVE ADDRESS
         LM    R14,R12,12(R13)                RESTORE REGISTERS
         SR    R15,R15                        SET RC
         BR    R14                            ETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXD_START         DC      Q(DXDDSCB)

F1000000          DC      F'1000000'

EDIT_WORD_1       DC      X'40202020202020202120'
EDIT_WORD_2       DC      X'402020202120'
ASCII             DC      CL5'ASCII'
N_A               DC      CL9'N/A'
                  LTORG
DSCBEND           EQU     *
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDDSCB           DSECT
                  COPY    DXDPREF

WORK_WORK_1       DS      XL10
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         BPXYSTAT DSECT=YES,LIST=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         OSSPFD   TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
FORMAT_1 DSECT
         IBMMAC   VTOC=1
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY     REGEQU
         END      OSDSCB
