* ------------------------------------------------------------------- *
*                                                                     *
*  Module name: OSLMIDRB                                              *
*                                                                     *
*  Display IDRB (Binder identification) data                          *
*                                                                     *
* ------------------------------------------------------------------- *
OSLMIDRB CSECT
OSLMIDRB AMODE 31
OSLMIDRB RMODE ANY
         USING OSLMIDRB,R15
         USING OSCOMM,R12
         USING SESSION,R11
         B     INIT0000
MODID    DC    CL8'OSLMIDRB'
         DC    CL8'&SYSDATE'
         DC    CL8'&SYSTIME'
         DC    A(IDRBEND-OSLMIDRB)
INIT0000 DS    0H
         STM   R14,R12,12(R13)                SAVE REGS
         LR    R10,R15
         DROP  R15
         USING OSLMIDRB,R10
         L     R5,COMM_OSSPFD
         USING OSSPFD,R5
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         CLI   SESS_FORMAT_FLAGS,$FORMAT_INITIALIZE
         BNE   INIT0010                       NO
         LA    R0,OSLMIDRB_DXD_L              WORK AREA LENGTH
         ST    R0,SESS_DXD_LENGTH             SAVE LENGTH
         GETMAIN RU,                                                   +
               LV=(0),                                                 +
               LOC=ANY
         ST    R1,SESS_DXD_ADDR               SAVE ADDRESS
         LR    R2,R1
         LR    R0,R1
         LA    R1,OSLMIDRB_DXD_L
         SR    R14,R14
         SR    R15,R15
         MVCL  R0,R14                         INITIALIZE WORK AREA
         ST    R13,4(,R2)
         ST    R2,8(,R13)
         LR    R13,R2                         SET WORK AREA ADDRESS
         USING OSLMIDRB_DXD,R13               DEFINE BASE
         MVC   DXD_CSECT,MODID                SET MODULE ID
         ITRACE ID=ENTRY1
         LA    R1,DATASPACE_1
         ST    R1,DSPCREQ_DATASPACE
         LA    R1,IDRB_DATA
         ST    R1,DSPCREQ_RECORD_ADDR
         MVC   DSPCREQ_RECORD_NBR,DATASPACE_1_DISPLAY_FIRST_RECORD
         LA    R1,PANEL_NAME                  PANEL NAME
         MVI   COMM_VDATA_FUNC,$VDATA_GETMAIN SET FUNCTION
         L     R15,COMM_V_OSVDATA
         BALR  R14,R15                        GETMAIN VDATA
         ST    R0,DXD_VDATA_LENGTH            SAVE LENGTH
         ST    R1,DXD_VDATA_ADDR              SAVE ADDRESS
         MVC   DXD_VDEPTH,SPF_VDEPTH          SAVE DEPTH
         MVC   DXD_VWIDTH,SPF_VWIDTH          SAVE WIDTH
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
INIT0010 DS    0H
         L     R1,SESS_DXD_ADDR               WORK AREA ADDRESS
         ST    R13,4(,R1)
         ST    R1,8(,R13)
         LR    R13,R1
         ITRACE ID=ENTRY2
         CLI   SESS_FORMAT_FLAGS,$FORMAT_CONTROL
         BE    EXIT0000
         CLI   SESS_FORMAT_FLAGS,$FORMAT_CLEANUP
         BE    CLEAN000
* ------------------------------------------------------------------- *
*                   DATA STORED, DISPLAY IT                           *
* ------------------------------------------------------------------- *
IDRB0000 DS    0H
         L     R0,DXD_VDATA_ADDR
         L     R1,DXD_VDATA_LENGTH
         SR    R14,R14
         SR    R15,R15
         MVCL  R0,R14
         NI    DXD_FLAGS,255-$HEADING
         MVC   DSPCREQ_RECORD_NBR,DATASPACE_1_DISPLAY_FIRST_RECORD
         L     R4,DXD_VDATA_ADDR
         L     R3,DXD_VDEPTH
IDRB0020 DS    0H
         ITRACE ID=RETRIEVE,                                           +
               DATA1=(DSPCREQ_RECORD_NBR,4)
         MVI   DSPCREQ_FUNC,$DSPCREQ_RETRIEVE
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE
         BALR  R14,R15
         CLI   DSPCREQ_RC,$DSPCREQ_OK
         BNE   ERR0010
         TM    DXD_FLAGS,$HEADING          HEADING ISSUED?
         BO    IDRB0030                    YES
         OI    DXD_FLAGS,$HEADING          SET FLAG
         MVC   0(HEADING_L,R4),HEADING     COPY HEADING
         A     R4,DXD_VWIDTH               NEXT LINE
         BCTR  R3,0                        ACCOUNT FOR LINE USED
IDRB0030 DS    0H
         MVC   0(MSG01_L,R4),MSG01
         USING MSG01,R4
         MVC   MSG01_BINDER_ID+0(4),IDRB_BINDER_ID+0
         MVI   MSG01_BINDER_ID+4,C'-'
         MVC   MSG01_BINDER_ID+5(3),IDRB_BINDER_ID+4
         MVC   MSG01_BINDER_ID+9(3),IDRB_BINDER_ID+7
         MVC   MSG01_VERSION,IDRB_VERSION
         MVC   MSG01_RELEASE,IDRB_RELEASE
         MVC   MSG01_DATE_BOUND+0(4),IDRB_DATE_BOUND+0
         MVI   MSG01_DATE_BOUND+4,C'.'
         MVC   MSG01_DATE_BOUND+5(3),IDRB_DATE_BOUND+4
         MVC   MSG01_TIME_BOUND+0(2),IDRB_TIME_BOUND+0
         MVI   MSG01_TIME_BOUND+2,C':'
         MVC   MSG01_TIME_BOUND+3(2),IDRB_TIME_BOUND+2
         MVI   MSG01_TIME_BOUND+5,C':'
         MVC   MSG01_TIME_BOUND+6(2),IDRB_TIME_BOUND+4
         UNPK  MSG01_SIZE(9),IDRB_MODULE_SIZE(5)
         TR    MSG01_SIZE,COMM_HEXCHAR
         MVI   MSG01_SIZE+8,C' '
         MVC   MSG01_CALLERID,IDRB_CALLERID
         A     R4,DXD_VWIDTH
         BCT   R3,IDRB0040
         B     DISP0000
IDRB0040 DS    0H
         LA    R0,1                        CONSTANT 1
         A     R0,DSPCREQ_RECORD_NBR       PLUS CURRENT RECORD NBR
         C     R0,DATASPACE_1_LAST_RECORD  BEYOND HIGH RECORD?
         BH    DISP0000                    YES
         ST    R0,DSPCREQ_RECORD_NBR       UPDATE RECORD NBR
         B     IDRB0020
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0010  DS    0H
         MVC   SPF_MSG_1,COMM_MSG_1
         MVC   SPF_MSG_2,COMM_MSG_2
         MVC   SPF_MSG_3,COMM_MSG_3
         MVC   SPF_MSG_4,COMM_MSG_4
         MVC   SPF_MSG_5,COMM_MSG_5
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DISP0000 DS    0H
         ITRACE ID=DISPLAY
         MVC   SESS_DISP_PANEL,PANEL_NAME
         MVC   SESS_DISP_VDATA_ADDR,DXD_VDATA_ADDR
         MVC   SESS_DISP_VDATA_LENGTH,DXD_VDATA_LENGTH
         XC    SESS_DISP_CURSOR,SESS_DISP_CURSOR
         L     R15,COMM_V_OSDISP           ISPLINK ENTRY POINT
         BALR  R14,R15                     DISPLAY
         ITRACE ID=DISP_RC,                                            +
               DATA1=(SESS_RC,2),                                      +
               DATA2=(SESS_STATUS,1)
         CLC   COMM_NEW_FORMAT,COMM_BLANKS NEW FORMAT WANTED?
         BNE   EXIT0000                    YES
         CLI   COMM_SESS_FUNC,0            SESSION FUNCTION?
         BNE   EXIT0000                    YES
         OC    SESS_RC,SESS_RC             RC=0?
         BZ    DISP0010                    YES
         ITRACE ID=ENDING
         MVI   COMM_SESS_FUNC,$SESS_REMOVE SET FUNCTION
         B     EXIT0000                    EXIT
* ------------------------------------------------------------------- *
*                                                                     *
*        Check for scroll command(s)                                  *
*                                                                     *
*        Note that 'up', 'down', 'top', and 'bottom' are the only     *
*        valid scroll commands.                                       *
*                                                                     *
* ------------------------------------------------------------------- *
DISP0010 DS    0H
         ITRACE ID=ZVERB,                                              +
               DATA1=SPF_ZVERB,                                        +
               DATA2=SPF_ZSCROLLA
         CLI   SPF_ZVERB,C'B'              BOTTOM?
         BE    DISP0100                    YES
         CLI   SPF_ZVERB,C'D'              DOWN?
         BE    DISP0200                    YES
         CLI   SPF_ZVERB,C'T'              TOP?
         BE    DISP0300                    YES
         CLI   SPF_ZVERB,C'U'              UP?
         BE    DISP0400                    YES
         B     DISP0000

DISP0100 DS    0H
         ITRACE ID=BOTTOM
         MVC   DATASPACE_1_DISPLAY_FIRST_RECORD,DATASPACE_1_LAST_RECORD
         B     IDRB0000
DISP0200 DS    0H
         ITRACE ID=DOWN,                                               +
               DATA1=SPF_ZSCROLLA,                                     +
               DATA2=(SPF_ZSCROLLN,4)
         CLI   SPF_ZSCROLLA,C'M'                   MAX?
         BE    DISP0100                            YES.. SAME AS BOTTOM
         L     R1,SPF_ZSCROLLN                     LINES TO SCROLL
         A     R1,DATASPACE_1_DISPLAY_FIRST_RECORD PLUS CURRENT RECORD
         C     R1,DATASPACE_1_LAST_RECORD          BEYOND HIGH RECORD?
         BH    DISP0100                            YES.. GO TO BOTTOM
         ST    R1,DATASPACE_1_DISPLAY_FIRST_RECORD UPDATE RECORD NUMBER
         B     IDRB0000                            GO BUILD DATA
DISP0300 DS    0H
         ITRACE ID=TOP
         MVC   DATASPACE_1_DISPLAY_FIRST_RECORD,F1
         B     IDRB0000
DISP0400 DS    0H
         ITRACE ID=UP,                                                 +
               DATA1=SPF_ZSCROLLA,                                     +
               DATA2=(SPF_ZSCROLLN,4)
         CLI   SPF_ZSCROLLA,C'M'                   MAX?
         BE    DISP0300                            YES.. GO TO TOP
         L     R1,DATASPACE_1_DISPLAY_FIRST_RECORD
         S     R1,SPF_ZSCROLLN                     MINUS SCROLL
         C     R1,F1                               BELOW 1?
         BL    DISP0300                            YES, GO TO TOP
         ST    R1,DATASPACE_1_DISPLAY_FIRST_RECORD UPDATE RECORD NUMBER
         B     IDRB0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
CLEAN000 DS    0H
         ITRACE ID=CLEAN_UP,                                           +
               DATA1=(DXD_VDATA_ADDR,4),                               +
               DATA2=(DXD_VDATA_LENGTH,4)
         ICM   R1,15,DXD_VDATA_ADDR         VDATA GETMAINED?
         BZ    CLEAN010                     NO
         L     R0,DXD_VDATA_LENGTH          VDATA LENGTH
         MVI   COMM_VDATA_FUNC,$VDATA_FREEMAIN
         L     R15,COMM_V_OSVDATA
         BALR  R14,R15                      FREE VDATA
         XC    DXD_VDATA_ADDR,DXD_VDATA_ADDR
         XC    DXD_VDATA_LENGTH,DXD_VDATA_LENGTH
CLEAN010 DS    0H
         LA    R0,OSLMIDRB_DXD_L            WORK AREA SIZE
         LR    R1,R13                       COPY WORK AREA ADDRESS
         L     R13,4(,R13)                  RESTORE R13
         FREEMAIN RU,                       FREE WORK AREA             +
               A=(1),                                                  +
               LV=(0)
         XC    SESS_DXD_ADDR,SESS_DXD_ADDR
         XC    SESS_DXD_LENGTH,SESS_DXD_LENGTH
         B     EXIT0010
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=EXIT
         L     R13,4(,R13)           RESTORE SAVE AREA ADDRESS
EXIT0010 DS    0H
         LM    R14,R12,12(R13)       RESTORE REMAINING REGISTERS
         SR    R15,R15
         BR    R14                   RETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
F1                DC   F'1'

PANEL_NAME        DC   CL8'OSLMIDRB'

                  LTORG

HEADING           DS   0C
                  DC   AL1($SCREEN_ATTR_HIGH_TURQUOISE)
                  DC   C'Binder ID    Ver  Rel   Date     Time'
                  DC   C'     Size   Caller ID'
HEADING_L         EQU  *-HEADING

MSG01             DS   0C
                  DC   AL1($SCREEN_ATTR_NORMAL)
MSG01_BINDER_ID   DC   CL12' '
                  DC   C' '
MSG01_VERSION     DC   CL2' '
                  DC   C'   '
MSG01_RELEASE     DC   CL2' '
                  DC   C'  '
MSG01_DATE_BOUND  DC   CL8' '
                  DC   C' '
MSG01_TIME_BOUND  DC   CL8' '
                  DC   C' '
MSG01_SIZE        DC   CL8' '
                  DC   C' '
MSG01_CALLERID    DC   CL30' '
MSG01_L           EQU  *-MSG01

IDRBEND           EQU  *
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
OSLMIDRB_DXD      DSECT
                  COPY  DXDPREF

DXD_VDATA_ADDR    DS    A
DXD_VDATA_LENGTH  DS    F

DXD_VDEPTH        DS    F
DXD_VWIDTH        DS    F

DXD_FLAGS         DS    X
$HEADING          EQU   X'80'

                  DSPCREQ  DSECT=NO
                  COPY  IDRBDATA
OSLMIDRB_DXD_L    EQU   *-OSLMIDRB_DXD
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         SESSION
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         BPXYSTAT  DSECT=YES,LIST=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         OSSPFD    TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY      ATTRS
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY      TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY      REGEQU
         END       OSLMIDRB
