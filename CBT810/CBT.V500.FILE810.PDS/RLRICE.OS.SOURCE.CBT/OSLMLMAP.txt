* ------------------------------------------------------------------- *
*                                                                     *
*  Module name: OSLMLMAP                                              *
*                                                                     *
*  Display LMOD map                                                   *
*                                                                     *
* ------------------------------------------------------------------- *
OSLMLMAP CSECT
OSLMLMAP AMODE 31
OSLMLMAP RMODE ANY
         USING OSLMLMAP,R15
         USING OSCOMM,R12
         USING SESSION,R11
         B     INIT0000
MODID    DC    CL8'OSLMLMAP'
         DC    CL8'&SYSDATE'
         DC    CL8'&SYSTIME'
         DC    A(OSLMLMAPEND-OSLMLMAP)
INIT0000 DS    0H
         STM   R14,R12,12(R13)                SAVE REGISTERS
         LR    R10,R15                        COPY BASE ADDRESS
         LA    R9,2048(,R10)
         LA    R9,2048(,R9)
         USING OSLMLMAP,R10,R9
         DROP  R15
         L     R5,COMM_OSSPFD
         USING OSSPFD,R5
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         CLI   SESS_FORMAT_FLAGS,$FORMAT_INITIALIZE
         BNE   INIT0010                       NO
         LA    R0,OSLMLMAP_DXD_L              WORK AREA LENGTH
         ST    R0,SESS_DXD_LENGTH             SAVE LENGTH
         GETMAIN RU,                                                   +
               LV=(0),                                                 +
               LOC=ANY
         ST    R1,SESS_DXD_ADDR               SAVE ADDRESS
         LR    R2,R1
         LR    R0,R1
         LA    R1,OSLMLMAP_DXD_L
         SR    R14,R14
         SR    R15,R15
         MVCL  R0,R14                         INITIALIZE WORK AREA
         ST    R13,4(,R2)
         ST    R2,8(,R13)
         LR    R13,R2                         SET WORK AREA ADDRESS
         USING OSLMLMAP_DXD,R13               DEFINE BASE
         MVC   DXD_CSECT,MODID                SET MODULE ID
         ITRACE ID=ENTRY1
         MVI   DXD_FLAGS,0
         LA    R1,DATASPACE_1
         ST    R1,DSPCREQ_DATASPACE
         LA    R1,LMOD_MAP_DATA
         ST    R1,DSPCREQ_RECORD_ADDR
         MVC   DSPCREQ_RECORD_NBR,DATASPACE_1_DISPLAY_FIRST_RECORD
         LA    R1,PANEL_NAME                  PANEL NAME
         MVI   COMM_VDATA_FUNC,$VDATA_GETMAIN SET FUNCTION
         L     R15,COMM_V_OSVDATA
         BALR  R14,R15                        GETMAIN VDATA
         ST    R0,DXD_VDATA_LENGTH            SAVE LENGTH
         ST    R1,DXD_VDATA_ADDR              SAVE ADDRESS
         MVC   DXD_VDEPTH,SPF_VDEPTH          SAVE DEPTH
         MVC   DXD_VWIDTH,SPF_VWIDTH          SAVE WIDTH
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
INIT0010 DS    0H
         L     R1,SESS_DXD_ADDR               WORK AREA ADDRESS
         ST    R13,4(,R1)
         ST    R1,8(,R13)
         LR    R13,R1
         ITRACE ID=ENTRY2
         CLI   SESS_FORMAT_FLAGS,$FORMAT_CONTROL
         BE    EXIT0000
         CLI   SESS_FORMAT_FLAGS,$FORMAT_CLEANUP
         BE    CLEAN000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAP0000  DS    0H
         L     R0,DXD_VDATA_ADDR
         L     R1,DXD_VDATA_LENGTH
         SR    R14,R14
         SR    R15,R15
         MVCL  R0,R14
         MVC   DSPCREQ_RECORD_NBR,DATASPACE_1_DISPLAY_FIRST_RECORD
         NI    DXD_FLAGS,255-$HEADING
         L     R4,DXD_VDATA_ADDR
         L     R3,DXD_VDEPTH
         ITRACE ID=BUILD,                                              +
               RDATA1=R4,                                              +
               RDATA2=R3
MAP0020  DS    0H
         ITRACE ID=RETRIEVE,                                           +
               DATA1=(DSPCREQ_RECORD_NBR,4)
         MVI   DSPCREQ_FUNC,$DSPCREQ_RETRIEVE
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE
         BALR  R14,R15
         ITRACE ID=DSPC_RC,                                            +
               DATA1=(DSPCREQ_RC,1)
         CLI   DSPCREQ_RC,$DSPCREQ_OK
         BNE   ERR0010
         CLI   LMOD_MAP_TYPE,C'S'             SECTION WITHIN CLASS?
         BNE   MAP0040                        NO
         CLI   LMOD_MAP_NAME_SPACE,X'64'      SPACE X'64'?
         BNE   MAP0040                        NO
         CLC   LMOD_MAP_RECFM,X0001           RECORD FORMAT 1?
         BNE   MAP0040                        NO
         TM    DXD_FLAGS,$HEADING             HEADING DONE?
         BO    MAP0025                        YES
         OI    DXD_FLAGS,$HEADING             SET FLAG
         MVC   0(HEADING_L,R4),HEADING
         A     R4,DXD_VWIDTH                  NEXT LINE
         BCTR  R3,0                           MINUS 1 AVAILABLE
MAP0025  DS    0H
         MVC   0(MSG01_L,R4),MSG01
         USING MSG01,R4
         MVC   MSG01_NAME,LMOD_MAP_NAME       COPY NAME
         UNPK  MSG01_OFFSET(9),LMOD_MAP_OFFSET(5)
         TR    MSG01_OFFSET,COMM_HEXCHAR
         MVI   MSG01_OFFSET+8,C' '            RESTORE BLANK
         UNPK  MSG01_LENGTH(9),LMOD_MAP_QUANTITY(5)
         TR    MSG01_LENGTH,COMM_HEXCHAR
         MVI   MSG01_LENGTH+8,C' '            RESTORE BLANK
MAP0030  DS    0H
         DROP  R4
         A     R4,DXD_VWIDTH                  NEXT LINE
         BCT   R3,MAP0040
         B     DISP0000                       SCREEN FULL
MAP0040  DS    0H
         L     R1,DSPCREQ_RECORD_NBR          CURRENT RECORD
         C     R1,DATASPACE_1_LAST_RECORD     AT HIGH RECORD?
         BE    DISP0000                       YES
         LA    R1,1(,R1)                      PLUS 1
         ST    R1,DSPCREQ_RECORD_NBR          UPDATE RECORD NBR
         B     MAP0020
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0010  DS    0H
         ITRACE ID=ERR0010
         MVC   SPF_MSG_1,COMM_MSG_1
         MVC   SPF_MSG_2,COMM_MSG_2
         MVC   SPF_MSG_3,COMM_MSG_3
         MVC   SPF_MSG_4,COMM_MSG_4
         MVC   SPF_MSG_5,COMM_MSG_5
         MVI   COMM_SESS_FUNC,$SESS_REMOVE
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DISP0000 DS    0H
         ITRACE ID=DISPLAY
         MVC   SESS_DISP_PANEL,PANEL_NAME
         MVC   SESS_DISP_VDATA_ADDR,DXD_VDATA_ADDR
         MVC   SESS_DISP_VDATA_LENGTH,DXD_VDATA_LENGTH
         XC    SESS_DISP_CURSOR,SESS_DISP_CURSOR
         L     R15,COMM_V_OSDISP
         BALR  R14,R15
         ITRACE ID=DISP_RC,                                            +
               DATA1=(SESS_RC,2),                                      +
               DATA2=(SESS_STATUS,1)
         CLC   COMM_NEW_FORMAT,COMM_BLANKS NEW FORMAT SELECTED?
         BNE   EXIT0000                    YES
         CLI   COMM_SESS_FUNC,0            SESSION FUNCTION?
         BNE   EXIT0000                    YES
         OC    SESS_RC,SESS_RC             RC=0?
         BZ    DISP0010                    YES
         ITRACE ID=ENDING
         MVI   COMM_SESS_FUNC,$SESS_REMOVE SET FUNCTION
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
*        Check for scroll command(s)                                  *
*                                                                     *
*        Note that 'up', 'down', 'top', and 'bottom' are the only     *
*        valid scroll commands.                                       *
*                                                                     *
* ------------------------------------------------------------------- *
DISP0010 DS    0H
         CLI   SPF_ZSCROLLA,C'C'           CSR?
         BNE   DISP0020                    NO
DISP0020 DS    0H
         ITRACE ID=ZVERB,                                              +
               DATA1=SPF_ZVERB,                                        +
               DATA2=SPF_ZSCROLLA
         CLI   SPF_ZVERB,C'B'              BOTTOM?
         BE    DISP0100                    YES
         CLI   SPF_ZVERB,C'D'              DOWN?
         BE    DISP0200                    YES
         CLI   SPF_ZVERB,C'T'              TOP?
         BE    DISP0300                    YES
         CLI   SPF_ZVERB,C'U'              UP?
         BE    DISP0400                    YES
         B     DISP0000

DISP0100 DS    0H
         ITRACE ID=BOTTOM
         MVC   DATASPACE_1_DISPLAY_FIRST_RECORD,DATASPACE_1_LAST_RECORD
         B     MAP0000
DISP0200 DS    0H
         ITRACE ID=DOWN,                                               +
               DATA1=SPF_ZSCROLLA,                                     +
               DATA2=(SPF_ZSCROLLN,4)
         CLI   SPF_ZSCROLLA,C'M'                   MAX?
         BE    DISP0100                            YES.. SAME AS BOTTOM
         L     R1,SPF_ZSCROLLN                     NUMBER TO SCROLL
         A     R1,DATASPACE_1_DISPLAY_FIRST_RECORD PLUS CURRENT RECORD
         C     R1,DATASPACE_1_LAST_RECORD          BEYOND HIGH RECORD?
         BH    DISP0100                            YES.. GO TO BOTTOM
         ST    R1,DATASPACE_1_DISPLAY_FIRST_RECORD UPDATE RECORD NUMBER
         B     MAP0000                             GO BUILD DATA
DISP0300 DS    0H
         ITRACE ID=TOP
         MVC   DATASPACE_1_DISPLAY_FIRST_RECORD,F1
         B     MAP0000
DISP0400 DS    0H
         ITRACE ID=UP,                                                 +
               DATA1=SPF_ZSCROLLA,                                     +
               DATA2=(SPF_ZSCROLLN,4)
         CLI   SPF_ZSCROLLA,C'M'                   MAX?
         BE    DISP0300                            YES.. GO TO TOP
         L     R1,DATASPACE_1_DISPLAY_FIRST_RECORD
         S     R1,SPF_ZSCROLLN                     MINUS SCROLL
         C     R1,F1                               BELOW 1?
         BL    DISP0300                            YES, GO TO TOP
         ST    R1,DATASPACE_1_DISPLAY_FIRST_RECORD UPDATE RECORD NUMBER
         B     MAP0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
CLEAN000 DS    0H
         ITRACE ID=CLEAN_UP,                                           +
               DATA1=(DXD_VDATA_ADDR,4),                               +
               DATA2=(DXD_VDATA_LENGTH,4)
         ICM   R1,15,DXD_VDATA_ADDR                VDATA GETMAINED?
         BZ    CLEAN010                            NO
         L     R0,DXD_VDATA_LENGTH                 VDATA LENGTH
         MVI   COMM_VDATA_FUNC,$VDATA_FREEMAIN
         L     R15,COMM_V_OSVDATA
         BALR  R14,R15                             FREE VDATA
         XC    DXD_VDATA_ADDR,DXD_VDATA_ADDR
         XC    DXD_VDATA_LENGTH,DXD_VDATA_LENGTH
CLEAN010 DS    0H
         LA    R0,OSLMLMAP_DXD_L                   WORK AREA SIZE
         LR    R1,R13                              COPY ADDRESS
         L     R13,4(,R13)                         RESTORE R13
         FREEMAIN RU,                              FREE WORK AREA      +
               A=(1),                                                  +
               LV=(0)
         XC    SESS_DXD_ADDR,SESS_DXD_ADDR
         XC    SESS_DXD_LENGTH,SESS_DXD_LENGTH
         SR    R15,R15
         B     EXIT0010
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=EXIT
         L     R13,4(,R13)                         RESTORE R13
EXIT0010 DS    0H
         LM    R14,R12,12(R13)                     RESTORE REGISTERS
         SR    R15,R15                             SET RC
         BR    R14                                 RETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
F1                DC   F'1'
X0001             DC   X'0001'
PANEL_NAME        DC   CL8'OSLMAP'

                  LTORG

HEADING           DS   0C
                  DC   AL1($SCREEN_ATTR_HIGH_TURQUOISE)
                  DC   CL8'NAME'
                  DC   CL4' '
                  DC   CL8' OFFSET '
                  DC   CL4' '
                  DC   CL8' LENGTH '
HEADING_L         EQU  *-HEADING

MSG01             DS   0C
                  DC   AL1($SCREEN_ATTR_HIGH)
MSG01_NAME        DC   CL8' '
                  DC   AL1($SCREEN_ATTR_NORMAL)
                  DC   CL3' '
MSG01_OFFSET      DC   CL8' '
                  DC   CL4' '
MSG01_LENGTH      DC   CL8' '
                  DC   C' '
MSG01_L                EQU  *-MSG01


OSLMLMAPEND       EQU  *
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
OSLMLMAP_DXD      DSECT
                  COPY  DXDPREF

DXD_VDATA_ADDR    DS    A
DXD_VDATA_LENGTH  DS    F

DXD_VDEPTH        DS    F
DXD_VWIDTH        DS    F

DXD_FLAGS         DS    X
$HEADING          EQU   X'80'

                  COPY  MAPDATA

                  DSPCREQ  DSECT=NO
OSLMLMAP_DXD_L    EQU   *-OSLMLMAP_DXD
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         SESSION
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         BPXYSTAT  DSECT=YES,LIST=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         OSSPFD    TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY      ATTRS
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY      TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY      REGEQU
         END       OSLMLMAP
