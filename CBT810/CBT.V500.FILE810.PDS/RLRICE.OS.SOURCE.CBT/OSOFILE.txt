* ------------------------------------------------------------------- *
*                                                                     *
*       Obtain output file info                                       *
*                                                                     *
* ------------------------------------------------------------------- *
OSOFILE  CSECT
OSOFILE  AMODE 31
OSOFILE  RMODE ANY
         USING OSOFILE,R15
         USING OSCOMM,R12
         USING SESSION,R11
         B     INIT0000
MODID    DC    CL8'OSOFILE'
         DC    CL8'&SYSDATE'
         DC    CL8'&SYSTIME'
         DC    A(OFILEEND-OSOFILE)
INIT0000 DS    0H
         STM   R14,R12,12(R13)                SAVE REGISTERS
         LR    R10,R15                        COPY BASE ADDRESS
         DROP  R15
         USING OSOFILE,R10
         L     R15,COMM_DXD                   DXD AREA BASE ADDRESS
         A     R15,DXD_START                  PLUS OFFSET
         ST    R13,4(,R15)
         ST    R15,8(,R13)
         LR    R13,R15                        COPY DXD AREA ADDRESS
         USING DXDOFILE,R13                   DEFINE WORK AREA BASE
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY
         MVC   SESS_DISP_PANEL,0(R1)          COPY PANEL NAME
         L     R9,COMM_OSSPFD                 SPF DATA
         USING OSSPFD,R9                      DEFINE BASE
         MVC   COMM_MSG_CSECT,MODID           COPY MODULE NAME
MAIN0010 DS    0H
         LA    R1,VPUT                        COMMAND NAME
         ST    R1,DXD_SPF_1                   SET ADDRESS
         LA    R1,VAR_NAMES                   LIST OF NAMES
         ST    R1,DXD_SPF_2                   SET ADDRESS
         OI    DXD_SPF_2,X'80'                FLAG END-OF-LIST
         LA    R1,DXD_SPF_PARMS               SPF PARM LIST
         ITRACE ID=VPUT,                                               +
               RDATA1=R1
         L     R15,COMM_ISPLINK               ISPLINK ENTRY POINT
         BALR  R14,R15                        LINK TO SPF
         ITRACE ID=VPUT_RC,                                            +
               RDATA1=R15
MAIN0020 DS    0H
         LA    R1,DISPLAY                     COMMAND NAME
         ST    R1,DXD_SPF_1                   SET ADDRESS
         LA    R1,SESS_DISP_PANEL             PANEL NAME
         ST    R1,DXD_SPF_2                   SET ADDRESS
         OI    DXD_SPF_2,X'80'                FLAG END-OF-LIST
         LA    R1,DXD_SPF_PARMS               SPF PARM LIST
         ITRACE ID=DISPLAY,                                            +
               RDATA1=R1
         L     R15,COMM_ISPLINK               ISPLINK ENTRY POINT
         BALR  R14,R15                        LINK TO SPF
         ITRACE ID=DISP_RC,                                            +
               RDATA1=R15
         LTR   R15,R15                        RC=0?
         BNZ   EXIT0004                       NO
         LA    R1,VGET
         ST    R1,DXD_SPF_1
         LA    R1,NAME_LIST
         ST    R1,DXD_SPF_2
         OI    DXD_SPF_2,X'80'
         LA    R1,DXD_SPF_PARMS
         L     R15,COMM_ISPLINK
         BALR  R14,R15
         ITRACE ID=VGET_RC,                                            +
               RDATA1=R15
         OC    SPF_ZCMD,COMM_BLANKS           FORCE UPPER CASE
         ITRACE ID=ZCMD,                                               +
               DATA1=SPF_ZCMD+0,                                       +
               DATA2=SPF_ZCMD+8
         CLC   ABEND,SPF_ZCMD                 BLOW YOURSELF UP?
         BE    ABEND000                       WELL, THEY ASKED FOR IT
         CLC   =C'??',SPF_ZCMD                HELP?
         BE    HELP0000                       YES
         OC    SPF_OUTPUT_DSN,COMM_BLANKS
         OC    SPF_OUTPUT_DISP,COMM_BLANKS
         OC    SPF_OUTPUT_UNIT,COMM_BLANKS
         OC    SPF_OUTPUT_VOLSER,COMM_BLANKS
         OC    SPF_YES_OR_NO,COMM_BLANKS
         ITRACE ID=DSN,                                                +
               DATA1=SPF_OUTPUT_DSN+0,                                 +
               DATA2=SPF_OUTPUT_DSN+8
         ITRACE ID=DISP,                                               +
               DATA1=(SPF_OUTPUT_DISP,3)
         ITRACE ID=UNIT_VOL,                                           +
               DATA1=SPF_OUTPUT_UNIT,                                  +
               DATA2=SPF_OUTPUT_VOLSER

         CLC   SPF_OUTPUT_DSN,COMM_BLANKS     DATA SET NAME?
         BE    MAIN0030                       NO
         CLC   SPF_OUTPUT_PATH,COMM_BLANKS    PATH NAME?
         BNE   ERR0020                        YES.. CAN'T HAVE BOTH
         B     MAIN0040
MAIN0030 DS    0H
         CLC   SPF_OUTPUT_PATH,COMM_BLANKS    PATH NAME?
         BE    ERR0060                        NO.. NEED DSN OR PATH
         B     MAIN0050                       SKIP VOLSER FOR PATHS
MAIN0040 DS    0H
         CLC   SPF_OUTPUT_VOLSER,COMM_BLANKS
         BE    MAIN0050
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         ITRACE ID=VER_VOL
         XC    DXD_UCB_WORK,DXD_UCB_WORK      INITIALIZE COPY OF UCB
         MVC   DXD_UCBSCAN(UCBSCAN_L),UCBSCAN_I
         XC    DXD_UCB_WORK,DXD_UCB_WORK
         ITRACE ID=UCBSCAN
         UCBSCAN COPY,                                                 +
               WORKAREA=DXD_UCB_WORK,                                  +
               VOLSER=SESS_VOLSER,                                     +
               UCBAREA=DXD_UCB,                                        +
               DYNAMIC=YES,                                            +
               RANGE=ALL,                                              +
               MF=(E,DXD_UCBSCAN)
         ITRACE ID=UCB_RC,                                             +
               RDATA1=R15
         LTR   R15,R15                        VALID VOLSER?
         BZ    ERR0050                        NO
MAIN0050 DS    0H
         CLC   SESS_DISP_PANEL,OS0002         PANEL 2?
         BNE   EXIT0000                       NO
         CLC   SPF_OUTPUT_DISP,NEW            NEW?
         BE    MAIN0060                       YES
         CLC   SPF_OUTPUT_DISP,SHR            SHR?
         BE    EXIT0000                       YES
         CLC   SPF_OUTPUT_DISP,MOD            MOD?
         BE    EXIT0000                       YES
         MVI   COMM_MSG_ID+1,1                SET MESSAGE ID
         B     ERR0100
MAIN0060 DS    0H
         ITRACE ID=SPACE,                                              +
               DATA1=(SPF_OUTPUT_SPACE_PRI,5),                         +
               DATA2=(SPF_OUTPUT_SPACE_SEC,5)
         LA    R1,SPF_OUTPUT_SPACE_PRI
         LA    R2,L'SPF_OUTPUT_SPACE_PRI
MAIN0070 DS    0H
         CLI   0(R1),C'0'                     VALID DIGIT?
         BL    ERR0030                        NO
         CLI   0(R1),C'9'                     VALID DIGIT?
         BH    ERR0030                        NO
         LA    R1,1(,R1)                      NEXT DIGIT
         BCT   R2,MAIN0070                    LOOP
         LA    R1,SPF_OUTPUT_SPACE_SEC
         LA    R2,L'SPF_OUTPUT_SPACE_SEC
MAIN0080 DS    0H
         CLI   0(R1),C'0'                     VALID DIGIT?
         BL    ERR0040                        NO
         CLI   0(R1),C'9'                     VALID DIGIT?
         BH    ERR0040                        NO
         LA    R1,1(,R1)                      NEXT DIGIT
         BCT   R2,MAIN0080                    LOOP
         B     EXIT0000                       EXIT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
HELP0000 DS    0H
         ITRACE ID=HELP
         LA    R1,DISPLAY                     COMMAND NAME
         ST    R1,DXD_SPF_1                   SET ADDRESS
         LA    R1,HELP_PANEL                  PANEL NAME
         ST    R1,DXD_SPF_2                   SET ADDRESS
         OI    DXD_SPF_2,X'80'                FLAG END-OF-LIST
         LA    R1,DXD_SPF_PARMS               SPF PARM LIST
         ITRACE ID=DISPLAY,                                            +
               RDATA1=R1
         L     R15,COMM_ISPLINK               ISPLINK ENTRY POINT
         BALR  R14,R15                        LINK TO SPF
         B     MAIN0020
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ABEND000 DS    0H
         ITRACE ID=ABEND
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0020  DS    0H
         MVI   COMM_MSG_ID+1,2                OUTPUT DSN AND PATH
         B     ERR0100
ERR0030  DS    0H
         ITRACE ID=BAD_PRI
         MVI   COMM_MSG_ID+1,3                BAD DIGIT IN PRI QTY
         B     ERR0100
ERR0040  DS    0H
         ITRACE ID=BAD_SEC
         MVI   COMM_MSG_ID+1,4                BAD DIGIT IN SEC QTY
         B     ERR0100
ERR0050  DS    0H
         ITRACE ID=BAD_SEC
         MVI   COMM_MSG_ID+1,5                BAD DIGIT IN SEC QTY
         B     ERR0100
ERR0060  DS    0H
         MVI   COMM_MSG_ID+1,6                NEED DSN OR PATH
ERR0100  DS    0H
         L     R15,COMM_V_OSMSG               PROCESS MESSAGE
         BALR  R14,R15                        BUILD MESSAGE
         MVC   SPF_MSG_1,COMM_MSG_1           COPY MESSAGE
         MVC   SPF_MSG_2,COMM_MSG_2           COPY MESSAGE
         MVC   SPF_MSG_3,COMM_MSG_3           COPY MESSAGE
         MVC   SPF_MSG_4,COMM_MSG_4           COPY MESSAGE
         MVC   SPF_MSG_5,COMM_MSG_5           COPY MESSAGE
         B     MAIN0010                       DISPLAY THE PANEL AGAIN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         SR    R15,R15
         B     EXITEXIT
EXIT0004 DS    0H
         LA    R15,4
EXITEXIT DS    0H
         ITRACE ID=EXIT,                                               +
               RDATA1=R15
         L     R13,4(,R13)                    RESTORE SAVE AREA ADDRESS
         L     R14,12(,R13)
         LM    R0,R12,20(R13)                 RESTORE REGISTERS
         BR    R14                            RETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXD_START     DC    Q(DXDOFILE)

UCBSCAN_I     UCBSCAN MF=(L,ANYNAME)
UCBSCAN_L     EQU    *-UCBSCAN_I

ABEND         DC    C'ABEND '
DISPLAY       DC    CL8'DISPLAY'
HELP_PANEL    DC    CL8'SEPHELP'
VGET          DC    CL8'VGET'
VPUT          DC    CL8'VPUT'
NAME_LIST     DC    C'(ZSCROLLA ZSCROLLN ZVERB)'
VAR_NAMES     DC    C'(PODSN POD POUNIT POVOL POPRI POSEC PRL)'
OS0002        DC    CL8'OS0002'

NEW           DC    CL3'NEW'
SHR           DC    CL3'SHR'
MOD           DC    CL3'MOD'

              LTORG

OFILEEND      EQU  *
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDOFILE       DSECT
               COPY    DXDPREF

DXD_SPF_PARMS  DS  0A
DXD_SPF_1      DS  A
DXD_SPF_2      DS  A
DXD_SPF_3      DS  A
DXD_SPF_4      DS  A
DXD_SPF_5      DS  A

DXD_UCBSCAN    DS  0F,(UCBSCAN_L)X
DXD_UCB_WORK   DS  100X
DXD_UCB        DS  48X
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               BPXYSTAT DSECT=YES,LIST=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               OSSPFD TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY    TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY  REGEQU
         END   OSOFILE
