* ------------------------------------------------------------------- *
*                                                                     *
*  Module name: OSFIND                                                *
*                                                                     *
*                                                                     *
*  Process FIND command                                               *
*                                                                     *
*                                                                     *
*  Find is used by multiple functions.                                *
*  It is not used by all functins. .                                  *
*  Find strings are unique to each "session"                          *
*  Find needs to allow "repeated" (RFIND in SPF terms) finds          *
*                                                                     *
* ------------------------------------------------------------------- *
OSFIND   CSECT
OSFIND   AMODE 31
OSFIND   RMODE ANY
         USING OSCOMM,R12
         USING SESSION,R11
         USING OSFIND,R15
         B     INIT0000
MODID    DC    CL8'OSFIND'
         DC    CL8'&SYSDATE'
         DC    CL7'&SYSTIME'
INIT0000 DS    0H
         STM   R14,R12,12(R13)                SAVE REGS
         LR    R10,R15                        COPY ENTRY POINT
         DROP  R15
         USING OSFIND,R10                     DEFINE BASE
         L     R15,COMM_DXD                   WORK AREA BASE
         A     R15,DXD_START                  PLUS DISP
         ST    R13,4(,R15)                    SAVE R13
         ST    R15,8(,R13)                    CHAIN SAVE AREA
         LR    R13,R15
         USING DXDFIND,R13                    DEFINE WORK AREA BASE
         CLC   DXD_CSECT,MODID                INITIALIZED?
         BE    INIT0010                       YES
         MVC   DXD_CSECT,MODID
INIT0010 DS    0H
         ITRACE ID=ENTRY
         L     R8,COMM_OSSPFD
         USING OSSPFD,R8
         XC    DXD_RC,DXD_RC                  RESET RC
         CLC   COMM_OPERANDS_NBR,H2           CORRECT NBR OF OPERANDS?
         BNE   ERR0010                        NO
         ICM   R1,15,OPERAND_02_ADDRESS       STRING'S ADDRESS
         CLC   =C'? ',0(R1)                   WHAT'S THE STING?
         BE    FIND0020                       YES
         SR    R2,R2
         ICM   R2,3,OPERAND_02_LENGTH         OPERAND LENGTH
         TM    OPERAND_02_TYPE,$OPERAND_HEX   HEX?
         BO    FIND0010                       YES
* ------------------------------------------------------------------- *
*                                                                     *
*        Character strings                                            *
*                                                                     *
* ------------------------------------------------------------------- *
         NI    SESS_SEARCH_FLAGS,255-$SESS_SEARCH_HEX
         MVC   SESS_SEARCH_ARG,COMM_BLANKS    INITIALIZE SEARCH ARG
         STCM  R2,3,SESS_SEARCH_ARG_LENGTH    SAVE LENGTH
         BCTR  R2,0
         EX    R2,MVC01                       COPY SEARCH ARG
         ITRACE ID=CHAR,                                               +
               DATA1=SESS_SEARCH_ARG,                                  +
               DATA2=(SESS_SEARCH_ARG_LENGTH,2)
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
*        HEX strings                                                  *
*                                                                     *
* ------------------------------------------------------------------- *
FIND0010 DS    0H
         OI    SESS_SEARCH_FLAGS,$SESS_SEARCH_HEX
         LR    R3,R2                          COPY LENGTH
         BCTR  R2,0                           FOR EX
         MVC   COMM_INFO_01,COMM_BLANKS
         EX    R2,MVC02                       COPY DATA
         EX    R2,TR01                        TRANSLATE DATA
         XC    COMM_INFO_02,COMM_INFO_02      INITIALIZE OUTPUT
         PACK  COMM_INFO_02+00(5),COMM_INFO_01+00(9)
         PACK  COMM_INFO_02+04(5),COMM_INFO_01+08(9)
         PACK  COMM_INFO_02+08(5),COMM_INFO_01+16(9)
         PACK  COMM_INFO_02+12(5),COMM_INFO_01+24(9)
         PACK  COMM_INFO_02+16(5),COMM_INFO_01+32(9)
         SRL   R3,1                           DIVIDE LENGTH BY 2
         STH   R3,SESS_SEARCH_ARG_LENGTH      SET LENGTH
         BCTR  R3,0                           MINUS 1 FOR EX
         EX    R3,MVC03                       COPY SEARCH STRING
         ITRACE ID=HEX,                                                +
               DATA1=SESS_SEARCH_ARG,                                  +
               DATA2=(SESS_SEARCH_ARG_LENGTH,2)
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
FIND0020 DS    0H
         MVI   SPF_ZCMD,C'F'
         MVI   SPF_ZCMD+1,C' '
         TM    SESS_SEARCH_FLAGS,$SESS_SEARCH_HEX
         BO    FIND0030
         MVC   SPF_ZCMD+2(L'SPF_ZCMD-2),SESS_SEARCH_ARG
         MVI   DXD_RC+1,4
         B     EXIT0000
FIND0030 DS    0H
         UNPK  COMM_INFO_01+00(9),SESS_SEARCH_ARG+00(5)
         UNPK  COMM_INFO_01+08(9),SESS_SEARCH_ARG+04(5)
         UNPK  COMM_INFO_01+16(9),SESS_SEARCH_ARG+08(5)
         UNPK  COMM_INFO_01+24(9),SESS_SEARCH_ARG+12(5)
         UNPK  COMM_INFO_01+32(9),SESS_SEARCH_ARG+16(5)
         UNPK  COMM_INFO_01+40(9),SESS_SEARCH_ARG+20(5)
         UNPK  COMM_INFO_01+48(9),SESS_SEARCH_ARG+24(5)
         UNPK  COMM_INFO_01+56(9),SESS_SEARCH_ARG+28(5)
         UNPK  COMM_INFO_01+64(9),SESS_SEARCH_ARG+32(5)
         TR    COMM_INFO_01(64),COMM_HEXCHAR
         MVI   SPF_ZCMD+2,C'X'
         MVI   SPF_ZCMD+3,X'7D'
         SR    R2,R2
         ICM   R2,3,SESS_SEARCH_ARG_LENGTH
         SLL   R2,1                           MULTIPLY BY 2
         BCTR  R2,0
         EX    R2,MVC04
         LA    R3,SPF_ZCMD+5(R2)
         MVI   0(R3),X'7D'
         MVI   DXD_RC+1,4                     SET RC
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0010  DS    0H
         MVI   COMM_MSG_ID,0
         MVC   COMM_MSG_CSECT,MODID
         L     R15,COMM_V_OSMSG
         BALR  R14,R15
         MVC   SPF_MSG_1,COMM_MSG_1           COPY MESSAGE
         MVC   SPF_MSG_2,COMM_MSG_2           COPY MESSAGE
         MVC   SPF_MSG_3,COMM_MSG_3           COPY MESSAGE
         MVC   SPF_MSG_4,COMM_MSG_4           COPY MESSAGE
         MVC   SPF_MSG_5,COMM_MSG_5           COPY MESSAGE
         MVI   DXD_RC+1,8                     SET RC
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=EXIT
         LH    R15,DXD_RC                     RETURN CODE
         L     R13,4(,R13)                    RESTORE R13
         L     R14,12(,R13)                   RESTORE R14
         LM    R0,R12,20(R13)                 RESTORE REGISTERS
         BR    R14                            RETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MVC01    MVC   SESS_SEARCH_ARG(0),0(R1)
MVC02    MVC   COMM_INFO_01(0),0(R1)
TR01     TR    COMM_INFO_01(0),CHARHEX
MVC03    MVC   SESS_SEARCH_ARG(0),COMM_INFO_02
MVC04    MVC   SPF_ZCMD+4(0),COMM_INFO_01
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXD_START      DC       Q(DXDFIND)

H2             DC       H'2'

CHARHEX        DC       256C' '
               ORG      CHARHEX+C'A'
               DC       X'0A0B0C0D0E0F'
               ORG      CHARHEX+C'0'
               DC       X'00010203040506070809'
               ORG      CHARHEX+256

               LTORG

* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDFIND        DSECT
               COPY   DXDPREF

DXD_RC         DS     H

* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               BPXYSTAT DSECT=YES,LIST=NO
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               OSSPFD   TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY     OPERAND
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY     ATTRS
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY     REGEQU
               END      OSFIND
