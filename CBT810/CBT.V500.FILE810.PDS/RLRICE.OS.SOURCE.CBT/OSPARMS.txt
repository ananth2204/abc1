* ------------------------------------------------------------------- *
*                                                                     *
*        Process parms on invoking commands                           *
*                                                                     *
* ------------------------------------------------------------------- *
OSPARMS  CSECT
OSPARMS  AMODE 31
OSPARMS  RMODE ANY
         USING OSPARMS,R15                       DEFINE BASE
         USING OSCOMM,R12
         USING SESSION,R11
         B     INIT0000
MODID    DC    CL8'OSPARMS'                      MODULE NAME
         DC    CL8'&SYSDATE'                     DATE OF ASSEMBLY
         DC    CL8'&SYSTIME'                     TIME OF ASSEMBLY
         DC    A(PARMSEND-OSPARMS)
INIT0000 DS    0H
         STM   R14,R12,12(R13)                   SAVE ENTRY REGISTERS
         LR    R10,R15                           COPY BASE
         DROP  R15
         USING OSPARMS,R10
         LR    R9,R1
         USING PARMSREQ,R9
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         L     R15,COMM_DXD                      WORK AREA ADDRESS
         A     R15,DXD_START                     PLUS DISPLACEMENT
         ST    R13,4(,R15)
         ST    R15,8(,R13)
         LR    R13,R15
         USING DXDPARMS,R13
         MVC   DXD_CSECT,MODID
         MVC   COMM_MSG_CSECT,MODID
* ------------------------------------------------------------------- *
*        Parse the invoking command                                   *
* ------------------------------------------------------------------- *
         ITRACE ID=PARSE,                                              +
               DATA1=(COMM_COMMAND_LENGTH,4),                          +
               DATA2=COMM_COMMAND
         MVC   PARSE_COMMAND,COMM_COMMAND        ADDRESS OF COMMAND
         MVC   PARSE_LENGTH,COMM_COMMAND_LENGTH  LENGTH OF COMMAND
         LA    R1,COMM_OPERANDS                  OPERANDS INFO TABLE
         ST    R1,PARSE_OPERANDS                 PASS ADDRESS
         LA    R1,20                             MAX OPERANDS
         STH   R1,PARSE_OPERANDS_NBR
         LA    R1,PARSEREQ                       PARM BLOCK ADDRESS
         L     R15,COMM_V_OSPARSE                OSPARSE ADDRESS
         ITRACE ID=CALL,                                               +
               RDATA1=R15,                                             +
               RDATA2=R1
         BALR  R14,R15                           LINK TO OSPARSE
         ITRACE ID=RC,                                                 +
               RDATA1=R15
         LTR   R15,R15                           SUCCESSFUL?
         BNZ   ERR0010                           NO
         MVC   COMM_OPERANDS_NBR,PARSE_OPERANDS_NBR
* ------------------------------------------------------------------- *
*        Process operands on invoking command                         *
* ------------------------------------------------------------------- *
PARM0010 DS    0H
         MVI   SESS_KEYWORD_FLAGS,0              CLEAR ALL FLAGS
         LA    R2,COMM_OPERANDS                  1ST OPERAND'S DATA
         USING OPERAND,R2
         SR    R3,R3                             CLEAR REGISTER
         ICM   R3,3,COMM_OPERANDS_NBR            NUMBER OF OPERANDS
         ITRACE ID=OPERANDS,                                           +
               RDATA1=R2,                                              +
               RDATA2=R3
         MVI   SESS_USER_OPTIONS,0               RESET ALL OPTIONS
PARM0020 DS    0H
         ICM   R4,15,OPERAND_ADDRESS             OPERAND TEXT ADDRESS
         SR    R5,R5                             CLEAR REGISTER
         ICM   R5,3,OPERAND_LENGTH               OPERAND'S LENGTH
         ITRACE ID=OPERAND,                                            +
               DATA1=0(R4),                                            +
               RDATA2=R5
         SR    R6,R6                             CLEAR REGISTER
         CH    R3,H2                             AT LEAST TWO LEFT?
         BL    PARM0030                          NO
         LA    R6,OPERAND_L(,R2)                 NEXT OPERAND
NEXT     USING OPERAND,R6
PARM0030 DS    0H
         LA    R14,KEYWORDS                      FIRST KEYWORD
         USING KEYWORD,R14
PARM0040 DS    0H
         ICM   R15,15,KEYWORD_LINK               WHERE TO LINK
         CH    R5,KEYWORD_LENGTH                 TOO LONG?
         BH    PARM0050                          YES
         LR    R1,R5                             COPY LENGTH
         BCTR  R1,0                              FOR EXECUTE
         EX    R1,KEY_CLC                        FOUND THE KEYWORD?
         BER   R15                               YES
PARM0050 DS    0H
         LA    R14,KEYWORD_L(,R14)               NEXT KEYWORD
         CLI   0(R14),X'FF'                      END OF LIST?
         BE    ERR0020                           YES
         B     PARM0040                          LOOP
PARM0060 DS    0H
         LA    R2,OPERAND_L(,R2)                 NEXT OPERAND
         BCT   R3,PARM0070
         B     EXIT0000                          ALL OPERANDS PROCESSED
PARM0070 DS    0H
         LA    R2,OPERAND_L(,R2)                 NEXT OPERAND
         BCT   R3,PARM0020
         B     EXIT0000                          ALL OPERANDS PROCESSED
* ------------------------------------------------------------------- *
*        Process ABEND keyword                                        *
* ------------------------------------------------------------------- *
ABEND000 DS    0H
         ITRACE ID=SET_AB
         OI    COMM_FLAGS,$COMM_ABEND            SET FLAG
         B     PARM0070
* ------------------------------------------------------------------- *
*        PROCESS ACT (DISPLAY ACTIVE REGIONS) KEYWORD                 *
* ------------------------------------------------------------------- *
ACT0000  DS    0H
         ITRACE ID=ACT
         CLI   SESS_KEYWORD_FLAGS,0              OTHER KEYWORD(S) USED?
         BNE   ERR0490                           YES.. NOT ALLOWED
         CLI   SESS_KEYWORD_FLAGS_2,0            OTHER KEYWORD(S) USED?
         BNE   ERR0490                           YES.. NOT ALLOWED
         OI    SESS_KEYWORD_FLAGS_2,$KEYWORD_ACT SET PARM INDICATOR
         MVI   SESS_DATA_TYPE,$DATA_TYPE_OTHER   DATA TYPE IS 'OTHER'
         MVC   SESS_FORMAT,OSACT
         B     PARM0070
* ------------------------------------------------------------------- *
*        Process ASCII operand                                        *
* ------------------------------------------------------------------- *
ASCII000 DS    0H
         ITRACE ID=ASCII
         OI    SESS_USER_OPTIONS,$OPTIONS_ASCII
         B     PARM0070
* ------------------------------------------------------------------- *
*        Process DD= keyword                                          *
* ------------------------------------------------------------------- *
DD0000   DS    0H
         TM    SESS_KEYWORD_FLAGS,$KEYWORD_DSN
         BO    ERR0060
         TM    SESS_KEYWORD_FLAGS,$KEYWORD_DD
         BO    ERR0070
         OI    SESS_KEYWORD_FLAGS,$KEYWORD_DD
         LTR   R6,R6                             DD NAME PRESENT?
         BZ    ERR0080                           NO
         LH    R14,NEXT.OPERAND_LENGTH
         CH    R14,H8                            TOO LONG?
         BH    ERR0090
         BCTR  R14,0
         ICM   R15,15,NEXT.OPERAND_ADDRESS
         EX    R14,DD_MVC                        COPY DD NAME
         ITRACE ID=SET_DD,                                             +
               DATA1=SESS_DD
         B     PARM0060                          CONTINUE
* ------------------------------------------------------------------- *
*        Process DSN= keyword                                         *
* ------------------------------------------------------------------- *
DSN0000  DS    0H
         ITRACE ID=DSN
         TM    SESS_KEYWORD_FLAGS,$KEYWORD_DD
         BO    ERR0070
         TM    SESS_KEYWORD_FLAGS,$KEYWORD_DSN
         BO    ERR0100
         OI    SESS_KEYWORD_FLAGS,$KEYWORD_DSN
         LTR   R6,R6                             DSN PRESENT?
         BZ    ERR0110                           NO
         ICM   R15,15,NEXT.OPERAND_ADDRESS       DSN ADDRESS
         LH    R14,NEXT.OPERAND_LENGTH           DSN LENGTH
         CH    R14,H4                            4 BYTES?
         BNE   DSN0010                           NO
         CLC   VTOC,0(R15)                       VTOC?
         BE    DSN0050                           YES
         CLC   STORAGE(4),0(R15)                 STOR?
         BE    DSN0070                           YES
         B     DSN0020
DSN0010  DS    0H
         CH    R14,H7                            7 BYTES?
         BNE   DSN0020                           NO
         CLC   STORAGE,0(R15)                    STORAGE?
         BE    DSN0070                           YES
DSN0020  DS    0H
         TM    NEXT.OPERAND_TYPE,$OPERAND_QUOTED
         BO    DSN0030                           YES
         LH    R1,COMM_USER_LENGTH               USER ID LENGTH
         LA    R1,1(,R1)                         PLUS 1 FOR '.'
         AR    R1,R14                            PLUS OPERAND LENGTH
         CH    R1,H44                            TOO LONG?
         BH    ERR0120                           YES
         LH    R1,COMM_USER_LENGTH
         EX    R1,MVC_PREFIX
         LA    R1,SESS_DSN                       DSN IN COMM AREA
         AH    R1,COMM_USER_LENGTH               PLUS LENGTH OF USER ID
         MVI   0(R1),C'.'                        INSERT PERIOD
         LA    R1,1(,R1)                         ADD 1 TO ADDRESS
         B     DSN0040
DSN0030  DS    0H
         CH    R14,H44                           TOO LONG?
         BH    ERR0120                           YES
         LA    R1,SESS_DSN
DSN0040  DS    0H
         BCTR  R14,0
         ICM   R15,15,NEXT.OPERAND_ADDRESS
         EX    R14,DSN_MVC                       COPY DD NAME
         OC    SESS_DSN,COMM_BLANKS              TRANSLATE NULLS
         ITRACE ID=SET_DSN,                                            +
               DATA1=SESS_DSN
         B     PARM0060                          CONTINUE
DSN0050  DS    0H
         ITRACE ID=VTOC
         OI    SESS_KEYWORD_FLAGS_2,$KEYWORD_VTOC
         MVI   SESS_DSN,X'04'
         MVC   SESS_DSN+1(L'SESS_DSN-1),SESS_DSN
         B     PARM0060                          CONTINUE
DSN0060  DS    0H
         ITRACE ID=RAWVTOC
         OI    SESS_KEYWORD_FLAGS_2,$KEYWORD_RAWVTOC
         MVI   SESS_DSN,X'04'
         MVC   SESS_DSN+1(L'SESS_DSN-1),SESS_DSN
         B     PARM0060                          CONTINUE
DSN0070  DS    0H
         ITRACE ID=STOR
         OI    SESS_KEYWORD_FLAGS_2,$KEYWORD_STOR
         B     PARM0060                          CONTINUE
* ------------------------------------------------------------------- *
*        Process DUMPDATA operand                                     *
* ------------------------------------------------------------------- *
DUMP0000 DS    0H
         ITRACE ID=DUMPDATA
         OI    SESS_USER_OPTIONS,$OPTIONS_DUMPDATA
         B     PARM0070
* ------------------------------------------------------------------- *
*        Process F= or FORMAT= keyword                                *
* ------------------------------------------------------------------- *
FMAT0000 DS    0H
         TM    SESS_KEYWORD_FLAGS,$KEYWORD_FORMAT
         BO    ERR0380                           DUPLICATE FORMAT
         OI    SESS_KEYWORD_FLAGS,$KEYWORD_FORMAT
         TM    NEXT.OPERAND_TYPE,$OPERAND_PRESENT
         BNO   ERR0390                           NO
         LH    R14,NEXT.OPERAND_LENGTH           FORMAT NAME LENGTH
         CH    R14,H8                            TOO LONG?
         BH    ERR0400                           YES
         MVC   FMATREQ_FORMAT,COMM_BLANKS
         ICM   R15,15,NEXT.OPERAND_ADDRESS
         BCTR  R14,0
         EX    R14,FMAT_MVC                      COPY FORMAT NAME
         ITRACE ID=VFY_FMAT,                                           +
               DATA1=FMATREQ_FORMAT
         MVI   LOADREQ_FUNC,$LOADREQ_LOAD
         MVC   LOADREQ_MODULE,MODULE_OSFORMAT
         LA    R1,LOADREQ
         L     R15,COMM_V_OSLOAD
         BALR  R14,R15                           LOAD OSFORMAT
         MVI   FMATREQ,$FMATREQ_VERIFY           SET FUNCTION CODE
         LA    R1,FMATREQ
         ICM   R15,15,LOADREQ_EP
         BALR  R14,R15                           CALL FMFORMAT
         CLI   FMATREQ_RC,$FMATREQ_OK            FORMAT NAME OK?
         BNE   ERR0410                           NO
         MVC   SESS_FORMAT,FMATREQ_FORMAT
         B     PARM0060                          PROCESS NEXT OPERAND
FMAT_MVC MVC   FMATREQ_FORMAT(0),0(R15)
* ------------------------------------------------------------------- *
*        Process OSE keyword                                          *
* ------------------------------------------------------------------- *
OS0000   DS    0H
         ITRACE ID=ONSCREEN
         B     PARM0070                          PROCESS NEXT OPERAND
* ------------------------------------------------------------------- *
*        Process OSE keyword                                          *
* ------------------------------------------------------------------- *
OSE0000  DS    0H
         ITRACE ID=OS_EDIT
         MVC   SESS_FORMAT,EDIT                  EDIT
         OI    SESS_EDIT_FLAGS,$SESS_EDIT        SET EDIT SESSION
         B     PARM0070                          PROCESS NEXT OPERAND
* ------------------------------------------------------------------- *
*        Process FROMKEY= keyword                                     *
* ------------------------------------------------------------------- *
FROM0000 DS    0H
         TM    SESS_USER_OPTIONS,$OPTIONS_RBA
         BO    ERR0130
         ITRACE ID=FROMKEY
         TM    NEXT.OPERAND_TYPE,$OPERAND_PRESENT
         BNO   ERR0140
         LH    R14,NEXT.OPERAND_LENGTH           STARTING KEY LENGTH
         CH    R14,H40                           TOO LONG?
         BH    ERR0150                           YES
         OI    SESS_USER_OPTIONS,$OPTIONS_FROMKEY
         ICM   R15,15,NEXT.OPERAND_ADDRESS
         TM    NEXT.OPERAND_TYPE,$OPERAND_HEX    HEX VALUE?
         BO    FROM0010                          YES
         ST    R14,SESS_FROMKEY_LENGTH           SET LENGTH
         BCTR  R14,0                             FOR EXECUTE
         EX    R14,FROM_MVC
         B     PARM0060                          CONTINUE
FROM0010 DS    0H
         XC    SESS_FROM_KEY,SESS_FROM_KEY       CLEAR KEY
         MVC   COMM_INFO_01(40),0(R15)
         TR    COMM_INFO_01(40),CHAR_TO_HEX
         PACK  DXD_PACK+00(6),COMM_INFO_01+00(11)
         PACK  DXD_PACK+05(6),COMM_INFO_01+10(11)
         PACK  DXD_PACK+10(6),COMM_INFO_01+20(11)
         PACK  DXD_PACK+15(6),COMM_INFO_01+30(11)
         SRL   R14,1
         ST    R14,SESS_FROMKEY_LENGTH           SET LENGTH
         BCTR  R14,0
         EX    R14,FROM_HEX
         B     PARM0060
FROM_MVC MVC   SESS_FROM_KEY(0),0(R15)
FROM_HEX MVC   SESS_FROM_KEY(0),DXD_PACK
* ------------------------------------------------------------------- *
*        Process M= keyword                                           *
* ------------------------------------------------------------------- *
MEM0000  DS    0H
         ITRACE ID=MEMBER
         TM    SESS_KEYWORD_FLAGS,$KEYWORD_MEMBER
         BO    ERR0030
         OI    SESS_KEYWORD_FLAGS,$KEYWORD_MEMBER
         LTR   R6,R6                             MEMBER NAME PRESENT?
         BZ    ERR0040                           NO
         LH    R14,NEXT.OPERAND_LENGTH
         CH    R14,H8                            TOO LONG?
         BH    ERR0050                           YES
         BCTR  R14,0
         ICM   R15,15,NEXT.OPERAND_ADDRESS
         EX    R14,MEM_MVC                       COPY MEMBER NAME
         ITRACE ID=SET_MEM,                                            +
               DATA1=SESS_MEMBER
         B     PARM0060                          CONTINUE
* ------------------------------------------------------------------- *
*        Process NBR= keyword                                         *
* ------------------------------------------------------------------- *
NBR0000  DS    0H
         ITRACE ID=NUMBER
         TM    SESS_KEYWORD_FLAGS,$KEYWORD_RECORDS
         BO    ERR0190
         OI    SESS_KEYWORD_FLAGS,$KEYWORD_RECORDS
         LTR   R6,R6                             NUMBER PRESENT?
         BZ    ERR0200                           NO
         TM    NEXT.OPERAND_TYPE,$OPERAND_NUMERIC
         BNO   ERR0290                           NO
         LH    R14,NEXT.OPERAND_LENGTH
         CH    R14,H8                            TOO MANY DIGITS?
         BH    ERR0210                           YES
         ICM   R15,15,NEXT.OPERAND_ADDRESS
         BCTR  R14,0
         EX    R14,PACK_OPR
         CVB   R0,COMM_DWORD                     CONVERT TO BINARY
         ST    R0,SESS_NUMBER                    SET NUMBER OF RECORDS
         ITRACE ID=SET_NBR,                                            +
               DATA1=(SESS_NUMBER,4)
         B     PARM0060                          CONTINUE
* ------------------------------------------------------------------- *
*                                                                     *
*        Process NOSTAE keyword                                       *
*                                                                     *
*    Rather than issue a "ESTAE   0", set a flag in the common area.  *
*    This will allow the exit to be enabled or disabled by primary    *
*    command area command.                                            *
*                                                                     *
* ------------------------------------------------------------------- *
NOSTAE00 DS    0H
         ITRACE ID=NOSTAE                        TURN OFF ESTAE EXIT
         OI    SESS_ABEND_FLAG,$ABEND_NO_STAE
         B     PARM0070                          CONTINUE
* ------------------------------------------------------------------- *
*        Process PATH= keyword                                        *
* ------------------------------------------------------------------- *
PATH0000 DS    0H
         ITRACE ID=PATH
         TM    SESS_KEYWORD_FLAGS,$KEYWORD_DSN
         BO    ERR0420
         TM    SESS_KEYWORD_FLAGS,$KEYWORD_DD
         BO    ERR0430
         TM    SESS_KEYWORD_FLAGS,$KEYWORD_VOL
         BO    ERR0440
         TM    SESS_KEYWORD_FLAGS,$KEYWORD_MEMBER
         BO    ERR0450
         TM    SESS_KEYWORD_FLAGS,$KEYWORD_PATH
         BO    ERR0460
         OI    SESS_KEYWORD_FLAGS,$KEYWORD_PATH
         TM    NEXT.OPERAND_TYPE,$OPERAND_PRESENT
         BNO   ERR0470
         LH    R14,NEXT.OPERAND_LENGTH
         CH    R14,H256                          TOO LONG?
         BH    ERR0480                           YES
         MVC   SESS_PATH,COMM_BLANKS
         XC    SESS_PATH_LENGTH,SESS_PATH_LENGTH
         STH   R14,SESS_PATH_LENGTH+2
         BCTR  R14,0
         ICM   R15,15,NEXT.OPERAND_ADDRESS
         EX    R14,PATH_MVC
         B     PARM0060
PATH_MVC MVC   SESS_PATH(0),0(R15)
* ------------------------------------------------------------------- *
*        Process RBA= keyword                                         *
* ------------------------------------------------------------------- *
RBA0000  DS    0H
         ITRACE ID=RBA
         TM    SESS_USER_OPTIONS,$OPTIONS_RBA
         BO    ERR0220
         TM    SESS_USER_OPTIONS,$OPTIONS_FROMKEY+$OPTIONS_TOKEY
         BNZ   ERR0230
         OI    SESS_USER_OPTIONS,$OPTIONS_RBA
         LTR   R6,R6                             NUMBER PRESENT?
         BZ    ERR0240                           NO
         TM    NEXT.OPERAND_TYPE,$OPERAND_NUMERIC
         BNO   ERR0250                           NO
         LH    R14,NEXT.OPERAND_LENGTH
         CH    R14,H8                            TOO MANY DIGITS?
         BH    ERR0260                           YES
         ICM   R15,15,NEXT.OPERAND_ADDRESS
         BCTR  R14,0
         EX    R14,PACK_OPR
         CVB   R0,COMM_DWORD                     CONVERT TO BINARY
         ST    R0,SESS_RBA                       SET RBA
         ITRACE ID=SET_RBA,                                            +
               DATA1=(SESS_RBA,4)
         B     PARM0060                          CONTINUE
* ------------------------------------------------------------------- *
*        Process START= operand                                       *
* ------------------------------------------------------------------- *
START000 DS    0H
         ITRACE ID=START
         TM    SESS_KEYWORD_FLAGS,$KEYWORD_START
         BO    ERR0270
         OI    SESS_KEYWORD_FLAGS,$KEYWORD_START
         LTR   R6,R6                             NUMBER PRESENT?
         BZ    ERR0280                           NO
         TM    NEXT.OPERAND_TYPE,$OPERAND_NUMERIC
         BNO   ERR0290                           NO
         LH    R14,NEXT.OPERAND_LENGTH
         CH    R14,H8                            TOO MANY DIGITS?
         BH    ERR0300                           YES
         ICM   R15,15,NEXT.OPERAND_ADDRESS
         BCTR  R14,0
         EX    R14,PACK_OPR
         CVB   R1,COMM_DWORD                     CONVERT TO BINARY
         LTR   R1,R1                             ZERO?
         BZ    ERR0310                           YES
         ST    R1,DATASPACE_1_FIRST_RECORD       SET FIRST RECORD
         ITRACE ID=SET_STRT,                                           +
               DATA1=(DATASPACE_1_FIRST_RECORD,4)
         B     PARM0060                          CONTINUE
* ------------------------------------------------------------------- *
*        Process TOKEY= keyword                                       *
* ------------------------------------------------------------------- *
TO0000   DS    0H
         ITRACE ID=TOKEY
         TM    SESS_USER_OPTIONS,$OPTIONS_RBA
         BO    ERR0160
         TM    NEXT.OPERAND_TYPE,$OPERAND_PRESENT
         BNO   ERR0170
         LH    R14,NEXT.OPERAND_LENGTH           ENDING KEY LENGTH
         CH    R14,H40                           TOO LONG?
         BH    ERR0180                 YES
         OI    SESS_USER_OPTIONS,$OPTIONS_TOKEY
         ICM   R15,15,NEXT.OPERAND_ADDRESS
         TM    NEXT.OPERAND_TYPE,$OPERAND_HEX    IN HEX?
         BO    TO0010                            YES
         ST    R14,SESS_TOKEY_LENGTH             SET LENGTH
         BCTR  R14,0                             FOR EXECUTE
         EX    R14,TO_MVC
         B     PARM0060                          CONTINUE
TO0010   DS    0H
         XC    SESS_TO_KEY,SESS_TO_KEY
         MVC   COMM_INFO_01(40),0(R15)           COPY
         TR    COMM_INFO_01(40),CHAR_TO_HEX      TRANSLATE
         PACK  DXD_PACK+00(6),COMM_INFO_01+00(11)
         PACK  DXD_PACK+05(6),COMM_INFO_01+10(11)
         PACK  DXD_PACK+10(6),COMM_INFO_01+20(11)
         PACK  DXD_PACK+15(6),COMM_INFO_01+30(11)
         SRL   R14,1
         ST    R14,SESS_TOKEY_LENGTH             SET LENGTH
         BCTR  R14,0
         EX    R14,TO_HEX
         B     PARM0060
TO_MVC   MVC   SESS_TO_KEY(0),0(R15)             COPY TO KEY
TO_HEX   MVC   SESS_TO_KEY(0),DXD_PACK           COPY PACKED KEY
* ------------------------------------------------------------------- *
*        Process VOL= operand                                         *
* ------------------------------------------------------------------- *
VOL0000  DS    0H
         ITRACE ID=VOL
         TM    SESS_KEYWORD_FLAGS,$KEYWORD_VOL
         BO    ERR0320
         OI    SESS_KEYWORD_FLAGS,$KEYWORD_VOL
         LTR   R6,R6                             VOLSER PRESENT?
         BZ    ERR0330                           NO
         CLC   NEXT.OPERAND_LENGTH,H6            CORRECT LENGTH?
         BNE   ERR0340                           NO
         ICM   R15,15,NEXT.OPERAND_ADDRESS
         MVC   SESS_VOLSER,0(R15)                COPY VOLSER
         ITRACE ID=SETVOL,                                             +
               DATA1=(SESS_VOLSER,6)
         OI    SESS_USER_OPTIONS,$OPTIONS_VOLSER
         B     PARM0060                          CONTINUE
* ------------------------------------------------------------------- *
*        Process SYSOUT OPERAND                                       *
* ------------------------------------------------------------------- *
SYSOUT00 DS    0H
         ITRACE ID=SYSOUT
         CLC   NEXT.OPERAND_LENGTH,H1            CORRECT LENGTH?
         BNE   ERR0350                           NO
         ICM   R15,15,NEXT.OPERAND_ADDRESS
         MVC   COMM_SYSOUT_CLASS,0(R1)           COPY SYSOUT CLASS
         B     PARM0060
* ------------------------------------------------------------------- *
*        Process VTOC= keyword                                        *
* ------------------------------------------------------------------- *
VTOC0000 DS    0H
         ITRACE ID=VTOC_1
         CLC   NEXT.OPERAND_LENGTH,H6            CORRECT LENGTH?
         BNE   ERR0360                           NO
         OI    SESS_KEYWORD_FLAGS,$KEYWORD_VOL
         OI    SESS_KEYWORD_FLAGS_2,$KEYWORD_VTOC
         OI    SESS_USER_OPTIONS,$OPTIONS_VOLSER
         ICM   R15,15,NEXT.OPERAND_ADDRESS
         MVC   SESS_VOLSER,0(R15)                COPY VOLSER
         MVI   SESS_DSN,X'04'
         MVC   SESS_DSN+1(L'SESS_DSN-1),SESS_DSN
         B     PARM0060
* ------------------------------------------------------------------- *
*        Process RAWVTOC= keyword                                     *
* ------------------------------------------------------------------- *
RVTOC000 DS    0H
         ITRACE ID=RAWVTOC1
         CLC   NEXT.OPERAND_LENGTH,H6            CORRECT LENGTH?
         BNE   ERR0370                           NO
         OI    SESS_KEYWORD_FLAGS,$KEYWORD_VOL
         OI    SESS_KEYWORD_FLAGS_2,$KEYWORD_RAWVTOC
         OI    SESS_USER_OPTIONS,$OPTIONS_VOLSER
         ICM   R15,15,NEXT.OPERAND_ADDRESS
         MVC   SESS_VOLSER,0(R15)                COPY VOLSER
         MVI   SESS_DSN,X'04'
         MVC   SESS_DSN+1(L'SESS_DSN-1),SESS_DSN
         B     PARM0060
         DROP  NEXT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0010  DS    0H
         MVI   COMM_MSG_ID+1,1                   PARSE FAILED
         B     ERR900
ERR0020  DS    0H
         MVI   COMM_MSG_ID+1,2                   INVALID KEYWORD
         BCTR  R5,0
         MVC   COMM_INFO_01,COMM_BLANKS
         EX    R5,ERR_MVC
         B     ERR900
ERR_MVC  MVC   COMM_INFO_01(0),0(R4)
ERR0030  DS    0H
         MVI   COMM_MSG_ID+1,3      DUPLICATE MEMBER KEYWORD
         B     ERR900
ERR0040  DS    0H
         MVI   COMM_MSG_ID+1,4      MEMBER NAME BLANK
         B     ERR900
ERR0050  DS    0H
         MVI   COMM_MSG_ID+1,5      MEMBER NAME TOO LONG
         B     ERR900
ERR0060  DS    0H
         MVI   COMM_MSG_ID+1,6      DUPLICATE DD KEYWORD
         B     ERR900
ERR0070  DS    0H
         MVI   COMM_MSG_ID+1,7      DSN AND DD ARE MUTALLY EXCLUSIVE
         B     ERR900
ERR0080  DS    0H
         MVI   COMM_MSG_ID+1,8      DD NAME IS BLANK
         B     ERR900
ERR0090  DS    0H
         MVI   COMM_MSG_ID+1,9      DD NAME IS TOO LONG
         B     ERR900
ERR0100  DS    0H
         MVI   COMM_MSG_ID+1,10     DUPLICATE DSN KEYWORD
         B     ERR900
ERR0110  DS    0H
         MVI   COMM_MSG_ID+1,11     DSN IS BLANK
         B     ERR900
ERR0120  DS    0H
         MVI   COMM_MSG_ID+1,12     DSN IS TOO LONG
         B     ERR900
ERR0130  DS    0H
         MVI   COMM_MSG_ID+1,13     FROMKEY AND RBA MUTUALLY EXCLUSIVE
         B     ERR900
ERR0140  DS    0H
         MVI   COMM_MSG_ID+1,14     FROMKEY IS MISSING
         B     ERR900
ERR0150  DS    0H
         MVI   COMM_MSG_ID+1,15     FROMKEY TOO LONG
         B     ERR900
ERR0160  DS    0H
         MVI   COMM_MSG_ID+1,16     TOKEY AND RBA MUTUALLY EXCLUSIVE
         B     ERR900
ERR0170  DS    0H
         MVI   COMM_MSG_ID+1,17     TOKEY MISSING
         B     ERR900
ERR0180  DS    0H
         MVI   COMM_MSG_ID+1,18     TOKEY TOO LONG
         B     ERR900
ERR0190  DS    0H
         MVI   COMM_MSG_ID+1,19     DUPLICATE NBR/RECORDS KEYWORD
         B     ERR900
ERR0200  DS    0H
         MVI   COMM_MSG_ID+1,20     NBR MISSING
         B     ERR900
ERR0210  DS    0H
         MVI   COMM_MSG_ID+1,21     NBR TOO LONG
         B     ERR900
ERR0220  DS    0H
         MVI   COMM_MSG_ID+1,22     DUPLICATE RBA KEYWORD
         B     ERR900
ERR0230  DS    0H
         MVI   COMM_MSG_ID+1,23     RBA AND FROMKEY/TOKEY MUTUALLY EXCL
         B     ERR900
ERR0240  DS    0H
         MVI   COMM_MSG_ID+1,24     RBA MISSING
         B     ERR900
ERR0250  DS    0H
         MVI   COMM_MSG_ID+1,25     RBA NOT NUMERIC
         B     ERR900
ERR0260  DS    0H
         MVI   COMM_MSG_ID+1,26     RBA TOO LONG
         B     ERR900
ERR0270  DS    0H
         MVI   COMM_MSG_ID+1,27     DUPLICATE START KEYWORD
         B     ERR900
ERR0280  DS    0H
         MVI   COMM_MSG_ID+1,28     START VALUE MISSING
         B     ERR900
ERR0290  DS    0H
         MVI   COMM_MSG_ID+1,29     START NOT NUMERIC
         B     ERR900
ERR0300  DS    0H
         MVI   COMM_MSG_ID+1,30     START VALUE TOO LONG
         B     ERR900
ERR0310  DS    0H
         MVI   COMM_MSG_ID+1,31     START MAY NOT BE ZERO
         B     ERR900
ERR0320  DS    0H
         MVI   COMM_MSG_ID+1,32     DUPLICATE VOLSER KEYWORD
         B     ERR900
ERR0330  DS    0H
         MVI   COMM_MSG_ID+1,33     VOLSER VALUE MISSING
         B     ERR900
ERR0340  DS    0H
         MVI   COMM_MSG_ID+1,34     VOLSER WRONG LENGTH
         B     ERR900
ERR0350  DS    0H
         MVI   COMM_MSG_ID+1,35     SYSOUT WRONG LENGTH
         B     ERR900
ERR0360  DS    0H
         MVI   COMM_MSG_ID+1,36     VTOC VOLSER NOT 6 CHARACTERS
         B     ERR900
ERR0370  DS    0H
         MVI   COMM_MSG_ID+1,37     RVTOC VOLSER NOT 6 CHARACTERS
         B     ERR900
ERR0380  DS    0H
         MVI   COMM_MSG_ID+1,38     DUPLICATE FORMAT
         B     ERR900
ERR0390  DS    0H
         MVI   COMM_MSG_ID+1,39     FORMAT NAME MISSING
         B     ERR900
ERR0400  DS    0H
         MVI   COMM_MSG_ID+1,40     FORMAT NAME TOO LONG
         B     ERR900
ERR0410  DS    0H
         MVI   COMM_MSG_ID+1,41     FORMAT NAME NOT VALID
         B     ERR900
ERR0420  DS    0H
         MVI   COMM_MSG_ID+1,42     PATH & DSN
         B     ERR900
ERR0430  DS    0H
         MVI   COMM_MSG_ID+1,43     PATH & DD
         B     ERR900
ERR0440  DS    0H
         MVI   COMM_MSG_ID+1,44     PATH & VOLSER
         B     ERR900
ERR0450  DS    0H
         MVI   COMM_MSG_ID+1,45     PATH & MEMBER
         B     ERR900
ERR0460  DS    0H
         MVI   COMM_MSG_ID+1,46     DUPLICATE PATH
         B     ERR900
ERR0470  DS    0H
         MVI   COMM_MSG_ID+1,47     PATH MISSING
         B     ERR900
ERR0480  DS    0H
         MVI   COMM_MSG_ID+1,48     PATH TOO LONG
         B     ERR900
ERR0490  DS    0H
         MVI   COMM_MSG_ID+1,49     PARMS USED WITH "ACT"
ERR900   DS    0H
         L     R15,COMM_V_OSMSG
         BALR  R14,R15
         LA    R3,8
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=EXIT
         L     R13,4(,R13)
         L     R14,12(,R13)         RESTORE R14
         LR    R15,R3               SET RETURN CODE
         LM    R0,R12,20(R13)       RESTORE REMAINING REGISTERS
         BR    R14                  EXIT
* ------------------------------------------------------------------- *
KEY_CLC     CLC   KEYWORD_KEYWORD(0),0(R4)
DD_MVC      MVC   SESS_DD(0),0(15)
MEM_MVC     MVC   SESS_MEMBER(0),0(R15)
MVC_PREFIX  MVC   SESS_DSN(0),COMM_USER
DSN_MVC     MVC   0(0,R1),0(R15)
PACK_OPR    PACK  COMM_DWORD,0(0,R15)
* ------------------------------------------------------------------- *
*                                                                     *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
DXD_START   DC   Q(DXDPARMS)  DISPLACEMENT TO WORK AREA

H1          DC   H'1'
H2          DC   H'2'
H4          DC   H'4'
H6          DC   H'6'
H7          DC   H'7'
H8          DC   H'8'
H40         DC   H'40'
H44         DC   H'44'
H256        DC   H'256'

CHAR_TO_HEX EQU  *-C'A'
            DC     X'0A0B0C0D0E0F000000000000000000'
            DC   X'00000000000000000000000000000000'
            DC   X'00000000000000000000000000000000'
            DC   X'00010203040506070809000000000000'

EDIT            DC   CL8'EDIT    '
OSACT           DC   CL8'OSACT'
MODULE_OSFORMAT DC   CL8'OSFORMAT'
STORAGE         DC   C'STORAGE'
VTOC            DC   C'VTOC'

KEYWORDS    DS  0C
            KEYDEF 'ABEND',ABEND000
            KEYDEF 'ACT',ACT0000
            KEYDEF 'ASCII',ASCII000
            KEYDEF 'DUMPDATA',DUMP0000
            KEYDEF 'DD=',DD0000
            KEYDEF 'DSN=',DSN0000
            KEYDEF 'F=',FMAT0000
            KEYDEF 'OSEDIT',OSE0000
            KEYDEF 'OSE',OSE0000
            KEYDEF 'OSMAIN',OS0000
            KEYDEF 'OS',OS0000
            KEYDEF 'FORMAT=',FMAT0000
            KEYDEF 'FROMKEY=',FROM0000
            KEYDEF 'M=',MEM0000
            KEYDEF 'MEM=',MEM0000
            KEYDEF 'MEMBER=',MEM0000
            KEYDEF 'NBR=',NBR0000
            KEYDEF 'NOSTAE',NOSTAE00
            KEYDEF 'P=',PATH0000
            KEYDEF 'PATH=',PATH0000
            KEYDEF 'RBA=',RBA0000
            KEYDEF 'RECS=',NBR0000
            KEYDEF 'RECORDS=',NBR0000
            KEYDEF 'RAWVTOC',DSN0060
            KEYDEF 'RAWVTOC=',RVTOC000
            KEYDEF 'START=',START000
            KEYDEF 'SYSOUT=',SYSOUT00
            KEYDEF 'TOKEY=',TO0000
            KEYDEF 'V=',VOL0000
            KEYDEF 'VOL=',VOL0000
            KEYDEF 'VOLSER=',VOL0000
            KEYDEF 'VTOC',DSN0050
            KEYDEF 'VTOC=',VTOC0000
            KEYDEF 'VTOCRAW=',RVTOC000
            DC  X'FF'

            LTORG

PARMSEND    EQU    *
* ------------------------------------------------------------------- *
*                                                                     *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
DXDPARMS    DSECT
            COPY     DXDPREF

DXD_PACK    DS       CL42

            FMATREQ  DSECT=NO
            LOADREQ  DSECT=NO
            PARSEREQ DSECT=NO
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
            PARMSREQ DSECT=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
KEYWORD           DSECT
KEYWORD_KEYWORD   DS       CL8
KEYWORD_LENGTH    DS       XL2
KEYWORD_LINK      DS       XL4
KEYWORD_L         EQU      *-KEYWORD
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COMMON   TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY     OPERAND
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         BPXYSTAT DSECT=YES,LIST=YES
* ------------------------------------------------------------------- *
         COPY     TRENTRY
* ------------------------------------------------------------------- *
         COPY     REGEQU
         END      OSPARMS
