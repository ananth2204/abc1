* ------------------------------------------------------------------- *
*                                                                     *
*  Module name: OSVDATA                                               *
*                                                                     *
*     There are three functions:                                      *
*       GETMAIN                                                       *
*       FREEMAIN                                                      *
*       VDEFINE                                                       *
*                                                                     *
*                                                                     *
*   On GETMAINs, issue a PQUERY to determine the dimensions of        *
*   the dynamic area on the SPF panel.  The address of the panel      *
*   name is passed in R1.  At exit, R0 is the length of the area      *
*   and R1 is the address of the area GETMAINed.                      *
*                                                                     *
*   On FREEMAINs, the length of the area to be freed is passed        *
*   in R0, the address is passed in R1.                               *
*                                                                     *
*   On VDEFINEs, the length of the area to be VDEFINED is passed      *
*   in R0, the address is passed in R1.                               *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
OSVDATA  CSECT
OSVDATA  AMODE 31
OSVDATA  RMODE ANY
         USING OSCOMM,R12
         USING OSVDATA,R15
         B     INIT0000
MODID    DC    CL8'OSVDATA'
         DC    CL8'&SYSDATE'
         DC    CL8'&SYSTIME'
         DC    A(VDATAEND-OSVDATA)
INIT0000 DS    0H
         USING SESSION,R11                    DEFINE SESSION BASE
         STM   R14,R12,12(R13)                SAVE REGS
         LR    R10,R15                        COPY ENTRY POINT
         LR    R2,R0                          COPY R0
         LR    R3,R1                          COPY R1
         DROP  R15
         USING OSVDATA,R10                    DEFINE BASE
         L     R15,COMM_DXD
         A     R15,DXD_START
         ST    R13,4(,R15)                    SAVE R13
         ST    R15,8(,R13)                    CHAIN SAVE AREA
         LR    R13,R15                        COPY WORK AREA ADDRESS
         USING DXDVDATA,R13                   DEFINE WORK AREA BASE
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY,                                              +
               DATA1=COMM_VDATA_FUNC
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         L     R9,COMM_OSSPFD                 SPF DATA
         USING OSSPFD,R9                      DEFINE BASE
         CLI   COMM_VDATA_FUNC,$VDATA_GETMAIN
         BE    VDATA010
         CLI   COMM_VDATA_FUNC,$VDATA_FREEMAIN
         BE    VDATA030
         CLI   COMM_VDATA_FUNC,$VDATA_VDEFINE
         BE    VDATA020
         ITRACE ID=BAD_FUNC,                                           +
               DATA1=(COMM_VDATA_FUNC,1)
         DC    H'0'
* ------------------------------------------------------------------- *
*                                                                     *
*        GETMAIN                                                      *
*                                                                     *
*        Determine panel size                                         *
*        COMPUTE SIZE OF AREA                                         *
*        ISSUE SYSTEM GETMAIN                                         *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
VDATA010 DS    0H
         ITRACE ID=PQUERY,                    ISSUING PQUERY           +
               DATA1=0(R3)                    .. PANEL NAME
         LA    R1,PQUERY                      COMMAND'S ADDRESS
         ST    R1,WORK_SPF_1                  SET IN PARM LIST
         ST    R3,WORK_SPF_2                  SET ADDR OF PANEL NAME
         LA    R1,CVDATA                      AREA NAME
         ST    R1,WORK_SPF_3                  SET IN PARM LIST
         LA    R1,CVTYPE                      AREA TYPE
         ST    R1,WORK_SPF_4                  SET IN PARM LIST
         LA    R1,CVWIDTH                     SCREEN DEPTH
         ST    R1,WORK_SPF_5                  SET IN PARM LIST
         LA    R1,CVDEPTH                     SCREEN WIDTH
         ST    R1,WORK_SPF_6                  SET IN PARM LIST
         LA    R1,CVROW                       SCREEN ROW
         ST    R1,WORK_SPF_7                  SET IN PARM LIST
         LA    R1,CVCOL                       SCREEN COLUMN
         ST    R1,WORK_SPF_8                  SET IN PARM LIST
         OI    WORK_SPF_8,X'80'               FLAG 'END-OF-LIST'
         LA    R1,WORK_SPF_PARMS              PQUERY PARM LIST ADDRESS
         L     R15,COMM_ISPLINK               SPF INTERFACE'S ADDRESS
         BALR  R14,R15                        ISSUE QUERY
         ITRACE ID=PQUERYRC,                                           +
               RDATA1=R15
         LTR   R15,R15                        PQUERY SUCCESSFUL?
         BNZ   ERR0010                        NO
* ------------------------------------------------------------------- *
*        Compute size of VDATA                                        *
* ------------------------------------------------------------------- *
         L     R0,SPF_VWIDTH                  DYNAMIC AREA WIDTH
         MH    R0,SPF_VDEPTH+2                MULTIPLY BY DEPTH
* ------------------------------------------------------------------- *
*        Acquire storage for dynamic area                             *
* ------------------------------------------------------------------- *
         ITRACE ID=DYNA_W_D,                  WIDTH AND DEPTH          +
               DATA1=(SPF_VWIDTH,4),                                   +
               DATA2=(SPF_VDEPTH,4)
         ST    R0,WORK_VDATA_LENGTH           SAVE LENGTH
         GETMAIN RU,                                                   +
               LV=(R0),                                                +
               LOC=ANY
         ST    R1,WORK_VDATA_ADDR             SAVE ADDRESS
         ITRACE ID=GETMAIN,                                            +
               RDATA1=R1,                                              +
               RDATA2=R0
         B     EXIT0000
* ------------------------------------------------------------------- *
*        DEFINE 'VDATA' TO SPF                                        *
* ------------------------------------------------------------------- *
VDATA020 DS    0H
         LA    R1,VDELETE                     VDELETE FIRST
         ST    R1,WORK_SPF_1
         LA    R1,CVDATA
         ST    R1,WORK_SPF_2
         OI    WORK_SPF_2,X'80'
         ITRACE ID=VDEL_1,                                             +
               RDATA1=R1
         LA    R1,WORK_SPF_PARMS
         L     R15,COMM_ISPLINK               VDELETE VDATA
         BALR  R14,R15
         ITRACE ID=VDEFINE,                   DEFINING 'VDATA'         +
               DATA1=(SESS_DISP_VDATA_ADDR,4),                         +
               DATA2=(SESS_DISP_VDATA_LENGTH,4)
         LA    R1,VDEFINE                     COMMAND'S ADDRESS
         ST    R1,WORK_SPF_1                  SET ADDRESS IN PARM LIST
         LA    R1,CVDATA                      VARIABLE NAME ADDRESS
         ST    R1,WORK_SPF_2                  SET ADDRESS IN PARM LIST
         ST    R3,WORK_SPF_3                  SET ADDRESS OF DATA
         LA    R1,CHARFORM                    DATA FORMAT ADDRESS
         ST    R1,WORK_SPF_4                  SET ADDRESS IN PARM LIST
         ST    R2,WORK_VDATA_LENGTH           SAVE LENGTH
         LA    R1,WORK_VDATA_LENGTH           ADDRESS OF LENGTH
         ST    R1,WORK_SPF_5                  SET ADDRESS IN PARM LIST
         OI    WORK_SPF_5,X'80'               FLAG END-OF-LIST
         LA    R1,WORK_SPF_PARMS              SPF PARM LIST ADDRESS
         L     R15,COMM_ISPLINK               SPF ENTRY POINT
         BALR  R14,R15                        DEFINE 'VDATA'
         LTR   R15,R15                        DEFINE SUCCESSFUL?
         BNZ   ERR0030                        NO
         B     EXIT0000
* ------------------------------------------------------------------- *
*        FREEMAIN VDATA                                               *
* ------------------------------------------------------------------- *
VDATA030 DS    0H
         ITRACE ID=FREEMAIN,                                           +
               RDATA1=R3,                                              +
               RDATA2=R2
         FREEMAIN RU,                         FREEMAIN THE AREA        +
               A=(R3),                                                 +
               LV=(R2)
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0010  DS    0H
         ITRACE ID=PQRYFAIL
         DC    H'1'
         DC    C'PQUERY FAILED'
ERR0020  DS    0H
         ITRACE ID=VDELFAIL
         DC    H'2'
         DC    C'VDELETE FAILED'
ERR0030  DS    0H
         ITRACE ID=VDEFFAIL
         DC    H'3'
         DC    C'VDEFINE FAILED'
ERR0040  DS    0H
         ITRACE ID=NO_ADDR
         DC    H'4'
         DC    C'NO VDATA ADDR'
ERR0050  DS    0H
         ITRACE ID=NO_LEN
         DC    H'5'
         DC    C'NO VDATA LENGTH'
* ------------------------------------------------------------------- *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=EXIT,                                               +
               DATA1=(WORK_VDATA_ADDR,4),                              +
               DATA2=(WORK_VDATA_LENGTH,4)
         L     R0,WORK_VDATA_LENGTH
         L     R1,WORK_VDATA_ADDR
         L     R13,4(,R13)                    RESTORE SAVE AREA ADDRESS
         L     R14,12(,R13)                   RESTORE R14
         LM    R2,R12,28(R13)                 RESTORE REGISTERS
         SR    R15,R15                        SET RC
         BR    R14                            RETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXD_START     DC    Q(DXDVDATA)
CHARFORM      DC    CL8'CHAR'
CVDATA        DC    CL8'VDATA'
CVDEPTH       DC    CL8'VDEPTH'
CVTYPE        DC    CL8'VTYPE'
CVWIDTH       DC    CL8'VWIDTH'
CVROW         DC    CL8'VROW'
CVCOL         DC    CL8'VCOL'
PQUERY        DC    CL8'PQUERY'
VDEFINE       DC    CL8'VDEFINE'
VDELETE       DC    CL8'VDELETE'
              LTORG
VDATAEND      EQU   *
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDVDATA          DSECT
                  COPY    DXDPREF

WORK_SPF_PARMS    DS      0A     SPF PARM LIST
WORK_SPF_1        DS      A
WORK_SPF_2        DS      A
WORK_SPF_3        DS      A
WORK_SPF_4        DS      A
WORK_SPF_5        DS      A
WORK_SPF_6        DS      A
WORK_SPF_7        DS      A
WORK_SPF_8        DS      A
WORK_SPF_9        DS      A

WORK_VDATA_ADDR   DS      A
WORK_VDATA_LENGTH DS      F
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         BPXYSTAT DSECT=YES,LIST=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         OSSPFD   TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY     REGEQU
         END      OSVDATA
