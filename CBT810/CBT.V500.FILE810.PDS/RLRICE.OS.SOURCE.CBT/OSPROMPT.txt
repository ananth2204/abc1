* ------------------------------------------------------------------- *
*                                                                     *
*        PROMPT for parameters for initial or new sessions            *
*                                                                     *
* ------------------------------------------------------------------- *
OSPROMPT CSECT
OSPROMPT AMODE 31
OSPROMPT RMODE ANY
         USING OSPROMPT,R15
         USING OSCOMM,R12
         USING SESSION,R11
         B     INIT0000
MODID    DC    CL8'OSPROMPT'
         DC    CL8'&SYSDATE'
         DC    CL8'&SYSTIME'
         DC    A(PROMPTEND-OSPROMPT)
INIT0000 DS    0H
         STM   R14,R12,12(R13)                SAVE REGISTERS
         LR    R10,R15                        COPY BASE ADDRESS
         DROP  R15
         USING OSPROMPT,R10
         L     R15,COMM_DXD                   DXD AREA BASE ADDRESS
         A     R15,DXD_START                  PLUS OFFSET
         ST    R13,4(,R15)
         ST    R15,8(,R13)
         LR    R13,R15                        COPY DXD AREA ADDRESS
         USING DXDPRMPT,R13                   DEFINE WORK AREA BASE
         MVC   DXD_CSECT,MODID
         MVC   COMM_MSG_CSECT,MODID
         ITRACE ID=ENTRY,                                              +
               RDATA1=R1
         L     R9,COMM_OSSPFD
         USING OSSPFD,R9
         MVC   SPF_DSN,COMM_BLANKS
         MVC   SPF_DD,COMM_BLANKS
         MVC   SPF_MEMBER,COMM_BLANKS
         MVC   SPF_VOLSER,COMM_BLANKS
         MVC   SPF_FORMAT,COMM_BLANKS
         MVC   SPF_FIRST_RECORD,COMM_BLANKS
         MVC   SPF_RECORDS,COMM_BLANKS
         MVC   SPF_RBA,COMM_BLANKS
         MVC   SPF_PATH,COMM_BLANKS
MAIN0120 DS    0H
         MVC   SESS_DISP_PANEL,OS0000         PANEL TO DISPLAY
         XC    SESS_DISP_VDATA_ADDR,SESS_DISP_VDATA_ADDR
         XC    SESS_DISP_CURSOR,SESS_DISP_CURSOR
         ITRACE ID=DISP
         L     R15,COMM_V_OSDISP
         BALR  R14,R15                        LINK TO OSDISP
         ITRACE ID=DISP_RC,                                            +
               DATA1=(SESS_RC,2)
         OC    SESS_RC,SESS_RC                RC ZERO?
         BNZ   EXIT0000                       NO
         OC    SPF_ZCMD,COMM_BLANKS           FORCE UPPER CASE
         CLC   SPF_ZCMD,COMM_BLANKS           ANYTHING IN ZCMD?
         BE    MAIN0130                       NO
         ITRACE ID=ZCMD                       USE WHATEVER IS IN ZCMD
         B     EXIT0000
MAIN0130 DS    0H
         ITRACE ID=NO_ZCMD
         CLC   SPF_DSN,COMM_BLANKS            DATA SET NAME PRESENT?
         BE    MAIN0170                       NO
         CLC   SPF_DD,COMM_BLANKS             DD NAME PRESENT ALSO?
         BNE   ERR0010                        YES
         CLC   SPF_PATH,COMM_BLANKS           PATH PRESENT ALSO?
         BNE   ERR0100                        YES
         ITRACE ID=DSN
         OI    SESS_KEYWORD_FLAGS,$KEYWORD_DSN
         CLI   SPF_DSN,X'7D'                  OPEN QUOTE?
         BE    MAIN0140                       YES
         ITRACE ID=NOQUOTE
         MVC   SESS_DSN(L'COMM_USER),COMM_USER  COPY USER ID AS PERFIX
         LA    R1,SESS_DSN                    DATA SET NAME ADDRESS
         AH    R1,COMM_USER_LENGTH            PLUS USER ID LENGTH
         MVI   0(R1),C'.'                     INSERT PERIOD
         LA    R1,1(,R1)                      NEXT BYTE
         LA    R15,L'SESS_DSN                 LENGTH OF DSN
         SH    R15,COMM_USER_LENGTH           MINUS LENGTH OF USER ID
         BCTR  R15,0                          MINUS LENGTH OF PERIOD
         BCTR  R15,0                          MINUS 1
         EX    R15,DSN_MVC1                   COPY SPECIFIED NAME
         B     MAIN0210
DSN_MVC1 MVC   0(0,R1),SPF_DSN                COPY SPECIFIED NAME
MAIN0140 DS    0H
         ITRACE ID=QUOTED
         LA    R1,SPF_DSN+(L'SPF_DSN-1)
         LA    R15,L'SPF_DSN-1                LENGTH OF NAME
MANI0150 DS    0H
         CLI   0(R1),X'7D'                    CLOSING QUOTE?
         BE    MAIN0160                       YES
         BCTR  R1,0
         BCT   R15,MANI0150
         B     ERR0090                        NO CLOSING QUOTE
MAIN0160 DS    0H
         MVC   SESS_DSN,COMM_BLANKS           INITIALIZE DSN
         BCTR  R15,0                          MINUS 1 FOR QUOTE
         BCTR  R15,0                          FOR EX INSTRUCTION
         EX    R15,DSN_MVC2                   COPY MINUS QUOTES
         B     MAIN0210
DSN_MVC2 MVC   SESS_DSN(0),SPF_DSN+1
MAIN0170 DS    0H
         CLC   SPF_DD,COMM_BLANKS             DD NAME PRESENT?
         BE    MAIN0180                       NO
         CLC   SPF_PATH,COMM_BLANKS           PATH PRESENT ALSO
         BNE   ERR0110                        YES
         ITRACE ID=DDNAME
         OI    SESS_KEYWORD_FLAGS,$KEYWORD_DD
         B     MAIN0210
MAIN0180 DS    0H
         NI    SESS_KEYWORD_FLAGS,255-$KEYWORD_PATH
         CLC   SPF_PATH,COMM_BLANKS           UNIX PATH PRESENT?
         BE    ERR0020                        NO
         CLC   SPF_MEMBER,COMM_BLANKS
         BNE   ERR0120                        NOT ALLOWED WITH PATH
         CLC   SPF_VOLSER,COMM_BLANKS
         BNE   ERR0130                        VOLSER NOT ALLOWED
         ITRACE ID=PATH
         MVC   SESS_PATH,SPF_PATH
         OI    SESS_KEYWORD_FLAGS,$KEYWORD_PATH
         MVI   SESS_DATA_TYPE,$DATA_TYPE_HFS
         LA    R14,SESS_PATH
         SR    R15,R15
MAIN0190 DS    0H
         CLI   0(R14),C' '                    BLANK?
         BE    MAIN0200                       YES
         LA    R14,1(,R14)
         LA    R15,1(,R15)
         B     MAIN0190
MAIN0200 DS    0H
         ST    R15,SESS_PATH_LENGTH           SAVE PATH LENGTH
MAIN0210 DS    0H
         CLC   SPF_FORMAT,COMM_BLANKS
         BE    MAIN0220
         ITRACE ID=VFY_FMAT,                                           +
               DATA1=SPF_FORMAT
         MVI   LOADREQ_FUNC,$LOADREQ_LOAD
         MVC   LOADREQ_MODULE,MODULE_OSFORMAT
         LA    R1,LOADREQ
         L     R15,COMM_V_OSLOAD
         BALR  R14,R15
         MVI   FMATREQ,$FMATREQ_VERIFY
         MVC   FMATREQ_FORMAT,SPF_FORMAT
         LA    R1,FMATREQ
         ICM   R15,15,LOADREQ_EP
         BALR  R14,R15
         CLI   FMATREQ_RC,$FMATREQ_OK
         BNE   ERR910
         MVC   SESS_FORMAT,SPF_FORMAT
         OI    SESS_KEYWORD_FLAGS,$KEYWORD_FORMAT
MAIN0220 DS    0H
         CLC   SPF_FIRST_RECORD,COMM_BLANKS
         BE    MAIN0230
         ITRACE ID=START,                                              +
               DATA1=SPF_FIRST_RECORD
         LA    R1,SPF_FIRST_RECORD
         BAL   R8,NBR0000                     CONVERT
         BM    ERR0040                        NOT VALID
         OI    SESS_KEYWORD_FLAGS,$KEYWORD_START
         ST    R1,DATASPACE_1_FIRST_RECORD
MAIN0230 DS    0H
         CLC   SPF_RECORDS,COMM_BLANKS
         BE    MAIN0240
         ITRACE ID=RECORDS,                                            +
               DATA1=SPF_RECORDS
         LA    R1,SPF_RECORDS
         BAL   R8,NBR0000                     CONVERT
         BM    ERR0050                        NOT VALID
         OI    SESS_KEYWORD_FLAGS,$KEYWORD_RECORDS
         ST    R1,SESS_NUMBER
MAIN0240 DS    0H
         CLC   SPF_RBA,COMM_BLANKS
         BE    MAIN0250
         CLC   SPF_FROM_KEY,COMM_BLANKS
         BNE   ERR0060
         CLC   SPF_TO_KEY,COMM_BLANKS
         BNE   ERR0070
         ITRACE ID=RECORDS,                                            +
               DATA1=SPF_RBA
         LA    R1,SPF_RBA
         BAL   R8,NBR0000                     CONVERT
         BM    ERR0080                        NOT VALID
         OI    SESS_USER_OPTIONS,$OPTIONS_RBA
         ST    R1,SESS_RBA
MAIN0250 DS    0H
         MVC   SESS_DD,SPF_DD
         MVC   SESS_MEMBER,SPF_MEMBER
         MVC   SESS_FORMAT,SPF_FORMAT
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0010  DS    0H
         MVI   COMM_MSG_ID+1,1
         B     ERR900
ERR0020  DS    0H
         MVI   COMM_MSG_ID+1,2
         B     ERR900
ERR0040  DS    0H
         MVI   COMM_MSG_ID+1,4
         B     ERR900
ERR0050  DS    0H
         MVI   COMM_MSG_ID+1,5
         B     ERR900
ERR0060  DS    0H
         MVI   COMM_MSG_ID+1,6
         B     ERR900
ERR0070  DS    0H
         MVI   COMM_MSG_ID+1,7
         B     ERR900
ERR0080  DS    0H
         MVI   COMM_MSG_ID+1,8
         B     ERR900
ERR0090  DS    0H
         MVI   COMM_MSG_ID+1,9
         B     ERR900
ERR0100  DS    0H
         MVI   COMM_MSG_ID+1,10
         B     ERR900
ERR0110  DS    0H
         MVI   COMM_MSG_ID+1,11
         B     ERR900
ERR0120  DS    0H
         MVI   COMM_MSG_ID+1,12
         B     ERR900
ERR0130  DS    0H
         MVI   COMM_MSG_ID+1,13
ERR900   DS    0H
         L     R15,COMM_V_OSMSG
         BALR  R14,R15
ERR910   DS    0H
         MVC   SPF_MSG_1,COMM_MSG_1
         MVC   SPF_MSG_2,COMM_MSG_2
         MVC   SPF_MSG_3,COMM_MSG_3
         MVC   SPF_MSG_4,COMM_MSG_4
         MVC   SPF_MSG_5,COMM_MSG_5
         B     MAIN0120
* ------------------------------------------------------------------- *
*                                                                     *
*                                                                     *
*     Convert numeric values to binary                                *
*                                                                     *
*                                                                     *
*     At entry R1 is address of 10 character field to convert         *
*              R8 return address                                      *
*                                                                     *
*                                                                     *
*     At exit  R1 zero or positive, valid numeric                     *
*              R1 negative, field was not numeric                     *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
NBR0000  DS    0H
         MVC   DXD_NBR_IN,CHAR_ZERO            INITIALIZE WORK AREA
         LA    R15,10                          INITIALIZE LENGTH
NBR0010  DS    0H
         CLI   0(R1),C' '                      BLANK?
         BE    NBR0020                         YES
         CLI   0(R1),C'0'                      VALID DIGIT?
         BL    NBR0040                         NO
         CLI   0(R1),C'9'                      STILL VALID?
         BH    NBR0040                         NO
         MVC   DXD_NBR_IN(L'DXD_NBR_IN-1),DXD_NBR_IN+1
         MVC   DXD_NBR_IN+(L'DXD_NBR_IN-1)(1),0(R1)
         LA    R1,1(,R1)                       NEXT DIGIT
         BCT   R15,NBR0010                     LOOP
         B     NBR0030
NBR0020  DS    0H
         BCTR  R15,0                           FOR EXECUTE
         EX    R15,NBR_CLC                     REMAINING BLANKS?
         BNE   NBR0040                         NO
NBR0030  DS    0H
         PACK  COMM_DWORD,DXD_NBR_IN           PACK
         CVB   R1,COMM_DWORD                   CONVERT TO BINARY
         BR    R8
NBR0040  DS    0H
         ICM   R1,15,MINUS_1                   NOT VALID
         BR    R8
NBR_CLC  CLC   0(0,R1),COMM_BLANKS
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=EXIT
         L     R13,4(,R13)
         L     R14,12(,R13)                    RESTORE R14
         SR    R15,R15                         SET RETURN CODE
         LM    R0,R12,20(R13)                  RESTORE REGISTERS
         BR    R14                             EXIT
* ------------------------------------------------------------------- *
*                                                                     *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
DXD_START       DC   Q(DXDPRMPT)
MINUS_1         DC   F'-1'

ASCII           DC   CL6'ASCII'
CHAR_ZERO       DC   10C'0'
EDIT_WORD       DC   X'F02020202020202020202120'
OS0000          DC   CL8'OS0000'
MODULE_OSFORMAT DC   CL8'OSFORMAT'
                LTORG
PROMPTEND       EQU  *
* ------------------------------------------------------------------- *
*                                                                     *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
DXDPRMPT    DSECT
            COPY     DXDPREF

DXD_FLAGS   DS       X
$DXD_INIT   EQU      X'80'

DXD_NBR_IN  DS       CL10
DXD_NBR_OUT DS       XL12

            FMATREQ  DSECT=NO
            LOADREQ  DSECT=NO
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
            OSSPFD   TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
            COMMON   TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
            SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
            BPXYSTAT DSECT=YES,LIST=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
            COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
            COPY     REGEQU
            END      OSPROMPT
