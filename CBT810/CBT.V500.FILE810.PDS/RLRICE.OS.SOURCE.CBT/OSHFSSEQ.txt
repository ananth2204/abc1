* ------------------------------------------------------------------- *
*                                                                     *
*  MODULE NAME: OSHFSSEQ                                              *
*                                                                     *
*  Read HFS files                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
OSHFSSEQ CSECT
OSHFSSEQ AMODE 31
OSHFSSEQ RMODE ANY
         USING OSCOMM,R12
         USING SESSION,R11
         USING OSHFSSEQ,R15
         B     INIT0000
MODID    DC    CL8'OSHFSSEQ'
         DC    CL8'&SYSDATE'
         DC    CL8'&SYSTIME'
         DC    A(OSHFSSEQEND-OSHFSSEQ)
INIT0000 DS    0H
         STM   R14,R12,12(R13)                  SAVE REGS
         LR    R10,R15                          COPY ENTRY POINT
         DROP  R15
         USING OSHFSSEQ,R10                     DEFINE BASE
         GETMAIN RU,                                                   +
               LV=OSHFSSEQ_WORK_L,                                     +
               LOC=ANY
         ST    R13,4(,R1)                       SAVE R13
         ST    R1,8(,R13)                       CHAIN SAVE AREA
         LR    R13,R1                           COPY WORK AREA ADDRESS
         USING OSHFSSEQ_WORK,R13                DEFINE WORK AREA BASE
         MVC   DXD_CSECT,MODID                  COPY CSECT NAME
         ITRACE ID=ENTRY
         MVC   SESS_DATA_LOADER,MODID           WHO LOADED DATA
INIT0010 DS    0H
         LA    R1,DATASPACE_1                   USE DATA SPACE 1
         ST    R1,DSPCREQ_DATASPACE
         XC    DSPCREQ_RECORD_NBR,DSPCREQ_RECORD_NBR
         MVI   DSPCREQ_FUNC,$DSPCREQ_STORE
         TM    SESS_EDIT_FLAGS,$SESS_EDIT       EDIT SESSION?
         BO    INIT0020                         YES
         MVC   SESS_FORMAT,HFS
         MVC   SESS_DEFAULT_FORMAT,HFS
         B     INIT0030
INIT0020 DS    0H
         MVC   SESS_FORMAT,EDIT
         MVC   SESS_DEFAULT_FORMAT,EDIT
INIT0030 DS    0H
         LA    R2,SESS_PATH
         LA    R3,SESS_PATH_LENGTH
         LA    R4,SESS_STAT
         USING STAT,(R4)
         USING S_MODE,ST_MODE
         CLI   S_TYPE,FT_SYMLINK                SYMLINK?
         BNE   INIT0040                         NO
         LA    R2,SESS_REAL_PATH
         LA    R3,SESS_REAL_PATH_LENGTH
         LA    R4,SESS_REAL_STAT
* ------------------------------------------------------------------- *
*        OPEN the file                                                *
* ------------------------------------------------------------------- *
INIT0040 DS    0H
         ITRACE ID=OPEN,                                               +
               RDATA1=R2,                                              +
               RDATA2=R4
         XC    O_FLAGS,O_FLAGS                  INITIALIZE ALL FLAGS
         MVI   O_FLAGS4,O_RDONLY
         CALL  BPX1OPN,                                                +
               ((R3),                                                  +
               (R2),                                                   +
               O_FLAGS,                                                +
               F0,                                                     +
               WORK_RETURN_VALUE,                                      +
               WORK_RETURN_CODE,                                       +
               WORK_REASON_CODE),                                      +
               VL,                                                     +
               MF=(E,WORK_HFS_PARMLIST)
         ITRACE ID=OPEN_RC,                                            +
               DATA1=(WORK_RETURN_VALUE,4),                            +
               DATA2=(WORK_RETURN_CODE,4)
         TM    WORK_RETURN_VALUE,X'80'          SUCCESSFUL?
         BO    ERR0010                          NO
         MVC   WORK_FILE_DESCRIPTOR,WORK_RETURN_VALUE
         ITRACE ID=FILEDESC,                                           +
               DATA1=(WORK_FILE_DESCRIPTOR,4)
* ------------------------------------------------------------------- *
*        READ                                                         *
* ------------------------------------------------------------------- *
MAIN0010 DS    0H
         ITRACE ID=READ,                                               +
               DATA1=(WORK_FILE_DESCRIPTOR,4),                         +
               DATA2=(SESS_IO_AREA,4)
         CALL  BPX1RED,                         READ                   +
               (WORK_FILE_DESCRIPTOR,                                  +
               SESS_IO_AREA,                                           +
               F0,                                                     +
               COMM_IO_SIZE,                                           +
               WORK_RETURN_VALUE,                                      +
               WORK_RETURN_CODE,                                       +
               WORK_REASON_CODE),                                      +
               VL,                                                     +
               MF=(E,WORK_HFS_PARMLIST)
         ITRACE ID=READ_RC,                                            +
               DATA1=(WORK_RETURN_VALUE,4),                            +
               DATA2=(WORK_RETURN_CODE,4)
         ICM   R9,15,WORK_RETURN_VALUE
         BM    ERR0020                          NEGATIVE.. ERROR
         BZ    MAIN0090                         ZERO..
         L     R8,SESS_IO_AREA                  I/O AREA ADDRESS
         CLC   FT_CCSID,=AL2(FT_BINARYTAG)
         BE    MAIN0050                         BINARY
MAIN0020 DS    0H
         ITRACE ID=FIND_NL,                     SEARCH FOR NEW LINE    +
               RDATA1=R8,                                              +
               RDATA2=R9
         LR    R14,R8                           STARTING POINT
         LR    R15,R9                           MAXIMUM LENGTH
MAIN0030 DS    0H
         CLI   0(R14),NEW_LINE                  NEW LINE?
         BE    MAIN0040                         YES
         LA    R14,1(,R14)                      NEXT
         BCT   R15,MAIN0030                     LOOP
MAIN0040 DS    0H
         LR    R0,R9                            STARTING LENGTH
         SR    R0,R15                           LENGTH LEFT
         ST    R0,DSPCREQ_RECORD_LENGTH
         B     MAIN0060
MAIN0050 DS    0H
         ST    R9,DSPCREQ_RECORD_LENGTH
MAIN0060 DS    0H
         ITRACE ID=RECORD,                                             +
               DATA1=(DSPCREQ_RECORD_ADDR,4),                          +
               DATA2=(DSPCREQ_RECORD_LENGTH,4)
         ST    R8,DSPCREQ_RECORD_ADDR           RECORD ADDRESS
         L     R1,DSPCREQ_RECORD_NBR            RECORD NUMBER
         LA    R1,1(,R1)                        PLUS 1
         ST    R1,DSPCREQ_RECORD_NBR            SAVE RECORD NUMBER
         CLC   DSPCREQ_RECORD_NBR,DATASPACE_1_FIRST_RECORD
         BL    MAIN0080                         SKIP UNTIL START
         LA    R1,DSPCREQ                       OSDSPACE PARMS
         L     R15,COMM_OSDSPACE                OSDSPACE ENTRY POINT
         BALR  R14,R15                          STORE THIS RECORD
         CLI   DSPCREQ_RC,$DSPCREQ_OK           SUCCESSFUL?
         BNE   MAIN0100                         NO
         TM    SESS_KEYWORD_FLAGS,$KEYWORD_RECORDS
         BNO   MAIN0070                         NO LIMIT..
         CLC   DSPCREQ_RECORD_NBR,SESS_NUMBER
         BNL   MAIN0110                         TIME TO STOP
MAIN0070 DS    0H
         L     R1,DSPCREQ_RECORD_LENGTH
         CLC   FT_CCSID,=AL2(FT_BINARYTAG)
         BE    MAIN0080                         BINARY
         LA    R1,1(,R1)                        PLUS LENGTH OF LF
MAIN0080 DS    0H
         ITRACE ID=NEXTRECD,                                           +
               RDATA1=R8,                                              +
               RDATA2=R9
         AR    R8,R1                            UPDATE BUFFER ADDRESS
         SR    R9,R1                            UPDATE LENGTH REMAINING
         BH    MAIN0020                         NEXT RECORD
         B     MAIN0010                         READ ANOTHER BUFFER
MAIN0090 DS    0H
         ITRACE ID=ZERO
         B     MAIN0110
MAIN0100 DS    0H
         CLI   DSPCREQ_RC,$DSPCREQ_FULL
         BE    MAIN0110                         YES
         ITRACE ID=DSPC_ERR,                                           +
               DATA1=(DSPCREQ_RC,1)
         DC    H'0'
MAIN0110 DS    0H
         ITRACE ID=CLOSE
         CALL  BPX1CLO,                                                +
               (WORK_FILE_DESCRIPTOR,                                  +
               WORK_RETURN_VALUE,                                      +
               WORK_RETURN_CODE,                                       +
               WORK_REASON_CODE),                                      +
               VL,                                                     +
               MF=(E,WORK_HFS_PARMLIST)
         ITRACE ID=CLOSE_RC,                                           +
               DATA1=(WORK_RETURN_VALUE,4),                            +
               DATA2=(WORK_RETURN_CODE,4)
         ICM   R9,15,WORK_RETURN_VALUE
         BM    ERR0050                          NEGATIVE.. ERROR
         SR    R3,R3                            SET RC TO ZERO
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0010  DS    0H
         LA    R3,1
         B     ERR1000
ERR0020  DS    0H
         LA    R3,2
         B     ERR1000
ERR0030  DS    0H
         LA    R3,3
         MVC   COMM_INFO_01(2),2(R8)
         MVC   COMM_INFO_02,4(R8)
         B     ERR1010
ERR0040  DS    0H
         LA    R3,4
         B     ERR1000
ERR0050  DS    0H
         LA    R3,5
ERR1000  DS    0H
         MVC   COMM_INFO_01,WORK_RETURN_VALUE
         MVC   COMM_INFO_02,WORK_RETURN_CODE
         MVC   COMM_INFO_03,WORK_REASON_CODE
ERR1010  DS    0H
         STCM  R3,3,COMM_MSG_ID
         L     R15,COMM_V_OSMSG
         BALR  R14,R15
         LA    R3,8
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=EXIT
         LR    R1,R13                  COPY WORK AREA ADDRESS
         L     R13,4(,R13)            RESTORE SAVE AREA ADDRESS
         FREEMAIN RU,                                                  +
               A=(1),                                                  +
               LV=OSHFSSEQ_WORK_L
         L     R14,12(,R13)
         LR    R15,R3                  SET RC
         LM    R0,R12,20(R13)          RESTORE REMAINING REGISTERS
         BR    R14                     RETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
F0                   DC      F'0'
EDIT                 DC      CL8'EDIT'
HFS                  DC      CL8'HFS'
                     LTORG

OSHFSSEQEND          EQU     *
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
OSHFSSEQ_WORK        DSECT
                     COPY    DXDPREF
WORK_FILE_DESCRIPTOR DS      F
WORK_RETURN_VALUE    DS      F
WORK_RETURN_CODE     DS      F
WORK_REASON_CODE     DS      F
WORK_HFS_PARMLIST    DS      10F

                     BPXYOPNF DSECT=NO,LIST=YES
                     DSPCREQ  DSECT=NO

OSHFSSEQ_WORK_L      EQU     *-OSHFSSEQ_WORK
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
          COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
          SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
          BPXYSTAT DSECT=YES,LIST=YES
          BPXYMODE DSECT=YES,LIST=YES
          BPXYFTYP LIST=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
          COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
          COPY     REGEQU
NEW_LINE  EQU      X'15'
$BUFFSIZE EQU      65535
          END      OSHFSSEQ
