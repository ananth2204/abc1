         MACRO
         FDEF  &F,&D,&T
         DC    AL2(&F-STAT)
         DC    CL12&D
         DC    CL1'&T'
         MEND

* ------------------------------------------------------------------- *
*                                                                     *
*  Module name: OSHDIR                                                *
*                                                                     *
*  Build main body for HFS directories                                *
*                                                                     *
* ------------------------------------------------------------------- *
OSHDIR   CSECT
OSHDIR   AMODE 31
OSHDIR   RMODE ANY
         USING OSCOMM,R12
         USING SESSION,R11
         USING OSHDIR,R15
         B     INIT0000
MODID    DC    CL8'OSHDIR'
         DC    CL8'&SYSDATE'
         DC    CL8'&SYSTIME'
         DC    A(OSHDIREND-OSHDIR)
INIT0000 DS    0H
         USING OSCOMM,R12                       COMMON AREA BASE
         STM   R14,R12,12(R13)                  SAVE REGS
         LR    R10,R15                          COPY ENTRY POINT
         LA    R9,2048(,R10)                    SET 2ND BASE REGISTER
         LA    R9,2048(,R9)                     ..........
         LA    R6,2048(,R9)                     SET 3RD BASE REGISTER
         LA    R6,2048(,R6)                     ..........
         DROP  R15
         USING OSHDIR,R10,R9,R6                 DEFINE BASE
         L     R4,COMM_OSSPFD                   SPF DATA
         USING OSSPFD,R4                        DEFINE BASE
         CLI   SESS_FORMAT_FLAGS,$FORMAT_INITIALIZE
         BNE   INIT0010
         XC    SESS_DXD_ADDR,SESS_DXD_ADDR
         BNZ   ERR0010
         LA    R0,OSHDIR_DXD_L
         ST    R0,SESS_DXD_LENGTH
         GETMAIN RU,                                                   +
               LV=OSHDIR_DXD_L,                                        +
               LOC=BELOW
         ST    R1,SESS_DXD_ADDR
         LR    R2,R1
         LR    R0,R1
         LA    R1,OSHDIR_DXD_L
         SR    R14,R14
         SR    R15,R15
         MVCL  R0,R14
         ST    R13,4(,R2)                       SAVE R13
         ST    R2,8(,R13)                       CHAIN SAVE AREA
         LR    R13,R2                           COPY WORK AREA ADDRESS
         USING OSHDIR_WORK,R13                  DEFINE WORK AREA BASE
         USING S_MODE,ST_MODE                   DEFINE BASE
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY1
         LA    R0,DATASPACE_1
         ST    R0,DSPCREQ_DATASPACE
         LA    R0,DXD_RECORD
         ST    R0,DSPCREQ_RECORD_ADDR
         ITRACE ID=I_O_AREA,                                           +
               RDATA1=R1
         MVC   DXD_OPEN(OPEN_L),OPEN_I
         MVC   DXD_CLOSE(CLOSE_L),CLOSE_I
         MVC   SESS_DISP_PANEL,HDIR_PANEL       SET PANEL NAME
         LA    R1,SESS_DISP_PANEL
         MVI   COMM_VDATA_FUNC,$VDATA_GETMAIN
         L     R15,COMM_V_OSVDATA
         BALR  R14,R15                          GETMAIN VDATA STORAGE
         ST    R0,DXD_VDATA1_LENGTH             SAVE VDATA LENGTH
         ST    R1,DXD_VDATA1_ADDR               SAVE VDATA ADDR
         ITRACE ID=VDATA1,                                             +
               DATA1=(DXD_VDATA1_ADDR,4),                              +
               DATA2=(DXD_VDATA1_LENGTH,4)
         LA    R1,SESS_DISP_PANEL
         L     R15,COMM_V_OSVDATA
         BALR  R14,R15                          GETMAIN VDATA STORAGE
         ST    R0,DXD_VDATA2_LENGTH             SAVE VDATA LENGTH
         ST    R1,DXD_VDATA2_ADDR               SAVE VDATA ADDR
         ITRACE ID=VDATA2,                                             +
               DATA1=(DXD_VDATA2_ADDR,4),                              +
               DATA2=(DXD_VDATA2_LENGTH,4)
         MVC   DXD_VWIDTH,SPF_VWIDTH            SAVE WIDTH
         MVC   DXD_VDEPTH,SPF_VDEPTH            SAVE DEPTH
         XC    DXD_CURSOR,DXD_CURSOR
         B     EXIT0000
INIT0010 DS    0H
         ICM   R15,15,SESS_DXD_ADDR
         BZ    ERR0070
         ST    R13,4(,R15)
         ST    R15,8(,R13)
         LR    R13,R15
         ITRACE ID=ENTRY2,                                             +
               DATA1=(SESS_FORMAT_FLAGS,1),                            +
               DATA2=(COMM_SESS_FUNC,1)
         XC    DXD_RC,DXD_RC
         CLI   SESS_FORMAT_FLAGS,$FORMAT_CONTROL
         BE    EXIT0000
         CLI   SESS_FORMAT_FLAGS,$FORMAT_CLEANUP
         BE    CLEAN000
         CLI   COMM_SESS_FUNC,$SESS_SWITCH      SESSION SWITCH?
         BNE   MAIN0000                         NO
         TM    DXD_FLAGS,$DXD_INIT              INIT FLAG ON?
         BNO   MAIN0000                         NO
         L     R7,DXD_LINES_REMAINING           RESTORE LINES REMAINING
         L     R8,DXD_CURRENT_VDATA             RESTORE VDATA ADDR
         ITRACE ID=RESUME,                                             +
               RDATA1=R8,                                              +
               RDATA2=R7
         LTR   R7,R7                            RESUMING LINE COMMAND?
         BZ    MAIN0120                         NO
         B     MAIN0160
* ------------------------------------------------------------------- *
*          (re)build display                                          *
* ------------------------------------------------------------------- *
MAIN0000 DS    0H
         OI    DXD_FLAGS,$DXD_INIT              SET INIT FLAG
         MVC   DSPCREQ_RECORD_NBR,DATASPACE_1_DISPLAY_FIRST_RECORD
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0010 DS    0H
         L     R0,DXD_VDATA1_ADDR
         L     R1,DXD_VDATA1_LENGTH
         SR    R14,R14
         SR    R15,R15
         MVCL  R0,R14                           INITIALIZE BUFFER
         XC    DXD_LINES,DXD_LINES              LINES BUILT
         L     R7,DXD_VDEPTH                    LINES AVAILABLE
         L     R8,DXD_VDATA1_ADDR               BUFFER ADDRESS
* ------------------------------------------------------------------- *
*        "Retrieve" data and build body of SPF panel                  *
* ------------------------------------------------------------------- *
MAIN0020 DS    0H
         ITRACE ID=RETRIEVE,                                           +
               DATA1=(DSPCREQ_RECORD_NBR,4),                           +
               DATA2=(DATASPACE_1_LAST_RECORD,4)
         CLC   DSPCREQ_RECORD_NBR,DATASPACE_1_LAST_RECORD
         BH    MAIN0120                         END OF RECORDS REACHED
         MVI   DSPCREQ_FUNC,$DSPCREQ_RETRIEVE
         ITRACE ID=GET_RECD,                                           +
               DATA1=(DSPCREQ_RECORD_NBR,4),                           +
               DATA2=(DSPCREQ_RECORD_ADDR,4)
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE
         BALR  R14,R15
         CLI   DSPCREQ_RC,$DSPCREQ_OK           SUCCESSFUL?
         BNE   ERR0020                          NO.. DATASPACE ERROR
         MVC   0(MSG01_L,R8),MSG01              INITIALIZE MESSAGE
         USING MSG01,R8
         CLI   DXD_STAT_STATUS,$STATUS_ERROR    BPXYSTAT INFO GOOD?
         BE    MAIN0040                         NO
         TM    DXD_STAT_STATUS,$STATUS_DELETED  DELETED?
         BO    MAIN0030                         YES
         BAL   R5,MODE0000                      PROCESS MODE
         MVC   MSG01_ATTR2,DXD_ATTR             COPY ATTRIBUTE
         MVC   MSG01_MODE,DXD_MODE              COPY MODE INFO
         LA    R2,ST_UID                        ADDRESS OF UID
         BAL   R5,UID0000                       PROCESS UID
         MVC   MSG01_USER,DXD_UID+11            COPY CONVERTED UID
         LA    R2,ST_GID                        ADDRESS OF GID
         BAL   R5,GID0000                       PROCESS GID
         MVC   MSG01_GROUP,DXD_GID+11           COPY CONVERTED GID
         B     MAIN0050
MAIN0030 DS    0H
         MVC   MSG01_MODE,DELETED               FILE/DIRECTORY DELETED
         MVI   MSG01_ATTR1,$SCREEN_ATTR_NORMAL  DON'T ALLOW SELECT
         B     MAIN0050
MAIN0040 DS    0H
         MVI   MSG01_ATTR1,$SCREEN_ATTR_NORMAL  DON'T ALLOW SELECT
         MVC   MSG01_MODE,UNKNOWN               INFO NOT AVAILABLE
MAIN0050 DS    0H
         MVI   MSG01_ATTR3,$SCREEN_ATTR_NORMAL  DEFAULT TO NORMAL
         TM    DXD_STAT_STATUS,$STATUS_TAGGED   TAGGED MEMBER?
         BNO   MAIN0060                         NO
         MVI   MSG01_ATTR3,$SCREEN_ATTR_HIGH    SET ATTRIBUTE
MAIN0060 DS    0H
         L     R1,ST_SIZE_L                     LOWER WORD OF LENGTH
         CVD   R1,COMM_DWORD                    CONVERT TO DECIMAL
         ED    MSG01_NAME_LENGTH,COMM_DWORD+3   EDIT LENGTH
         LH    R1,DXD_NAME_LENGTH               NAME LENGTH
         CH    R1,=Y(L'MSG01_NAME)              TOO LONG?
         BNH   MAIN0070                         NO
         LH    R1,=Y(L'MSG01_NAME)              LIMIT LENGTH
MAIN0070 DS    0H
         BCTR  R1,0                             FOR EXECUTE
         EX    R1,NAME_MVC1                     COPY NAME
         CLC   DXD_NAME+(L'MSG01_NAME)(L'DXD_NAME-L'MSG01_NAME),COMM_BL+
               ANKS
         BE    MAIN0080                         ALL BLANK
         MVI   MSG01_NAME+(L'MSG01_NAME-1),C'+'
MAIN0080 DS    0H
         LTR   R1,R1                            LENGTH ZERO (1 REALLY)?
         BNZ   MAIN0090                         NO
         CLI   DXD_NAME,C'.'                    CURRENT DIRECTORY?
         BNE   MAIN0100                         NO
         MVI   MSG01_ATTR1,$SCREEN_ATTR_NORMAL SET ATTRIBUTE
         B     MAIN0100
MAIN0090 DS    0H
         CH    R1,H1                            LENGTH 1 (TWO REALLY)?
         BH    MAIN0100                         NO
         CLC   DOT_DOT,DXD_NAME                 PARENT DIRECTORY?
         BNE   MAIN0100                         NO
         MVI   MSG01_ATTR1,$SCREEN_ATTR_NORMAL SET ATTRIBUTE
MAIN0100 DS    0H
         LH    R1,DXD_LINES                     LINES BUILT
         LA    R1,1(,R1)                        PLUS 1
         STH   R1,DXD_LINES                     SAVE TOTAL
         A     R8,SPF_VWIDTH                    NEXT LINE
         BCT   R7,MAIN0110
         B     MAIN0120                         SCREEN FULL
MAIN0110 DS    0H
         L     R1,DSPCREQ_RECORD_NBR            CURRENT RECORD NUMBER
         LA    R1,1(,R1)                        PLUS 1
         ST    R1,DSPCREQ_RECORD_NBR            UPDATE RECORD NUMBER
         B     MAIN0020
         USING MSG01,R8
NAME_MVC1 MVC   MSG01_NAME(0),DXD_NAME
         DROP  R8
* ------------------------------------------------------------------- *
*                                                                     *
*        Detail info has been built.                                  *
*        Build DSCB info                                              *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0120 DS    0H
         ITRACE ID=OSDSCB
         MVC   DATASPACE_1_DISPLAY_LAST_RECORD,DSPCREQ_RECORD_NBR
         L     R15,COMM_V_OSDSCB                OSDSCB ENTRY POINT
         BALR  R14,R15                          FORMAT THE DSCB INFO
* ------------------------------------------------------------------- *
*                                                                     *
*        Data has been built.                                         *
*        Display the panel.                                           *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0130 DS    0H
         ITRACE ID=DISPLAY1
         MVC   SESS_DISP_PANEL,HDIR_PANEL
         MVC   SESS_DISP_VDATA_ADDR,DXD_VDATA1_ADDR
         MVC   SESS_DISP_VDATA_LENGTH,DXD_VDATA1_LENGTH
         MVC   SESS_DISP_CURSOR,DXD_CURSOR
         OI    COMM_FLAGS,$COMM_NO_UPPER        DON'T TRANSLATE
         NI    DXD_FLAGS,255-$CONFIRM_ALL       RESET CONFIRM ALL
         L     R15,COMM_V_OSDISP
         BALR  R14,R15                          LINK TO OSDISP
         NI    COMM_FLAGS,255-$COMM_NO_UPPER    RESET INDICATOR
         ITRACE ID=DISP_RC,                                            +
               DATA1=(SESS_RC,2),                                      +
               DATA2=(COMM_SESS_FUNC,1)
         CLC   COMM_NEW_FORMAT,COMM_BLANKS      NEW FORMAT WANTED?
         BNE   EXIT0000                         YES
         CLI   COMM_SESS_FUNC,0                 SESSION FUNCTION?
         BNE   SESS0000                         YES
         OC    SESS_RC,SESS_RC                  RC ZERO?
         BZ    MAIN0140                         YES
         ITRACE ID=ENDING
         MVI   COMM_SESS_FUNC,$SESS_REMOVE      SET FUNCTION
         B     EXIT0000
* ------------------------------------------------------------------- *
*        Process any line command(s)                                  *
* ------------------------------------------------------------------- *
MAIN0140 DS    0H
         NI    DXD_FLAGS,255-$LINE_CMD          RESET INDICATOR
         MVC   DSPCREQ_RECORD_NBR,DATASPACE_1_DISPLAY_FIRST_RECORD
         LH    R7,DXD_LINES                     LINES OF DISPLAY BUILT
         L     R8,DXD_VDATA1_ADDR               SPF DISPLAY BUFFER
         ITRACE ID=CMDS,                                               +
               RDATA1=R8,                                              +
               RDATA2=R7
         LA    R1,MSG01_SELECTION-MSG01+1
         ST    R1,DXD_CURSOR
MAIN0150 DS    0H
         USING MSG01,R8
         OI    MSG01_SELECTION,C' '             'TRANSLATE'
         CLI   MSG01_SELECTION,C' '             BLANK?
         BE    MAIN0170                         YES
         MVI   SESS_COMMAND_FLAGS,$SESS_COMMAND_SEARCH
         LA    R1,MSG01_SELECTION               COMMAND ADDRESS
         ST    R1,SESS_COMMAND_ADDRESS          SET ADDRESS
         MVC   SESS_COMMAND_LENGTH,H1           SET LENGTH
         LA    R0,LINE_COMMANDS                 LIST OF COMMANDS
         ST    R0,COMM_MAIN_COMMAND_TABLE
         XC    COMM_SUB_COMMAND_TABLE,COMM_SUB_COMMAND_TABLE
         L     R15,COMM_V_OSCMD                 OSCMD ENTRY POINT
         BALR  R14,R15                          SEARCH COMMANDS
         TM    SESS_COMMAND_FLAGS,$SESS_COMMAND_FOUND
         BOR   R15                              BRANCH TO COMMAND
         DROP  R8
         MVC   COMM_INFO_01(1),MSG01_SELECTION
         MVI   COMM_MSG_ID+1,1                  SET MESSAGE ID
         B     ERR1000
MAIN0160 DS    0H
         OI    DXD_FLAGS,$LINE_CMD              LINE CMD WAS FOUND
MAIN0170 DS    0H
         L     R1,DSPCREQ_RECORD_NBR            RECORD NUMBER
         A     R1,F1                            PLUS 1
         ST    R1,DSPCREQ_RECORD_NBR            SAVE RECORD NUMBER
MAIN0180 DS    0H
         L     R1,DXD_CURSOR                    CURRENT CURSOR POSITION
         A     R1,DXD_VWIDTH                    PLUS WIDTH
         ST    R1,DXD_CURSOR                    UPDATE CURSOR POSITION
         A     R8,DXD_VWIDTH                    NEXT LINE
         BCT   R7,MAIN0150                      LOOP
         XC    DXD_CURSOR,DXD_CURSOR            CLEAR CURSOR POSITION
         TM    DXD_FLAGS,$LINE_CMD              LINE COMMAND FOUND?
         BO    MAIN0000                         YES
* ------------------------------------------------------------------- *
*                                                                     *
*       No line command(s) were found.                                *
*                                                                     *
*       Process any primary command.                                  *
*                                                                     *
* ------------------------------------------------------------------- *
         CLC   SPF_ZCMD,COMM_BLANKS             ALL BLANK?
         BE    MAIN0210                         YES
         ITRACE ID=PRI_CMD,                                            +
               DATA1=SPF_ZCMD+00,                                      +
               DATA2=SPF_ZCMD+08
         SR    R3,R3
         ICM   R3,3,COMM_OPERANDS_NBR
         BZ    MAIN0210                         NO OPERANDS
         CH    R3,H1                            MORE THAN ONE OPERAND?
         BH    MAIN0190                         YES.. NOT A NEW PATH
         CLI   SPF_ZCMD,C'/'                    A NEW PATH?
         BE    MAIN1200                         YES
MAIN0190 DS    0H
         MVC   SESS_COMMAND_ADDRESS,OPERAND_01_ADDRESS
         MVC   SESS_COMMAND_LENGTH,OPERAND_01_LENGTH
         MVI   SESS_COMMAND_FLAGS,$SESS_COMMAND_SEARCH
         LA    R0,PRIMARY_COMMANDS              COMMAND TABLE
         ST    R0,COMM_MAIN_COMMAND_TABLE
         XC    COMM_SUB_COMMAND_TABLE,COMM_SUB_COMMAND_TABLE
         L     R15,COMM_V_OSCMD                 OSCMD ENTRY POINT
         BALR  R14,R15                          SEARCH COMMANDS
         TM    SESS_COMMAND_FLAGS,$SESS_COMMAND_FOUND
         BOR   R15
         B     ERR0030                          COMMAND NOT FOUND
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0200 DS    0H
         ITRACE ID=REFRESH
         MVC   SPF_ZCMD,COMM_BLANKS              RESET COMMAND
         MVI   DSPCREQ_FUNC,$DSPCREQ_CLEAR       DATA SPACE FUNCTION
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE
         BALR  R14,R15                           CLEAR DATA SPACE
         MVI   LOADREQ_FUNC,$LOADREQ_LOAD
         MVC   LOADREQ_MODULE,MODULE_OSHFSDIR    MODULE NAME
         LA    R1,LOADREQ
         L     R15,COMM_V_OSLOAD
         BALR  R14,R15                           LOAD OSHFSDIR
         ICM   R15,15,LOADREQ_EP                 OSHFSDIR ENTRY POINT
         BALR  R14,R15                           LINK TO OSHFSDIR
         ITRACE ID=HFSDIRRC,                                           +
               RDATA1=R15
         CLC   DATASPACE_1_DISPLAY_FIRST_RECORD,DATASPACE_1_LAST_RECORD
         BNH   MAIN0000                          DISPLAY
         B     MAIN0280                          GO TO BOTTOM
* ------------------------------------------------------------------- *
*                                                                     *
*        We have not processed any command.                           *
*        Check for a scroll command.                                  *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0210 DS    0H
         ICM   R1,15,SPF_ZSCROLLN               LINES TO SCROLL
         BZ    MAIN0220                         ZERO..
         BCTR  R1,0                             MINUS 1 (HEADING LINE)
MAIN0220 DS    0H
         ST    R1,SESS_VERTICAL_SCROLL
         MVC   SESS_HORIZONTAL_SCROLL,SPF_ZSCROLLN
         ITRACE ID=ZVERB,                                              +
               DATA1=SPF_ZVERB,                                        +
               DATA2=(SPF_ZSCROLLN,4)
         CLI   SPF_ZVERB,C'B'                   BOTTOM?
         BE    MAIN0280                         YES
         CLI   SPF_ZVERB,C'D'                   SCROLL DOWN?
         BE    MAIN0250                         YES
         CLI   SPF_ZVERB,C'L'                   SCROLL LEFT?
         BE    MAIN0130                         YES
         CLI   SPF_ZVERB,C'R'                   SCROLL RIGHT?
         BE    MAIN0130                         YES
         CLI   SPF_ZVERB,C'T'                   TOP?
         BE    MAIN0270                         YES
         CLI   SPF_ZVERB,C'U'                   SCROLL UP?
         BE    MAIN0230                         YES
         B     MAIN0130                         DISPLAY SAME DATA AGAIN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0230 DS    0H
         ITRACE ID=UP
         MVC   SPF_ZCMD,COMM_BLANKS             CLEAR ZCMD
         CLI   SPF_ZSCROLLA,C'M'                MAX?
         BE    MAIN0270                         YES
         L     R1,SESS_VERTICAL_SCROLL          RECORDS TO SCROLL
         L     R2,DATASPACE_1_DISPLAY_FIRST_RECORD  FIRST DISPLAYED
         SR    R2,R1                            MINUS SCROLL AMOUNT
         C     R2,DATASPACE_1_FIRST_RECORD      LESS THAN FIRST RECORD?
         BNL   MAIN0240                         NO
         L     R2,DATASPACE_1_FIRST_RECORD      LIMIT TO 1ST RECD
MAIN0240 DS    0H
         ST    R2,DATASPACE_1_DISPLAY_FIRST_RECORD CHANGE FIRST RECORD
         B     MAIN0000                         REBUILD EVERYTHING
* ------------------------------------------------------------------- *
*        SCROLL DOWN                                                  *
* ------------------------------------------------------------------- *
MAIN0250 DS    0H
         ITRACE ID=DOWN
         MVC   SPF_ZCMD,COMM_BLANKS             CLEAR ZCMD
         CLI   SPF_ZSCROLLA,C'M'                MAX?
         BE    MAIN0280                         YES
         L     R1,SESS_VERTICAL_SCROLL          RECORDS TO SCROLL
         L     R2,DATASPACE_1_DISPLAY_FIRST_RECORD FIRST RCD DISPLAYED
         AR    R2,R1                            PLUS RECORDS TO SCROLL
         C     R2,DATASPACE_1_LAST_RECORD       BEYOND END OF DATA?
         BH    MAIN0260                         YES
         ST    R2,DATASPACE_1_DISPLAY_FIRST_RECORD  CHANGE FIRST RECORD
         B     MAIN0000                         REBUILD EVERYTHING
MAIN0260 DS    0H
         L     R2,DATASPACE_1_LAST_RECORD       HIGHEST RECORD STORED
         S     R2,DATASPACE_1_DISPLAY_FIRST_RECORD   MINUS 1ST ON DISP
         C     R2,DXD_VDEPTH                    MORE THAN ONE SCREEN?
         BNH   MAIN0130                         NO.. STAY PUT
         L     R2,DATASPACE_1_LAST_RECORD       HIGHEST RECORD STORED
         S     R2,DXD_VDEPTH                    MINUS 1 SCREEN
         A     R2,F2                            PLUS 2
         ST    R2,DATASPACE_1_DISPLAY_FIRST_RECORD   SET FIRST TO DISP
         B     MAIN0000                         REBUILD EVERYTHING
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0270 DS    0H
         ITRACE ID=TOP
         MVC   SPF_ZCMD,COMM_BLANKS             CLEAR ZCMD
        MVC   DATASPACE_1_DISPLAY_FIRST_RECORD,DATASPACE_1_FIRST_RECORD
         XC    SESS_LAST_FOUND,SESS_LAST_FOUND
         B     MAIN0000                         REBUILD EVERYTHING
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0280 DS    0H
         ITRACE ID=BOTTOM
         MVC   SPF_ZCMD,COMM_BLANKS             CLEAR ZCMD
         L     R2,DATASPACE_1_LAST_RECORD       HIGHEST RECORD
         S     R2,DXD_VDEPTH                    MINUS LINES ON DISPLAY
         A     R2,F2                            PLUS 2
         CH    R2,H1                            STILL HAVE AT LEAST 1?
         BNL   MAIN0290                         YES
         LH    R2,H1                            FORCE RECORD 1
MAIN0290 DS    0H
         ST    R2,DATASPACE_1_DISPLAY_FIRST_RECORD  CHANGE FIRST RECORD
         B     MAIN0000                         REBUILD EVERYTHING
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0300 DS    0H
         ITRACE ID=PRIM_SEL
         CH    R3,H2                            TWO OPERANDS?
         BH    ERR0050                          TOO MANY
         NI    DXD_FLAGS,255-$NEW_FILE          RESET NEW FILE FLAG
         MVC   DSPCREQ_FILE,COMM_BLANKS
         ICM   R14,15,OPERAND_02_ADDRESS
         SR    R15,R15
         ICM   R15,3,OPERAND_02_LENGTH
         BCTR  R15,0
         EX    R15,PATH_MVC1                    COPY FILE NAME
         ITRACE ID=LOOKUP,                                             +
               DATA1=(DSPCREQ_FILE+0,8),                               +
               DATA2=(DSPCREQ_FILE+8,8)
         XC    DSPCREQ_RECORD_NBR,DSPCREQ_RECORD_NBR
         MVI   DSPCREQ_FUNC,$DSPCREQ_FIND_FILE_S
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE
         BALR  R14,R15
         ITRACE ID=DSPC_RC,                                            +
               DATA1=(DSPCREQ_RC,1)
         CLI   DSPCREQ_RC,$DSPCREQ_OK           SUCCESSFUL?
         BE    MAIN0310                         YES
         TM    SESS_EDIT_FLAGS,$SESS_EDIT       EDIT SESSION?
         BNO   ERR0060                          NO.. NOT FOUND
         OI    DXD_FLAGS,$NEW_FILE              SET FLAG
MAIN0310 DS    0H
         ITRACE ID=BLD_NEW
         GETMAIN RU,                                                   +
               LV=SESSION_L,                                           +
               LOC=ANY
         LR    R3,R1                            SAVE ADDRESS
NEW      USING SESSION,R3
         ST    R3,COMM_NEW_SESSION
         LR    R0,R3
         LA    R1,SESSION_L
         LR    R14,R11                          CURRENT SESSION ADDR
         LR    R15,R1                           COPY LENGTH
         MVCL  R0,R14                           COPY SESSION BLOCK
         L     R1,SESS_NEXT                     NEXT ON CHAIN
NEXT     USING SESSION,R1
         ST    R1,NEW.SESS_NEXT                 SET NEXT IN NEW BLOCK
         ST    R11,NEW.SESS_PREV                SET PREV IN NEW BLOCK
         ST    R3,SESS_NEXT                     SET NEXT IN CURRENT
         LTR   R1,R1                            'NEXT' BLOCK?
         BZ    MAIN0320                         NO
         ST    R3,NEXT.SESS_PREV                SET PREV IN NEXT BLOCK
         DROP  NEXT
MAIN0320 DS    0H
         MVI   NEW.SESS_FORMAT_FLAGS,0
         XC    NEW.SESS_DXD_ADDR,NEW.SESS_DXD_ADDR
         XC    NEW.SESS_DXD_LENGTH,NEW.SESS_DXD_LENGTH
         XC    NEW.SESS_LAST_FOUND,NEW.SESS_LAST_FOUND
         XC    NEW.SESS_SEARCH_ARG_LENGTH,NEW.SESS_SEARCH_ARG_LENGTH
         MVC   NEW.SESS_SEARCH_ARG,COMM_BLANKS
         MVC   NEW.SESS_DSN,COMM_BLANKS
         MVC   NEW.SESS_MEMBER,COMM_BLANKS
         MVC   NEW.SESS_PATH,SESS_PATH
         XC    NEW.SESS_DISP_VDATA_ADDR,NEW.SESS_DISP_VDATA_ADDR
         XC    NEW.SESS_DISP_VDATA_LENGTH,NEW.SESS_DISP_VDATA_ADDR
         XC    NEW.SESS_DCB_ADDR,NEW.SESS_DCB_ADDR
         XC    NEW.SESS_DCB_LENGTH,NEW.SESS_DCB_LENGTH
         XC    NEW.DATASPACE_1_ALET,NEW.DATASPACE_1_ALET
         XC    NEW.DATASPACE_1_TOKEN,NEW.DATASPACE_1_TOKEN
         MVC   NEW.DATASPACE_1_NAME,COMM_BLANKS
         MVC   NEW.DATASPACE_1_FIRST_RECORD,F1
         MVC   NEW.DATASPACE_1_DISPLAY_FIRST_RECORD,F1
         MVC   NEW.DATASPACE_1_DISPLAY_FIRST_COLUMN,F1
         L     R0,COMM_IO_SIZE
         GETMAIN RU,                                                   +
               LV=(0),                                                 +
               LOC=BELOW
         TM    DXD_FLAGS,$NEW_FILE              NEW FILE?
         BNO   MAIN0330                         NO
         OI    NEW.SESS_EDIT_FLAGS,$SESS_EDIT_EMPTY
         MVC   NEW.SESS_FORMAT,EDIT
         MVC   NEW.SESS_DEFAULT_FORMAT,EDIT
MAIN0330 DS    0H
         LA    R1,NEW.SESS_PATH                 NEW PATH
         L     R2,SESS_PATH_LENGTH              LENGTH SO FAR
         ITRACE ID=OLD_PATH,                                           +
               DATA1=0(R1),                                            +
               RDATA2=R2
         AR    R1,R2                            COMPUTE ADDRESS
         BCTR  R1,0                             .. OF END OF NAME
         CLI   0(R1),C'/'                       END WITH A SLASH?
         BE    MAIN0340                         YES
         LA    R1,1(,R1)                        ACCOUNT FOR SLASH
         LA    R2,1(,R2)                        ADD 1 TO PREFIX LENGTH
         MVI   0(R1),C'/'                       INSERT DELIMITING SLASH
MAIN0340 DS    0H
         LA    R1,1(,R1)
         LA    R14,L'NEW.SESS_PATH              LENGTH OF FIELD
         SR    R14,R2                           MINUS LENGTH OF PREFIX
         BCTR  R14,0                            MINUS LENGTH OF SLASH
         CLM   R14,3,OPERAND_02_LENGTH
         BL    MAIN0350
         ICM   R14,3,OPERAND_02_LENGTH
MAIN0350 DS    0H
         BCTR  R14,0                            FOR EXECUTE
         ICM   R15,15,OPERAND_02_ADDRESS
         EX    R14,PATH_MVC2                    COPY NEW
MAIN0360 DS    0H
         CLI   0(R1),C' '                       BLANK?
         BE    MAIN0370
         LA    R1,1(,R1)
         LA    R2,1(,R2)
         B     MAIN0360
MAIN0370 DS    0H
         ST    R2,NEW.SESS_PATH_LENGTH
         ITRACE ID=NEW_PATH,                                           +
               DATA1=NEW.SESS_PATH,                                    +
               RDATA2=R2
         MVI   NEW.SESS_KEYWORD_FLAGS,$KEYWORD_PATH
         MVI   COMM_SESS_FUNC,$SESS_SWITCH
         B     EXIT0000                         EXIT
PATH_MVC1  MVC DSPCREQ_FILE(0),0(R14)
PATH_MVC2  MVC 0(0,R1),0(R15)
         DROP  NEW
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0400 DS    0H
         USING MSG01,R8
         ITRACE ID=LINE_SEL,                                           +
               DATA1=MSG01_NAME+0,                                     +
               DATA2=MSG01_NAME+8
         MVC   DXD_SELECTION,MSG01_SELECTION    SAVE SELECTION
         MVI   MSG01_SELECTION,C' '             RESET THE SELECTION
         MVI   DSPCREQ_FUNC,$DSPCREQ_RETRIEVE   SET FUNCTION
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE
         BALR  R14,R15                          RETRIEVE DATA
         CLI   DSPCREQ_RC,$DSPCREQ_OK           SUCCESSFUL?
         BNE   ERR0020                          NO
         ITRACE ID=BLD_NEW
         GETMAIN RU,                                                   +
               LV=SESSION_L,                                           +
               LOC=ANY
         LR    R3,R1                            SAVE ADDRESS
NEW      USING SESSION,R3
         ST    R3,COMM_NEW_SESSION
         LR    R0,R3
         LA    R1,SESSION_L
         LR    R14,R11                          CURRENT SESSION ADDR
         LR    R15,R1                           COPY LENGTH
         MVCL  R0,R14                           COPY SESSION BLOCK
         L     R1,SESS_NEXT                     NEXT ON CHAIN
NEXT     USING SESSION,R1
         ST    R1,NEW.SESS_NEXT                 SET NEXT IN NEW BLOCK
         ST    R11,NEW.SESS_PREV                SET PREV IN NEW BLOCK
         ST    R3,SESS_NEXT                     SET NEXT IN CURRENT
         LTR   R1,R1                            'NEXT' BLOCK?
         BZ    MAIN0410                         NO
         ST    R3,NEXT.SESS_PREV                SET PREV IN NEXT BLOCK
         DROP  NEXT
MAIN0410 DS    0H
         XC    NEW.SESS_DXD_ADDR,NEW.SESS_DXD_ADDR
         XC    NEW.SESS_DXD_LENGTH,NEW.SESS_DXD_LENGTH
         XC    NEW.SESS_LAST_FOUND,NEW.SESS_LAST_FOUND
         XC    NEW.SESS_SEARCH_ARG_LENGTH,NEW.SESS_SEARCH_ARG_LENGTH
         MVC   NEW.SESS_SEARCH_ARG,COMM_BLANKS
         MVC   NEW.SESS_DEFAULT_FORMAT,COMM_BLANKS
         MVC   NEW.SESS_FORMAT,COMM_BLANKS
         MVC   NEW.SESS_DATA_LOADER,COMM_BLANKS
         MVC   NEW.SESS_FUNCTION_MODULE,COMM_BLANKS
         MVC   NEW.SESS_EXIT_MODULE,COMM_BLANKS
         MVC   NEW.SESS_DSN,COMM_BLANKS
         MVC   NEW.SESS_MEMBER,COMM_BLANKS
         MVC   NEW.SESS_PATH,SESS_PATH
         XC    NEW.SESS_DISP_VDATA_ADDR,NEW.SESS_DISP_VDATA_ADDR
         XC    NEW.SESS_DISP_VDATA_LENGTH,NEW.SESS_DISP_VDATA_ADDR
         MVC   NEW.SESS_PATH,SESS_PATH
         MVC   NEW.SESS_PATH_LENGTH,SESS_PATH_LENGTH
         MVI   NEW.SESS_FORMAT_FLAGS,0
         MVC   NEW.DATASPACE_1_FIRST_RECORD,F1
         MVC   NEW.DATASPACE_1_DISPLAY_FIRST_RECORD,F1
         MVC   NEW.DATASPACE_1_DISPLAY_FIRST_COLUMN,F1
         XC    NEW.DATASPACE_1_ALET,NEW.DATASPACE_1_ALET
         XC    NEW.DATASPACE_1_TOKEN,NEW.DATASPACE_1_TOKEN
         MVC   NEW.DATASPACE_1_NAME,COMM_BLANKS
         XC    NEW.SESS_DCB_ADDR,NEW.SESS_DCB_ADDR
         XC    NEW.SESS_DCB_LENGTH,NEW.SESS_DCB_LENGTH
         CLI   DXD_SELECTION,C'E'               EDIT?
         BNE   MAIN0420                         NO
         OI    NEW.SESS_EDIT_FLAGS,$SESS_EDIT   INDICATE EDIT SESSION
MAIN0420 DS    0H
         CLI   DXD_SELECTION,C'A'               BROWSE IN ASCII MODE?
         BNE   MAIN0430                         NO
         ITRACE ID=ASCII
         OI    NEW.SESS_USER_OPTIONS,$OPTIONS_ASCII
MAIN0430 DS    0H
         L     R0,COMM_IO_SIZE
         GETMAIN RU,                                                   +
               LV=(0),                                                 +
               LOC=BELOW
         ST    R1,NEW.SESS_IO_AREA
         BAL   R5,PATH0100                      BUILD PATH
         MVC   NEW.SESS_PATH,DXD_NEW_PATH
         MVC   NEW.SESS_PATH_LENGTH,DXD_NEW_PATH_LENGTH
         ITRACE ID=NEW_PATH,                                           +
               DATA1=NEW.SESS_PATH+0,                                  +
               DATA2=NEW.SESS_PATH+8
         MVI   NEW.SESS_KEYWORD_FLAGS,$KEYWORD_PATH
         MVI   COMM_SESS_FUNC,$SESS_SWITCH
         ST    R7,DXD_LINES_REMAINING           SAVE LINES REMAINING
         ST    R8,DXD_CURRENT_VDATA             SAVE POSITION IN VDATA
         B     EXIT0000                         EXIT
PATH_MVC3 MVC   0(0,R1),MSG01_NAME
         DROP  R8,NEW
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0450 DS    0H
         ITRACE ID=FILE
         ST    R7,DXD_LINES_REMAINING
         ST    R8,DXD_CURRENT_VDATA
         USING MSG01,R8
         MVI   MSG01_SELECTION,C' '             RESET THE SELECTION
         DROP  R8
         MVI   DSPCREQ_FUNC,$DSPCREQ_RETRIEVE   SET FUNCTION
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE
         BALR  R14,R15                          RETRIEVE DATA
         CLI   DSPCREQ_RC,$DSPCREQ_OK           SUCCESSFUL?
         BNE   ERR0020                          NO
* ------------------------------------------------------------------- *
*        Create pipes                                                 *
* ------------------------------------------------------------------- *
         ITRACE ID=PIPE1
         CALL  BPX1PIP,                         CREATE PIPE 1          +
               (DXD_PIPE1_READ,                                        +
               DXD_PIPE1_WRITE,                                        +
               DXD_RETURN_VALUE,                                       +
               DXD_RETURN_CODE,                                        +
               DXD_REASON_CODE),                                       +
               VL,                                                     +
               MF=(E,DXD_CALL_PARMS)
         ITRACE ID=RC,                                                 +
               DATA1=(DXD_RETURN_VALUE,4),                             +
               DATA2=(DXD_RETURN_CODE,4)
         TM    DXD_RETURN_VALUE,X'80'           SUCCESSFUL?
         BO    ERR0080                          NO
         ITRACE ID=PIPE2
         CALL  BPX1PIP,                         CREATE PIPE 2          +
               (DXD_PIPE2_READ,                                        +
               DXD_PIPE2_WRITE,                                        +
               DXD_RETURN_VALUE,                                       +
               DXD_RETURN_CODE,                                        +
               DXD_REASON_CODE),                                       +
               VL,                                                     +
               MF=(E,DXD_CALL_PARMS)
         ITRACE ID=RC,                                                 +
               DATA1=(DXD_RETURN_VALUE,4),                             +
               DATA2=(DXD_RETURN_CODE,4)
         TM    DXD_RETURN_VALUE,X'80'           SUCCESSFUL?
         BO    ERR0090                          NO
* ------------------------------------------------------------------- *
*        Spawn "file" command                                         *
* ------------------------------------------------------------------- *
         LA    R0,FILE_COMMAND
         ST    R0,DXD_ARG_1_ADDR
         LA    R0,L'FILE_COMMAND
         ST    R0,DXD_ARG_1_LENGTH
         LA    R0,DXD_ARG_1_LENGTH
         ST    R0,DXD_ARG_1_LENGTH_ADDR

         BAL   R5,PATH0100                      CONSTRUCT PATH NAME
         LA    R0,DXD_NEW_PATH
         ST    R0,DXD_ARG_2_ADDR
         MVC   DXD_ARG_2_LENGTH,DXD_NEW_PATH_LENGTH
         LA    R0,DXD_ARG_2_LENGTH
         ST    R0,DXD_ARG_2_LENGTH_ADDR

         MVC   DXD_DESCRIPTOR_0,DXD_PIPE1_READ  SET DESCRIPTOR 0
         MVC   DXD_DESCRIPTOR_1,DXD_PIPE2_WRITE SET DESCRIPTOR 1
         MVC   DXD_DESCRIPTOR_2,DXD_PIPE2_WRITE SET DESCRIPTOR 2
         ITRACE ID=SPAWN
         CALL  BPX1SPN,                                                +
               (DXD_ARG_1_LENGTH,               .. COMMAND PATH LENGTH +
               FILE_COMMAND,                    .. COMMAND PATH        +
               F2,                              .. ARGUEMENT COUNT     +
               DXD_LENGTH_VECTOR,               .. ARGUEMENT LENGTH VEC+
               DXD_ARGS_VECTOR,                 .. ARGUEMENT VECTOR    +
               F0,                              .. ENVIRONMENT COUNT   +
               F0,                              .. ENVIRONMENT LENGTH  +
               F0,                              .. ENVIRONMENT LIST    +
               F3,                              .. NBR OF DESCRIPTORS  +
               DXD_FILE_DESCRIPTORS,            .. FILE DESCRIPTORS    +
               F0,                              .. INHERIT AREA LENGTH +
               F0,                              .. INHERIT AREA        +
               DXD_RETURN_VALUE,                                       +
               DXD_RETURN_CODE,                                        +
               DXD_REASON_CODE),                                       +
               VL,                                                     +
               MF=(E,DXD_CALL_PARMS)
         ITRACE ID=RC,                                                 +
               DATA1=(DXD_RETURN_VALUE,4),                             +
               DATA2=(DXD_REASON_CODE,4)
         TM    DXD_RETURN_VALUE,X'80'           SUCCESSFUL?
         BO    ERR0100                          NO
         MVC   DXD_CHILD_PID,DXD_RETURN_VALUE   SAVE PID
         ITRACE ID=CHILDPID,                                           +
               DATA1=DXD_CHILD_PID
* ------------------------------------------------------------------- *
*        Close pipe 1 read                                            *
* ------------------------------------------------------------------- *
         ITRACE ID=CLS1READ,                                           +
               DATA1=(DXD_PIPE1_READ,4)
         CALL  BPX1CLO,                                                +
               (DXD_PIPE1_READ,                                        +
               DXD_RETURN_VALUE,                                       +
               DXD_RETURN_CODE,                                        +
               DXD_REASON_CODE),                                       +
               VL,                                                     +
               MF=(E,DXD_CALL_PARMS)
         ITRACE ID=CLOSE_RC,                                           +
               DATA1=(DXD_RETURN_VALUE,4),                             +
               DATA2=(DXD_REASON_CODE,4)
         TM    DXD_RETURN_VALUE,X'80'           SUCCESSFUL?
         BO    ERR0130                          NO
* ------------------------------------------------------------------- *
*        Close pipe 2 write                                           *
* ------------------------------------------------------------------- *
         ITRACE ID=CLS2WRT,                                            +
               DATA1=(DXD_PIPE2_WRITE,4)
         CALL  BPX1CLO,                                                +
               (DXD_PIPE2_WRITE,                                       +
               DXD_RETURN_VALUE,                                       +
               DXD_RETURN_CODE,                                        +
               DXD_REASON_CODE),                                       +
               VL,                                                     +
               MF=(E,DXD_CALL_PARMS)
         ITRACE ID=CLOSE_RC,                                           +
               DATA1=(DXD_RETURN_VALUE,4)
         TM    DXD_RETURN_VALUE,X'80'           SUCCESSFUL?
         BO    ERR0130                          NO
*        ITRACE ID=DUMMY
*        XC    DXD_DUMMY_ECB,DXD_DUMMY_ECB
*        WAIT  ECB=DXD_DUMMY_ECB
* ------------------------------------------------------------------- *
*        Close pipe 1 write                                           *
* ------------------------------------------------------------------- *
         ITRACE ID=CLS1WRT,                                            +
               DATA1=(DXD_PIPE1_WRITE,4)
         CALL  BPX1CLO,                                                +
               (DXD_PIPE1_WRITE,                                       +
               DXD_RETURN_VALUE,                                       +
               DXD_RETURN_CODE,                                        +
               DXD_REASON_CODE),                                       +
               VL,                                                     +
               MF=(E,DXD_CALL_PARMS)
         ITRACE ID=CLOSE_RC,                                           +
               DATA1=(DXD_RETURN_VALUE,4),                             +
               DATA2=(DXD_REASON_CODE,4)
         TM    DXD_RETURN_VALUE,X'80'           SUCCESSFUL?
         BO    ERR0130                          NO
* ------------------------------------------------------------------- *
*        WAIT for child process                                       *
* ------------------------------------------------------------------- *
         ITRACE ID=WAIT,                                               +
               DATA1=(DXD_CHILD_PID,4)
         CALL  BPX1WAT,                                                +
               (DXD_CHILD_PID,                  .. PID                 +
               F0,                              .. OPTIONS (NONE)      +
               DXD_WAIT_STATUS,                 .. RETURNED STATUS     +
               DXD_RETURN_VALUE,                                       +
               DXD_RETURN_CODE,                                        +
               DXD_REASON_CODE),                                       +
               VL,                                                     +
               MF=(E,DXD_CALL_PARMS)
         ITRACE ID=RC,                                                 +
               DATA1=(DXD_RETURN_VALUE,4),                             +
               DATA2=(DXD_REASON_CODE,4)
         TM    DXD_RETURN_VALUE,X'80'           SUCCESSFUL?
         BO    ERR0110                          NO
* ------------------------------------------------------------------- *
*        Clear VDATA                                                  *
* ------------------------------------------------------------------- *
         L     R0,DXD_VDATA2_ADDR
         L     R1,DXD_VDATA2_LENGTH
         SR    R14,R14
         SR    R15,R15
         MVCL  R0,R14
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         L     R2,DXD_VDATA2_ADDR
         L     R3,DXD_VDATA2_LENGTH
* ------------------------------------------------------------------- *
*        Read STDOUT from "file" command                              *
* ------------------------------------------------------------------- *
MAIN0470 DS    0H
         ITRACE ID=READ_2,                                             +
               RDATA1=R2,                                              +
               RDATA2=R3
         ST    R2,DXD_PIPE_DATA
         ST    R3,DXD_PIPE_LENGTH
         CALL  BPX1RED,                                                +
               (DXD_PIPE2_READ,                 .. FILE DESCRIPTOR     +
               DXD_PIPE_DATA,                   .. BUFFER              +
               F0,                              .. ALET                +
               DXD_PIPE_LENGTH,                 .. LENGTH              +
               DXD_RETURN_VALUE,                                       +
               DXD_RETURN_CODE,                                        +
               DXD_REASON_CODE),                                       +
               VL,                                                     +
               MF=(E,DXD_CALL_PARMS)
         ITRACE ID=READ_RC,                                            +
               DATA1=(DXD_RETURN_VALUE,4),                             +
               DATA2=(DXD_REASON_CODE,4)
         ICM   R1,15,DXD_RETURN_VALUE
         BM    ERR0120                          ERROR
         BZ    MAIN0480                         ZERO.. NO MORE DATA
         AR    R2,R1                            UPDATE BUFFER ADDRESS
         SR    R3,R1                            LENGTH LEFT
         B     MAIN0470
* ------------------------------------------------------------------- *
*        Close pipe 2 read                                            *
* ------------------------------------------------------------------- *
MAIN0480 DS    0H
         ITRACE ID=CLOSE2,                                             +
               DATA1=(DXD_PIPE2_READ,4)
         CALL  BPX1CLO,                                                +
               (DXD_PIPE2_READ,                                        +
               DXD_RETURN_VALUE,                                       +
               DXD_RETURN_CODE,                                        +
               DXD_REASON_CODE),                                       +
               VL,                                                     +
               MF=(E,DXD_CALL_PARMS)
         ITRACE ID=CLOSE_RC,                                           +
               DATA1=(DXD_RETURN_VALUE,4)
         TM    DXD_RETURN_VALUE,X'80'           SUCCESSFUL?
         BO    ERR0130                          NO
         B     MAIN0640                         DISPLAY FILE INFO
NAME_MVC MVC   COMM_INFO_02(0),DXD_NAME
* ------------------------------------------------------------------- *
*                                                                     *
*        Build "long" form info for selected file                     *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0500 DS    0H
         ST    R7,DXD_LINES_REMAINING
         ST    R8,DXD_CURRENT_VDATA
         USING MSG01,R8
         ITRACE ID=LONGINFO,                                           +
               DATA1=MSG01_NAME+0,                                     +
               DATA2=MSG01_NAME+8
         MVI   MSG01_SELECTION,C' '             RESET THE SELECTION
         DROP  R8
         MVI   DSPCREQ_FUNC,$DSPCREQ_RETRIEVE   SET FUNCTION
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE
         BALR  R14,R15                          RETRIEVE DATA
         CLI   DSPCREQ_RC,$DSPCREQ_OK           SUCCESSFUL?
         BNE   ERR0020                          NO
         L     R0,DXD_VDATA2_ADDR               2NDARY VDATA ADDR
         L     R1,DXD_VDATA2_LENGTH
         SR    R14,R14
         SR    R15,R15
         MVCL  R0,R14                           INITIALIZE VDATA
         L     R8,DXD_VDATA2_ADDR
         L     R7,SPF_VDEPTH
         MVC   COMM_INFO_01,COMM_BLANKS
         MVC   COMM_INFO_02,COMM_BLANKS
         MVI   0(R8),$SCREEN_ATTR_HIGH
         LH    R1,DXD_NAME_LENGTH               NAME LENGTH
         BCTR  R1,0                             FOR EXECUTE
         EX    R1,NAME_MVC2                     COPY NAME
         A     R8,SPF_VWIDTH
         BCTR  R7,0
         LA    R3,FIELD_TABLE                   FIRST FIELD
         USING FDEF_DSECT,R3                    DEFINE BASE
MAIN0510 DS    0H
         ITRACE ID=FIELD,                                              +
               DATA1=(FDEF_DESC,8),                                    +
               DATA2=(FDEF_TYPE,1)
         MVC   0(MSG_02_L,R8),MSG_02           INITIALIZE
         USING MSG_02,R8
         MVC   MSG_02_FIELD,FDEF_DESC          COPY FIELD DESCRIPTION
         SR    R2,R2                           CLEAR REGISTER
         ICM   R2,3,FDEF_DISP                  DISP TO DATA
         LA    R2,STAT(R2)                     COMPUTE ADDRESS OF DATA
         CLI   FDEF_TYPE,$FDEF_CHARACTER
         BE    MAIN0520
         CLI   FDEF_TYPE,$FDEF_DEV             DEVICE?
         BE    MAIN0530
         CLI   FDEF_TYPE,$FDEF_HEX_FOUR        FOUR BYTE HEX?
         BE    MAIN0540
         CLI   FDEF_TYPE,$FDEF_FULLWORD        FULLWORD?
         BE    MAIN0550
         CLI   FDEF_TYPE,$FDEF_GID             GROUP ID?
         BE    MAIN0560
         CLI   FDEF_TYPE,$FDEF_MODE            FILE MODE?
         BE    MAIN0570
         CLI   FDEF_TYPE,$FDEF_HEX_ONE         ONE BYTE HEX?
         BE    MAIN0580
         CLI   FDEF_TYPE,$FDEF_PATH            PATH?
         BE    MAIN0590
         CLI   FDEF_TYPE,$FDEF_TIME            TIME?
         BE    MAIN0600
         CLI   FDEF_TYPE,$FDEF_UID             USER ID?
         BE    MAIN0610
         ITRACE ID=BADTYPE,                                            +
               DATA1=(FDEF_TYPE,1)
         DC    H'0'
MAIN0520 DS    0H
         MVC   MSG_02_VALUE(8),0(R2)
         B     MAIN0620                        NEXT FIELD
MAIN0530 DS    0H
         BAL   R5,DEV0000
         MVC   MSG_02_VALUE(13),COMM_INFO_01
         B     MAIN0620
MAIN0540 DS    0H
         UNPK  COMM_INFO_01(9),0(5,R2)
         TR    COMM_INFO_01(8),COMM_HEXCHAR
         MVC   MSG_02_VALUE(8),COMM_INFO_01
         B     MAIN0620
MAIN0550 DS    0H
         BAL   R5,FULL0000
         MVC   MSG_02_VALUE(L'EDIT_WORD_1),COMM_INFO_01
         B     MAIN0620
MAIN0560 DS    0H
         BAL   R5,GID0000
         MVC   MSG_02_VALUE(L'DXD_GROUP),DXD_GROUP
         B     MAIN0620
MAIN0570 DS    0H
         BAL   R5,MODE0000
         MVC   MSG_02_VALUE(L'EDIT_WORD_1),DXD_MODE
         B     MAIN0620
MAIN0580 DS    0H
         UNPK  COMM_INFO_01(3),0(2,R2)
         TR    COMM_INFO_01(2),COMM_HEXCHAR
         MVC   MSG_02_VALUE(2),COMM_INFO_01
         B     MAIN0620
MAIN0590 DS    0H
         MVC   MSG_02_VALUE,SESS_PATH
         CLI   S_MODE,FT_SYMLINK                SYMBOLIC LINK?
         BNE   MAIN0620                         NO
         A     R8,SPF_VWIDTH
         BCTR  R7,0
         MVC   0(MSG_02_L,R8),MSG_02
         MVC   MSG_02_FIELD,=CL12'REAL PATH'
         MVC   MSG_02_VALUE,SESS_REAL_PATH
         B     MAIN0620
MAIN0600 DS    0H
         BAL   R5,TIME0000
         MVC   MSG_02_VALUE,COMM_INFO_01
         B     MAIN0620
MAIN0610 DS    0H
         BAL   R5,UID0000
         MVC   MSG_02_VALUE(L'DXD_USER),DXD_USER
MAIN0620 DS    0H
         A     R8,SPF_VWIDTH                    NEXT LINE
         BCT   R7,MAIN0630                      ACCOUNT FOR LINE USED
         B     MAIN0640                         SCREEN FULL
MAIN0630 DS    0H
         ITRACE ID=NEXTFDEF
         MVC   COMM_INFO_01,COMM_BLANKS
         MVC   COMM_INFO_02,COMM_BLANKS
         LA    R3,FDEF_L(,R3)
         CLI   0(R3),X'FF'                      END OF TABLE?
         BNE   MAIN0510                         NO
MAIN0640 DS    0H
         ITRACE ID=DISPLAY2
         MVC   SESS_DISP_PANEL,HDIR_PANEL
         MVC   SESS_DISP_VDATA_ADDR,DXD_VDATA2_ADDR
         MVC   SESS_DISP_VDATA_LENGTH,DXD_VDATA2_LENGTH
         XC    SESS_DISP_CURSOR,SESS_DISP_CURSOR
         L     R15,COMM_V_OSDISP
         BALR  R14,R15                          LINK TO OSDISP
         ITRACE ID=DISP_RC,                                            +
               DATA1=(SESS_RC,2),                                      +
               DATA2=(SESS_STATUS,1)
         L     R7,DXD_LINES_REMAINING           RESTORE LINES REMAINING
         L     R8,DXD_CURRENT_VDATA             RESTORE ADDR IN VDATA1
         B     MAIN0160                         CONTINUE LINE COMMANDS
NAME_MVC2 MVC  1(0,R8),DXD_NAME
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0700 DS    0H
         USING MSG01,R8
         MVI   MSG01_SELECTION,C' '             RESET THE SELECTION
         MVI   DSPCREQ_FUNC,$DSPCREQ_RETRIEVE   SET FUNCTION
         DROP  R8
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE
         BALR  R14,R15                          RETRIEVE DATA
         CLI   DSPCREQ_RC,$DSPCREQ_OK           SUCCESSFUL?
         BNE   ERR0020                          NO
         BAL   R5,PATH0100                      CONSTRUCT NAME
         ITRACE ID=DELETE,                                             +
               DATA1=DXD_NEW_PATH+00,                                  +
               DATA2=DXD_NEW_PATH+08
         ITRACE ID=DELETE+,                                            +
               DATA1=DXD_NEW_PATH+16,                                  +
               DATA2=DXD_NEW_PATH+24
         TM    DXD_FLAGS,$CONFIRM_ALL           CONFIRMED FOR ALL?
         BO    MAIN0710                         YES
         MVC   SESS_DISP_PANEL,OS0010           SET PANEL NAME
         MVC   SPF_PATH,DXD_NEW_PATH            SET PATH
         ITRACE ID=CONFIRM
         L     R15,COMM_V_OSCNFRM
         BALR  R14,R15                          CONFIRM DELETE
         LTR   R15,R15                          RC=0?
         BNZ   MAIN0740                         NO
         CLI   SPF_YES_OR_NO,C'N'               USER REPLAY 'NO'
         BE    MAIN0740                         YES, THEY DID, BYPASS
         CLC   SPF_YES_OR_NO,ALL                YES TO ALL?
         BNE   MAIN0710                         NO
         OI    DXD_FLAGS,$CONFIRM_ALL           SET CONFIRMED FOR ALL
MAIN0710 DS    0H
         ITRACE ID=CONFIRMD
         LA    R2,DXD_NEW_PATH                  NEW PATH
         LA    R3,DXD_NEW_PATH_LENGTH           PATH LENGTH
         CLI   S_MODE,FT_DIR                    DIRECTORY?
         BE    MAIN0720                         YES
         CALL  BPX1UNL,                         DELETE (UNLINK)        +
               ((R3),                                                  +
               (R2),                                                   +
               DXD_RETURN_VALUE,                                       +
               DXD_RETURN_CODE,                                        +
               DXD_REASON_CODE),                                       +
               VL,                                                     +
               MF=(E,DXD_CALL_PARMS)
         B     MAIN0730
MAIN0720 DS    0H
         ITRACE ID=DEL_DIR
         CALL  BPX1RMD,                         DELETE DIRECTORY       +
               ((R3),                                                  +
               (R2),                                                   +
               DXD_RETURN_VALUE,                                       +
               DXD_RETURN_CODE,                                        +
               DXD_REASON_CODE),                                       +
               VL,                                                     +
               MF=(E,DXD_CALL_PARMS)
MAIN0730 DS    0H
         TM    DXD_RETURN_VALUE,X'80'           SUCCESSFUL?
         BO    MAIN0750                         NO
         OI    DXD_STAT_STATUS,$STATUS_DELETED  STATUS
         MVI   DSPCREQ_FUNC,$DSPCREQ_UPDATE
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE
         BALR  R14,R15
         CLI   DSPCREQ_RC,$DSPCREQ_OK           SUCCESSFUL?
         BNE   ERR0020                          NO.. DATASPACE ERROR
         GETMAIN RU,                                                   +
               LV=DELETE_MSG_L,                                        +
               LOC=BELOW
         LR    R2,R1
         MVC   0(DELETE_MSG_L,R2),DELETE_MSG    COPY MESSAGE
         MVC   DELETE_MSG_NAME-DELETE_MSG(L'DELETE_MSG_NAME,R2),DXD_NAM+
               E
         LA    R3,DELETE_MSG_L
         TPUT  (R2),(R3),R
         FREEMAIN RU,                                                  +
               A=(R2),                                                 +
               LV=DELETE_MSG_L
         B     MAIN0160                         CONTINUE
MAIN0740 DS    0H
         MVC   SPF_MSG_1(ABORT_MSG_L),ABORT_MSG
         MVC   SPF_MSG_2,DXD_NEW_PATH
         B     MAIN0160
MAIN0750 DS    0H
         ITRACE ID=DEL_FAIL,                                           +
               DATA1=DXD_RETURN_CODE,                                  +
               DATA2=DXD_REASON_CODE
         CLI   DXD_RETURN_CODE+3,X'88'          DIRECTORY NOT EMPTY?
         BE    MAIN0760                         YES
         DC    H'0'
MAIN0760 DS    0H
         MVC   SPF_MSG_1(EMPTY_MSG_L),EMPTY_MSG
         B     MAIN0130
* ------------------------------------------------------------------- *
*        SET TAG FLAG(S)                                              *
* ------------------------------------------------------------------- *
MAIN0800 DS    0H
         ITRACE ID=TAG
         USING MSG01,R8
         MVI   MSG01_SELECTION,C' '             RESET THE SELECTION
         MVI   DSPCREQ_FUNC,$DSPCREQ_RETRIEVE   SET FUNCTION
         DROP  R8
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE
         BALR  R14,R15                          RETRIEVE DATA
         CLI   DSPCREQ_RC,$DSPCREQ_OK           SUCCESSFUL?
         BNE   ERR0020                          NO
         OI    DXD_STAT_STATUS,$STATUS_TAGGED   SET TAGGED FLAG
         MVI   DSPCREQ_FUNC,$DSPCREQ_UPDATE     SET FUNCTION
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE
         BALR  R14,R15                          UPDATE DATA
         CLI   DSPCREQ_RC,$DSPCREQ_OK           SUCCESSFUL?
         BNE   ERR0020                          NO
         B     MAIN0160
* ------------------------------------------------------------------- *
*        RESET TAG FLAG(S)                                            *
* ------------------------------------------------------------------- *
MAIN0810 DS    0H
         ITRACE ID=RESET
         USING MSG01,R8
         MVI   MSG01_SELECTION,C' '             RESET THE SELECTION
         MVI   DSPCREQ_FUNC,$DSPCREQ_RETRIEVE   SET FUNCTION
         DROP  R8
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE
         BALR  R14,R15                          RETRIEVE DATA
         CLI   DSPCREQ_RC,$DSPCREQ_OK           SUCCESSFUL?
         BNE   ERR0020                          NO
         NI    DXD_STAT_STATUS,255-$STATUS_TAGGED   RESET TAGGED FLAG
         MVI   DSPCREQ_FUNC,$DSPCREQ_UPDATE     SET FUNCTION
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE
         BALR  R14,R15                          UPDATE DATA
         CLI   DSPCREQ_RC,$DSPCREQ_OK           SUCCESSFUL?
         BNE   ERR0020                          NO
         B     MAIN0160
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN1000 DS    0H
         ITRACE ID=DSP_PRIM
         MVI   SESS_COMMAND_FLAGS,$SESS_COMMAND_DISPLAY
         LA    R0,PRIMARY_COMMANDS
         ST    R0,COMM_MAIN_COMMAND_TABLE
         XC    COMM_SUB_COMMAND_TABLE,COMM_SUB_COMMAND_TABLE
         L     R15,COMM_V_OSCMD
         BALR  R14,R15
         B     MAIN0130
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN1010 DS    0H
         ITRACE ID=DSP_LINE
         USING MSG01,R8
         MVI   MSG01_SELECTION,C' '             RESET THE SELECTION
         DROP  R8
         MVI   SESS_COMMAND_FLAGS,$SESS_COMMAND_DISPLAY
         LA    R0,LINE_COMMANDS
         ST    R0,COMM_MAIN_COMMAND_TABLE
         XC    COMM_SUB_COMMAND_TABLE,COMM_SUB_COMMAND_TABLE
         L     R15,COMM_V_OSCMD
         BALR  R14,R15
         B     MAIN0130
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN1200 DS    0H
         ITRACE ID=NEW_PATH,                                           +
               DATA1=(SPF_ZCMD+0,8),                                   +
               DATA2=(SPF_ZCMD+8,8)
         GETMAIN RU,                                                   +
               LV=SESSION_L,                                           +
               LOC=ANY
         LR    R3,R1                            SAVE ADDRESS
NEW      USING SESSION,R3
         ST    R3,COMM_NEW_SESSION
         LR    R0,R3
         LA    R1,SESSION_L
         LR    R14,R11                          CURRENT SESSION ADDR
         LR    R15,R1                           COPY LENGTH
         MVCL  R0,R14                           COPY SESSION BLOCK
         L     R1,SESS_NEXT                     NEXT ON CHAIN
NEXT     USING SESSION,R1
         ST    R1,NEW.SESS_NEXT                 SET NEXT IN NEW BLOCK
         ST    R11,NEW.SESS_PREV                SET PREV IN NEW BLOCK
         ST    R3,SESS_NEXT                     SET NEXT IN CURRENT
         LTR   R1,R1                            'NEXT' BLOCK?
         BZ    MAIN1210                         NO
         ST    R3,NEXT.SESS_PREV                SET PREV IN NEXT BLOCK
         DROP  NEXT
MAIN1210 DS    0H
         XC    NEW.SESS_DXD_ADDR,NEW.SESS_DXD_ADDR
         XC    NEW.SESS_DXD_LENGTH,NEW.SESS_DXD_LENGTH
         XC    NEW.SESS_LAST_FOUND,NEW.SESS_LAST_FOUND
         XC    NEW.SESS_SEARCH_ARG_LENGTH,NEW.SESS_SEARCH_ARG_LENGTH
         MVC   NEW.SESS_SEARCH_ARG,COMM_BLANKS
         MVC   NEW.SESS_DSN,COMM_BLANKS
         MVC   NEW.SESS_MEMBER,COMM_BLANKS
         XC    NEW.SESS_DISP_VDATA_ADDR,NEW.SESS_DISP_VDATA_ADDR
         XC    NEW.SESS_DISP_VDATA_LENGTH,NEW.SESS_DISP_VDATA_ADDR
         MVC   NEW.SESS_PATH,SPF_ZCMD
         XC    NEW.SESS_PATH_LENGTH(2),NEW.SESS_PATH_LENGTH
         MVC   NEW.SESS_PATH_LENGTH+2(2),OPERAND_01_LENGTH
         MVI   NEW.SESS_FORMAT_FLAGS,0
         MVC   NEW.DATASPACE_1_FIRST_RECORD,F1
         MVC   NEW.DATASPACE_1_DISPLAY_FIRST_RECORD,F1
         MVC   NEW.DATASPACE_1_DISPLAY_FIRST_COLUMN,F1
         XC    NEW.DATASPACE_1_ALET,NEW.DATASPACE_1_ALET
         XC    NEW.DATASPACE_1_TOKEN,NEW.DATASPACE_1_TOKEN
         MVC   NEW.DATASPACE_1_NAME,COMM_BLANKS
         XC    NEW.SESS_DCB_ADDR,NEW.SESS_DCB_ADDR
         XC    NEW.SESS_DCB_LENGTH,NEW.SESS_DCB_LENGTH
         L     R0,COMM_IO_SIZE
         GETMAIN RU,                                                   +
               LV=(0),                                                 +
               LOC=BELOW
         MVI   NEW.SESS_KEYWORD_FLAGS,$KEYWORD_PATH
         MVI   COMM_SESS_FUNC,$SESS_SWITCH
         ST    R7,DXD_LINES_REMAINING           SAVE LINES REMAINING
         ST    R8,DXD_CURRENT_VDATA             SAVE POSITION IN VDATA
         B     EXIT0000                         EXIT
* ------------------------------------------------------------------- *
*                                                                     *
*   Append selected file/directory path to current                    *
*                                                                     *
*    At entry                                                         *
*       SESS_PATH               current path                          *
*       SESS_PATH_LENGTH        length of current path name           *
*       DXD_NAME                selected file/dir path name           *
*       DXD_NAME_LENGTH         length of selected file/dir path name *
*                                                                     *
*    on exit                                                          *
*       DXD_NEW_PATH            full path name of selected file/dir   *
*       DXD_NEW_PATH_LENGTH     length of full path                   *
*                                                                     *
*    R1, R14, R15 work registers                                      *
*                                                                     *
*    R5 return address                                                *
*                                                                     *
* ------------------------------------------------------------------- *
PATH0100 DS    0H
         MVC   DXD_NEW_PATH(256),SESS_PATH      START WITH CURRENT
         MVC   DXD_NEW_PATH+256(256),COMM_BLANKS
         L     R1,SESS_PATH_LENGTH              PATH NAME LENGTH
         LA    R15,DXD_NEW_PATH(R1)
         BCTR  R15,0
         CLI   0(R15),C'/'                      END WITH A SLASH?
         BE    PATH0110                         YES
         LA    R1,1(,R1)                        ADD 1 TO LENGTH
         LA    R15,1(,R15)                      ADD 1 TO ADDRESS
         MVI   0(R15),C'/'                      INSERT SLASH
PATH0110 DS    0H
         MVC   1(256,R15),DXD_NAME              COPY SELECTED FILE/DIR
         AH    R1,DXD_NAME_LENGTH               PLUS NEW LENGTH
         CH    R1,H256                          MORE THAN 256?
         BNH   PATH0120                         NO
         LH    R1,H256                          LIMIT TO 256
PATH0120 DS    0H
         ST    R1,DXD_NEW_PATH_LENGTH           SAVE TOTAL LENGTH
         BR    R5
* ------------------------------------------------------------------- *
*        Left justify numbers in COMM_INFO_01                         *
* ------------------------------------------------------------------- *
NBR0000  DS    0H
         LA    R15,L'COMM_INFO_01               MAXIMUM NBR OF LOOPS
NBR0010  DS    0H
         CLI   COMM_INFO_01,C' '                BLANK?
         BNER  R14                     NO
         MVC   COMM_INFO_01(L'COMM_INFO_01-1),COMM_INFO_01+1
         MVI   COMM_INFO_01+(L'COMM_INFO_01-1),C' '
         BCT   R15,NBR0010                      LOOP
         BR    R14                              SHOULD NOT HAPPEN...
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DUMP0000 DS    0H
         ITRACE ID=DUMPDATA
         MVC   DAIR_DD,OSDUMP_DD                SET DD NAME
         MVC   DAIR_SYSOUT,COMM_SYSOUT_CLASS    SET SYSOUT CLASS
         MVI   DAIR_FUNC,$DAIR_ALLOC            SET DAIR FUNCTION
         MVI   DAIR_OPTS,$DAIR_SYSOUT           TYPE OF DATA SET
         LA    R1,DAIRREQ                       OSDAIR PARM LIST
         L     R15,COMM_V_OSDAIR                OSDAIR ENTRY POINT
         BALR  R14,R15                          LINK TO OSDAIR
         OC    DAIR_R15,DAIR_R15                SUCCESSFUL?
         BNZ   ERR0020                          NO
         GETMAIN RU,                                                   +
               LV=DUMP_CONTROL_L,                                      +
               LOC=BELOW
         ITRACE ID=DUMPCNTL,                                           +
               RDATA1=R1
         LR    R8,R1
         USING DUMP_CONTROL,R8                  DEFINE BASE
         MVC   DUMP_DCB(DUMP_DCB_L),DUMP_DCB_I
         LA    R2,DUMP_DCB                      DCB ADDRESS
         ST    R2,DSPCREQ_DCB                   PASS IT TO OSDSPACE
         ITRACE ID=OPENDUMP,                                           +
               RDATA1=R2
         OPEN  ((R2),OUTPUT),                                          +
               MODE=31,                                                +
               MF=(E,DXD_OPEN)
         LA    R1,DUMP_IO_AREA                  I/O AREA ADDRESS
         ST    R1,DSPCREQ_RECORD_ADDR           PASS ADDRESS
         MVI   DSPCREQ_FUNC,$DSPCREQ_DUMP
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE
         BALR  R14,R15
         ITRACE ID=DUMP,                                               +
               RDATA1=R1,                                              +
               RDATA2=R15
         BALR  R14,R15                          DUMP THE DATA SPACE
         ITRACE ID=CLS_DUMP,                                           +
               RDATA1=R2
         CLOSE ((R2)),                                                 +
               MODE=31,                                                +
               MF=(E,DXD_CLOSE)
         FREEPOOL (R2)
         MVI   DAIR_FUNC,$DAIR_FREE
         LA    R1,DAIRREQ                       OSDAIR PARM LIST
         L     R15,COMM_V_OSDAIR                OSDAIR ENTRY POINT
         BALR  R14,R15                          LINK TO OSDAIR
         ITRACE ID=FREE_DMP,                                           +
               RDATA1=R8
         FREEMAIN RU,                                                  +
               A=(R8),                                                 +
               LV=DUMP_CONTROL_L
         MVC   SPF_MSG_1(DUMP_MSG_L),DUMP_MSG
         B     MAIN0130


DUMP_MSG        DC    C'DATASPACE dump complete'
DUMP_MSG_L      EQU   *-DUMP_MSG
ABORT_MSG       DC    C'Member deletion aborted'
ABORT_MSG_L     EQU   *-ABORT_MSG
DELETE_MSG      DC    C'Deleted '
DELETE_MSG_NAME DC    CL50' '
DELETE_MSG_L    EQU   *-DELETE_MSG
EMPTY_MSG       DC    C'Directory not empty'
EMPTY_MSG_L     EQU   *-EMPTY_MSG


* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DEV0000  DS    0H
         ITRACE ID=DEV
         SR    R1,R1                             CLEAR REGISTER
         ICM   R1,3,0(R2)                        MAJOR NUMBER
         CVD   R1,COMM_DWORD                     CONVERT TO DECIMAL
         MVC   COMM_INFO_02(L'EDIT_WORD_2),EDIT_WORD_2
         ED    COMM_INFO_02(L'EDIT_WORD_2),COMM_DWORD+5
         MVC   COMM_INFO_01+0(5),COMM_INFO_02+1
         ICM   R1,3,2(R2)                        MINOR NUMBER
         CVD   R1,COMM_DWORD                     CONVERT TO DECIMAL
         MVC   COMM_INFO_02(L'EDIT_WORD_2),EDIT_WORD_2
         ED    COMM_INFO_02(L'EDIT_WORD_2),COMM_DWORD+5
         MVC   COMM_INFO_01+11(5),COMM_INFO_02+1
         BAL   R14,NBR0000
         BR    R5
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
FULL0000 DS    0H
         ITRACE ID=FULL
         ICM   R1,15,0(R2)                      NUMBER IN BINARY
         CVD   R1,COMM_DWORD
         MVC   COMM_INFO_01(L'EDIT_WORD_1),EDIT_WORD_1
         ED    COMM_INFO_01(L'EDIT_WORD_1),COMM_DWORD+2
         BAL   R14,NBR0000
         BR    R5
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
GID0000  DS    0H
         ITRACE ID=GID,                                                +
               RDATA1=R2,                                              +
               DATA2=(0(R2),4)
         ICM   R1,15,0(R2)                      NUMBER IN BINARY
         CVD   R1,COMM_DWORD
         MVC   COMM_INFO_02(L'EDIT_WORD_1),EDIT_WORD_1
         ED    COMM_INFO_02(L'EDIT_WORD_1),COMM_DWORD+2
         MVC   COMM_INFO_01(L'EDIT_WORD_1+2),COMM_INFO_02+2
         BAL   R14,NBR0000                      LEFT JUSTIFY
         MVC   DXD_GID,COMM_INFO_01
         ITRACE ID=GET_GRP,                                            +
               DATA1=(0(R2),4)
         CALL  BPX1GGI,                         LOOK UP GROUP NAME     +
               (0(R2),                                                 +
               DXD_RETURN_VALUE,                                       +
               DXD_RETURN_CODE,                                        +
               DXD_REASON_CODE),                                       +
               VL,                                                     +
               MF=(E,DXD_CALL_PARMS)
         ICM   R1,15,DXD_RETURN_VALUE           RETURNED DATA
         BZ    GID0020                          ZERO.. ERROR
         USING GIDS,R1                          DEFINE BASE
         MVC   DXD_GROUP_NAME,COMM_BLANKS       INITIALIZE NAME
         L     R14,GIDS_G_LEN                   LENGTH OF GROUP ID
         CH    R14,=Y(L'DXD_GROUP_NAME)         TOO LONG?
         BNH   GID0010                          NO
         LH    R14,=Y(L'DXD_GROUP_NAME)         LIMIT LENGTH
GID0010  DS    0H
         BCTR  R14,0
         EX    R14,GROUP_MVC                    COPY GROUP NAME
         BR    R5
GID0020  DS    0H
         MVC   DXD_GROUP_NAME,COMM_INFO_01
         BR    R5

GROUP_MVC  MVC  DXD_GROUP_NAME(0),GIDS_G_NAME

         DROP  R1

         BR    R5
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MODE0000 DS    0H
         ITRACE ID=MODE
         MVI   DXD_ATTR,$SCREEN_ATTR_NORMAL     DEFAULT ATTRIBUTE
         CLI   S_MODE,FT_DIR                    DIRECTORY?
         BE    MODE0010                         YES
         CLI   S_MODE,FT_CHARSPEC               CHARACTER SPECIAL?
         BE    MODE0020                         YES
         CLI   S_MODE,FT_REGFILE                REGULAR FILE?
         BE    MODE0030                         YES
         CLI   S_MODE,FT_FIFO                   NAMED PIPE (FIFO)?
         BE    MODE0040                         YES
         CLI   S_MODE,FT_SYMLINK                SYMBOLIC LINK?
         BE    MODE0050                         YES
         CLI   S_MODE,FT_SOCKET                 SOCKET?
         BE    MODE0060                         YES
         MVI   DXD_TYPE,C'?'                    UNKNOWN TYPE
         B     MODE0070
MODE0010 DS    0H
         MVI   DXD_TYPE,C'd'                    DIRECTORY
         MVI   DXD_ATTR,$SCREEN_ATTR_HIGH_GREEN ATTRIBUTE
         B     MODE0070
MODE0020 DS    0H
         MVI   DXD_TYPE,C'c'                    CHARACTER SPECIAL
         MVI   DXD_ATTR,$SCREEN_ATTR_HIGH_TURQUOISE
         B     MODE0070
MODE0030 DS    0H
         MVI   DXD_TYPE,C'-'                    REGULAR FILE
         B     MODE0070
MODE0040 DS    0H
         MVI   DXD_TYPE,C'p'                    NAMED FIFO PIPE
         MVI   DXD_ATTR,$SCREEN_ATTR_HIGH_TURQUOISE
         B     MODE0070
MODE0050 DS    0H
         MVI   DXD_TYPE,C'l'                    SYMBOLIC LINK
         MVI   DXD_ATTR,$SCREEN_ATTR_HIGH_YELLOW
         B     MODE0070
MODE0060 DS    0H
         MVI   DXD_TYPE,C's'                    SOCKET
         MVI   DXD_ATTR,$SCREEN_ATTR_HIGH_TURQUOISE
MODE0070 DS    0H
         MVC   DXD_O_PERMISSION,=C'---'
         MVC   DXD_G_PERMISSION,=C'---'
         MVC   DXD_W_PERMISSION,=C'---'
         TM    S_MODE2,S_IRUSR                  OWNER READ?
         BNO   MODE0080                         NO
         MVI   DXD_O_PERMISSION+0,C'r'
MODE0080 DS    0H
         TM    S_MODE3,S_IWUSR                  OWNER WRITE?
         BNO   MODE0090                         NO
         MVI   DXD_O_PERMISSION+1,C'w'
MODE0090 DS    0H
         TM    S_MODE3,S_IXUSR                  OWNER EXECUTE?
         BNO   MODE0100                         NO
         MVI   DXD_O_PERMISSION+2,C'x'
MODE0100 DS    0H
         TM    S_MODE3,S_IRGRP                  GROUP READ?
         BNO   MODE0110                         NO
         MVI   DXD_G_PERMISSION+0,C'r'
MODE0110 DS    0H
         TM    S_MODE3,S_IWGRP                  GROUP WRITE?
         BNO   MODE0120                         NO
         MVI   DXD_G_PERMISSION+1,C'w'
MODE0120 DS    0H
         TM    S_MODE3,S_IXGRP                  GROUP EXECUTE?
         BNO   MODE0130                         NO
         MVI   DXD_G_PERMISSION+2,C'x'
MODE0130 DS    0H
         TM    S_MODE3,S_IROTH                  OTHER (WORLD) READ?
         BNO   MODE0140                         NO
         MVI   DXD_W_PERMISSION+0,C'r'
MODE0140 DS    0H
         TM    S_MODE3,S_IWOTH                  OTHER (WORLD) WRITE?
         BNO   MODE0150                         NO
         MVI   DXD_W_PERMISSION+1,C'w'
         MVI   DXD_ATTR,$SCREEN_ATTR_HIGH_RED
MODE0150 DS    0H
         TM    S_MODE3,S_IXOTH                  OTHER (WORLD) EXECUTE
         BNO   MODE0160                         NO
         MVI   DXD_W_PERMISSION+2,C'x'
MODE0160 DS    0H
         TM    S_MODE2,S_ISUID                  SET UID?
         BNO   MODE0170                         NO
         MVI   DXD_O_PERMISSION+2,C's'
         B     MODE0180
MODE0170 DS    0H
         TM    S_MODE2,S_ISGID                  SET GID?
         BNO   MODE0180                         NO
         MVI   DXD_G_PERMISSION+2,C's'
MODE0180 DS    0H
         BR    R5

* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
TIME0000 DS    0H
         ITRACE ID=TIME,                                               +
               RDATA1=R2,                                              +
               DATA2=(0(R2),4)
         MVC   COMM_INFO_01,COMM_BLANKS
         SR    R14,R14
         ICM   R15,15,0(R2)                     TIME SINCE  JAN 1, 1970
         BZ    TIME0040                         ALL ZERO
         M     R14,F1000000                     CONVERT TO MICROSECONDS
         AL    R15,X_1970+4                     MIRCOSECONDS TO 1970
         BC    12,TIME0010
         AL    R14,F1                           PLUS 1 (OVERFLOW)
TIME0010 DS    0H
         AL    R14,X_1970
         SLDL  R14,12                           CONVERT TO STCK FORMAT
         L     R1,CVTPTR                        CVT
         L     R1,CVTEXT2-CVT(,R1)              OS/VS2 COMMON EXTENSION
         USING CVTXTNT2,R1
         AL    R15,CVTLDTOR                     DATE OFFSET RIGHT WORD
         BC    12,TIME0020
         AL    R14,F1                           PLUS 1 (OVERFLOW)
TIME0020 DS    0H
         AL    R14,CVTLDTOL                     DATE OFFSET LEFT WORD
         SL    R15,CVTLSOL                      MINUS LOW WORD
         BC    3,TIME0030
         BCTR  R14,0                            MINUS 1 (OVERFLOW)
TIME0030 DS    0H
         SL    R14,CVTLSOH                      MINUS HIGH WORD
         DROP  R1
         STM   R14,R15,COMM_DWORD               SAVE AS STCK
         STCKCONV TIMETYPE=DEC,                 CONVERT TIME           +
               DATETYPE=YYYYMMDD,                                      +
               STCKVAL=COMM_DWORD,                                     +
               CONVVAL=DXD_DATE_TIME,                                  +
               MF=(E,DXD_STCKCONV)
         UNPK  COMM_INFO_01+00(3),DXD_DATE_TIME+10(2)  MONTH
         MVI   COMM_INFO_01+02,C'/'
         UNPK  COMM_INFO_01+03(3),DXD_DATE_TIME+11(2)  DAY
         MVI   COMM_INFO_01+05,C'/'
         UNPK  COMM_INFO_01+06(3),DXD_DATE_TIME+09(2)  YEAR
         MVI   COMM_INFO_01+08,C' '
         UNPK  COMM_INFO_01+11(3),DXD_DATE_TIME+00(2)  HOUR
         MVI   COMM_INFO_01+13,C':'
         UNPK  COMM_INFO_01+14(3),DXD_DATE_TIME+01(2)  MINUTE
         MVI   COMM_INFO_01+16,C' '
         BR    R5
TIME0040 DS    0H
         MVC   COMM_INFO_01(L'ZERO),ZERO
         BR    R5
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
UID0000  DS    0H
         ITRACE ID=UID,                                                +
               RDATA1=R2,                                              +
               DATA2=(0(R2),4)
         ICM   R1,15,0(R2)                      NUMBER IN BINARY
         CVD   R1,COMM_DWORD
         MVC   COMM_INFO_02(L'EDIT_WORD_1),EDIT_WORD_1
         ED    COMM_INFO_02(L'EDIT_WORD_1),COMM_DWORD+2
         MVC   COMM_INFO_01(L'EDIT_WORD_1+2),COMM_INFO_02+2
         BAL   R14,NBR0000                      LEFT JUSTIFY
         MVC   DXD_UID,COMM_INFO_01
         ITRACE ID=GET_USER,                                           +
               DATA1=(0(R2),4)
         CALL  BPX1GPU,                                                +
               (0(R2),                                                 +
               DXD_RETURN_VALUE,                                       +
               DXD_RETURN_CODE,                                        +
               DXD_REASON_CODE),                                       +
               VL,                                                     +
               MF=(E,DXD_CALL_PARMS)
         ICM   R1,15,DXD_RETURN_VALUE           RETURNED DATA
         ITRACE ID=RC,                                                 +
               RDATA1=R1
         LTR   R1,R1
         BZ    UID0020                          ZERO.. ERROR
         USING GIDN,R1                          DEFINE BASE
         MVC   DXD_USER_NAME,COMM_BLANKS
         L     R14,GIDN_U_LEN                   LENGTH OF USER ID
         CH    R14,=Y(L'DXD_USER_NAME)          TOO LONG?
         BNH   UID0010                          NO
         LH    R14,=Y(L'DXD_USER_NAME)          LIMIT LENGTH
UID0010  DS    0H
         BCTR  R14,0
         EX    R14,USER_MVC                     COPY USER NAME
         BR    R5
UID0020  DS    0H
         MVC   DXD_USER_NAME,COMM_INFO_01
         BR    R5
USER_MVC MVC   DXD_USER_NAME(0),GIDN_U_NAME
         DROP  R1

* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0010  DS    0H
         DC    H'0'
         DC    C'INTERNAL ERROR, DXD AREA EXISTS'
ERR0020  DS    0H
         MVC   SPF_MSG_1,COMM_MSG_1             COPY MESSAGE
         MVC   SPF_MSG_2,COMM_MSG_2             COPY MESSAGE
         MVC   SPF_MSG_3,COMM_MSG_3             COPY MESSAGE
         MVC   SPF_MSG_4,COMM_MSG_4             COPY MESSAGE
         MVC   SPF_MSG_5,COMM_MSG_5             COPY MESSAGE
         B     MAIN0130                         DISPLAY WITH MESSAGE
ERR0030  DS    0H
         MVI   COMM_MSG_ID+1,2                  INVALID PRIMARY COMMAND
         B     ERR1000
ERR0050  DS    0H
         MVI   COMM_MSG_ID+1,4                  TOO MANY OPERANDS
         B     ERR1000
ERR0060  DS    0H
         MVI   COMM_MSG_ID+1,5                  NOT IN THIS DIRECTORY
         B     ERR1000
ERR0070  DS    0H
         DC    H'0'
ERR0080  DS    0H
         DC    H'0'
ERR0090  DS    0H
         DC    H'0'
ERR0100  DS    0H
         DC    H'0'
ERR0110  DS    0H
         DC    H'0'
ERR0120  DS    0H
         DC    H'0'
ERR0130  DS    0H
         DC    H'0'
ERR1000  DS    0H
         MVC   COMM_MSG_CSECT,MODID             CSECT FOR MESSAGES
         MVI   COMM_MSG_ID+0,0
         L     R15,COMM_V_OSMSG                 OSMSG ENTRY POINT
         BALR  R14,R15                          BUILD THE MESSAGE
         MVC   SPF_MSG_1,COMM_MSG_1             COPY MESSAGE
         MVC   SPF_MSG_2,COMM_MSG_2             COPY MESSAGE
         MVC   SPF_MSG_3,COMM_MSG_3             COPY MESSAGE
         MVC   SPF_MSG_4,COMM_MSG_4             COPY MESSAGE
         MVC   SPF_MSG_5,COMM_MSG_5             COPY MESSAGE
         B     MAIN0130                         EXIT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
SESS0000 DS    0H
         ITRACE ID=SESS,                                               +
               RDATA1=R7,                                              +
               RDATA2=R9
         ST    R7,DXD_LINES_REMAINING           RESTORE LINES REMAINING
         ST    R8,DXD_CURRENT_VDATA             RESTORE VDATA ADDR
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DEBUG000 DS    0H
         ITRACE ID=DEBUG
         L     R15,COMM_V_OSDEBUG
         BALR  R14,R15
         B     MAIN0130
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
CLEAN000 DS    0H
         ITRACE ID=CLEAN_UP
         MVI   COMM_VDATA_FUNC,$VDATA_FREEMAIN
         L     R0,DXD_VDATA1_LENGTH
         L     R1,DXD_VDATA1_ADDR
         L     R15,COMM_V_OSVDATA
         BALR  R14,R15                          FREE VDATA 1
         L     R0,DXD_VDATA2_LENGTH
         L     R1,DXD_VDATA2_ADDR
         L     R15,COMM_V_OSVDATA
         BALR  R14,R15                          FREE VDATA 2
         XC    SESS_DISP_VDATA_ADDR,SESS_DISP_VDATA_ADDR
         XC    SESS_DISP_VDATA_LENGTH,SESS_DISP_VDATA_LENGTH
         LA    R0,OSHDIR_DXD_L                  WORK AREA SIZE
         LR    R1,R13                           COPY WORK AREA ADDRESS
         L     R13,4(,R13)                      RESTORE R13
         FREEMAIN RU,                           FREE WORK AREA         +
               A=(1),                                                  +
               LV=(0)
         XC    SESS_DXD_ADDR,SESS_DXD_ADDR
         XC    SESS_DXD_LENGTH,SESS_DXD_LENGTH
         XC    COMM_MAIN_COMMAND_TABLE,COMM_MAIN_COMMAND_TABLE
         XC    COMM_SUB_COMMAND_TABLE,COMM_SUB_COMMAND_TABLE
         SR    R15,R15
         B     EXIT0010
* ------------------------------------------------------------------
*
* ------------------------------------------------------------------
EXIT0000 DS    0H
         ITRACE ID=EXIT
         LH    R15,DXD_RC                       RETURN CODE
         L     R13,4(,R13)                      RESTORE R13
EXIT0010 DS    0H
         L     R14,12(,R13)                     RESTORE R14
         LM    R0,R12,20(R13)                   RESTORE REGISTERS
         BR    R14                              RETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *

*
*       Time in micro seconds since Jan 1, 1970
*
*       (70*365.25)*24*3600*1000000
*
X_1970         DS    0D
               DC    FL8'2208988800E6'

DUMP_DCB_I     DCB DSORG=PS,                                           +
               DDNAME=OSDUMP,                                          +
               RECFM=FBA,                                              +
               LRECL=133,                                              +
               BLKSIZE=13300,                                          +
               MACRF=PM
DUMP_DCB_L     EQU      *-DUMP_DCB_I
OPEN_I         OPEN     (*,OUTPUT),                                    +
               MODE=31,                                                +
               MF=L
OPEN_L         EQU      *-OPEN_I
CLOSE_I        CLOSE    (*),                                           +
               MODE=31,                                                +
               MF=L
CLOSE_L        EQU      *-CLOSE_I

FILE_CLOSE     DC    A(F_CLOSFD)

F0             DC    F'0'
F1             DC    F'1'
F2             DC    F'2'
F3             DC    F'3'
F1000000       DC    F'1000000'
H1             DC    H'1'
H2             DC    H'2'
H3             DC    H'3'
H4             DC    H'4'
H8             DC    H'8'
H256           DC    H'256'

EDIT_WORD_1     DC    X'402020202020202020202120'
EDIT_WORD_2     DC    X'402020202120'

ALL             DC    C'ALL'
DISPLAY         DC    C'DISPLAY'
DELETED         DC    CL10'DELETED'
DOT_DOT         DC    C'..'
EDIT            DC    CL8'EDIT'
FILE_COMMAND    DC    C'/bin/file'
OSDUMP_DD       DC    CL8'OSDUMP'
HDIR_PANEL      DC    CL8'OSHDIR'
MODULE_OSHFSDIR DC    CL8'OSHFSDIR'
OS0010          DC    CL8'OS0010'
UNKNOWN         DC    CL10'UNKNOWN'
ZERO            DC    C'-zero-'

* ------------------------------------------------------------------- *
*                                                                     *
*     To allow for file names to be lower case, the parser does       *
*     not translate anything to upper case.  Therefore, the commands  *
*     may be lower or upper case.  I suppose I should allow for all   *
*     combinations of upper/lower case.  Well, you can't please       *
*     everyone...                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
PRIMARY_COMMANDS DS   0X
                CMD   /xxxx,MAIN1200,'Display file or directory xxx'
                CMD   r,MAIN0200,'Refresh info'
                CMD   R,MAIN0200,'Refresh info'
                CMD   s,MAIN0300,'select (browse)'
                CMD   S,MAIN0300,'Select (browse)'
                CMD   FL,0,'List available formats'
                CMD   ref,MAIN0200,'refresh info'
                CMD   REF,MAIN0200,'REFresh info'
                CMD   sel,MAIN0300,'select (browse)'
                CMD   SEL,MAIN0300,'SELect (browse)'
                CMD   LIST,0,'List available formats'
                CMD   FLIST,0,'List available formats'
                CMD   select,MAIN0300,'select (browse)'
                CMD   SELECT,MAIN0300,'SELECT (browse)'
                CMD   refresh,MAIN0200,'refresh info'
                CMD   REFRESH,MAIN0200,'REFRESH info'
                CMD   ??,MAIN1000,'Display list of available commands'
                DC    X'FF'

* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
LINE_COMMANDS   DS    0C
                CMD   A,MAIN0400,'Browse in ASCII mode'
                CMD   B,MAIN0400,'Browse'
                CMD   D,MAIN0700,'Delete'
                CMD   F,MAIN0450,'Issue "file" command'
                CMD   I,MAIN0500,'Long info'
                CMD   L,MAIN0500,'Long info'
                CMD   S,MAIN0400,'Select (browse)'
                CMD   T,MAIN0800,'Tag'
                CMD   V,MAIN0400,'View (browse)'
                CMD   X,MAIN0810,'Reset tag'
                CMD   ?,MAIN1010,'Display list of line commands'
                DC    X'FF'

FIELD_TABLE     DS    0X
                FDEF  ST_MODE,'MODE',M
                FDEF  ST_INO,'INODE',F
                FDEF  ST_DEV,'DEVICE',D
                FDEF  ST_NLINK,'LINKS',F
                FDEF  ST_UID,'UID',U
                FDEF  ST_GID,'GID',G
                FDEF  ST_SIZE+4,'SIZE',F
                FDEF  ST_ATIME,'ATIME',T
                FDEF  ST_MTIME,'MTIME',T
                FDEF  ST_CTIME,'CTIME',T
                FDEF  ST_REFTIME,'RTIME',T
                FDEF  ST_BLKSIZE,'Block size',F
                FDEF  FT_CCSID,'CC ID',4
                FDEF  FT_FLAGS,'FLAGS',4
                FDEF  ST_BLOCKS,'Blks alloc',F
                FDEF  ST_GENVALUE,'Gen ATTRs',4
                FDEF  ST_FILEFMT,'File format',1
                FDEF  ST_FSPFLAG2,'ACL flags',1
                FDEF  ST_SECLABEL,'SEC label',C
                DC    X'FF'

                LTORG

*
*  Due to base register use, keep this below the LTORG
*

MSG01                DS    0C
MSG01_ATTR1          DC    AL1($SCREEN_ATTR_INPUT)
MSG01_SELECTION      DC    CL1' '
MSG01_ATTR2          DC    AL1($SCREEN_ATTR_NORMAL)
MSG01_MODE           DC    CL10' '
                     DC    C' '
MSG01_USER           DC    CL10' '
                     DC    C' '
MSG01_GROUP          DC    CL10' '
                     DC    C' '
MSG01_NAME_LENGTH    DC    X'40202020202020202120'
MSG01_ATTR3          DC    AL1($SCREEN_ATTR_NORMAL)
MSG01_NAME           DC    CL33' '
MSG01_L              EQU   *-MSG01

MSG_02               DS    0C
                     DC    AL1($SCREEN_ATTR_HIGH)
MSG_02_FIELD         DC    CL12' '
                     DC    AL1($SCREEN_ATTR_NORMAL)
MSG_02_VALUE         DC    CL65' '
MSG_02_L             EQU   *-MSG_02

OSHDIREND            EQU   *-OSHDIR
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
OSHDIR_WORK          DSECT
                     COPY     DXDPREF

DXD_OPEN             DS       0F,(OPEN_L)X
DXD_CLOSE            DS       0F,(CLOSE_L)X

DXD_DUMMY_ECB        DS       F

DXD_SPF_PARMS        DS       0F
DXD_SPF_1            DS       F
DXD_SPF_2            DS       F
DXD_SPF_3            DS       F

DXD_CURRENT_VDATA    DS       A
DXD_LINES_REMAINING  DS       F

DXD_VDATA1_ADDR      DS       A
DXD_VDATA1_LENGTH    DS       F
DXD_VDATA2_ADDR      DS       A
DXD_VDATA2_LENGTH    DS       F
DXD_CURSOR           DS       F

DXD_VWIDTH           DS       F
DXD_VDEPTH           DS       F

DXD_RETURN_VALUE     DS       F
DXD_RETURN_CODE      DS       F
DXD_REASON_CODE      DS       F
DXD_CALL_PARMS       DS       20F

DXD_PIPE1_READ       DS       F
DXD_PIPE1_WRITE      DS       F
DXD_PIPE2_READ       DS       F
DXD_PIPE2_WRITE      DS       F

DXD_ARG_LENGTHS      DS       0F
DXD_ARG_1_LENGTH     DS       F
DXD_ARG_2_LENGTH     DS       F

DXD_LENGTH_VECTOR     DS      0A
DXD_ARG_1_LENGTH_ADDR DS      A
DXD_ARG_2_LENGTH_ADDR DS      A

DXD_ARGS_VECTOR      DS       0A
DXD_ARG_1_ADDR       DS       A
DXD_ARG_2_ADDR       DS       A

DXD_PIPE_DATA        DS       A
DXD_PIPE_LENGTH      DS       F

DXD_FILE_DESCRIPTORS DS      0F
DXD_DESCRIPTOR_0     DS       F
DXD_DESCRIPTOR_1     DS       F
DXD_DESCRIPTOR_2     DS       F

DXD_CHILD_PID        DS       F

DXD_WAIT_STATUS      DS       F

DXD_DATE_TIME        DS       4F
DXD_STCKCONV         STCKCONV MF=L

DXD_RC               DS       H
DXD_LINES            DS       H

DXD_SELECTION        DS       X

DXD_ATTR             DS       X

DXD_FLAGS            DS       X
$DXD_INIT            EQU      X'80'
$LINE_CMD            EQU      X'40'
$NEW_FILE            EQU      X'20'
$CONFIRM_ALL         EQU      X'10'            ACTION CONFIRMED FOR ALL

DXD_EDIT_WORK        DS       XL12

DXD_USER             DS       0CL61
DXD_UID              DS       CL10
                     DS       C
DXD_USER_NAME        DS       CL50

DXD_GROUP            DS       0CL61
DXD_GID              DS       CL10
                     DS       C
DXD_GROUP_NAME       DS       CL50

DXD_MODE             DS       0CL10
DXD_TYPE             DS       C
DXD_O_PERMISSION     DS       CL3
DXD_G_PERMISSION     DS       CL3
DXD_W_PERMISSION     DS       CL3

DXD_RECORD           DS       0D
DXD_NAME_LENGTH      DS       H
DXD_NAME             DS       CL256
DXD_STAT_STATUS      DS       X
$STATUS_OK           EQU      X'00'
$STATUS_DELETED      EQU      X'80'
$STATUS_TAGGED       EQU      X'40'
$STATUS_ERROR        EQU      X'FF'
                     BPXYSTAT DSECT=NO,LIST=YES
DXD_RECORD_L         EQU      *-DXD_RECORD

DXD_REAL_PATH        DS       CL256

DXD_NEW_PATH_LENGTH  DS       F
DXD_NEW_PATH         DS       CL512

                     DAIRREQ  DSECT=NO
                     DSPCREQ  DSECT=NO
                     LOADREQ  DSECT=NO

OSHDIR_WORK          DSECT
OSHDIR_DXD_L         EQU      *-OSHDIR_WORK
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                  COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                  SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                  COPY     OPERAND
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
COMMAND_DSECT     DSECT
COMMAND_LENGTH    DS       X
COMMAND_COMMAND   DS       CL8
COMMAND_ADDRESS   DS       AL4
COMMAND_L         EQU      *-COMMAND_DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
FDEF_DSECT        DSECT
FDEF_DISP         DS       AL2          DISP TO DATA
FDEF_DESC         DS       CL12         FIELD DESCRIPTION
FDEF_TYPE         DS       C            FIELD TYPE
$FDEF_CHARACTER   EQU      C'C'         .. CHARACTER
$FDEF_DEV         EQU      C'D'         .. DEVICE
$FDEF_HEX_FOUR    EQU      C'4'         .. 4 BYTE HEX
$FDEF_FULLWORD    EQU      C'F'         .. FULLWORD
$FDEF_GID         EQU      C'G'         .. GROUP ID
$FDEF_MODE        EQU      C'M'         .. MODE
$FDEF_HEX_ONE     EQU      C'1'         .. 1 BYTE HEX
$FDEF_PATH        EQU      C'P'         .. PATH NAME
$FDEF_TIME        EQU      C'T'         .. TIME
$FDEF_UID         EQU      C'U'         .. USER ID
FDEF_L            EQU      *-FDEF_DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DUMP_CONTROL      DSECT
DUMP_DCB          DS       0F,(DUMP_DCB_L)X
DUMP_IO_AREA      DS       CL133
DUMP_CONTROL_L    EQU      *-DUMP_CONTROL
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         OSSPFD TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         IBMMAC  CVT=YES
         IBMMAC  DCBD=PO
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY ATTRS
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         BPXYMODE  DSECT=YES,LIST=YES
         BPXYFCTL  DSECT=NO,LIST=YES
         BPXYFTYP  LIST=YES
         BPXYGIDN  DSECT=YES,LIST=YES
         BPXYGIDS  DSECT=YES,LIST=YES
         BPXYSTAT  DSECT=YES,LIST=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY REGEQU
         END  OSHDIR
