* ------------------------------------------------------------------- *
*                                                                     *
*  Module name: OSUSER                                                *
*                                                                     *
*                                                                     *
*  Some formatting may be too complicated for USERFMT to handle.      *
*  At least not very easily.  This program allows users to write      *
*  essentialy a user exit to format records.  What this code will     *
*  do is "retrieve" the records one at a time and call the user       *
*  function.  The users would not have to be as familiar with the     *
*  internals of OS and the same code that does the record retreival,  *
*  display, scrolling, etc does not have to be re-coded for each      *
*  user format.                                                       *
*                                                                     *
*                                                                     *
*  The exit is defined via an EXIT statement in the FORMAT            *
*  definition.  FORMATs are defined by members in the OSFORMAT        *
*  PDS (or PDS/E).  The exit will have been loaded by OSFORMAT        *
*  and the address saved in SESS_FUNCTION_EP.                         *
*                                                                     *
*  User exits have addressability to the common area (mapped by       *
*  the macro called COMMON), therefore they can locate and access     *
*  most any internal control block.                                   *
*                                                                     *
*  To make it easier to develop a function exit, most of the info     *
*  an exit would need is collected into the USEREXIT data area.       *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
OSUSER   CSECT
OSUSER   AMODE 31
OSUSER   RMODE ANY
         USING OSCOMM,R12
         USING SESSION,R11
         USING OSUSER,R15
         B     INIT0000
MODID    DC    CL8'OSUSER'
         DC    CL8'&SYSDATE'
         DC    CL8'&SYSTIME'
         DC    A(OSUSEREND-OSUSER)
INIT0000 DS    0H
         USING OSCOMM,R12                     DEFINE COMMON AREA BASE
         STM   R14,R12,12(R13)                SAVE REGS
         LR    R10,R15                        COPY ENTRY POINT
         DROP  R15
         USING OSUSER,R10                     DEFINE BASE
         L     R9,COMM_OSSPFD
         USING OSSPFD,R9
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         CLI   SESS_FORMAT_FLAGS,$FORMAT_INITIALIZE
         BNE   INIT0010
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         GETMAIN RU,                                                   +
               LV=OSUSER_WORK_L,                                       +
               LOC=ANY
         ST    R13,4(,R1)                     SAVE R13
         ST    R1,8(,R13)                     CHAIN SAVE AREA
         LR    R13,R1                         COPY WORK AREA ADDRESS
         USING OSUSER_WORK,R13                DEFINE WORK AREA BASE
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY
         LA    R1,DATASPACE_1                 USE DATA SPACE 1
         ST    R1,DSPCREQ_DATASPACE
         ITRACE ID=VDATA
         MVC   WORK_PANEL,OS0001              SET PANEL NAME
         LA    R1,WORK_PANEL                  PANEL NAME ADDR
         MVI   COMM_VDATA_FUNC,$VDATA_GETMAIN
         L     R15,COMM_V_OSVDATA
         BALR  R14,R15                        GET VDATA
         ST    R0,WORK_VDATA_LENGTH
         ST    R1,WORK_VDATA_ADDR
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
INIT0010 DS    0H
         ICM   R15,15,SESS_DXD_ADDR
         BZ    ERR0010
         ST    R13,4(,R15)
         ST    R15,8(,R13)
         LR    R13,R15
         ITRACE ID=ENTRY
         XC    WORK_RC,WORK_RC
         CLI   SESS_FORMAT_FLAGS,$FORMAT_CONTROL
         BE    EXIT0000
         CLI   SESS_FORMAT_FLAGS,$FORMAT_CLEANUP
         BE    CLEAN000
         CLI   COMM_SESS_FUNC,$SESS_SWITCH
         BE    MAIN0110
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         ITRACE ID=USERINIT
         XC    USEREXIT(USEREXIT_FUNC-USEREXIT),USEREXIT
         MVC   USEREXIT_MSG_1,COMM_BLANKS
         MVC   USEREXIT_MSG_2,COMM_BLANKS
         MVC   USEREXIT_MSG_3,COMM_BLANKS
         MVC   USEREXIT_MSG_4,COMM_BLANKS
         MVC   USEREXIT_MSG_5,COMM_BLANKS
         MVI   USEREXIT_FUNC,$USEREXIT_INITIALIZE
         LA    R1,USEREXIT
         L     R15,SESS_FUNCTION_EXIT
         BALR  R14,R15
         L     R6,SESS_IO_AREA                I/O AREA ADDRESS
         ST    R6,DSPCREQ_RECORD_ADDR
         ST    R6,USEREXIT_RECORD_ADDR
         MVC   DSPCREQ_RECORD_NBR,DATASPACE_1_DISPLAY_FIRST_RECORD
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         L     R1,DATASPACE_1_LAST_RECORD
         BCTR  R1,0
         ST    R1,USEREXIT_MAX_RECORD_NBR
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0000 DS    0H
         MVC   USEREXIT_VDATA_ADDR,WORK_VDATA_ADDR
         MVC   USEREXIT_VDATA_LENGTH,WORK_VDATA_LENGTH
         MVC   DSPCREQ_RECORD_NBR,DATASPACE_1_DISPLAY_FIRST_RECORD
* ------------------------------------------------------------------- *
*        Initialize VDATA buffer                                      *
* ------------------------------------------------------------------- *
MAIN0010 DS    0H
         L     R0,WORK_VDATA_ADDR
         L     R1,WORK_VDATA_LENGTH
         SR    R14,R14
         SR    R15,R15
         MVCL  R0,R14                         INITIALIZE VDATA
* ------------------------------------------------------------------- *
*        "Retrieve" the record and format it                          *
* ------------------------------------------------------------------- *
         MVI   DSPCREQ_FUNC,$DSPCREQ_RETRIEVE
         CLC   DSPCREQ_RECORD_NBR,DATASPACE_1_LAST_RECORD
         BH    MAIN1000                       END OF RECORDS REACHED
         ITRACE ID=GET_RECD,                                           +
               DATA1=DSPCREQ_RECORD_NBR,                               +
               DATA2=DATASPACE_1_LAST_RECORD
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE              OSDSPACE ENTRY POINT
         BALR  R14,R15                        LINK TO OSDSPACE
         CLI   DSPCREQ_RC,$DSPCREQ_NOT_STORED
         BE    ERR910                         NOT STORED
         CLI   DSPCREQ_RC,$DSPCREQ_OK         SUCCESSFUL?
         BNE   ERR910                         NO
         ITRACE ID=RECORD,                                             +
               DATA1=DSPCREQ_RECORD_ADDR,                              +
               DATA2=DSPCREQ_RECORD_LENGTH
* ------------------------------------------------------------------- *
*        Format fields                                                *
* ------------------------------------------------------------------- *
         MVC   USEREXIT_RECORD_LENGTH,DSPCREQ_RECORD_LENGTH
         MVC   USEREXIT_RECORD_NBR,DSPCREQ_RECORD_NBR
         MVC   USEREXIT_ROWS,SPF_VDEPTH
         MVC   USEREXIT_COLUMNS,SPF_VWIDTH
         MVI   USEREXIT_FUNC,$USEREXIT_FORMAT SET FUNCTION
         LA    R1,USEREXIT                    EXIT PARMS
         L     R15,SESS_FUNCTION_EXIT         FUNCTION EXIT EP
         ITRACE ID=CALLEXIT,                                           +
               RDATA1=R15
         BALR  R14,R15                        LINK TO EXIT
         MVC   SPF_MSG_1,USEREXIT_MSG_1
         MVC   SPF_MSG_2,USEREXIT_MSG_2
         MVC   SPF_MSG_3,USEREXIT_MSG_3
         MVC   SPF_MSG_4,USEREXIT_MSG_4
         MVC   SPF_MSG_5,USEREXIT_MSG_5
* ------------------------------------------------------------------- *
*        Build the DSCB info                                          *
* ------------------------------------------------------------------- *
MAIN0100 DS    0H
         MVC   DATASPACE_1_DISPLAY_LAST_RECORD,DSPCREQ_RECORD_NBR
         MVC   SPF_COLUMN_FROM,DATASPACE_1_DISPLAY_FIRST_COLUMN
         ITRACE ID=OSDSCB
         L     R15,COMM_V_OSDSCB              OSDSCB ENTRY POINT
         BALR  R14,R15                        FORMAT THE DSCB INFO
* ------------------------------------------------------------------- *
*        DISPLAY THE PANEL                                            *
* ------------------------------------------------------------------- *
MAIN0110 DS    0H
         ITRACE ID=DISPLAY,                                            +
               DATA1=WORK_PANEL,                                       +
               RDATA2=R15
         MVC   SESS_DISP_PANEL,WORK_PANEL
         MVC   SESS_DISP_VDATA_ADDR,WORK_VDATA_ADDR
         MVC   SESS_DISP_VDATA_LENGTH,WORK_VDATA_LENGTH
         XC    SESS_DISP_CURSOR,SESS_DISP_CURSOR
         L     R15,COMM_V_OSDISP              OSDISP ENTRY POINT
         BALR  R14,R15                        LINK TO OSDISP
         ITRACE ID=DISP_RC,                                            +
               DATA1=(SESS_RC,2),                                      +
               DATA2=(SESS_STATUS,1)
         NI    SESS_SEARCH_FLAGS,255-$SESS_SEARCH_NOW
         CLC   COMM_NEW_FORMAT,COMM_BLANKS    NEW FORMAT WANTED?
         BNE   EXIT0000                       YES
         CLI   COMM_SESS_FUNC,0               SESSION FUNCTION?
         BNE   EXIT0000                       YES
         OC    SESS_RC,SESS_RC                RC=0
         BZ    MAIN0200                       YES
         ITRACE ID=ENDING
         MVI   COMM_SESS_FUNC,$SESS_REMOVE    SET FUNCTION
         B     EXIT0000
* ------------------------------------------------------------------- *
*        Process command(s)                                           *
* ------------------------------------------------------------------- *
MAIN0200 DS    0H
         MVC   USEREXIT_ZCMD,SPF_ZCMD
         MVC   USEREXIT_ZVERB,SPF_ZVERB
         MVC   USEREXIT_ZSCROLLA,SPF_ZSCROLLA
         MVC   USEREXIT_ZSCROLLN,SPF_ZSCROLLN
         MVI   USEREXIT_FUNC,$USEREXIT_CMD
         LA    R1,USEREXIT
         L     R15,SESS_FUNCTION_EXIT
         ITRACE ID=CMD
         BALR  R14,R15                        CALL EXIT FOR COMMAND
         MVC   DATASPACE_1_DISPLAY_FIRST_RECORD,USEREXIT_RECORD_NBR
         B     MAIN0010
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN1000 DS    0H
         MVC   SPF_MSG_1(EOF_MSG_L),EOF_MSG
         B     MAIN0100
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0010  DS    0H
         CLI   SESS_FORMAT_FLAGS,$FORMAT_CLEANUP
         BE    EXIT0000
         DC    H'0'
ERR910   DS    0H
         MVC   SPF_MSG_1,COMM_MSG_1           COPY MESSAGE
         MVC   SPF_MSG_2,COMM_MSG_2           COPY MESSAGE
         MVC   SPF_MSG_3,COMM_MSG_3           COPY MESSAGE
         MVC   SPF_MSG_4,COMM_MSG_4           COPY MESSAGE
         MVC   SPF_MSG_5,COMM_MSG_5           COPY MESSAGE
         B     MAIN0100
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
CLEAN000 DS    0H
         ITRACE ID=CLEAN_UP
         ICM   R1,15,WORK_VDATA_ADDR
         BZ    CLEAN010
         L     R0,WORK_VDATA_LENGTH
         MVI   COMM_VDATA_FUNC,$VDATA_FREEMAIN
         L     R15,COMM_V_OSVDATA
         BALR  R14,R15
CLEAN010 DS    0H
         LA    R0,OSUSER_WORK_L               WORK AREA SIZE
         LR    R1,R13                         COPY WORK AREA ADDRESS
         L     R13,4(,R13)                    RESTORE R13
         FREEMAIN RU,                         FREE WORK REA            +
               A=(1),                                                  +
               LV=(0)
         XC    SESS_DXD_ADDR,SESS_DXD_ADDR
         XC    SESS_DXD_LENGTH,SESS_DXD_LENGTH
         SR    R15,R15
         B     EXIT0010
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=EXIT
         LH    R15,WORK_RC                    RETURN CODE
         L     R13,4(,R13)                    RESTORE R13
EXIT0010 DS    0H
         L     R14,12(,R13)                   RESTORE R14
         LM    R0,R12,20(R13)                 RESTORE REGISTERS
         BR    R14                            RETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
OS0001          DC       CL8'OS0001'

EOF_MSG         DS       0C
                DC       AL1($SCREEN_ATTR_HIGH)
                DC       C'---- END OF DATA ----'
EOF_MSG_L       EQU      *-EOF_MSG

                LTORG

OSUSEREND       EQU  *
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
OSUSER_WORK        DSECT
                   COPY     DXDPREF
WORK_VDATA_ADDR    DS       A
WORK_VDATA_LENGTH  DS       F
WORK_PANEL         DS       CL8
WORK_RC            DS       H
                   DSPCREQ  DSECT=NO
                   USEREXIT DSECT=NO
OSUSER_WORK_L      EQU      *-OSUSER_WORK
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                BPXYSTAT DSECT=YES,LIST=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                OSSPFD   TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                COPY     ATTRS
* ------------------------------------------------------------------- *
*               EQUATES                                               *
* ------------------------------------------------------------------- *
                COPY     REGEQU
                END      OSUSER
