* ------------------------------------------------------------------- *
*                                                                     *
*  MODULE NAME: OSRDBSAM                                              *
*                                                                     *
*  Read sequential files or PDS members.                              *
*                                                                     *
*  The data set is already allocated.                                 *
*  The DD name is in SESS_DD.                                         *
*                                                                     *
*                                                                     *
*  The sequential file or PDS member is read a block at a time        *
*  using BSAM.  If the DCB RECFM is "blocked", the logical records    *
*  are "deblocked".  If the RECFM is "fixed", the records are         *
*  deblocked using the LRECL in the format-1 label.  If the RECFM     *
*  is "variable", the records are assumed to have "LLBB" info in      *
*  the first four bytes... the length taken from the first two bytes. *
*                                                                     *
*  The records are stored in data space 1.                            *
*                                                                     *
* ------------------------------------------------------------------- *
OSRDBSAM CSECT
OSRDBSAM AMODE 31
OSRDBSAM RMODE 24
         USING OSCOMM,R12
         USING SESSION,R11
         USING OSRDBSAM,R15
         B     INIT0000
MODID    DC    CL8'OSRDBSAM'
         DC    CL8'&SYSDATE'
         DC    CL8'&SYSTIME'
         DC    A(BSAMEND-OSRDBSAM)
INIT0000 DS    0H
         USING DS1FMTID,SESS_DSCB               DEFINE  BASE
         STM   R14,R12,12(R13)                  SAVE REGS
         LR    R10,R15                          COPY ENTRY POINT
         DROP  R15
         USING OSRDBSAM,R10
         GETMAIN RU,                                                   +
               LV=OSRDBSAM_WORK_L,                                     +
               LOC=BELOW
         ST    R13,4(,R1)                       SAVE R13
         ST    R1,8(,R13)                       CHAIN SAVE AREA
         LR    R13,R1                           COPY WORK AREA ADDRESS
         USING OSRDBSAM_WORK,R13                DEFINE WORK AREA BASE
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY,                                              +
               RDATA1=R13
         L     R9,COMM_OSSPFD
         USING OSSPFD,R9
         MVC   SESS_DATA_LOADER,MODID           WHO LOADED DATA
         MVC   WORK_RETRY,SESS_RETRY            SAVE RETRY ADDRESS
         LA    R1,RTRY0000                      OUR RETRY ADDRESS
         ST    R1,SESS_RETRY                    PUT IT INTO EFFECT
         XC    DSPCREQ_RECORD_NBR,DSPCREQ_RECORD_NBR
         LA    R0,DCB_L                         DCB LENGTH
         ST    R0,SESS_DCB_LENGTH
         GETMAIN RU,                                                   +
               LV=(0),                                                 +
               LOC=BELOW
         LR    R7,R1
         ST    R7,SESS_DCB_ADDR
         MVC   0(DCB_L,R7),DCB_I
         USING IHADCB,R7                        DEFINE DCB ADDRESS
         USING DECB,WORK_DECB                   DEFINE BASE
         LA    R1,DATASPACE_1                   USE DATA SPACE 1
         ST    R1,DSPCREQ_DATASPACE
         TM    SESS_EDIT_FLAGS,$SESS_EDIT       EDIT SESSION?
         BO    INIT0020                         YES
         MVC   SESS_DEFAULT_FORMAT,BROWSE
         TM    SESS_KEYWORD_FLAGS,$KEYWORD_FORMAT
         BO    INIT0010
         MVC   SESS_FORMAT,BROWSE
INIT0010 DS    0H
         XC    WORK_LENGTH,WORK_LENGTH
         B     INIT0030
INIT0020 DS    0H
         ITRACE ID=EDIT
         MVC   SESS_DEFAULT_FORMAT,EDIT
         MVC   SESS_FORMAT,EDIT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
INIT0030 DS    0H
         TM    SESS_EDIT_FLAGS,$SESS_EDIT_EMPTY EMPTY FILE OR MEMBER?
         BO    MAIN0150
         MVC   WORK_OPEN(OPEN_L),OPEN_I
         MVC   DCBDDNAM,SESS_DD                 SET DD NAME IN DCB
         ITRACE ID=OPEN,                                               +
               RDATA1=R7,                                              +
               DATA2=DCBDDNAM
         TM    DS1DSORG+1,DS1ORGAM              VSAM?
         BNO   INIT0040                         NO
         OI    COMM_FLAGS,$COMM_AUTH_ON         NEED AUTHORIZATION
         L     R15,COMM_V_OSAUTH                OSAUTH ENTRY POINT
         BALR  R14,R15                          AUTHORIZE OURSELVES
         ITRACE ID=SUP
         MODESET MODE=SUP
INIT0040 DS    0H
         OPEN  ((R7),INPUT),                                           +
               MODE=31,                                                +
               MF=(E,WORK_OPEN)
         TM    DCBOFLGS,DCBOFOPN                SUCCESFUL?
         BNO   ERR0010                          NO
         OI    SESS_DCB_FLAGS,$SESS_DCB_OPEN    INDICATE DCB IS OPEN
         TM    DCBRECFM,DCBRECU                 RECFM=U?
         BNO   MAIN0000                         NO
         OC    DCBLRECL,DCBLRECL                LRECL ZERO?
         BNZ   MAIN0000                         NO
         OC    DCBBLKSI,DCBBLKSI                BLOCK SIZE ZERO ALSO?
         BNZ   MAIN0000                         NO
         MVC   DCBLRECL,H32760                  SET LRECL TO 32760
         MVC   DCBBLKSI,H32760                  SET BLOCK SIZE TO 32760
         MVC   DS1LRECL,H32760                  SET LRECL TO 32760
         MVC   DS1BLKL,H32760                   SET BLOCK SIZE TO 32760
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0000 DS    0H
         L     R8,SESS_IO_AREA                  RESET I/O AREA ADDRESS
         ITRACE ID=READ,                                               +
               RDATA1=R7,                                              +
               RDATA2=R8
         XC    WORK_DECB(DECB_L),WORK_DECB
         READ  WORK_DECB,                       READ                   +
               SF,                              .. SEQUENTIALLY FWD    +
               (R7),                            .. DCB ADDRESS         +
               (R8),                            .. I/O AREA            +
               'S',                                                    +
               MF=E
*        SAM31
         ITRACE ID=CHECK
         CHECK WORK_DECB                        WAIT FOR THE I/O
         L     R6,DECIOBPT                      IOB ADDRESS
         USING IOBSTDRD,R6                      DEFINE IOB BASE
         MVC   DSPCREQ_CCHHR,IOBCC              SAVE DISK ADDR
         ITRACE ID=CCHHR,DATA1=(IOBCC,5)
         LH    R5,DCBBLKSI                      BLOCK SIZE
         SR    R1,R1                            CLEAR R1
         ICM   R1,3,IOBSTDRD+14                 RESIDUAL LENGTH
         SR    R5,R1                            LENGTH READ
         ITRACE ID=RECORD,                                             +
               RDATA1=R8,                                              +
               RDATA2=R5
         XC    WORK_SEQ,WORK_SEQ                RESET SEQUENCE NBR
MAIN0010 DS    0H
         TM    DCBRECFM,DCBRECU                 RECFM U?
         BO    MAIN0110                         YES
         TM    DCBRECFM,DCBRECBR                BLOCKED RECORDS?
         BNO   MAIN0110                         NO
         TM    DCBRECFM,DCBRECV                 RECFM V?
         BO    MAIN0050                         YES
* ------------------------------------------------------------------- *
*        Fixed blocked records                                        *
* ------------------------------------------------------------------- *
MAIN0020 DS    0H
         LTR   R5,R5                            ANY DATA LEFT?
         BZ    MAIN0000                         NO.. READ NEXT BLOCK
         BM    ERR0050
         ITRACE ID=FIXED,                                              +
               RDATA1=R8,                                              +
               RDATA2=R5
         ST    R8,DSPCREQ_RECORD_ADDR
         XC    DSPCREQ_RECORD_LENGTH(2),DSPCREQ_RECORD_LENGTH
         MVC   DSPCREQ_RECORD_LENGTH+2(2),DCBLRECL
         BAL   R4,MAIN0200
MAIN0030 DS    0H
         OC    SESS_NUMBER,SESS_NUMBER          SPECIFIC NBR TO READ?
         BZ    MAIN0040                         NO
         CLC   DATASPACE_1_LAST_RECORD,SESS_NUMBER       STOP POINT?
         BNL   EOF0010                          YES.. STOP READING
MAIN0040 DS    0H
         AH    R8,DCBLRECL                      PLUS RECORD SIZE
         SH    R5,DCBLRECL                      MINUS RECORD SIZE
         B     MAIN0020                         LOOP
* ------------------------------------------------------------------- *
*        Variable length, blocked records                             *
* ------------------------------------------------------------------- *
MAIN0050 DS    0H
         SR    R5,R5                            CLEAR R5
         ICM   R5,3,0(R8)                       BLOCK LENGTH
         BZ    MAIN0000                         SHOULD NOT HAPPEN
         SH    R5,H4                            MINUS LENGTH OF BDW
         LA    R8,4(,R8)                        SKIP BDW
MAIN0060 DS    0H
         LTR   R5,R5                            DATA LEFT?
         BZ    MAIN0000                         NO
         ITRACE ID=VAR,                                                +
               RDATA1=R8,                                              +
               RDATA2=R5
         TM    2(R8),X'03'                      PART OF SPANNED RECORD?
         BZ    MAIN0070                         NO
         LA    R0,4(,R8)                        DATA PORTION
         LH    R1,0(R8)                         LENGTH OF DATA
         SH    R1,H4                            MINUS LENGTH OF LLBB
         LA    R14,WORK_IO
         AH    R14,WORK_LENGTH
         LR    R15,R1
         MVCL  R14,R0                           COPY DATA TO BUFFER
         LH    R1,WORK_LENGTH
         AH    R1,0(R8)                         PLUS NEW DATA LENGTH
         SH    R1,H4                            MINUS LENGTH OF LLBB
         STH   R1,WORK_LENGTH
         TM    2(R8),X'02'                      END OF SPANNED RECORD?
         BNO   MAIN0100                         NO
         LA    R1,WORK_IO                       DATA
         ST    R1,DSPCREQ_RECORD_ADDR           SET ADDRESS
         LH    R1,WORK_LENGTH                   DATA LENGTH
         XC    WORK_LENGTH,WORK_LENGTH
         B     MAIN0080
MAIN0070 DS    0H
         LA    R1,4(,R8)                        DATA ADDRESS
         ST    R1,DSPCREQ_RECORD_ADDR
         LH    R1,0(R8)                         RECORD LENGTH
         SH    R1,H4                            MINUS RDW LENGTH
MAIN0080 DS    0H
         ST    R1,DSPCREQ_RECORD_LENGTH
         ITRACE ID=VAR_RECD,                                           +
               DATA1=(DSPCREQ_RECORD_ADDR,4),                          +
               DATA2=(DSPCREQ_RECORD_LENGTH,4)
         BAL   R4,MAIN0200
MAIN0090 DS    0H
         OC    SESS_NUMBER,SESS_NUMBER          SPECIFIC NBR TO READ?
         BZ    MAIN0100                         NO
         CLC   DATASPACE_1_LAST_RECORD,SESS_NUMBER
         BNL   EOF0010                          TIME TO STOP
MAIN0100 DS    0H
         SH    R5,0(R8)                         MINUS RECORD SIZE
         BM    ERR0040
         BZ    MAIN0000                         END OF BLOCK
         AH    R8,0(R8)                         PLUS RECORD SIZE
         B     MAIN0060                         LOOP
* ------------------------------------------------------------------- *
*        Unblocked or undefined records                               *
* ------------------------------------------------------------------- *
MAIN0110 DS    0H
         ITRACE ID=UNBLOCKD,                                           +
               RDATA1=R8,                                              +
               RDATA2=R5
         TM    DCBRECFM,DCBRECU                 UNDEFINED LENGTH?
         BO    MAIN0120                         YES
         TM    DCBRECFM,DCBRECF                 FIXED LENGTH?
         BO    MAIN0120                         YES
         ITRACE ID=RECFM_V
         LH    R5,0(R8)                         LENGTH OF BLOCK
         SH    R5,H4                            MINUS BDW LENGTH
         LA    R8,4(,R8)                        LEAVE OUT BDW
MAIN0120 DS    0H
         ST    R8,DSPCREQ_RECORD_ADDR
         ST    R5,DSPCREQ_RECORD_LENGTH
         BAL   R4,MAIN0200
         OC    SESS_NUMBER,SESS_NUMBER          SPECIFIC NBR TO READ?
         BZ    MAIN0000                         NO
         CLC   DATASPACE_1_LAST_RECORD,SESS_NUMBER
         BNL   EOF0010                          TIME TO STOP
         B     MAIN0000                         LOOP
* ------------------------------------------------------------------- *
*        Empty file or PDS member                                     *
* ------------------------------------------------------------------- *
MAIN0150 DS    0H
         ITRACE ID=EMPTY
         MVC   COMM_MSG_1(MSG_03_L),MSG_03
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0200 DS    0H
         L     R1,DSPCREQ_RECORD_NBR
         LA    R1,1(,R1)
         ST    R1,DSPCREQ_RECORD_NBR
         CLC   DSPCREQ_RECORD_NBR,DATASPACE_1_FIRST_RECORD
         BLR   R4                               LOW.. DON'T STORE IT
         MVI   DSPCREQ_FUNC,$DSPCREQ_STORE
         LH    R1,WORK_SEQ
         LA    R1,1(,R1)
         STH   R1,WORK_SEQ
         ST    R1,DSPCREQ_SEQ                   SEQ NBR
         LA    R1,DSPCREQ                       DATA SPACE PARMS
         L     R15,COMM_OSDSPACE                DATA SPACE MANAGER
         BALR  R14,R15                          LINK TO OSDSPACE
         CLI   DSPCREQ_RC,$DSPCREQ_FULL         DATA SPACE FULL?
         BE    EOF0000                          YES
         CLI   DSPCREQ_RC,$DSPCREQ_OK           SUCCESSFUL?
         BNE   EXIT0000                         NO
         BR    R4
* ------------------------------------------------------------------- *
*        EOF                                                          *
* ------------------------------------------------------------------- *
EOF0000  DS    0H
         SAM31
         ITRACE ID=FULL,                                               +
               DATA1=(DATASPACE_1_LAST_RECORD,4) .. LOGICAL RECORD NBR
         B     EOF0030
EOF0010  DS    0H
         SAM31
         ITRACE ID=LOGICEOF,                                           +
               DATA1=(DATASPACE_1_LAST_RECORD,4) .. LOGICAL RECORD NBR
         B     EOF0030
EOF0020  DS    0H
         SAM31
         ITRACE ID=REAL_EOF
EOF0030  DS    0H
         MVC   WORK_CLOSE(CLOSE_L),CLOSE_I
         ITRACE ID=CLOSE,                                              +
               RDATA1=R7
         CLOSE ((R7)),                                                 +
               MODE=31,                                                +
               MF=(E,WORK_CLOSE)
         ITRACE ID=FREEPOOL
         FREEPOOL (R7)
         L     R0,SESS_DCB_LENGTH
         ITRACE ID=FREE_DCB,                                           +
               RDATA1=R7,                                              +
               RDATA2=R0
         FREEMAIN RU,                                                  +
               A=(R7),                                                 +
               LV=(0)
         XC    SESS_DCB_ADDR,SESS_DCB_ADDR
         XC    SESS_DCB_LENGTH,SESS_DCB_LENGTH
         NI    SESS_DCB_FLAGS,255-$SESS_DCB_OPEN
         L     R1,DSPCREQ_RECORD_NBR
         S     R1,DATASPACE_1_FIRST_RECORD
         LA    R1,1(,R1)
         CVD   R1,COMM_DWORD
         MVC   COMM_MSG_2,COMM_BLANKS
         MVC   COMM_MSG_2(MSG_01_L),MSG_01
         ED    COMM_MSG_2+(MSG_01_RECORDS-MSG_01)(L'MSG_01_RECORDS),COM+
               M_DWORD+3
         B     EXIT0000                         EXIT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
RTRY0000 DS    0H
         ITRACE ID=ABEND,                                              +
               DATA1=(SESS_SYSTEM_CODE,6)
         CLC   SESS_SYSTEM_CODE,X_0013
         BNE   RTRY0010
         CLC   SESS_ABEND_R15,X_0018
         BNE   RTRY0010
         ITRACE ID=NO_MEM,                                             +
               DATA1=SESS_MEMBER
         OI    SESS_STATUS,$STATUS_NO_MEMBER
         NI    SESS_ABEND_FLAG,255-$ABENDED
         MVC   COMM_MSG_LINES,H1
         MVC   COMM_MSG_1(MSG_02_L),MSG_02
         MVC   COMM_MSG_1+(MSG_02_MEMBER-MSG_02)(8),SESS_MEMBER
         B     EXIT0000
RTRY0010 DS    0H
         ITRACE ID=CALLABND
         L     R15,COMM_V_OSABEND
         BALR  R14,R15
         OI    SESS_STATUS,$STATUS_NO_DATA
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0010  DS    0H
         DC    H'1',C'OPEN failed'
ERR0020  DS    0H
         DC    H'2',C'Block size is zero after OPEN'
ERR0040  DS    0H
         DC    H'4'
         DC    C'Length left in VB record went negative'
ERR0050  DS    0H
         DC    H'5'
         DC    C'Length left in FB block went negative'
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=EXIT
         TM    DS1DSORG+1,DS1ORGAM              VSAM?
         BNO   EXIT0010                         NO
         ITRACE ID=PROB
         MODESET MODE=PROB                      RETURN TO PROBLEM STATE
         NI    COMM_FLAGS,255-$COMM_AUTH_ON     TURN OFF AUTHORIZATION
         L     R15,COMM_V_OSAUTH                OSAUTH ENTRY POINT
         BALR  R14,R15                          AUTHORIZE OURSELVES
EXIT0010 DS    0H
         MVC   SESS_RETRY,WORK_RETRY            RESET RETRY ADDRESS
         LR    R1,R13                           COPY WORK AREA ADDRESS
         L     R13,4(,R13)                      RESTORE SAVE AREA
         FREEMAIN RU,                                                  +
               A=(1),                                                  +
               LV=OSRDBSAM_WORK_L
         LM    R14,R12,12(R13)                  RESTORE REGISTERSS
         SR    R15,R15                          SET RC
         BR    R14                              RETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DCB_I    DCB   DSORG=PS,                                               +
               DDNAME=ANYDD,                                           +
               EODAD=EOF0010,                                          +
               MACRF=R
DCB_L          EQU   *-DCB_I
OPEN_I   OPEN  (*,INPUT),                                              +
               MODE=31,                                                +
               MF=L
OPEN_L         EQU   *-OPEN_I
CLOSE_I  CLOSE (*),                                                    +
               MODE=31,                                                +
               MF=L
CLOSE_L        EQU   *-CLOSE_I

F1             DC    F'1'

H1             DC    H'1'
H4             DC    H'4'
H5             DC    H'5'
H32760         DC    H'32760'

X_0013         DC    X'0013'
X_0018         DC    X'0018'

BROWSE         DC    CL8'BROWSE'
EDIT           DC    CL8'EDIT  '

MSG_01         DS    0C
MSG_01_RECORDS DC    X'402020206B2020206B202120'
               DC    C' records'
MSG_01_L       EQU   *-MSG_01

MSG_02         DC    0C
               DC    C'Member '
MSG_02_MEMBER  DC    CL8' '
               DC    C' not found'
MSG_02_L       EQU   *-MSG_02

MSG_03         DC    0C
               DC    C'Data set or member has no records'
MSG_03_L       EQU   *-MSG_03

               LTORG

BSAMEND        EQU  *
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
OSRDBSAM_WORK    DSECT
                 COPY  DXDPREF
WORK_OPEN        DS    0F,(OPEN_L)X
WORK_CLOSE       DS    0F,(CLOSE_L)X
WORK_DECB        DS    0F,(DECB_L)X

WORK_RETRY       DS    A                  SAVED RETRY ADDRESS

                 DSPCREQ DSECT=NO

WORK_SEQ         DS    H

WORK_LENGTH      DS    H
WORK_IO          DS    32768X

OSRDBSAM_WORK_L  EQU   *-OSRDBSAM_WORK
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 BPXYSTAT DSECT=YES,LIST=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 OSSPFD   TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         IBMMAC DCBD=PS,                                               +
               IOB=YES,                                                +
               DUMMY=X
FORMAT_1         DSECT
                 IBMMAC  VTOC=1
                 IBMMAC  DECB=YES
DECB_L           EQU     *-DECB
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COPY TRENTRY
* ------------------------------------------------------------------- *
*                EQUATES                                              *
* ------------------------------------------------------------------- *
                 COPY REGEQU
$BUFFSIZE        EQU  65535
                 END  OSRDBSAM
