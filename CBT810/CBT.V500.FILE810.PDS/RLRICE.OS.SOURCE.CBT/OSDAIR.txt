* ------------------------------------------------------------------- *
*                                                                     *
*  MODULE NAME: OSDAIR                                                *
*                                                                     *
*  Interface with the dynamic allocation (DAIR)                       *
*                                                                     *
*  The parameters are passed via a DAIRREQ control block.             *
*                                                                     *
*  The DYNALLOC (SVC 99) return code (R15), error, and reason         *
*  codes are passed back in the same DAIRREQ block.                   *
*                                                                     *
*  If the return code is non zero, OSMSG will be called to            *
*  create messages that are in display format.                        *
*                                                                     *
*                                                                     *
*  DAIRREQ is in the MACLIB.                                          *
*                                                                     *
* ------------------------------------------------------------------- *
OSDAIR   CSECT
OSDAIR   AMODE  31
OSDAIR   RMODE  ANY
         USING  OSDAIR,R15
         USING  OSCOMM,R12
         USING  SESSION,R11
         B      INIT0000
MODID    DC     CL8'OSDAIR'
         DC    CL8'&SYSDATE'
         DC    CL8'&SYSTIME'
         DC    A(DAIREND-OSDAIR)
INIT0000 DS    0H
         STM   R14,R12,12(R13)                SAVE REGISTERS
         LR    R10,R15                        COPY BASE ADDRESS
         DROP  R15
         USING OSDAIR,R10
         L     R15,COMM_DXD                   DXD AREA BASE ADDRESS
         A     R15,DXD_START                  PLUS OFFSET
         ST    R13,4(,R15)
         ST    R15,8(,R13)
         LR    R13,R15                        COPY DXD AREA ADDRESS
         USING DXDDAIR,R13                    DEFINE WORK AREA BASE
         MVC   DXD_CSECT,MODID
         MVC   DXD_UNIT,COMM_BLANKS
         MVC   DXD_VOLSER,COMM_BLANKS
* ------------------------------------------------------------------- *
         LR    R9,R1
         USING DAIRREQ,R9                     DEFINE BASE
         USING S99RB,DXD_SVC99_RB
         ITRACE ID=ENTRY,                                              +
               RDATA1=R9,                                              +
               DATA2=(DAIR_FUNC,1)
         TM    DXD_FLAGS,$FLAG_INIT           WORK AREA INITIALIZED?
         BO    DAIR0010                       YES
         ITRACE ID=INIT
         MVC   DXD_CSECT,MODID
         MVC   DXD_TEXT_UNITS(TEXT_UNITS_L),TEXT_UNITS_I
         XC    DXD_SVC99_RB(S99RBEND-S99RB),DXD_SVC99_RB
         LA    R1,DXD_SVC99_RB
         ST    R1,DXD_SVC99_RB_ADDRESS
         OI    DXD_SVC99_RB_ADDRESS,X'80'
         MVI   S99RBLN,S99RBEND-S99RB
         LA    R1,DXD_TEXT_POINTER_1
         ST    R1,S99TXTPP                    FIRST TEXT UNIT ADDRESS
         OI    DXD_FLAGS,$FLAG_INIT           SET INIT INDICATOR
DAIR0010 DS    0H
         CLI   DAIR_FUNC,$DAIR_ALLOC          ALLOCATE?
         BE    DAIR0100                       YES
         CLI   DAIR_FUNC,$DAIR_FREE           FREE?
         BE    DAIR0300                       YES
         CLI   DAIR_FUNC,$DAIR_CONCAT         CONCATENATE?
         BE    DAIR0400                       YES
         ABEND 1,DUMP,,USER                   BAD NEWS
* ------------------------------------------------------------------- *
*        Allocation                                                   *
* ------------------------------------------------------------------- *
DAIR0100 DS    0H
         ITRACE ID=ALLOC
         MVI   S99VERB,S99VRBAL               SET VERB
         TM    DAIR_OPTS,$DAIR_SYSOUT         SYSOUT?
         BO    DAIR0200                       YES
         TM    DAIR_OPTS,$DAIR_USE_DD
         BO    DAIR0110
         AP    COMM_DD_NUMBER,P1              ADD 1
         MVC   DXD_DD,DD_EDIT_WORD            INITIALIZE WITH EDIT WORD
         ED    DXD_DD,COMM_DD_NUMBER
         MVC   DXD_DD(L'OS_DD),OS_DD          SET DD PREFIX
         MVC   DAIR_DD,DXD_DD                 PASS IT BACK TO CALLER
         ITRACE ID=NEW_DD,                                             +
               DATA1=DAIR_DD
         B     DAIR0120
DAIR0110 DS    0H
         ITRACE ID=USE_DD,                                             +
               DATA1=DAIR_DD
         MVC   DXD_DD,DAIR_DD                 COPY SPECIFIED DD NAME
DAIR0120 DS    0H
         TM    DAIR_OPTS,$DAIR_INTRDR         INTERNAL READER?
         BO    DAIR0210                       YES
         TM    DAIR_OPTS,$DAIR_DUMMY          DUMMY?
         BO    DAIR0220                       YES
         TM    DAIR_OPTS,$DAIR_VOLUME         ALLOCATE VOLUME?
         BO    DAIR0230                       YES
         TM    DAIR_OPTS,$DAIR_TEMP           TEMP?
         BO    DAIR0240                       YES
         LA    R2,DXD_TEXT_POINTER_1          FIRST TEXT UNIT POINTER
         LA    R1,DXD_DD_TEXT_UNITS           DD TEXT UNITS
         ST    R1,0(,R2)                      SET ADDRESS
         LA    R2,4(,R2)
         MVC   DXD_DSN,DAIR_DSN               COPY DATA SET NAME
         ITRACE ID=DSN_1,                                              +
               DATA1=DXD_DSN+00,                                       +
               DATA2=DXD_DSN+08
         ITRACE ID=DSN_2,                                              +
               DATA1=DXD_DSN+16,                                       +
               DATA2=DXD_DSN+24
         ITRACE ID=DSN_3,                                              +
               DATA1=DXD_DSN+32,                                       +
               DATA2=DXD_DSN+40
         LA    R1,DXD_DSN_TEXT_UNITS          DSN TEXT UNITS
         ST    R1,0(,R2)                      SET ADDRESS
         LA    R2,4(,R2)                      NEXT ADDRESS
         MVC   DXD_INIT_DISP,DAIR_DISP
         LA    R1,DXD_INIT_DISP_TEXT_UNITS
         ST    R1,0(,R2)                      SET ADDRESS
         LA    R2,4(,R2)                      NEXT ADDRESS
         CLI   DAIR_DISP,$DAIR_DISP_NEW       NEW?
         BE    DAIR0130                       YES
         CLC   DAIR_MEMBER,COMM_BLANKS        MEMBER?
         BE    DAIR0140                       NO
         MVC   DXD_MEMBER,DAIR_MEMBER         COPY MEMBER NAME
         ITRACE ID=MEMBER,                                             +
               DATA1=DXD_MEMBER
         LA    R1,DXD_MEMBER_TEXT_UNITS       MEMBER TEXT UNITS
         ST    R1,0(,R2)                      SET ADDRESS
         LA    R2,4(,R2)                      NEXT ADDRESS
         B     DAIR0140                       SKIP CATLG TEXT UNITS
DAIR0130 DS    0H
         LA    R1,DXD_NORM_DISP_TEXT_UNITS    DISP (CATLG)
         ST    R1,0(,R2)                      SET ADDRESS
         LA    R2,4(,R2)                      NEXT ADDRESS
DAIR0140 DS    0H
         CLC   DAIR_VOLSER,COMM_BLANKS        VOLSER GIVEN?
         BE    DAIR0160                       NO
         MVC   DXD_VOLSER,DAIR_VOLSER         COPY VOLSER
         LA    R1,DXD_VOLSER_TEXT_UNITS       TEXT UNIT ADDRESS
         ST    R1,0(,R2)                      SET ADDRESS IN PARM LIST
         LA    R2,4(,R2)                      NEXT TEXT UNIT POINTER
         MVC   DXD_UNIT,DAIR_UNIT             COPY UNIT NAME
         CLC   DXD_UNIT,COMM_BLANKS           UNIT GIVEN?
         BNE   DAIR0150                       YES
         MVC   DXD_UNIT,SYSALLDA              SET UNIT TO SYSALLDA
DAIR0150 DS    0H
         ITRACE ID=VOLSER,                                             +
               DATA1=(DXD_VOLSER,6),                                   +
               DATA2=DXD_UNIT
         LA    R1,DXD_UNIT_TEXT_UNITS         TEXT UNIT ADDRESS
         ST    R1,0(,R2)                      SET ADDRESS IN PARM LIST
         LA    R2,4(,R2)                      NEXT TEXT UNIT POINTER
         B     DAIR0170
DAIR0160 DS    0H
         MVC   DXD_UNIT,DAIR_UNIT             COPY UNIT NAME
         CLC   DXD_UNIT,COMM_BLANKS           UNIT GIVEN?
         BE    DAIR0170                       NO
         ITRACE ID=UNIT,                                               +
               DATA1=DXD_UNIT
         MVC   DXD_UNIT,SYSALLDA              SET UNIT NAME
         LA    R1,DXD_UNIT_TEXT_UNITS         TEXT UNIT ADDRESS
         ST    R1,0(,R2)                      SET ADDRESS IN PARM LIST
         LA    R2,4(,R2)                      NEXT TEXT UNIT POINTER
DAIR0170 DS    0H
         CLI   DAIR_DISP,$DAIR_DISP_NEW       NEW?
         BNE   DAIR0180                       NO
         LA    R1,DXD_SPACE_UNITS_TEXT_UNITS
         ST    R1,0(,R2)                      SET ADDRESS
         LA    R2,4(,R2)                      NEXT TEXT UNIT ADDRESS
         MVC   DXD_PRI,DAIR_PRI               COPY PRIMARY QTY
         LA    R1,DXD_PRI_TEXT_UNITS
         ST    R1,0(,R2)                      SET ADDRESS
         LA    R2,4(,R2)                      NEXT TEXT UNIT ADDRESS
         MVC   DXD_SEC,DAIR_SEC               COPY PRIMARY QTY
         LA    R1,DXD_SEC_TEXT_UNITS
         ST    R1,0(,R2)                      SET ADDRESS
         LA    R2,4(,R2)                      NEXT TEXT UNIT ADDRESS
DAIR0180 DS    0H
         SH    R2,H4                          LAST USED 'POINTER'
         OI    0(R2),X'80'                    FLAG END-OF-LIST
         B     DAIR0500
* ------------------------------------------------------------------- *
*        Allocate SYSOUT (with FREE=CLOSE)                            *
* ------------------------------------------------------------------- *
DAIR0200 DS    0H
         ITRACE ID=SYSOUT,                                             +
               DATA1=DAIR_DD,                                          +
               DATA2=(DAIR_SYSOUT,1)
         MVC   DXD_DD,DAIR_DD
         MVC   DXD_SYSOUT,DAIR_SYSOUT
         LA    R1,DXD_DD_TEXT_UNITS
         ST    R1,DXD_TEXT_POINTER_1
         LA    R1,DXD_SYSOUT_TEXT_UNITS
         ST    R1,DXD_TEXT_POINTER_2
         LA    R1,DXD_FREE_TEXT_UNITS
         ST    R1,DXD_TEXT_POINTER_3
         OI    DXD_TEXT_POINTER_3,X'80'
         B     DAIR0500
* ------------------------------------------------------------------- *
*        Allocate Internal reader (with FREE=CLOSE)                   *
* ------------------------------------------------------------------- *
DAIR0210 DS    0H
         ITRACE ID=INTRDR,                                             +
               DATA1=DAIR_DD,                                          +
               DATA2=(DAIR_SYSOUT,1)
         MVC   DXD_DD,DAIR_DD
         MVC   DXD_SYSOUT,DAIR_SYSOUT
         LA    R1,DXD_DD_TEXT_UNITS
         ST    R1,DXD_TEXT_POINTER_1
         LA    R1,DXD_SYSOUT_TEXT_UNITS
         ST    R1,DXD_TEXT_POINTER_2
         LA    R1,DXD_FREE_TEXT_UNITS
         ST    R1,DXD_TEXT_POINTER_3
         LA    R1,DXD_SYSOUT_PROGRAM_TEXT_UNITS
         ST    R1,DXD_TEXT_POINTER_4
         OI    DXD_TEXT_POINTER_4,X'80'
         B     DAIR0500
* ------------------------------------------------------------------- *
*        Allocate DUMMY DD                                            *
* ------------------------------------------------------------------- *
DAIR0220 DS    0H
         ITRACE ID=DUMMY
         LA    R1,DXD_DD_TEXT_UNITS
         ST    R1,DXD_TEXT_POINTER_1
         LA    R1,DXD_DUMMY_TEXT_UNITS
         ST    R1,DXD_TEXT_POINTER_2
         OI    DXD_TEXT_POINTER_2,X'80'
         B     DAIR0500
* ------------------------------------------------------------------- *
*        Allocate a volume                                            *
* ------------------------------------------------------------------- *
DAIR0230 DS    0H
         ITRACE ID=VOLUME
         MVC   DXD_INIT_DISP,DAIR_DISP
         MVC   DXD_VOLSER,SESS_VOLSER
         MVC   DXD_UNIT,SYSALLDA
         LA    R1,DXD_DD_TEXT_UNITS
         ST    R1,DXD_TEXT_POINTER_1
         LA    R1,DXD_UNIT_TEXT_UNITS
         ST    R1,DXD_TEXT_POINTER_2
         LA    R1,DXD_VOLSER_TEXT_UNITS
         ST    R1,DXD_TEXT_POINTER_3
         LA    R1,DXD_INIT_DISP_TEXT_UNITS
         ST    R1,DXD_TEXT_POINTER_4
         OI    DXD_TEXT_POINTER_4,X'80'
         B     DAIR0500
* ------------------------------------------------------------------- *
*        Allocate a temp data set                                     *
* ------------------------------------------------------------------- *
DAIR0240 DS    0H
         ITRACE ID=TEMP
         MVC   DXD_DSN,COMM_BLANKS
         AP    COMM_DD_NUMBER,P1              ADD 1
         MVC   DXD_DSN(L'DD_EDIT_WORD),DD_EDIT_WORD
         ED    DXD_DSN(L'DD_EDIT_WORD),COMM_DD_NUMBER
         MVI   DXD_DSN+0,C'&&'                INSERT AN AMPERSAND
         MVI   DXD_DSN+1,C'&&'                INSERT SECOND AMPERSAND
         MVI   DXD_DSN+2,C'T'                 INSERT A 'T'
         MVI   DXD_INIT_DISP,X'04'            DISP=NEW
         MVC   DXD_UNIT,SYSDA                 UNIT=SYSDA
         OC    DAIR_PRI,DAIR_PRI              USER SPECIFIED SPACE?
         BNZ   DAIR0250                       YES
         MVC   DXD_PRI,NINETY                 PRIMARY QTY=90
         MVC   DXD_SEC,NINETY                 SECONDARY QTY=90
         B     DAIR0260
DAIR0250 DS    0H
         MVC   DXD_PRI,DAIR_PRI               USE USER QTY
         MVC   DXD_SEC,DAIR_SEC
DAIR0260 DS    0H
         LA    R1,DXD_DD_TEXT_UNITS
         ST    R1,DXD_TEXT_POINTER_1
         LA    R1,DXD_INIT_DISP_TEXT_UNITS
         ST    R1,DXD_TEXT_POINTER_2
         LA    R1,DXD_UNIT_TEXT_UNITS
         ST    R1,DXD_TEXT_POINTER_3
         LA    R1,DXD_PRI_TEXT_UNITS
         ST    R1,DXD_TEXT_POINTER_4
         LA    R1,DXD_SEC_TEXT_UNITS
         ST    R1,DXD_TEXT_POINTER_5
         OI    DXD_TEXT_POINTER_5,X'80'
         B     DAIR0500
* ------------------------------------------------------------------- *
*        Free a DD                                                    *
* ------------------------------------------------------------------- *
DAIR0300 DS    0H
         ITRACE ID=FREE,                                               +
               DATA1=DAIR_DD
         MVI   S99VERB,S99VRBUN               SET VERB
         MVC   DXD_DD,DAIR_DD
         LA    R1,DXD_DD_TEXT_UNITS           DD TEXT UNITS
         ST    R1,DXD_TEXT_POINTER_1          SET ADDRESS
         OI    DXD_TEXT_POINTER_1,X'80'
         B     DAIR0500
* ------------------------------------------------------------------- *
*        Concatenate DDs                                              *
* ------------------------------------------------------------------- *
DAIR0400 DS    0H
         ITRACE ID=CONCAT,                                             +
               DATA1=DAIR_DD,                 .. 1ST DD                +
               DATA2=DAIR_DSN                 .. 2ND DD
         MVI   S99VERB,S99VRBCC               SET VERB
         MVC   DXD_CONCAT_DD1,DAIR_DD         COPY DD 1
         MVC   DXD_CONCAT_DD2,DAIR_DSN        COPY DD 2
         LA    R1,DXD_CONCAT_TEXT_UNITS       CONCAT TEXT UNITS
         ST    R1,DXD_TEXT_POINTER_1          SET ADDRESS
         OI    DXD_TEXT_POINTER_1,X'80'
         B     DAIR0500
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DAIR0500 DS    0H
         LA    R1,DXD_SVC99_RB_ADDRESS
         ITRACE ID=SVC_99,                                             +
               RDATA1=R1
         SVC   99                             ISSUE THE SVC
         STCM  R15,3,DAIR_R15                 SAVE R15
         MVC   DAIR_ERROR,S99ERROR            COPY ERROR CODE
         MVC   DAIR_INFO,S99INFO              COPY INFO CODE
         ITRACE ID=SVC_99RC,                                           +
               RDATA1=R15,                                             +
               DATA2=(DAIR_ERROR,4)
         LTR   R15,R15                        SUCCESSFUL?
         BZ    EXIT0000                       YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         TM    DAIR_OPTS,$DAIR_NO_MSGS        MESSAGES WANTED?
         BO    EXIT0000                       NO
         MVC   COMM_MSG_CSECT,MODID           SET CSECT NAME
         CLI   DAIR_FUNC,$DAIR_ALLOC          ALLOCATING?
         BNE   ERR0020                        NO
         MVI   COMM_MSG_ID+1,1                ALLOCATION MESSAGE
         B     ERR0030
ERR0020  DS    0H
         MVI   COMM_MSG_ID+1,2                DE-ALLOCATION
ERR0030  DS    0H
         MVC   COMM_INFO_01(l'DAIR_DD),DAIR_DD
         MVC   COMM_INFO_02(l'DAIR_DSN),DAIR_DSN
         MVC   COMM_INFO_03(l'DAIR_MEMBER),DAIR_MEMBER
         MVC   COMM_DWORD,DAIR_R15            COPY R15, ERROR, AND INFO
         L     R15,COMM_V_OSMSG               OSMSG ENTRY POINT
         BALR  R14,R15                        BUILD MESSAGE
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=EXIT
         L     R13,4(,R13)                    RESTORE SAVE AREA
         LM    R14,R12,12(R13)                RESTORE REGISTERS
         SR    R15,R15                        SET RC
         BR    R14                            RETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXD_START     DC    Q(DXDDAIR)
H4            DC    H'4'
NINETY        DC    AL3(90)
P1            DC    P'1'
OS_DD         DC    C'OS'
DD_EDIT_WORD  DC    X'F020202020202020'
SYSALLDA      DC    CL8'SYSALLDA'
SYSDA         DC    CL8'SYSDA'

TEXT_UNITS_I  DS    0X
              DC    AL2(DALDDNAM)         DDNAME PARMS
              DC    AL2(1)
              DC    AL2(8)
              DC    CL8' '                DDNAME
              DC    AL2(DALDSNAM)         DATASET NAME PARMS
              DC    AL2(1)
              DC    AL2(44)
              DC    CL44' '               DATASET NAME
              DC    AL2(DALMEMBR)         MEMBER NAME PARMS
              DC    AL2(1)
              DC    AL2(8)
              DC    CL8' '                DATASET NAME
              DC    AL2(DALSTATS)         INITIAL DISPOSITION PARMS
              DC    AL2(1)
              DC    AL2(1)
              DC    X'00'                 INITIAL DISPOSITION
              DC    AL2(DALNDISP)         NORMAL DISPOSITION PARMS
              DC    AL2(1)
              DC    AL2(1)
              DC    X'02'                 NORMAL DISPOSITION
              DC    AL2(DALUNIT)          UNIT PARAMETERS
              DC    AL2(1)
              DC    AL2(8)
              DC    CL8' '                UNIT NAME
              DC    AL2(DALVLSER)         VOLSER PARAMETERS
              DC    AL2(1)
              DC    AL2(6)
              DC    CL6' '
              DC    AL2(DALTRK)           SPACE UNITS
              DC    AL2(0)
              DC    AL2(DALPRIME)         PRIMARY SPACE PARAMETERS
              DC    AL2(1)
              DC    AL2(3)
              DC    XL3'0'                PRIMARY SPACE (IN TRACKS)
              DC    AL2(DALSECND)         SECONDARY SPACE PARAMETERS
              DC    AL2(1)
              DC    AL2(3)
              DC    XL3'0'                SECONDARY SPACE (IN TRACKS)
              DC    AL2(DALRLSE)          RELEASE UNUSED SPACE
              DC    AL2(0)
              DC    AL2(DALSYSOU)         ALLOCATE SYSOUT
              DC    AL2(1)
              DC    AL2(1)
              DC    C'O'                  SYSOUT CLASS
              DC    AL2(DALCLOSE)         SAME AS FREE=CLOSE
              DC    AL2(0)
              DC    AL2(DALSPGNM)         SYSOUT PROGRAM NAME
              DC    AL2(1)
              DC    AL2(6)
              DC    CL6'INTRDR'

              DC    AL2(DALDUMMY)         DUMMY ALLOCATION
              DC    AL2(0)
              DC    AL2(0)

              DC    AL2(DCCDDNAM)         DD NAME'S TO CONCAT
              DC    AL2(2)
              DC    AL2(8)
              DC    CL8' '
              DC    AL2(8)
              DC    CL8' '

TEXT_UNITS_L  EQU   *-TEXT_UNITS_I
              LTORG

DAIREND       EQU  *
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDDAIR                       DSECT
                              COPY DXDPREF

DXD_SVC99_RB_ADDRESS          DS   A
DXD_SVC99_RB                  DS   (S99RBEND-S99RB)X

DXD_TEXT_POINTER_1            DS   A
DXD_TEXT_POINTER_2            DS   A
DXD_TEXT_POINTER_3            DS   A
DXD_TEXT_POINTER_4            DS   A
DXD_TEXT_POINTER_5            DS   A
DXD_TEXT_POINTER_6            DS   A
DXD_TEXT_POINTER_7            DS   A
DXD_TEXT_POINTER_8            DS   A
DXD_TEXT_POINTER_9            DS   A

DXD_FLAGS                     DS   X
$FLAG_INIT                    EQU  X'80'

DXD_TEXT_UNITS                DS   0C
DXD_DD_TEXT_UNITS             DS   AL2,AL2,AL2
DXD_DD                        DS   CL8
DXD_DSN_TEXT_UNITS            DS   AL2,AL2,AL2
DXD_DSN                       DS   CL44
DXD_MEMBER_TEXT_UNITS         DS   AL2,AL2,AL2
DXD_MEMBER                    DS   CL8
DXD_INIT_DISP_TEXT_UNITS      DS   AL2,AL2,AL2
DXD_INIT_DISP                 DS   X
DXD_NORM_DISP_TEXT_UNITS      DS   AL2,AL2,AL2
DXD_NORM_DISP                 DS   X
DXD_UNIT_TEXT_UNITS           DS   AL2,AL2,AL2
DXD_UNIT                      DS   CL8
DXD_VOLSER_TEXT_UNITS         DS   AL2,AL2,AL2
DXD_VOLSER                    DS   CL6
DXD_SPACE_UNITS_TEXT_UNITS    DS   AL2,AL2
DXD_PRI_TEXT_UNITS            DS   AL2,AL2,AL2
DXD_PRI                       DS   XL3
DXD_SEC_TEXT_UNITS            DS   AL2,AL2,AL2
DXD_SEC                       DS   XL3
DXD_REL_TEXT_UNITS            DS   AL2,AL2
DXD_SYSOUT_TEXT_UNITS         DS   AL2,AL2,AL2
DXD_SYSOUT                    DS   C
DXD_FREE_TEXT_UNITS           DS   AL2,AL2
DXD_SYSOUT_PROGRAM_TEXT_UNITS DS   AL2,AL2,AL2
DXD_SYSOUT_PROGRAM            DS   CL6

DXD_DUMMY_TEXT_UNITS          DS   AL2,AL2,AL2

DXD_CONCAT_TEXT_UNITS         DS   AL2,AL2,AL2
DXD_CONCAT_DD1                DS   CL8
                              DS   AL2
DXD_CONCAT_DD2                DS   CL8
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         BPXYSTAT DSECT=YES,LIST=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         DAIRREQ  DSECT=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         IBMMAC   S99=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY     REGEQU
         END      OSDAIR
