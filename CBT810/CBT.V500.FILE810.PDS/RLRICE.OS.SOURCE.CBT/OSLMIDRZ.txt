* ------------------------------------------------------------------- *
*                                                                     *
*  Module name: OSLMIDRZ                                              *
*                                                                     *
*  Display IDRZ (AMASPZAP) data                                       *
*                                                                     *
* ------------------------------------------------------------------- *
OSLMIDRZ CSECT
OSLMIDRZ AMODE 31
OSLMIDRZ RMODE ANY
         USING OSLMIDRZ,R15
         USING OSCOMM,R12
         USING SESSION,R11
         B     INIT0000
MODID    DC    CL8'OSLMIDRZ'
         DC    CL8'&SYSDATE'
         DC    CL8'&SYSTIME'
         DC    A(IDRZEND-OSLMIDRZ)
INIT0000 DS    0H
         STM   R14,R12,12(R13)                  SAVE REGISTERS
         LR    R10,R15                          COPY BASE ADDRESS
         LA    R9,2048(,R10)
         LA    R9,2048(,R9)
         USING OSLMIDRZ,R10,R9
         L     R5,COMM_OSSPFD
         USING OSSPFD,R5
         DROP  R15
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         CLI   SESS_FORMAT_FLAGS,$FORMAT_INITIALIZE
         BNE   INIT0010                         NO
         LA    R0,OSLMIDRZ_DXD_L                WORK AREA LENGTH
         ST    R0,SESS_DXD_LENGTH               SAVE LENGTH
         GETMAIN RU,                                                   +
               LV=(0),                                                 +
               LOC=ANY
         ST    R1,SESS_DXD_ADDR                 SAVE ADDRESS
         LR    R2,R1
         LR    R0,R1
         LA    R1,OSLMIDRZ_DXD_L
         SR    R14,R14
         SR    R15,R15
         MVCL  R0,R14                           INITIALIZE WORK AREA
         ST    R13,4(,R2)
         ST    R2,8(,R13)
         LR    R13,R2                           SET WORK AREA ADDRESS
         USING OSLMIDRZ_DXD,R13                 DEFINE BASE
         MVC   DXD_CSECT,MODID                  SET MODULE ID
         ITRACE ID=ENTRY1
         LA    R1,DATASPACE_1
         ST    R1,DSPCREQ_DATASPACE
         LA    R1,LMOD_IDRZ_DATA
         ST    R1,DSPCREQ_RECORD_ADDR
         MVC   DSPCREQ_RECORD_NBR,DATASPACE_1_DISPLAY_FIRST_RECORD
         LA    R1,PANEL_NAME                    PANEL NAME
         MVI   COMM_VDATA_FUNC,$VDATA_GETMAIN   SET FUNCTION
         L     R15,COMM_V_OSVDATA
         BALR  R14,R15                          GETMAIN VDATA
         ST    R0,DXD_VDATA_LENGTH              SAVE LENGTH
         ST    R1,DXD_VDATA_ADDR                SAVE ADDRESS
         MVC   DXD_VDEPTH,SPF_VDEPTH            SAVE DEPTH
         MVC   DXD_VWIDTH,SPF_VWIDTH            SAVE WIDTH
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
INIT0010 DS    0H
         L     R1,SESS_DXD_ADDR                 WORK AREA ADDRESS
         ST    R13,4(,R1)
         ST    R1,8(,R13)
         LR    R13,R1
         ITRACE ID=ENTRY2
         CLI   SESS_FORMAT_FLAGS,$FORMAT_CONTROL
         BE    EXIT0000
         CLI   SESS_FORMAT_FLAGS,$FORMAT_CLEANUP
         BE    CLEAN000
* ------------------------------------------------------------------- *
*        Data storeed, display it                                     *
* ------------------------------------------------------------------- *
IDRZ0000 DS    0H
         L     R0,DXD_VDATA_ADDR
         L     R1,DXD_VDATA_LENGTH
         SR    R14,R14
         SR    R15,R15
         MVCL  R0,R14

         NI    DXD_FLAGS,255-$HEADING
         L     R4,DXD_VDATA_ADDR
         L     R3,DXD_VDEPTH
IDRZ0010 DS    0H
         MVC   DSPCREQ_RECORD_NBR,DATASPACE_1_DISPLAY_FIRST_RECORD
         MVI   DSPCREQ_FUNC,$DSPCREQ_RETRIEVE
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE
         BALR  R14,R15
IDRZ0030 DS    0H
         TM    DXD_FLAGS,$HEADING               HEAD BUILT?
         BO    IDRZ0040                         YES
         OI    DXD_FLAGS,$HEADING               SET FLAG
         MVC   0(HEADING_L,R4),HEADING          COPY HEADING
         A     R4,DXD_VWIDTH                    NEXT LINE
         BCTR  R3,0
IDRZ0040 DS    0H
         MVC   0(MSG01_L,R4),MSG01
         USING MSG01,R4
         MVC   MSG01_DATE+0(4),LMOD_IDRZ_DATE+0
         MVI   MSG01_DATE+4,C'.'
         MVC   MSG01_DATE+5(3),LMOD_IDRZ_DATE+4
         MVC   MSG01_ZAP_DATA,LMOD_IDRZ_ZAP_DATA
         MVC   MSG01_RESIDENT,LMOD_IDRZ_RESIDENT
         DROP  R4
         A     R4,DXD_VWIDTH
         BCT   R3,IDRZ0050
         B     DISP0000                         SCREEN IS FULL
IDRZ0050 DS    0H
         LA    R0,1                             CONSTANT 1
         A     R0,DSPCREQ_RECORD_NBR            PLUS CURRENT RECORD NBR
         C     R0,DATASPACE_1_LAST_RECORD       BEYOND LAST RECORD?
         BH    DISP0000                         YES
         ST    R0,DSPCREQ_RECORD_NBR            UPDATE RECORD NBR
         B     IDRZ0010
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DISP0000 DS    0H
         ITRACE ID=DISPLAY
         MVC   SESS_DISP_PANEL,PANEL_NAME
         MVC   SESS_DISP_VDATA_ADDR,DXD_VDATA_ADDR
         MVC   SESS_DISP_VDATA_LENGTH,DXD_VDATA_LENGTH
         XC    SESS_DISP_CURSOR,SESS_DISP_CURSOR
         L     R15,COMM_V_OSDISP
         BALR  R14,R15
         ITRACE ID=DISP_RC,                                            +
               DATA1=(SESS_RC,2),                                      +
               DATA2=(SESS_STATUS,1)
         CLC   COMM_NEW_FORMAT,COMM_BLANKS      NEW FORMAT SELECTED?
         BNE   EXIT0000                         YES
         CLI   COMM_SESS_FUNC,0                 SESSION FUNCTION?
         BNE   EXIT0000                         YES
         OC    SESS_RC,SESS_RC                  RC=0?
         BZ    DISP0010                         YES
         ITRACE ID=ENDING
         MVI   COMM_SESS_FUNC,$SESS_REMOVE SET FUNCTION
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
*        Check for scroll command(s)                                  *
*                                                                     *
*        Note that 'up', 'down', 'top', and 'bottom' are the only     *
*        valid scroll commands.                                       *
*                                                                     *
* ------------------------------------------------------------------- *
DISP0010 DS    0H
         ITRACE ID=ZVERB,                                              +
               DATA1=SPF_ZVERB,                                        +
               DATA2=SPF_ZSCROLLA
         CLI   SPF_ZVERB,C'B'                   BOTTOM?
         BE    DISP0100                         YES
         CLI   SPF_ZVERB,C'D'                   DOWN?
         BE    DISP0200                         YES
         CLI   SPF_ZVERB,C'T'                   TOP?
         BE    DISP0300                         YES
         CLI   SPF_ZVERB,C'U'                   UP?
         BE    DISP0400                         YES
         B     DISP0000

DISP0100 DS    0H
         ITRACE ID=BOTTOM
         MVC   DATASPACE_1_DISPLAY_FIRST_RECORD,DATASPACE_1_LAST_RECORD
         B     IDRZ0000
DISP0200 DS    0H
         ITRACE ID=DOWN,                                               +
               DATA1=SPF_ZSCROLLA,                                     +
               DATA2=(SPF_ZSCROLLN,4)
         CLI   SPF_ZSCROLLA,C'M'                   MAX?
         BE    DISP0100                            YES.. SAME AS BOTTOM
         L     R1,SPF_ZSCROLLN                     NUMBER TO SCROLL
         A     R1,DATASPACE_1_DISPLAY_FIRST_RECORD PLUS CURRENT RECORD
         C     R1,DATASPACE_1_LAST_RECORD          BEYOND HIGH RECORD?
         BH    DISP0100                            YES.. GO TO BOTTOM
         ST    R1,DATASPACE_1_DISPLAY_FIRST_RECORD UPDATE RCD NBR
         B     IDRZ0000                            GO BUILD DATA
DISP0300 DS    0H
         ITRACE ID=TOP
         MVC   DATASPACE_1_DISPLAY_FIRST_RECORD,F1
         B     IDRZ0000
DISP0400 DS    0H
         ITRACE ID=UP,                                                 +
               DATA1=SPF_ZSCROLLA,                                     +
               DATA2=(SPF_ZSCROLLN,4)
         CLI   SPF_ZSCROLLA,C'M'                   MAX?
         BE    DISP0300                            YES.. GO TO TOP
         L     R1,DATASPACE_1_DISPLAY_FIRST_RECORD
         S     R1,SPF_ZSCROLLN                     MINUS SCROLL
         C     R1,F1                               BELOW 1?
         BL    DISP0300                            YES, GO TO TOP
         ST    R1,DATASPACE_1_DISPLAY_FIRST_RECORD UPDATE RECORD NUMBER
         B     IDRZ0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
CLEAN000 DS    0H
         ITRACE ID=CLEAN_UP,                                           +
               DATA1=(DXD_VDATA_ADDR,4),                               +
               DATA2=(DXD_VDATA_LENGTH,4)
         ICM   R1,15,DXD_VDATA_ADDR             VDATA GETMAINED?
         BZ    CLEAN010                         NO
         L     R0,DXD_VDATA_LENGTH              VDATA LENGTH
         MVI   COMM_VDATA_FUNC,$VDATA_FREEMAIN
         L     R15,COMM_V_OSVDATA
         BALR  R14,R15                          FREE VDATA
         XC    DXD_VDATA_ADDR,DXD_VDATA_ADDR
         XC    DXD_VDATA_LENGTH,DXD_VDATA_LENGTH
CLEAN010 DS    0H
         LA    R0,OSLMIDRZ_DXD_L                WORK AREA SIZE
         LR    R1,R13                           COPY WORK AREA ADDRESS
         L     R13,4(,R13)                      RESTORE R13
         FREEMAIN RU,                           FREE WORK AREA         +
               A=(1),                                                  +
               LV=(0)
         XC    SESS_DXD_ADDR,SESS_DXD_ADDR
         XC    SESS_DXD_LENGTH,SESS_DXD_LENGTH
         SR    R15,R15
         B     EXIT0010
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=EXIT
         L     R13,4(,R13)                      RESTORE R13
EXIT0010 DS    0H
         LM    R14,R12,12(R13)                  RESTORE REGISTERS
         SR    R15,R15                          SET RC
         BR    R14                              RETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
F1                   DC   F'1'
PANEL_NAME           DC   CL8'OSLMIDRZ'

                     LTORG
HEADING              DS   0C
                     DC   AL1($SCREEN_ATTR_HIGH_TURQUOISE)
                     DC   C' Date    data    Resident'
HEADING_L            EQU  *-HEADING

MSG01                DS   0C
                     DC   AL1($SCREEN_ATTR_NORMAL)
MSG01_DATE           DC   CL7' '
                     DC   CL2' '
MSG01_ZAP_DATA       DC   CL8' '
                     DC   CL2' '
MSG01_RESIDENT       DC   CL60' '
MSG01_L              EQU  *-MSG01

IDRZEND              EQU  *
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
OSLMIDRZ_DXD         DSECT
                     COPY  DXDPREF

DXD_VDATA_ADDR       DS    A
DXD_VDATA_LENGTH     DS    F

DXD_VDEPTH           DS    F
DXD_VWIDTH           DS    F

DXD_NUMBER           DS    CL12

DXD_FLAGS            DS    X
$HEADING             EQU   X'80'

                     COPY  IDRZDATA
                     DSPCREQ  DSECT=NO
OSLMIDRZ_DXD_L       EQU   *-OSLMIDRZ_DXD
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         SESSION
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         BPXYSTAT DSECT=YES,LIST=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         OSSPFD   TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY     ATTRS
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         IBMMAC   VTOC=1
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY     REGEQU
         END      OSLMIDRZ
