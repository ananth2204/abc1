* ------------------------------------------------------------------- *
*                                                                     *
*  Module name: LMODIDRB                                              *
*                                                                     *
*  Process and store IDRB (Binder identification) data                *
*                                                                     *
* ------------------------------------------------------------------- *
LMODIDRB CSECT
LMODIDRB AMODE 31
LMODIDRB RMODE ANY
         USING LMODIDRB,R15
         USING OSCOMM,R12
         USING SESSION,R11
         B     INIT0000
MODID    DC    CL8'LMODIDRB'
         DC    CL8'&SYSDATE'
         DC    CL8'&SYSTIME'
         DC    A(IDRBEND-LMODIDRB)
INIT0000 DS    0H
         STM   R14,R12,12(R13)                  SAVE REGISTERS
         LR    R10,R15                          COPY BASE ADDRESS
         LA    R9,2048(,R10)
         LA    R9,2048(,R9)
         DROP  R15
         USING LMODIDRB,R10,R9
         L     R15,SESS_DXD_ADDR
         A     R15,DXD_START                    PLUS OFFSET
         ST    R13,4(,R15)
         ST    R15,8(,R13)
         LR    R13,R15                          COPY DXD AREA ADDRESS
         USING DXDIDRB,R13                      DEFINE WORK AREA BASE
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY
         L     R5,COMM_NEW_SESSION
NEW      USING SESSION,R5
         MVC   COMM_MSG_CSECT,MODID
         XC    COMM_MSG_ID,COMM_MSG_ID
         L     R6,COMM_OSSPFD
         USING OSSPFD,R6
         MVC   DXD_IEWBIND(IEWBIND_L),IEWBIND_I
         MVI   DXD_FLAGS,0
         LA    R1,NEW.DATASPACE_1
         ST    R1,DSPCREQ_DATASPACE
* ------------------------------------------------------------------- *
*        Obtain and initialize IDRB buffer                            *
* ------------------------------------------------------------------- *
         ITRACE ID=GET_SECT
         IEWBUFF FUNC=GETBUF,                                          +
               TYPE=IDRB
         ST    R8,DXD_IDRB_BUFFER
         ITRACE ID=INITIDRB,                                           +
               RDATA1=R8,                                              +
               RDATA2=R7
         IEWBUFF FUNC=INITBUF,                                         +
               TYPE=IDRB
* ------------------------------------------------------------------- *
*        Store the info for the specified section                     *
* ------------------------------------------------------------------- *
         LA    R1,IDRB_DATA
         ST    R1,DSPCREQ_RECORD_ADDR
         LA    R1,IDRB_DATA_L
         ST    R1,DSPCREQ_RECORD_LENGTH
         XC    DSPCREQ_RECORD_NBR,DSPCREQ_RECORD_NBR
         MVI   DSPCREQ_FUNC,$DSPCREQ_STORE
         XC    DXD_IDRB_CURSOR,DXD_IDRB_CURSOR
* ------------------------------------------------------------------- *
*                                                                     *
*     Store the IDRB info                                             *
*                                                                     *
*     R8 IS THE IDRB HEADER (IDBH) BASE                               *
*     R7 IS THE IDRB ENTRY (IDB_ENTRY) BASE                           *
*                                                                     *
* ------------------------------------------------------------------- *
IDRB0000 DS    0H
         ITRACE ID=GETDIDRB,                                           +
               DATA1=NEW.SESS_DIALOG_SECTION_LENGTH+0,                 +
               DATA2=NEW.SESS_DIALOG_SECTION_LENGTH+8
         L     R8,DXD_IDRB_BUFFER
         LA    R7,IDBH_END
         IEWBIND FUNC=GETD,                                            +
               VERSION=5,                                              +
               WORKMOD=NEW.SESS_DIALOG_WORKMOD,                        +
               CLASS=B_IDRB,                                           +
               SECTION=DUMMY_SECTION,                                  +
               AREA=IEWBIDB,                                           +
               CURSOR=DXD_IDRB_CURSOR,                                 +
               COUNT=DXD_IDRB_RETURNED,                                +
               RETCODE=DXD_RETURN_CODE,                                +
               RSNCODE=DXD_REASON_CODE,                                +
               MF=(E,DXD_IEWBIND,COMPLETE)
         ITRACE ID=GETD_RC,                                            +
               DATA1=(DXD_RETURN_CODE,4),                              +
               DATA2=(DXD_REASON_CODE,4)
         CLC   DXD_RETURN_CODE,F8
         BH    ERR0010
         BE    ERR0020                          NO DATA
         CLC   DXD_RETURN_CODE,F4
         BL    IDRB0010
         CLC   DXD_REASON_CODE,X_83000800
         BE    IDRB0010
         CLC   DXD_REASON_CODE,X_83000801
         BE    ERR0030
IDRB0010 DS    0H
         ICM   R4,15,DXD_IDRB_RETURNED
         BZ    ERR0020
         OI    DXD_FLAGS,$FLAGS_DISPLAYED
IDRB0020 DS    0H
         ITRACE ID=BLD_SECT,                                           +
               RDATA1=R7,                                              +
               RDATA2=R4

* ----------------  Build data -------------------------------------- *
         MVC   IDRB_BINDER_ID,IDB_BINDER_ID
         MVC   IDRB_VERSION,IDB_VERSION
         MVC   IDRB_RELEASE,IDB_RELEASE
         MVC   IDRB_DATE_BOUND,IDB_DATE_BOUND
         MVC   IDRB_TIME_BOUND,IDB_TIME_BOUND
         MVC   IDRB_MODULE_SIZE,IDB_MODULE_SIZE
         MVC   IDRB_CALLERID,COMM_BLANKS
         SR    R14,R14
         ICM   R14,3,IDB_CALLERID_CHARS
         BZ    IDRB0040
         CH    R14,=Y(L'IDRB_CALLERID)
         BL    IDRB0030
         LH    R14,=Y(L'IDRB_CALLERID)
IDRB0030 DS    0H
         BCTR  R14,0
         EX    R14,CALLERID_MVC

* ----------------  Store record just built ------------------------- *

IDRB0040 DS    0H
         L     R1,DSPCREQ_RECORD_NBR
         LA    R1,1(,R1)
         ST    R1,DSPCREQ_RECORD_NBR
         ITRACE ID=STORE,                                              +
               RDATA1=R1
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE
         BALR  R14,R15
         CLI   DSPCREQ_RC,$DSPCREQ_FULL
         BE    IDRB0050
         CLI   DSPCREQ_RC,$DSPCREQ_OK
         BNE   ERR0040

* ----------------  Locate next record in buffer -------------------- *
         A     R7,IDBH_ENTRY_LENG               PLUS LENGTH
         BCT   R4,IDRB0020                      LOOP
         CLC   DXD_REASON_CODE,X_83000800       MORE DATA?
         BNE   IDRB0060                         YES
         ITRACE ID=DONE
         B     IDRB0060
IDRB0050 DS    0H
         ITRACE ID=FULL
         L     R1,DSPCREQ_RECORD_NBR
         BCTR  R1,0
         ST    R1,DSPCREQ_RECORD_NBR
         MVC   SPF_MSG_1,COMM_MSG_1
         MVC   SPF_MSG_2,COMM_MSG_2
         MVC   SPF_MSG_3,COMM_MSG_3
         MVC   SPF_MSG_4,COMM_MSG_4
         MVC   SPF_MSG_5,COMM_MSG_5
IDRB0060 DS    0H
         MVC   NEW.DATASPACE_1_DISPLAY_FIRST_RECORD,F1
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
CALLERID_MVC   MVC  IDRB_CALLERID(0),IDB_CALLERID
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0010  DS    0H
         ITRACE ID=ERR0010
         MVI   COMM_MSG_ID+1,1
         MVC   COMM_INFO_01(20),INFO_GET_IDRB
         MVC   COMM_INFO_02,DXD_RETURN_CODE
         MVC   COMM_INFO_03,DXD_REASON_CODE
         B     ERR0100
ERR0020  DS    0H
         MVC   COMM_INFO_01,NEW.SESS_DIALOG_SECTION
         TM    DXD_FLAGS,$FLAGS_DISPLAYED
         BO    ERR0030
         MVI   COMM_MSG_ID+1,2
         B     ERR0100
ERR0030  DS    0H
         MVC   COMM_INFO_01,NEW.SESS_DIALOG_SECTION
         MVI   COMM_MSG_ID+1,3
         B     ERR0100
ERR0040  DS    0H
         MVC   SPF_MSG_1,COMM_MSG_1
         MVC   SPF_MSG_2,COMM_MSG_2
         MVC   SPF_MSG_3,COMM_MSG_3
         MVC   SPF_MSG_4,COMM_MSG_4
         MVC   SPF_MSG_5,COMM_MSG_5
         B     EXIT0000
ERR0100  DS    0H
         ITRACE ID=CALL_MSG,                                           +
               DATA1=(COMM_MSG_ID,2)
         MVC   COMM_MSG_CSECT,MODID
         L     R15,COMM_V_OSMSG
         BALR  R14,R15
         MVC   SPF_MSG_1,COMM_MSG_1
         MVC   SPF_MSG_2,COMM_MSG_2
         MVC   SPF_MSG_3,COMM_MSG_3
         MVC   SPF_MSG_4,COMM_MSG_4
         MVC   SPF_MSG_5,COMM_MSG_5
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         L     R13,4(,R13)                     RESTORE R13
         L     R14,12(,R13)                    RESTORE R14
         LR    R15,R2                          SET RC
         LM    R0,R12,20(R13)                  RESTORE REGISTERS
         BR    R14                             RETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         IEWBUFF FUNC=MAPBUF,                                          +
               TYPE=IDRB,                                              +
               HEADREG=R8,                                             +
               ENTRYREG=R7,                                            +
               SIZE=50,                                                +
               VERSION=5

               IEWBIND MF=(L,IEWBIND_I)
IEWBIND_L      EQU     *-IEWBIND_I
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXD_START            DC   Q(DXDIDRB)

F1                   DC   F'1'
F4                   DC   F'4'
F8                   DC   F'8'
B_IDRB               DC   H'6',C'B_IDRB'
DUMMY_SECTION        DC   H'8',X'00000001',CL4' '
X_83000800           DC   X'83000800'
X_83000801           DC   X'83000801'
INFO_GET_IDRB        DC   CL20'GET IDRB info'

IDRBEND              EQU  *
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDIDRB              DSECT
                     COPY  DXDPREF

DXD_IEWBIND          DS    0F,(IEWBIND_L)X

DXD_RETURN_CODE      DS    XL4            RETURN CODE
DXD_REASON_CODE      DS    XL4            REASON CODE

DXD_IDRB_BUFFER      DS    A

DXD_IDRB_RETURNED    DS    F
DXD_IDRB_CURSOR      DS    F

DXD_FLAGS            DS    X
$FLAGS_DISPLAYED     EQU   X'80'            SOME DATA DISPLAYED

                     COPY  IDRBDATA

                     DSPCREQ  DSECT=NO

* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         SESSION
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         BPXYSTAT  DSECT=YES,LIST=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         OSSPFD    TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY      TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY      REGEQU
         END       LMODIDRB
