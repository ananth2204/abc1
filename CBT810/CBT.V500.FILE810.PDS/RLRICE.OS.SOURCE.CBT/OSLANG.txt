* --------------------------------------------------------------------*
*                                                                     *
*  MODULE NAME: OSLANG                                                *
*                                                                     *
*  To allow On Screen to be about as independant of national          *
*  languages, few messages are hard-coded.  The messages for each     *
*  national language can be interpreted/customized for that national  *
*  language.                                                          *
*                                                                     *
*  Since the DCB and some other I/O related stuff needs to be below   *
*  the 24-bit line, it was more straight-forward to link this         *
*  separately and LOAD/DELETE it at run time.                         *
*                                                                     *
*                                                                     *
*  At run-time, On Screen can allow the users to select the national  *
*  language they want. The name of the national language needs to     *
*  be associated with the name of the load module with the messages   *
*  for that national lanuguage.  For example US english messages      *
*  are in load module OSMSGUS.                                        *
*                                                                     *
*  This program has two functions.                                    *
*     Return the name(s) of the "known" national languages            *
*     Return the load module name for a requested national language.  *
*                                                                     *
*                                                                     *
*  "Known" languages are defined in the On Screen options data set.   *
*                                                                     *
*                                                                     *
*  The format of a "language" defintion is:                           *
*                                                                     *
*       columns                                                       *
*         1- 8       Literal "LANGUAGE"                               *
*         9-16       Name of the national language                    *
*        17-24       Name of load module                              *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
OSLANG   CSECT
OSLANG   AMODE 31
OSLANG   RMODE 24
         USING OSCOMM,R12
         USING SESSION,R11
         USING OSLANG,R15
         B     INIT0000
MODID    DC    CL8'OSLANG'
         DC    CL8'&SYSDATE'
         DC    CL8'&SYSTIME'
         DC    A(OSLANGEND-OSLANG)
INIT0000 DS    0H
         STM   R14,R12,12(R13)                 SAVE REGS
         LR    R10,R15                         COPY ENTRY POINT ADDR
         DROP  R15
         USING OSLANG,R10                      DEFINE BASE
         LR    R9,R1                           SAVE PARM BLOCK ADDRESS
         USING LANGREQ,R9                      DEFINE BASE
         GETMAIN RU,                                                   +
               LV=OSLANG_WORK_L,                                       +
               LOC=BELOW
         ST    R13,4(,R1)                      SAVE R13
         ST    R1,8(,R13)                      CHAIN SAVE AREA
         LR    R13,R1                          COPY WORK AREA ADDRESS
         USING OSLANG_WORK,R13
         MVC   DXD_CSECT,MODID
         USING IHADCB,WORK_DCB
* ------------------------------------------------------------------- *
         ITRACE ID=ENTRY,                                              +
               RDATA1=R9,                                              +
               DATA2=LANG_NAME
         MVC   WORK_DCB(READDCB_L),READDCB
         MVC   WORK_DCBE(DCBE_L),DCBE_I
         LA    R1,WORK_DCBE
         STCM  R1,7,DCBDCBE
         MVC   WORK_OPEN(OPEN_L),OPEN_I
         OPEN  WORK_DCB,                                               +
               MODE=31,                                                +
               MF=(E,WORK_OPEN)
         CLI   LANG_FUNC,$LANG_NBR            NBR OF DEFINED LANGUAGES?
         BE    LANG0100                       YES
         CLI   LANG_FUNC,$LANG_LIST           RETURN LIST OF LANGUAGES
         BE    LANG0120                       YES
         CLI   LANG_FUNC,$LANG_LOOKUP
         BE    LANG0140                       YES
         ABEND 1,DUMP,,USER
* ------------------------------------------------------------------- *
*        Return number of languages                                   *
* ------------------------------------------------------------------- *
LANG0100 DS    0H
         ITRACE ID=NBR
         SR    R2,R2                          CLEAR FOR COUNTER
LANG0110 DS    0H
         GET   WORK_DCB,WORK_IO_AREA          READ A RECORD
         CLC   WORK_TYPE,TYPE_LANGUAGE        LANGUAGE DEFINITION?
         BNE   LANG0110                       NO
         LA    R2,1(,R2)                      PLUS 1
         B     LANG0110
* ------------------------------------------------------------------- *
*        Return list of languages                                     *
* ------------------------------------------------------------------- *
LANG0120 DS    0H
         ITRACE ID=LIST
         SR    R2,R2                          CLEAR FOR COUNTER
         L     R3,LANG_LIST                   AREA FOR RETURNED LIST
LANG0130 DS    0H
         GET   WORK_DCB,WORK_IO_AREA          READ A RECORD
         CLC   WORK_TYPE,TYPE_LANGUAGE        LANGUAGE DEFINITION?
         BNE   LANG0110                       NO
         LA    R2,1(,R2)                      PLUS 1
         MVC   0(8,R3),WORK_DATA              COPY NAME
         LA    R3,8(,R3)                      NEXT
         B     LANG0130                       READ AGAIN
LANG0140 DS    0H
         ITRACE ID=LOOK_UP
         MVI   LANG_RC,$LANG_UNKNOWN          ASSUME THE WORST
         GET   WORK_DCB,WORK_IO_AREA          READ A RECORD
         CLC   LANG_NAME,WORK_DATA            IS THIS THE LANGUAGE?
         BNE   LANG0140                       NO, TRY AGAIN
         MVC   LANG_MODULE,WORK_DATA+8        COPY THE MODULE NAME
         ITRACE ID=FOUND,                                              +
               DATA1=LANG_NAME,                                        +
               DATA2=LANG_MODULE
         MVI   LANG_RC,$LANG_OK               LANGUAGE FOUND
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
LANG0150 DS    0H
         ITRACE ID=LANG_EOF
         ST    R2,LANG_NBR
         MVC   WORK_CLOSE(CLOSE_L),CLOSE_I
         CLOSE WORK_DCB,                      CLOSE THE OPTIONS FILE   +
               MODE=31,                                                +
               MF=(E,WORK_CLOSE)
         FREEPOOL WORK_DCB
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=EXIT
         LR    R1,R13                         COPY WORK AREA ADDRESS
         L     R13,4(,R13)                    RESTORE SAVE AREA ADDRESS
         FREEMAIN RU,                                                  +
               A=(1),                                                  +
               LV=OSLANG_WORK_L
         LM    R14,R12,12(R13)                RESTORE REGISTERS
         BR    R14                            RETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
READDCB  DCB   DSORG=PS,                                               +
               DDNAME=OSOPTS,                                          +
               DCBE=*,                                                 +
               EODAD=LANG0150,                                         +
               RECFM=FB,                                               +
               LRECL=80,                                               +
               MACRF=GM
READDCB_L      EQU   *-READDCB
DCBE_I   DCBE  RMODE31=BUFF
DCBE_L   EQU   *-DCBE_I
OPEN_I   OPEN  (*,INPUT),                                              +
               MODE=31,                                                +
               MF=L
OPEN_L         EQU   *-OPEN_I
CLOSE_I  CLOSE (*),                                                    +
               MODE=31,                                                +
               MF=L
CLOSE_L        EQU   *-CLOSE_I
TYPE_LANGUAGE  DC    CL8'LANGUAGE'
               LTORG
OSLANGEND      EQU   *
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
OSLANG_WORK      DSECT
                 COPY  DXDPREF
WORK_DCB         DS    0F,(READDCB_L)X
WORK_DCBE        DS    0F,(DCBE_L)X
WORK_OPEN        DS    0F,(OPEN_L)X
WORK_CLOSE       DS    0F,(CLOSE_L)X

WORK_IO_AREA     DS    0C
WORK_TYPE        DS    CL8             I/O AREA
WORK_DATA        DS    CL72

OSLANG_WORK_L    EQU  *-OSLANG_WORK
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         SESSION
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         BPXYSTAT DSECT=YES,LIST=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         IBMMAC   DCBD=PS
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         LANGREQ  DSECT=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY     REGEQU
         END      OSLANG
