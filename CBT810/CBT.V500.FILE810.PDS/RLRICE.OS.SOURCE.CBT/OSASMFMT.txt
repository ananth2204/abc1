* ------------------------------------------------------------------- *
*                                                                     *
*  Module name: OSASMFMT                                              *
*                                                                     *
*  Perform customized formatting for assembler listings               *
*                                                                     *
* ------------------------------------------------------------------- *
OSASMFMT CSECT
OSASMFMT AMODE 31
OSASMFMT RMODE ANY
         USING OSCOMM,R12
         USING OSASMFMT,R15
         B     INIT0000
MODID    DC    CL8'OSASMFMT'
         DC    CL8'&SYSDATE'
         DC    CL8'&SYSTIME'
         DC    A(ASMFMTEND-OSASMFMT)
INIT0000 DS    0H
         USING OSCOMM,R12                        DEFINE BASE
         USING SESSION,R11                       DEFINE AREA BASE
         USING DS1FMTID,SESS_DSCB                DEFINE BASE
         STM   R14,R12,12(R13)                   SAVE REGS
         LR    R10,R15                           COPY ENTRY POINT
         DROP  R15
         USING OSASMFMT,R10                      DEFINE BASES
         L     R2,SESS_DXD_ADDR                  WORK AREA BASE
         A     R2,DXDSTART
         ST    R13,4(,R2)                        SAVE R13
         ST    R2,8(,R13)                        CHAIN SAVE AREA
         LR    R13,R2                            COPY WORK AREA ADDRESS
         USING DXDASM,R13                        DEFINE WORK AREA BASE
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY
         L     R9,COMM_OSSPFD
         USING OSSPFD,R9
         MVI   DXD_RC,0                          RESET RC
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         LTR   R1,R1                             DSPCREQ BLOCK PASSED?
         BNZ   MAIN0100                          YES
* ------------------------------------------------------------------- *
*        No DSPCREQ passed                                            *
* ------------------------------------------------------------------- *
         ITRACE ID=RECFM,                                              +
               DATA1=(DS1RECFM,1)
         TM    DS1RECFM,DS1RECFU                 RECFM U?
         BO    MAIN0030                          YES
         ITRACE ID=LRECL,                                              +
               DATA1=(DS1LRECL,2)
         CLC   DS1LRECL,H137                     LRECL > 137?
         BH    MAIN0030                          YES
         LA    R0,DATASPACE_1
         ST    R0,DSPCREQ_DATASPACE
         MVI   DSPCREQ_FUNC,$DSPCREQ_RETRIEVE
         MVC   DSPCREQ_RECORD_NBR,DATASPACE_1_FIRST_RECORD
         MVC   DSPCREQ_RECORD_ADDR,SESS_IO_AREA  SET I/O AREA ADDR
MAIN0010 DS    0H
         LA    R1,DSPCREQ                        DSPCREQ ADDR
         L     R15,COMM_OSDSPACE                 DATA SPACE MANAGER
         BALR  R14,R15                           RETRIEVE THE RECORD
         CLI   DSPCREQ_RC,$DSPCREQ_OK            GOT THE RECORD?
         BNE   MAIN0030                          NO
         CLC   DSPCREQ_RECORD_LENGTH,F41         MINIMUM LENGTH?
         BH    MAIN0020                          YES
MAIN0015 DS    0H
         LA    R0,1
         A     R0,DSPCREQ_RECORD_NBR
         ST    R0,DSPCREQ_RECORD_NBR             NEXT RECORD NBR
         B     MAIN0010
MAIN0020 DS    0H
         L     R8,DSPCREQ_RECORD_ADDR            DATA ADDRESS
         CLC   OPTIONS_STATEMENT,3(R8)           OPTIONS HEADING?
         BNE   MAIN0015                          NO
         LA    R0,1
         A     R0,DSPCREQ_RECORD_NBR
         ST    R0,DSPCREQ_RECORD_NBR             NEXT RECORD NBR
         LA    R1,DSPCREQ                        DSPCREQ ADDR
         L     R15,COMM_OSDSPACE                 DATA SPACE MANAGER
         BALR  R14,R15                           RETRIEVE THE RECORD
         CLI   DSPCREQ_RC,$DSPCREQ_OK            GOT THE RECORD?
         BNE   ERR0010                           NO
         CLC   DSPCREQ_RECORD_LENGTH,F41         MINIMUM LENGTH?
         BL    MAIN0030                          NO
         L     R8,DSPCREQ_RECORD_ADDR            DATA ADDRESS
         CLC   ADATA_STATEMENT,5(R8)             ADATA OPTION?
         BNE   MAIN0030                          NO
         LA    R0,1
         A     R0,DSPCREQ_RECORD_NBR
         ST    R0,DSPCREQ_RECORD_NBR             NEXT RECORD NBR
         LA    R1,DSPCREQ                        DSPCREQ ADDR
         L     R15,COMM_OSDSPACE                 DATA SPACE MANAGER
         BALR  R14,R15                           RETRIEVE THE RECORD
         CLI   DSPCREQ_RC,$DSPCREQ_OK            GOT THE RECORD?
         BNE   ERR0010                           NO
         CLC   DSPCREQ_RECORD_LENGTH,F41         MINIMUM LENGTH?
         BL    MAIN0030                          NO
         L     R8,DSPCREQ_RECORD_ADDR            DATA ADDRESS
         CLC   ALIGN_STATEMENT,5(R8)             ALIGN OPTION?
         BNE   MAIN0030                          NO
         ITRACE ID=IS_ASM
         B     EXIT0000                          I'M SATISFIED
MAIN0030 DS    0H
         MVI   DXD_RC,8                          NOT ASSEMBLER LISTING
         B     EXIT0000

* ------------------------------------------------------------------- *
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0100 DS    0H
         MVC   DSPCREQ(256),0(R1)                COPY PASSED DSPCREQ
         L     R1,DSPCREQ_RECORD_ADDR            DATA ADDR
         L     R2,DSPCREQ_RECORD_LENGTH          DATA LENGTH
         ITRACE ID=FORMAT,                                             +
               RDATA1=R1,                                              +
               RDATA2=R2
         BCTR  R2,0
         XC    COMM_INFO_01,COMM_INFO_01         INITIALIZE WORK AREA
         EX    R2,MVC_1                          COPY TO WORK AREA
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         CLC   RELOCATION_STATEMENT,COMM_INFO_01+50
         BE    MAIN0540
         TM    DXD_FLAGS,$BYPASS                 BYPASS FLAG ON?
         BO    MAIN1000                          YES
         MVI   DXD_ATTR,$SCREEN_ATTR_NORMAL
         TM    DXD_FLAGS,$HEADINGS               IN PAGE HEADINGS?
         BO    MAIN0530                          YES
         CLC   USING_STATEMENT,COMM_INFO_01+3    USING HEADING?
         BE    MAIN0530                          YES
         CLI   COMM_INFO_01+40,C'+'              MACRO EXPANSION?
         BE    MAIN0500                          YES
         CLI   COMM_INFO_01+40,C'='              COPY?
         BE    MAIN0510                          YES
         CLI   COMM_INFO_01+41,C'*'              COMMENT
         BE    MAIN0520                          YES
         TM    DXD_FLAGS,$CONTINUED              CONTINUED STATEMENT?
         BO    MAIN1000                          YES
         LA    R6,COMM_INFO_01+41                1ST BYTE OF A LABEL
         L     R5,DSPCREQ_RECORD_LENGTH          RECORD LENGTH
         S     R5,F41                            LENGTH REMAINING
         BL    MAIN1000                          NO MORE DATA
         CLI   COMM_INFO_01+39,C'0'              STATEMENT NUMBER?
         BL    MAIN1000                          NO
         CLI   COMM_INFO_01+41,C' '              LABEL PRESENT?
         BE    MAIN0130                          NO
         ITRACE ID=LABLATTR
         MVI   COMM_INFO_01+40,$SCREEN_ATTR_HIGH
MAIN0120 DS    0H
         CLI   0(R6),C' '                        END OF LABEL?
         BE    MAIN0130                          YES
         LA    R6,1(,R6)                         NEXT
         BCT   R5,MAIN0120                       LOOP
         B     MAIN1000
MAIN0130 DS    0H
         ITRACE ID=OP_ATTR,                                            +
               RDATA1=R6,                                              +
               RDATA2=R8
         MVI   0(R6),$SCREEN_ATTR_HIGH_GREEN
         LA    R6,1(,R6)
         BCT   R5,MAIN0140
         B     MAIN1000
MAIN0140 DS    0H
         CLI   0(R6),C' '                        BLANK?
         BNE   MAIN0150                          NO
         LA    R6,1(,R6)                         NEXT
         BCT   R5,MAIN0140
         B     MAIN1000
MAIN0150 DS    0H
         CLI   0(R6),C' '                        BLANK?
         BE    MAIN0160
         LA    R6,1(,R6)                         NEXT
         BCT   R5,MAIN0150
         B     MAIN1000
MAIN0160 DS    0H
         ITRACE ID=DATAATTR,                                           +
               RDATA1=R6,                                              +
               RDATA2=R8
         MVI   0(R6),$SCREEN_ATTR_HIGH_BLUE
         B     MAIN1000                          OTHERWISE LEAVE ALONE
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0500 DS    0H
         ITRACE ID=MACRO
         MVI   DXD_ATTR,$SCREEN_ATTR_HIGH_YELLOW
         B     MAIN1000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0510 DS    0H
         ITRACE ID=COPY
         MVI   DXD_ATTR,$SCREEN_ATTR_HIGH_GREEN
         B     MAIN1000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0520 DS    0H
         ITRACE ID=COMMENT
         MVI   DXD_ATTR,$SCREEN_ATTR_HIGH_TURQUOISE
         B     MAIN1000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0530 DS    0H
         ITRACE ID=HEADING
         OI    DXD_FLAGS,$HEADINGS
         CLC   LOC_STATEMENT,COMM_INFO_01+3      "LOC  OBJECT"?
         BNE   MAIN1000                          NO.. STILL IN HEADING
         NI    DXD_FLAGS,255-$HEADINGS           TURN OFF HEADING FLAG
         B     MAIN1000
* ------------------------------------------------------------------- *
*        Relocation dictionary found, bypass formatting from now on   *
* ------------------------------------------------------------------- *
MAIN0540 DS    0H
         OI    DXD_FLAGS,$BYPASS                 SET BYPASS FLAG
* ------------------------------------------------------------------- *
*        All formatting complete.  Copy to VDATA.                     *
* ------------------------------------------------------------------- *
MAIN1000 DS    0H
         L     R2,DSPCREQ_RECORD_LENGTH          DATA LENGTH
         L     R1,DATASPACE_1_DISPLAY_FIRST_COLUMN
         BCTR  R1,0                              RELATIVE TO ZERO
         SR    R2,R1                             COMPUTE LENGTH LEFT
         BNH   EXIT0000                          NONE TO DISPLAY
         C     R2,SPF_VWIDTH                     TOO MUCH FOR 1 LINE?
         BNH   MAIN1010                          NO
         L     R2,SPF_VWIDTH                     LIMIT LENGTH
MAIN1010 DS    0H
         ITRACE ID=SET_ATTR,                                           +
               DATA1=(DXD_ATTR,1)
         MVC   COMM_INFO_01(1),DXD_ATTR
         BCTR  R2,0                              FOR EX
         LA    R3,COMM_INFO_01                   WORK AREA ADDR
         AR    R3,R1                             PLUS LEFT MOST COLUMN
         ITRACE ID=MVC_2,                                              +
               RDATA1=R2,                                              +
               RDATA2=R3
         EX    R2,MVC_2                          COPY TO VDATA
         NI    DXD_FLAGS,255-$CONTINUED          RESET FLAG
         CLI   COMM_INFO_01+112,C' '             CONTINUED?
         BNO   EXIT0000                          NO
         OI    DXD_FLAGS,$CONTINUED              SET INDICATOR
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0010  DS    0H
         ITRACE ID=DSPC_ERR,                                           +
               DATA1=(DSPCREQ_RC,1)
         DC    H'0'
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=EXIT
         SR    R15,R15
         IC    R15,DXD_RC                        RETURN CODE
         L     R13,4(,R13)                       RESTORE R13
         L     R14,12(,R13)                      RESTORE R14
         LM    R0,R12,20(R13)                    RESTORE REGISTERS
         BR    R14                               RETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MVC_1    MVC   COMM_INFO_01(0),0(R1)             COPY TO WORK AREA
MVC_2    MVC   0(0,R8),0(R3)                     COPY TO VDATA
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDSTART        DC    Q(DXDASM)
F41             DC    F'41'
H137            DC    H'137'

OPTIONS_STATEMENT     DC    C'Options for this Assembly'
ADATA_STATEMENT       DC    C'ADATA'
ALIGN_STATEMENT       DC    C'ALIGN'
USING_STATEMENT       DC    C'Active Usings: '
LOC_STATEMENT         DC    C'Loc  Object Code  '
RELOCATION_STATEMENT  DC    C'Relocation Dictionary'

PRINTABLES      DS    0C
*                        0 1 2 3 4 5 6 7 8 9 A B C D E F
                DC    X'40404040404040404040404040404040' 00-0F
                DC    X'40404040404040404040404040404040' 10-1F
                DC    X'40404040404040404040404040404040' 20-2F
                DC    X'40404040404040404040404040404040' 30-3F
                DC    X'404040404040404040404A4B4C4D4E4F' 40-4F
                DC    X'504040404040404040405A5B5C5D5E5F' 50-5F
                DC    X'60614040404040404040406B6C6D6E6F' 60-6F
                DC    X'404040404040404040407A7B7C7D7E7F' 70-7F
                DC    X'40818283848586878889408B8C8D8E8F' 80-8F
                DC    X'40919293949596979899409B9C9D9E9F' 90-9F
                DC    X'4040A2A3A4A5A6A7A8A940ABACADAEAF' A0-AF
                DC    X'4040404040404040404040BBBCBDBEBF' B0-BF
                DC    X'40C1C2C3C4C5C6C7C8C9404040404040' C0-CF
                DC    X'40D1D2D3D4D5D6D7D8D9404040404040' D0-DF
                DC    X'4040E2E3E4E5E6E7E8E9404040404040' E0-EF
                DC    X'F0F1F2F3F4F5F6F7F8F9404040404040' F0-FF

ASMFMTEND       EQU  *
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDASM          DSECT
                COPY     DXDPREF

DXD_RC          DS       X
DXD_ATTR        DS       X
DXD_FLAGS       DS       X
$CONTINUED      EQU      X'80'
$HEADINGS       EQU      X'40'
$BYPASS         EQU      X'20'

                DSPCREQ  DSECT=NO
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         BPXYSTAT DSECT=YES,LIST=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         OSSPFD   TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY     ATTRS
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
FORMAT_1 DSECT
         IBMMAC   VTOC=1
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY     REGEQU
         END      OSASMFMT
