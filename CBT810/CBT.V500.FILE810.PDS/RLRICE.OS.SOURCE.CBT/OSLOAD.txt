* ------------------------------------------------------------------- *
*                                                                     *
*  Module name: OSLOAD                                                *
*                                                                     *
*  Load On Screen modules.                                            *
*                                                                     *
*  Due to On Screen's multiple 'sessions' and session switching,      *
*  use counts on modules could get confusing.  This allows a given    *
*  module to be loaded once and used by multiple sessions.  Once      *
*  a module is loaded, it is not deleted until the last session       *
*  ends.                                                              *
*                                                                     *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
OSLOAD   CSECT
OSLOAD   AMODE 31
OSLOAD   RMODE ANY
         USING OSCOMM,R12
         USING SESSION,R11
         USING OSLOAD,R15
         B     INIT0000
MODID    DC    CL8'OSLOAD'
         DC    CL8'&SYSDATE'
         DC    CL8'&SYSTIME'
         DC    A(LOADEND-OSLOAD)
INIT0000 DS    0H
         STM   R14,R12,12(R13)                   SAVE REGS
         LR    R10,R15                           COPY ENTRY POINT
         DROP  R15
         USING OSLOAD,R10                        DEFINE BASE
         LR    R9,R1                             COPY PARM ADDRESS
         USING LOADREQ,R9                        DEFINE BASE
         GETMAIN RU,                                                   +
               LV=OSLOAD_WORK_L,                                       +
               LOC=ANY
         ST    R13,4(,R1)                        SAVE R13
         ST    R1,8(,R13)                        CHAIN SAVE AREA
         LR    R13,R1                            COPY WORK AREA ADDRESS
         USING OSLOAD_WORK,R13                   DEFINE WORK AREA BASE
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY,                                              +
               DATA1=(LOADREQ_FUNC,1),                                 +
               DATA2=LOADREQ_MODULE
         MVC   DXD_SAVED_RETRY,SESS_RETRY        SAVE CALLER'S RETRY
         LTR   R11,R11                           SESSION ACTIVE YET?
         BZ    LOAD0010                          NO
         LA    R1,RTRY0000                       OUR RETRY ADDRESS
         ST    R1,SESS_RETRY
LOAD0010 DS    0H
         MVI   LOADREQ_RC,$LOADREQ_OK
         CLI   LOADREQ_FUNC,$LOADREQ_LOAD
         BE    LOAD0100
         CLI   LOADREQ_FUNC,$LOADREQ_DELETE
         BE    LOAD0200
         CLI   LOADREQ_FUNC,$LOADREQ_CLEANUP
         BE    LOAD0300
         ITRACE ID=BAD_FUNC
         DC    H'0'
LOAD0100 DS    0H
         ITRACE ID=LOAD,                                               +
               DATA1=LOADREQ_MODULE
         LA    R2,COMM_LOAD_CHAIN                ADDRESS OF 1ST BLOCK
LOAD0110 DS    0H
         ICM   R3,15,0(R2)                       END OF CHAIN?
         BZ    LOAD0130                          YES
         USING LOAD_BLOCK,R3                     DEFINE BASE
         ITRACE ID=CHECK1,                                             +
               RDATA1=R3,                                              +
               DATA2=LOAD_BLOCK_MODULE
         CLC   LOADREQ_MODULE,LOAD_BLOCK_MODULE  MODULE LOADED?
         BE    LOAD0120                          YES
         LR    R2,R3
         B     LOAD0110                          CHECK AGAIN
LOAD0120 DS    0H
         ITRACE ID=ONCHAIN1,                                           +
               DATA1=(LOAD_BLOCK_EP,4)
         MVC   LOADREQ_EP,LOAD_BLOCK_EP          COPY ENTRY POINT
         B     EXIT0000
LOAD0130 DS    0H
         ITRACE ID=NOT_FND1
         GETMAIN RU,                             GETMAIN NEW LOAD BLOCK+
               LV=LOAD_BLOCK_L,                                        +
               LOC=ANY
         LR    R3,R1                             COPY ADDRESS
         ST    R3,0(,R2)                         ADD TO CHAIN
         XC    LOAD_BLOCK_NEXT,LOAD_BLOCK_NEXT   CLEAR 'NEXT' ADDRESS
         MVC   LOAD_BLOCK_MODULE,LOADREQ_MODULE  COPY MODULE NAME
         ITRACE ID=LOAD,                                               +
               DATA1=LOADREQ_MODULE
         LOAD  EPLOC=LOADREQ_MODULE              LOAD THE MODULE
         STCM  R0,R15,LOAD_BLOCK_EP              SAVE THE ADDRES
         STCM  R0,R15,LOADREQ_EP                 GIVE IT TO CALLER TOO
         ITRACE ID=LOADED,                                             +
               RDATA1=R0,                                              +
               RDATA2=R3
         B     EXIT0000

LOAD0200 DS    0H
         ITRACE ID=DELETE
         LA    R2,COMM_LOAD_CHAIN                ADDRESS OF 1ST BLOCK
LOAD0210 DS    0H
         ICM   R3,15,0(R2)                       END OF CHAIN?
         BZ    EXIT0000                          YES, NOT LOADED
         ITRACE ID=CHECK2,                                             +
               RDATA1=R3,                                              +
               DATA2=LOAD_BLOCK_MODULE
         CLC   LOADREQ_MODULE,LOAD_BLOCK_MODULE  MODULE LOADED?
         BE    LOAD0220                          YES
         LR    R2,R3
         B     LOAD0210                          CHECK AGAIN
LOAD0220 DS    0H
         ITRACE ID=ONCHAIN2,                                           +
               DATA1=(LOAD_BLOCK_EP,4)
         MVC   0(4,R2),LOAD_BLOCK_NEXT           REMOVE FROM CHAIN
         DELETE EPLOC=LOADREQ_MODULE             DELETE THE MODULE
         ITRACE ID=FREE1,                                              +
               RDATA1=R3
         FREEMAIN RU,                                                  +
               A=(R3),                                                 +
               LV=LOAD_BLOCK_L
         BZ    EXIT0000

LOAD0300 DS    0H
         ITRACE ID=CLEAN_UP
         ICM   R3,15,COMM_LOAD_CHAIN             ADDRESS OF 1ST BLOCK
         BZ    EXIT0000
LOAD0310 DS     0H
         ITRACE ID=DELETE3,                                            +
               RDATA1=R3,                                              +
               DATA2=LOAD_BLOCK_MODULE
         DELETE EP=LOAD_BLOCK_MODULE
         L     R2,LOAD_BLOCK_NEXT
         FREEMAIN RU,                                                  +
               A=(R3),                                                 +
               LV=LOAD_BLOCK_L
         LTR   R3,R2                             ANOTHER BLOCK?
         BNZ   LOAD0310                          YES
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
RTRY0000 DS    0H
         ITRACE ID=ABEND,                                              +
               DATA1=(SESS_SYSTEM_CODE,6)
         L     R15,COMM_V_OSABEND
         BALR  R14,R15                           LINK TO OSABEND
         NI    SESS_ABEND_FLAG,255-$ABENDED
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=EXIT
         LTR   R11,R11                           SESSION ACTIVE?
         BZ    EXIT0010                          NO
         MVC   SESS_RETRY,DXD_SAVED_RETRY        RESTORE CALLER'S RETRY
EXIT0010 DS    0H
         LR    R1,R13                            COPY WORK AREA ADDR
         L     R13,4(,R13)                       AREA ADDRESS
         FREEMAIN RU,                            RESTORE R13           +
               A=(1),                            FREE WORK AREA        +
               LV=OSLOAD_WORK_L
         LM    R14,R12,12(R13)                   RESTORE REGISTERS
         SR    R15,R15                           SET RETURN CODE
         BR    R14                               RETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                    LTORG

LOADEND             EQU   *
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
OSLOAD_WORK         DSECT
                    COPY  DXDPREF
DXD_SAVED_RETRY     DS    A
OSLOAD_WORK_L       EQU   *-OSLOAD_WORK
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                    LOADREQ DSECT=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
LOAD_BLOCK          DSECT
LOAD_BLOCK_NEXT     DS   A
LOAD_BLOCK_MODULE   DS   CL8
LOAD_BLOCK_EP       DS   A
LOAD_BLOCK_L        EQU  *-LOAD_BLOCK
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                    COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                    SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                    BPXYSTAT DSECT=YES,LIST=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                    COPY TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                    COPY REGEQU
                    END  OSLOAD
