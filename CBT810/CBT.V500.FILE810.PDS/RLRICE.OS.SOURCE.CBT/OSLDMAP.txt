* ------------------------------------------------------------------- *
*                                                                     *
*  Module name: OSLDMAP                                               *
*                                                                     *
*  Display LMOD MAP detail info                                       *
*                                                                     *
* ------------------------------------------------------------------- *
OSLDMAP  CSECT
OSLDMAP  AMODE 31
OSLDMAP  RMODE ANY
         USING OSLDMAP,R15
         USING OSCOMM,R12
         USING SESSION,R11
         B     INIT0000
MODID    DC    CL8'OSLDMAP'
         DC    CL8'&SYSDATE'
         DC    CL8'&SYSTIME'
         DC    A(MAPEND-OSLDMAP)
INIT0000 DS    0H
         STM   R14,R12,12(R13)                SAVE REGISTERS
         LR    R10,R15                        COPY BASE ADDRESS
         LA    R9,2048(,R10)
         LA    R9,2048(,R9)
         USING OSLDMAP,R10,R9
         DROP  R15
         L     R5,COMM_OSSPFD
         USING OSSPFD,R5
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         CLI   SESS_FORMAT_FLAGS,$FORMAT_INITIALIZE
         BNE   INIT0010                       NO
         LA    R0,OSLDMAP_DXD_L               WORK AREA LENGTH
         ST    R0,SESS_DXD_LENGTH             SAVE LENGTH
         GETMAIN RU,                                                   +
               LV=(0),                                                 +
               LOC=ANY
         ST    R1,SESS_DXD_ADDR               SAVE ADDRESS
         LR    R2,R1
         LR    R0,R1
         LA    R1,OSLDMAP_DXD_L
         SR    R14,R14
         SR    R15,R15
         MVCL  R0,R14                         INITIALIZE WORK AREA
         ST    R13,4(,R2)
         ST    R2,8(,R13)
         LR    R13,R2                         SET WORK AREA ADDRESS
         USING OSLDMAP_DXD,R13                DEFINE BASE
         MVC   DXD_CSECT,MODID                SET MODULE ID
         ITRACE ID=ENTRY1
         LA    R1,DATASPACE_1
         ST    R1,DSPCREQ_DATASPACE
         LA    R1,LMOD_MAP_DATA
         ST    R1,DSPCREQ_RECORD_ADDR
         MVC   DSPCREQ_RECORD_NBR,DATASPACE_1_DISPLAY_FIRST_RECORD
         LA    R1,PANEL_NAME                  PANEL NAME
         MVI   COMM_VDATA_FUNC,$VDATA_GETMAIN SET FUNCTION
         L     R15,COMM_V_OSVDATA
         BALR  R14,R15                        GETMAIN VDATA
         ST    R0,DXD_VDATA_LENGTH            SAVE LENGTH
         ST    R1,DXD_VDATA_ADDR              SAVE ADDRESS
         MVC   DXD_VDEPTH,SPF_VDEPTH          SAVE DEPTH
         MVC   DXD_VWIDTH,SPF_VWIDTH          SAVE WIDTH
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
INIT0010 DS    0H
         L     R1,SESS_DXD_ADDR               WORK AREA ADDRESS
         ST    R13,4(,R1)
         ST    R1,8(,R13)
         LR    R13,R1
         ITRACE ID=ENTRY2
         CLI   SESS_FORMAT_FLAGS,$FORMAT_CONTROL
         BE    EXIT0000
         CLI   SESS_FORMAT_FLAGS,$FORMAT_CLEANUP
         BE    CLEAN000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAP0000  DS    0H
         L     R4,DXD_VDATA_ADDR
         L     R3,DXD_VDEPTH
MAP0010  DS    0H
         MVC   0(80,R4),COMM_BLANKS
         A     R4,DXD_VWIDTH
         BCT   R3,MAP0010
         MVC   DSPCREQ_RECORD_NBR,DATASPACE_1_DISPLAY_FIRST_RECORD
         L     R4,DXD_VDATA_ADDR
         L     R3,DXD_VDEPTH
         BCTR  R3,0                 <--- RESERVE LINE FOR TOTAL MSG
         BCTR  R3,0                 <--- RESERVE LINE FOR TOTAL MSG
MAP0020  DS    0H
         ITRACE ID=RETRIEVE,                                           +
               DATA1=(DSPCREQ_RECORD_NBR,4)
         MVI   DSPCREQ_FUNC,$DSPCREQ_RETRIEVE
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE
         BALR  R14,R15
         ITRACE ID=DSPC_RC,                                            +
               DATA1=(DSPCREQ_RC,1)
         CLI   DSPCREQ_RC,$DSPCREQ_OK
         BNE   ERR0010

         MVC   0(MSG_01_L,R4),MSG_01
         USING MSG_01,R4
         ICM   R1,15,LMOD_MAP_NEXT
         BAL   R14,NBR0000
         MVC   MSG_01_NEXT,DXD_NUMBER
         BCT   R3,MAP0030
         DROP  R4
         B     MAP0250                        SCREEN FULL

MAP0030  DS    0H
         MVC   0(MSG_02_L,R4),MSG_02
         USING MSG_02,R4
         MVC   MSG_02_TYPE,LMOD_MAP_TYPE
         CLI   LMOD_MAP_TYPE,C'M'             MODULE?
         BE    MAP0040
         CLI   LMOD_MAP_TYPE,C'C'             CLASS?
         BE    MAP0050
         CLI   LMOD_MAP_TYPE,C'O'             OVERLAY?
         BE    MAP0060
         CLI   LMOD_MAP_TYPE,C'S'             SECTION WITHIN CLASS?
         BE    MAP0070
         CLI   LMOD_MAP_TYPE,C'P'             PART WITHIN CLASS?
         BE    MAP0080
         CLI   LMOD_MAP_TYPE,C'L'             LABEL WITHIN SECTION?
         BE    MAP0090
         CLI   LMOD_MAP_TYPE,C'X'             TEXT EXTENT?
         BE    MAP0100
         CLI   LMOD_MAP_TYPE,C'E'             END OF MODULE?
         BE    MAP0110
         B     MAP0120
MAP0040  DS    0H
         MVC   MSG_02_DESC,TYPE_DESC_MODULE
         B     MAP0120
MAP0050  DS    0H
         MVC   MSG_02_DESC,TYPE_DESC_CLASS
         B     MAP0120
MAP0060  DS    0H
         MVC   MSG_02_DESC,TYPE_DESC_OVERLAY
         B     MAP0120
MAP0070  DS    0H
         MVC   MSG_02_DESC,TYPE_DESC_SECTION
         B     MAP0120
MAP0080  DS    0H
         MVC   MSG_02_DESC,TYPE_DESC_PART
         B     MAP0120
MAP0090  DS    0H
         MVC   MSG_02_DESC,TYPE_DESC_LABEL
         B     MAP0120
MAP0100  DS    0H
         MVC   MSG_02_DESC,TYPE_DESC_TEXT
         B     MAP0120
MAP0110  DS    0H
         MVC   MSG_02_DESC,TYPE_DESC_END
MAP0120  DS    0H
         A     R4,DXD_VWIDTH
         BCT   R3,MAP0130
         DROP  R4
         B     MAP0250

MAP0130  DS    0H
         MVC   0(MSG_03_L,R4),MSG_03
         USING MSG_03,R4
         UNPK  MSG_03_FLAGS(3),LMOD_MAP_FLAGS(2)
         TR    MSG_03_FLAGS,COMM_HEXCHAR
         MVI   MSG_03_FLAGS+2,C' '
         LA    R14,MSG_03_DESC
         TM    LMOD_MAP_FLAGS,X'80'           SINGLE EXTENT?
         BNO   MAP0140
         MVC   0(L'FLAG_DESC_SINGLE,R14),FLAG_DESC_SINGLE
         LA    R14,L'FLAG_DESC_SINGLE+2(,R14)
MAP0140  DS    0H
         TM    LMOD_MAP_FLAGS,X'40'           LOADABLE TEXT?
         BNO   MAP0150
         MVC   0(L'FLAG_DESC_LOADABLE,R14),FLAG_DESC_LOADABLE
         LA    R14,L'FLAG_DESC_LOADABLE+2(,R14)
MAP0150  DS    0H
         TM    LMOD_MAP_FLAGS,X'20'           EXECUTABLE TEXT?
         BNO   MAP0160
         MVC   0(L'FLAG_DESC_EXECUTABLE,R14),FLAG_DESC_EXECUTABLE
MAP0160  DS    0H
         A     R4,DXD_VWIDTH
         BCT   R3,MAP0170
         DROP  R4
         B     MAP0250

MAP0170  DS    0H
         MVC   0(MSG_04_L,R4),MSG_04
         USING MSG_04,R4
         MVC   MSG_04_NAME,LMOD_MAP_NAME
         A     R4,DXD_VWIDTH
         BCT   R3,MAP0180
         DROP  R4
         B     MAP0250

MAP0180  DS    0H
         MVC   0(MSG_05_L,R4),MSG_05
         USING MSG_05,R4
         ICM   R1,15,LMOD_MAP_OFFSET
         BAL   R14,NBR0000
         MVC   MSG_05_OFFSET,DXD_NUMBER
         UNPK  MSG_05_HEX(9),LMOD_MAP_OFFSET(5)
         TR    MSG_05_HEX,COMM_HEXCHAR
         MVI   MSG_05_HEX+8,C' '
         A     R4,DXD_VWIDTH
         BCT   R3,MAP0190
         DROP  R4
         B     MAP0250

MAP0190  DS    0H
         MVC   0(MSG_06_L,R4),MSG_06
         USING MSG_06,R4
         ICM   R1,15,LMOD_MAP_QUANTITY
         BAL   R14,NBR0000
         MVC   MSG_06_QUANTITY,DXD_NUMBER
         UNPK  MSG_06_HEX(9),LMOD_MAP_QUANTITY(5)
         TR    MSG_06_HEX,COMM_HEXCHAR
         MVI   MSG_06_HEX+8,C' '
         A     R4,DXD_VWIDTH
         BCT   R3,MAP0200
         DROP  R4
         B     MAP0250

MAP0200  DS    0H
         MVC   0(MSG_07_L,R4),MSG_07
         USING MSG_07,R4
         UNPK  MSG_07_NAME_SPACE(3),LMOD_MAP_NAME_SPACE(2)
         TR    MSG_07_NAME_SPACE,COMM_HEXCHAR
         MVI   MSG_07_NAME_SPACE+2,C' '
         CLI   LMOD_MAP_NAME_SPACE,X'01'      EXTERNAL LABELS(L)?
         BE    MAP0210
         CLI   LMOD_MAP_NAME_SPACE,X'02'      PSEUDOREGISTERS(P)?
         BE    MAP0220
         B     MAP0230
MAP0210  DS    0H
         MVC   MSG_07_DESC,SPACE_DESC_LABELS
         B     MAP0230
MAP0220  DS    0H
         MVC   MSG_07_DESC,SPACE_DESC_PSEUDO
MAP0230  DS    0H
         A     R4,DXD_VWIDTH
         BCT   R3,MAP0240
         DROP  R4
         B     MAP0250

MAP0240  DS    0H
         MVC   0(MSG_08_L,R4),MSG_08
         USING MSG_08,R4
         SR    R1,R1
         ICM   R1,3,LMOD_MAP_RECFM
         BAL   R14,NBR0000
         MVC   MSG_08_RECFM,DXD_NUMBER
         A     R4,DXD_VWIDTH
         DROP  R4

MAP0250 DS     0H
         A     R4,DXD_VWIDTH
         A     R4,DXD_VWIDTH
         MVC   0(TOTAL_MSG_L,R4),TOTAL_MSG
         USING TOTAL_MSG,R4
         L     R1,DSPCREQ_RECORD_NBR
         CVD   R1,COMM_DWORD
         ED    TOTAL_MSG_CURRENT,COMM_DWORD+5
         L     R1,DATASPACE_1_LAST_RECORD
         CVD   R1,COMM_DWORD
         ED    TOTAL_MSG_TOTAL,COMM_DWORD+5
         DROP  R4

         B     DISP0000

* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
NBR0000  DS    0H
         CVD   R1,COMM_DWORD
         MVC   DXD_NUMBER,EDIT_WORD
         ED    DXD_NUMBER,COMM_DWORD+3
         LA    R15,L'DXD_NUMBER
NBR0010  DS    0H
         CLI   DXD_NUMBER,C' '
         BNER  R14
         MVC   DXD_NUMBER(L'DXD_NUMBER),DXD_NUMBER+1
         MVI   DXD_NUMBER+(L'DXD_NUMBER-1),C' '
         BCT   R15,NBR0010
         BR    R14
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0010  DS    0H
         ITRACE ID=ERR0010
         MVC   SPF_MSG_1,COMM_MSG_1
         MVC   SPF_MSG_2,COMM_MSG_2
         MVC   SPF_MSG_3,COMM_MSG_3
         MVC   SPF_MSG_4,COMM_MSG_4
         MVC   SPF_MSG_5,COMM_MSG_5
         MVI   COMM_SESS_FUNC,$SESS_REMOVE
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DISP0000 DS    0H
         ITRACE ID=DISPLAY
         MVC   SESS_DISP_PANEL,PANEL_NAME
         MVC   SESS_DISP_VDATA_ADDR,DXD_VDATA_ADDR
         MVC   SESS_DISP_VDATA_LENGTH,DXD_VDATA_LENGTH
         XC    SESS_DISP_CURSOR,SESS_DISP_CURSOR
         L     R15,COMM_V_OSDISP
         BALR  R14,R15
         ITRACE ID=DISP_RC,                                            +
               DATA1=(SESS_RC,2),                                      +
               DATA2=(SESS_STATUS,1)
         CLC   COMM_NEW_FORMAT,COMM_BLANKS    NEW FORMAT SELECTED?
         BNE   EXIT0000                       YES
         CLI   COMM_SESS_FUNC,0               SESSION FUNCTION?
         BNE   EXIT0000                       YES
         OC    SESS_RC,SESS_RC                RC=0?
         BZ    DISP0005                       YES
         ITRACE ID=ENDING
         MVI   COMM_SESS_FUNC,$SESS_REMOVE    SET FUNCTION
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
*        Check for scroll command(s)                                  *
*                                                                     *
*        Note that 'up', 'down', 'top', and 'bottom' are the only     *
*        valid scroll commands.                                       *
*                                                                     *
* ------------------------------------------------------------------- *
DISP0005 DS    0H
         CLI   SPF_ZSCROLLA,C'C'              CSR?
         BNE   DISP0010                       NO
         MVC   SPF_ZSCROLLN,F1                CSR SCROLLING IS 1 LINE
DISP0010 DS    0H
         ITRACE ID=ZVERB,                                              +
               DATA1=SPF_ZVERB,                                        +
               DATA2=SPF_ZSCROLLA
         CLI   SPF_ZVERB,C'B'                 BOTTOM?
         BE    DISP0100                       YES
         CLI   SPF_ZVERB,C'D'                 DOWN?
         BE    DISP0200                       YES
         CLI   SPF_ZVERB,C'T'                 TOP?
         BE    DISP0300                       YES
         CLI   SPF_ZVERB,C'U'                 UP?
         BE    DISP0400                       YES
         B     DISP0000

DISP0100 DS    0H
         ITRACE ID=BOTTOM
         MVC   DATASPACE_1_DISPLAY_FIRST_RECORD,DATASPACE_1_LAST_RECORD
         B     MAP0000
DISP0200 DS    0H
         ITRACE ID=DOWN,                                               +
               DATA1=SPF_ZSCROLLA,                                     +
               DATA2=(SPF_ZSCROLLN,4)
         CLI   SPF_ZSCROLLA,C'M'                   MAX?
         BE    DISP0100                            YES.. SAME AS BOTTOM
         L     R1,SPF_ZSCROLLN                     NUMBER TO SCROLL
         A     R1,DATASPACE_1_DISPLAY_FIRST_RECORD PLUS CURRENT RECORD
         C     R1,DATASPACE_1_LAST_RECORD          BEYOND HIGH RECORD?
         BH    DISP0100                            YES.. GO TO BOTTOM
         ST    R1,DATASPACE_1_DISPLAY_FIRST_RECORD UPDATE RECORD NUMBER
         B     MAP0000                             GO BUILD DATA
DISP0300 DS    0H
         ITRACE ID=TOP
         MVC   DATASPACE_1_DISPLAY_FIRST_RECORD,F1
         B     MAP0000
DISP0400 DS    0H
         ITRACE ID=UP,                                                 +
               DATA1=SPF_ZSCROLLA,                                     +
               DATA2=(SPF_ZSCROLLN,4)
         CLI   SPF_ZSCROLLA,C'M'                   MAX?
         BE    DISP0300                            YES.. GO TO TOP
         L     R1,DATASPACE_1_DISPLAY_FIRST_RECORD
         S     R1,SPF_ZSCROLLN                     MINUS SCROLL
         C     R1,F1                               BELOW 1?
         BL    DISP0300                            YES, GO TO TOP
         ST    R1,DATASPACE_1_DISPLAY_FIRST_RECORD UPDATE RECORD NUMBER
         B     MAP0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
CLEAN000 DS    0H
         ITRACE ID=CLEAN_UP,                                           +
               DATA1=(DXD_VDATA_ADDR,4),                               +
               DATA2=(DXD_VDATA_LENGTH,4)
         ICM   R1,15,DXD_VDATA_ADDR           VDATA GETMAINED?
         BZ    CLEAN010                       NO
         L     R0,DXD_VDATA_LENGTH            VDATA LENGTH
         MVI   COMM_VDATA_FUNC,$VDATA_FREEMAIN
         L     R15,COMM_V_OSVDATA
         BALR  R14,R15                        FREE VDATA
         XC    DXD_VDATA_ADDR,DXD_VDATA_ADDR
         XC    DXD_VDATA_LENGTH,DXD_VDATA_LENGTH
CLEAN010 DS    0H
         LA    R0,OSLDMAP_DXD_L               WORK AREA SIZE
         LR    R1,R13                         COPY WORK AREA ADDRESS
         L     R13,4(,R13)                    RESTORE R13
         FREEMAIN RU,                         FREE WORK AREA           +
               A=(1),                                                  +
               LV=(0)
         XC    SESS_DXD_ADDR,SESS_DXD_ADDR
         XC    SESS_DXD_LENGTH,SESS_DXD_LENGTH
         SR    R15,R15
         B     EXIT0010
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=EXIT
         L     R13,4(,R13)                    RESTORE SAVE AREA ADDRESS
EXIT0010 DS    0H
         LM    R14,R12,12(R13)                RESTORE REGISTERS
         SR    R15,R15                        SET RC
         BR    R14                            RETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
F1                   DC   F'1'
PANEL_NAME           DC   CL8'OSLDMAP'
EDIT_WORD            DC   X'402020206B2020206B202120'

TYPE_DESC_MODULE     DC   CL20'Module'
TYPE_DESC_CLASS      DC   CL20'Class'
TYPE_DESC_OVERLAY    DC   CL20'Overlay segment'
TYPE_DESC_SECTION    DC   CL20'Section within class'
TYPE_DESC_PART       DC   CL20'Part within class'
TYPE_DESC_LABEL      DC   CL20'Label within class'
TYPE_DESC_TEXT       DC   CL20'Text'
TYPE_DESC_END        DC   CL20'End of module'

FLAG_DESC_SINGLE     DC   C'Single extent'
FLAG_DESC_LOADABLE   DC   C'Loadable text'
FLAG_DESC_EXECUTABLE DC   C'Executable'


SPACE_DESC_LABELS    DC   CL20'External labels'
SPACE_DESC_PSEUDO    DC   CL20'Pseudo registers'

                     LTORG

MSG_01               DS   0C
                     DC   AL1($SCREEN_ATTR_HIGH)
                     DC   C'Next           '
                     DC   AL1($SCREEN_ATTR_NORMAL)
MSG_01_NEXT          DC   CL12' '
MSG_01_L             EQU  *-MSG_01

MSG_02               DS   0C
                     DC   AL1($SCREEN_ATTR_HIGH)
                     DC   C'Type           '
                     DC   AL1($SCREEN_ATTR_NORMAL)
MSG_02_TYPE          DC   C' '
                     DC   C'    '
MSG_02_DESC          DC   CL20' '
MSG_02_L             EQU  *-MSG_02

MSG_03               DS   0C
                     DC   AL1($SCREEN_ATTR_HIGH)
                     DC   C'Flags          '
                     DC   AL1($SCREEN_ATTR_NORMAL)
MSG_03_FLAGS         DC   CL2' '
                     DC   C'   '
MSG_03_DESC          DC   CL60' '
MSG_03_L             EQU  *-MSG_03

MSG_04               DS   0C
                     DC   AL1($SCREEN_ATTR_HIGH)
                     DC   C'Name           '
                     DC   AL1($SCREEN_ATTR_NORMAL)
MSG_04_NAME          DC   CL60' '
MSG_04_L             EQU  *-MSG_04

MSG_05               DS   0C
                     DC   AL1($SCREEN_ATTR_HIGH)
                     DC   C'Offset         '
                     DC   AL1($SCREEN_ATTR_NORMAL)
MSG_05_OFFSET        DC   CL12' '
                     DC   C'   ('
MSG_05_HEX           DC   CL8' '
                     DC   C' HEX)'
MSG_05_L             EQU  *-MSG_05

MSG_06               DS   0C
                     DC   AL1($SCREEN_ATTR_HIGH)
                     DC   C'Length/quantity'
                     DC   AL1($SCREEN_ATTR_NORMAL)
MSG_06_QUANTITY      DC   CL12' '
                     DC   C'   ('
MSG_06_HEX           DC   CL8' '
                     DC   C' HEX)'
MSG_06_L             EQU  *-MSG_06

MSG_07               DS   0C
                     DC   AL1($SCREEN_ATTR_HIGH)
                     DC   C'Name space     '
                     DC   AL1($SCREEN_ATTR_NORMAL)
MSG_07_NAME_SPACE    DC   CL2' '
                     DC   C'      '
MSG_07_DESC          DC   CL20' '
MSG_07_L             EQU  *-MSG_07

MSG_08               DS   0C
                     DC   AL1($SCREEN_ATTR_HIGH)
                     DC   C'Record format  '
                     DC   AL1($SCREEN_ATTR_NORMAL)
MSG_08_RECFM         DC   CL5' '
MSG_08_L             EQU  *-MSG_08

TOTAL_MSG            DS   0C
                     DC   AL1($SCREEN_ATTR_NORMAL)
                     DC   C'Displaying item '
                     DC   AL1($SCREEN_ATTR_HIGH)
TOTAL_MSG_CURRENT    DC   X'402020202120'
                     DC   AL1($SCREEN_ATTR_NORMAL)
                     DC   C' of'
                     DC   AL1($SCREEN_ATTR_HIGH)
TOTAL_MSG_TOTAL      DC   X'402020202120'
                     DC   AL1($SCREEN_ATTR_NORMAL)
TOTAL_MSG_L          EQU  *-TOTAL_MSG

MAPEND               EQU  *
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
OSLDMAP_DXD          DSECT
                     COPY  DXDPREF

DXD_VDATA_ADDR       DS    A
DXD_VDATA_LENGTH     DS    F

DXD_VDEPTH           DS    F
DXD_VWIDTH           DS    F

DXD_MAP_BUFFER       DS    A

DXD_NUMBER           DS    CL12

                     COPY  MAPDATA

                     DSPCREQ  DSECT=NO
OSLDMAP_DXD_L        EQU   *-OSLDMAP_DXD
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         SESSION
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         BPXYSTAT  DSECT=YES,LIST=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         OSSPFD    TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY      ATTRS
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY      TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY REGEQU
         END  OSLDMAP
