* ------------------------------------------------------------------- *
*                                                                     *
*                                                                     *
*     Generate separator lines for APPEND or OFFLOAD                  *
*                                                                     *
*     Each member may be followed by up to 3 'separator' lines        *
*                                                                     *
*                                                                     *
*                                                                     *
*     Variables available for customizing the separator lines:        *
*                                                                     *
*                                                                     *
*      &DSN     data set name                                         *
*      &MEMBER  member name                                           *
*      &DATE1   date in 'Julian'   form (YYDDD)  (five digits)        *
*      &DATE2   date in 'Julian'   form (YY.DDD) (six digits)         *
*      &DATE3   date in MMDDYYYY   form (8 bytes)                     *
*      &DATE4   date in MM/DD/YYYY form (10 bytes)                    *
*      &DATE5   date in DDMMYYYY   form (8 bytes)                     *
*      &DATE6   date in DD/MM/YYYY form (10 bytes)                    *
*      &DATE7   date in YYYYMMDD   form (8 bytes)                     *
*      &DATE8   date in YYYY/MM/DD form (10 bytes)                    *
*      &SEQ     Sequence number (first file is 00001,                 *
*                                second is 00002, etc)                *
*      &TIME1   time in HH:MM:SS        (8 bytes)                     *
*      &TIME2   time in HH:MM:SS:th     (11 bytes)                    *
*      &RECORDS Number of records in last file appended               *
*      &SIZE    Number of bytes in last file appended                 *
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
OSSEP    CSECT
OSSEP    AMODE 31
OSSEP    RMODE ANY
         USING OSCOMM,R12
         USING OSSEP,R15
         B     INIT0000
MODID    DC    CL8'OSSEP'
         DC    CL8'&SYSDATE'
         DC    CL8'&SYSTIME'
         DC    A(SEPEND-OSSEP)
INIT0000 DS    0H
         USING SESSION,R11                    DEFINE SESSION BASE
         STM   R14,R12,12(R13)                SAVE REGS
         LR    R10,R15                        COPY ENTRY POINT
         DROP  R15
         USING OSSEP,R10                      DEFINE BASE
         LR    R9,R1
         USING SEPREQ,R9                      DEFINE BASE
         L     R1,COMM_DXD                    WORK AREA BASE
         A     R1,DXD_START                   PLUS DISP
         ST    R13,4(,R1)                     SAVE R13
         ST    R1,8(,R13)                     CHAIN SAVE AREA
         LR    R13,R1                         COPY WORK AREA ADDRESS
         USING SEPDXD,R13                     DEFINE WORK AREA BASE
         MVC   DXD_CSECT,MODID
         MVC   WORK_TIME(TIME_L),TIME_I
         ITRACE ID=ENTRY,                                              +
               RDATA1=R9
         L     R8,SEPREQ_FIRST_SEPARATOR      FIRST SEPARATOR
         L     R7,SEPREQ_NBR                  NUMBER OF SEPARATORS
         L     R6,SEPREQ_DCB                  OUTPUT DCB ADDRESS
MAIN0110 DS    0H
         MVC   COMM_INFO_01,COMM_BLANKS
         CLC   0(72,R8),COMM_BLANKS           ALL BLANK?
         BE    MAIN0290                       YES
         CLC   =C'BLANK ',0(R8)               WANT A BLANK LINE?
         BE    MAIN0280                       YES
         LA    R5,COMM_INFO_01                OUTPUT AREA
         LR    R2,R8                          SEPARATOR SOURCE
         LA    R3,72                          LENGTH TO SCAN
MAIN0120 DS    0H
         CLC   MEMBER_KEYWORD,0(R2)           MEMBER NAME KEYWORD?
         BE    MAIN0130                       YES
         CLC   DSN_KEYWORD,0(R2)              DATA SET NAME KEYWORD?
         BE    MAIN0140                       YES
         CLC   SEQ_KEYWORD,0(R2)              SEQUENCE NUMBER KEYWORD?
         BE    MAIN0150                       YES
         CLC   TIME1_KEYWORD,0(R2)            TIME (HH.MM.SS.TH)?
         BE    MAIN0160                       YES
         CLC   TIME2_KEYWORD,0(R2)            TIME (HH.MM.SS)?
         BE    MAIN0160                       YES
         CLC   DATE1_KEYWORD,0(R2)            YYDDD?
         BE    MAIN0180                       YES
         CLC   DATE2_KEYWORD,0(R2)            YY.DDD?
         BE    MAIN0190                       YES
         CLC   DATE3_KEYWORD,0(R2)            MMDDYYYY?
         BE    MAIN0200                       YES
         CLC   DATE4_KEYWORD,0(R2)            MM/DD/YYYY?
         BE    MAIN0210                       YES
         CLC   DATE5_KEYWORD,0(R2)            DDMMYYYY?
         BE    MAIN0220                       YES
         CLC   DATE6_KEYWORD,0(R2)            DD/MM/YYYY?
         BE    MAIN0230                       YES
         CLC   DATE7_KEYWORD,0(R2)            YYYYMMDD?
         BE    MAIN0240                       YES
         CLC   DATE8_KEYWORD,0(R2)            YYYY/MM/DD?
         BE    MAIN0250                       YES
         CLC   RECORDS_KEYWORD,0(R2)          NUMBER OF RECORDS
         BE    MAIN0260                       YES
         CLC   SIZE_KEYWORD,0(R2)             SIZE OF LAST FILE
         BE    MAIN0270                       YES
         MVC   0(1,R5),0(R2)                  COPY SEPARATOR TO OUTPUT
         LA    R5,1(,R5)                      NEXT IN OUTPUT
         LA    R2,1(,R2)                      NEXT IN SOURCE
         BCT   R3,MAIN0120                    LOOP
         B     MAIN0280
MAIN0130 DS    0H
         MVC   0(L'SEPREQ_MEMBER,R5),SEPREQ_MEMBER
         LA    R5,L'SEPREQ_MEMBER(,R5)        NEXT IN OUTPUT
         LA    R2,L'MEMBER_KEYWORD(,R2)       NEXT IN SOURCE
         SH    R3,=Y(L'MEMBER_KEYWORD)
         BH    MAIN0120                       CHECK FOR ANOTHER KEYWORD
         B     MAIN0280                       ALL DATA CHECKED
MAIN0140 DS    0H
         MVC   0(L'SESS_DSN,R5),SESS_DSN      DATA SET NAME
         LA    R5,L'SESS_DSN(,R5)             NEXT IN OUTPUT
         LA    R2,L'DSN_KEYWORD(,R2)          NEXT IN SOURCE
         SH    R3,=Y(L'DSN_KEYWORD)
         BH    MAIN0120                       CHECK FOR ANOTHER KEYWORD
         B     MAIN0280                       ALL DATA CHECKED
MAIN0150 DS    0H
         L     R0,SEPREQ_SEQ                  SEQUENCE NUMBER
         CVD   R0,COMM_DWORD                  CONVERT TO DECIMAL
         MVC   COMM_INFO_02(L'EDIT_WORD_1),EDIT_WORD_1
         ED    COMM_INFO_02(L'EDIT_WORD_1),COMM_DWORD+5
         MVC   0(5,R5),COMM_INFO_02+1         COPY SEQUENCE NUMBER
         LA    R5,5(,R5)                      NEXT IN OUTPUT
         LA    R2,L'SEQ_KEYWORD(,R2)          NEXT IN SOURCE
         SH    R3,=Y(L'SEQ_KEYWORD)
         BH    MAIN0120                       CHECK FOR ANOTHER KEYWORD
         B     MAIN0280                       ALL DATA CHECKED
MAIN0160 DS    0H
         TIME  DEC,                                                    +
               COMM_INFO_03,                                           +
               DATETYPE=YYYYDDD,                                       +
               LINKAGE=SYSTEM,                                         +
               MF=(E,WORK_TIME)
         UNPK  COMM_INFO_02(9),COMM_INFO_03(5)
         TR    COMM_INFO_02(8),COMM_HEXCHAR   TRANSLATE IT
         MVC   0(2,R5),COMM_INFO_02+00        COPY HOURS (HH)
         MVI   2(R5),C':'                     INSERT COLON
         MVC   3(2,R5),COMM_INFO_02+02        COPY MINUTES (MM)
         MVI   5(R5),C':'                     INSERT COLON
         MVC   6(2,R5),COMM_INFO_02+04        COPY SECONDS (SS)
         CLC   TIME2_KEYWORD,0(R2)            TIME 2 (HH.MM.SS)?
         BE    MAIN0170
         MVI   8(R5),C':'                     INSERT COLON
         MVC   9(2,R5),COMM_INFO_02+06        COPY SECONDS (TH)
         LA    R5,11(,R5)                     NEXT IN OUTPUT
         LA    R2,L'TIME1_KEYWORD(,R2)        NEXT IN SOURCE
         SH    R3,=Y(L'TIME1_KEYWORD)
         BH    MAIN0120                       CHECK FOR ANOTHER KEYWORD
         B     MAIN0280                       ALL DATA CHECKED
MAIN0170 DS    0H
         LA    R5,8(,R5)                      NEXT IN OUTPUT
         LA    R2,L'TIME2_KEYWORD(,R2)        NEXT IN SOURCE
         SH    R3,=Y(L'TIME2_KEYWORD)
         BH    MAIN0120                       CHECK FOR ANOTHER KEYWORD
         B     MAIN0280                       ALL DATA CHECKED
* ------------------------------------------------------------------- *
*        Date (YYYYDDD)                                               *
* ------------------------------------------------------------------- *
MAIN0180 DS    0H
         ITRACE ID=DATE1
         TIME  DEC,                                                    +
               COMM_INFO_03,                                           +
               DATETYPE=YYYYDDD,                                       +
               LINKAGE=SYSTEM,                                         +
               MF=(E,WORK_TIME)
         UNPK  COMM_INFO_02(9),COMM_INFO_03+8(5)
         TR    COMM_INFO_02(8),COMM_HEXCHAR
         MVC   0(7,R5),COMM_INFO_02+1         COPY YEAR AND DATE
         LA    R5,7(,R5)                      NEXT IN OUTPUT
         LA    R2,L'DATE1_KEYWORD(,R2)        NEXT IN SOURCE
         SH    R3,=Y(L'DATE1_KEYWORD)
         BH    MAIN0120                       CHECK FOR ANOTHER KEYWORD
         B     MAIN0280                       ALL DATA CHECKED
* ------------------------------------------------------------------- *
*        Date (YYYY.DDD)                                              *
* ------------------------------------------------------------------- *
MAIN0190 DS    0H
         ITRACE ID=DATE2
         TIME  DEC,                                                    +
               COMM_INFO_03,                                           +
               DATETYPE=YYYYDDD,                                       +
               LINKAGE=SYSTEM,                                         +
               MF=(E,WORK_TIME)
         UNPK  COMM_INFO_02(9),COMM_INFO_03+8(5)
         TR    COMM_INFO_02(8),COMM_HEXCHAR
         MVC   0(4,R5),COMM_INFO_02+1         COPY YEAR
         MVI   4(R5),C'.'                     INSERT PERIOD
         MVC   5(3,R5),COMM_INFO_02+5         COPY DAY OF YEAR
         LA    R5,8(,R5)                      NEXT IN OUTPUT
         LA    R2,L'DATE2_KEYWORD(,R2)        NEXT IN SOURCE
         SH    R3,=Y(L'DATE2_KEYWORD)
         BH    MAIN0120                       CHECK FOR ANOTHER KEYWORD
         B     MAIN0280                       ALL DATA CHECKED
* ------------------------------------------------------------------- *
*        Date (MMDDYYYY)                                              *
* ------------------------------------------------------------------- *
MAIN0200 DS    0H
         ITRACE ID=DATE3
         TIME  DEC,                                                    +
               COMM_INFO_03,                                           +
               DATETYPE=MMDDYYYY,                                      +
               LINKAGE=SYSTEM,                                         +
               MF=(E,WORK_TIME)
         UNPK  COMM_INFO_02(9),COMM_INFO_03+8(5)
         TR    COMM_INFO_02(8),COMM_HEXCHAR
         MVC   0(8,R5),COMM_INFO_02           COPY DATE
         LA    R5,8(,R5)                      NEXT IN OUTPUT
         LA    R2,L'DATE3_KEYWORD(,R2)        NEXT IN SOURCE
         SH    R3,=Y(L'DATE3_KEYWORD)
         BH    MAIN0120                       CHECK FOR ANOTHER KEYWORD
         B     MAIN0280                       ALL DATA CHECKED
* ------------------------------------------------------------------- *
*        Date form 4 (MM/DD/YYYY)                                     *
* ------------------------------------------------------------------- *
MAIN0210 DS    0H
         ITRACE ID=DATE4
         TIME  DEC,                                                    +
               COMM_INFO_03,                                           +
               DATETYPE=MMDDYYYY,                                      +
               LINKAGE=SYSTEM,                                         +
               MF=(E,WORK_TIME)
         UNPK  COMM_INFO_02(9),COMM_INFO_03+8(5)
         TR    COMM_INFO_02(8),COMM_HEXCHAR
         MVC   00(2,R5),COMM_INFO_02+00       COPY MONTH
         MVI   02(R5),C'/'                    INSERT SLASH
         MVC   03(2,R5),COMM_INFO_02+02       COPY DAY OF MONTH
         MVI   05(R5),C'/'                    INSERT SLASH
         MVC   06(4,R5),COMM_INFO_02+04       COPY YEAR
         LA    R5,10(,R5)                     NEXT IN OUTPUT
         LA    R2,L'DATE4_KEYWORD(,R2)        NEXT IN SOURCE
         SH    R3,=Y(L'DATE4_KEYWORD)
         BH    MAIN0120                       CHECK FOR ANOTHER KEYWORD
         B     MAIN0280                       ALL DATA CHECKED
* ------------------------------------------------------------------- *
*        Date form 5 (DDMMYYYY)                                       *
* ------------------------------------------------------------------- *
MAIN0220 DS    0H
         ITRACE ID=DATE5
         TIME  DEC,                                                    +
               COMM_INFO_03,                                           +
               DATETYPE=DDMMYYYY,                                      +
               LINKAGE=SYSTEM,                                         +
               MF=(E,WORK_TIME)
         UNPK  COMM_INFO_02(9),COMM_INFO_03+8(5)
         TR    COMM_INFO_02(8),COMM_HEXCHAR
         MVC   0(8,R5),COMM_INFO_02           COPY DATE
         LA    R5,8(,R5)                      NEXT IN OUTPUT
         LA    R2,L'DATE5_KEYWORD(,R2)        NEXT IN SOURCE
         SH    R3,=Y(L'DATE5_KEYWORD)
         BH    MAIN0120                       CHECK FOR ANOTHER KEYWORD
         B     MAIN0280                       ALL DATA CHECKED
* ------------------------------------------------------------------- *
*        Date form 6 (DD/MM/YYYY)                                     *
* ------------------------------------------------------------------- *
MAIN0230 DS    0H
         ITRACE ID=DATE6
         TIME  DEC,                                                    +
               COMM_INFO_03,                                           +
               DATETYPE=DDMMYYYY,                                      +
               LINKAGE=SYSTEM,                                         +
               MF=(E,WORK_TIME)
         UNPK  COMM_INFO_02(9),COMM_INFO_03+8(5)
         TR    COMM_INFO_02(8),COMM_HEXCHAR
         MVC   00(2,R5),COMM_INFO_02+00       COPY DAY OF MONTH
         MVI   02(R5),C'/'                    INSERT SLASH
         MVC   03(2,R5),COMM_INFO_02+02       COPY MONTH
         MVI   05(R5),C'/'                    INSERT SLASH
         MVC   06(4,R5),COMM_INFO_02+04       COPY YEAR
         LA    R5,10(,R5)                     NEXT IN OUTPUT
         LA    R2,L'DATE6_KEYWORD(,R2)        NEXT IN SOURCE
         SH    R3,=Y(L'DATE6_KEYWORD)
         BH    MAIN0120                       CHECK FOR ANOTHER KEYWORD
         B     MAIN0280                       ALL DATA CHECKED
* ------------------------------------------------------------------- *
*        Date form 7 (YYYYMMDD)                                       *
* ------------------------------------------------------------------- *
MAIN0240 DS    0H
         ITRACE ID=DATE7
         TIME  DEC,                                                    +
               COMM_INFO_03,                                           +
               DATETYPE=YYYYMMDD,                                      +
               LINKAGE=SYSTEM,                                         +
               MF=(E,WORK_TIME)
         UNPK  COMM_INFO_02(9),COMM_INFO_03+8(5)
         TR    COMM_INFO_02(8),COMM_HEXCHAR
         MVC   0(8,R5),COMM_INFO_02           COPY DATE
         LA    R5,8(,R5)                      NEXT IN OUTPUT
         LA    R2,L'DATE7_KEYWORD(,R2)        NEXT IN SOURCE
         SH    R3,=Y(L'DATE7_KEYWORD)
         BH    MAIN0120                       CHECK FOR ANOTHER KEYWORD
         B     MAIN0280                       ALL DATA CHECKED
* ------------------------------------------------------------------- *
*        Date form 8 (YYYY/MM/DD)                                     *
* ------------------------------------------------------------------- *
MAIN0250 DS    0H
         ITRACE ID=DATE8
         TIME  DEC,                                                    +
               COMM_INFO_03,                                           +
               DATETYPE=YYYYMMDD,                                      +
               LINKAGE=SYSTEM,                                         +
               MF=(E,WORK_TIME)
         UNPK  COMM_INFO_02(9),COMM_INFO_03+8(5)
         TR    COMM_INFO_02(8),COMM_HEXCHAR
         MVC   00(4,R5),COMM_INFO_02+00       COPY YEAR
         MVI   04(R5),C'/'                    INSERT SLASH
         MVC   05(2,R5),COMM_INFO_02+04       COPY MONTH
         MVI   07(R5),C'/'                    INSERT SLASH
         MVC   08(4,R5),COMM_INFO_02+06       COPY DAY OF MONTH
         LA    R5,10(,R5)                     NEXT IN OUTPUT
         LA    R2,L'DATE8_KEYWORD(,R2)        NEXT IN SOURCE
         SH    R3,=Y(L'DATE8_KEYWORD)
         BH    MAIN0120                       CHECK FOR ANOTHER KEYWORD
         B     MAIN0280
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0260 DS    0H
         L     R1,SEPREQ_RECORDS              NUMBER OF RECORDS
         ITRACE ID=RECORDS,                                            +
               RDATA1=R1
         CVD   R1,COMM_DWORD
         MVC   COMM_INFO_02(L'EDIT_WORD_2),EDIT_WORD_2
         ED    COMM_INFO_02(L'EDIT_WORD_2),COMM_DWORD+4
         MVC   0(7,R5),COMM_INFO_02+1         COPY NUMBER OF RECORDS
         LA    R5,7(,R5)                      NEXT IN OUTPUT
         LA    R2,L'RECORDS_KEYWORD(,R2)      NEXT IN SOURCE
         SH    R3,=Y(L'RECORDS_KEYWORD)
         BH    MAIN0120                       CHECK FOR NEXT KEYWORD
         B     MAIN0280
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0270 DS    0H
         L     R1,SEPREQ_BYTES                NUMBER OF BYTES
         ITRACE ID=BYTES,                                              +
               RDATA1=R1
         CVD   R1,COMM_DWORD
         MVC   COMM_INFO_02(L'EDIT_WORD_2),EDIT_WORD_2
         ED    COMM_INFO_02(L'EDIT_WORD_2),COMM_DWORD+4
         MVC   0(7,R5),COMM_INFO_02+1         COPY NUMBER OF RECORDS
         LA    R5,7(,R5)                      NEXT IN OUTPUT
         LA    R2,L'SIZE_KEYWORD(,R2)         NEXT IN SOURCE
         SH    R3,=Y(L'SIZE_KEYWORD)
         BH    MAIN0120                       CHECK FOR NEXT KEYWORD
         B     MAIN0280
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0280 DS    0H
         PUT   (R6),COMM_INFO_01              OUTPUT SEPARATOR
MAIN0290 DS    0H
         LA    R8,72(,R8)                     NEXT SEPARATOR
         BCT   R7,MAIN0110                    LOOP
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         L     R13,4(,R13)                    RESTORE SAVE AREA ADDRESS
         LM    R14,R12,12(R13)                RESTORE REGISTERS
         SR    R15,R15                        SET RC
         BR    R14                            RETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXD_START        DC     Q(SEPDXD)
TIME_I           TIME   LINKAGE=SYSTEM,MF=L
TIME_L           EQU    *-TIME_I

EDIT_WORD_1      DC     X'F02020202120'
EDIT_WORD_2      DC     X'4020202020202120'

DATE1_KEYWORD    DC     C'&&DATE1'
DATE2_KEYWORD    DC     C'&&DATE2'
DATE3_KEYWORD    DC     C'&&DATE3'
DATE4_KEYWORD    DC     C'&&DATE4'
DATE5_KEYWORD    DC     C'&&DATE5'
DATE6_KEYWORD    DC     C'&&DATE6'
DATE7_KEYWORD    DC     C'&&DATE7'
DATE8_KEYWORD    DC     C'&&DATE8'
DSN_KEYWORD      DC     C'&&DSN'
MEMBER_KEYWORD   DC     C'&&MEMBER'
SEQ_KEYWORD      DC     C'&&SEQ'
TIME1_KEYWORD    DC     C'&&TIME1'
TIME2_KEYWORD    DC     C'&&TIME2'
RECORDS_KEYWORD  DC     C'&&RECORDS'
SIZE_KEYWORD     DC     C'&&SIZE'

                 LTORG

SEPEND           EQU    *
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
SEPDXD          DSECT
                COPY     DXDPREF

WORK_TIME       DS       0F,(TIME_L)X

SEPDXD_L        EQU      *-SEPDXD
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               BPXYSTAT DSECT=YES,LIST=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               SEPREQ   DSECT=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               IBMMAC   DCBD=PS
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY REGEQU
         END  OSSEP
