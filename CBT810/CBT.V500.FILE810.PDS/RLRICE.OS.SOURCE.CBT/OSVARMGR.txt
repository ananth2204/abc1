* ------------------------------------------------------------------- *
*                                                                     *
*  MODULE NAME: OSVARMGR                                              *
*                                                                     *
*  Define or delete SPF variables.                                    *
*                                                                     *
* ------------------------------------------------------------------- *
OSVARMGR CSECT
OSVARMGR AMODE 31
OSVARMGR RMODE ANY
         USING OSVARMGR,R15
         USING OSCOMM,R12
         USING SESSION,R11
         B     INIT0000
MODID    DC    CL8'OSVARMGR'
         DC    CL8'&SYSDATE'
         DC    CL8'&SYSTIME'
         DC    A(VARMGREND-OSVARMGR)
INIT0000 DS    0H
         STM   R14,R12,12(R13)                SAVE REGISTERS
         LR    R10,R15                        COPY BASE ADDRESS
         DROP  R15
         USING OSVARMGR,R10
         L     R15,COMM_DXD                   DXD AREA BASE ADDRESS
         A     R15,DXD_START                  PLUS OFFSET
         ST    R13,4(,R15)
         ST    R15,8(,R13)
         LR    R13,R15                        COPY DXD AREA ADDRESS
         USING DXDVMGR,R13                    DEFINE WORK AREA BASE
         MVC   DXD_CSECT,MODID
* ------------------------------------------------------------------- *
         LR    R9,R1
         USING VARREQ,R9                      DEFINE BASE
         ITRACE ID=ENTRY,                                              +
               RDATA1=R9,                                              +
               DATA2=VAR_COMMAND
         L     R8,COMM_OSSPFD                 SPF DATA
         USING OSSPFD,R8                      DEFINE BASE
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
VAR0010  DS    0H
         CLI   VAR_COMMAND,$VAR_DEFINE        DEFINE VARIABLES?
         BE    VAR0100                        YES
         CLI   VAR_COMMAND,$VAR_DELETE        DELETE VARIABLE?
         BE    VAR0200                        YES
         CLI   VAR_COMMAND,$VAR_GET           VGET VARIABLE?
         BE    VAR0300                        YES
         SR    R5,R5
         ICM   R5,1,VAR_COMMAND
         ITRACE ID=BADCMD
         ABEND 1,DUMP,,USER                   BAD COMMAND
* ------------------------------------------------------------------- *
*        PROCESS DEFINE COMMANDS                                      *
* ------------------------------------------------------------------- *
VAR0100  DS    0H
         ITRACE ID=DEFINE,                    DEFINE VARIABLES         +
               DATA1=VAR_TABLE,                                        +
               DATA2=VAR_AREA
         L     R6,VAR_TABLE                   FIRST VARIABLE
VAR0110  DS    0H
         USING VAR_DSECT,R6                   DEFINE BASE
         MVI   DXD_FLAGS,0
         TM    VAR_FLAG,$VDEFINE              VARIABLE ALREADY DEFINED?
         BO    VAR0120                        YES
         TM    VAR_FLAG,$VNEVER               NEVER DEFINE?
         BO    VAR0120                        YES
         LA    R1,VDEFINE                     COMMAND'S ADDRESS
         ST    R1,DXD_SPF1                    SET ADDRESS IN PARM LIST
         LA    R1,VAR_NAME                    VARIABLE'S NAME
         ST    R1,DXD_SPF2                    SET NAME'S ADDRESS
         SR    R3,R3                          CLEAR REGISTER
         ICM   R3,3,VAR_DISP                  DISPLACEMENT TO DATA
         A     R3,VAR_AREA                    PLUS BASE ADDRESS
         ST    R3,DXD_SPF3                    SET ADDRESS IN PARM LIST
         LA    R1,VAR_FORMAT                  DATA FORMAT
         ST    R1,DXD_SPF4                    SET ADDRESS IN PARM LIST
         LA    R1,VAR_LENGTH                  VARIABLE LENGTH
         ST    R1,DXD_SPF5                    SET ADDRESS IN PARM LIST
         OI    DXD_SPF5,X'80'                 SET END-OF-LIST
         ITRACE ID=VDEFINE,                   DEFINING A VARIABLE      +
               DATA1=VAR_NAME,                .. VARIABLE'S NAME       +
               DATA2=DXD_SPF3                 .. ADDRESS OF DATA
         LA    R1,DXD_SPFP                    SPF PARM LIST ADDRESS
         L     R15,COMM_ISPLINK               SPFLINK ENTRY POINT
         BALR  R14,R15                        LINK TO SPF
         ITRACE ID=VDEF_RC,                                            +
               RDATA1=R15
         LTR   R15,R15                        VDEFINE SUCCESSFUL?
         BNZ   ERR0010                        NO
         OI    VAR_FLAG,$VDEFINE              VARIABLE HAS BEEN DEFINED
         TM    VAR_FLAG,$VGET                 VGET NOW?
         BNO   VAR0120                        NO
         MVC   DXD_NAMELIST,COMM_BLANKS
         MVC   DXD_NAMELIST+1(8),VAR_NAME
         BAL   R7,VAR0400
VAR0120  DS    0H
         LA    R6,VAR_DSECT_L(,R6)            NEXT VARIABLE
         CLI   0(R6),X'FF'                    END OF TABLE?
         BNE   VAR0110                        NO
         MVC   SPF_RFIND,PASSTHRU             SET RFIND OPTION
         B     EXIT0000                       ALL PROCESSED
* ------------------------------------------------------------------- *
*        PROCESS VARIABLE DELETION                                    *
* ------------------------------------------------------------------- *
VAR0200  DS    0H
         ITRACE ID=DELETE                     DELETING ALL VARIABLES
         MVC   DXD_POOL,SHARED
         LA    R0,VPUT                        SPF COMMAND
         ST    R0,DXD_SPF1                    SET ADDRESS
         LA    R0,DXD_NAMELIST                NAME LIST
         ST    R0,DXD_SPF2                    SET ADDRESS
         LA    R0,DXD_POOL
         ST    R0,DXD_SPF3
         OI    DXD_SPF3,X'80'
         L     R6,VAR_TABLE                   START WITH FIRST VARIABLE
VAR0210  DS    0H
         CLI   0(R6),X'FF'                    END OF TABLE?
         BE    EXIT0000                       YES
         TM    VAR_FLAG,$VDEFINE              VARIABLE DEFINED?
         BNO   VAR0250                        NO
         TM    VAR_FLAG,$VPUT                 VPUT BEFORE DELETING?
         BNO   VAR0240                        NO
         MVC   DXD_NAMELIST,COMM_BLANKS
         MVI   DXD_NAMELIST+0,C'('
         MVC   DXD_NAMELIST+1(8),VAR_NAME     COPY VARIABLE NAME
         LA    R1,DXD_NAMELIST+10
         LA    R2,8
VAR0220  DS    0H
         CLI   0(R1),C' '                     BLANK?
         BNE   VAR0230                        NO
         BCTR  R1,0                           BACK UP 1
         BCT   R2,VAR0220                     LOOP
VAR0230  DS    0H
         MVI   1(R1),C')'                     INSERT CLOSING PAREN
         LA    R0,VPUT                        SPF COMMAND
         ST    R0,DXD_SPF1                    SET ADDRESS
         LA    R0,DXD_NAMELIST                NAME LIST
         ST    R0,DXD_SPF2                    SET ADDRESS
         LA    R0,DXD_POOL
         ST    R0,DXD_SPF3
         OI    DXD_SPF3,X'80'
         ITRACE ID=VPUT,                                               +
               DATA1=DXD_NAMELIST+1,                                   +
               DATA2=DXD_POOL
         LA    R1,DXD_SPF1                    SPF PARM LIST
         L     R15,COMM_ISPLINK               SPF ENTRY POINT
         BALR  R14,R15                        VPUT THE VARIABLE
         ITRACE ID=VPUT_RC,                   TRACE VPUT RC            +
               RDATA1=R15
         LTR   R15,R15                        VPUT OK?
         BNZ   ERR0040                        NO
VAR0240  DS    0H
         LA    R0,VDELETE                     COMMAND'S ADDRESS
         ST    R0,DXD_SPF1                    SET ADDRESS IN PARM LIST
         ST    R6,DXD_SPF2                    SET VARIABLE NAME ADDRESS
         OI    DXD_SPF2,X'80'                 FLAG END-OF-LIST
         ITRACE ID=VDELETE,                   DELETING A VARIABLE      +
               DATA1=VAR_NAME                 .. VARIABLE NAME
         LA    R1,DXD_SPFP                    PARM LIST'S ADDRESS
         L     R15,COMM_ISPLINK               SPFLINK ENTRY POINT
         BALR  R14,R15                        LINK TO SPF
         LTR   R15,R15                        VDELETE SUCCESSFUL?
         BNZ   ERR0020                        NO
VAR0250  DS    0H
         LA    R6,VAR_DSECT_L(,R6)            NEXT VARIABLE
         B     VAR0210                        PROCESS ALL VARIABLES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
VAR0300  DS    0H
         MVC   DXD_NAMELIST,COMM_BLANKS
         MVC   DXD_NAMELIST+1(8),VAR_VARIABLE
         BAL   R7,VAR0400                     DO VGET
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
VAR0400  DS    0H
         MVI   DXD_NAMELIST+0,C'('
         LA    R1,DXD_NAMELIST+10
         LA    R2,8
VAR0410  DS    0H
         CLI   0(R1),C' '
         BNE   VAR0420
         BCTR  R1,0
         BCT   R2,VAR0410
VAR0420  DS    0H
         MVI   1(R1),C')'
         LA    R0,VGET                        VGET COMMAND
         ST    R0,DXD_SPF1                    SET ADDRESS
         LA    R0,DXD_NAMELIST          < --  DON'T LIKE THE CLOSE NAME
         ST    R0,DXD_SPF2                    SET NAME
         LA    R0,DXD_POOL                    SHARED OF PROFILE POOL
         ST    R0,DXD_SPF3
         OI    DXD_SPF3,X'80'                 FLAG END-OF-LIST
         TM    VAR_FLAG,$VPUT                 THIS VARIABLE SAVED?
         BO    VAR0430                        YES
         ITRACE ID=SHARED
         MVC   DXD_POOL,SHARED                FROM SHARED POOL
         B     VAR0440
VAR0430  DS    0H
         ITRACE ID=PROFILE
         MVC   DXD_POOL,PROFILE
VAR0440  DS    0H
         ST    R0,DXD_SPF3                    SET ADDRESS
         ITRACE ID=VGET,                                               +
               DATA1=DXD_NAMELIST+1,                                   +
               DATA2=DXD_POOL
         LA    R1,DXD_SPF1
         L     R15,COMM_ISPLINK               SPF ENTRY POINT
         BALR  R14,R15
         ITRACE ID=VGET_RC,                                            +
               RDATA1=R15,                                             +
               DATA2=0(R3)
         LTR   R15,R15                        SUCCESSFUL?
         BZR   R7                             YES
         TM    DXD_FLAGS,$DXD_VPUT            VPUT ISSUED?
         BO    ERR0030
         LA    R0,VPUT
         ST    R0,DXD_SPF1
         LA    R1,DXD_SPF1                    CHANGE VGET TO VPUT
         ITRACE ID=VPUT,                                               +
               DATA1=DXD_NAMELIST+0,                                   +
               DATA2=DXD_POOL
         L     R15,COMM_ISPLINK               SPF ENTRY POINT
         BALR  R14,R15
         ITRACE ID=VPUT_RC,                                            +
               RDATA1=R15,                                             +
               DATA2=0(R3)
         OI    DXD_FLAGS,$DXD_VPUT
         B     VAR0420                        TRY AGAIN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0010  DS    0H
         ITRACE ID=VDEFFAIL,                                           +
               RDATA1=R15
         DC    H'12'
ERR0020  DS    0H
         ITRACE ID=VDELFAIL,                                           +
               RDATA1=R15
         DC    H'13'
ERR0030  DS    0H
         ITRACE ID=VGETFAIL,                                           +
               RDATA1=R15
         DC    H'14'
ERR0040  DS    0H
         ITRACE ID=VPUTFAIL,                                           +
               RDATA1=R15
         DC    H'15'
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=EXIT
         L     R13,4(,R13)                    RESTORE SAVE AREA ADDRESS
         LM    R14,R12,12(R13)                RESTORE REGISTERS
         BR    R14                            RETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXD_START DC    Q(DXDVMGR)
VARNAME_I DC    F'1'
          DC    F'0'
          DC    CL8' '
VARNAME_L EQU   *-VARNAME_I
PASSTHRU  DC    CL8'PASSTHRU'
SHARED    DC    CL8'SHARED'
PROFILE   DC    CL8'PROFILE'
VDEFINE   DC    CL8'VDEFINE'
VDELETE   DC    CL8'VDELETE'
VGET      DC    CL8'VGET'
VPUT      DC    CL8'VPUT'
          LTORG
VARMGREND EQU   *
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDVMGR      DSECT
             COPY  DXDPREF
DXD_SPFP     DS    0A                    ISPF PARM LIST
DXD_SPF1     DS    A                     PARM 1 ADDRESS
DXD_SPF2     DS    A                     PARM 2 ADDRESS
DXD_SPF3     DS    A                     PARM 3 ADDRESS
DXD_SPF4     DS    A                     PARM 4 ADDRESS
DXD_SPF5     DS    A                     PARM 5 ADDRESS

DXD_FLAGS    DS    X
$DXD_VPUT    EQU   X'80'

DXD_NAMELIST DS    CL12

DXD_POOL     DS    CL8
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         BPXYSTAT DSECT=YES,LIST=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY     VARDSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         VARREQ   DSECT=YES
         OSSPFD   TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY     REGEQU
         END      OSVARMGR
