* ------------------------------------------------------------------- *
*                                                                     *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
OSMAIN   CSECT
OSMAIN   AMODE 31
OSMAIN   RMODE ANY
         USING OSMAIN,R15                       DEFINE BASE
         B     INIT0000                         BRANCH
MODID    DC    CL8'OSMAIN'                      MODULE NAME
         DC    CL8'&SYSDATE'                    DATE OF ASSEMBLY
         DC    CL6'&SYSTIME'                    TIME OF ASSEMBLY
         DC    A(MAINEND-OSMAIN)
INIT0000 DS    0H
         STM   R14,R12,12(R13)                  SAVE ENTRY REGISTERS
         LR    R10,R15                          COPY BASE
         LA    R9,2048(,R10)                    INITIALIZE 2ND BASE
         LA    R9,2048(,R9)
         DROP  R15
         USING OSMAIN,R10,R9                    DEFINE PROGRAM BASE
         SR    R12,R12                          COMMON AREA NOT LOADED
         SR    R11,R11                          NO SESSION YET
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         LTR   R8,R1                            COPY PARM ADDRESS
* ------------------------------------------------------------------- *
*        Acquire storage for work areas                               *
* ------------------------------------------------------------------- *
INIT0010 DS    0H
         L     R3,DXDSIZE                       WORK AREA SIZE
         GETMAIN RU,                            GETMAIN WORK AREA      +
               LV=(R3),                                                +
               LOC=ANY
         LR    R4,R1                            COPY STORAGE ADDRESS
         LR    R5,R1                            COPY STORAGE ADDRESS
         LR    R2,R1                            COPY WORK AREA ADDRESS
         SR    R1,R1                            CLEAR REGISTER
         MVCL  R2,R0                            CLEAR WORK AREA
         A     R5,DXDSTART                      PLUS DISPLACEMENT
         ST    R13,4(,R5)                       CHAINn AREA TO OURS
         ST    R5,8(,R13)                       chain AREA TO CALLER'S
         LR    R13,R5                           SET SAVE AREA ADDRESS
         USING DXDMAIN,R13                      DEFINE WORK AREA BASE
         MVC   DXD_CSECT,MODID                  IDENTIFY AREA OWNER
* ------------------------------------------------------------------- *
*        Determine if On Screen is already active                     *
* ------------------------------------------------------------------- *
         MVC   DXD_ENQ(ENQ_L),ENQ_I             INITIALIZE ENQ
         ENQ   (MODID,                          ENQ 'OSMAIN'           +
               MODID,                                                  +
               E,                                                      +
               8,                                                      +
               STEP),                                                  +
               RET=USE,                                                +
               MF=(E,DXD_ENQ)
         LTR   R15,R15                          SUCCESSFUL?
         BNZ   ERR3000                          NO
* ------------------------------------------------------------------- *
*        LOAD COMMON DATA MODULE                                      *
* ------------------------------------------------------------------- *
         LOAD  EP=OSCOMM                        LOAD COMMON DATA MODULE
         LR    R12,R0                           COPY TO PERMANENT BASE
         LA    R12,0(R12)                       TURN OFF AMODE BIT(S)
         USING OSCOMM,R12                       DEFINE COMMON AREA BASE
         ST    R5,COMM_DXD                      SAVE WORK AREA BASE
         MVC   COMM_DXD_LENGTH,DXDSIZE
         MVC   COMM_MSG_CSECT,MODID
* ------------------------------------------------------------------- *
*        Initialize addresses of statically linked modules            *
* ------------------------------------------------------------------- *
         LA    R0,VCONI                         V CONSTANTS
         LA    R1,VCONL                         V CONSTANTS LENGTH
         LA    R14,COMM_VCONS                   AREA IN OSCOMM
         LR    R15,R1                           COPY LENGTH
         MVCL  R14,R0                           COPY V CONSTANTS
* ------------------------------------------------------------------- *
*        ACQUIRE STORAGE FOR TRACE TABLE                              *
* ------------------------------------------------------------------- *
         L     R3,TRACE_TABLE_SIZE              TRACE TABLE SIZE
         GETMAIN RU,                            GETMAIN TRACE TABLE    +
               LV=(R3),                                                +
               LOC=BELOW
         ST    R1,COMM_TRACE                    SAVE ADDRESS
         SR    R0,R0                            ASSUME ON BOUNDARY
         TM    COMM_TRACE,X'07'                 ALREADY ON BOUNDARY?
         BNO   INIT0020                         YES
         SRL   R1,5                             SHIFT OUT LOW BITS ..
         SLL   R1,5                             .. TO ROUND
         LA    R1,32(,R1)                       PLUS 32
         LA    R0,TR_ENTRY_L                    1 LESS
INIT0020 DS    0H
         ST    R1,COMM_TRACE_1ST                FIRST TRACE ENTRY
         ST    R1,COMM_TRACE_CURR               CURRENT TRACE ENTRY
         A     R1,TRACE_TABLE_SIZE              LAST ENTRY IN TABLE
         SH    R1,=Y(TR_ENTRY_L)                MINUS 1 ENTRY
         SR    R1,R0                            MINUS BOUNDARY
         ST    R1,COMM_TRACE_LAST               LAST TRACE ENTRY
         ITRACE ID=OSCOMM,                                             +
               RDATA1=R12,                      .. COMMON AREA         +
               DATA2=(COMM_DXD,4)               .. DXD AREA ADDRESS
* ------------------------------------------------------------------- *
*        Set trap entry point                                         *
* ------------------------------------------------------------------- *
         MVC   COMM_TRAP,COMM_V_OSTRAP
* ------------------------------------------------------------------- *
*        Establish ESTAE exit                                         *
* ------------------------------------------------------------------- *
         MVC   DXD_ESTAE(ESTAE_L),ESTAE_I
         L     R2,COMM_V_OSESTAE
         AGO   .NOSTAE
         ESTAE (R2),               SET ESTAE                           +
               CT,                 .. CREATING NEW ESTAE               +
               PARAM=(R12),        .. ESTAE WILL NEED COMM AREA ADDRESS+
               PURGE=NONE,         .. TRY NOT TO PURGE I/O REQUESTS    +
               ASYNCH=YES,         .. ALLOW ASYNCHRONOUS EVENTS        +
               TERM=NO,            .. DO NOT INTERCEPT X22 ABEND       +
               MF=(E,DXD_ESTAE)    ..
.NOSTAE  ANOP
* ------------------------------------------------------------------- *
*        Load 24-bit stuff                                            *
* ------------------------------------------------------------------- *
         ITRACE ID=LOAD_24
         MVI   LOADREQ_FUNC,$LOADREQ_LOAD
         MVC   LOADREQ_MODULE,MODULE_OSFORMAT
         LA    R1,LOADREQ
         L     R15,COMM_V_OSLOAD
         BALR  R14,R15
         MVC   DXD_OSFORMAT,LOADREQ_EP          SAVE OSFORMAT EP
* ------------------------------------------------------------------- *
*        Initialize RECALL                                            *
* ------------------------------------------------------------------- *
         MVI   RCALLREQ_FUNC,$RCALLREQ_INIT
         LA    R1,RCALLREQ
         L     R15,COMM_V_OSRECALL
         BALR  R14,R15
* ------------------------------------------------------------------- *
*        Load data space manager                                      *
* ------------------------------------------------------------------- *
         LOAD  EP=OSDSPACE
         ST    R0,COMM_OSDSPACE                 SPACE MANAGER
* ------------------------------------------------------------------- *
*        LOAD SPF interface                                           *
* ------------------------------------------------------------------- *
INIT0070 DS    0H
         ITRACE ID=LOADSPF
         MVC   LOADREQ_MODULE,MODULE_ISPLINK
         LA    R1,LOADREQ
         L     R15,COMM_V_OSLOAD
         BALR  R14,R15
         MVC   COMM_ISPLINK,LOADREQ_EP
* ------------------------------------------------------------------- *
*        LOAD SPF variable definitions                                *
* ------------------------------------------------------------------- *
INIT0080 DS    0H
         ITRACE ID=LOADSPFV
         MVC   LOADREQ_MODULE,MODULE_OSSPFV
         LA    R1,LOADREQ
         L     R15,COMM_V_OSLOAD
         BALR  R14,R15
         MVC   COMM_OSSPFV,LOADREQ_EP
         NI    COMM_OSSPFV,X'7F'                TURN OFF AMODE BIT
* ------------------------------------------------------------------- *
*        LOAD SPF data                                                *
* ------------------------------------------------------------------- *
         ITRACE ID=LOADSPFD
         MVC   LOADREQ_MODULE,MODULE_OSSPFD
         LA    R1,LOADREQ
         L     R15,COMM_V_OSLOAD
         BALR  R14,R15
         MVC   COMM_OSSPFD,LOADREQ_EP
         NI    COMM_OSSPFD,X'7F'
* ------------------------------------------------------------------- *
*        Define SPF variables                                         *
* ------------------------------------------------------------------- *
         ITRACE ID=VDEFINE
         MVI   VAR_COMMAND,$VAR_DEFINE
         MVC   VAR_TABLE,COMM_OSSPFV
         MVC   VAR_AREA,COMM_OSSPFD
         LA    R1,VARREQ                        INDICATE 'DEFINE'
         L     R15,COMM_V_OSVARMGR              SPF VARIABLE MANAGER
         BALR  R14,R15                          DEFINE SPF VARIABLES
* ------------------------------------------------------------------- *
*        Read the user's profile                                      *
* ------------------------------------------------------------------- *
         ITRACE ID=READPROF
         LOAD  EP=OSPROF
         LR    R15,R0
         MVI   PROF_FUNC,$PROF_READ
         LA    R1,PROFREQ
         BALR  R14,R15                          LINK TO OSPROF
         DELETE EP=OSPROF
         ITRACE ID=PROF_RC,                                            +
               DATA1=(PROF_RC,1)
         CLI   PROF_RC,$PROF_OK                 SUCCESSFUL?
         BNE   ERR0020                          NO
* ------------------------------------------------------------------- *
*        Verify message table is present                              *
* ------------------------------------------------------------------- *
         OC    COMM_MESSAGE_TABLE,COMM_MESSAGE_TABLE
         BNZ   INIT0090
         MVC   COMM_NATIONAL_LANGUAGE,LANGUAGE_US
         MVC   COMM_LANGUAGE_MODULE,MODULE_OSMSGUS
         LOAD  EPLOC=COMM_LANGUAGE_MODULE
         ST    R0,COMM_MESSAGE_TABLE
* ------------------------------------------------------------------- *
*        Determine if OSOPTS DD is present                            *
* ------------------------------------------------------------------- *
INIT0090 DS    0H
         ITRACE ID=SCANTIOT
         USING PSA,R0                           DEFINE BASE
         L     R1,PSATNEW                       OUR TCB ADDRESS
         USING TCB,R1                           DEFINE TCB BASE
         L     R2,TCBTIO                        TIOT ADDRESS
         USING TIOT1,R2                         DEFINE TIOT BASE
         LA    R3,TIOENTRY                      FIRST TIOT ENTRY
         DROP  R2
         USING TIOENTRY,R3                      DEFINE TIOT ENTRY BASE
INIT0100 DS    0H
         SR    R4,R4                            CLEAR FOR LENGTHS
         ICM   R4,1,TIOELNGH                    LENGTH OF THIS ENTRY
         BZ    INIT0130                         END OF TABLE
         CLC   TIOEDDNM,OSOPTS_DD               OSOPTS DD PRESENT?
         BE    INIT0120                         YES
INIT0110 DS    0H
         AR    R3,R4                            NEXT ENTRY
         B     INIT0100                         LOOP
INIT0120 DS    0H
         OI    DXD_FLAGS,$OSOPTS_DD             SET INDICATOR
         DROP  R3
* ------------------------------------------------------------------- *
*        Determine user's user id                                     *
* ------------------------------------------------------------------- *
INIT0130 DS    0H
         USING PSA,R0                           DEFINE BASE
         L     R1,PSAAOLD                       OUR ASCB'S ADDRESS
         USING ASCB,R1                          DEFINE BASE
         L     R14,ASCBASXB                     ASXB ADDRESS
         USING ASXB,R14                         DEFINE ASCB BASE
         L     R15,ASXBSENV                     ACEE'S ADDRESS
         USING ACEE,R15                         DEFINE ACEE BASE
         SR    R1,R1                            CLEAR REGISTER
         IC    R1,ACEEUSRL                      USER ID LENGTH
         STH   R1,COMM_USER_LENGTH              SET USER ID LENGTH
         BCTR  R1,0                             MINUS 1
         MVC   COMM_USER,COMM_BLANKS            INITIALIZE USERID
         EX    R1,UIDMVC                        COPY USER ID
         ITRACE ID=USER,                                               +
               DATA1=COMM_USER,                                        +
               DATA2=COMM_USER_LENGTH
* ------------------------------------------------------------------- *
*        Determine invoking command's address and length              *
* ------------------------------------------------------------------- *
         LTR   R8,R8                            PARM PRESENT?
         BZ    INIT0170                         NO
         ITRACE ID=CMD_ADDR,                                           +
               RDATA1=R8,                                              +
               DATA2=(0(R8),4)
         ICM   R14,15,0(R8)                     PARM PRESENT?
         BZ    INIT0170                         NO
         LH    R15,0(R14)                       COMMAND'S LENGTH
         SH    R15,H4                           MINUS PREFIX LENGTH
         LA    R1,4(,R14)
INIT0140 DS    0H
         CLI   0(R1),C' '                       BLANK?
         BE    INIT0150                         YES
         LA    R1,1(,R1)                        NEXT
         BCT   R15,INIT0140
         B     INIT0160
INIT0150 DS    0H
         CLI   0(R1),C' '                       BLANK
         BNE   INIT0160                         NO
         LA    R1,1(,R1)                        NEXT
         BCT   R15,INIT0150
INIT0160 DS    0H
         STH   R15,COMM_COMMAND_LENGTH
         ST    R1,COMM_COMMAND
INIT0170 DS    0H
         ITRACE ID=INIT_CMD,                                           +
               DATA1=(COMM_COMMAND,4),                                 +
               DATA2=(COMM_COMMAND_LENGTH,4)
* ------------------------------------------------------------------- *
*                                                                     *
*        Create 1st SESSION                                           *
*                                                                     *
* ------------------------------------------------------------------- *
         GETMAIN RU,                            GETMAIN SESSION BLOCK  +
               LV=SESSION_L,                                           +
               LOC=ANY
         LR    R11,R1                           SAVE ADDRESS
         USING SESSION,R11
         ST    R11,COMM_ACTIVE_SESSION          SET ACTIVE SESSION ADDR
         LR    R0,R1                            COPY ADDRESS
         LA    R1,SESSION_L                     SESSION BLOCK LENGTH
         SR    R14,R14                          ZERO R14
         SR    R15,R15                          ZERO R15
         MVCL  R0,R14                           INITIALIZE SESSION
         ITRACE ID=1ST_SESS,                                           +
               RDATA1=R11,                                             +
               DATA2=(SESS_KEYWORD_FLAGS_2,1)
         MVC   SESS_EYE,SESSION_ID
         MVC   SESS_DEFAULT_FORMAT,COMM_BLANKS
         MVC   SESS_FORMAT,COMM_BLANKS
         MVC   SESS_FUNCTION_MODULE,COMM_BLANKS
         MVC   SESS_DATA_LOADER,COMM_BLANKS
         MVC   SESS_EXIT_MODULE,COMM_BLANKS
         MVC   SESS_DD,COMM_BLANKS
         MVC   SESS_DSN,COMM_BLANKS
         MVC   SESS_PATH,COMM_BLANKS
         MVC   SESS_REAL_PATH,COMM_BLANKS
         MVC   SESS_MEMBER,COMM_BLANKS
         MVC   SESS_VOLSER,COMM_BLANKS
         MVC   SESS_FROM_KEY,COMM_BLANKS
         MVC   SESS_TO_KEY,COMM_BLANKS
         MVC   SESS_SEARCH_ARG,COMM_BLANKS
         USING DS1FMTID,SESS_DSCB               DEFINE DSCB BASE
* ------------------------------------------------------------------- *
*        GETMAIN I/O area                                             *
* ------------------------------------------------------------------- *
         L     R0,COMM_IO_SIZE                  I/O AREA SIZE
         GETMAIN RU,                                                   +
               LV=(0),                                                 +
               LOC=BELOW
         ST    R1,SESS_IO_AREA                  SAVE ADDRESS
         ITRACE ID=IO_AREA,                                            +
               RDATA1=R1
* ------------------------------------------------------------------- *
*        If On Screen was started with no parms, prompt the user      *
* ------------------------------------------------------------------- *
         OC    COMM_COMMAND_LENGTH,COMM_COMMAND_LENGTH
         BNZ   INIT0210
INIT0190 DS    0H
         OI    DXD_FLAGS,$PROMPT
INIT0200 DS    0H
         ITRACE ID=PROMPT
         NI    SESS_STATUS,255-$STATUS_NO_MEMBER
         L     R15,COMM_V_OSPROMPT
         BALR  R14,R15
         ITRACE ID=PROMPTRC,                                           +
               DATA1=SESS_RC
         OC    SESS_RC,SESS_RC
         BNZ   INIT0220
         L     R2,COMM_OSSPFD
         USING OSSPFD,R2
         CLC   SPF_ZCMD,COMM_BLANKS
         BE    INIT0230
* ------------------------------------------------------------------- *
*        Process operands on invoking command                         *
* ------------------------------------------------------------------- *
INIT0210 DS    0H
         ITRACE ID=PARMS
         L     R2,COMM_OSSPFD
         LA    R1,PARMSREQ
         L     R15,COMM_V_OSPARMS               OSPARMS ENTRY POINT
         BALR  R14,R15                          PROCESS PARMS
         LTR   R15,R15                          PARMS OK?
         BZ    INIT0230                         YES
         TM    DXD_FLAGS,$PROMPT                FOLLOWING A PROMPT?
         BNO   ERR0030                          NO
         MVC   SPF_MSG_1,COMM_MSG_1
         MVC   SPF_MSG_2,COMM_MSG_2
         MVC   SPF_MSG_3,COMM_MSG_3
         MVC   SPF_MSG_4,COMM_MSG_4
         MVC   SPF_MSG_5,COMM_MSG_5
         B     INIT0200
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
INIT0220 DS    0H
         ITRACE ID=BAIL_OUT
         NI    DXD_FLAGS,255-$PROMPT
         B     MAIN0540
* ------------------------------------------------------------------- *
*        Determine if required operands are present                   *
* ------------------------------------------------------------------- *
INIT0230 DS    0H
         ITRACE ID=INIT0230,                                           +
               DATA1=(SESS_KEYWORD_FLAGS,1),                           +
               DATA2=(SESS_KEYWORD_FLAGS_2,1)
         TM    SESS_KEYWORD_FLAGS_2,$KEYWORD_ACT
         BO    MAIN0400                         YES
         TM    SESS_KEYWORD_FLAGS_2,$KEYWORD_VTOC
         BO    MAIN0180                         YES
         TM    SESS_KEYWORD_FLAGS_2,$KEYWORD_STOR
         BO    MAIN0250                         YES
         TM    SESS_KEYWORD_FLAGS_2,$KEYWORD_RAWVTOC
         BO    MAIN0180                         YES
         TM    SESS_KEYWORD_FLAGS,$KEYWORD_PATH UNIX PATH?
         BO    MAIN0190                         YES
         TM    SESS_KEYWORD_FLAGS,$KEYWORD_DSN  DATASET NAME?
         BO    MAIN0000                         YES
         TM    SESS_KEYWORD_FLAGS,$KEYWORD_DD   DD NAME?
         BO    MAIN0020                         YES
         B     ERR0040                          OTHERWISE...



* ------------------------------------------------------------------- *
*                                                                     *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0000 DS    0H
         ITRACE ID=MAIN0000,                                           +
               DATA1=(SESS_DATA_TYPE,1),                               +
               DATA2=(SESS_DATASET_FLAGS,1)
         CLI   SESS_DATA_TYPE,$DATA_TYPE_OTHER  'OTHER'?
         BE    MAIN0400                         YES
         CLI   SESS_DATA_TYPE,0                 DATA TYPE KNOWN?
         BE    MAIN0010                         NO
         TM    SESS_DATASET_FLAGS,$SESS_DATASET_NO_DCB
         BO    MAIN0190                         YES
* ------------------------------------------------------------------- *
*                                                                     *
*        Allocate the data set if necessary                           *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0010 DS    0H
         ITRACE ID=ALOC_DSN
         MVI   DAIR_FUNC,$DAIR_ALLOC            SET DAIR FUNCTION
         MVI   DAIR_OPTS,X'00'                  CLEAR THE OPTIONS
         MVC   DAIR_DSN,SESS_DSN                COPY DATA SET NAME
         MVC   DAIR_MEMBER,SESS_MEMBER          COPY MEMBER NAME
         MVC   DAIR_VOLSER,SESS_VOLSER          COPY VOLSER
         MVC   DAIR_UNIT,COMM_BLANKS            SET UNIT TO BLANKS
         MVI   DAIR_DISP,$DAIR_DISP_SHR         DISP=SHR
         LA    R1,DAIRREQ                       DAIR PARMS
         L     R15,COMM_V_OSDAIR                OSDAIR ENTRY POINT
         BALR  R14,R15                          CALL OSDAIR
         OC    DAIR_R15,DAIR_R15                SUCCESSFUL?
         BNZ   ERR0050                          NO
         OI    SESS_DATASET_FLAGS,$SESS_DATASET_ALLOCATED
         MVC   SESS_DD,DAIR_DD                  COPY RETURNED DD NAME
* ------------------------------------------------------------------- *
*                                                                     *
*        Either the user specified a DD name or the data set has      *
*        been dynamically allocated.                                  *
*                                                                     *
*        Locate the UCB                                               *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0020 DS    0H
         CLI   SESS_DATA_TYPE,0                 DATA TYPE KNOWN?
         BE    MAIN0030                         NO
         TM    SESS_DATASET_FLAGS,$SESS_DATASET_NO_DCB
         BO    MAIN0190                         NO DCB/ACB, UCB, DSORG
MAIN0030 DS    0H
         ITRACE ID=FIND_DD,                                            +
               DATA1=SESS_DD
         USING PSA,R0                           DEFINE BASE
         L     R1,PSATNEW                       TCB ADDRESS FROM PSA
         USING TCB,R1                           DEFINE TCB BASE
         L     R2,TCBTIO                        TIOT ADDRESS
         USING TIOT1,R2                         DEFINE TIOT BASE
         LA    R3,TIOENTRY                      FIRST TIOT ENTRY
         DROP  R2
         USING TIOENTRY,R3                      DEFINE TIOT ENTRY BASE
MAIN0040 DS    0H
         SR    R4,R4                            CLEAR FOR LENGTHS
         ICM   R4,1,TIOELNGH                    LENGTH OF THIS ENTRY
         BZ    ERR0060                          END OF TABLE
         CLC   TIOEDDNM,SESS_DD                 SPECIFIED DD FOUND?
         BE    MAIN0050                         YES
         AR    R3,R4                            NEXT ENTRY
         B     MAIN0040                         LOOP
MAIN0050 DS    0H
         ITRACE ID=DD_FND
         TM    SESS_KEYWORD_FLAGS,$KEYWORD_DSN
         BO    MAIN0060                         YES
         LA    R5,DXD_EPA
         ST    R5,DXD_SWEPAPTR
         USING ZB505,R5
         XC    SWAEPAX,SWAEPAX
         MVC   SWVA,TIOEJFCB
         ITRACE ID=SWAREQ
         SWAREQ FCODE=RL,                                              +
               EPA=DXD_SWEPAPTR,                                       +
               UNAUTH=YES,                                             +
               MF=(E,DXD_SWAPARMS)
         L     R1,SWBLKPTR
         USING JFCB,R1                          DEFINE BASE
         MVC   SESS_DSN,JFCBDSNM                COPY DATA SET NAME
MAIN0060 DS    0H
         MVC   SESS_UCB+1(3),TIOEFSRT           COPY UCB ADDRESS
         DROP  R3
* ------------------------------------------------------------------- *
*        Determine data set's DSORG, etc                              *
* ------------------------------------------------------------------- *
MAIN0070 DS    0H
         ITRACE ID=OSDSINFO                     CALLING OSDSINFO
         MVI   DSINFO_OPTIONS,0                 RESET OPTIONS
         MVC   DSINFO_DSN,SESS_DSN              SET DATA SET NAME
         MVC   DSINFO_VOLSER,COMM_BLANKS
         TM    SESS_KEYWORD_FLAGS,$KEYWORD_VOL  VOLSER SPECIFIED?
         BNO   MAIN0080                         NO
         OI    DSINFO_OPTIONS,$DSINFO_VOLSER    SPECIFIC VOLSER
         MVC   DSINFO_VOLSER,SESS_VOLSER        VOLSER
MAIN0080 DS    0H
         LA    R1,DSINFREQ                      REQUEST BLOCK ADDRESS
         L     R15,COMM_V_OSDSINFO              OSDSINFO ENTRY POINT
         BALR  R14,R15
         OC    DSINFO_ERROR_INFO,DSINFO_ERROR_INFO
         BNZ   ERR0070
         MVC   SESS_DATA_TYPE,DSINFO_DATASET_TYPE
         MVC   SESS_VOLSER,DSINFO_VOLSER
         MVC   SESS_DSCB,DSINFO_DSCB
         ITRACE ID=DS_TYPE,                                            +
               DATA1=(SESS_DATA_TYPE,1)
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         ITRACE ID=LOADER,                                             +
               DATA1=SESS_DATA_LOADER
         CLC   SESS_DATA_LOADER,COMM_BLANKS     DATA ALREADY LOADED?
         BH    MAIN0230                         YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         CLI   SESS_DATA_TYPE,$DATA_TYPE_PDS_DIR
         BE    MAIN0100                         YES
         CLI   SESS_DATA_TYPE,$DATA_TYPE_PDS_MEMBER
         BE    MAIN0110                         YES
         CLI   SESS_DATA_TYPE,$DATA_TYPE_SEQUENTIAL
         BE    MAIN0120                         YES
         CLI   SESS_DATA_TYPE,$DATA_TYPE_DATA   DATA COMPONENT?
         BE    MAIN0130                         YES
         CLI   SESS_DATA_TYPE,$DATA_TYPE_INDEX  INDEX COMPONENT?
         BE    MAIN0140                         YES
         TM    SESS_DATA_TYPE,$DATA_TYPE_VSAM   VSAM?
         BO    MAIN0160                         YES
         ITRACE ID=BAD_TYPE
         MVI   COMM_MSG_ID+1,2                  UNKNOWN DATA SET TYPE
         B     ERR1000
* ------------------------------------------------------------------- *
*                                                                     *
*        PDS/E directory                                              *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0100 DS    0H
         CLC   SESS_MEMBER,COMM_BLANKS          MEMBER NAME GIVEN?
         BNE   MAIN0110                         YES..
         ITRACE ID=PDS_E
         MVI   SESS_DATA_TYPE,$DATA_TYPE_PDS_DIR   DATA TYPE
         BAL   R8,DSPC0000                      CREATE DATASPACE
         MVC   LOADREQ_MODULE,MODULE_OSDESERV
         LA    R1,LOADREQ
         L     R15,COMM_V_OSLOAD
         BALR  R14,R15
         ICM   R15,15,LOADREQ_EP                OSDESERV ENTRY POINT
         BALR  R14,R15                          LINK TO OSDESERV
         ITRACE ID=DESERVRC,                                           +
               RDATA1=R15
         LTR   R15,R15                          SUCCESSFUL?
         BNZ   MAIN0510                         NO
         B     MAIN0230                         DISPLAY
* ------------------------------------------------------------------- *
*                                                                     *
*        PDS/E member                                                 *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0110 DS    0H
         ITRACE ID=PDSMEMBR
         MVI   SESS_DATA_TYPE,$DATA_TYPE_PDS_MEMBER
         BAL   R8,DSPC0000                      CREATE DATASPACE
         MVC   LOADREQ_MODULE,MODULE_OSRDBSAM
         LA    R1,LOADREQ
         L     R15,COMM_V_OSLOAD
         BALR  R14,R15
         ICM   R15,15,LOADREQ_EP                OSRDBSAM ENTRY POINT
         BALR  R14,R15                          LINK TO OSRDBSAM
         ITRACE ID=BSAM_RC,                                            +
               RDATA1=R15
         LTR   R15,R15                          SUCCESSFUL?
         BNZ   MAIN0510                         NO
         B     MAIN0230                         DISPLAY
* ------------------------------------------------------------------- *
*                                                                     *
*        Sequential data set                                          *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0120 DS    0H
         ITRACE ID=SEQ
         L     R1,SESS_UCB                      UCB
         USING UCBOB,R1                         DEFINE BASE
         TM    UCBTBYT3,UCB3DACC                DASD DEVICE?
         BNO   ERR0110                          NO..
         MVI   SESS_DATA_TYPE,$DATA_TYPE_SEQUENTIAL
         B     MAIN0150
* ------------------------------------------------------------------- *
*                                                                     *
*        VSAM DATA component                                          *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0130 DS    0H
         ITRACE ID=VSAMDATA
         B     MAIN0150
* ------------------------------------------------------------------- *
*                                                                     *
*        VSAM INDEX component                                         *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0140 DS    0H
         ITRACE ID=VSAMINDX
* ------------------------------------------------------------------- *
*        Load OSRDBSAM                                                *
* ------------------------------------------------------------------- *
MAIN0150 DS    0H
         BAL   R8,DSPC0000                      CREATE DATASPACE
         ITRACE ID=LOADREAD,                                           +
               DATA1=MODULE_OSRDBSAM
         MVC   LOADREQ_MODULE,MODULE_OSRDBSAM
         LA    R1,LOADREQ
         L     R15,COMM_V_OSLOAD
         BALR  R14,R15
* ------------------------------------------------------------------- *
*        Link to OSRDBSAM to read data                                *
* ------------------------------------------------------------------- *
         ICM   R15,15,LOADREQ_EP                OSRDBSAM ENTRY POINT
         ITRACE ID=CALLBSAM,                                           +
               RDATA1=R15
         BALR  R14,R15                          LINK TO OSRDBSAM
         B     MAIN0170                         FREE THE DD
* ------------------------------------------------------------------- *
*                                                                     *
*        VSAM                                                         *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0160 DS    0H
         ITRACE ID=VSAM
         MVI   SESS_DATA_TYPE,$DATA_TYPE_VSAM   SOME SORT OF VSAM
         BAL   R8,DSPC0000                      CREATE DATASPACE
         MVC   LOADREQ_MODULE,MODULE_OSRDVSAM
         LA    R1,LOADREQ
         L     R15,COMM_V_OSLOAD
         BALR  R14,R15                          LOAD OSRDVSAM
         ICM   R15,15,LOADREQ_EP
         ITRACE ID=CALLVSAM,                                           +
               RDATA1=R15
         BALR  R14,R15                          LINK TO OSRDVSAM
         ITRACE ID=VSAM_RC,                                            +
               RDATA1=R15,                                             +
               DATA2=(SESS_DATA_TYPE,1)
         LTR   R15,R15                          SUCCESSFUL?
         BNZ   ERR1010                          NO
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0170 DS    0H
         ITRACE ID=FREE_DD,                                            +
               DATA1=SESS_DD
         MVI   DAIR_FUNC,$DAIR_FREE
         MVC   DAIR_DD,SESS_DD
         LA    R1,DAIRREQ
         L     R15,COMM_V_OSDAIR                OSDAIR ENTRY POINT
         BALR  R14,R15                          CALL OSDAIR
         OC    DAIR_R15,DAIR_R15                SUCCESSFUL?
         BNZ   ERR0050                          NO
         TM    SESS_STATUS,$STATUS_NO_MEMBER
         BO    ERR1010                          MEMBER WAS NOT FOUND
         B     MAIN0230
* ------------------------------------------------------------------- *
*       Read data from VTOC                                           *
* ------------------------------------------------------------------- *
MAIN0180 DS    0H
         ITRACE ID=VTOC
         MVI   SESS_DATA_TYPE,$DATA_TYPE_VTOC
         OI    SESS_DATASET_FLAGS,$SESS_DATASET_NO_DCB
         MVI   SESS_DD,C'V'
         MVC   SESS_DD+1(6),SESS_VOLSER
         MVI   SESS_DD+7,C' '
         BAL   R8,DSPC0000                      CREATE DATASPACE
         MVC   LOADREQ_MODULE,MODULE_OSRDVTOC
         LA    R1,LOADREQ
         L     R15,COMM_V_OSLOAD
         BALR  R14,R15                          LOAD OSRDVTOC
         ITRACE ID=READVTOC,                                           +
               RDATA1=R15
         ICM   R15,15,LOADREQ_EP
         BALR  R14,R15                          LINK TO OSRDVTOC
         LTR   R15,R15                          SUCCESSFUL?
         BNZ   ERR1010                          NO
         B     MAIN0230
* ------------------------------------------------------------------- *
*        Determine HFS file type                                      *
* ------------------------------------------------------------------- *
MAIN0190 DS    0H
         ITRACE ID=MAIN0190,                                           +
               DATA1=(SESS_DATA_TYPE,1)
         TM    SESS_DATA_TYPE,$DATA_TYPE_HFS    HFS?
         BNO   MAIN0230                         NO
         ITRACE ID=STAT,                                               +
               DATA1=(SESS_PATH_LENGTH,4),                             +
               DATA2=SESS_PATH
         MVC   LOADREQ_MODULE,MODULE_OSHFSTYP   MODULE TO LOAD
         LA    R1,LOADREQ
         L     R15,COMM_V_OSLOAD
         BALR  R14,R15                          LOAD OSHFSTYP
         ICM   R15,15,LOADREQ_EP
         BALR  R14,R15                          LINK TO OSHFSTYP
         ITRACE ID=STAT_RC,                                            +
               RDATA1=R15
         LTR   R15,R15                          SUCCESSFUL?
         BNZ   ERR0090                          NO
         OI    SESS_DATASET_FLAGS,$SESS_DATASET_NO_DCB
         LA    R2,SESS_STAT                     STAT FOR PATH ENTERED
         USING STAT,R2
         ITRACE ID=PATHTYPE,                                           +
               DATA1=(ST_MODE,4)
         CLI   ST_MODE,X'FF'                    STATUS X'FF'?
         BE    MAIN0230                         YES.. DID NOT EXIST
         CLI   ST_MODE,FT_SYMLINK               SYMLINK?
         BNE   MAIN0200                         NO
         LA    R2,SESS_REAL_STAT                STAT FOR REAL PATH
MAIN0200 DS    0H
         CLI   ST_MODE,FT_DIR                   DIRECTORY?
         BE    MAIN0210                         YES
         ITRACE ID=HFSSEQ
         MVI   SESS_DATA_TYPE,$DATA_TYPE_HFS_FILE
         MVC   LOADREQ_MODULE,MODULE_OSHFSSEQ   'NORMAL' FILE READER
         B     MAIN0220
MAIN0210 DS    0H
         ITRACE ID=HFSDIR
         MVI   SESS_DATA_TYPE,$DATA_TYPE_HFS_DIR
         MVC   LOADREQ_MODULE,MODULE_OSHFSDIR   DIRECTORY READER
MAIN0220 DS    0H
         BAL   R8,DSPC0000                      CREATE DATASPACE
         LA    R1,LOADREQ
         L     R15,COMM_V_OSLOAD
         BALR  R14,R15                          LOAD HFS READER
         ICM   R15,15,LOADREQ_EP
         BALR  R14,R15                          LINK
         ITRACE ID=HFS_RC,                                             +
               RDATA1=R15
         LTR   R15,R15                          SUCCESSFUL?
         BNZ   ERR1010                          NO
* ------------------------------------------------------------------- *
*       Data has been read and loaded into the DATASPACE              *
* ------------------------------------------------------------------- *
MAIN0230 DS    0H
         ITRACE ID=LOADED
        MVC   DATASPACE_1_DISPLAY_FIRST_RECORD,DATASPACE_1_FIRST_RECORD
         L     R2,COMM_OSSPFD
         USING OSSPFD,R2
         MVC   SPF_MSG_1,COMM_MSG_1
         MVC   SPF_MSG_2,COMM_MSG_2
         MVC   SPF_MSG_3,COMM_MSG_3
         MVC   SPF_MSG_4,COMM_MSG_4
         MVC   SPF_MSG_5,COMM_MSG_5
         MVC   COMM_MSG_1,COMM_BLANKS
         MVC   COMM_MSG_2,COMM_BLANKS
         MVC   COMM_MSG_3,COMM_BLANKS
         MVC   COMM_MSG_4,COMM_BLANKS
         MVC   COMM_MSG_5,COMM_BLANKS
         DROP  R2
         TM    SESS_USER_OPTIONS,$OPTIONS_DUMPDATA
         BNO   MAIN0400
         ITRACE ID=DUMPDATA
         MVI   DAIR_OPTS,$DAIR_SYSOUT           TYPE OF DATA SET
         MVI   DAIR_FUNC,$DAIR_ALLOC            SET FUNCTION (ALLOCATE)
         MVC   DAIR_DD,OSDUMP_DD                COPY DDNAME
         MVC   DAIR_SYSOUT,COMM_SYSOUT_CLASS
         LA    R1,DAIRREQ
         L     R15,COMM_V_OSDAIR
         BALR  R14,R15
         OC    DAIR_R15,DAIR_R15                SUCCESSFUL?
         BNZ   ERR0050                          NO
         MVC   DUMP_DCB(DUMP_DCB_L),DUMP_DCB_I
         MVC   DUMP_OPEN(OPEN_L),OPEN_I
         MVC   DUMP_CLOSE(CLOSE_L),CLOSE_I
         LA    R2,DUMP_DCB                      DCB ADDRESS
         ST    R2,DSPCREQ_DCB                   PASS IT TO OSDSPACE
         ITRACE ID=OPENDUMP,                                           +
               RDATA1=R2
         OPEN  ((R2),OUTPUT),                                          +
               MODE=31,                                                +
               MF=(E,DUMP_OPEN)
         LA    R1,DUMP_IO_AREA                  I/O AREA ADDRESS
         ST    R1,DSPCREQ_RECORD_ADDR           PASS ADDRESS
         MVI   DSPCREQ_FUNC,$DSPCREQ_DUMP
         LA    R0,DATASPACE_1
         ST    R0,DSPCREQ_DATASPACE
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE
         BALR  R14,R15
         ITRACE ID=CLS_DUMP,                                           +
               RDATA1=R2
         CLOSE ((R2)),                          CLOSE DCB              +
               MODE=31,                                                +
               MF=(E,DUMP_CLOSE)
         FREEPOOL (R2)
         MVI   DAIR_FUNC,$DAIR_FREE
         LA    R1,DAIRREQ
         L     R15,COMM_V_OSDAIR
         BALR  R14,R15
         B     MAIN0400
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0250 DS    0H
         ITRACE ID=STOR
         MVI   SESS_DATA_TYPE,$DATA_TYPE_STOR
         OI    SESS_DATASET_FLAGS,$SESS_DATASET_NO_DCB
         MVC   SESS_FORMAT,FORMAT_STOR

* ------------------------------------------------------------------- *
*                                                                     *
*                                                                     *
*        Load and initialize the function                             *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0400 DS    0H
         ITRACE ID=LOADFMAT,                                           +
               DATA1=SESS_FORMAT
         MVI   FMATREQ_FUNC,$FMATREQ_FORMAT
         MVC   FMATREQ_FORMAT,SESS_FORMAT
         LA    R1,FMATREQ
         L     R15,DXD_OSFORMAT                 OSFORMAT EP
         BALR  R14,R15                          LOAD FUNCTION
         ITRACE ID=FMAT_RC,                                            +
               DATA1=(FMATREQ_RC,1)
         CLI   FMATREQ_RC,$FMATREQ_OK           SUCCESSFUL?
         BNE   ERR0100                          NO
* ------------------------------------------------------------------- *
*        Clear ZCMD                                                   *
* ------------------------------------------------------------------- *
         L     R2,COMM_OSSPFD
         USING OSSPFD,R2
         MVC   SPF_ZCMD,COMM_BLANKS
* ------------------------------------------------------------------- *
*                                                                     *
*                                                                     *
*                                                                     *
*        Display the data                                             *
*                                                                     *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0500 DS    0H
         ITRACE ID=DISPLAY
         NI    DXD_FLAGS,255-$PROMPT            RESET FLAG
         TM    SESS_STATUS,$STATUS_NO_DATA      ANY DATA TO DISPLAY?
         BO    MAIN0510                         NO
         MVI   SESS_STATUS,0                    CLEAR STATUS
         MVI   SESS_FORMAT_FLAGS,$FORMAT_FORMAT SET FUNCTION
         L     R2,COMM_OSSPFD
         MVC   SPF_FORMAT,SESS_FORMAT           COPY FORMAT NAME
         L     R15,SESS_FUNCTION_EP             FUNCTION ENTRY POINT
         ITRACE ID=FORMAT,                                             +
               RDATA1=R15,                                             +
               DATA2=SESS_FORMAT
         BALR  R14,R15                          LINK TO FUNCTION
         ITRACE ID=SESSFUNC,                                           +
               DATA1=(COMM_SESS_FUNC,1)
         CLC   COMM_NEW_FORMAT,COMM_BLANKS      NEW FORMAT?
         BNE   FMT0000                          YES
         CLI   COMM_SESS_FUNC,0                 ANY SESSION FUNCTION?
         BE    MAIN0500                         NO..
         CLI   COMM_SESS_FUNC,$SESS_REMOVE      REMOVE CURRENT SESSION?
         BE    MAIN0550                         YES
         CLI   COMM_SESS_FUNC,$SESS_SWITCH      SWITCH SESSIONS?
         BE    SESS0000                         YES
         CLI   COMM_SESS_FUNC,$SESS_NEW_SESSION NEW SESSION?
         BE    SESS0100                         YES
         DC    H'0'
MAIN0510 DS    0H
         ITRACE ID=NO_DATA
         MVC   SESS_DISP_PANEL,OSNODATA         NO DATA TO DISPLAY
         L     R15,COMM_V_OSDISP
         BALR  R14,R15
* ------------------------------------------------------------------- *
*                                                                     *
*       Check for any keywords                                        *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0520 DS    0H
         L     R2,COMM_OSSPFD
         OC    SPF_ZCMD,COMM_BLANKS
         CLC   SPF_ZCMD,COMM_BLANKS
         BE    MAIN0530
         MVI   COMM_MSG_ID+1,4                  INVALID COMMAND
         L     R15,COMM_V_OSMSG
         BALR  R14,R15
         MVC   SPF_MSG_1,COMM_MSG_1
         MVC   SPF_MSG_2,COMM_MSG_2
         MVC   SPF_MSG_3,COMM_MSG_3
         MVC   SPF_MSG_4,COMM_MSG_4
         MVC   SPF_MSG_5,COMM_MSG_5
         B     MAIN0500
         DROP  R2
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0530 DS    0H
         CLI   SESS_RC+1,0                      RC ZERO?
         BE    MAIN0500                         YES.. RE-DISPLAY
MAIN0540 DS    0H
         ITRACE ID=MAIN0540
         TM    DXD_FLAGS,$PROMPT
         BO    INIT0200
* ------------------------------------------------------------------- *
*                                                                     *
*       A session has ended                                           *
*                                                                     *
*       Delete the data space                                         *
*       Free the FORMAT info                                          *
*       If the DCB is open                                            *
*          close the DCB                                              *
*          FREEMAIN the DCB storage                                   *
*          deallocate the DD                                          *
*       FREEMAIN the I/O area                                         *
*       FREEMAIN VDATA if present                                     *
*       Determine if there are other sessions                         *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0550 DS    0H
         ITRACE ID=ENDED,                                              +
               DATA1=SESS_DD,                                          +
               DATA2=DATASPACE_1_NAME
         TM    SESS_STATUS,$STATUS_NO_DATA
         BO    MAIN0560                         NO DATA
         ITRACE ID=FREEFMT1,                                           +
               DATA1=SESS_FORMAT
         MVI   FMATREQ_FUNC,$FMATREQ_FREE
         MVC   FMATREQ_FORMAT,SESS_FORMAT
         LA    R1,FMATREQ                       FORMAT REQUEST BLOCK
         L     R15,DXD_OSFORMAT                 OSFORMAT ENTRY POINT
         BALR  R14,R15                          FREE THE FORMAT
         ITRACE ID=FREED_1,                                            +
               DATA1=(FMATREQ_RC,1)
         CLI   FMATREQ_RC,$FMATREQ_OK           SUCCESSFUL?
         BNE   ERR0100                          NO
MAIN0560 DS    0H
         TM    SESS_KEYWORD_FLAGS_2,$KEYWORD_STOR
         BO    MAIN0580                         NO DCB, NO DD ALLOCATED
         ICM   R2,15,SESS_DCB_ADDR              DCB ADDRESS
         BZ    MAIN0570                         NO DCB
         TM    SESS_DCB_FLAGS,$SESS_DCB_OPEN    DCB OPEN?
         BNO   MAIN0565                         NO
         MVC   DXD_CLOSE(CLOSE_L),CLOSE_I
         ITRACE ID=CLOSE,                                              +
               RDATA1=R2,                                              +
               DATA2=SESS_DD
         CLOSE ((R2)),                          CLOSE DCB              +
               MODE=31,                                                +
               MF=(E,DXD_CLOSE)
         NI    SESS_DCB_FLAGS,255-$SESS_DCB_OPEN
MAIN0565 DS    0H
         L     R0,SESS_DCB_LENGTH               DCB LENGTH
         ITRACE ID=FREE_DCB,                                           +
               RDATA1=R2,                                              +
               RDATA2=R0
         FREEMAIN RU,                           FREEMAIN DCB           +
               A=(R2),                                                 +
               LV=(0)
         XC    SESS_DCB_ADDR,SESS_DCB_ADDR      RESET ADDRESS
         XC    SESS_DCB_LENGTH,SESS_DCB_LENGTH  RESET LENGTH
MAIN0570 DS    0H
         ITRACE ID=FREE_DD,                                            +
               DATA1=SESS_DD
         MVI   DAIR_FUNC,$DAIR_FREE             SET DAIR FUNCTION
         MVC   DAIR_DD,SESS_DD                  SET DD NAME
         LA    R1,DAIRREQ
         L     R15,COMM_V_OSDAIR                OSDAIR ENTRY POINT
         BALR  R14,R15                          FREE THE DD
         ITRACE ID=DD_FREED
MAIN0580 DS    0H
         ICM   R1,15,SESS_IO_AREA               I/O AREA ADDRESS
         BZ    MAIN0590                         NOT GETMAINED
         ITRACE ID=FREE_IO,                                            +
               RDATA1=R1
         L     R0,COMM_IO_SIZE                  I/O AREA SIZE
         FREEMAIN RU,                                                  +
               A=(1),                                                  +
               LV=(0)
MAIN0590 DS    0H
         OC    DATASPACE_1_ALET,DATASPACE_1_ALET DATASPACE PRESENT?
         BZ    MAIN0600                          NO
         ITRACE ID=D_SPACE,                                            +
               DATA1=(DATASPACE_1_NAME,8),                             +
               DATA2=(DATASPACE_1_ALET,4)
         LA    R1,DATASPACE_1                    DATA SPACE FOR SESSION
         ST    R1,DSPCREQ_DATASPACE              SET ADDRESS
         MVI   DSPCREQ_FUNC,$DSPCREQ_DELETE      SET COMMAND
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE
         BALR  R14,R15                           DELETE DATASPACE
         CLI   DSPCREQ_RC,$DSPCREQ_OK            SUCCESSFUL?
         BNE   ERR0080
* ------------------------------------------------------------------- *
*                                                                     *
*     Remove the active session block                                 *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0600 DS    0H
         ITRACE ID=REM_SESS,                                           +
               DATA1=(COMM_ACTIVE_SESSION,4)
         ICM   R1,15,SESS_NEXT                  NEXT ON CHAIN PRESENT?
         BZ    MAIN0610                         NO
NEXT     USING SESSION,R1
         MVC   NEXT.SESS_PREV,SESS_PREV         UNCHAIN CURRENT BLOCK
         DROP  NEXT
MAIN0610 DS    0H
         ICM   R1,15,SESS_PREV                  PREV ON CHAIN PRESENT?
         BZ    MAIN0620                         NO
PREV     USING SESSION,R1
         MVC   PREV.SESS_NEXT,SESS_NEXT         UNCHAIN CURRENT BLOCK
         DROP  PREV
MAIN0620 DS    0H
         LR    R1,R11                           COPY SESSION BLOCK ADDR
         ICM   R2,15,SESS_NEXT                  NEXT SESSION BLOCK
         BNZ   MAIN0630
         ICM   R2,15,SESS_PREV                  PREVIOUS SESSION BLOCK
MAIN0630 DS    0H
         LA    R0,SESSION_L
         FREEMAIN RU,                                                  +
               A=(R1),                                                 +
               LV=(0)
         LTR   R11,R2                           SESSION TO RESUME
         BZ    EXIT0000                         NO SESSIONS.. ALL DONE
         MVI   COMM_SESS_FUNC,$SESS_SWITCH      SWITCHING BACK
         B     MAIN0500                         DISPLAY SESSION'S INFO
* ------------------------------------------------------------------- *
*                                                                     *
*     Process new format(s)                                           *
*                                                                     *
* ------------------------------------------------------------------- *
FMT0000  DS    0H
         ITRACE ID=NEW_FMAT,                                           +
               DATA1=SESS_FORMAT,                                      +
               DATA2=COMM_NEW_FORMAT
         MVI   FMATREQ_FUNC,$FMATREQ_FREE       SET CMD (FREE FORMAT)
         MVC   FMATREQ_FORMAT,SESS_FORMAT       FORMAT BEING FREED
         LA    R1,FMATREQ
         L     R15,DXD_OSFORMAT                 OSFORMAT ENTRY POINT
         BALR  R14,R15                          FREE CURRENT FORMAT
         MVC   SESS_FORMAT,COMM_NEW_FORMAT      CHANGE FORMAT NAME
         MVC   COMM_NEW_FORMAT,COMM_BLANKS      RESET NEW NAME
         B     MAIN0400                         LOAD NEW FUNCTION
* ------------------------------------------------------------------- *
*                                                                     *
*     Process new sessions or session switches                        *
*                                                                     *
*     This occurs when                                                *
*          a data set is selected on a VTOC display                   *
*          a member is selected on a PDS(/E) directory display        *
*          a file or path is selected on an HFS directory display     *
*                                                                     *
* ------------------------------------------------------------------- *
SESS0000 DS    0H
         ITRACE ID=SESS0000,                                           +
               DATA1=(SESS_FORMAT_FLAGS,1)
         L     R11,COMM_NEW_SESSION             NEW SESSION ADDR
         ST    R11,COMM_ACTIVE_SESSION          NEW IS NOW CURRENT
         XC    COMM_NEW_SESSION,COMM_NEW_SESSION
         CLI   SESS_FORMAT_FLAGS,$FORMAT_FORMAT IN FORMAT STATE?
         BE    SESS0010                         YES
         ITRACE ID=NEW_SESS,                    NEW SESSION            +
               RDATA1=R11
         B     MAIN0000                         PROCESS NEW SESSION
SESS0010 DS    0H
         ITRACE ID=RESUME,                      RESUMING A SESSION     +
               RDATA1=R11
         B     MAIN0500                         DISPLAY DATA AGAIN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
SESS0100 DS    0H
         GETMAIN RU,                                                   +
               LV=SESSION_L,                                           +
               LOC=ANY
         ITRACE ID=NEW_OS,                                             +
               RDATA1=R1
         LR    R2,R1                            SAVE NEW SESSION ADDR
NEW      USING SESSION,R2
         LR    R0,R1                            COPY ADDRESS
         L     R1,=A(SESSION_L)                 LENGTH
         SR    R14,R14
         SR    R15,R15
         MVCL  R0,R14                           INITIALIZE STORAGE
         MVC   NEW.SESS_EYE,SESSION_ID          IDENTIFY BLOCK
         ST    R11,NEW.SESS_PREV                SET PREVIOUS BLOCK
         ICM   R1,15,SESS_NEXT                  NEXT SESS BLOCK
         BZ    SESS0110
NEXT     USING SESSION,R1
         ST    R2,NEXT.SESS_PREV                SET PREVIOUS
SESS0110 DS    0H
         ST    R1,NEW.SESS_NEXT                 NEXT IS NEW'S NEXT
         ST    R2,SESS_NEXT                     NEW IS NEXT TO CURRENT
         LR    R11,R2                           NEW IS NOW CURRENT
         ST    R11,COMM_ACTIVE_SESSION          SET ACTIVE SESSION
         DROP  NEW,NEXT
         L     R0,COMM_IO_SIZE                  I/O AREA SIZE
         GETMAIN RU,                                                   +
               LV=(0),                                                 +
               LOC=BELOW
         ST    R1,SESS_IO_AREA                  SET I/O AREA ADDRESS
         LA    R1,DATASPACE_1                   DATA SPACE 1 INFO
         ST    R1,DSPCREQ_DATASPACE             SET IN PARMS
         MVC   SESS_DSN,COMM_BLANKS
         MVC   SESS_MEMBER,COMM_BLANKS
         MVC   SESS_VOLSER,COMM_BLANKS
         MVC   DATASPACE_1_FIRST_RECORD,F1
         MVC   DATASPACE_1_DISPLAY_FIRST_RECORD,F1
         MVC   DATASPACE_1_DISPLAY_FIRST_COLUMN,F1
         B     INIT0190                         PROMPT USER
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DSPC0000 DS    0H
         ITRACE ID=CRE_DSPC                     CREATING A DATA SPACE
         LA    R0,DATASPACE_1
         ST    R0,DSPCREQ_DATASPACE
         MVI   DSPCREQ_FUNC,$DSPCREQ_CREATE
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE
         BALR  R14,R15                          CREATE DATASPACE
         ITRACE ID=DSPC_RC,                                            +
               DATA1=(DSPCREQ_RC,1)
         CLI   DSPCREQ_RC,$DSPCREQ_OK
         BNE   ERR0080
         ITRACE ID=XREF,                                               +
               DATA1=SESS_DD,                                          +
               DATA2=DATASPACE_1_NAME
         MVC   DATASPACE_1_FIRST_RECORD,F1
         MVC   DATASPACE_1_DISPLAY_FIRST_RECORD,F1
         MVC   DATASPACE_1_DISPLAY_FIRST_COLUMN,F1
         BR    R8
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0010  DS    0H
         ITRACE ID=NO_SPFQRY
         STH   R15,COMM_DWORD
         MVI   COMM_MSG_ID+1,5                  SPFQRY FAILED
         B     ERR1000
ERR0020  DS    0H
         ITRACE ID=PROFFAIL
         B     ERR1010
ERR0030  DS    0H
         ITRACE ID=PARM_ERR
         B     ERR1010
ERR0040  DS    0H
         ITRACE ID=REQUIRED
         MVI   COMM_MSG_ID+1,6                  REQUIRED PARM MISSING
         B     ERR1000
ERR0050  DS    0H
         ITRACE ID=DAIRFAIL
         B     ERR1010
ERR0060  DS    0H
         ITRACE ID=NO_TIOT
         MVI   COMM_MSG_ID+1,7                  NO TIOT ENTRY FOUND
         B     ERR1000
ERR0070  DS    0H
         ITRACE ID=DSINFO_RC,                                          +
               DATA1=DSINFO_ERROR_INFO
         MVC   COMM_DWORD,DSINFO_ERROR_INFO
         MVI   COMM_MSG_ID+1,8                  DSINFO FAILED
         B     ERR1000
ERR0080  DS    0H
         ITRACE ID=BAD_DSPC
         B     ERR1010
ERR0090  DS    0H
         ITRACE ID=HFS_ERR
         B     ERR1010
* ------------------------------------------------------------------- *
*                                                                     *
*      This should probably be changed to display the message         *
*      rather than ABENDing.                                          *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0100  DS    0H
         DC    H'0'
         DC    C'Format not found'
ERR0110  DS    0H
         ITRACE ID=NOTDASD
         MVI   COMM_MSG_ID+1,9                  DATA NOT ON DASD
ERR1000  DS    0H
         MVC   COMM_MSG_CSECT,MODID             SET CSECT NAME
         MVI   COMM_MSG_ID,0                    SET BYTE 1 TO ZERO
         L     R15,COMM_V_OSMSG                 OSMSG ENTRY POINT
         BALR  R14,R15                          LOOKUP/CONSTRUCT MSG
         ITRACE ID=MSG_RC,                                             +
               RDATA1=R15
         LTR   R15,R15                          SUCCESSFUL?
         BZ    ERR1010                          YES
         ABEND 1,DUMP,,USER
ERR1010  DS    0H
         LA    R2,COMM_MSG_1                    FIRST LINE OF MESSAGE
         LH    R4,COMM_MSG_LINES                NUMBER OF LINES
         ITRACE ID=MSGS,                                               +
               RDATA1=R2,                                              +
               RDATA2=R4
ERR1020  DS    0H
         LA    R3,80                            LENGTH FOR TPUT
         BAL   R8,ERR2000
         LA    R2,L'COMM_MSG_1(,R2)
         BCT   R4,ERR1020
         MVC   COMM_MSG_1,COMM_BLANKS
         MVC   COMM_MSG_2,COMM_BLANKS
         MVC   COMM_MSG_3,COMM_BLANKS
         MVC   COMM_MSG_4,COMM_BLANKS
         MVC   COMM_MSG_5,COMM_BLANKS
         B     MAIN0540                         SESSION ENDED
ERR2000  DS    0H
         ITRACE ID=TPUT,                                               +
               RDATA1=R2,                                              +
               RDATA2=R3
         GETMAIN RU,                                                   +
               LV=(R3),                                                +
               LOC=BELOW
         LR    R5,R1
         BCTR  R3,0
         EX    R3,TPUT_MVC
         LA    R3,1(,R3)
         TPUT  (R5),(R3),R
         FREEMAIN RU,                                                  +
               A=(R5),                                                 +
               LV=(R3)
         BR    R8
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ERR3000  DS    0H
         LA    R3,MSG_01_L
         GETMAIN RU,                                                   +
               LV=(R3),                                                +
               LOC=BELOW
         LR    R2,R1
         MVC   0(MSG_01_L,R2),MSG_01
         TPUT  (R2),(R3),R
         FREEMAIN RU,                                                  +
               A=(R2),                                                 +
               LV=(R3)
         LR    R2,R13
         LA    R3,8
         B     EXIT0090
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=EXITING
         TM    COMM_FLAGS,$COMM_ABEND           ABEND REQUESTED?
         BNO   EXIT0010                         NO
         DC    H'0'
* ------------------------------------------------------------------- *
*        Update the user's profile                                    *
* ------------------------------------------------------------------- *
EXIT0010 DS    0H
         ITRACE ID=UPDTPROF
         LOAD  EP=OSPROF
         LR    R15,R0
         MVI   PROF_FUNC,$PROF_WRITE
         LA    R1,PROFREQ
         BALR  R14,R15                          LINK TO OSPROF
         DELETE EP=OSPROF
         ITRACE ID=PROF_RC,                                            +
               DATA1=(PROF_RC,1)
         CLI   PROF_RC,$PROF_OK                 SUCCESSFUL?
         BNE   ERR0020                          NO
* ------------------------------------------------------------------- *
*        Delete SPF variables                                         *
* ------------------------------------------------------------------- *
         ICM   R1,15,COMM_ISPLINK
         BZ    EXIT0030
         OC    COMM_OSSPFD,COMM_OSSPFD
         BZ    EXIT0030
         MVI   VAR_COMMAND,$VAR_DELETE
         MVC   VAR_TABLE,COMM_OSSPFV
         MVC   VAR_AREA,COMM_OSSPFD
         LA    R1,VARREQ
         L     R15,COMM_V_OSVARMGR
         BALR  R14,R15
         DELETE EP=OSSPFV
         XC    COMM_OSSPFV,COMM_OSSPFV
         DELETE EP=OSSPFD
         XC    COMM_OSSPFD,COMM_OSSPFD
         DELETE EP=ISPLINK
         XC    COMM_ISPLINK,COMM_ISPLINK
* ------------------------------------------------------------------- *
*        Free up FORMAT info                                          *
* ------------------------------------------------------------------- *
EXIT0030 DS    0H
         LTR   R11,R11                          SESSION TO CLEAN UP?
         BZ    EXIT0050                         NO
         ITRACE ID=FREEFMT2
         MVI   FMATREQ_FUNC,$FMATREQ_FREE       FORMAT FUNCTION
         MVC   FMATREQ_FORMAT,SESS_FORMAT       FORMAT TO FREE
         LA    R1,FMATREQ                       FORMAT PARMS
         L     R15,DXD_OSFORMAT                 OSFORMAT EP
         BALR  R14,R15                          FREE FORMAT
* ------------------------------------------------------------------- *
*        Close format(s) DCB                                          *
* ------------------------------------------------------------------- *
EXIT0050 DS    0H
         ITRACE ID=CLSEFMAT
         MVI   FMATREQ_FUNC,$FMATREQ_CLOSE      FORMAT FUNCTION
         LA    R1,FMATREQ                       FORMAT PARMS
         L     R15,DXD_OSFORMAT                 OSFORMAT EP
         BALR  R14,R15                          CLOSE FORMAT(S) DCB
* ------------------------------------------------------------------- *
*        Delete messages                                              *
* ------------------------------------------------------------------- *
         ITRACE ID=DEL_MSGS
         DELETE EPLOC=COMM_LANGUAGE_MODULE
* ------------------------------------------------------------------- *
*        FREE the recall command table                                *
* ------------------------------------------------------------------- *
         ITRACE ID=FREERCAL
         MVI   RCALLREQ_FUNC,$RCALLREQ_TERM
         LA    R1,RCALLREQ
         L     R15,COMM_V_OSRECALL
         BALR  R14,R15
* ------------------------------------------------------------------- *
*        Delete any loaded modules                                    *
* ------------------------------------------------------------------- *
EXIT0060 DS    0H
         ITRACE ID=CLEAN_UP
         MVI   LOADREQ_FUNC,$LOADREQ_CLEANUP
         LA    R1,LOADREQ
         L     R15,COMM_V_OSLOAD
         BALR  R14,R15
* ------------------------------------------------------------------- *
*        Delete the data space manager                                *
* ------------------------------------------------------------------- *
         OC    COMM_OSDSPACE,COMM_OSDSPACE
         BZ    EXIT0070
         DELETE EP=OSDSPACE
         XC    COMM_OSDSPACE,COMM_OSDSPACE
* ------------------------------------------------------------------- *
*                                                                     *
*        FREEMAIN the trace table                                     *
*                                                                     *
*        No ITRACE after this point!                                  *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0070 DS    0H
         L     R1,COMM_TRACE
         L     R0,TRACE_TABLE_SIZE
         FREEMAIN RU,                                                  +
               A=(1),                                                  +
               LV=(0)
* ------------------------------------------------------------------- *
*        Cancel ESTAE exit                                            *
* ------------------------------------------------------------------- *
         ESTAE 0
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         TM    COMM_TRACE_FLAG,$XTR_ON          EXTERNAL TRACE ACTIVE?
         BNO   EXIT0080                         NO
         CLOSE XTR_DCB,                         CLOSE EXTERNAL TRACE   +
               MODE=31,                                                +
               MF=(E,DXD_CLOSE)
* ------------------------------------------------------------------- *
*        Delete OSCOMM                                                *
* ------------------------------------------------------------------- *
EXIT0080 DS    0H
         L     R2,COMM_DXD
         LH    R3,SESS_RC
         DELETE EP=OSCOMM                       DELETE COMMON
* ------------------------------------------------------------------- *
*        DEQ On Screen                                                *
* ------------------------------------------------------------------- *
         MVC   DXD_ENQ(DEQ_L),DEQ_I
         DEQ   MF=(E,DXD_ENQ)
* ------------------------------------------------------------------- *
*        FREEMAIN the work area                                       *
* ------------------------------------------------------------------- *
EXIT0090 DS    0H
         L     R13,4(,R13)                      RESTORE ADDRESS
         LTR   R2,R2
         BZ    EXIT0100
         L     R0,DXDSIZE                       WORK AREA STORAGE SIZE
         FREEMAIN RU,                           FREEMAIN WORK AREA     +
               A=(R2),                                                 +
               LV=(0)
* ------------------------------------------------------------------- *
*        Set RC and exit                                              *
* ------------------------------------------------------------------- *
EXIT0100 DS    0H
         L     R14,12(R13)                      RESTORE R14
         LR    R15,R3                           SET RETURN CODE
         LM    R0,R12,20(R13)                   RESTORE REGISTERS
         BR    R14                              EXIT
* ------------------------------------------------------------------- *
            USING ACEE,R15
UIDMVC      MVC   COMM_USER(0),ACEEUSRI
TPUT_MVC    MVC   0(0,R5),0(R2)
* ------------------------------------------------------------------- *
*                                                                     *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
DXDSTART          DC   Q(DXDMAIN)               DISP TO WORK AREA
DXDSIZE           CXD                           WORK AREA TOTAL SIZE
TRACE_TABLE_SIZE  DC   A(TR_ENTRY_L*10000)

DUMP_DCB_I     DCB DSORG=PS,                                           +
               DDNAME=OSDUMP,                                          +
               RECFM=FBA,                                              +
               LRECL=133,                                              +
               BLKSIZE=13300,                                          +
               MACRF=PM
DUMP_DCB_L     EQU      *-DUMP_DCB_I
OPEN_I         OPEN     (*,OUTPUT),                                    +
               MODE=31,                                                +
               MF=L
OPEN_L         EQU      *-OPEN_I
CLOSE_I        CLOSE    (*),                                           +
               MODE=31,                                                +
               MF=L
CLOSE_L        EQU      *-CLOSE_I

ESTAE_I        ESTAE  *,CT,MF=L
ESTAE_L        EQU    *-ESTAE_I
DEQ_I          ENQ    (MODID,MODID,E,,STEP),MF=L
DEQ_L          EQU    *-DEQ_I
ENQ_I          ENQ    *,MF=L
ENQ_L          EQU    *-ENQ_I

SWAPARMS_I     SWAREQ MF=L
SWAPARMS_L     EQU    *-SWAPARMS_I

VCONI          DS     0F
               VCONS
VCONL          EQU    *-VCONI

V_OSESTAE      DC     V(OSESTAE)

H1               DC   H'1'
H2               DC   H'2'
H3               DC   H'3'
H4               DC   H'4'
H5               DC   H'5'
H6               DC   H'6'
H7               DC   H'7'
H8               DC   H'8'
H9               DC   H'9'
H40              DC   H'40'
H44              DC   H'44'

F1               DC   F'1'
STAT_LENGTH      DC   A(STAT#LENGTH)

FORMAT_STOR      DC   CL8'STOR'
OSNODATA         DC   CL8'OSNODATA'
OSDUMP_DD        DC   CL8'OSDUMP'
OSOPTS_DD        DC   CL8'OSOPTS'
MODULE_OSRDBSAM  DC   CL8'OSRDBSAM'
MODULE_OSDESERV  DC   CL8'OSDESERV'
MODULE_OSHFSDIR  DC   CL8'OSHFSDIR'
MODULE_OSHFSSEQ  DC   CL8'OSHFSSEQ'
MODULE_OSHFSTYP  DC   CL8'OSHFSTYP'
MODULE_OSFORMAT  DC   CL8'OSFORMAT'
MODULE_OSSPFD    DC   CL8'OSSPFD'
MODULE_OSSPFV    DC   CL8'OSSPFV'
MODULE_OSRDVSAM  DC   CL8'OSRDVSAM'
MODULE_OSRDVTOC  DC   CL8'OSRDVTOC'
MODULE_ISPLINK   DC   CL8'ISPLINK'
MODULE_OSMSGUS   DC   CL8'OSMSGUS'
LANGUAGE_US      DC   CL8'US'
SESSION_ID       DC   CL8'SESSION'
INIT             DC   CL8'INIT'

MSG_01           DS   0C
                 DC   C'OSMAIN99E On Screen is already active.'
MSG_01_L         EQU  *-MSG_01

                 LTORG

MAINEND          EQU  *
* ------------------------------------------------------------------- *
*                                                                     *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
DXDMAIN           DSECT
                  COPY     DXDPREF
DXD_SPF_PARMS     DS       0A
DXD_SPF_1         DS       A
DXD_SPF_2         DS       A

DXD_CLOSE         DS       0F,(CLOSE_L)X
DXD_ENQ           DS       0F,(ENQ_L)X
DXD_ESTAE         DS       0F,(ESTAE_L)X
DXD_SWAPARMS      DS       0F,(SWAPARMS_L)X

DXD_SWEPAPTR      DS       A
DXD_EPA           DS       CL28

DXD_OSFORMAT      DS       A

DXD_FLAGS         DS       X
$OSOPTS_DD        EQU      X'80'
$PROMPT           EQU      X'40'

DXD_PACK          DS       CL42

                  DSINFREQ DSECT=NO
                  DAIRREQ  DSECT=NO
                  DSPCREQ  DSECT=NO
                  FMATREQ  DSECT=NO
                  LOADREQ  DSECT=NO
                  PARMSREQ DSECT=NO
                  PROFREQ  DSECT=NO
                  RCALLREQ DSECT=NO
                  VARREQ   DSECT=NO

DUMP_CONTROL      DS       0F
DUMP_DCB          DS       0F,(DUMP_DCB_L)X
DUMP_OPEN         DS       0F,(OPEN_L)X
DUMP_CLOSE        DS       0F,(CLOSE_L)X
DUMP_IO_AREA      DS       CL133
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         IBMMAC ACEE=YES,                                              +
               ASCB=YES,                                               +
               ASXB=YES,                                               +
               CDE=YES,                                                +
               CVT=YES,                                                +
               JFCB=YES,                                               +
               PSA=YES,                                                +
               RB=YES,                                                 +
               TCB=YES,                                                +
               TIOT=YES,                                               +
               UCB=YES,                                                +
               DUMMY=DUMMY

               IEFJESCT
               IEFZB505     LOCEPAX=YES

VTOC_FORMAT_1  DSECT
         IBMMAC  VTOC=1
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COMMON  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         OSSPFD TYPE=DSECT
* ------------------------------------------------------------------- *
         COPY    TRENTRY
* ------------------------------------------------------------------- *
         BPXYFTYP LIST=YES
         BPXYMODE DSECT=YES,LIST=YES
         BPXYSTAT DSECT=YES,LIST=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY  REGEQU
         END   OSMAIN
