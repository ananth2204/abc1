         MACRO
         AREA  &CSECT,&DXD
         DC    CL8'&CSECT'
         DC    V(&CSECT)
         AIF   (T'&DXD EQ 'O').LBL0010
         DC    CL8'&DXD'
         DC    Q(&DXD)
         MEXIT
.LBL0010 ANOP
         DC    CL8'NONAME'
         DC    A(-1)
         MEND
* ------------------------------------------------------------------- *
*                                                                     *
*  MODULE NAME: OSDXD                                                 *
*                                                                     *
*  Map and display modules and dynamic work area(s)                   *
*                                                                     *
* ------------------------------------------------------------------- *
OSDXD    CSECT
OSDXD    AMODE 31
OSDXD    RMODE ANY
         USING OSDXD,R15
         B     INIT0000
MODID    DC    CL8'OSDXD'
         DC    CL8'&SYSDATE'
         DC    CL8'&SYSTIME'
MODLEN   DC    A(DXDEND-OSDXD)
INIT0000 DS    0H
         STM   R14,R12,12(R13)
         LR    R10,R15                        COPY BASE
         DROP  R15
         USING OSDXD,R10
         USING OSCOMM,R12
         USING SESSION,R11
         L     R15,COMM_DXD                   DXD BASE
         A     R15,DXD_START
         ST    R13,4(,R15)
         ST    R15,8(,R13)
         LR    R13,R15                        COPY WORK AREA ADDRESS
         USING DXDDXD,R13
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY
         L     R9,COMM_OSSPFD
         USING OSSPFD,R9
         MVC   SPF_ZCMD,COMM_BLANKS               RESET COMMAND
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         ITRACE ID=VDATA
         LA    R1,PANEL_NAME                      PANEL NAME
         MVI   COMM_VDATA_FUNC,$VDATA_GETMAIN
         L     R15,COMM_V_OSVDATA
         BALR  R14,R15                            GETMAIN VDATA
         ST    R0,DXD_VDATA_LENGTH                SAVE LENGTH
         ST    R1,DXD_VDATA_ADDR                  SAVE ADDRESS
BLD0000  DS    0H
         L     R0,DXD_VDATA_LENGTH
         L     R1,DXD_VDATA_ADDR
         MVI   COMM_VDATA_FUNC,$VDATA_VDEFINE
         L     R15,COMM_V_OSVDATA
         BALR  R14,R15                            VDEFINE VDATA
         MVC   DXD_VDEPTH,SPF_VDEPTH              SAVE DEPTH
         MVC   DXD_VWIDTH,SPF_VWIDTH              SAVE WIDTH
         L     R0,DXD_VDATA_ADDR
         L     R1,DXD_VDATA_LENGTH
         SR    R14,R14
         SR    R15,R15
         MVCL  R0,R14                             INITIALIZE VDATA
         LA    R8,DXD_TABLE                       FIRST DXD AREA
         USING AREA_DSECT,R8
         L     R7,DXD_VDATA_ADDR                  VDATA ADDRESS
         L     R6,DXD_VDEPTH                      SCREEN DEPTH
         USING DETAIL,R7
BLD0010  DS    0H
         MVI   DETAIL_ATTR_1,$SCREEN_ATTR_INPUT
         MVI   DETAIL_EP_SELECT,C' '
         MVI   DETAIL_ATTR_2,$SCREEN_ATTR_HIGH
         MVC   DETAIL_NAME,AREA_NAME              COPY NAME
         MVI   DETAIL_ATTR_3,$SCREEN_ATTR_NORMAL
         CLC   AREA_NAME,COMMON_NAME              OSCOMM?
         BE    BLD0020                            YES
         ICM   R1,15,AREA_EP                      AREA ADDRESS
         BM    BLD0030                            NO ADDRESS
         ST    R1,DXD_WORK
         UNPK  DETAIL_EP(9),DXD_WORK(5)           UNPACK EP
         TR    DETAIL_EP,COMM_HEXCHAR
         MVI   DETAIL_EP+8,C' '
         UNPK  DETAIL_LENGTH(9),MODLEN-OSDXD(5,R1)
         TR    DETAIL_LENGTH,COMM_HEXCHAR
         MVI   DETAIL_LENGTH+8,C' '
         B     BLD0030
BLD0020  DS    0H
         ST    R12,DXD_WORK                       OSCOMM ADDRESS
         UNPK  DETAIL_EP(9),DXD_WORK(5)           UNPACK ADDRESS
         TR    DETAIL_EP,COMM_HEXCHAR             TRANSLATE
         MVI   DETAIL_EP+8,C' '
         UNPK  DETAIL_LENGTH(9),COMMON_LENGTH(5)  UNPACK LENGTH
         TR    DETAIL_LENGTH,COMM_HEXCHAR
         MVI   DETAIL_LENGTH+8,C' '
BLD0030  DS    0H
         MVI   DETAIL_ATTR_4,$SCREEN_ATTR_INPUT
         MVI   DETAIL_DXD_SELECT,C' '
         MVI   DETAIL_ATTR_5,$SCREEN_ATTR_HIGH
         MVC   DETAIL_DXD_NAME,AREA_DXD_NAME      COPY DXD NAME
         MVI   DETAIL_ATTR_6,$SCREEN_ATTR_NORMAL
         CLC   AREA_DXD_NAME,SESSION_NAME         SESSION BLOCK?
         BE    BLD0060                            YES
         L     R1,COMM_DXD                        DXD ADDRESS
         A     R1,AREA_DXD_DISP                   PLUS DISP
         ST    R1,DXD_WORK
         UNPK  DETAIL_DXD_ADDR(9),DXD_WORK(5)     UNPACK ADDRESS
         TR    DETAIL_DXD_ADDR,COMM_HEXCHAR       TRANSLATE ADDRESS
         MVI   DETAIL_DXD_ADDR+8,C' '
         LA    R14,AREAL(,R8)                     NEXT ENTRY
NEXT     USING AREA_DSECT,R14
         CLI   NEXT.AREA_NAME,X'FF'               END OF TABLE?
         BE    BLD0040
         L     R1,NEXT.AREA_DXD_DISP              NEXT DXD'S DISP
         B     BLD0050
BLD0040  DS    0H
         L     R1,COMM_DXD_LENGTH                 USE LENGTH
BLD0050  DS    0H
         S     R1,AREA_DXD_DISP                   COMPUTE LENGTH
         ST    R1,DXD_WORK
         UNPK  DETAIL_DXD_LENGTH(9),DXD_WORK(5)   UNPACK LENGTH
         TR    DETAIL_DXD_LENGTH,COMM_HEXCHAR     TRANSLATE LENGTH
         MVI   DETAIL_DXD_LENGTH+8,C' '
         B     BLD0070
BLD0060  DS    0H
         ST    R11,DXD_WORK
         UNPK  DETAIL_DXD_ADDR(9),DXD_WORK(5)
         TR    DETAIL_DXD_ADDR,COMM_HEXCHAR
         MVI   DETAIL_DXD_ADDR+8,C' '
         UNPK  DETAIL_DXD_LENGTH(9),SESSION_LENGTH(5)
         TR    DETAIL_DXD_LENGTH,COMM_HEXCHAR
         MVI   DETAIL_DXD_LENGTH+8,C' '
BLD0070  DS    0H
         A     R7,DXD_VWIDTH                      NEXT LINE
         BCT   R6,BLD0080
         B     BLD0090                            SCREEN FULL
BLD0080  DS    0H
         LA    R8,AREAL(,R8)                      NEXT
         CLI   0(R8),X'FF'                        END OF TABLE?
         BNE   BLD0010                            NO
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
BLD0090  DS    0H
         ITRACE ID=DISPLAY
         LA    R1,DISPLAY
         ST    R1,DXD_PARM_1
         LA    R1,PANEL_NAME
         ST    R1,DXD_PARM_2
         OI    DXD_PARM_2,X'80'
         LA    R1,DXD_SPF_PARMS
         L     R15,COMM_ISPLINK
         BALR  R14,R15                            DISPLAY
         ITRACE ID=DISP_RC,                                            +
               RDATA1=R15
         LTR   R15,R15                            RC = 0?
         BNZ   EXIT0000                           YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         LA    R8,DXD_TABLE                       FIRST DXD AREA
         L     R7,DXD_VDATA_ADDR                  VDATA ADDRESS
         L     R6,DXD_VDEPTH                      SCREEN DEPTH
PROC0000 DS    0H
         ITRACE ID=SELECT,                                             +
               RDATA1=R7,                                              +
               RDATA2=R8
         CLI   DETAIL_EP_SELECT,C' '              ENTRY POINT SELECTED?
         BNE   PROC0020                           YES
         CLI   DETAIL_DXD_SELECT,C' '             DXD SELECTED?
         BNE   PROC0030                           YES
PROC0010 DS    0H
         LA    R8,AREAL(,R8)                      NEXT AREA
         CLI   0(R8),X'FF'                        END OF TABLE?
         BE    BLD0090                            YES
         A     R7,DXD_VWIDTH                      NEXT LINE
         BCT   R6,PROC0000                        LOOP
         B     BLD0010
PROC0020 DS    0H
         ITRACE ID=EP,                                                 +
               RDATA1=R8,                                              +
               DATA2=(AREA_EP,4)
         MVC   STORE_AREA_NAME,AREA_NAME          COPY AREA NAME
         CLC   AREA_NAME,COMMON_NAME              COMMON AREA?
         BE    PROC0025                           YES
         MVC   STORE_AREA_ADDR,AREA_EP            COPY ADDRESS
         L     R1,AREA_EP
         MVC   STORE_AREA_LENGTH,MODLEN-OSDXD(R1) COPY LENGTH
         B     PROC0060
PROC0025 DS    0H
         ST    R12,STORE_AREA_ADDR                AREA ADDRESS
         MVC   STORE_AREA_LENGTH,COMMON_LENGTH    AREA LENGTH
         B     PROC0060                           DISPLAY COMMON AREA
PROC0030 DS    0H
         ITRACE ID=DXD,                                                +
               RDATA1=R8,                                              +
               DATA2=(AREA_DXD_DISP,4)
         MVC   STORE_AREA_NAME,AREA_DXD_NAME      COPY AREA NAME
         CLC   AREA_DXD_NAME,SESSION_NAME         SESSION?
         BE    PROC0055                           YES
         L     R1,COMM_DXD                        DXD ADDRESS
         A     R1,AREA_DXD_DISP                   PLUS DISP
         ST    R1,STORE_AREA_ADDR                 SET ADDRESS
         L     R1,AREA_DXD_DISP                   DISP
         LA    R14,AREAL(,R8)                     NEXT AREA
         CLI   NEXT.AREA_DXD_DISP,X'FF'           END OF TABLE?
         BE    PROC0040                           YES
         L     R15,NEXT.AREA_DXD_DISP             NEXT DXD AREA DISP
         B     PROC0050
PROC0040 DS    0H
         L     R15,COMM_DXD_LENGTH                TOTAL LENGTH
PROC0050 DS    0H
         SR    R15,R1                             COMPUTE LENGTH
         ST    R15,STORE_AREA_LENGTH              SET LENGTH OF AREA
         B     PROC0060
PROC0055 DS    0H
         ST    R11,STORE_AREA_ADDR                SESSION ADDRESS
         MVC   STORE_AREA_LENGTH,SESSION_LENGTH   SESSION BLOCK LENGTH
PROC0060 DS    0H
         XC    STORE_AREA_ALET,STORE_AREA_ALET    IN OUR ADDRESS SPACE
         LA    R1,STORPARM
         L     R15,COMM_V_OSSTOR                  OSSTOR ENTRY POINT
         ITRACE ID=CALLSTOR,                                           +
               RDATA1=R15,                                             +
               RDATA2=R1
         BALR  R14,R15                            LINK TO OSSTOR
         ITRACE ID=STOR_X
         B     BLD0000                            REBUILD STORAGE
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         L     R0,DXD_VDATA_LENGTH
         L     R1,DXD_VDATA_ADDR
         MVI   COMM_VDATA_FUNC,$VDATA_FREEMAIN
         L     R15,COMM_V_OSVDATA
         BALR  R14,R15                            FREE VDATA
         ITRACE ID=EXIT
         L     R13,4(,R13)                        RESTORE SAVE AREA
         LM    R14,R12,12(R13)                    RESTORE REGISTERS
         SR    R15,R15                            SET RC
         BR    R14                                RETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXD_START      DC    Q(DXDDXD)

DXD_TABLE      DS    0A
               AREA  OSCOMM,SESSION
               AREA  OSMAIN,DXDMAIN
               AREA  OSVDATA,DXDVMGR
               AREA  OSRECALL,DXDRCALL
               AREA  OSPROMPT,DXDPRMPT
               AREA  OSPARMS,DXDPARMS
               AREA  OSPARSE,DXDPARSE
               AREA  OSOFILE,DXDOFILE
               AREA  OSMSG,DXDMSG
               AREA  OSESTAE,WKESTAE
               AREA  OSDXD,DXDDXD
               AREA  OSDSINFO,DXDDSINF
               AREA  OSDSCB,DXDDSCB
               AREA  OSDISP,DXDDISP
               AREA  OSDEBUG,DXDDEBUG
               AREA  OSDAIR,DXDDAIR
               AREA  OSABEND,WKABEND
               AREA  OSCNFRM,DXDCNFRM
               DC    X'FF'

PANEL_NAME     DC    CL8'OSDXD'
DISPLAY        DC    CL8'DISPLAY'

COMMON_NAME    DC    CL8'OSCOMM'
COMMON_LENGTH  DC    A(COMMON_L)

SESSION_NAME   DC    CL8'SESSION'
SESSION_LENGTH DC    A(SESSION_L)

               LTORG

DXDEND         EQU  *
* ------------------------------------------------------------------- *
*                                                                     *
*      To keep the assembler happy....                                *
*                                                                     *
* ------------------------------------------------------------------- *
DXDMAIN           DSECT
DXDVMGR           DSECT
DXDTRACE          DSECT
DXDSESS           DSECT
DXDRCALL          DSECT
DXDPRMPT          DSECT
DXDPARMS          DSECT
DXDPARSE          DSECT
DXDOFILE          DSECT
DXDMSG            DSECT
WKESTAE           DSECT
DXDDXD            DSECT
DXDDSINF          DSECT
DXDDSCB           DSECT
DXDDISP           DSECT
DXDDEBUG          DSECT
DXDDAIR           DSECT
WKABEND           DSECT
DXDCNFRM          DSECT
* ------------------------------------------------------------------- *
*        WORK AREA                                                    *
* ------------------------------------------------------------------- *
DXDDXD            DSECT
                  COPY DXDPREF
DXD_VDEPTH        DS   F
DXD_VWIDTH        DS   F
DXD_VDATA_ADDR    DS   A
DXD_VDATA_LENGTH  DS   F

DXD_SPF_PARMS     DS  0A
DXD_PARM_1        DS   A
DXD_PARM_2        DS   A

DXD_WORK          DS   CL10

DXD_SAVE_PANEL    DS   CL8

                  STORPARM DSECT=NO
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
AREA_DSECT        DSECT
AREA_NAME         DS   CL8
AREA_EP           DS   XL4
AREA_DXD_NAME     DS   CL8
AREA_DXD_DISP     DS   XL4
AREAL             EQU  *-AREA_DSECT

DETAIL            DSECT
DETAIL_ATTR_1     DS   X
DETAIL_EP_SELECT  DS   C
DETAIL_ATTR_2     DS   X
DETAIL_NAME       DS   CL8
DETAIL_ATTR_3     DS   X
                  DS   CL3
DETAIL_EP         DS   CL8
                  DS   CL3
DETAIL_LENGTH     DS   CL8
                  DS   CL3
DETAIL_ATTR_4     DS   X
DETAIL_DXD_SELECT DS   C
DETAIL_ATTR_5     DS   X
DETAIL_DXD_NAME   DS   CL8
DETAIL_ATTR_6     DS   X
                  DS   CL3
DETAIL_DXD_ADDR   DS   CL8
                  DS   CL4
DETAIL_DXD_LENGTH DS   CL8
                  DS   C
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         SESSION
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         OSSPFD   TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         BPXYSTAT DSECT=YES,LIST=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY     ATTRS
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY     REGEQU
         END      OSDXD
