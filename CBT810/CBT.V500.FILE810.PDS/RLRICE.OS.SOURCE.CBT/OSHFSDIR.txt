* ------------------------------------------------------------------- *
*                                                                     *
*  MODULE NAME: OSHFSDIR                                              *
*                                                                     *
*  Read HFS directories                                               *
*                                                                     *
* ------------------------------------------------------------------- *
OSHFSDIR CSECT
OSHFSDIR AMODE 31
OSHFSDIR RMODE 24
         USING OSCOMM,R12
         USING SESSION,R11
         USING OSHFSDIR,R15
         B     INIT0000
MODID    DC    CL8'OSHFSDIR'
         DC    CL8'&SYSDATE'
         DC    CL8'&SYSTIME'
         DC    A(OSHFSDIREND-OSHFSDIR)
INIT0000 DS    0H
         STM   R14,R12,12(R13)                SAVE REGS
         LR    R10,R15                        COPY ENTRY POINT
         DROP  R15
         USING OSHFSDIR,R10                   DEFINE BASE
         GETMAIN RU,                                                   +
               LV=OSHFSDIR_WORK_L,                                     +
               LOC=ANY
         ST    R13,4(,R1)                     SAVE R13
         ST    R1,8(,R13)                     CHAIN SAVE AREA
         LR    R13,R1                         COPY WORK AREA ADDRESS
         USING OSHFSDIR_WORK,R13              DEFINE WORK AREA BASE
         MVC   DXD_CSECT,MODID                COPY CSECT NAME
         ITRACE ID=ENTRY
         MVC   SESS_DATA_LOADER,MODID         IDENTIFY WHO LOADED DATA
         LA    R1,DATASPACE_1                 USE DATA SPACE 1
         ST    R1,DSPCREQ_DATASPACE
         MVC   DSPCREQ_RECORD_NBR,F1
         LA    R1,WORK_RECORD
         ST    R1,DSPCREQ_RECORD_ADDR
         LA    R1,WORK_RECORD_L
         ST    R1,DSPCREQ_RECORD_LENGTH
         MVI   DSPCREQ_FUNC,$DSPCREQ_STORE
         MVC   SESS_FORMAT,HFSDIR
         MVC   SESS_DEFAULT_FORMAT,HFSDIR
* ------------------------------------------------------------------- *
*        OPEN the directory                                           *
* ------------------------------------------------------------------- *
INIT0010 DS    0H
         ITRACE ID=OPEN_DIR,                                           +
               DATA1=(SESS_PATH_LENGTH,4),                             +
               DATA2=(SESS_PATH,8)
         CALL  BPX1OPD,                                                +
               (SESS_PATH_LENGTH,                                      +
               SESS_PATH,                                              +
               WORK_RETURN_VALUE,                                      +
               WORK_RETURN_CODE,                                       +
               WORK_REASON_CODE),                                      +
               VL,                                                     +
               MF=(E,WORK_HFS_PARMLIST)
         ITRACE ID=OPEN_RC,                                            +
               DATA1=(WORK_RETURN_VALUE,4),                            +
               DATA2=(WORK_RETURN_CODE,4)
         ICM   R1,15,WORK_RETURN_VALUE
         BM    ERR0010                        ERROR
         ST    R1,WORK_FILE_DESCRIPTOR
         ITRACE ID=FILEDESC,                                           +
               RDATA1=R1
         LA    R0,SESS_PATH                   PATH USER ENTERED
         LA    R1,SESS_PATH_LENGTH            LENGTH OF PATH ENTERED
         LA    R14,WORK_PATH                  WORK AREA FOR PATH
         LA    R15,L'WORK_PATH                LENGTH OF WORK AREA
         ICM   R15,8,COMM_BLANKS              PAD CHARACTER
         MVCL  R14,R0                         COPY BASE DIRECTORY PATH
         LA    R14,WORK_PATH                  PATH WORK AREA
         L     R15,SESS_PATH_LENGTH           LENGTH USER ENTERED
         AR    R14,R15                        COMPUTE ADDRESS
         BCTR  R14,0                          .. FOR LAST CHARACTER
         CLI   0(R14),C'/'                    ENDING SLASH?
         BE    INIT0020                       YES
         LA    R14,1(,R14)
         MVI   0(R14),C'/'                    INSERT SLASH
         LA    R15,1(,R15)                    ADD 1 TO LENGTH
INIT0020 DS    0H
         LA    R14,1(,R14)                    PLUS 1
         ST    R14,WORK_PATH_X                SAVE ADDRESS
         ST    R15,WORK_PREFIX_LENGTH         SAVE PREFIX LENGTH
         ITRACE ID=PREFIX,                                             +
               RDATA1=R14,                                             +
               RDATA2=R15
* ------------------------------------------------------------------- *
*        READ a directory block                                       *
* ------------------------------------------------------------------- *
MAIN0010 DS    0H
         ITRACE ID=READ,                                               +
               DATA1=(WORK_FILE_DESCRIPTOR,4),                         +
               DATA2=(SESS_IO_AREA,4)
         CALL  BPX1RDD,                       READ DIRECTORY           +
               (WORK_FILE_DESCRIPTOR,         .. FILE DESCRIPTOR       +
               SESS_IO_AREA,                  .. BUFFER ADDRESS        +
               F0,                            .. ALET (HOME space)     +
               COMM_IO_SIZE,                  .. BUFFER SIZE           +
               WORK_RETURN_VALUE,             ..                       +
               WORK_RETURN_CODE,              ..                       +
               WORK_REASON_CODE),             ..                       +
               VL,                                                     +
               MF=(E,WORK_HFS_PARMLIST)
         ITRACE ID=READ_RC,                                            +
               DATA1=(WORK_RETURN_VALUE,4),                            +
               DATA2=(WORK_RETURN_CODE,4)
         ICM   R9,15,WORK_RETURN_VALUE
         BM    ERR0020                        NEGATIVE.. ERROR
         BZ    MAIN0050                       ZERO.. END OF DIRECTORY
         L     R8,SESS_IO_AREA                FIRST ENTRY IN BUFFER
MAIN0020 DS    0H
         CLC   X_00040000,0(R8)               END OF DIRECTORY?
         BE    MAIN0050                       YES
         CLC   DSPCREQ_RECORD_NBR,DATASPACE_1_FIRST_RECORD
         BL    MAIN0030                       READ UNTIL START REACHED
         LH    R1,2(R8)                       LENGTH OF NAME
         CH    R1,H256                        TOO LONG?
         BH    ERR0030                        YES
         STH   R1,WORK_NAME_LENGTH            SET LENGTH
         BCTR  R1,0
         MVC   WORK_NAME,COMM_BLANKS          INITIALIZE NAME
         EX    R1,NAME_MVC                    COPY PATH NAME
         L     R1,WORK_PATH_X
         MVC   0(L'WORK_NAME,R1),WORK_NAME
         L     R1,WORK_PREFIX_LENGTH          PATH PREFIX LENGTH
         AH    R1,WORK_NAME_LENGTH            PLUS NAME LENGTH
         ST    R1,WORK_PATH_LENGTH            TOTAL LENGTH
         MVI   WORK_STAT_STATUS,$WORK_STATUS_OK
         ITRACE ID=GET_STAT,                                           +
               DATA1=WORK_PATH,                                        +
               DATA2=(WORK_PATH_LENGTH,4)
         CALL  BPX1LST,                                                +
               (WORK_PATH_LENGTH,                                      +
               WORK_PATH,                                              +
               STAT_LENGTH,                                            +
               STAT,                                                   +
               WORK_RETURN_VALUE,                                      +
               WORK_RETURN_CODE,                                       +
               WORK_REASON_CODE),                                      +
               VL,                                                     +
               MF=(E,WORK_HFS_PARMLIST)
         ITRACE ID=STAT_RC,                                            +
               DATA1=(WORK_RETURN_VALUE,4),                            +
               DATA2=(WORK_RETURN_CODE,4)
         TM    WORK_RETURN_VALUE,X'80'        SUCCESSFUL?
         BNO   MAIN0030                       YES
         OI    WORK_STAT_STATUS,$WORK_STATUS_ERROR
MAIN0030 DS    0H
         LA    R1,DSPCREQ                     OSDSPACE PARMS
         L     R15,COMM_OSDSPACE              OSDSPACE ENTRY POINT
         BALR  R14,R15                        STORE THIS RECORD
         TM    SESS_KEYWORD_FLAGS,$KEYWORD_RECORDS
         BNO   MAIN0040                       DON'T CHECK
         CLC   DSPCREQ_RECORD_NBR,SESS_NUMBER
         BNL   MAIN0050                       TIME TO STOP
MAIN0040 DS    0H
         L     R1,DSPCREQ_RECORD_NBR          RECORD NUMBER
         LA    R1,1(,R1)                      PLUS 1
         ST    R1,DSPCREQ_RECORD_NBR          SAVE RECORD NUMBER
         AH    R8,0(R8)                       NEXT ENTRY
         BCT   R9,MAIN0020                    PROCESS NEXT ENTRY
         B     MAIN0010                       READ NEXT BLOCK
MAIN0050 DS    0H
         ITRACE ID=CLOSE
         CALL  BPX1CLD,                       CLOSE DIRECTORY          +
               (WORK_FILE_DESCRIPTOR,         .. FILE DESCRIPTOR       +
               WORK_RETURN_VALUE,             ..                       +
               WORK_RETURN_CODE,              ..                       +
               WORK_REASON_CODE),             ..                       +
               VL,                                                     +
               MF=(E,WORK_HFS_PARMLIST)
         ITRACE ID=CLOSE_RC,                                           +
               DATA1=(WORK_RETURN_VALUE,4),                            +
               DATA2=(WORK_RETURN_CODE,4)
         ICM   R9,15,WORK_RETURN_VALUE
         BM    ERR0040                        NEGATIVE.. ERROR
         SR    R3,R3                          SET RC TO ZERO
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0010  DS    0H
         LA    R3,1
         B     ERR1000
ERR0020  DS    0H
         LA    R3,2
         B     ERR1000
ERR0030  DS    0H
         LA    R3,3
         MVC   COMM_INFO_01(2),2(R8)
         MVC   COMM_INFO_02,4(R8)
         B     ERR1010
ERR0040  DS    0H
         LA    R3,5
ERR1000  DS    0H
         MVC   COMM_INFO_01(L'WORK_RETURN_VALUE),WORK_RETURN_VALUE
         MVC   COMM_INFO_02(L'WORK_RETURN_CODE),WORK_RETURN_CODE
         MVC   COMM_INFO_03(L'WORK_REASON_CODE),WORK_REASON_CODE
ERR1010  DS    0H
         STCM  R3,3,COMM_MSG_ID
         MVC   COMM_MSG_CSECT,MODID
         L     R15,COMM_V_OSMSG
         BALR  R14,R15
         LA    R3,8
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=EXIT
         LR    R1,R13                         COPY WORK AREA ADDRESS
         L     R13,4(,R13)                    RESTORE SAVE AREA ADDRESS
         FREEMAIN RU,                                                  +
               A=(1),                                                  +
               LV=OSHFSDIR_WORK_L
         L     R14,12(,R13)
         LR    R15,R3                         SET RC
         LM    R0,R12,20(R13)                 RESTORE REGISTERS
         BR    R14                            RETURN
* ------------------------------------------------------------------- *
NAME_MVC MVC   WORK_NAME(0),4(R8)
PATH_MVC MVC   COMM_INFO_01+8(0),WORK_PATH
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
F0                   DC      F'0'
F1                   DC      F'1'
STAT_LENGTH          DC      A(STAT#LENGTH)
H64                  DC      H'64'
H256                 DC      H'256'
HFSDIR               DC      CL8'HFSDIR'
X_00040000           DC      X'00040000'
                     LTORG

OSHFSDIREND          EQU     *
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
OSHFSDIR_WORK        DSECT
                     COPY    DXDPREF
WORK_FILE_DESCRIPTOR DS      F
WORK_RETURN_VALUE    DS      F
WORK_RETURN_CODE     DS      F
WORK_REASON_CODE     DS      F
WORK_PREFIX_LENGTH   DS      F
WORK_PATH_LENGTH     DS      F
WORK_PATH_X          DS      A
WORK_HFS_PARMLIST    DS      10F

                     DSPCREQ DSECT=NO

WORK_RECORD          DS      0D
WORK_NAME_LENGTH     DS      H
WORK_NAME            DS      CL256
WORK_STAT_STATUS     DS      X           STATUS OF BYXYSTAT INFO
$WORK_STATUS_OK      EQU     X'00'       .. BPXYSTAT INFO IS VALID
$WORK_STATUS_ERROR   EQU     X'FF'       .. ERROR DURING LSTAT CALL
                     BPXYSTAT DSECT=NO,LIST=YES
WORK_RECORD_L        EQU     *-WORK_RECORD

WORK_PATH            DS      CL512

OSHFSDIR_WORK_L      EQU     *-OSHFSDIR_WORK
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         BPXYSTAT DSECT=YES,LIST=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              COPY REGEQU
$BUFFSIZE     EQU  65535
              END  OSHFSDIR
