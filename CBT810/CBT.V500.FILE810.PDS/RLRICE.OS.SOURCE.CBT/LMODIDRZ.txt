* ------------------------------------------------------------------- *
*                                                                     *
*  Module name: LMODIDRZ                                              *
*                                                                     *
*  Process IDRZ (AMASPZAP) data                                       *
*                                                                     *
* ------------------------------------------------------------------- *
LMODIDRZ CSECT
LMODIDRZ AMODE 31
LMODIDRZ RMODE ANY
         USING LMODIDRZ,R15
         USING OSCOMM,R12
         USING SESSION,R11
         B     INIT0000
MODID    DC    CL8'LMODIDRZ'
         DC    CL8'&SYSDATE'
         DC    CL8'&SYSTIME'
         DC    A(IDRZEND-LMODIDRZ)
INIT0000 DS    0H
         STM   R14,R12,12(R13)                  SAVE REGISTERS
         LR    R10,R15                          COPY BASE ADDRESS
         LA    R9,2048(,R10)
         LA    R9,2048(,R9)
         USING LMODIDRZ,R10,R9
         DROP  R15
         L     R15,SESS_DXD_ADDR
         A     R15,DXD_START                    PLUS OFFSET
         ST    R13,4(,R15)
         ST    R15,8(,R13)
         LR    R13,R15                          COPY DXD AREA ADDRESS
         USING DXDIDRZ,R13                      DEFINE WORK AREA BASE
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY
         L     R5,COMM_NEW_SESSION
NEW      USING SESSION,R5
         XC    COMM_MSG_ID,COMM_MSG_ID
         L     R9,COMM_OSSPFD
         USING OSSPFD,R9
         MVC   DXD_IEWBIND(IEWBIND_L),IEWBIND_I
         MVI   DXD_FLAGS,0
         LA    R1,NEW.DATASPACE_1
         ST    R1,DSPCREQ_DATASPACE
* ------------------------------------------------------------------- *
*        Obtain and initialize IDRZ buffer                            *
* ------------------------------------------------------------------- *
         ITRACE ID=GET_SECT
         IEWBUFF FUNC=GETBUF,                                          +
               TYPE=IDRZ
         ST    R8,DXD_IDRZ_BUFFER
         ITRACE ID=INITIDRZ,                                           +
               RDATA1=R8,                                              +
               RDATA2=R7
         IEWBUFF FUNC=INITBUF,                                         +
               TYPE=IDRZ
         MVC   NEW.DATASPACE_1_FIRST_RECORD,F1
* ------------------------------------------------------------------- *
*        Store the info for the specified section                     *
* ------------------------------------------------------------------- *
         LA    R1,LMOD_IDRZ_DATA
         ST    R1,DSPCREQ_RECORD_ADDR
         LA    R1,LMOD_IDRZ_DATA_L
         ST    R1,DSPCREQ_RECORD_LENGTH
         XC    DSPCREQ_RECORD_NBR,DSPCREQ_RECORD_NBR
         MVI   DSPCREQ_FUNC,$DSPCREQ_STORE
         XC    DXD_IDRZ_CURSOR,DXD_IDRZ_CURSOR
* ------------------------------------------------------------------- *
*                                                                     *
*     Store the IDRZ info                                             *
*                                                                     *
*     R8 IS THE IDRZ HEADER (IDZH) BASE                               *
*     R7 IS THE IDRZ ENTRY (IDZ_ENTRY) BASE                           *
*                                                                     *
* ------------------------------------------------------------------- *
IDRZ0000 DS    0H
         ITRACE ID=GETDIDRZ,                                           +
               DATA1=NEW.SESS_DIALOG_SECTION_LENGTH+0,                 +
               DATA2=NEW.SESS_DIALOG_SECTION_LENGTH+8
         L     R8,DXD_IDRZ_BUFFER
         LA    R7,IDZH_END
         IEWBIND FUNC=GETD,                                            +
               VERSION=5,                                              +
               WORKMOD=NEW.SESS_DIALOG_WORKMOD,                        +
               CLASS=B_IDRZ,                                           +
               SECTION=NEW.SESS_DIALOG_SECTION_LENGTH,                 +
               AREA=IEWBIDZ,                                           +
               CURSOR=DXD_IDRZ_CURSOR,                                 +
               COUNT=DXD_IDRZ_RETURNED,                                +
               RETCODE=DXD_RETURN_CODE,                                +
               RSNCODE=DXD_REASON_CODE,                                +
               MF=(E,DXD_IEWBIND,COMPLETE)
         ITRACE ID=GETD_RC,                                            +
               DATA1=(DXD_RETURN_CODE,4),                              +
               DATA2=(DXD_REASON_CODE,4)
         CLC   DXD_RETURN_CODE,F8
         BH    ERR0010
         BE    ERR0020                          NO DATA
         CLC   DXD_RETURN_CODE,F4
         BL    IDRZ0010
         CLC   DXD_REASON_CODE,X_83000800
         BE    IDRZ0010
         CLC   DXD_REASON_CODE,X_83000801
         BE    ERR0030
IDRZ0010 DS    0H
         ICM   R4,15,DXD_IDRZ_RETURNED
         BZ    ERR0020
         OI    DXD_FLAGS,$FLAGS_DISPLAYED
IDRZ0020 DS    0H
         ITRACE ID=BLD_SECT,                                           +
               RDATA1=R7,                                              +
               RDATA2=R4

* ----------------  Build data -------------------------------------- *

         MVC   LMOD_IDRZ_DATE,IDZ_DATE
         MVC   LMOD_IDRZ_ZAP_DATA,IDZ_ZAP_DATA
         SR    R14,R14
         ICM   R14,3,IDZ_RESIDENT_CHARS
         BZ    IDRZ0040
         CH    R14,=Y(L'LMOD_IDRZ_RESIDENT)
         BL    IDRZ0030
         LH    R14,=Y(L'LMOD_IDRZ_RESIDENT)
IDRZ0030 DS    0H
         BCTR  R14,0
         ICM   R15,15,IDZ_RESIDENT_PTR
         EX    R14,RESIDENT_MVC

* ----------------  Store record just built ------------------------- *
IDRZ0040 DS    0H
         L     R1,DSPCREQ_RECORD_NBR
         LA    R1,1(,R1)
         ST    R1,DSPCREQ_RECORD_NBR
         ITRACE ID=STORE,                                              +
               RDATA1=R1
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE
         BALR  R14,R15
         CLI   DSPCREQ_RC,$DSPCREQ_FULL
         BE    IDRZ0050
         CLI   DSPCREQ_RC,$DSPCREQ_OK
         BNE   ERR0040

* ----------------  Locate next record in buffer -------------------- *
         A     R7,IDZH_ENTRY_LENG               PLUS LENGTH
         BCT   R4,IDRZ0020                      LOOP
         CLC   DXD_REASON_CODE,X_83000800       MORE DATA?
         BNE   IDRZ0000                         YES
         ITRACE ID=DONE
         B     IDRZ0060

* ------------------------------------------------------------------- *
IDRZ0050 DS    0H
         ITRACE ID=FULL
         L     R1,DSPCREQ_RECORD_NBR
         BCTR  R1,0
         ST    R1,DSPCREQ_RECORD_NBR
         MVC   SPF_MSG_1,COMM_MSG_1
         MVC   SPF_MSG_2,COMM_MSG_2
         MVC   SPF_MSG_3,COMM_MSG_3
         MVC   SPF_MSG_4,COMM_MSG_4
         MVC   SPF_MSG_5,COMM_MSG_5
         MVI   COMM_SESS_FUNC,$SESS_REMOVE
IDRZ0060 DS    0H
         MVC   NEW.DATASPACE_1_DISPLAY_FIRST_RECORD,F1

         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
RESIDENT_MVC   MVC   LMOD_IDRZ_RESIDENT(0),0(R15)
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0010  DS    0H
         ITRACE ID=ERR0010
         MVI   COMM_MSG_ID+1,1
         MVC   COMM_INFO_01(20),INFO_GET_IDRZ
         MVC   COMM_INFO_02,DXD_RETURN_CODE
         MVC   COMM_INFO_03,DXD_REASON_CODE
         B     ERR0100
ERR0020  DS    0H
         MVC   COMM_INFO_01,NEW.SESS_DIALOG_SECTION
         TM    DXD_FLAGS,$FLAGS_DISPLAYED
         BO    ERR0030
         MVI   COMM_MSG_ID+1,2
         B     ERR0100
ERR0030  DS    0H
         MVC   COMM_INFO_01,NEW.SESS_DIALOG_SECTION
         MVI   COMM_MSG_ID+1,3
         B     ERR0100
ERR0040  DS    0H
         MVC   SPF_MSG_1,COMM_MSG_1
         MVC   SPF_MSG_2,COMM_MSG_2
         MVC   SPF_MSG_3,COMM_MSG_3
         MVC   SPF_MSG_4,COMM_MSG_4
         MVC   SPF_MSG_5,COMM_MSG_5
         B     EXIT0000
ERR0100  DS    0H
         ITRACE ID=CALL_MSG,                                           +
               DATA1=(COMM_MSG_ID,2)
         MVC   COMM_MSG_CSECT,MODID
         L     R15,COMM_V_OSMSG
         BALR  R14,R15
         MVC   SPF_MSG_1,COMM_MSG_1
         MVC   SPF_MSG_2,COMM_MSG_2
         MVC   SPF_MSG_3,COMM_MSG_3
         MVC   SPF_MSG_4,COMM_MSG_4
         MVC   SPF_MSG_5,COMM_MSG_5
         MVI   COMM_SESS_FUNC,$SESS_REMOVE
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=EXIT
         L     R13,4(,R13)                      RESTORE R13
         LM    R14,R12,12(R13)                  RESTORE REGISTERS
         SR    R15,R15                          SET RC
         BR    R14                              RETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         IEWBUFF FUNC=MAPBUF,                                          +
               TYPE=IDRZ,                                              +
               HEADREG=R8,                                             +
               ENTRYREG=R7,                                            +
               SIZE=50,                                                +
               VERSION=5

               IEWBIND MF=(L,IEWBIND_I)
IEWBIND_L      EQU     *-IEWBIND_I
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXD_START            DC   Q(DXDIDRZ)

F1                   DC   F'1'
F4                   DC   F'4'
F8                   DC   F'8'
B_IDRZ               DC   H'6',C'B_IDRZ'
X_83000800           DC   X'83000800'
X_83000801           DC   X'83000801'
INFO_GET_IDRZ        DC   CL20'GET IDRZ info'
IDRZEND              EQU  *
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDIDRZ              DSECT
                     COPY  DXDPREF

DXD_IEWBIND          DS    0F,(IEWBIND_L)X

DXD_RETURN_CODE      DS    XL4            RETURN CODE
DXD_REASON_CODE      DS    XL4            REASON CODE

DXD_IDRZ_BUFFER      DS    A

DXD_IDRZ_RETURNED    DS    F
DXD_IDRZ_CURSOR      DS    F

DXD_FLAGS            DS    X
$FLAGS_DISPLAYED     EQU   X'80'            SOME DATA DISPLAYED

                     DSPCREQ  DSECT=NO
                     COPY  IDRZDATA
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         SESSION
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         BPXYSTAT DSECT=YES,LIST=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         OSSPFD   TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY     REGEQU
         END      LMODIDRZ
