* ------------------------------------------------------------------- *
*                                                                     *
*  Module name: OSDIR                                                 *
*                                                                     *
*  Build main body for non-load module PDS or PDSE directories.       *
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
*  Jan 2010                                                           *
*  This thing has a lot of functions so it is large.                  *
*  It was using three base registers.                                 *
*  I decided to change it to use the newer "relative addressing mode" *
*  instructions where possible.  All the code that needs a "base"     *
*  register should be "below" label BASED_AREA.                       *
*                                                                     *
*                                                                     *
*  Register usage                                                     *
*        R12      OSCOMM address                                      *
*        R11      SESSION address                                     *
*        R10      BASED area address                                  *
*        R9       BAL, BRAS, linkage                                  *
*        R8       DCB address                                         *
*        R7       Dynamic area (VDATA)                                *
*        R6       Lines available when building data                  *
*                 Lines left to process when processing line commands *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
OSDIR    CSECT
OSDIR    AMODE 31
OSDIR    RMODE ANY
         USING OSCOMM,R12
         USING SESSION,R11
         USING VDATA,R7
         USING IHADCB,R8
         J     INIT0000
MODID    DC    CL8'OSDIR'
         DC    CL8'&SYSDATE'
         DC    CL8'&SYSTIME'
         DC    A(OSDIREND-OSDIR)
INIT0000 DS    0H
         USING OSCOMM,R12                     DEFINE COMMON AREA BASE
         STM   R14,R12,12(R13)                SAVE REGS
         LARL  R10,BASED_AREA
         USING BASED_AREA,R10
         L     R5,COMM_OSSPFD                 SPF DATA
         USING OSSPFD,R5                      DEFINE BASE
         CLI   SESS_FORMAT_FLAGS,$FORMAT_INITIALIZE
         JNE   INIT0010
* ------------------------------------------------------------------- *
*        Perform initialization                                       *
* ------------------------------------------------------------------- *
         OC    SESS_DXD_ADDR,SESS_DXD_ADDR
         JNZ   ERR0150
         LA    R0,OSDIR_DXD_L
         ST    R0,SESS_DXD_LENGTH
         BRAS  R9,GETMAIN1
         ST    R1,SESS_DXD_ADDR               SAVE WORK AREA ADDRESS
         LR    R2,R1
         LR    R0,R1
         LA    R1,OSDIR_DXD_L
         SR    R14,R14
         SR    R15,R15
         MVCL  R0,R14
         ST    R13,4(,R2)                     SAVE R13
         ST    R2,8(,R13)                     CHAIN SAVE AREA
         LR    R13,R2                         COPY WORK AREA ADDRESS
         USING OSDIR_WORK,R13                 DEFINE WORK AREA BASE
         LARL  R1,MODID
         MVC   DXD_CSECT,0(R1)                COPY MODID
         ITRACE ID=ENTRY1
         XC    DXD_RC,DXD_RC                  INITIALIZE RC
         LA    R1,DIR_RECORD                  I/O AREA ADDRESS
         ST    R1,DSPCREQ_RECORD_ADDR
         ITRACE ID=I_O_AREA,                                           +
               RDATA1=R1
         MVC   DXD_OPEN(OPEN_L),OPEN_I        INITIALIZE OPEN
         MVC   DXD_CLOSE(CLOSE_L),CLOSE_I     INITIALIZE CLOSE
         MVI   LOADREQ_FUNC,$LOADREQ_LOAD     SET OSLOAD FUNCTION
         LA    R0,DATASPACE_1                 USE DATA SPACE 1
         ST    R0,DSPCREQ_DATASPACE
         ITRACE ID=VDATA
         MVC   SESS_DISP_PANEL,DIR_PANEL      SET PANEL NAME
         LA    R1,SESS_DISP_PANEL
         MVI   COMM_VDATA_FUNC,$VDATA_GETMAIN
         L     R15,COMM_V_OSVDATA
         BALR  R14,R15
         ST    R0,DXD_VDATA_LENGTH            SAVE VDATA LENGTH
         ST    R1,DXD_VDATA_ADDR              SAVE VDATA ADDR
         MVC   DXD_VWIDTH,SPF_VWIDTH          SAVE WIDTH
         MVC   DXD_VDEPTH,SPF_VDEPTH          SAVE DEPTH
         ICM   R8,15,SESS_DCB_ADDR            DCB ALREADY PRESENT?
         JNZ   EXIT0000                       YES
         LA    R0,PDSDCB_L                    DCB LENGTH
         ST    R0,SESS_DCB_LENGTH
         BRAS  R9,GETMAIN1
         LR    R8,R1                          SAVE DCB ADDRESS
         ST    R8,SESS_DCB_ADDR
         L     R1,=A(PDSDCB_I)
         MVC   0(PDSDCB_L,R8),0(R1)           INITIALIZE DCB
         MVC   DCBDDNAM,SESS_DD               SET DD NAME
         J     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
INIT0010 DS    0H
         ICM   R15,15,SESS_DXD_ADDR
         JZ    ERR0200
         ST    R13,4(,R15)
         ST    R15,8(,R13)
         LR    R13,R15
         ITRACE ID=ENTRY2,                                             +
               DATA1=(SESS_FORMAT_FLAGS,1),                            +
               DATA2=(COMM_SESS_FUNC,1)
         XC    DXD_RC,DXD_RC
         CLI   SESS_FORMAT_FLAGS,$FORMAT_CONTROL
         JE    EXIT0000
         CLI   SESS_FORMAT_FLAGS,$FORMAT_CLEANUP
         JE    CLEAN000
         L     R8,SESS_DCB_ADDR               DCB ADDRESS
         LARL  R1,RTRY0000                    RETRY ADDRESS
         ST    R1,SESS_RETRY
         CLI   COMM_SESS_FUNC,$SESS_SWITCH    SESSION SWITCH?
         JNE   MAIN0000                       NO
         TM    DXD_FLAGS,$DXD_INIT            EVER BUILT DATA?
         JNO   MAIN0000                       NO
         L     R6,DXD_LINES_REMAINING         LINES REMAINING
         L     R7,DXD_CURRENT_VDATA           POSITION IN VDATA
         ITRACE ID=RESUME,                                             +
               RDATA1=R6,                                              +
               RDATA2=R7
         LTR   R6,R6                          RESUMING LINE CMDS?
         JNZ   MAIN0210                       YES
         J     MAIN0180                       RESUME AFTER PRIMARY CMD
* ------------------------------------------------------------------- *
*          (re)build display                                          *
* ------------------------------------------------------------------- *
MAIN0000 DS    0H
         OI    DXD_FLAGS,$DXD_INIT            SET FLAG
         MVC   DSPCREQ_RECORD_NBR,DATASPACE_1_DISPLAY_FIRST_RECORD
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0020 DS    0H
         XC    DXD_LINES,DXD_LINES            LINES BUILT
         L     R0,DXD_VDATA_ADDR              VDATA ADDRESS
         L     R1,DXD_VDATA_LENGTH            VDATA LENGTH
         SR    R14,R14                        CLEAR R14
         SR    R15,R15                        CLEAR R15
         MVCL  R0,R14                         INITIALIZE VDATA BUFFER
         CLI   SESS_DATA_TYPE,$DATA_TYPE_PDS_DIR DIRECTORY DATA?
         JNE   ERR0220                        NO
         L     R6,DXD_VDEPTH                  LINES AVAILABLE
         L     R7,DXD_VDATA_ADDR              BUFFER ADDRESS
* ------------------------------------------------------------------- *
*        Add heading to body                                          *
* ------------------------------------------------------------------- *
         ITRACE ID=HEADING,                                            +
               RDATA1=R6,                                              +
               RDATA2=R7
         LA    R2,HEADING_01_L                HEADING LENGTH
         C     R2,DXD_VWIDTH                  GREATER THAN SCREEN WIDTH
         JNH   MAIN0030                       NO
         L     R2,DXD_VWIDTH                  LIMIT TO SCREEN WIDTH
MAIN0030 DS    0H
         BCTR  R2,0                           INSTRUCTION LENGTH
         EX    R2,MVC_01                      COPY HEADING
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         XC    DXD_CURSOR_POSITION,DXD_CURSOR_POSITION
         J     MAIN0160
* ------------------------------------------------------------------- *
*        "Retrieve" data and build body of SPF panel                  *
* ------------------------------------------------------------------- *
MAIN0040 DS    0H
         ITRACE ID=RETRIEVE,                                           +
               DATA1=(DSPCREQ_RECORD_NBR,4),                           +
               DATA2=(DATASPACE_1_LAST_RECORD,4)
         CLC   DSPCREQ_RECORD_NBR,DATASPACE_1_LAST_RECORD
         JH    MAIN0180
         MVI   DSPCREQ_FUNC,$DSPCREQ_RETRIEVE
         ITRACE ID=GET_RECD,                                           +
               DATA1=(DSPCREQ_RECORD_NBR,4),                           +
               DATA2=(DSPCREQ_RECORD_ADDR,4)
         MVI   DSPCREQ_FUNC,$DSPCREQ_RETRIEVE
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE
         BALR  R14,R15
         CLI   DSPCREQ_RC,$DSPCREQ_OK
         JNE   ERR0010
         TM    SESS_USER_OPTIONS,$OPTIONS_SHOW_TAGGED
         JO    MAIN0050
         TM    SESS_USER_OPTIONS,$OPTIONS_SHOW_NOT_TAGGED
         JO    MAIN0060
         J     MAIN0070
MAIN0050 DS    0H
         TM    DIR_FLAGS,$DIR_TAGGED
         JNO   MAIN0170
         J     MAIN0070
MAIN0060 DS    0H
         TM    DIR_FLAGS,$DIR_TAGGED
         JO    MAIN0170
MAIN0070 DS    0H
         MVC   VDATA,COMM_BLANKS
         MVI   VDATA_ATTR_1,$SCREEN_ATTR_INPUT
         MVC   VDATA_MEMBER,DIR_NAME
         TM    DIR_FLAGS,$DIR_TAGGED
         JO    MAIN0080
         MVI   VDATA_ATTR_2,$SCREEN_ATTR_NORMAL
         J     MAIN0090
MAIN0080 DS    0H
         MVI   VDATA_ATTR_2,$SCREEN_ATTR_HIGH
MAIN0090 DS    0H
         MVI   VDATA_ATTR_3,$SCREEN_ATTR_NORMAL
         UNPK  DXD_WORK_1(7),DIR_TTR(4)
         TR    DXD_WORK_1(6),COMM_HEXCHAR
         MVC   VDATA_TTR,DXD_WORK_1
         TM    DIR_FLAGS,$DIR_DELETED
         JO    MAIN0120
         TM    DIR_FLAGS,$DIR_RENAMED
         JO    MAIN0130
         TM    DIR_FLAGS,$DIR_SPF
         JNO   MAIN0140
* ----------       FORMAT SPF STATISTICS            ----------------- *
         SR    R1,R1
         IC    R1,DIR_SPF_VERSION
         CVD   R1,COMM_DWORD
         MVC   DXD_WORK_1(L'EDIT_WORD_1),EDIT_WORD_1
         ED    DXD_WORK_1(L'EDIT_WORD_1),COMM_DWORD+4
         MVC   VDATA_VERSION,DXD_WORK_1+6
         OI    VDATA_VERSION,C'0'
         MVI   VDATA_DOT,C'.'
         IC    R1,DIR_SPF_LEVEL
         CVD   R1,COMM_DWORD
         MVC   DXD_WORK_1(L'EDIT_WORD_1),EDIT_WORD_1
         ED    DXD_WORK_1(L'EDIT_WORD_1),COMM_DWORD+4
         MVC   VDATA_LEVEL,DXD_WORK_1+6
         OI    VDATA_LEVEL,C'0'
         ICM   R1,3,DIR_SPF_SIZE
         CVD   R1,COMM_DWORD
         MVC   DXD_WORK_1(L'EDIT_WORD_1),EDIT_WORD_1
         ED    DXD_WORK_1(L'EDIT_WORD_1),COMM_DWORD+4
         MVC   VDATA_SIZE,DXD_WORK_1+1
         ICM   R1,3,DIR_SPF_INIT
         CVD   R1,COMM_DWORD
         MVC   DXD_WORK_1(L'EDIT_WORD_1),EDIT_WORD_1
         ED    DXD_WORK_1(L'EDIT_WORD_1),COMM_DWORD+4
         MVC   VDATA_INIT,DXD_WORK_1+1
AP1      AP    DIR_SPF_C_DATE,P1900000
         MVC   DXD_WORK_1(L'EDIT_WORD_2),EDIT_WORD_2
         ED    DXD_WORK_1(L'EDIT_WORD_2),DIR_SPF_C_DATE
         MVC   VDATA_CREATE_DATE,DXD_WORK_1+1
MAIN0100 DS    0H
AP2      AP    DIR_SPF_M_DATE,P1900000
         MVC   DXD_WORK_1(L'EDIT_WORD_2),EDIT_WORD_2
         ED    DXD_WORK_1(L'EDIT_WORD_2),DIR_SPF_M_DATE
         MVC   VDATA_MOD_DATE,DXD_WORK_1+1
MAIN0110 DS    0H
         UNPK  DXD_WORK_1(7),DIR_SPF_M_TIME(4)
         TR    DXD_WORK_1(6),COMM_HEXCHAR
         MVC   VDATA_MOD_TIME+0(2),DXD_WORK_1+0
         MVI   VDATA_MOD_TIME+2,C':'
         MVC   VDATA_MOD_TIME+3(2),DXD_WORK_1+2
         MVI   VDATA_MOD_TIME+5,C':'
         MVC   VDATA_MOD_TIME+6(2),DXD_WORK_1+4
         MVC   VDATA_USER,DIR_SPF_USER
         J     MAIN0140
MAIN0120 DS    0H
         MVI   VDATA_ATTR_2,$SCREEN_ATTR_HIGH_TURQUOISE
         MVC   VDATA_STATISTICS(DELETED_MSG_L),DELETED_MSG
         J     MAIN0140
MAIN0130 DS    0H
         MVI   VDATA_ATTR_2,$SCREEN_ATTR_HIGH_TURQUOISE
         MVC   VDATA_STATISTICS(RENAMED_MSG_L),RENAMED_MSG
MAIN0140 DS    0H
         L     R1,DSPCREQ_RECORD_NBR          CURRENT RECORD NUMBER
         A     R1,F1                          ADD 1
         ST    R1,DSPCREQ_RECORD_NBR          SAVE NEW RECORD NUMBER
         LH    R1,DXD_LINES                   LINES DISPLAYED
         A     R1,F1                          ADD 1
         STH   R1,DXD_LINES                   SAVE LINES DISPLAYED
MAIN0160 DS    0H
         ITRACE ID=LOOP,                                               +
               RDATA1=R6,                                              +
               RDATA2=R7
         A     R7,DXD_VWIDTH                  NEXT LINE
         BRCT  R6,MAIN0040                    MINUS 1 LINE
         J     MAIN0180
MAIN0170 DS    0H
         L     R1,DSPCREQ_RECORD_NBR          CURRENT RECORD NUMBER
         A     R1,F1                          ADD 1
         ST    R1,DSPCREQ_RECORD_NBR          SAVE NEW RECORD NUMBER
         J     MAIN0040
* ------------------------------------------------------------------- *
*        Build the DSCB info and headings                             *
* ------------------------------------------------------------------- *
MAIN0180 DS    0H
         ITRACE ID=OSDSCB
         MVC   DATASPACE_1_DISPLAY_LAST_RECORD,DSPCREQ_RECORD_NBR
         MVC   SPF_COLUMN_FROM,H1             INITIALIZE COLUMN
         L     R15,COMM_V_OSDSCB              OSDSCB ENTRY POINT
         BALR  R14,R15                        FORMAT THE DSCB INFO
* ------------------------------------------------------------------- *
*        Display the panel.                                           *
* ------------------------------------------------------------------- *
MAIN0185 DS    0H
         ITRACE ID=DISPLAY
         MVC   SESS_DISP_PANEL,DIR_PANEL      SET PANEL NAME
         MVC   SESS_DISP_VDATA_ADDR,DXD_VDATA_ADDR
         MVC   SESS_DISP_VDATA_LENGTH,DXD_VDATA_LENGTH
         MVC   SESS_DISP_CURSOR,DXD_CURSOR_POSITION
         L     R15,COMM_V_OSDISP
         BALR  R14,R15                        LINK TO OSDISP
         ITRACE ID=DISP_RC,                                            +
               DATA1=(SESS_RC,2),                                      +
               DATA2=(SESS_STATUS,1)
         CLC   COMM_NEW_FORMAT,COMM_BLANKS    NEW FORMAT WANTED?
         JNE   EXIT0000                       YES
         CLI   COMM_SESS_FUNC,0               SESSION FUNCTION?
         JNE   MAIN0190                       YES
         OC    SESS_RC,SESS_RC                DISPLAY RC=0?
         JZ    MAIN0200                       YES
         ITRACE ID=ENDING
         MVI   COMM_SESS_FUNC,$SESS_REMOVE
         J     EXIT0000
* ------------------------------------------------------------------- *
*        Save VDATA address and lines remaining                       *
* ------------------------------------------------------------------- *
MAIN0190 DS    0H
         ITRACE ID=SAVE_6_7,                                           +
               RDATA1=R6,                                              +
               RDATA2=R7
         ST    R6,DXD_LINES_REMAINING         SAVE LIENS REMAINING
         ST    R7,DXD_CURRENT_VDATA           SAVE ADDR IN VDATA
         J     EXIT0000
* ------------------------------------------------------------------- *
*        Process any line command(s)                                  *
* ------------------------------------------------------------------- *
MAIN0200 DS    0H
         NI    DXD_FLAGS,255-$LINE_CMD        RESET INDICATOR
         MVC   DSPCREQ_RECORD_NBR,DATASPACE_1_DISPLAY_FIRST_RECORD
         SR    R6,R6
         ICM   R6,3,DXD_LINES                 LINES OF DISPLAY BUILT
         JZ    MAIN0250                       NONE
         L     R7,DXD_VDATA_ADDR              SPF DISPLAY BUFFER ADDR
         ITRACE ID=CMDS,                                               +
               RDATA1=R7,                                              +
               RDATA2=R6
         A     R7,DXD_VWIDTH                  SKIP HEADING LINE
         L     R1,DXD_VWIDTH                  SCREEN WIDTH
         AH    R1,=Y(VDATA_SELECTION-VDATA+1) PLUS DISP TO SELECTION
         ST    R1,DXD_CURSOR_POSITION         INIT CURSOR POSITION
MAIN0210 DS    0H
         ITRACE ID=LINE_CMD,                                           +
               DATA1=(VDATA_SELECTION,1),                              +
               DATA2=VDATA_MEMBER
         OI    VDATA_SELECTION,C' '           'TRANSLATE' TO UPPER CASE
         CLI   VDATA_SELECTION,C' '           BLANK?
         JE    MAIN0230                       YES
         CLC   DELETED_MSG(DELETED_MSG_L),VDATA_STATISTICS
         JE    ERR0130
         CLC   RENAMED_MSG(RENAMED_MSG_L),VDATA_STATISTICS
         JE    ERR0130
         MVI   SESS_COMMAND_FLAGS,$SESS_COMMAND_SEARCH
         LA    R1,VDATA_SELECTION             COMMAND ADDRESS
         ST    R1,SESS_COMMAND_ADDRESS        SET ADDRESS
         MVC   SESS_COMMAND_LENGTH,H1         SET LENGTH
         LARL  R0,LINE_TABLE                  LIST OF COMMANDS
         ST    R0,COMM_MAIN_COMMAND_TABLE     SET ADDRESS
         XC    COMM_SUB_COMMAND_TABLE,COMM_SUB_COMMAND_TABLE
         L     R15,COMM_V_OSCMD               OSCMD ENTRY POINT
         BALR  R14,R15                        SEARCH COMMANDS
         TM    SESS_COMMAND_FLAGS,$SESS_COMMAND_FOUND
         BOR   R15                            YES.. BRANCH
         MVC   COMM_INFO_01,VDATA_SELECTION
         MVC   COMM_MSG_ID,H1                 SET MESSAGE ID
         BRAS  R9,MSG0000
         MVC   SPF_MSG_1,COMM_MSG_1           COPY MESSAGE
         J     MAIN0180                       DISPLAY WITH MESSAGE
MAIN0220 DS    0H
         OI    DXD_FLAGS,$LINE_CMD            LINE CMD WAS FOUND
         MVI   VDATA_SELECTION,C' '           RESET CMD
MAIN0230 DS    0H
         L     R1,DSPCREQ_RECORD_NBR          RECORD NUMBER
         A     R1,F1                          PLUS 1
         ST    R1,DSPCREQ_RECORD_NBR          SAVE RECORD NUMBER
MAIN0240 DS    0H
         L     R1,DXD_CURSOR_POSITION         CURSOR POSITION
         A     R1,DXD_VWIDTH                  NEXT LINE
         ST    R1,DXD_CURSOR_POSITION         UPDATE CURSOR POSITION
         A     R7,DXD_VWIDTH                  NEXT LINE
         BRCT  R6,MAIN0210                    LOOP
         XC    DXD_CURSOR_POSITION,DXD_CURSOR_POSITION
         TM    DXD_FLAGS,$LINE_CMD            LINE COMMAND FOUND?
         JO    MAIN0180                       YES
* ------------------------------------------------------------------- *
*                                                                     *
*       No line command(s) were found.                                *
*                                                                     *
*       Process any primary command.                                  *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0250 DS    0H
         CLC   SPF_ZCMD,COMM_BLANKS           ANY PRIMARY COMMAND?
         JE    MAIN0330                       NO
         MVC   SESS_COMMAND_ADDRESS,OPERAND_01_ADDRESS
         MVC   SESS_COMMAND_LENGTH,OPERAND_01_LENGTH
         MVI   SESS_COMMAND_FLAGS,$SESS_COMMAND_SEARCH
         LARL  R0,COMMAND_TABLE
         ST    R0,COMM_MAIN_COMMAND_TABLE
         XC    COMM_SUB_COMMAND_TABLE,COMM_SUB_COMMAND_TABLE
         L     R15,COMM_V_OSCMD
         BALR  R14,R15                        SEARCH COMMAND
         TM    SESS_COMMAND_FLAGS,$SESS_COMMAND_FOUND
         JNO   ERR0080                        NOT FOUND
         LH    R3,COMM_OPERANDS_NBR           NBR OF OPERANDS
         BR    R15                            PROCESS COMMAND
* ------------------------------------------------------------------- *
*                                                                     *
*        Check for a scroll command.                                  *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0330 DS    0H
         ICM   R1,15,SPF_ZSCROLLN             NUMBER OF LINES TO SCROLL
         JZ    MAIN0340                       ZERO..
         BCTR  R1,0                           MINUS 1 (HEADING LINE)
MAIN0340 DS    0H
         ST    R1,SESS_VERTICAL_SCROLL
         MVC   SESS_HORIZONTAL_SCROLL,SPF_ZSCROLLN
         ITRACE ID=ZVERB,                                              +
               DATA1=SPF_ZVERB,                                        +
               DATA2=(SPF_ZSCROLLN,4)
         CLI   SPF_ZVERB,C'B'                 BOTTOM?
         JE    MAIN0400                       YES
         CLI   SPF_ZVERB,C'D'                 SCROLL DOWN?
         JE    MAIN0370                       YES
         CLI   SPF_ZVERB,C'L'                 SCROLL LEFT?
         JE    MAIN0180                       YES
         CLI   SPF_ZVERB,C'R'                 SCROLL RIGHT?
         JE    MAIN0180                       YES
         CLI   SPF_ZVERB,C'T'                 TOP?
         JE    MAIN0390                       YES
         CLI   SPF_ZVERB,C'U'                 SCROLL UP?
         JE    MAIN0350                       YES
         J     MAIN0180                       DISPLAY SAME DATA AGAIN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0350 DS    0H
         ITRACE ID=UP
         MVC   SPF_ZCMD,COMM_BLANKS                 CLEAR ZCMD
         CLI   SPF_ZSCROLLA,C'M'                    MAX?
         JE    MAIN0390                             YES
         L     R1,SESS_VERTICAL_SCROLL              NUMBER TO SCROLL
         L     R2,DATASPACE_1_DISPLAY_FIRST_RECORD  1ST ON DISPLAY
         SR    R2,R1                                MINUS SCROLL
         C     R2,DATASPACE_1_FIRST_RECORD          LESS THAN FIRST?
         JNL   MAIN0360                             NO
         L     R2,DATASPACE_1_FIRST_RECORD          FORCE TO FIRST
MAIN0360 DS    0H
         ST    R2,DATASPACE_1_DISPLAY_FIRST_RECORD  FIRST TO DISPLAY
         J     MAIN0000                             REBUILD EVERYTHING
* ------------------------------------------------------------------- *
*        SCROLL DOWN                                                  *
* ------------------------------------------------------------------- *
MAIN0370 DS    0H
         ITRACE ID=DOWN
         MVC   SPF_ZCMD,COMM_BLANKS                 CLEAR ZCMD
         CLI   SPF_ZSCROLLA,C'M'                    MAX?
         JE    MAIN0400                             YES
         L     R1,SESS_VERTICAL_SCROLL              NUMBER TO SCROLL
         L     R2,DATASPACE_1_DISPLAY_FIRST_RECORD  RECORD DISPLAYED
         AR    R2,R1                                PLUS SCROLL
         C     R2,DATASPACE_1_LAST_RECORD
         JH    MAIN0380                             WOULD GO BEYOND END
         ST    R2,DATASPACE_1_DISPLAY_FIRST_RECORD  FIRST RECORD NBR
         J     MAIN0000                             REBUILD EVERYTHING
MAIN0380 DS    0H
         L     R2,DATASPACE_1_LAST_RECORD           HIGHEST STORED
         S     R2,DATASPACE_1_DISPLAY_FIRST_RECORD  FIRST DISPLAYED
         C     R2,DXD_VDEPTH                        MORE THAN ONE SCRN?
         JNH   MAIN0180                             NO.. STAY PUT
         L     R2,DATASPACE_1_LAST_RECORD           HIGHEST STORED
         S     R2,DXD_VDEPTH                        MINUS 1 SCREEN
         A     R2,F2                                PLUS 2
         ST    R2,DATASPACE_1_DISPLAY_FIRST_RECORD  FIRST TO DISPLAY
         J     MAIN0000                             REBUILD EVERYTHING
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0390 DS    0H
         ITRACE ID=TOP
         MVC   SPF_ZCMD,COMM_BLANKS           CLEAR ZCMD
        MVC   DATASPACE_1_DISPLAY_FIRST_RECORD,DATASPACE_1_FIRST_RECORD
         XC    SESS_LAST_FOUND,SESS_LAST_FOUND
         J     MAIN0000                       REBUILD EVERYTHING
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0400 DS    0H
         ITRACE ID=BOTTOM
         MVC   SPF_ZCMD,COMM_BLANKS           CLEAR ZCMD
         L     R2,DATASPACE_1_LAST_RECORD     HIGHEST RECORD AVAILABLE
         S     R2,DXD_VDEPTH                  MINUS LINES ON DISPLAY
         A     R2,F2                          PLUS 2
         CH    R2,H1                          STILL HAVE AT LEAST 1?
         JNL   MAIN0410                       YES
         LH    R2,H1                          FORCE RECORD 1
MAIN0410 DS    0H
         ST    R2,DATASPACE_1_DISPLAY_FIRST_RECORD   FIRST RECORD NBR
         J     MAIN0000                       REBUILD EVERYTHING
* ------------------------------------------------------------------- *
*        APPEND (add to end of an existing file)                      *
* ------------------------------------------------------------------- *
MAIN0420 DS    0H
         ITRACE ID=APPEND1
         MVI   VDATA_SELECTION,C' '
         MVC   LOADREQ_MODULE,MODULE_OSAPPEND
         LA    R1,LOADREQ
         L     R15,COMM_V_OSLOAD
         BALR  R14,R15                        LOAD OSAPPEND
         MVC   SPF_MEMBER,VDATA_MEMBER        COPY MEMBER NAME
         MVC   DXD_MEMBER,VDATA_MEMBER        COPY MEMBER NAME
         MVI   APPNDREQ_FUNC,$APPNDREQ_OPEN
         LA    R1,APPNDREQ
         ICM   R15,15,LOADREQ_EP              COPY ENTRY POINT
         BALR  R14,R15                        LINK TO OSAPPEND
         ITRACE ID=APPND_RC,                                           +
               RDATA1=R15
         MVC   SPF_MEMBER,SESS_MEMBER
         CH    R15,H4                         PROBLEM?
         JE    MAIN0440                       YES
         CH    R15,H8                         EXIT?
         JE    MAIN0440                       YES
         MVC   APPNDREQ_MEMBER,DXD_MEMBER
         MVI   APPNDREQ_FUNC,$APPNDREQ_WRITE
         ICM   R15,15,LOADREQ_EP              OSAPPEND ENTRY POINT
         BALR  R14,R15                        LINK TO OSAPPEND
         MVI   APPNDREQ_FUNC,$APPNDREQ_CLOSE
         ICM   R15,15,LOADREQ_EP              OSAPPEND ENTRY POINT
         BALR  R14,R15                        LINK TO OSAPPEND
         MVC   VDATA_STATISTICS,COMM_BLANKS
         MVC   VDATA_STATISTICS(APPND_MSG_L),APPND_MSG
         J     MAIN0220                       PROCESS NEXT SELECTION
MAIN0440 DS    0H
         MVI   VDATA_ATTR_3,$SCREEN_ATTR_HIGH
         MVC   VDATA_STATISTICS(L'COMM_MSG_1),COMM_MSG_1
         J     MAIN0185
* ------------------------------------------------------------------- *
*        LOCATE (make a member the 1st shown on display)              *
* ------------------------------------------------------------------- *
MAIN0450 DS    0H
         ITRACE ID=LOCATE,                                             +
               RDATA1=R3
         CH    R3,H2                          TWO PARAMETERS?
         JL    MAIN0470                       LESS
         JH    ERR0060                        TOO MANY
         CLC   OPERAND_02_LENGTH,H8           MEMBER NAME TOO LONG?
         JH    ERR0070                        YES
         CLC   SESS_SEARCH_ARG_LENGTH,OPERAND_02_LENGTH
         JNE   MAIN0460                       DIFFERENT LENGTH
         CLC   SESS_SEARCH_ARG,0(R2)          SAME STRING?
         JE    MAIN0470                       YES
MAIN0460 DS    0H
         MVC   DSPCREQ_RECORD_NBR,DATASPACE_1_FIRST_RECORD
         ICM   R2,15,OPERAND_02_ADDRESS
         MVC   DSPCREQ_MEMBER,0(R2)           COPY MEMBER NAME
         MVC   SESS_SEARCH_ARG_LENGTH,OPERAND_02_LENGTH
         MVC   SESS_SEARCH_ARG,COMM_BLANKS
         MVC   SESS_SEARCH_ARG(8),0(R2)
         J     MAIN0480
MAIN0470 DS    0H
         MVC   SPF_ZCMD,COMM_BLANKS           CLEAR THE COMMAND
         OC    SESS_SEARCH_ARG_LENGTH,SESS_SEARCH_ARG_LENGTH
         JZ    ERR0170
         MVC   DSPCREQ_MEMBER,SESS_SEARCH_ARG
         ICM   R1,15,SESS_SEARCH_RECORD
         LA    R1,1(,R1)
         STCM  R1,15,DSPCREQ_RECORD_NBR
MAIN0480 DS    0H
         MVI   DSPCREQ_FUNC,$DSPCREQ_FIND_MEMBER_G
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE
         BALR  R14,R15
         CLI   DSPCREQ_RC,$DSPCREQ_OK
         JNE   MAIN0490                       NOT FOUND
         MVC   DATASPACE_1_DISPLAY_FIRST_RECORD,DSPCREQ_RECORD_NBR
         MVC   SESS_SEARCH_RECORD,DSPCREQ_RECORD_NBR
         MVC   SPF_ZCMD,COMM_BLANKS           CLEAR THE COMMAND
         J     MAIN0000
MAIN0490 DS    0H
         OC    SESS_LAST_FOUND,SESS_LAST_FOUND
         JZ    ERR0020                        NEVER FOUND
         J     ERR0180                        BOTTOM OF DATA REACHED
* ------------------------------------------------------------------- *
*        OFFLOAD member(s) to a sequential file                       *
* ------------------------------------------------------------------- *
MAIN0510 DS    0H
         ITRACE ID=OFFLOAD
         MVI   VDATA_SELECTION,C' '           RESET THE SELECTION
         MVC   SPF_MEMBER,VDATA_MEMBER        COPY MEMBER NAME
         MVC   DXD_MEMBER,VDATA_MEMBER        COPY MEMBER NAME
         MVC   LOADREQ_MODULE,MODULE_OSOFFLD
         LA    R1,LOADREQ
         L     R15,COMM_V_OSLOAD
         BALR  R14,R15                        LOAD OSOFFLD
         ITRACE ID=OSOFFLD,                                            +
               RDATA1=R15
         MVI   OFFLDREQ_FUNC,$OFFLDREQ_OPEN
         LA    R1,OFFLDREQ                    PARMS FOR OSOFFLD
         ICM   R15,15,LOADREQ_EP              COPY ENTRY POINT
         BALR  R14,R15                        LINK TO OSOFFLD
         ITRACE ID=OPEN_RC,                                            +
               RDATA1=R15
         LTR   R15,R15                        SUCCESSFUL?
         JNZ   MAIN0530                       NO
         MVC   SPF_MEMBER,SESS_MEMBER
         CH    R15,H4                         PROBLEM?
         JE    MAIN0520                       YES
         CH    R15,H8                         EXIT?
         JE    MAIN0520                       YES
         MVC   OFFLDREQ_MEMBER,DXD_MEMBER
         MVI   OFFLDREQ_FUNC,$OFFLDREQ_WRITE
         ICM   R15,15,LOADREQ_EP              OSOFFLD ENTRY POINT
         BALR  R14,R15                        LINK TO OSOFFLD
         ITRACE ID=WRITERC,                                            +
               RDATA1=R15
         LTR   R15,R15                        SUCCESSFUL?
         JNZ   MAIN0530                       NO
         MVI   OFFLDREQ_FUNC,$OFFLDREQ_CLOSE
         ICM   R15,15,LOADREQ_EP              OSOFFLD ENTRY POINT
         BALR  R14,R15                        LINK TO OSOFFLD
         ITRACE ID=CLOSERC,                                            +
               RDATA1=R15
         LTR   R15,R15                        SUCCESSFUL?
         JNZ   MAIN0530                       NO
         MVC   VDATA_STATISTICS,COMM_BLANKS
         MVC   VDATA_STATISTICS(OFFLD_MSG_L),OFFLD_MSG
         J     MAIN0220                       PROCESS NEXT SELECTION
MAIN0520 DS    0H
         MVI   VDATA_ATTR_3,$SCREEN_ATTR_HIGH
         MVC   VDATA_STATISTICS(L'COMM_MSG_1),COMM_MSG_1
         J     MAIN0220
MAIN0530 DS    0H
         ITRACE ID=MAIN0530,                                           +
               RDATA1=R15
         MVC   SPF_MSG_1,COMM_MSG_1
         MVC   SPF_MSG_2,COMM_MSG_2
         MVC   SPF_MSG_3,COMM_MSG_3
         MVC   SPF_MSG_4,COMM_MSG_4
         MVC   SPF_MSG_5,COMM_MSG_5
         J     MAIN0180
* ------------------------------------------------------------------- *
*        Reset TAG indicator (LINE COMMAND)                           *
* ------------------------------------------------------------------- *
MAIN0540 DS    0H
         ITRACE ID=LINE_RST
         MVI   VDATA_SELECTION,C' '              RESET THE SELECTION
         MVI   DSPCREQ_FUNC,$DSPCREQ_RETRIEVE
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE
         BALR  R14,R15
         TM    DIR_FLAGS,$DIR_TAGGED             MEMBER TAGGED?
         JNO   MAIN0220                          NO..
         NI    DIR_FLAGS,255-$DIR_TAGGED
         MVI   DSPCREQ_FUNC,$DSPCREQ_UPDATE
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE
         BALR  R14,R15
         MVI   VDATA_ATTR_2,$SCREEN_ATTR_NORMAL
         MVC   VDATA_STATISTICS(RESET_MSG_L),RESET_MSG
         J     MAIN0220                          PROCESS NEXT SELECTION
* ------------------------------------------------------------------- *
*        Reset tag indicator (primary command)                        *
* ------------------------------------------------------------------- *
MAIN0550 DS    0H
         ITRACE ID=PRIM_RST
         CH    R3,H2                             PROPER PARAMETERS?
         JL    ERR0050                           TOO FEW
         ICM   R2,15,OPERAND_02_ADDRESS          OPERAND 2 ADDRESS
         CLC   ALL,0(R2)                         RESET ALL MEMBERS?
         JE    MAIN0560                          YES
         BRAS  R9,MAIN1600                       VERIFY NAMES
         MVI   DXD_AND,255-$DIR_TAGGED           FLAGS TO TURN OFF
         MVI   DXD_OR,0                          FLAGS TO TURN ON
         BRAS  R9,MAIN1700                       PROCESS COMMAND
         MVC   SPF_ZCMD,COMM_BLANKS              CLEAR COMMAND
         J     MAIN0000                          REBUILD DISPLAY
MAIN0560 DS    0H
         ITRACE ID=RESETALL
         MVC   DSPCREQ_RECORD_NBR,DATASPACE_1_FIRST_RECORD
         SR    R2,R2                             CLEAR FOR COUNTER
MAIN0570 DS    0H
         MVI   DSPCREQ_FUNC,$DSPCREQ_RETRIEVE    SET COMMAND
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE
         BALR  R14,R15                           RETRIEVE THE MEMBER
         TM    DIR_FLAGS,$DIR_TAGGED             MEMBER TAGGED?
         JNO   MAIN0580                          NO
         A     R2,F1                             PLUS 1
         NI    DIR_FLAGS,255-$DIR_TAGGED         RESET FLAG
         MVI   DSPCREQ_FUNC,$DSPCREQ_UPDATE      SET COMMAND
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE
         BALR  R14,R15                           UPDATE DATA SPACE
MAIN0580 DS    0H
         L     R1,DSPCREQ_RECORD_NBR             CURRENT RECORD NUMBER
         C     R1,DATASPACE_1_LAST_RECORD        AT LAST RECORD
         JE    MAIN0590                          YES
         A     R1,F1                             PLUS 1
         ST    R1,DSPCREQ_RECORD_NBR             SET UPDATED NUMBER
         J     MAIN0570
MAIN0590 DS    0H
         MVC   SPF_ZCMD,COMM_BLANKS              CLEAR COMMAND
         ST    R2,COMM_DWORD
         MVI   COMM_MSG_ID+1,14                  SET MESSAGE NUMBER
         BRAS  R9,MSG0000                        BUILD MESSAGE
         MVC   SPF_MSG_1,COMM_MSG_1              COPY TO SPF AREA
         J     MAIN0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0620 DS    0H
         ITRACE ID=LINE_SEL,                                           +
               DATA1=VDATA_MEMBER
         NI    DXD_FLAGS,255-$EMPTY_MEMBER       RESET INDICATOR
         MVC   DXD_MEMBER,VDATA_MEMBER           COPY MEMBER NAME
         MVC   DXD_SELECTION,VDATA_SELECTION     COPY LINE CMD
         J     MAIN0660
MAIN0630 DS    0H
         ICM   R2,15,OPERAND_01_ADDRESS
         ITRACE ID=PRIM_SEL,                                           +
               DATA1=(0(R2),1)
         SR    R7,R7                             NOT FROM LINE CMD
         MVC   DXD_SELECTION,0(R2)               SAVE 1ST CHAR OF CMD
         CH    R3,H2                             PROPER PARAMETERS?
         JL    ERR0050                           TOO FEW
         JH    ERR0060                           TOO MANY
         CLC   OPERAND_02_LENGTH,H8              MEMBER NAME TOO LONG?
         JH    ERR0070                           YES
         MVC   DXD_MEMBER,COMM_BLANKS            INITIALIZE MEMBER NAME
         SR    R1,R1                             CLEAR R1
         ICM   R1,3,OPERAND_02_LENGTH            NAME LENGTH
         BCTR  R1,0                              FOR EX INSTRUCTION
         ICM   R2,15,OPERAND_02_ADDRESS          MEMBER NAME ADDRESS
         EX    R1,MVC_02                         COPY MEMBER NAME
         MVC   DSPCREQ_RECORD_NBR,DATASPACE_1_FIRST_RECORD
         NI    DXD_FLAGS,255-$EMPTY_MEMBER       RESET INDICATOR
         MVI   DSPCREQ_FUNC,$DSPCREQ_FIND_MEMBER_S
         MVC   DSPCREQ_RECORD_NBR,F1             START WITH 1ST MEMBER
         MVC   DSPCREQ_MEMBER,DXD_MEMBER
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE
         BALR  R14,R15
         CLI   DSPCREQ_RC,$DSPCREQ_OK            MEMBER LOCATED?
         JE    MAIN0640                          YES
         TM    SESS_EDIT_FLAGS,$SESS_EDIT        EDIT SESSION?
         JNO   ERR0020                           NO
         OI    DXD_FLAGS,$EMPTY_MEMBER           NEW MEMBER (EMPTY)
         J     MAIN0650
MAIN0640 DS    0H
         TM    DIR_FLAGS,$DIR_DELETED+$DIR_RENAMED
         JNZ   ERR0130
MAIN0650 DS    0H
         MVC   SPF_ZCMD,COMM_BLANKS              RESET THE COMMAND
MAIN0660 DS    0H
         ITRACE ID=BLD_NEW
         LA    R0,SESSION_L
         BRAS  R9,GETMAIN2
         LR    R3,R1                            SAVE ADDRESS
NEW      USING SESSION,R3
         ST    R3,COMM_NEW_SESSION              SET NEW BLOCK ADDR
         LR    R0,R3
         LA    R1,SESSION_L
         LR    R14,R11                          CURRENT SESSION ADDR
         LR    R15,R1                           COPY LENGTH
         MVCL  R0,R14                           COPY SESSION BLOCK
         L     R1,SESS_NEXT                     NEXT ON CHAIN
NEXT     USING SESSION,R1
         ST    R1,NEW.SESS_NEXT                 SET NEXT IN NEW BLOCK
         ST    R11,NEW.SESS_PREV                SET PREV IN NEW BLOCK
         ST    R3,SESS_NEXT                     SET NEXT IN CURRENT
         LTR   R1,R1                            'NEXT' BLOCK?
         JZ    MAIN0670                         NO
         ST    R3,NEXT.SESS_PREV                SET PREV IN NEXT BLOCK
         DROP  NEXT
MAIN0670 DS    0H
         MVI   NEW.SESS_FORMAT_FLAGS,0
         XC    NEW.DATASPACE_1_ALET,NEW.DATASPACE_1_ALET
         XC    NEW.DATASPACE_1_TOKEN,NEW.DATASPACE_1_TOKEN
         MVC   NEW.DATASPACE_1_NAME,COMM_BLANKS
         MVC   NEW.DATASPACE_1_FIRST_RECORD,F1
         MVC   NEW.DATASPACE_1_DISPLAY_FIRST_RECORD,F1
         MVC   NEW.DATASPACE_1_DISPLAY_FIRST_COLUMN,F1
         XC    SESS_IO_AREA,SESS_IO_AREA
         XC    NEW.SESS_DXD_ADDR,NEW.SESS_DXD_ADDR
         XC    NEW.SESS_DXD_LENGTH,NEW.SESS_DXD_LENGTH
         XC    NEW.SESS_LAST_FOUND,NEW.SESS_LAST_FOUND
         XC    NEW.SESS_SEARCH_ARG_LENGTH,NEW.SESS_SEARCH_ARG_LENGTH
         MVC   NEW.SESS_SEARCH_ARG,COMM_BLANKS
         MVC   NEW.SESS_DEFAULT_FORMAT,COMM_BLANKS
         MVC   NEW.SESS_FORMAT,COMM_BLANKS
         MVC   NEW.SESS_FUNCTION_MODULE,COMM_BLANKS
         MVC   NEW.SESS_DATA_LOADER,COMM_BLANKS
         MVC   NEW.SESS_EXIT_MODULE,COMM_BLANKS
         MVC   NEW.SESS_MEMBER,DXD_MEMBER
         MVC   NEW.SESS_PATH,SESS_PATH
         XC    NEW.SESS_DISP_VDATA_ADDR,NEW.SESS_DISP_VDATA_ADDR
         XC    NEW.SESS_DISP_VDATA_LENGTH,NEW.SESS_DISP_VDATA_ADDR
         XC    NEW.SESS_DCB_ADDR,NEW.SESS_DCB_ADDR
         XC    NEW.SESS_DCB_LENGTH,NEW.SESS_DCB_LENGTH
         MVI   NEW.SESS_KEYWORD_FLAGS,$KEYWORD_DSN+$KEYWORD_MEMBER+$KEY+
               WORD_VOL
         TM    DXD_FLAGS,$EMPTY_MEMBER           EMPTY MEMBER?
         JNO   MAIN0680                          NO
         OI    NEW.SESS_EDIT_FLAGS,$SESS_EDIT_EMPTY
MAIN0680 DS    0H
         L     R0,COMM_IO_SIZE                   I/O AREA SIZE
         BRAS  R9,GETMAIN1
         ST    R1,SESS_IO_AREA                   SAVE ADDRESS
         ITRACE ID=IO_AREA,                                            +
               RDATA1=R1
         ST    R6,DXD_LINES_REMAINING            SAVE LINES REMAINING
         ST    R7,DXD_CURRENT_VDATA              SAVE VDATA ADDR
         MVI   COMM_SESS_FUNC,$SESS_SWITCH       SET SWITCH INDICATOR
         ITRACE ID=SEL_CODE,                                           +
               DATA1=(DXD_SELECTION,1)
         CLI   DXD_SELECTION,C'B'                BROWSE?
         JE    MAIN0690                          YES
         CLI   DXD_SELECTION,C'S'                SELECT (BROWSE)?
         JE    MAIN0690                          YES
         LTR   R7,R7                             LINE COMMAND?
         JZ    EXIT0000                          NO
         MVI   VDATA_SELECTION,C' '              RESET THE SELECTION
         J     EXIT0000                          EXIT
MAIN0690 DS    0H
         ITRACE ID=BROWSE,DATA1=(COMM_SESS_FUNC,1)
         MVI   NEW.SESS_EDIT_FLAGS,0             TURN OFF EDIT MODE
         MVC   NEW.SESS_DEFAULT_FORMAT,BROWSE
         MVC   NEW.SESS_FORMAT,BROWSE
         MVC   NEW.SESS_FUNCTION_MODULE,MODULE_OSBROWSE
         LTR   R7,R7                             LINE COMMAND?
         JZ    EXIT0000                          NO
         MVI   VDATA_SELECTION,C' '              RESET THE SELECTION
         J     EXIT0000
         DROP  NEW
* ------------------------------------------------------------------- *
*        Tag member (line command)                                    *
* ------------------------------------------------------------------- *
MAIN0710 DS    0H
         ITRACE ID=LINETAG
         MVI   VDATA_SELECTION,C' '              RESET THE SELECTION
         MVI   DSPCREQ_FUNC,$DSPCREQ_RETRIEVE
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE
         BALR  R14,R15
         OI    DIR_FLAGS,$DIR_TAGGED             SET THE TAGGED FLAG
         MVI   DSPCREQ_FUNC,$DSPCREQ_UPDATE
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE
         BALR  R14,R15
         MVI   VDATA_ATTR_2,$SCREEN_ATTR_HIGH
         MVC   VDATA_STATISTICS(TAGGED_MSG_L),TAGGED_MSG
         J     MAIN0220                          PROCESS NEXT SELECTION
* ------------------------------------------------------------------- *
*        Tag member (primary command)                                 *
* ------------------------------------------------------------------- *
MAIN0720 DS    0H
         ITRACE ID=PRIMTAG
         CH    R3,H2                             PROPER PARAMETERS?
         JL    ERR0050                           TOO FEW
         ICM   R2,15,OPERAND_02_ADDRESS          OPERAND 2 ADDRESS
         BRAS  R9,MAIN1600                       VERIFY NAMES
         MVI   DXD_AND,X'FF'                     FLAGS TO TURN OFF
         MVI   DXD_OR,$DIR_TAGGED                FLAGS TO TURN ON
         BRAS  R9,MAIN1700                       PROCESS COMMAND
         MVC   SPF_ZCMD,COMM_BLANKS              CLEAR COMMAND
         J     MAIN0000                          REBUILD DISPLAY
* ------------------------------------------------------------------- *
*        APPEND members to a sequential file                          *
* ------------------------------------------------------------------- *
MAIN0800 DS    0H
         ITRACE ID=APPEND2
         CH    R3,H2                             TWO PARAMETERS?
         JH    ERR0060                           TOO MANY
         JL    MAIN0810                          ONLY 1
         CLC   OPERAND_02_LENGTH,H8              MEMBER NAME TOO LONG?
         JH    ERR0070                           YES
         ICM   R2,15,OPERAND_02_ADDRESS
         MVC   DXD_MEMBER,0(R2)                  COPY MEMBER NAME
         MVC   APPNDREQ_MEMBER,0(R2)             COPY MEMBER NAME
         MVI   DSPCREQ_FUNC,$DSPCREQ_FIND_MEMBER_S
         MVC   DSPCREQ_RECORD_NBR,F1             START WITH 1ST MEMBER
         MVC   DSPCREQ_MEMBER,APPNDREQ_MEMBER    MEMBER NAME
         LA    R1,DSPCREQ                        DATASPACE PARMS
         L     R15,COMM_OSDSPACE
         BALR  R14,R15                           FIND MEMBER
         CLI   DSPCREQ_RC,$DSPCREQ_OK            LOCATED?
         JNE   ERR0020                           NOT FOUND
         TM    DIR_FLAGS,$DIR_DELETED+$DIR_RENAMED
         JNZ   ERR0130
MAIN0810 DS    0H
         MVC   SPF_ZCMD,COMM_BLANKS              RESET COMMAND
         MVC   LOADREQ_MODULE,MODULE_OSAPPEND    MODULE TO LOAD
         LA    R1,LOADREQ                        OSLOAD PARMS
         L     R15,COMM_V_OSLOAD
         BALR  R14,R15                           LOAD OSAPPEND
         ICM   R15,15,LOADREQ_EP                 COPY ENTRY POINT
         MVI   APPNDREQ_FUNC,$APPNDREQ_OPEN      OSAPPEND FUNCTION
         LA    R1,APPNDREQ                       OSAPPEND PARMS
         BALR  R14,R15                           LINK TO OSAPPEND
         ITRACE ID=APPND_RC,                                           +
               RDATA1=R15
         CH    R15,H4                            PROBLEM?
         JE    MAIN0860                          YES
         CH    R15,H8                            EXIT?
         JE    MAIN0180                          YES
         TM    OPERAND_02_TYPE,$OPERAND_PRESENT  OPERAND 2 PRESENT?
         JO    MAIN0830                          YES
         MVC   DSPCREQ_RECORD_NBR,F1             FIND 'TAGGED' MEMBERS
         LA    R1,DIR_RECORD                     I/O AREA ADDR
         ST    R1,DSPCREQ_RECORD_ADDR            SET IN DSPC PARMS
MAIN0820 DS    0H
         CLC   DSPCREQ_RECORD_NBR,DATASPACE_1_LAST_RECORD
         JH    MAIN0850
         MVI   DSPCREQ_FUNC,$DSPCREQ_RETRIEVE    SET FUNCTION
         LA    R1,DSPCREQ                        DATA SPACE PARMS
         L     R15,COMM_OSDSPACE
         BALR  R14,R15                           RETRIEVE RECORD
         CLI   DSPCREQ_RC,$DSPCREQ_NOT_STORED    THIS RECORD STORED?
         JE    MAIN0840                          NO
         CLI   DSPCREQ_RC,$DSPCREQ_OK            SUCCESSFUL?
         JNE   ERR0010                           NO
         TM    DIR_FLAGS,$DIR_TAGGED             TAGGED?
         JNO   MAIN0840                          NO
         MVC   SPF_MEMBER,DIR_NAME               SET MEMBER NAME
         MVC   APPNDREQ_MEMBER,DIR_NAME          SET MEMBER NAME
MAIN0830 DS    0H
         MVI   APPNDREQ_FUNC,$APPNDREQ_WRITE     SET APPEND FUNCTION
         LA    R1,APPNDREQ                       OSAPPEND PARMS
         ICM   R15,15,LOADREQ_EP                 COPY ENTRY POINT
         BALR  R14,R15                           LINK TO OSAPPEND
         TM    OPERAND_02_TYPE,$OPERAND_PRESENT  OPERAND 2 PRESENT?
         JO    MAIN0850                          YES
MAIN0840 DS    0H
         L     R1,DSPCREQ_RECORD_NBR             CURRENT RECORD NUMBER
         A     R1,F1                             PLUS 1
         ST    R1,DSPCREQ_RECORD_NBR             SAVE NEW RECORD NUMBER
         J     MAIN0820                          EXAMINE THIS MEMBER
MAIN0850 DS    0H
         MVI   APPNDREQ_FUNC,$APPNDREQ_CLOSE     SET APPEND FUNCTION
         LA    R1,APPNDREQ                       OSAPPEND PARMS
         ICM   R15,15,LOADREQ_EP                 COPY ENTRY POINT
         BALR  R14,R15                           LINK TO OSAPPEND
         MVC   COMM_INFO_01,APPNDREQ_COUNT       MEMBERS APPENDED
         MVC   COMM_INFO_02,APPNDREQ_BYTES
         MVC   COMM_MSG_ID,H3                    SET MESSAGE NUMBER
         BRAS  R9,MSG0000                        BUILD MESSAGE
         MVC   SPF_MSG_1,COMM_MSG_1
         MVC   SPF_ZCMD,COMM_BLANKS              CLEAR COMMAND
         J     MAIN0180                          EXIT WITH RC=0
MAIN0860 DS    0H
         MVC   SPF_MSG_1,COMM_MSG_1
         MVC   SPF_MSG_2,COMM_MSG_2
         MVC   SPF_MSG_3,COMM_MSG_3
         MVC   SPF_MSG_4,COMM_MSG_4
         MVC   SPF_MSG_5,COMM_MSG_5
         J     MAIN0180                          EXIT WITH RC=0
* ------------------------------------------------------------------- *
*                                                                     *
*        OFFLOAD members to a sequential file                         *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0900 DS    0H
         ITRACE ID=OFFLOAD2
         CH    R3,H2                             TWO PARAMETERS?
         JH    ERR0060                           TOO MANY
         JL    MAIN0910                          ONLY 1
         CLC   OPERAND_02_LENGTH,H8              MEMBER NAME TOO LONG?
         JH    ERR0070                           YES
         ICM   R2,15,OPERAND_02_ADDRESS
         MVC   OFFLDREQ_MEMBER,0(R2)             COPY MEMBER NAME
         MVC   SPF_MEMBER,0(R2)                  COPY MEMBER NAME
         MVC   DXD_MEMBER,0(R2)                  COPY MEMBER NAME
         MVI   DSPCREQ_FUNC,$DSPCREQ_FIND_MEMBER_S
         MVC   DSPCREQ_RECORD_NBR,F1             START WITH 1ST MEMBER
         MVC   DSPCREQ_MEMBER,DXD_MEMBER         SET MEMBER NAME
         LA    R1,DSPCREQ                        DATASPACE PARMS
         L     R15,COMM_OSDSPACE
         BALR  R14,R15                           FIND MEMBER
         CLI   DSPCREQ_RC,$DSPCREQ_OK            SUCCESSFUL?
         JNE   ERR0020                           NO
         TM    DIR_FLAGS,$DIR_DELETED+$DIR_RENAMED
         JNZ   ERR0130
MAIN0910 DS    0H
         MVC   SPF_ZCMD,COMM_BLANKS              RESET COMMAND
         MVC   LOADREQ_MODULE,MODULE_OSOFFLD     MODULE TO LOAD
         LA    R1,LOADREQ                        LOAD PARMS
         L     R15,COMM_V_OSLOAD
         BALR  R14,R15                           LOAD OSOFFLD
         ICM   R15,15,LOADREQ_EP                 OSOFFLD ENTRY POINT
         ITRACE ID=OPEN
         MVI   OFFLDREQ_FUNC,$OFFLDREQ_OPEN      SET OFFLOAD FUNCTION
         LA    R1,OFFLDREQ                       OSOFFLD PARMS
         BALR  R14,R15                           LINK TO OSOFFLD
         ITRACE ID=OPEN_RC,                                            +
               RDATA1=R15
         CH    R15,H4                            OPEN SUCCESSFUL?
         JE    MAIN0960                          NO
         CH    R15,H8                            EXIT?
         JE    MAIN0180                          YES
         TM    OPERAND_02_TYPE,$OPERAND_PRESENT  OPERAND 2 PRESENT?
         JO    MAIN0930                          YES
         MVC   DSPCREQ_RECORD_NBR,F1             FIRST RECORD
         LA    R1,DIR_RECORD                     I/O AREA
         ST    R1,DSPCREQ_RECORD_ADDR            SET ADDR IN PARMS
MAIN0920 DS    0H
         CLC   DSPCREQ_RECORD_NBR,DATASPACE_1_LAST_RECORD
         JH    MAIN0950                          END OF MEMBERS
         MVI   DSPCREQ_FUNC,$DSPCREQ_RETRIEVE    SET FUNCTION
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE
         BALR  R14,R15                           RETRIEVE MEMBER
         CLI   DSPCREQ_RC,$DSPCREQ_NOT_STORED    STORED?
         JE    MAIN0940                          NO
         CLI   DSPCREQ_RC,$DSPCREQ_OK            SUCCESSFUL?
         JNE   ERR0010                           NO
         TM    DIR_FLAGS,$DIR_TAGGED             TAGGED?
         JNO   MAIN0940                          NO
         MVC   SPF_MEMBER,DIR_NAME               SET MEMBER NAME
         MVC   OFFLDREQ_MEMBER,DIR_NAME          SET MEMBER NAME
MAIN0930 DS    0H
         ITRACE ID=WRITE,                                              +
               DATA1=OFFLDREQ_MEMBER
         MVI   OFFLDREQ_FUNC,$OFFLDREQ_WRITE     SET OFFLOAD FUNCTION
         LA    R1,OFFLDREQ                       OSOFFLD PARMS
         ICM   R15,15,LOADREQ_EP                 OSOFFLD ENTRY POINT
         BALR  R14,R15                           LINK TO OSOFFLD
         TM    OPERAND_02_TYPE,$OPERAND_PRESENT  OPERAND 2 PRESENT?
         JO    MAIN0950                          YES
MAIN0940 DS    0H
         L     R1,DSPCREQ_RECORD_NBR             CURRENT RECORD NUMBER
         A     R1,F1                             PLUS 1
         ST    R1,DSPCREQ_RECORD_NBR             SAVE NEW RECORD NUMBER
         J     MAIN0920                          CHECK THIS MEMBER
MAIN0950 DS    0H
         ITRACE ID=CLOSE
         MVI   OFFLDREQ_FUNC,$OFFLDREQ_CLOSE
         LA    R1,OFFLDREQ                       OSOFFLD PARMS
         ICM   R15,15,LOADREQ_EP                 OSOFFLD ENTRY POINT
         BALR  R14,R15                           LINK TO OSOFFLD
         MVC   COMM_DWORD,OFFLDREQ_COUNT
         MVC   COMM_MSG_ID,H4                    SET MESSAGE NUMBER
         BRAS  R9,MSG0000                        BUILD MESSAGE
         MVC   SPF_MSG_1,COMM_MSG_1              COPY MESSAGE
         MVC   SPF_ZCMD,COMM_BLANKS              CLEAR COMMAND
         J     MAIN0180                          EXIT WITH RC=0
MAIN0960 DS    0H
         ITRACE ID=OPEN_ERR
         MVC   SPF_MSG_1,COMM_MSG_1
         MVC   SPF_MSG_2,COMM_MSG_2
         MVC   SPF_MSG_3,COMM_MSG_3
         MVC   SPF_MSG_4,COMM_MSG_4
         MVC   SPF_MSG_5,COMM_MSG_5
         J     MAIN0180
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN1000 DS    0H
         ITRACE ID=SORT
         CH    R3,H2                             TWO OPERANDS?
         JL    MAIN1010                          DEFAULT TO 'BY NAME'
         JH    ERR0060                           TOO MANY
         ICM   R2,15,OPERAND_02_ADDRESS          2ND OPERAND'S ADDRESS
         CLC   NAME,0(R2)                        SORT BY NAME?
         JE    MAIN1010
         CLC   TTR,0(R2)                         SORT BY TTR?
         JE    MAIN1020
         CLC   CDATE,0(R2)                       SORT BY CDATE?
         JE    MAIN1030
         CLC   MDATE,0(R2)                       SORT BY MDATE/TIME?
         JE    MAIN1040
         CLC   SIZE,0(R2)                        SORT BY SIZE?
         JE    MAIN1050
         CLC   USER,0(R2)                        SORT BY USER?
         JE    MAIN1060
         MVI   COMM_MSG_ID+1,5
         J     ERR1000                           EXIT
MAIN1010 DS    0H
         ITRACE ID=BY_NAME
         MVI   DSPCREQ_FUNC,$DSPCREQ_SORT_NAME
         J     MAIN1070
MAIN1020 DS    0H
         ITRACE ID=BY_TTR
         MVI   DSPCREQ_FUNC,$DSPCREQ_SORT_TTR
         J     MAIN1070
MAIN1030 DS    0H
         ITRACE ID=BY_CDATE
         MVI   DSPCREQ_FUNC,$DSPCREQ_SORT_CDATE
         J     MAIN1070
MAIN1040 DS    0H
         ITRACE ID=BY_MDATE
         MVI   DSPCREQ_FUNC,$DSPCREQ_SORT_MDATE
         J     MAIN1070
MAIN1050 DS    0H
         ITRACE ID=BY_SIZE
         MVI   DSPCREQ_FUNC,$DSPCREQ_SORT_SIZE
         J     MAIN1070
MAIN1060 DS    0H
         ITRACE ID=BY_USER
         MVI   DSPCREQ_FUNC,$DSPCREQ_SORT_USER
MAIN1070 DS    0H
         ITRACE ID=SORTDSPC
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE
         BALR  R14,R15                           SORT THE DATA SPACE
         MVC   SPF_ZCMD,COMM_BLANKS              CLEAR COMMAND
         J     MAIN0000
* ------------------------------------------------------------------- *
*        SUBMIT                                                       *
* ------------------------------------------------------------------- *
MAIN1100 DS    0H
         ITRACE ID=SUBMIT
         CH    R3,H2                             PROPER NUMBER?
         JL    ERR0050                           TOO FEW
         JH    ERR0060                           TOO MANY
         CLC   OPERAND_02_LENGTH,H8              OPERAND 2 TOO LONG?
         JH    ERR0070                           YES
         ICM   R2,15,OPERAND_02_ADDRESS          ADDR OF MEMBER'S NAME
         MVC   DXD_MEMBER,0(R2)                  COPY MEMBER NAME
         MVI   DSPCREQ_FUNC,$DSPCREQ_FIND_MEMBER_S
         MVC   DSPCREQ_RECORD_NBR,F1             START WIT RECORD 1
         MVC   DSPCREQ_MEMBER,DXD_MEMBER         SET MEMBER NAME
         LA    R1,DSPCREQ                        DATA SPACE PARMS
         L     R15,COMM_OSDSPACE
         BALR  R14,R15                           LOCATE MEMBER
         CLI   DSPCREQ_RC,$DSPCREQ_OK            MEMBER FOUND?
         JNE   ERR0210                           NO
         TM    DIR_FLAGS,$DIR_DELETED+$DIR_RENAMED
         JNZ   ERR0130                           DELETED OR RENAMED..
         MVC   LOADREQ_MODULE,MODULE_OSSUBMIT    MODULE TO LOAD
         LA    R1,LOADREQ                        LOAD PARMS
         L     R15,COMM_V_OSLOAD
         BALR  R14,R15                           LOAD OSSUBMIT
         MVC   SUBREQ_DSN,SESS_DSN               COPY DATASET NAME
         MVC   SUBREQ_MEMBER,0(R2)               COPY MEMBER NAME
         LA    R1,SUBREQ                         SUBMIT PARMS
         ITRACE ID=CALL_SUB,                                           +
               RDATA1=R1
         ICM   R15,15,LOADREQ_EP                 OSSUBMIT ENTRY POINT
         BALR  R14,R15                           CALL OSSUBMIT
         MVC   SPF_ZCMD,COMM_BLANKS
         MVC   SPF_MSG_1,COMM_MSG_1
         MVC   SPF_MSG_2,COMM_MSG_2
         MVC   SPF_MSG_3,COMM_MSG_3
         MVC   SPF_MSG_4,COMM_MSG_4
         MVC   SPF_MSG_5,COMM_MSG_5
         J     MAIN0180                          EXIT
MAIN1110 DS    0H
         ITRACE ID=SUBMIT
         MVI   DSPCREQ_FUNC,$DSPCREQ_FIND_MEMBER_S
         MVC   DSPCREQ_MEMBER,VDATA_MEMBER       SET MEMBER NAME
         LA    R1,DSPCREQ                        DATA SPACE PARMS
         L     R15,COMM_OSDSPACE
         BALR  R14,R15                           LOCATE MEMBER
         CLI   DSPCREQ_RC,$DSPCREQ_OK            MEMBER FOUND?
         JNE   ERR0210                           NO
         TM    DIR_FLAGS,$DIR_DELETED+$DIR_RENAMED
         JNZ   ERR0130                           DELETED OR RENAMED..
         MVC   LOADREQ_MODULE,MODULE_OSSUBMIT    MODULE TO LOAD
         LA    R1,LOADREQ                        LOAD PARMS
         L     R15,COMM_V_OSLOAD
         BALR  R14,R15                           LOAD OSSUBMIT
         MVC   SUBREQ_DSN,SESS_DSN               COPY DATASET NAME
         MVC   SUBREQ_MEMBER,VDATA_MEMBER        COPY MEMBER NAME
         LA    R1,SUBREQ                         SUBMIT PARMS
         ITRACE ID=CALL_SUB,                                           +
               RDATA1=R1
         ICM   R15,15,LOADREQ_EP                 OSSUBMIT ENTRY POINT
         BALR  R14,R15                           CALL OSSUBMIT
         MVC   DXD_MSG,COMM_MSG_2
         LA    R2,DXD_MSG
         LA    R3,L'DXD_MSG
         TPUT  (R2),(R3),R
         J     MAIN0220
* ------------------------------------------------------------------- *
*        Delete a member                                              *
* ------------------------------------------------------------------- *
MAIN1200 DS    0H
         ITRACE ID=LINE_DEL,                                           +
               DATA1=VDATA_MEMBER
         MVI   VDATA_SELECTION,C' '              RESET THE SELECTION
         MVC   SPF_MEMBER,VDATA_MEMBER
         MVC   DXD_MEMBER,VDATA_MEMBER
         MVC   SESS_DISP_PANEL,OS0006
         L     R15,COMM_V_OSCNFRM
         BALR  R14,R15                           REQUEST CONFIRMATION
         CH    R15,H4                            RC = 4?
         JE    MAIN1250                          YES, ABORT
         CH    R15,H8                            RC = 4?
         JE    MAIN1250                          YES, ABORT
         OC    SPF_YES_OR_NO,COMM_BLANKS
         CLI   SPF_YES_OR_NO,C'Y'                DELETE CONFIRMED?
         JE    MAIN1220                          YES
         CLI   SPF_YES_OR_NO,C'N'                ANSWER 'NO'?
         JE    MAIN1250                          YES
MAIN1220 DS    0H
         BRAS  R9,OPEN0000                       MAKE SURE DCB IS OPEN
         LA    R3,DXD_MEMBER
         ITRACE ID=STOW,                                               +
               RDATA1=R8,                                              +
               RDATA2=R3
         STOW  (R8),(R3),D                       DELETE THE MEMBER
         ITRACE ID=STOW_RC,                                            +
               RDATA1=R15
         LTR   R15,R15                           SUCCESSFUL?
         JNZ   ERR0100                           NO
         MVI   DSPCREQ_FUNC,$DSPCREQ_RETRIEVE
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE
         BALR  R14,R15
         OI    DIR_FLAGS,$DIR_DELETED
         MVI   DSPCREQ_FUNC,$DSPCREQ_UPDATE
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE
         BALR  R14,R15
         MVI   VDATA_ATTR_2,$SCREEN_ATTR_HIGH
         MVC   VDATA_STATISTICS(DELETED_MSG_L),DELETED_MSG
         J     MAIN0220                          PROCESS NEXT SELECTION
* ------------------------------------------------------------------- *
*        Delete (primary command)                                     *
* ------------------------------------------------------------------- *
MAIN1230 DS    0H
         ITRACE ID=PRIM_DEL
         CH    R3,H2                             TWO PARAMETERS?
         JL    MAIN1300                          JUST ONE
         JH    ERR0060                           TOO MANY
         CLC   OPERAND_02_LENGTH,H8              MEMBER NAME TOO LONG?
         JH    ERR0070                           YES
         ICM   R2,15,OPERAND_02_ADDRESS
         MVC   DSPCREQ_MEMBER,0(R2)              COPY MEMBER NAME
         MVC   DXD_MEMBER,0(R2)                  COPY MEMBER NAME
         MVI   DSPCREQ_FUNC,$DSPCREQ_FIND_MEMBER_S
         MVC   DSPCREQ_RECORD_NBR,F1             START WITH RECORD 1
         LA    R1,DSPCREQ                        PARM BLOCK ADDRESS
         L     R15,COMM_OSDSPACE                 OSDSPACE ENTRY POINT
         BALR  R14,R15                           FIND THE MEMBER
         CLI   DSPCREQ_RC,$DSPCREQ_OK
         JNE   ERR0020                           NOT FOUND
         TM    DIR_FLAGS,$DIR_DELETED+$DIR_RENAMED
         JNZ   ERR0130
         MVC   SPF_MEMBER,DSPCREQ_MEMBER
         MVC   SPF_ZCMD,COMM_BLANKS
         MVC   SESS_DISP_PANEL,OS0006
         L     R15,COMM_V_OSCNFRM
         BALR  R14,R15                           LINK TO SPF
         CH    R15,H4                            RC = 4?
         JE    MAIN1250                          YES, ABORT
         CH    R15,H8                            RC = 4?
         JE    MAIN1250                          YES, ABORT
         LTR   R15,R15                           SUCCESSFUL?
         JNZ   ERR0090                           NO
         CLI   SPF_YES_OR_NO,C'Y'                DELETE CONFIRMED?
         JE    MAIN1240                          YES
         CLI   SPF_YES_OR_NO,C'N'                ANSWER 'NO'?
         JE    MAIN1250                          YES
MAIN1240 DS    0H
         BRAS  R9,OPEN0000                       MAKE SURE DCB IS OPEN
         LA    R3,DSPCREQ_MEMBER                 MEMBER NAME
         ITRACE ID=STOW,                                               +
               RDATA1=R8,                                              +
               RDATA2=R3
         STOW  (R8),(R3),D                       DELETE THE MEMBER
         ITRACE ID=STOW_RC,                                            +
               RDATA1=R15
         LTR   R15,R15                           SUCCESSFUL?
         JNZ   ERR0100                           NO
         MVI   DSPCREQ_FUNC,$DSPCREQ_RETRIEVE
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE                 OSDSPACE ENTRY POINT
         BALR  R14,R15                           RETRIEVE THE RECORD
         OI    DIR_FLAGS,$DIR_DELETED
         MVI   DSPCREQ_FUNC,$DSPCREQ_UPDATE
         L     R15,COMM_OSDSPACE                 OSDSPACE ENTRY POINT
         BALR  R14,R15                           UPDATE THE RECORD
         J     MAIN0000                          REBUILD DISPLAY
MAIN1250 DS    0H
         MVC   SPF_ZCMD,COMM_BLANKS              CLEAR THE COMMAND
         MVC   SPF_MSG_1(MSG02_L),MSG02
         J     MAIN0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN1300 DS    0H
         ITRACE ID=DELFLAGD
         MVC   DSPCREQ_RECORD_NBR,F1             FIRST RECORD
         LA    R1,DIR_RECORD
         ST    R1,DSPCREQ_RECORD_ADDR
MAIN1310 DS    0H
         CLC   DSPCREQ_RECORD_NBR,DATASPACE_1_LAST_RECORD
         JH    MAIN0000
         MVI   DSPCREQ_FUNC,$DSPCREQ_RETRIEVE
         LA    R1,DSPCREQ                        DSPCREQ PARMS
         L     R15,COMM_OSDSPACE                 OSDSPACE ENTRY POINT
         BALR  R14,R15                           RETRIEVE THE RECORD
         CLI   DSPCREQ_RC,$DSPCREQ_NOT_STORED
         JE    MAIN0000                          NOT STORED
         CLI   DSPCREQ_RC,$DSPCREQ_OK
         JNE   ERR0010                           ERROR..
         TM    DIR_FLAGS,$DIR_DELETED
         JO    MAIN1340                          MEMBER DELETED
         TM    DIR_FLAGS,$DIR_TAGGED             TAGGED?
         JNO   MAIN1340                          NO
MAIN1320 DS    0H
         ITRACE ID=MAIN1320,                                           +
               DATA1=DIR_NAME
         MVC   SPF_MEMBER,DIR_NAME               COPY MEMBER NAME
         MVC   SPF_ZCMD,COMM_BLANKS
         MVC   SESS_DISP_PANEL,OS0006
         L     R15,COMM_V_OSCNFRM
         BALR  R14,R15                           LINK TO OSCNFRM
         CH    R15,H4                            RC = 4?
         JE    MAIN0000                          YES, ABORT
         CH    R15,H8                            RC = 4?
         JE    MAIN0000                          YES, ABORT
         LTR   R15,R15                           SUCCESSFUL?
         JNZ   ERR0090                           NO
         CLI   SPF_YES_OR_NO,C'Y'                DELETE CONFIRMED?
         JE    MAIN1330                          YES
         CLI   SPF_YES_OR_NO,C'N'                ANSWER 'NO'?
         JE    MAIN1340                          YES
MAIN1330 DS    0H
         BRAS  R9,OPEN0000                       MAKE SURE DCB IS OPEN
         LA    R3,DIR_NAME                       MEMBER NAME
         ITRACE ID=DELETE,                                             +
               RDATA1=R8,                                              +
               RDATA2=R3
         STOW  (R8),(R3),D                       DELETE THE MEMBER
         ITRACE ID=STOW_RC,                                            +
               RDATA1=R15
         LTR   R15,R15                           SUCCESSFUL?
         JNZ   ERR0100                           NO
         OI    DIR_FLAGS,$DIR_DELETED
         MVI   DSPCREQ_FUNC,$DSPCREQ_UPDATE
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE
         BALR  R14,R15                           UPDATE INFO
MAIN1340 DS    0H
         ITRACE ID=MAIN1340
         L     R1,DSPCREQ_RECORD_NBR
         A     R1,F1                             PLUS 1
         ST    R1,DSPCREQ_RECORD_NBR             SAVE RECORD NUMBER
         J     MAIN1310
* ------------------------------------------------------------------- *
*        Rename (primary command)                                     *
* ------------------------------------------------------------------- *
MAIN1500 DS    0H
         ITRACE ID=PRIM_REN
         CH    R3,H2                             PROPER PARAMETERS?
         JL    ERR0050                           TOO FEW
         JH    ERR0060                           TOO MANY
         CLC   OPERAND_02_LENGTH,H8              MEMBER NAME TOO LONG?
         JH    ERR0070                           YES
         ICM   R2,15,OPERAND_02_ADDRESS
         MVC   SPF_MEMBER,0(R2)                  COPY MEMBER NAME
         MVC   DSPCREQ_MEMBER,SPF_MEMBER         COPY MEMBER NAME
         MVI   DSPCREQ_FUNC,$DSPCREQ_FIND_MEMBER_S
         MVC   DSPCREQ_RECORD_NBR,F1             START WITH RECORD 1
         LA    R1,DSPCREQ                        DSPCREQ PARMS
         L     R15,COMM_OSDSPACE                 OSDSPACE ENTRY POINT
         BALR  R14,R15                           LINK TO OSDSPACE
         CLI   DSPCREQ_RC,$DSPCREQ_OK            MEMBER EXIST?
         JNE   ERR0120                           NO
         MVC   SPF_ZCMD,COMM_BLANKS              CLEAR THE COMMAND
         MVC   SPF_NEW_MEMBER,COMM_BLANKS
MAIN1510 DS    0H
         LA    R1,DISPLAY                        SPF FUNCTION
         ST    R1,DXD_SPF_1                      SET ADDRESS
         LA    R1,OS0007                         PANEL NAME
         ST    R1,DXD_SPF_2                      SET ADDRESS
         OI    DXD_SPF_2,X'80'                   SET END-OF-LIST
         LA    R1,DXD_SPF_PARMS                  PARM LIST ADDRESS
         L     R15,COMM_ISPLINK                  SPF ENTRY POINT
         BALR  R14,R15                           LINK TO SPF
         CH    R15,H4                            RC = 4?
         JE    MAIN1550                          YES, ABORT
         CH    R15,H8                            RC = 4?
         JE    MAIN1550                          YES, ABORT
         LTR   R15,R15                           SUCCESSFUL?
         JNZ   ERR0090                           NO
         OC    SPF_NEW_MEMBER,COMM_BLANKS
         CLC   SPF_NEW_MEMBER,COMM_BLANKS
         JE    MAIN1510                          DISPLAY AGAIN
         MVI   DSPCREQ_FUNC,$DSPCREQ_FIND_MEMBER_S
         MVC   DSPCREQ_RECORD_NBR,F1             START WITH RECORD 1
         MVC   DSPCREQ_MEMBER,SPF_NEW_MEMBER
         LA    R1,DSPCREQ                        DSPCREQ PARMS
         L     R15,COMM_OSDSPACE                 OSDSPACE ENTRY POINT
         BALR  R14,R15                           LINK TO OSDSPACE
         CLI   DSPCREQ_RC,$DSPCREQ_OK            NAME ALREADY EXIST?
         JE    ERR0110                           YES
         MVC   DSPCREQ_MEMBER,SPF_MEMBER
         LA    R1,DSPCREQ                        DSPCREQ PARMS
         L     R15,COMM_OSDSPACE                 OSDSPACE ENTRY POINT
         BALR  R14,R15                           LINK TO OSDSPACE
         CLI   DSPCREQ_RC,$DSPCREQ_OK            MEMBER FOUND?
         JNE   ERR0120                           NO
         MVC   DXD_MEMBER,DIR_NAME               COPY MEMBER NAME
         TM    DIR_FLAGS,$DIR_DELETED+$DIR_RENAMED
         JNZ   ERR0130
MAIN1520 DS    0H
         MVC   DXD_RENAME_OLD,SPF_MEMBER
         MVC   DXD_RENAME_NEW,SPF_NEW_MEMBER
         BRAS  R9,OPEN0000                       MAKE SURE DCB IS OPEN
         LA    R3,DXD_RENAME_OLD
         ITRACE ID=STOW,                                               +
               DATA1=0(R3),                                            +
               DATA2=8(R3)
         STOW  (R8),(R3),C                       RENAME THE MEMBER
         ITRACE ID=STOW_RC,                                            +
               RDATA1=R15
         LTR   R15,R15                           SUCCESSFUL?
         JNZ   ERR0140                           NO
         MVC   DIR_NAME,SPF_NEW_MEMBER           COPY NEW NAME
         MVI   DSPCREQ_FUNC,$DSPCREQ_STORE
         L     R1,DATASPACE_1_LAST_RECORD        HIGHEST RECORD SO FAR
         A     R1,F1                             PLUS 1
         ST    R1,DSPCREQ_RECORD_NBR             SET RECORD NUMBER
         LA    R1,DSPCREQ                        OSDSPACE PARMS
         L     R15,COMM_OSDSPACE                 OSDSPACE ENTRY POINT
         BALR  R14,R15                           STORE THE NEW RECORD
         MVC   DSPCREQ_MEMBER,SPF_MEMBER
         MVI   DSPCREQ_FUNC,$DSPCREQ_FIND_MEMBER_S
         MVC   DSPCREQ_RECORD_NBR,F1             START WITH RECORD 1
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE                 OSDSPACE ENTRY POINT
         BALR  R14,R15                           RETRIEVE THE RECORD
         OI    DIR_FLAGS,$DIR_RENAMED
         MVC   DIR_DATA(DIR_SPF_L),COMM_BLANKS
         MVC   DIR_DATA(L'SPF_NEW_MEMBER),SPF_NEW_MEMBER
         MVI   DSPCREQ_FUNC,$DSPCREQ_UPDATE
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE                 OSDSPACE ENTRY POINT
         BALR  R14,R15                           UPDATE THE RECORD
         MVI   SESS_SORT,0                       RESET SORT INDICATORS
         J     MAIN0000                          REBUILD DISPLAY
* ------------------------------------------------------------------- *
*        Rename (line command)                                        *
* ------------------------------------------------------------------- *
MAIN1530 DS    0H
         ITRACE ID=LINE_REN
         MVI   VDATA_SELECTION,C' '              RESET THE SELECTION
         MVC   SPF_MEMBER,VDATA_MEMBER           COPY MEMBER
         MVC   DXD_MEMBER,VDATA_MEMBER           COPY MEMBER
         MVC   SPF_NEW_MEMBER,COMM_BLANKS
         LA    R1,DISPLAY                        SPF FUNCTION
         ST    R1,DXD_SPF_1                      SET ADDRESS
         LA    R1,OS0007                         PANEL NAME
         ST    R1,DXD_SPF_2                      SET ADDRESS
         OI    DXD_SPF_2,X'80'                   SET END-OF-LIST
         LA    R1,DXD_SPF_PARMS                  PARM LIST ADDRESS
         L     R15,COMM_ISPLINK                  SPF ENTRY POINT
         BALR  R14,R15                           LINK TO SPF
         CH    R15,H4                            RC = 4?
         JE    MAIN1550                          YES, ABORT
         CH    R15,H8                            RC = 8?
         JE    MAIN1550                          YES, ABORT
         LTR   R15,R15                           SUCCESSFUL?
         JNZ   ERR0090                           NO
         MVC   DSPCREQ_MEMBER,SPF_NEW_MEMBER
         MVI   DSPCREQ_FUNC,$DSPCREQ_FIND_MEMBER_S
         MVC   DSPCREQ_RECORD_NBR,F1             START WITH RECORD 1
         ITRACE ID=FIND_NEW,                                           +
               DATA1=DSPCREQ_MEMBER
         LA    R1,DSPCREQ                        PARM BLOCK ADDRESS
         L     R15,COMM_OSDSPACE                 OSDSPACE ENTRY POINT
         BALR  R14,R15                           FIND THE MEMBER
         CLI   DSPCREQ_RC,$DSPCREQ_OK
         JE    ERR0110                           MEMBER ALREADY EXISTS
         MVC   DSPCREQ_MEMBER,SPF_MEMBER
         MVC   DSPCREQ_RECORD_NBR,F1             START WITH RECORD 1
         ITRACE ID=FIND_OLD,                                           +
               DATA1=SPF_MEMBER
         LA    R1,DSPCREQ                        PARM BLOCK ADDRESS
         L     R15,COMM_OSDSPACE                 OSDSPACE ENTRY POINT
         BALR  R14,R15                           FIND THE MEMBER
         CLI   DSPCREQ_RC,$DSPCREQ_OK
         JNE   ERR0120                           MEMBER NOT FOUND
         MVC   DXD_RENAME_OLD,SPF_MEMBER
         MVC   DXD_RENAME_NEW,SPF_NEW_MEMBER
         BRAS  R9,OPEN0000                       MAKE SURE DCB IS OPEN
         LA    R3,DXD_RENAME_OLD
         ITRACE ID=STOW,                                               +
               DATA1=0(R3),                                            +
               DATA2=8(R3)
         STOW  (R8),(R3),C                       RENAME THE MEMBER
         ITRACE ID=STOW_RC,                                            +
               RDATA1=R15
         LTR   R15,R15                           SUCCESSFUL?
         JNZ   ERR0140                           NO
         MVC   DIR_NAME,SPF_NEW_MEMBER           COPY NEW NAME
         MVI   DSPCREQ_FUNC,$DSPCREQ_STORE
         L     R1,DATASPACE_1_LAST_RECORD        HIGHEST RECORD SO FAR
         A     R1,F1                             PLUS 1
         ST    R1,DSPCREQ_RECORD_NBR             SET RECORD NUMBER
         LA    R1,DSPCREQ                        OSDSPACE PARMS
         L     R15,COMM_OSDSPACE                 OSDSPACE ENTRY POINT
         BALR  R14,R15                           STORE THE NEW RECORD
         MVC   DSPCREQ_MEMBER,SPF_MEMBER
         MVI   DSPCREQ_FUNC,$DSPCREQ_FIND_MEMBER_S
         MVC   DSPCREQ_RECORD_NBR,F1             START WITH RECORD 1
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE                 OSDSPACE ENTRY POINT
         BALR  R14,R15                           RETRIEVE THE RECORD
         OI    DIR_FLAGS,$DIR_RENAMED
         MVC   DIR_DATA(DIR_SPF_L),COMM_BLANKS
         MVC   DIR_DATA(L'SPF_NEW_MEMBER),SPF_NEW_MEMBER
         MVI   DSPCREQ_FUNC,$DSPCREQ_UPDATE
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE                 OSDSPACE ENTRY POINT
         BALR  R14,R15                           UPDATE THE RECORD
         MVI   VDATA_ATTR_2,$SCREEN_ATTR_HIGH
         MVC   VDATA_STATISTICS(RENAMED_MSG_L),RENAMED_MSG
         MVI   SESS_SORT,0                       RESET SORT INDICATORS
         J     MAIN0220                          PROCESS NEXT SELECTION
MAIN1550 DS    0H
         MVC   SPF_ZCMD,COMM_BLANKS              CLEAR THE COMMAND
         MVC   SPF_MSG_1(MSG03_L),MSG03
         J     MAIN0000                          REBUILD DISPLAY
* ------------------------------------------------------------------- *
*                                                                     *
*        Verify member names in primary command are valid             *
*                                                                     *
*        . all member names must be 8 characters or less              *
*        . member names must be in the directory                      *
*        . members may not have been renamed or deleted               *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN1600 DS    0H
         LA    R3,OPERAND_02_ADDRESS             START WITH OPERAND 2
         USING OPERAND,R3
         LH    R4,COMM_OPERANDS_NBR              NBR OF OPERANDS
         BCTR  R4,0                              MINUS 1 FOR COMMAND
MAIN1610 DS    0H
         CLC   OPERAND_LENGTH,H8                 MEMBER NAME TOO LONG?
         JH    ERR0070                           YES
         ICM   R2,15,OPERAND_ADDRESS             OPERAND ADDRESS
         MVC   DXD_MEMBER,COMM_BLANKS            INITIALIZE NAME
         LH    R1,OPERAND_LENGTH                 NAME LENGTH
         BCTR  R1,0                              FOR EX
         EX    R1,MVC_02                         COPY MEMBER NAME
         MVC   DSPCREQ_RECORD_NBR,F1             START WITH 1ST MEMBER
         MVI   DSPCREQ_FUNC,$DSPCREQ_FIND_MEMBER_S
         MVC   DSPCREQ_MEMBER,DXD_MEMBER         COPY MEMBER NAME
         ITRACE ID=SRCH,                                               +
               DATA1=DSPCREQ_MEMBER
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE
         BALR  R14,R15                           LOCATE MEMBER
         CLI   DSPCREQ_RC,$DSPCREQ_OK            OK?
         JNE   ERR0020                           NOT FOUND
         TM    DIR_FLAGS,$DIR_DELETED+$DIR_RENAMED
         JNZ   ERR0130                           RENAMED OR DELETED
         LA    R3,OPERAND_L(,R3)                 NEXT OPERAND
         BRCT  R4,MAIN1610                       CHECK NEXT OPERAND
         BR    R9
* ------------------------------------------------------------------- *
*                                                                     *
*        Perform operation on list of named members                   *
*                                                                     *
*        names have already been verified                             *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN1700 DS    0H
         LA    R3,OPERAND_02_ADDRESS             START WITH OPERAND 2
         LH    R4,COMM_OPERANDS_NBR              NBR OF OPERANDS
         BCTR  R4,0                              MINUS 1 FOR COMMAND
MAIN1710 DS    0H
         ICM   R2,15,OPERAND_ADDRESS             OPERAND ADDRESS
         MVC   DXD_MEMBER,COMM_BLANKS            INITIALIZE NAME
         LH    R1,OPERAND_LENGTH                 NAME LENGTH
         BCTR  R1,0                              FOR EX
         EX    R1,MVC_02                         COPY MEMBER NAME
         MVC   DSPCREQ_RECORD_NBR,F1             START WITH 1ST MEMBER
         MVI   DSPCREQ_FUNC,$DSPCREQ_FIND_MEMBER_S
         MVC   DSPCREQ_MEMBER,DXD_MEMBER         COPY MEMBER NAME
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE
         BALR  R14,R15                           LOCATE MEMBER
         CLI   DSPCREQ_RC,$DSPCREQ_OK            OK?
         JNE   ERR0020                           NOT FOUND
         NC    DIR_FLAGS,DXD_AND                 'AND' FLAGS OFF
         OC    DIR_FLAGS,DXD_OR                  'OR' FLAGS ON
         MVI   DSPCREQ_FUNC,$DSPCREQ_UPDATE      SET COMMAND
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE
         BALR  R14,R15                           UPDATE DIRECTORY
         LA    R3,OPERAND_L(,R3)                 NEXT OPERAND
         BRCT  R4,MAIN1710                       CHECK NEXT OPERAND
         BR    R9
         DROP  R3
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN2000 DS    0H
         ITRACE ID=SHOW
         SR    R15,R15
         CLC   COMM_OPERANDS_NBR,H1              ONE OPERAND?
         JE    MAIN2040                          YES.. SHOW ALL
         CLC   COMM_OPERANDS_NBR,H3              TOO MANY?
         JH    ERR0060                           YES
         JNE   MAIN2010                          OTHERWISE TWO OPERANDS
         ICM   R1,15,OPERAND_02_ADDRESS
         CLC   OPERAND_02_LENGTH,H3              LENGTH = 3?
         JNE   ERR0190                           NO.. CANNOT BE 'NOT'
         CLC   NOT,0(R1)                         'NOT' KEYWORD?
         JNE   ERR0190
         ICM   R1,15,OPERAND_03_ADDRESS
         ICM   R15,3,OPERAND_03_LENGTH
         J     MAIN2020
MAIN2010 DS    0H
         ICM   R1,15,OPERAND_02_ADDRESS
         ICM   R15,3,OPERAND_02_LENGTH
         CLC   ALL,0(R1)                         ALL?
         JE    MAIN2040                          YES
MAIN2020 DS    0H
         BCTR  R15,0
         EX    R15,SHOW_CLC                      'TAGGED' KEYWORD?
         JNE   ERR0190
         CLC   COMM_OPERANDS_NBR,H2              TWO OPERANDS?
         JNE   MAIN2030
         ITRACE ID=SHOW_TAG
         MVC   SPF_TAGGED,TAGGED
         OI    SESS_USER_OPTIONS,$OPTIONS_SHOW_TAGGED
         NI    SESS_USER_OPTIONS,255-$OPTIONS_SHOW_NOT_TAGGED
         J     MAIN2050
MAIN2030 DS    0H
         ITRACE ID=SHOW_NOT
         OI    SESS_USER_OPTIONS,$OPTIONS_SHOW_NOT_TAGGED
         NI    SESS_USER_OPTIONS,255-$OPTIONS_SHOW_TAGGED
         MVC   SPF_TAGGED,NOT_TAGGED
         J     MAIN2050
MAIN2040 DS    0H
         ITRACE ID=SHOW_ALL
         NI    SESS_USER_OPTIONS,255-$OPTIONS_SHOW_TAGGED
         NI    SESS_USER_OPTIONS,255-$OPTIONS_SHOW_NOT_TAGGED
         MVC   SPF_TAGGED,COMM_BLANKS
MAIN2050 DS    0H
         MVC   SPF_ZCMD,COMM_BLANKS
         J     MAIN0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN2100 DS    0H
         ITRACE ID=REFRESH
         MVC   SPF_ZCMD,COMM_BLANKS              RESET COMMAND
         MVI   DSPCREQ_FUNC,$DSPCREQ_CLEAR       DATA SPACE FUNCTION
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE
         BALR  R14,R15                           CLEAR DATA SPACE
         MVC   LOADREQ_MODULE,MODULE_OSDESERV    MODULE NAME
         LA    R1,LOADREQ
         L     R15,COMM_V_OSLOAD
         BALR  R14,R15                           LOAD OSDESERV
         ICM   R15,15,LOADREQ_EP                 OSDESERV ENTRY POINT
         BALR  R14,R15                           LINK TO OSDESERV
         ITRACE ID=DESERVRC,                                           +
               RDATA1=R15
         CLC   DATASPACE_1_DISPLAY_FIRST_RECORD,DATASPACE_1_LAST_RECORD
         JNH   MAIN0000                          DISPLAY
         J     MAIN0400                          GO TO BOTTOM
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN2200 DS    0H
         ITRACE ID=LINECMDS
         MVI   SESS_COMMAND_FLAGS,$SESS_COMMAND_DISPLAY
         LARL  R0,LINE_TABLE
         ST    R0,COMM_MAIN_COMMAND_TABLE
         XC    COMM_SUB_COMMAND_TABLE,COMM_SUB_COMMAND_TABLE
         L     R15,COMM_V_OSCMD
         BALR  R14,R15
         J     MAIN0220
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN3000 DS    0H
         ITRACE ID=DUMP,                                               +
               DATA1=DATASPACE_1_NAME,                                 +
               DATA2=SESS_DD
         MVI   DAIR_FUNC,$DAIR_ALLOC             SET FUNCTION
         MVC   DAIR_DD,OSDUMP_DD                 COPY DDNAME
         MVC   DAIR_SYSOUT,COMM_SYSOUT_CLASS
         LA    R1,DAIRREQ
         L     R15,COMM_V_OSDAIR
         BALR  R14,R15
         OC    DAIR_R15,DAIR_R15                 SUCCESSFUL?
         JNZ   ERR0020                           NO
         L     R1,=A(DUMP_DCB_I)
         MVC   DUMP_DCB(DUMP_DCB_L),0(R1)
         LA    R2,DUMP_DCB                       DCB ADDRESS
         ST    R2,DSPCREQ_DCB                    PASS IT TO OSDSPACE
         ITRACE ID=OPENDUMP,                                           +
               RDATA1=R2
         OPEN  ((R2),OUTPUT),                    OPEN OUTPUT DCB       +
               MODE=31,                                                +
               MF=(E,DXD_OPEN)
         LA    R1,DUMP_IO_AREA                   I/O AREA ADDRESS
         ST    R1,DSPCREQ_RECORD_ADDR            PASS ADDRESS
         MVI   DSPCREQ_FUNC,$DSPCREQ_DUMP
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE
         BALR  R14,R15
         ITRACE ID=CLS_DUMP,                                           +
               RDATA1=R2
         CLOSE ((R2)),                           CLOSE DCB             +
               MODE=31,                                                +
               MF=(E,DXD_CLOSE)
         LR    R1,R2
         BRAS  R9,FREEPOOL
         MVC   SPF_MSG_1(MSG01_L),MSG01
         J     MAIN0180
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN3200 DS    0H
         ITRACE ID=DISP_CMD
         MVI   SESS_COMMAND_FLAGS,$SESS_COMMAND_DISPLAY
         LARL  R0,COMMAND_TABLE
         ST    R0,COMM_MAIN_COMMAND_TABLE
         XC    COMM_SUB_COMMAND_TABLE,COMM_SUB_COMMAND_TABLE
         L     R15,COMM_V_OSCMD
         BALR  R14,R15                           DISPLAY COMMANDS
         J     MAIN0185                          DISPLAY SAME DATA
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
OPEN0000 DS    0H
         TM    SESS_DCB_FLAGS,$SESS_DCB_OPEN     DCB OPEN?
         BOR   R2                                YES
         ITRACE ID=OPEN,                                               +
               RDATA1=R8
         OPEN  ((R8),OUTPUT),                                          +
               MODE=31,                          OPEN DCB              +
               MF=(E,DXD_OPEN)
         OI    SESS_DCB_FLAGS,$SESS_DCB_OPEN     REMEMBER DCB IS OPEN
         BR    R9
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
RTRY0000 DS    0H
         ITRACE ID=ABEND,                                              +
               DATA1=(SESS_SYSTEM_CODE,6)
         CLC   SESS_ABEND_DISP,AP1_DISP          ABEND ON CREATE DATE?
         JE    RTRY0010                          YES
         CLC   SESS_ABEND_DISP,AP2_DISP          MODIFICATION DATE?
         JE    RTRY0020                          YES
         L     R15,COMM_V_OSABEND
         BALR  R14,R15                           LINK TO OSABEND
         NI    SESS_ABEND_FLAG,255-$ABENDED
         J     EXIT0000
RTRY0010 DS    0H
         ITRACE ID=BAD_CREDT                     CREATE DATE NOT VALID
         MVC   VDATA_CREATE_DATE,=C'--------'
         J     MAIN0100                          RESUME
RTRY0020 DS    0H
         ITRACE ID=BAD_MODDT
         MVC   VDATA_MOD_DATE,=C'--------'
         J     MAIN0110                          RESUME
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0010  DS    0H
         DC    H'1',C'ERROR FROM OSDSPACE'
ERR0020  DS    0H
         ITRACE ID=DSPC_ERR
         MVC   SPF_MSG_1,COMM_MSG_1              COPY MESSAGE
         MVC   SPF_MSG_2,COMM_MSG_2              COPY MESSAGE
         MVC   SPF_MSG_3,COMM_MSG_3              COPY MESSAGE
         MVC   SPF_MSG_4,COMM_MSG_4              COPY MESSAGE
         MVC   SPF_MSG_5,COMM_MSG_5              COPY MESSAGE
         J     MAIN0180                          EXIT
ERR0050  DS    0H
         MVI   COMM_MSG_ID+1,6                   TOO FEW OPERANDS
         J     ERR1000
ERR0060  DS    0H
         MVI   COMM_MSG_ID+1,7                   TOO MANY OPERANDS
         J     ERR1000
ERR0070  DS    0H
         MVI   COMM_MSG_ID+1,8                   OPERAND TOO LONG
         J     ERR1000
ERR0080  DS    0H
         MVI   COMM_MSG_ID+1,2                   INVALID COMMAND
         MVC   COMM_INFO_01,SPF_ZCMD             COPY COMMAND
         J     ERR1000
ERR0090  DS    0H
         DC    H'9'
         DC    C'DISPLAY FAILED'
ERR0100  DS    0H
         ST    R15,COMM_DWORD                    SET RETURN CODE
         MVI   COMM_MSG_ID+1,9                   STOW (DELETE) FAILED
         J     ERR1000
ERR0110  DS    0H
         MVC   COMM_INFO_01,DSPCREQ_MEMBER
         MVI   COMM_MSG_ID+1,10                  MEMBER ALREADY EXISTS
         J     ERR1000
ERR0120  DS    0H
         MVI   COMM_MSG_ID+1,11                  MEMBER NOT FOUND
         MVC   COMM_INFO_01,SPF_MEMBER
         J     ERR1000
ERR0130  DS    0H
         MVI   COMM_MSG_ID+1,12                  DELETED OR RENAMED
         MVC   COMM_INFO_01,DXD_MEMBER
         J     ERR1000
ERR0140  DS    0H
         MVI   COMM_MSG_ID+1,13                  STOW (RENAME) FAILED
         ST    R15,COMM_DWORD                    SET RETURN CODE
         J     ERR1000
ERR0150  DS    0H
         DC    H'0'
ERR0160  DS    0H
         DC    H'0'
ERR0170  DS    0H
         MVI   COMM_MSG_ID+1,15                  NO SEARCH ARG
         J     ERR1000
ERR0180  DS    0H
         MVI   COMM_MSG_ID+1,16                  BOTTOM OF DATA REACHED
         J     ERR1000
ERR0190  DS    0H
         MVI   COMM_MSG_ID+1,17                  INVALID SHOW OPTION
         J     ERR1000
ERR0200  DS    0H
         CLI   SESS_FORMAT_FLAGS,$FORMAT_CLEANUP
         JE    EXIT0000
         DC    H'0'
ERR0210  DS    0H
         MVI   COMM_MSG_ID+1,18                  MEMBER NOT FOUND
         MVC   COMM_INFO_01(L'DXD_MEMBER),DXD_MEMBER MEMBER NAME
         J     ERR1000
ERR0220  DS    0H
         MVI   COMM_MSG_ID+1,19
ERR1000  DS    0H
         BRAS  R9,MSG0000                        BUILD THE MESSAGE
         MVC   SPF_MSG_1,COMM_MSG_1              COPY MESSAGE
         MVC   SPF_MSG_2,COMM_MSG_2              COPY MESSAGE
         MVC   SPF_MSG_3,COMM_MSG_3              COPY MESSAGE
         MVC   SPF_MSG_4,COMM_MSG_4              COPY MESSAGE
         MVC   SPF_MSG_5,COMM_MSG_5              COPY MESSAGE
         J     MAIN0180
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MSG0000  DS    0H
         MVC   COMM_MSG_CSECT,DXD_CSECT          CSECT FOR MESSAGES
         L     R15,COMM_V_OSMSG                  OSMSG ENTRY POINT
         BALR  R14,R15                           BUILD THE MESSAGE
         BR    R9
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DEBUG000 DS    0H
         ITRACE ID=DEBUG
         L     R15,COMM_V_OSDEBUG
         BALR  R14,R15
         J     MAIN0180
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
CLEAN000 DS    0H
         ITRACE ID=CLEAN_UP,                                           +
               DATA1=(DXD_VDATA_ADDR,4),                               +
               DATA2=(DXD_VDATA_LENGTH,4)
         L     R0,DXD_VDATA_LENGTH
         L     R1,DXD_VDATA_ADDR
         MVI   COMM_VDATA_FUNC,$VDATA_FREEMAIN   SET FUNCTION
         L     R15,COMM_V_OSVDATA
         BALR  R14,R15                           FREE VDATA
         TM    SESS_DCB_FLAGS,$SESS_DCB_OPEN     DCB OPEN?
         JNO   CLEAN010                          NO
         ITRACE ID=CLOSE,                                              +
               RDATA1=R8
         CLOSE ((R8)),                           CLOSE DCB             +
               MODE=31,                                                +
               MF=(E,DXD_CLOSE)
         NI    SESS_DCB_FLAGS,255-$SESS_DCB_OPEN DCB IS CLOSED
CLEAN010 DS    0H
         ICM   R8,15,SESS_DCB_ADDR               DCB PRESENT?
         JZ    CLEAN020                          NO
         ITRACE ID=FREEPOOL,                                           +
               RDATA1=R8
         LR    R1,R8
         BRAS  R9,FREEPOOL
         ITRACE ID=FREE_DCB,                                           +
               RDATA1=R8
         L     R0,SESS_DCB_LENGTH
         LR    R1,R8
         BRAS  R9,FREEMAIN
         XC    SESS_DCB_ADDR,SESS_DCB_ADDR
         XC    SESS_DCB_LENGTH,SESS_DCB_LENGTH
         NI    SESS_DCB_FLAGS,255-$SESS_DCB_OPEN
CLEAN020 DS    0H
         LA    R0,OSDIR_DXD_L                    WORK AREA SIZE
         LR    R1,R13                            COPY WORK AREA ADDRESS
         L     R13,4(,R13)                       RESTORE R13
         BRAS  R9,FREEMAIN
         XC    SESS_DXD_ADDR,SESS_DXD_ADDR
         XC    SESS_DXD_LENGTH,SESS_DXD_LENGTH
         XC    COMM_MAIN_COMMAND_TABLE,COMM_MAIN_COMMAND_TABLE
         XC    COMM_SUB_COMMAND_TABLE,COMM_SUB_COMMAND_TABLE
         SR    R15,R15
         J     EXIT0010
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         LH    R15,DXD_RC                        RETURN CODE
         ITRACE ID=EXIT,                                               +
               RDATA1=R15,                                             +
               DATA2=(12(R13),4)
         L     R13,4(,R13)                       RESTORE R13
EXIT0010 DS    0H
         L     R14,12(,R13)                      RESTORE RETURN POINT
         LM    R0,R12,20(R13)                    RESTORE REGISTERS
         BR    R14                               RETURN
* ------------------------------------------------------------------- *
*                                                                     *
*        For the most part, this code is "base register-less".        *
*        It uses the relative mode instructions where possible.       *
*                                                                     *
*        The parts of the code that do require a base register        *
*        are grouped here.                                            *
*                                                                     *
*        As of Jan 2010, GETMAIN, FREEMAIN, and FREEPOOL generate     *
*        B or BAL instructions.  I suspect that in the future, IBM    *
*        will changed these macros to also use relative mode          *
*        addressing instructions.  At that time the code could        *
*        changed so GETAIN, FREEMAIN, and FREEPOOL are in-line.       *
*                                                                     *
*        The base register is R10                                     *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
BASED_AREA DS  0H

GETMAIN1 DS    0H
         GETMAIN RU,                                                   +
               LV=(0),                                                 +
               LOC=BELOW
         BR    R9
GETMAIN2 DS    0H
         GETMAIN RU,                                                   +
               LV=(0),                                                 +
               LOC=ANY
         BR    R9
FREEMAIN DS    0H
         FREEMAIN RU,                                                  +
               A=(1),                                                  +
               LV=(0)
         BR    R9
FREEPOOL DS    0H
         FREEPOOL (1)
         BR    R9
* ------------------------------------------------------------------- *
MVC_01   MVC   0(0,R7),HEADING_01                COPY HEADING
MVC_02   MVC   DXD_MEMBER(0),0(R2)               COPY MEMBER NAME
SHOW_CLC CLC   0(0,R1),TAGGED
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
F1              DC    F'1'
F2              DC    F'2'
H1              DC    H'1'
H2              DC    H'2'
H3              DC    H'3'
H4              DC    H'4'
H8              DC    H'8'

PDSDCB_I DCB   DSORG=PO,                                               +
               DDNAME=ANYDD,                                           +
               MACRF=W
PDSDCB_L       EQU   *-PDSDCB_I

DUMP_DCB_I     DCB DSORG=PS,                                           +
               DDNAME=OSDUMP,                                          +
               RECFM=FBA,                                              +
               LRECL=133,                                              +
               BLKSIZE=13300,                                          +
               MACRF=PM
DUMP_DCB_L     EQU      *-DUMP_DCB_I

OPEN_I         OPEN     (*,OUTPUT),                                    +
               MODE=31,                                                +
               MF=L
OPEN_L         EQU      *-OPEN_I
CLOSE_I        CLOSE    (*),                                           +
               MODE=31,                                                +
               MF=L
CLOSE_L        EQU      *-CLOSE_I

AP1_DISP        DC    Y((AP1-OSDIR)+4)
AP2_DISP        DC    Y((AP2-OSDIR)+4)


P1900000        DC    P'1900000'

EDIT_WORD_1     DC    X'4020202020202120'
EDIT_WORD_2     DC    X'F0212020204B202021'
EDIT_WORD_3     DC    X'40202020202020202120'

MODULE_OSAPPEND DC    CL8'OSAPPEND'
MODULE_OSBROWSE DC    CL8'OSBROWSE'
MODULE_OSDESERV DC    CL8'OSDESERV'
MODULE_OSOFFLD  DC    CL8'OSOFFLD '
MODULE_OSSUBMIT DC    CL8'OSSUBMIT'
ALL             DC    C'ALL '
BROWSE          DC    CL8'BROWSE'
CDATE           DC    C'CDATE'
DISPLAY         DC    C'DISPLAY'
OSABEND         DC    CL8'OSABEND'
OSDUMP_DD       DC    CL8'OSDUMP'
DIR_PANEL       DC    CL8'OSDIR'
OS0006          DC    CL8'OS0006'
OS0007          DC    CL8'OS0007'
MDATE           DC    C'MDATE'
NAME            DC    C'NAME'
NOT             DC    C'NOT'
NOT_TAGGED      DC    C'NOT TAGGED'
SIZE            DC    C'SIZE'
TAGGED          DC    C'TAGGED    '
TTR             DC    C'TTR'
USER            DC    C'USER'

APPND_MSG       EQU   *
                DC    AL1($SCREEN_ATTR_HIGH)
                DC    C'<-------- Appended '
                DC    AL1($SCREEN_ATTR_NORMAL)
APPND_MSG_L     EQU   *-APPND_MSG

DELETED_MSG     EQU   *
                DC    AL1($SCREEN_ATTR_HIGH)
                DC    C'<-------- Deleted  '
                DC    AL1($SCREEN_ATTR_NORMAL)
DELETED_MSG_L   EQU   *-DELETED_MSG

RENAMED_MSG     EQU   *
                DC    AL1($SCREEN_ATTR_HIGH)
                DC    C'<-------- Renamed  '
                DC    AL1($SCREEN_ATTR_NORMAL)
RENAMED_MSG_L   EQU   *-RENAMED_MSG

OFFLD_MSG       EQU   *
                DC    AL1($SCREEN_ATTR_HIGH)
                DC    C'<-------- Offloaded'
                DC    AL1($SCREEN_ATTR_NORMAL)
OFFLD_MSG_L     EQU   *-OFFLD_MSG

RESET_MSG       EQU   *
                DC    AL1($SCREEN_ATTR_HIGH)
                DC    C'<-------- Reset    '
                DC    AL1($SCREEN_ATTR_NORMAL)
RESET_MSG_L     EQU   *-RESET_MSG

TAGGED_MSG      EQU   *
                DC    AL1($SCREEN_ATTR_HIGH)
                DC    C'<-------- Tagged   '
                DC    AL1($SCREEN_ATTR_NORMAL)
TAGGED_MSG_L    EQU   *-TAGGED_MSG

MSG01    DC    C'DATASPACE dump complete'
MSG01_L  EQU   *-MSG01
MSG02    DC    C'Member deletion aborted'
MSG02_L  EQU   *-MSG02
MSG03    DC    C'Member rename ABORTED'
MSG03_L  EQU   *-MSG03

HEADING_01      DS    0C
                DC    AL1($SCREEN_ATTR_HIGH_YELLOW)
                DC    CL3' '
                DC    Cl8'Name    '
                DC    CL3' '
                DC    CL6' TTR  '
                DC    CL2' '
                DC    CL5'VV.MM'
                DC    CL1' '
                DC    CL7'   Size'
                DC    CL1' '
                DC    CL7'   Init'
                DC    CL1' '
                DC    CL8'Created'
                DC    CL1' '
                DC    CL17'____Modified_____'
                DC    CL1' '
                DC    CL8'User'
HEADING_01_L    EQU   *-HEADING_01

                LTORG


LINE_TABLE      DS    0H
                CMD   A,MAIN0420,'Append'
                CMD   B,MAIN0620,'Browse'
                CMD   D,MAIN1200,'Delete'
                CMD   J,MAIN1110,'Submit as batch job'
                CMD   O,MAIN0510,'Offload'
                CMD   R,MAIN1530,'Rename'
                CMD   S,MAIN0620,'Select (browse)'
                CMD   T,MAIN0710,'Tag'
                CMD   V,MAIN0620,'View (browse)'
                CMD   X,MAIN0540,'Reset tag'
                CMD   ?,MAIN2200,'Display available line commands'
                DC    X'FF'

COMMAND_TABLE   DS    0H
                CMD   A,MAIN0800,'Append'
                CMD   B,MAIN0630,'Browse'
                CMD   D,MAIN1230,'Delete'
                CMD   F,MAIN0450,'Find'
                CMD   J,MAIN1100,'Submit as batch job'
                CMD   L,MAIN0450,'Locate'
                CMD   O,MAIN0900,'Offload'
                CMD   R,MAIN1500,'Rename'
                CMD   S,MAIN0630,'Select (browse)'
                CMD   T,MAIN0720,'Tag'
                CMD   X,MAIN0550,'Reset tag'
                CMD   BR,MAIN0630,'BRowse'
                CMD   FI,MAIN0450,'FInd'
                CMD   FL,0,'List available formats'
                CMD   SH,MAIN2000,'Set SHOW (tagged or all)'
                CMD   DEL,MAIN1230,'DELete'
                CMD   JOB,MAIN1100,'Subit as batch job'
                CMD   LOC,MAIN0450,'LOCate'
                CMD   OFF,MAIN0900,'OFFload'
                CMD   REN,MAIN1500,'REName'
                CMD   RES,MAIN0550,'RESet tag'
                CMD   SEL,MAIN0630,'SELect (browse)'
                CMD   SUB,MAIN1100,'SUBmit as batch job'
                CMD   TAG,MAIN0720,'TAG'
                CMD   FIND,MAIN0450,'FIND'
                CMD   FLAG,MAIN0720,'FLAG (tag)'
                CMD   LIST,0,'List available formats'
                CMD   SHOW,MAIN2000,'Set SHOW (tagged or all)'
                CMD   SORT,MAIN1000,'SORT name/ttr/cdate/mdate/size/use+
               r'
                CMD   VIEW,MAIN0630,'VIEW (browse)'
                CMD   DEBUG,DEBUG000,'Display internal data'
                CMD   FLIST,0,'List available formats'
                CMD   RESET,MAIN0550,'RESET tag'
                CMD   RFIND,MAIN0470,'Repeat previous FIND'
                CMD   APPEND,MAIN0800,'APPEND'
                CMD   BROWSE,MAIN0630,'BROWSE'
                CMD   DELETE,MAIN1230,'DELETE'
                CMD   LOCATE,MAIN0450,'LOCATE'
                CMD   RENAME,MAIN1500,'RENAME'
                CMD   SELECT,MAIN0630,'SELECT'
                CMD   SUBMIT,MAIN1100,'SUBMIT as batch job'
                CMD   OFFLOAD,MAIN0900,'OFFLOAD'
                CMD   REFRESH,MAIN2100,'REFRESH'
                CMD   DUMPDATA,MAIN3000,'Dump dataspace'
                CMD   ??,MAIN3200,'Display available commands'
                DC    X'FF'


OSDIREND        EQU   *
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
OSDIR_WORK       DSECT
                 COPY   DXDPREF

DXD_OPEN         DS     0F,(OPEN_L)X
DXD_CLOSE        DS     0F,(CLOSE_L)X

DXD_SPF_PARMS    DS     0F
DXD_SPF_1        DS     F
DXD_SPF_2        DS     F
DXD_SPF_3        DS     F

DXD_VWIDTH       DS     F
DXD_VDEPTH       DS     F

DXD_VDATA_ADDR      DS  A
DXD_VDATA_LENGTH    DS  F
DXD_CURSOR_POSITION DS  F

DXD_LINES_REMAINING DS  F
DXD_CURRENT_VDATA   DS  A

DXD_RC           DS     H

DXD_LINES        DS     H

DXD_FLAGS        DS     X
$DXD_INIT        EQU    X'80'
$LINE_CMD        EQU    X'40'
$EMPTY_MEMBER    EQU    X'20'

DXD_AND          DS     X
DXD_OR           DS     X

DXD_WORK_1       DS     CL10

DXD_RENAME_OLD   DS     CL8
DXD_RENAME_NEW   DS     CL8

DXD_SELECTION    DS     C
DXD_MEMBER       DS     CL8

                 COPY   DIR

DUMP_CONTROL     DS     0F
DUMP_DCB         DS     (DUMP_DCB_L)X
DUMP_IO_AREA     DS     CL133

DXD_MSG          DS     CL80

                 DAIRREQ  DSECT=NO
                 DSPCREQ  DSECT=NO
                 APPNDREQ DSECT=NO
                 LOADREQ  DSECT=NO
                 OFFLDREQ DSECT=NO
                 SUBREQ   DSECT=NO
OSDIR_WORK       DSECT
OSDIR_DXD_L      EQU      *-OSDIR_WORK
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
VDATA             DSECT
VDATA_ATTR_1      DS    X
VDATA_SELECTION   DS    X
VDATA_ATTR_2      DS    X
                  DS    C
VDATA_MEMBER      DS    CL8
VDATA_ATTR_3      DS    X
                  DS    CL2
VDATA_STATISTICS  DS    0CL65
VDATA_TTR         DS    CL6
                  DS    CL2
VDATA_VERSION     DS    CL2
VDATA_DOT         DS    C
VDATA_LEVEL       DS    CL2
                  DS    C
VDATA_SIZE        DS    CL7
                  DS    C
VDATA_INIT        DS    CL7
                  DS    C
VDATA_CREATE_DATE DS    CL8
                  DS    C
VDATA_MOD_DATE    DS    CL8
                  DS    C
VDATA_MOD_TIME    DS    CL8
                  DS    C
VDATA_USER        DS    CL8
VDATA_L           EQU   *-VDATA

* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                  COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                  SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                  COPY     OPERAND
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                  BPXYSTAT DSECT=YES,LIST=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                  COPY     COMMAND
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                  OSSPFD  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                  IBMMAC  DCBD=PO
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                  COPY    ATTRS
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                  COPY    TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                  COPY    REGEQU
                  END     OSDIR
