* ------------------------------------------------------------------- *
*                                                                     *
*  Module name: EXITSAMP                                              *
*                                                                     *
*                                                                     *
*  Sample user exit for formats specifying EXIT.                      *
*                                                                     *
*  On entry R1 is the address of the USEREXIT parameter block.        *
*                                                                     *
* ------------------------------------------------------------------- *
EXITSAMP CSECT
EXITSAMP AMODE 31
EXITSAMP RMODE ANY
         USING OSCOMM,R12
         USING SESSION,R11
         USING EXITSAMP,R15
         B     INIT0000
MODID    DC    CL8'EXITSAMP'
         DC    CL8'&SYSDATE'
         DC    CL7'&SYSTIME'
INIT0000 DS    0H
         STM   R14,R12,12(R13)                SAVE REGS
         LR    R10,R15                        COPY ENTRY POINT
         USING EXITSAMP,R10                   DEFINE BASE
         DROP  R15
         LR    R9,R1                          COPY USEREXIT BLOCK ADDR
         USING USEREXIT,R9                    DEFINE BASE
         CLI   USEREXIT_FUNC,$USEREXIT_INITIALIZE
         BE    INIT0010
         CLI   USEREXIT_FUNC,$USEREXIT_FORMAT
         BE    FMAT0000
         CLI   USEREXIT_FUNC,$USEREXIT_CMD
         BE    CMD0000
         CLI   USEREXIT_FUNC,$USEREXIT_CLEANUP
         BE    CLEAN000
         DC    H'0'                           <-- BAD FUNCTION
* ------------------------------------------------------------------- *
*        Initialization                                               *
* ------------------------------------------------------------------- *
INIT0010 DS    0H
         ITRACE ID=INIT
         GETMAIN RU,                                                   +
               LV=WORKAREA_L,                                          +
               LOC=ANY                        <-- MAY NEED TO CHANGE
         ST    R1,USEREXIT_USERWORD           SAVE WORK AREA ADDRESS
         USING WORKAREA,R1
         MVC   DXD_CSECT,MODID
         DROP  R1
         B     EXIT0010
* ------------------------------------------------------------------- *
*        FORMATTING                                                   *
* ------------------------------------------------------------------- *
FMAT0000 DS    0H
         L     R1,USEREXIT_USERWORD           WORK AREA
         ST    R13,4(R1)                      SAVE R13
         ST    R1,8(R13)                      CHAIN SAVE AREA
         LR    R13,R1                         COPY WORK AREA ADDRESS
         USING WORKAREA,R13                   DEFINE WORK AREA BASE
         ITRACE ID=FORMAT
         L     R8,USEREXIT_RECORD_ADDR        RECORD ADDRESS
         L     R7,USEREXIT_VDATA_ADDR         DYNAMIC STORAGE ADDR
         L     R6,USEREXIT_ROWS               NUMBER OF ROWS AVAILABLE



*         Add your formatting code here
*
*        R12   OSCOMM address
*        R11   SESSION address
*        R10   This program's base register
*        R9    Address of EXITPARM
*        R8    address of data to be formatted
*        R7    dynamic area address
*        R6    number of rows available in dynamic area





         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
CMD0000  DS    0H
         L     R1,USEREXIT_USERWORD           WORK AREA
         ST    R13,4(R1)                      SAVE R13
         ST    R1,8(R13)                      CHAIN SAVE AREA
         LR    R13,R1                         COPY WORK AREA ADDRESS
         USING WORKAREA,R13                   DEFINE WORK AREA BASE
         ITRACE ID=CMD,                                                +
               DATA1=USEREXIT_ZCMD,                                    +
               DATA2=USEREXIT_ZVERB
         CLI   USEREXIT_ZVERB,C'B'            BOTTOM?
         BE    CMD0010                        YES
         CLI   USEREXIT_ZVERB,C'D'            SCROLL DOWN?
         BE    CMD0020                        YES
         CLI   USEREXIT_ZVERB,C'T'            TOP?
         BE    CMD0030                        YES
         CLI   USEREXIT_ZVERB,C'U'            SCROLL UP?
         BE    CMD0040                        YES
         CLC   USEREXIT_ZCMD,COMM_BLANKS      ALL BLANK?
         BE    EXIT0000                       YES
         B     ERR0010
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
CMD0010  DS    0H
         ITRACE ID=BOTTOM
         MVC   USEREXIT_ZCMD,COMM_BLANKS                CLEAR CMD
         MVC   USEREXIT_RECORD_NBR,SESS_LAST_RECORD_1   SET RECORD NBR
         B     EXIT0000
CMD0020  DS    0H
         ITRACE ID=DOWN
         MVC   USEREXIT_ZCMD,COMM_BLANKS                CLEAR COMMAND
         CLI   USEREXIT_ZSCROLLA,C'M'                   MAX?
         BE    CMD0010                                  YES
         L     R1,USEREXIT_RECORD_NBR                   RECORD NBR
         A     R1,F1                                    PLUS 1
         C     R1,USEREXIT_MAX_RECORD_NBR               BEYOND LAST?
         BH    ERR0020                                  YES
         ST    R1,USEREXIT_RECORD_NBR                   SET RECORD NBR
         B     EXIT0000
CMD0030  DS    0H
         ITRACE ID=TOP
         MVC   USEREXIT_ZCMD,COMM_BLANKS                CLEAR COMMAND
         MVC   USEREXIT_RECORD_NBR,F1                   SET RECORD NBR
         B     EXIT0000
CMD0040  DS    0H
         ITRACE ID=UP
         MVC   USEREXIT_ZCMD,COMM_BLANKS                CLEAR COMMAND
         CLI   USEREXIT_ZSCROLLA,C'M'                   MAX?
         BE    CMD0050                                  YES
         L     R1,USEREXIT_RECORD_NBR                   CURRENT RECORD
         BCT   R1,CMD0050                               MINUS 1
         B     ERR0030                                  NO..
CMD0050  DS    0H
         ST    R1,USEREXIT_RECORD_NBR                   SET RECORD NBR
         B     EXIT0000
* ------------------------------------------------------------------- *
*        Clean up                                                     *
* ------------------------------------------------------------------- *
CLEAN000 DS    0H
         ITRACE ID=CLEAN_UP
         L     R1,USEREXIT_USERWORD           WORK AREA ADDRESS
         FREEMAIN RU,                         FREEMAIN IT              +
               A=(1),                                                  +
               LV=WORKAREA_L
         XC    USEREXIT_USERWORD,USEREXIT_USERWORD
         B     EXIT0010
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0010  DS    0H
         MVC   USEREXIT_MSG_1(ERROR_MSG_01_L),ERROR_MSG_01
         B     EXIT0000
ERR0020  DS    0H
         MVC   USEREXIT_MSG_1(ERROR_MSG_02_L),ERROR_MSG_02
         B     EXIT0000
ERR0030  DS    0H
         MVC   USEREXIT_MSG_1(ERROR_MSG_03_L),ERROR_MSG_03
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=EXIT0000
         L     R13,4(R13)                     RESTORE SAVE AREA ADDRESS
EXIT0010 DS    0H
         ITRACE ID=EXIT0010
         LM    R14,R12,12(R13)                RESTORE REGISTERS
         SR    R15,R15                        SET RC
         BR    R14                            RETURN
* ------------------------------------------------------------------- *
*                   Constants                                         *
* ------------------------------------------------------------------- *
F1              DC       F'1'


ERROR_MSG_01    DC       C'UNRECOGNIZED COMMAND'
ERROR_MSG_01_L  EQU      *-ERROR_MSG_01

ERROR_MSG_02    DC       C'AT BOTTOM OF DATA'
ERROR_MSG_02_L  EQU      *-ERROR_MSG_02

ERROR_MSG_03    DC       C'AT TOP OF DATA'
ERROR_MSG_03_L  EQU      *-ERROR_MSG_03

                LTORG
* ------------------------------------------------------------------- *
*               Work area                                             *
* ------------------------------------------------------------------- *
WORKAREA        DSECT
                COPY     DXDPREF
                USEREXIT DSECT=NO
WORKAREA_L      EQU      *-WORKAREA
* ------------------------------------------------------------------- *
*               Common data                                           *
* ------------------------------------------------------------------- *
                COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                BPXYSTAT DSECT=YES,LIST=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                COPY     ATTRS
* ------------------------------------------------------------------- *
*               EQUATES                                               *
* ------------------------------------------------------------------- *
                COPY     REGEQU
                END      EXITSAMP
