*---------------------------------------------------------------------*
*                                                                     *
*         Format SMF record type 25                                   *
*         JOB initiation                                              *
*                                                                     *
*                                                                     *
*   At entry                                                          *
*       R15    program base address                                   *
*       R14    return address                                         *
*       R9     OSSPFD address                                         *
*       R8     VDATA buffer address                                   *
*       R7     SMF record address +4 (does not include length)        *
*       R6     Number of lines available                              *
*                                                                     *
* ------------------------------------------------------------------- *
SMF0025  CSECT
SMF0025  AMODE 31
SMF0025  RMODE ANY
         USING SESSION,R11
         USING SMF0025,R15
         B     INIT0000
MODID    DC    CL8'SMF0025'
         DC    CL8'&SYSDATE'
         DC    CL7'&SYSTIME'
INIT0000 DS    0H
         USING OSCOMM,R12            DEFINE COMMON AREA BASE
         STM   R14,R12,12(R13)       SAVE REGS
         LR    R10,R15               COPY ENTRY POINT
         USING SMF0025,R10           DEFINE BASE
         DROP  R15
         L     R1,SESS_DXD_ADDR
         A     R1,DXDSTART
         ST    R13,4(R1)             SAVE R13
         ST    R1,8(R13)             CHAIN SAVE AREA
         LR    R13,R1                COPY WORK AREA ADDRESS
         USING WK0025,R13            DEFINE BASE
         MVC   DXD_CSECT,MODID
         USING OSSPFD,R9             DEFINE BASE
         ITRACE ID=ENTRY
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
         USING SMF25FLG,R7

         MVC   0(MSG_01_L,R8),MSG_01 COPY MESSAGE
         USING MSG_01,R8             DEFINE BASE
         MVC   MSG_01_JOB,SMF25JBN   COPY JOB NAME
         DROP  R8
         A     R8,SPF_VWIDTH
         BCTR  R6,0

         MVC   0(MSG_02_L,R8),MSG_02 COPY MESSAGE
         USING MSG_02,R8             DEFINE BASE
         MVC   MSG_02_USER,SMF25UIF  COPY USER ID
         DROP  R8
         A     R8,SPF_VWIDTH
         BCTR  R6,0

         MVC   0(MSG_03_L,R8),MSG_03 COPY MESSAGE
         USING MSG_03,R8             DEFINE BASE
         LA    R1,MSG_03_FLAGS
         TM    SMF25IND,X'80'        DYNAMIC ALLOCATION?
         BO    MAIN0010              YES
         MVC   0(L'STATIC,R1),STATIC
         LA    R1,L'STATIC+2(,R1)
         B     MAIN0020
MAIN0010 DS    0H
         MVC   0(L'DYNAMIC,R1),DYNAMIC
         LA    R1,L'DYNAMIC+2(,R1)
MAIN0020 DS    0H
         TM    SMF25IND,X'40'        NON-CATALOG ALLOCATIONS?
         BO    MAIN0030
         MVC   0(L'CATALOG,R1),CATALOG
         LA    R1,L'CATALOG+2(,R1)
         B     MAIN0040
MAIN0030 DS    0H
         MVC   0(L'NON_CATALOG,R1),NON_CATALOG
         LA    R1,L'NON_CATALOG+2(,R1)
MAIN0040 DS    0H
         TM    SMF25IND,X'20'        AUTOMATIC ALLOCATIONS?
         BO    MAIN0050              YES
         MVC   0(L'MANUAL,R1),MANUAL
         B     MAIN0060
MAIN0050 DS    0H
         MVC   0(L'AUTOMATIC,R1),AUTOMATIC
MAIN0060 DS    0H
         DROP  R8
         A     R8,SPF_VWIDTH
         BCTR  R6,0

         MVC   0(MSG_04_L,R8),MSG_04 COPY MESSAGE
         USING MSG_04,R8             DEFINE BASE
         ICM   R0,15,SMF25NTF        NBR OF TAPES FETCHED
         CVD   R0,COMM_DWORD
         ED    MSG_04_TAPES,COMM_DWORD+5
         DROP  R8
         A     R8,SPF_VWIDTH
         BCTR  R6,0

         MVC   0(MSG_05_L,R8),MSG_05 COPY MESSAGE
         USING MSG_05,R8             DEFINE BASE
         ICM   R0,15,SMF25NTM        NBR OF TAPES MOUNTED
         CVD   R0,COMM_DWORD
         ED    MSG_05_TAPES,COMM_DWORD+5
         DROP  R8
         A     R8,SPF_VWIDTH
         BCTR  R6,0

         MVC   0(MSG_06_L,R8),MSG_06 COPY MESSAGE
         USING MSG_06,R8             DEFINE BASE
         ICM   R0,15,SMF25NDF        NBR OF DISKS FETCHED
         CVD   R0,COMM_DWORD
         ED    MSG_06_DISKS,COMM_DWORD+5
         DROP  R8
         A     R8,SPF_VWIDTH
         BCTR  R6,0

         MVC   0(MSG_07_L,R8),MSG_07 COPY MESSAGE
         USING MSG_07,R8             DEFINE BASE
         ICM   R0,15,SMF25NDM        NBR OF DISKS MOUNTED
         CVD   R0,COMM_DWORD
         ED    MSG_07_DISKS,COMM_DWORD+5
         DROP  R8
         A     R8,SPF_VWIDTH
         BCTR  R6,0
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
EXIT0000 DS    0H
         SR    R2,R2
         B     EXITEXIT
EXIT0004 DS    0H
         LA    R2,4
EXITEXIT DS    0H
         ITRACE ID=EXIT,                                               +
               RDATA1=R2
         L     R13,4(R13)            RESTORE SAVE AREA ADDRESS
         L     R14,12(,R13)          RESTORE R14
         SR    R15,R15               SET RC
         LM    R0,R12,20(R13)        RESTORE REMAINING REGISTERS
         BR    R14                   RETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDSTART       DC    Q(WK0025)
DYNAMIC        DC    C'DYNAMIC'
STATIC         DC    C'STATIC'
CATALOG        DC    C'CATALOG'
NON_CATALOG    DC    C'NON-CATALOG'
AUTOMATIC      DC    C'AUTOMATIC'
MANUAL         DC    C'MANUAL'

MSG_01         DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'Job name     '
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_01_JOB     DC    CL8' '
MSG_01_L       EQU   *-MSG_01

MSG_02         DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'User         '
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_02_USER    DC    CL8' '
MSG_02_L       EQU   *-MSG_02

MSG_03         DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'Flags        '
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_03_FLAGS   DC    CL50' '
MSG_03_L       EQU   *-MSG_03

MSG_04         DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'Tapes fetched'
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_04_TAPES   DC    X'402020202120'
MSG_04_L       EQU   *-MSG_04

MSG_05         DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'Tapes mounted'
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_05_TAPES   DC    X'402020202120'
MSG_05_L       EQU   *-MSG_05

MSG_06         DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'Disks fetched'
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_06_DISKS   DC    X'402020202120'
MSG_06_L       EQU   *-MSG_06

MSG_07         DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'Disks mounted'
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_07_DISKS   DC    X'402020202120'
MSG_07_L       EQU   *-MSG_07


* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
WK0025         DSECT
               COPY     DXDPREF
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              SESSION
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              BPXYSTAT DSECT=YES,LIST=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              OSSPFD   TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
SMF_RECORD    DSECT
              IFASMFR  25
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              COPY     ATTRS
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              COPY     REGEQU
              END      SMF0025
