*---------------------------------------------------------------------*
*                                                                     *
*         Format Page data set activity (SMF type 75)                 *
*         ICF define activity                                         *
*                                                                     *
*   At entry                                                          *
*       R15    program base address                                   *
*       R14    return address                                         *
*       R9     OSSPFD ADDRESS                                         *
*       R8     VDATA buffer address                                   *
*       R7     SMF RECORD ADDRESS +4 (DOES NOT INCLUDE LENGTH)        *
*                                                                     *
* ------------------------------------------------------------------- *
SMF0075  CSECT
SMF0075  AMODE 31
SMF0075  RMODE ANY
         USING SESSION,R11
         USING SMF0075,R15
         B     INIT0000
MODID    DC    CL8'SMF0075'
         DC    CL8'&SYSDATE'
         DC    CL7'&SYSTIME'
INIT0000 DS    0H
         USING OSCOMM,R12                     DEFINE COMMON AREA BASE
         STM   R14,R12,12(R13)                SAVE REGS
         LR    R10,R15                        COPY ENTRY POINT
         USING SMF0075,R10                    DEFINE BASE
         DROP  R15
         L     R1,SESS_DXD_ADDR
         A     R1,DXDSTART
         ST    R13,4(R1)                      SAVE R13
         ST    R1,8(R13)                      CHAIN SAVE AREA
         LR    R13,R1                         COPY WORK AREA ADDRESS
         USING WK0075,R13                     DEFINE BASE
         MVC   DXD_CSECT,MODID
         USING OSSPFD,R9                      DEFINE BASE
         USING SMF75FLG,R7                    DEFINE BASE
         L     R6,SPF_VDEPTH
         L     R5,SMF75PSS                    OFFSET TO PAGE INFO
         AR    R5,R7                          PLUS RECORD BASE
         SH    R5,H4
         USING SMF75PSD,R5                    DEFINE BSE
         LH    R4,SMF75PSN                    NBR OF SECTIONS
         MVC   0(PAGE_HEADING_L,R8),PAGE_HEADING
         A     R8,SPF_VWIDTH
         BCTR  R6,0
         A     R8,SPF_VWIDTH
         BCTR  R6,0
MAIN0010 DS    0H
         MVC   0(MSG_01_L,R8),MSG_01          INITIALIZE MESSAGE
         USING MSG_01,R8
         MVC   MSG_01_DSN,SMF75DSN            COPY DATA SET NAME
         DROP  R8
         A     R8,SPF_VWIDTH                  NEXT LINE

         MVC   0(MSG_02_L,R8),MSG_02          INITIALIZE MESSAGE
         USING MSG_02,R8
         MVC   MSG_02_VOLSER,SMF75VOL         COPY VOLSER
         DROP  R8
         A     R8,SPF_VWIDTH                  NEXT LINE

         MVC   0(MSG_03_L,R8),MSG_03          INITIALIZE MESSAGE
         USING MSG_03,R8
         TM    SMF75PST,SMF75LPA              PLPA?
         BO    MAIN0020
         TM    SMF75PST,SMF75COM              COMMON?
         BO    MAIN0030
         TM    SMF75PST,SMF75LOC              LOCAL?
         BO    MAIN0040
         B     MAIN0050
MAIN0020 DS    0H
         MVC   MSG_03_TYPE,TYPE_PLPA
         B     MAIN0050
MAIN0030 DS    0H
         MVC   MSG_03_TYPE,TYPE_COMMON
         B     MAIN0050
MAIN0040 DS    0H
         MVC   MSG_03_TYPE,TYPE_LOCAL
         DROP  R8
MAIN0050 DS    0H
         A     R8,SPF_VWIDTH

MAIN0060 DS    0H
         TM    SMF75PST,SMF75DSB+SMF75ONL+SMF75OFL
         BNO   MAIN0100
         MVC   0(MSG_04_L,R8),MSG_04          INITIALIZE MESSAGE
         USING MSG_04,R8
         TM    SMF75PST,SMF75DSB              BAD?
         BNO   MAIN0070                       NO
         MVC   MSG_04_BAD,YES
MAIN0070 DS    0H
         TM    SMF75PST,SMF75ONL              VARIED ONLINE?
         BNO   MAIN0080                       NO
         MVC   MSG_04_ONLINE,YES
MAIN0080 DS    0H
         TM    SMF75PST,SMF75OFL              VARIED OFFLINE
         BNO   MAIN0090
         MVC   MSG_04_OFFLINE,YES
         DROP  R8
MAIN0090 DS    0H
         A     R8,SPF_VWIDTH                  NEXT LINE

MAIN0100 DS    0H
         MVC   0(MSG_05_L,R8),MSG_05          INITIALIZE MESSAGE
         USING MSG_05,R8
         L     R0,SMF75SLA                    TOTAL SLOTS
         CVD   R0,COMM_DWORD                  CONVERT TO DECIMAL
         ED    MSG_05_TOTAL,COMM_DWORD+4
         DROP  R8
         A     R8,SPF_VWIDTH

         MVC   0(MSG_06_L,R8),MSG_06          INITIALIZE MESSAGE
         USING MSG_06,R8
         L     R0,SMF75BDS                    BAD SLOTS
         CVD   R0,COMM_DWORD                  CONVERT TO DECIMAL
         ED    MSG_06_BAD,COMM_DWORD+4
         DROP  R8
         A     R8,SPF_VWIDTH

         MVC   0(MSG_07_L,R8),MSG_07          INITIALIZE MESSAGE
         USING MSG_07,R8
         L     R0,SMF75MXU                    MAX IN USE
         CVD   R0,COMM_DWORD                  CONVERT TO DECIMAL
         ED    MSG_07_MAX,COMM_DWORD+4
         DROP  R8
         A     R8,SPF_VWIDTH

         MVC   0(MSG_08_L,R8),MSG_08          INITIALIZE MESSAGE
         USING MSG_08,R8
         L     R0,SMF75MNU                    MINIMUM IN USE
         CVD   R0,COMM_DWORD                  CONVERT TO DECIMAL
         ED    MSG_08_MIN,COMM_DWORD+4
         DROP  R8
         A     R8,SPF_VWIDTH

         MVC   0(MSG_09_L,R8),MSG_09          INITIALIZE MESSAGE
         USING MSG_09,R8
         L     R0,SMF75AVU                    AVERAGE NUMBER IN USE
         CVD   R0,COMM_DWORD
         ED    MSG_09_AVERAGE,COMM_DWORD+4
         DROP  R8
         A     R8,SPF_VWIDTH                  NEXT LINE

         MVC   0(MSG_10_L,R8),MSG_10          INITIALIZE MESSAGE
         USING MSG_10,R8
         L     R0,SMF75USE                    NBR OF TIMES USED
         CVD   R0,COMM_DWORD                  CONVERT TO DECIMAL
         ED    MSG_10_USE,COMM_DWORD+4
         DROP  R8
         A     R8,SPF_VWIDTH

         MVC   0(MSG_11_L,R8),MSG_11          INITIALIZE MESSAGE
         USING MSG_11,R8
         L     R0,SMF75SIO                    NUMBER OF SIO'S
         CVD   R0,COMM_DWORD                  CONVERT TO DECIMAL
         ED    MSG_11_SIOS,COMM_DWORD+4
         DROP  R8
         A     R8,SPF_VWIDTH

         MVC   0(MSG_12_L,R8),MSG_12          INITIALIZE MESSAGE
         USING MSG_12,R8
         L     R0,SMF75PGX                    PAGES TRANSFERRED
         CVD   R0,COMM_DWORD                  CONVERT TO DECIMAL
         ED    MSG_12_PAGES,COMM_DWORD+4
         DROP  R8
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
EXIT0000 DS    0H
         ITRACE ID=EXIT
         L     R13,4(R13)                     RESTORE SAVE AREA ADDRESS
         L     R14,12(,R13)                   RESTORE R14
         LM    R14,R12,12(R13)                RESTORE REGISTERS
         SR    R15,R15
         BR    R14                            RETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDSTART       DC    Q(WK0075)
H4             DC    H'4'

TYPE_PLPA      DC    CL06'PLPA'
TYPE_COMMON    DC    CL06'COMMON'
TYPE_LOCAL     DC    CL06'LOCAL'
YES            DC    CL3'Yes'

PAGE_HEADING   DS    0C
               DC    20C' '
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'Page dataset(s)'
               DC    AL1($SCREEN_ATTR_NORMAL)
PAGE_HEADING_L EQU   *-PAGE_HEADING

MSG_01         DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'DSN                '
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_01_DSN     DC    CL44' '
MSG_01_L       EQU   *-MSG_01

MSG_02         DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'Volser             '
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_02_VOLSER  DC    CL06' '
MSG_02_L       EQU   *-MSG_02

MSG_03         DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'Type               '
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_03_TYPE    DC    CL06' '
MSG_03_L       EQU   *-MSG_03


MSG_04         DS    0C
               DC    AL1($SCREEN_ATTR_HIGH_TURQUOISE)
               DC    C'Data set bad?      '
               DC    AL1($SCREEN_ATTR_HIGH_RED)
MSG_04_BAD     DS    CL3' '
               DC    AL1($SCREEN_ATTR_HIGH_TURQUOISE)
               DC    C'    Varied online? '
               DC    AL1($SCREEN_ATTR_HIGH_GREEN)
MSG_04_ONLINE  DS    CL3' '
               DC    AL1($SCREEN_ATTR_HIGH_TURQUOISE)
               DC    C'    Varied offline?'
               DC    AL1($SCREEN_ATTR_HIGH_RED)
MSG_04_OFFLINE DS    CL3' '
MSG_04_L       EQU   *-MSG_04

MSG_05         DS    0C
               DC    C' Slots Total       '
MSG_05_TOTAL   DC    X'4020202020202120'
MSG_05_L       EQU   *-MSG_05

MSG_06         DS    0C
               DC    C' Slots bad         '
MSG_06_BAD     DC    X'4020202020202120'
MSG_06_L       EQU   *-MSG_06

MSG_07         DS    0C
               DC    C'Maximum nbr in use '
MSG_07_MAX     DC    X'4020202020202120'
MSG_07_L       EQU   *-MSG_07

MSG_08         DS    0C
               DC    C'Minimum nbr in use '
MSG_08_MIN     DC    X'4020202020202120'
MSG_08_L       EQU   *-MSG_08

MSG_09         DS    0C
               DC    C'Average nbr in use '
MSG_09_AVERAGE DC    X'4020202020202120'
MSG_09_L       EQU   *-MSG_09

MSG_10         DS    0C
               DC    C'Times in use by ASM'
MSG_10_USE     DC    X'4020202020202120'
MSG_10_L       EQU   *-MSG_10

MSG_11         DS    0C
               DC    C'SIOs               '
MSG_11_SIOS    DC    X'4020202020202120'
MSG_11_L       EQU   *-MSG_11

MSG_12         DS    0C
               DC    C'Pages transferred  '
MSG_12_PAGES   DC    X'4020202020202120'
MSG_12_L       EQU   *-MSG_12

* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
WK0075         DSECT
               COPY      DXDPREF
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               SESSION
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               BPXYSTAT  DSECT=YES,LIST=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               OSSPFD    TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
SMF_RECORD     DSECT
               ERBSMF75
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY      ATTRS
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY      TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY     REGEQU
               END      SMF0075
