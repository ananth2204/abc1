*---------------------------------------------------------------------*
*                                                                     *
*         Format SMF record type 09 (device varied on line)           *
*                                                                     *
*                                                                     *
*   At entry                                                          *
*       R15    program base address                                   *
*       R14    return address                                         *
*       R9     OSSPFD address                                         *
*       R8     VDATA buffer address                                   *
*       R7     SMF record address +4 (does not include length)        *
*                                                                     *
* ------------------------------------------------------------------- *
SMF0009  CSECT
SMF0009  AMODE 31
SMF0009  RMODE ANY
         USING OSCOMM,R12
         USING SESSION,R11
         USING SMF0009,R15
         B     INIT0000
MODID    DC    CL8'SMF0009'
         DC    CL8'&SYSDATE'
         DC    CL7'&SYSTIME'
INIT0000 DS    0H
         STM   R14,R12,12(R13)                SAVE REGS
         LR    R10,R15                        COPY ENTRY POINT
         DROP  R15
         USING SMF0009,R10                    DEFINE BASE
         L     R1,SESS_DXD_ADDR
         A     R1,DXDSTART
         ST    R13,4(R1)                      SAVE R13
         ST    R1,8(R13)                      CHAIN SAVE AREA
         LR    R13,R1                         COPY WORK AREA ADDRESS
         USING WK0009,R13                     DEFINE BASE
         USING OSSPFD,R9                      DEFINE BASE
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
MAIN0010 DS    0H
         USING SMF9FLG,R7
         A     R8,SPF_VWIDTH                  LEAVE A BLANK LINE
*---------------------------------------------------------------------*
*        Process SMF date and time                                    *
*---------------------------------------------------------------------*
         MVC   0(MSG01_L,R8),MSG01
         USING MSG01,R8                       DEFINE BASE
         MVC   DXD_DATE,SMF9DTE
         AP    DXD_DATE,P1900000              ADD CENTURY
         MVC   DXD_A(L'EDIT_WORD_2),EDIT_WORD_2
         ED    DXD_A(L'EDIT_WORD_2),DXD_DATE
         MVC   MSG01_DTE,DXD_A+1              COPY DATE
         ICM   R1,15,SMF9TME                  TIME
         BAL   R15,TIME0000                   CONVERT TIME
         MVC   MSG01_TME,DXD_B                COPY TIME
         A     R8,SPF_VWIDTH                  NEXT LINE
         DROP  R8
*---------------------------------------------------------------------*
*        Process device number                                        *
*---------------------------------------------------------------------*
         MVC   0(MSG02_L,R8),MSG02
         USING MSG02,R8                       DEFINE BASE
         UNPK  COMM_INFO_01(5),SMF9CUA(3)     UNPACK DEVICE
         TR    COMM_INFO_01(4),COMM_HEXCHAR   TRANSLATE
         MVC   MSG02_CUA,COMM_INFO_01         COPY TO VDATA
         DROP  R8
         A     R8,SPF_VWIDTH                  NEXT LINE
*---------------------------------------------------------------------*
*        Process vary path caller                                     *
*---------------------------------------------------------------------*
         MVC   0(MSG03_L,R8),MSG03
         USING MSG03,R8                       DEFINE BASE
         CLI   SMF9VPC,0                      CONSOLE?
         BE    MAIN0020                       YES
         CLI   SMF9VPC,2                      ESCON MANAGER?
         BE    MAIN0030
         B     EXIT0000
MAIN0020 DS    0H
         MVC   MSG03_CALLER,=CL15'CONSOLE'    INDICATE CONSOLE
         B     EXIT0000
MAIN0030 DS    0H
         MVC   MSG03_CALLER,=CL15'ESCON manager'
         B     EXIT0000
         DROP  R8
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
TIME0000 DS    0H
         SR    R0,R0
         D     R0,F100                        WHOLE SECONDS
         SR    R0,R0                          CLEAR REMAINDER
         D     R0,F3600                       COMPUTE HOURS
         CVD   R1,COMM_DWORD                  CONVERT HOURS
         MVC   DXD_A(L'EDIT_WORD_1),EDIT_WORD_1
         ED    DXD_A(L'EDIT_WORD_1),COMM_DWORD+6
         MVC   DXD_B+0(2),DXD_A+2             COPY HOURS
         MVI   DXD_B+2,C':'                   INSERT COLON
         LR    R1,R0                          COPY REMAINDER
         SR    R0,R0                          CLEAR R0
         D     R0,F60                         COMPUTE MINUTES
         CVD   R1,COMM_DWORD                  CONVERT MINUTES
         MVC   DXD_A(L'EDIT_WORD_1),EDIT_WORD_1
         ED    DXD_A(L'EDIT_WORD_1),COMM_DWORD+6
         MVC   DXD_B+3(2),DXD_A+2             COPY MINUTES
         MVI   DXD_B+5,C':'                   INSERT COLON
         CVD   R0,COMM_DWORD                  CONVERT SECONDS
         MVC   DXD_A(L'EDIT_WORD_1),EDIT_WORD_1
         ED    DXD_A(L'EDIT_WORD_1),COMM_DWORD+6
         MVC   DXD_B+6(2),DXD_A+2             COPY SECONDS
         BR    R15
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
EXIT0000 DS    0H
         SR    R2,R2
         ITRACE ID=EXIT,                                               +
               RDATA1=R2
         L     R13,4(R13)                     RESTORE SAVE AREA ADDRESS
         L     R14,12(,R13)                   RESTORE R14
         LR    R15,R2                         SET RC
         LM    R0,R12,20(R13)                 RESTORE REGISTERS
         BR    R14                            RETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDSTART       DC    Q(WK0009)
F60            DC    F'60'
F100           DC    F'100'
F3600          DC    F'3600'

P1900000       DC    P'1900000'
EDIT_WORD_1    DC    X'F0202020'
EDIT_WORD_2    DC    X'F0202020204B202020'

               LTORG

MSG01          DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'  SMF date/time '
MSG01_DTE      DC    CL8' '
               DC    C'  '
MSG01_TME      DC    CL8' '
MSG01_L        EQU   *-MSG01

MSG02          DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'Device number   '
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG02_CUA      DC    CL4' '
MSG02_L        EQU   *-MSG02

MSG03          DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'Vary path caller'
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG03_CALLER   DC    CL15'Unknown'
MSG03_L        EQU   *-MSG03
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
WK0009         DSECT
               COPY     DXDPREF
DXD_A          DS       CL80
DXD_B          DS       CL80
DXD_DATE       DS       PL4
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               SESSION
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               BPXYSTAT  DSECT=YES,LIST=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               OSSPFD    TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
SMF_RECORD     DSECT
               IFASMFR   9
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY      ATTRS
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY      TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY      REGEQU
               END       SMF0009
