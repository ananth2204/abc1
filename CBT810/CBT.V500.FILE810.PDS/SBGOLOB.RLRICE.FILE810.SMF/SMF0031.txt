*---------------------------------------------------------------------*
*                                                                     *
*         Format SMF record type 31 (TIOC initialization)             *
*                                                                     *
*                                                                     *
*   At entry                                                          *
*       R15    program base address                                   *
*       R14    return address                                         *
*       R9     OSSPFD address                                         *
*       R8     VDATA buffer address                                   *
*       R7     SMF record address +4 (does not include length)        *
*                                                                     *
* ------------------------------------------------------------------- *
SMF0031  CSECT
SMF0031  AMODE 31
SMF0031  RMODE ANY
         USING SMF0031,R15
         B     INIT0000
MODID    DC    CL8'SMF0031'
         DC    CL8'&SYSDATE'
         DC    CL7'&SYSTIME'
INIT0000 DS    0H
         USING OSCOMM,R12                      DEFINE COMMON AREA BASE
         USING SESSION,R11
         STM   R14,R12,12(R13)                 SAVE REGS
         LR    R10,R15                         COPY ENTRY POINT
         DROP  R15
         USING SMF0031,R10                     DEFINE BASE
         L     R1,SESS_DXD_ADDR
         A     R1,DXDSTART
         ST    R13,4(R1)                       SAVE R13
         ST    R1,8(R13)                       CHAIN SAVE AREA
         LR    R13,R1                          COPY WORK AREA ADDRESS
         USING WK0031,R13                      DEFINE BASE
         USING OSSPFD,R9                       DEFINE BASE
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
MAIN0010 DS    0H
         USING TTIRFLG,R7
         A     R8,SPF_VWIDTH                  LEAVE A BLANK LINE
         BCTR  R6,0

*---------------------------------------------------------------------*
*        Process SMF date and time                                    *
*---------------------------------------------------------------------*
         MVC   0(MSG_01_L,R8),MSG_01
         USING MSG_01,R8                      DEFINE BASE
         MVC   DXD_DATE,TTIRCDTE
         AP    DXD_DATE,P1900000              ADD CENTURY
         MVC   DXD_A(L'EDIT_WORD_2),EDIT_WORD_2
         ED    DXD_A(L'EDIT_WORD_2),DXD_DATE
         MVC   MSG_01_DTE,DXD_A+1             COPY DATE
         ICM   R1,15,TTIRCDTS                 TIME
         BAL   R15,TIME0000                   CONVERT TIME
         MVC   MSG_01_TME,DXD_B               COPY TIME
         DROP  R8
         A     R8,SPF_VWIDTH                  NEXT LINE
         BCTR  R6,0
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
         MVC   0(MSG_02_L,R8),MSG_02
         USING MSG_02,R8                      DEFINE BASE
         ICM   R1,15,TTINBF                   NBR OF TIMESHARING BFRS
         CVD   R1,COMM_DWORD                  CONVERT TO DECIMAL
         ED    MSG_02_BUFFERS,COMM_DWORD+3
         DROP  R8
         A     R8,SPF_VWIDTH                  NEXT LINE
         BCTR  R6,0
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
         MVC   0(MSG_03_L,R8),MSG_03
         USING MSG_03,R8                      DEFINE BASE
         ICM   R1,15,TTIBUFSE                 SIZE OF TIMESHARING BFRS
         CVD   R1,COMM_DWORD                  CONVERT TO DECIMAL
         ED    MSG_03_SIZE,COMM_DWORD+3
         DROP  R8
         A     R8,SPF_VWIDTH                  NEXT LINE
         BCTR  R6,0
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
         MVC   0(MSG_04_L,R8),MSG_04
         USING MSG_04,R8                      DEFINE BASE
         ICM   R1,15,TTIOMAX                  BUFFERS BEFORE OWAIT
         CVD   R1,COMM_DWORD                  CONVERT TO DECIMAL
         ED    MSG_04_BUFFERS,COMM_DWORD+3
         DROP  R8
         A     R8,SPF_VWIDTH                  NEXT LINE
         BCTR  R6,0
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
         MVC   0(MSG_05_L,R8),MSG_05
         USING MSG_05,R8                      DEFINE BASE
         ICM   R1,15,TTIIMAX                  BUFFERS BEFORE LWAIT
         CVD   R1,COMM_DWORD                  CONVERT TO DECIMAL
         ED    MSG_05_BUFFERS,COMM_DWORD+3
         DROP  R8
         A     R8,SPF_VWIDTH                  NEXT LINE
         BCTR  R6,0
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
         MVC   0(MSG_06_L,R8),MSG_06
         USING MSG_06,R8                      DEFINE BASE
         ICM   R1,15,TTIOWTH                  BFRS BEFORE ENDING OWAIT
         CVD   R1,COMM_DWORD                  CONVERT TO DECIMAL
         ED    MSG_06_BUFFERS,COMM_DWORD+3
         DROP  R8
         A     R8,SPF_VWIDTH                  NEXT LINE
         BCTR  R6,0
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
         MVC   0(MSG_07_L,R8),MSG_07
         USING MSG_07,R8                      DEFINE BASE
         ICM   R1,15,TTIRSTH                  BFRS BEFORE ENDING LWAIT
         CVD   R1,COMM_DWORD                  CONVERT TO DECIMAL
         ED    MSG_07_BUFFERS,COMM_DWORD+3
         DROP  R8
         A     R8,SPF_VWIDTH                  NEXT LINE
         BCTR  R6,0
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
         MVC   0(MSG_08_L,R8),MSG_08
         USING MSG_08,R8                      DEFINE BASE
         ICM   R1,15,TTIUSLW                  BUFFERS ON FREE QUEUE
         CVD   R1,COMM_DWORD                  CONVERT TO DECIMAL
         ED    MSG_08_BUFFERS,COMM_DWORD+3
         DROP  R8
         A     R8,SPF_VWIDTH                  NEXT LINE
         BCTR  R6,0
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
         MVC   0(MSG_09_L,R8),MSG_09
         USING MSG_09,R8                      DEFINE BASE
         ICM   R1,15,TTITSBS                  SIZE OF BUFFERS
         CVD   R1,COMM_DWORD                  CONVERT TO DECIMAL
         ED    MSG_09_SIZE,COMM_DWORD+3
         DROP  R8
         A     R8,SPF_VWIDTH                  NEXT LINE
         BCTR  R6,0
         B     EXIT0000
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
TIME0000 DS    0H
         SR    R0,R0
         D     R0,F100                        CONVERT TO WHOLE SECONDS
         SR    R0,R0                          CLEAR REMAINDER
         D     R0,F3600                       COMPUTE HOURS
         CVD   R1,COMM_DWORD                  CONVERT HOURS
         MVC   DXD_A(L'EDIT_WORD_1),EDIT_WORD_1
         ED    DXD_A(L'EDIT_WORD_1),COMM_DWORD+6
         MVC   DXD_B+0(2),DXD_A+2             COPY HOURS
         MVI   DXD_B+2,C':'                   INSERT COLON
         LR    R1,R0                          COPY REMAINDER
         SR    R0,R0                          CLEAR R0
         D     R0,F60                         COMPUTE MINUTES
         CVD   R1,COMM_DWORD                  CONVERT MINUTES
         MVC   DXD_A(L'EDIT_WORD_1),EDIT_WORD_1
         ED    DXD_A(L'EDIT_WORD_1),COMM_DWORD+6
         MVC   DXD_B+3(2),DXD_A+2             COPY MINUTES
         MVI   DXD_B+5,C':'                   INSERT COLON
         CVD   R0,COMM_DWORD                  CONVERT SECONDS
         MVC   DXD_A(L'EDIT_WORD_1),EDIT_WORD_1
         ED    DXD_A(L'EDIT_WORD_1),COMM_DWORD+6
         MVC   DXD_B+6(2),DXD_A+2             COPY SECONDS
         BR    R15
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
EXIT0000 DS    0H
         SR    R2,R2
         ITRACE ID=EXIT,                                               +
               RDATA1=R2
         L     R13,4(R13)                     RESTORE SAVE AREA ADDRESS
         L     R14,12(,R13)                   RESTORE R14
         LR    R15,R2                         SET RC
         LM    R0,R12,20(R13)                 RESTORE REGISTERS
         BR    R14                            RETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDSTART       DC    Q(WK0031)
F60            DC    F'60'
F100           DC    F'100'
F3600          DC    F'3600'

P1900000       DC    P'1900000'

EDIT_WORD_1    DC    X'F0202020'
EDIT_WORD_2    DC    X'F0202020204B202020'


MSG_01         DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'  SMF date/time '
MSG_01_DTE     DC    CL8' '
               DC    C'  '
MSG_01_TME     DC    CL8' '
MSG_01_L       EQU   *-MSG_01

MSG_02         DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'Number of timesharing buffers'
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_02_BUFFERS DC    X'402020206B2020206B202120'
MSG_02_L       EQU   *-MSG_02

MSG_03         DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'Timesharing buffer size'
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_03_SIZE    DC    X'402020206B2020206B202120'
MSG_03_L       EQU   *-MSG_03

MSG_04         DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'Buffers before OWAIT   '
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_04_BUFFERS DC    X'402020206B2020206B202120'
MSG_04_L       EQU   *-MSG_04

MSG_05         DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'Buffers before LWAIT   '
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_05_BUFFERS DC    X'402020206B2020206B202120'
MSG_05_L       EQU   *-MSG_05

MSG_06         DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'Buffers to end OWAIT   '
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_06_BUFFERS DC    X'402020206B2020206B202120'
MSG_06_L       EQU   *-MSG_06

MSG_07         DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'Buffers to end LWAIT   '
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_07_BUFFERS DC    X'402020206B2020206B202120'
MSG_07_L       EQU   *-MSG_07

MSG_08         DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'Buffers on free queue  '
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_08_BUFFERS DC    X'402020206B2020206B202120'
MSG_08_L       EQU   *-MSG_08

MSG_09         DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'Size of one TSB        '
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_09_SIZE    DC    X'402020206B2020206B202120'
MSG_09_L       EQU   *-MSG_09
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
WK0031         DSECT
               COPY     DXDPREF
DXD_A          DS       CL80
DXD_B          DS       CL80
DXD_DATE       DS       PL4
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              SESSION
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              BPXYSTAT DSECT=YES,LIST=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              OSSPFD   TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
SMF_RECORD    DSECT
              IFASMFR  31
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              COPY     ATTRS
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              COPY     REGEQU
              END      SMF0031
