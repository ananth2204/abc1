*---------------------------------------------------------------------*
*                                                                     *
*         Format SMF record type 60                                   *
*                                                                     *
*                                                                     *
*   At entry                                                          *
*       R15    program base address                                   *
*       R14    return address                                         *
*       R9     OSSPFD address                                         *
*       R8     VDATA buffer address                                   *
*       R7     SMF record address +4 (does not include length)        *
*                                                                     *
* ------------------------------------------------------------------- *
SMF0060  CSECT
SMF0060  AMODE 31
SMF0060  RMODE ANY
         USING SMF0060,R15
         B     INIT0000
MODID    DC    CL8'SMF0060'
         DC    CL8'&SYSDATE'
         DC    CL7'&SYSTIME'
INIT0000 DS    0H
         USING OSCOMM,R12                      DEFINE COMMON AREA BASE
         USING SESSION,R11
         STM   R14,R12,12(R13)                 SAVE REGS
         LR    R10,R15                         COPY ENTRY POINT
         USING SMF0060,R10                     DEFINE BASE
         DROP  R15
         L     R1,SESS_DXD_ADDR
         A     R1,DXDSTART
         ST    R13,4(R1)                       SAVE R13
         ST    R1,8(R13)                       CHAIN SAVE AREA
         LR    R13,R1                          COPY WORK AREA ADDRESS
         USING WK0060,R13                      DEFINE BASE
         MVC   DXD_CSECT,MODID
         USING OSSPFD,R9                       DEFINE BASE
         ITRACE ID=ENTRY
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
         USING SMF60SYS,R7
         MVC   0(MSG_01_L,R8),MSG_01           COPY MESSAGE
         USING MSG_01,R8                       DEFINE BASE
         MVC   MSG_01_PNM,SMF60PNM             COPY PRODUCT NAME
         MVC   MSG_01_VER,SMF60VER             COPY VERSION
         DROP  R8
         A     R8,SPF_VWIDTH                   NEXT LINE
         MVC   0(MSG_02_L,R8),MSG_02           COPY MESSAGE
         USING MSG_02,R8                       DEFINE BASE
         MVC   MSG_02_JNM,SMF60JNM             COPY JOB NAME
         DROP  R8
         A     R8,SPF_VWIDTH                   NEXT LINE
         MVC   0(MSG_03_L,R8),MSG_03           COPY MESSAGE
         USING MSG_03,R8                       DEFINE BASE
         MVC   MSG_03_UID,SMF60UID             COPY USER ID
         DROP  R8
         A     R8,SPF_VWIDTH                   NEXT LINE
         MVC   0(MSG_04_L,R8),MSG_04           COPY MESSAGE
         USING MSG_04,R8                       DEFINE BASE
         MVC   MSG_04_CNM,SMF60CNM             VVDS NAME
         DROP  R8
         A     R8,SPF_VWIDTH                   NEXT LINE
         MVC   0(MSG_05_L,R8),MSG_05           COPY MESSAGE
         USING MSG_05,R8                       DEFINE BASE
         MVC   MSG_05_ENM,SMF60ENM             ENTRY NAME
         DROP  R8
         A     R8,SPF_VWIDTH                   NEXT LINE
         MVC   0(MSG_06_L,R8),MSG_06           COPY MESSAGE
         USING MSG_06,R8                       DEFINE BASE
         LA    R15,TYPES                       FIRST TYPE
         USING TYPE_DSECT,R15
MAIN0010 DS    0H
         CLC   TYPE_TYPE,SMF60TYP              LOCATED THE TYPE?
         BE    MAIN0020                        YES
         LA    R15,TYPE_L(,R15)                NEXT TYPE
         CLI   0(R15),X'FF'                    END OF TABLE?
         BNE   MAIN0010                        NO
         MVC   MSG_06_TYP(L'UNKNOWN),UNKNOWN
         B     EXIT0000
MAIN0020 DS    0H
         MVC   MSG_06_TYP,TYPE_DESC
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
EXIT0000 DS    0H
         SR    R2,R2
         B     EXITEXIT
EXIT0004 DS    0H
         LA    R2,4
EXITEXIT DS    0H
         ITRACE ID=EXIT,                                               +
               RDATA1=R2
         L     R13,4(R13)                     RESTORE SAVE AREA ADDRESS
         L     R14,12(,R13)                   RESTORE R14
         SR    R15,R15                        SET RC
         LM    R0,R12,20(R13)                 RESTORE REGISTERS
         BR    R14                            RETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDSTART       DC    Q(WK0060)
UNKNOWN        DC    C'Unknown'

TYPES          DS    0C
               DC    C'A',CL40'Non-VSAM data set'
               DC    C'B',CL40'Generation data group (GDG) base'
               DC    C'C',CL40'Cluster'
               DC    C'D',CL40'Data set'
               DC    C'G',CL40'Alternate index'
               DC    C'I',CL40'Index'
               DC    C'M',CL40'Master catalog'
               DC    C'P',CL40'Page space'
               DC    C'R',CL40'Path'
               DC    C'U',CL40'User catalog'
               DC    C'V',CL40'Volume'
               DC    C'X',CL40'Alias'
               DC    C'Y',CL40'Upgrade'
               DC    X'FF'

MSG_01         DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'Product name'
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_01_PNM     DC    CL8' '
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C' Version'
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_01_VER     DC    CL2' '
MSG_01_L       EQU   *-MSG_01

MSG_02         DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'Job name    '
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_02_JNM     DC    CL8' '
MSG_02_L       EQU   *-MSG_02

MSG_03         DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'User        '
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_03_UID     DC    CL8' '
MSG_03_L       EQU   *-MSG_03

MSG_04         DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'VVDS name   '
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_04_CNM     DC    CL44' '
MSG_04_L       EQU   *-MSG_04

MSG_05         DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'Entry name  '
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_05_ENM     DC    CL44' '
MSG_05_L       EQU   *-MSG_05

MSG_06         DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'Entry type  '
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_06_TYP     DC    CL40' '
MSG_06_L       EQU   *-MSG_06

* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
WK0060         DSECT
               COPY     DXDPREF
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               SESSION
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               BPXYSTAT DSECT=YES,LIST=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
TYPE_DSECT     DSECT
TYPE_TYPE      DS       C
TYPE_DESC      DS       CL40
TYPE_L         EQU      *-TYPE_DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               OSSPFD   TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
SMF_RECORD     DSECT
               IFASMFI6 60
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY     ATTRS
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY     REGEQU
               END      SMF0060
