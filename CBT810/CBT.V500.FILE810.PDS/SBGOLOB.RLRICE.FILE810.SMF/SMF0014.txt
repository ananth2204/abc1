*---------------------------------------------------------------------*
*                                                                     *
*         Format SMF record type 14 (Data set activity)               *
*                                                                     *
*                                                                     *
*   At entry                                                          *
*       R15    program base address                                   *
*       R14    return address                                         *
*       R9     OSSPFD address                                         *
*       R8     VDATA buffer address                                   *
*       R7     SMF record address +4 (does not include length)        *
*                                                                     *
* ------------------------------------------------------------------- *
SMF0014  CSECT
SMF0014  AMODE 31
SMF0014  RMODE ANY
         USING OSCOMM,R12
         USING SESSION,R11
         USING SMF0014,R15
         B     INIT0000
MODID    DC    CL8'SMF0014'
         DC    CL8'&SYSDATE'
         DC    CL7'&SYSTIME'
INIT0000 DS    0H
         STM   R14,R12,12(R13)                SAVE REGS
         LR    R10,R15                        COPY ENTRY POINT
         USING SMF0014,R10                    DEFINE BASE
         DROP  R15
         L     R1,SESS_DXD_ADDR
         A     R1,DXDSTART
         ST    R13,4(R1)                      SAVE R13
         ST    R1,8(R13)                      CHAIN SAVE AREA
         LR    R13,R1                         COPY WORK AREA ADDRESS
         USING WK0014,R13                     DEFINE BASE
         MVC   DXD_CSECT,MODID
         USING OSSPFD,R9                      DEFINE BASE
         ITRACE ID=ENTRY
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
MAIN0010 DS    0H
         USING SMF14FLG,R7
         A     R8,SPF_VWIDTH                  LEAVE A BLANK LINE
         BCTR  R6,0

         MVC   0(MSG_01_L,R8),MSG_01
         USING MSG_01,R8                      DEFINE BASE
         MVC   DXD_DATE,SMF14RSD              COPY DATE
         AP    DXD_DATE,P1900000              ADD CENTURY
         MVC   DXD_A(L'EDIT_WORD_2),EDIT_WORD_2
         ED    DXD_A(L'EDIT_WORD_2),DXD_DATE
         MVC   MSG_01_RSD,DXD_A+1             COPY READER START DATE
         ICM   R1,15,SMF14RST                 READER START TIME
         BAL   R15,TIME0000                   CONVERT TIME
         MVC   MSG_01_RST,DXD_B               COPY READER START TIME
         DROP  R8
         A     R8,SPF_VWIDTH                  NEXT LINE
         BCTR  R6,0
         A     R8,SPF_VWIDTH                  NEXT LINE
         BCTR  R6,0

         MVC   0(MSG_02_L,R8),MSG_02
         USING MSG_02,R8
         MVC   MSG_02_JBN,SMF14JBN            COPY JOB NAME
         DROP  R8
         A     R8,SPF_VWIDTH                  NEXT LINE
         BCTR  R6,0

         SR    R0,R0
         SR    R14,R14
         SR    R15,R15
         IC    R0,SMF14NUC                    NUMBER OF UCB SECTIONS
         IC    R15,SMF14SUC                   SIZE OF EACH UCB
         MR    R14,R0                         COMPUTE SIZE OF UCB DATA
         LA    R1,SMF14UCB(R15)               EXTENDED DATA
         USING SMF14XIS,R1                    DEFINE BASE
         SR    R0,R0
         ICM   R0,3,SMF14SXS                  LENGTH OF ALL DATA
         SH    R0,H2                          MINUS LENGTH OF LENGTH
         LA    R1,SMF14XIC                    1ST SECTION
         USING SMF14XIC,R1
MAIN0020 DS    0H
         CLI   SMF14STY,SMF14STP              STEP INFO?
         BE    MAIN0030                       YES
         SH    R0,SMF14ESL                    MINUS LENGTH OF THIS DATA
         BNH   MAIN0040                       NO MORE DATA
         AH    R1,SMF14ESL                    NEXT SECTION
         B     MAIN0020
MAIN0030 DS    0H
         MVC   0(MSG_03_L,R8),MSG_03
         USING MSG_03,R8
         MVC   MSG_03_STEP,SMF14SPN           COPY STEP NAME
         DROP  R8

         A     R8,SPF_VWIDTH                  NEXT LINE
         BCTR  R6,0

         MVC   0(MSG_04_L,R8),MSG_04
         USING MSG_04,R8
         MVC   MSG_04_PROGRAM,SMF14PGN        COPY PROGRAM NAME
         DROP  R8

         A     R8,SPF_VWIDTH                  NEXT LINE
         BCTR  R6,0
         DROP  R1
MAIN0040 DS    0H
         MVC   0(MSG_05_L,R8),MSG_05
         USING MSG_05,R8
         MVC   MSG_05_UID,SMF14UID            COPY USER
         DROP  R8

         A     R8,SPF_VWIDTH                  NEXT LINE
         BCTR  R6,0
         A     R8,SPF_VWIDTH                  NEXT LINE
         BCTR  R6,0

         USING INFMJFCB,SMFJFCB1              DEFINE BASE
         MVC   0(MSG_06_L,R8),MSG_06
         USING MSG_06,R8                      DEFINE BASE
         MVC   MSG_06_DD,SMFTIOE5             COPY DD NAME
         DROP  R8

         A     R8,SPF_VWIDTH
         BCTR  R6,0
         MVC   0(MSG_07_L,R8),MSG_07
         USING MSG_07,R8                      DEFINE BASE
         MVC   MSG_07_DSN,JFCBDSNM            COPY DATA SET NAME
         DROP  R8

         A     R8,SPF_VWIDTH
         BCTR  R6,0
         TM    JFCBIND1,JFCPDS                PDS MEMBER?
         BNO   MAIN0050                       NO
         MVC   0(MSG_08_L,R8),MSG_08
         USING MSG_08,R8                      DEFINE BASE
         MVC   MSG_08_MEMBER,JFCBELNM         COPY MEMBER NAME
         DROP  R8
         A     R8,SPF_VWIDTH
         BCTR  R6,0

MAIN0050 DS    0H
         A     R8,SPF_VWIDTH                  NEXT LINE
         BCTR  R6,0
*---------------------------------------------------------------------*
*        Process indicators                                           *
*---------------------------------------------------------------------*
         MVC   0(MSG_09_L,R8),MSG_09
         USING MSG_09,R8                      DEFINE BASE
         LA    R1,MSG_09_RIN
         TM    SMF14RIN,SMF14TDS              TEMP DATA SET?
         BNO   MAIN0060                       NO
         MVC   0(L'TEMP,R1),TEMP              COPY 'TEMP'
         LA    R1,L'TEMP+1(R1)                UPDATE ADDRESS
MAIN0060 DS    0H
         TM    SMF14RIN,SMF14EOV              WRITTEN BY EOV?
         BNO   MAIN0070                       NO
         MVC   0(L'EOV,R1),EOV                COPY 'EOV'
         LA    R1,L'EOV+1(R1)                 UPDATE ADDRESS
MAIN0070 DS    0H
         TM    SMF14RIN,SMF14VIO              VIO?
         BNO   MAIN0080                       NO
         MVC   0(L'VIO,R1),VIO                COPY 'VIO'
         LA    R1,L'VIO+1(R1)                 UPDATE ADDRESS
MAIN0080 DS    0H
         TM    SMF14RIN+1,SMF14IPD            PDSE MANAGED?
         BNO   MAIN0090                       NO
         MVC   0(L'PDSE,R1),PDSE              COPY 'PDSE'
         LA    R1,L'PDSE+1(R1)                UPDATE ADDRES
MAIN0090 DS    0H
         TM    SMF14RIN+1,SMF14TRC            TRUNC ISSUED?
         BNO   MAIN0100                       NO
         MVC   0(L'TRUNC,R1),TRUNC            COPY 'TRUNC'
MAIN0100 DS    0H
         DROP  R8
         A     R8,SPF_VWIDTH
         BCTR  R6,0
         A     R8,SPF_VWIDTH
         BCTR  R6,0
*---------------------------------------------------------------------*
*        Process devices                                              *
*---------------------------------------------------------------------*
         SR    R4,R4
         IC    R4,SMF14NUC                    NUMBER OF DEVICES
         SR    R5,R5
         IC    R5,SMFTIOE1                    LENGTH OF DD ENTRY
         LA    R5,SMFTDDEX(R5)
         SH    R5,H4                          MINUS 4
         USING SMFUCBDV,R5
         SR    R2,R2
         IC    R2,SMF14SUC                    LENGTH OF EACH UCB ENTRY
MAIN0110 DS    0H
         MVC   0(MSG_10_L,R8),MSG_10
         USING MSG_10,R8                      DEFINE BASE
         UNPK  DXD_A(5),SMFUCBDV(3)           UNPACK DEVICE
         TR    DXD_A(4),COMM_HEXCHAR          TRANSLATE
         MVC   MSG_10_UCBDV,DXD_A             COPY DEVICE
         MVC   MSG_10_SRTEV,SMFSRTEV          COPY VOLSER
         L     R1,SMFEXCP                     NUMBER OF EXCP'S
         CVD   R1,COMM_DWORD                  CONVERT TO DECIMAL
         ED    MSG_10_EXCP,COMM_DWORD+3       EDIT EXCP'S
         A     R8,SPF_VWIDTH                  NEXT LINE
         AR    R5,R2                          NEXT DEVICE
         BCT   R6,MAIN0120
         B     EXIT0000                       SCREEN FULL
MAIN0120 DS    0H
         BCT   R4,MAIN0110                    LOOP
         DROP  R8
         B     EXIT0000
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
TIME0000 DS    0H
         ITRACE ID=TIME,                                               +
               RDATA1=R1
         SR    R0,R0
         D     R0,F100                        WHOLE SECONDS
         SR    R0,R0                          CLEAR REMAINDER
         D     R0,F3600                       COMPUTE HOURS
         CVD   R1,COMM_DWORD                  CONVERT HOURS
         MVC   DXD_A(L'EDIT_WORD_1),EDIT_WORD_1
         ED    DXD_A(L'EDIT_WORD_1),COMM_DWORD+6
         MVC   DXD_B+0(2),DXD_A+2             COPY HOURS
         MVI   DXD_B+2,C':'                   INSERT COLON
         LR    R1,R0                          COPY REMAINDER
         SR    R0,R0                          CLEAR R0
         D     R0,F60                         COMPUTE MINUTES
         CVD   R1,COMM_DWORD                  CONVERT MINUTES
         MVC   DXD_A(L'EDIT_WORD_1),EDIT_WORD_1
         ED    DXD_A(L'EDIT_WORD_1),COMM_DWORD+6
         MVC   DXD_B+3(2),DXD_A+2             COPY MINUTES
         MVI   DXD_B+5,C':'                   INSERT COLON
         CVD   R0,COMM_DWORD                  CONVERT SECONDS
         MVC   DXD_A(L'EDIT_WORD_1),EDIT_WORD_1
         ED    DXD_A(L'EDIT_WORD_1),COMM_DWORD+6
         MVC   DXD_B+6(2),DXD_A+2             COPY SECONDS
         BR    R15
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
EXIT0000 DS    0H
         SR    R2,R2
         B     EXITEXIT
EXIT0004 DS    0H
         LA    R2,4
EXITEXIT DS    0H
         ITRACE ID=EXIT,                                               +
               RDATA1=R2
         L     R13,4(R13)                     RESTORE SAVE AREA ADDRESS
         L     R14,12(,R13)                   RESTORE R14
         LR    R15,R2                         SET RC
         LM    R0,R12,20(R13)                 RESTORE REGISTERS
         BR    R14                            RETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDSTART       DC    Q(WK0014)
F60            DC    F'60'
F100           DC    F'100'
F3600          DC    F'3600'
H2             DC    H'2'
H4             DC    H'4'

P1900000       DC    P'1900000'

EDIT_WORD_1    DC    X'F0202120'
EDIT_WORD_2    DC    X'F0202020204B202020'

EOV            DC    C'EOV '
TEMP           DC    C'TEMP'
VIO            DC    C'VIO '
PDSE           DC    C'PDSE'
TRUNC          DC    C'TRUNC'
MEMBER         DC    C'MEMBER:'

MSG_01         DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'Reader start date/time'
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_01_RSD     DC    CL8' '
               DC    C' '
MSG_01_RST     DC    CL8' '
MSG_01_L       EQU   *-MSG_01

MSG_02         DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'Job name:    '
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_02_JBN     DC    CL8' '
MSG_02_L       EQU   *-MSG_02

MSG_03         DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'Step name:   '
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_03_STEP    DC    CL8' '
MSG_03_L       EQU   *-MSG_03

MSG_04         DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'Program name:'
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_04_PROGRAM DC    CL8' '
MSG_04_L       EQU   *-MSG_04

MSG_05         DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'User:        '
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_05_UID     DC    CL8' '
MSG_05_L       EQU   *-MSG_05

MSG_06         DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'DD:        '
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_06_DD      DC    CL8' '
MSG_06_L       EQU   *-MSG_06

MSG_07         DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'DSN:       '
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_07_DSN     DC    CL44' '
MSG_07_L       EQU   *-MSG_07

MSG_08         ds    0c
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'Member:    '
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_08_MEMBER  DC    CL8' '
MSG_08_L       EQU   *-MSG_08

MSG_09         DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'Indicators:'
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_09_RIN     DC    CL60'None'
MSG_09_L       EQU   *-MSG_09

MSG_10         DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'Device nbr:'
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_10_UCBDV   DC    CL4' '
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'VOLSER'
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_10_SRTEV   DC    CL6' '
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'I/O''s'
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_10_EXCP    DC    x'402020206b2020206b202120'
MSG_10_L       EQU   *-MSG_10

* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
WK0014         DSECT
               COPY     DXDPREF
DXD_A          DS       CL80
DXD_B          DS       CL80
DXD_DATE       DS       PL4
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              SESSION
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              BPXYSTAT  DSECT=YES,LIST=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              OSSPFD    TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
SMF_RECORD    DSECT
              IFASMFR   14
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DUMMY         DSECT
              IBMMAC    JFCB=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              COPY      ATTRS
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              COPY      TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              COPY      REGEQU
              END       SMF0014
