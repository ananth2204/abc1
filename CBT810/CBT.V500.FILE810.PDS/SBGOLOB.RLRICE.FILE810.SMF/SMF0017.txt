*---------------------------------------------------------------------*
*                                                                     *
*         Format SMF record type 14 (Data set activity)               *
*                                                                     *
*                                                                     *
*   At entry                                                          *
*       R15    program base address                                   *
*       R14    return address                                         *
*       R9     OSSPFD address                                         *
*       R8     VDATA buffer address                                   *
*       R7     SMF record address +4 (does not include length)        *
*                                                                     *
* ------------------------------------------------------------------- *
SMF0017  CSECT
SMF0017  AMODE 31
SMF0017  RMODE ANY
         USING SMF0017,R15
         B     INIT0000
MODID    DC    CL8'SMF0017'
         DC    CL8'&SYSDATE'
         DC    CL7'&SYSTIME'
INIT0000 DS    0H
         USING OSCOMM,R12                     DEFINE COMMON AREA BASE
         USING SESSION,R11                    DEFINE COMMON AREA BASE
         STM   R14,R12,12(R13)                SAVE REGS
         LR    R10,R15                        COPY ENTRY POINT
         USING SMF0017,R10                    DEFINE BASE
         DROP  R15
         L     R1,SESS_DXD_ADDR
         A     R1,DXDSTART
         ST    R13,4(R1)                      SAVE R13
         ST    R1,8(R13)                      CHAIN SAVE AREA
         LR    R13,R1                         COPY WORK AREA ADDRESS
         USING WK0017,R13                     DEFINE BASE
         USING OSSPFD,R9                      DEFINE BASE
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
MAIN0010 DS    0H
         USING SMF17FLG,R7
         A     R8,SPF_VWIDTH                  LEAVE A BLANK LINE
         BCTR  R6,0

         MVC   0(MSG_01_L,R8),MSG_01
         USING MSG_01,R8
         MVC   MSG_01_JBN,SMF17JBN            COPY JOB NAME
         DROP  R8
         A     R8,SPF_VWIDTH                  LEAVE A BLANK LINE
         BCTR  R6,0

         MVC   0(MSG_02_L,R8),MSG_02
         USING MSG_02,R8
         MVC   MSG_02_UID,SMF17UID            COPY USER
         DROP  R8
         A     R8,SPF_VWIDTH                  NEXT LINE
         BCTR  R6,0
* ------------------------------------------------------------------- *
*        Process reader start date/time                               *
* ------------------------------------------------------------------- *
         MVC   0(MSG_03_L,R8),MSG_03
         USING MSG_03,R8                      DEFINE BASE
         MVC   DXD_DATE,SMF17RSD
         AP    DXD_DATE,P1900000              ADD CENTURY
         MVC   DXD_A(L'EDIT_WORD_2),EDIT_WORD_2
         ED    DXD_A(L'EDIT_WORD_2),DXD_DATE
         MVC   MSG_03_RSD,DXD_A+1             COPY READER START DATE
         ICM   R1,15,SMF17RST                 READER START TIME
         BAL   R15,TIME0000                   CONVERT TIME
         MVC   MSG_03_RST,DXD_B               COPY READER START TIME
         DROP  R8
         A     R8,SPF_VWIDTH                  NEXT LINE
         BCTR  R6,0
         A     R8,SPF_VWIDTH                  NEXT LINE
         BCTR  R6,0
*---------------------------------------------------------------------*
*        PROCESS data set name                                        *
*---------------------------------------------------------------------*
         MVC   0(MSG_04_L,R8),MSG_04
         USING MSG_04,R8                      DEFINE BASE
         MVC   MSG_04_DSN,SMF17DSN
         DROP  R8
         A     R8,SPF_VWIDTH
         BCTR  R6,0
*---------------------------------------------------------------------*
*        PROCESS VOLSER(S)                                            *
*---------------------------------------------------------------------*
         SR    R2,R2
         ICM   R2,1,SMF17NVL                  NUMBER OF VOLUMES
         BZ    EXIT0000
         MVC   0(MSG_05_L,R8),MSG_05
         USING MSG_05,R8                      DEFINE BASE
         LA    R3,SMF17RV2                    1ST VOLSER
MAIN0020 DS    0H
         MVC   MSG_05_VOLSER,2(R3)            COPY VOLSER
         A     R8,SPF_VWIDTH                  NEXT LINE
         BCT   R6,MAIN0030
         B     EXIT0000
MAIN0030 DS    0H
         MVC   0(80,R8),COMM_BLANKS           FORCE NEXT LINE TO BLANKS
         LA    R3,8(R3)                       NEXT VOLUME
         BCT   R2,MAIN0020                    LOOP
         DROP  R8
         B     EXIT0000
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
TIME0000 DS    0H
         SR    R0,R0
         D     R0,F100                        CONVERT TO WHOLE SECONDS
         SR    R0,R0                          CLEAR REMAINDER
         D     R0,F3600                       COMPUTE HOURS
         CVD   R1,COMM_DWORD                  CONVERT HOURS TO DECIMAL
         MVC   DXD_A(L'EDIT_WORD_1),EDIT_WORD_1
         ED    DXD_A(L'EDIT_WORD_1),COMM_DWORD+6
         MVC   DXD_B+0(2),DXD_A+2             COPY HOURS
         MVI   DXD_B+2,C':'                   INSERT COLON
         LR    R1,R0                          COPY REMAINDER
         SR    R0,R0                          CLEAR R0
         D     R0,F60                         COMPUTE MINUTES
         CVD   R1,COMM_DWORD                  CONVERT MINUTES
         MVC   DXD_A(L'EDIT_WORD_1),EDIT_WORD_1
         ED    DXD_A(L'EDIT_WORD_1),COMM_DWORD+6
         MVC   DXD_B+3(2),DXD_A+2             COPY MINUTES
         MVI   DXD_B+5,C':'                   INSERT COLON
         CVD   R0,COMM_DWORD                  CONVERT SECONDS
         MVC   DXD_A(L'EDIT_WORD_1),EDIT_WORD_1
         ED    DXD_A(L'EDIT_WORD_1),COMM_DWORD+6
         MVC   DXD_B+6(2),DXD_A+2             COPY SECONDS
         BR    R15
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
EXIT0000 DS    0H
         SR    R2,R2
         L     R13,4(R13)                     RESTORE SAVE AREA ADDRESS
         L     R14,12(,R13)                   RESTORE R14
         LR    R15,R2                         SET RC
         LM    R0,R12,20(R13)                 RESTORE REGISTERS
         BR    R14                            RETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDSTART       DC    Q(WK0017)
F60            DC    F'60'
F100           DC    F'100'
F3600          DC    F'3600'
H4             DC    H'4'

P1900000       DC    P'1900000'

EDIT_WORD_1    DC    X'F0202020'
EDIT_WORD_2    DC    X'F0202020204B202020'

MSG_01         DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'Job name:                '
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_01_JBN     DC    CL8' '
MSG_01_L       EQU   *-MSG_01

MSG_02         DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'User:                    '
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_02_UID     DC    CL8' '
MSG_02_L       EQU   *-MSG_02

MSG_03         DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'Reader start date/time   '
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_03_RSD     DC    CL8' '
               DC    C' '
MSG_03_RST     DC    CL8' '
MSG_03_L       EQU   *-MSG_03

MSG_04         DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'Data set name:           '
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_04_DSN     DC    CL44' '
MSG_04_L       EQU   *-MSG_04

MSG_05         DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'Volser(s):               '
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_05_VOLSER  DC    CL6' '
MSG_05_L       EQU   *-MSG_05

* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
WK0017         DSECT
               COPY     DXDPREF
DXD_A          DS       CL80
DXD_B          DS       CL80
DXD_DATE       DS       PL4
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               SESSION
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               BPXYSTAT DSECT=YES,LIST=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               OSSPFD   TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
SMF_RECORD     DSECT
               IFASMFR  17
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY     ATTRS
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY     REGEQU
               END      SMF0017
