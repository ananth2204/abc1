*---------------------------------------------------------------------*
*                                                                     *
*         Format SMF record type 110                                  *
*         CICS data                                                   *
*                                                                     *
*   At entry                                                          *
*       R15    program base address                                   *
*       R14    return address                                         *
*       R9     OSSPFD ADDRESS                                         *
*       R8     VDATA buffer address                                   *
*       R7     SMF RECORD ADDRESS +4 (DOES NOT INCLUDE LENGTH)        *
*                                                                     *
* ------------------------------------------------------------------- *
SMF0110  CSECT
SMF0110  AMODE 31
SMF0110  RMODE ANY
         USING SESSION,R11
         USING SMF0110,R15
         B     INIT0000
MODID    DC    CL8'SMF0110'
         DC    CL8'&SYSDATE'
         DC    CL7'&SYSTIME'
INIT0000 DS    0H
         USING OSCOMM,R12                     DEFINE COMMON AREA BASE
         STM   R14,R12,12(R13)                SAVE REGS
         LR    R10,R15                        COPY ENTRY POINT
         USING SMF0110,R10                    DEFINE BASE
         DROP  R15
         L     R1,SESS_DXD_ADDR
         A     R1,DXDSTART
         ST    R13,4(R1)                      SAVE R13
         ST    R1,8(R13)                      CHAIN SAVE AREA
         LR    R13,R1                         COPY WORK AREA ADDRESS
         USING WK0110,R13                     DEFINE BASE
         MVC   DXD_CSECT,MODID
         USING OSSPFD,R9                      DEFINE BASE
         USING SMFFLG,R7                      DEFINE BASE
         ITRACE ID=ENTRY
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
         CLI   SMFSTY+1,SMFJCSTY              JOURNALLING?
         BE    MAIN0010                       YES
         CLI   SMFSTY+1,SMFMNSTY              MONITORING?
         BE    MAIN0020                       YES
         CLI   SMFSTY+1,SMFSTSTY              STATISTICS?
         BE    MAIN0030                       YES
         CLI   SMFSTY+1,SMFXQSTY              TS DATA SHARING?
         BE    MAIN0040                       YES
         CLI   SMFSTY+1,SMFCFSTY              CFDT SERVER STATS?
         BE    MAIN0050                       YES
         CLI   SMFSTY+1,SMFNCSTY              NAMED SERVER STATS?
         BE    MAIN0060                       YES
         B     EXIT0000
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
MAIN0010 DS    0H
         ITRACE ID=JOURNAL
         MVC   0(MSG_01_L,R8),MSG_01
         A     R8,SPF_VWIDTH

         MVC   0(MSG_02_L,R8),MSG_02
         USING MSG_02,R8
         SR    R0,R0
         IC    R0,SMFPSJID                    JOURNAL ID
         CVD   R0,COMM_DWORD                  CONVERT TO DECIMAL
         ED    MSG_02_JOURNAL,COMM_DWORD+6    EDIT JOURNAL ID
         DROP  R8
         A     R8,SPF_VWIDTH

         MVC   0(MSG_03_L,R8),MSG_03
         USING MSG_03,R8
         ED    MSG_03_RECORD,SMFPSRSN         EDIT RECORD NBR
         DROP  R8
         A     R8,SPF_VWIDTH

         MVC   0(MSG_04_L,R8),MSG_04
         USING MSG_04,R8
         ED    MSG_04_RECORD,SMFPSBKN         EDIT RECORD NBR
         DROP  R8
         A     R8,SPF_VWIDTH

         MVC   0(MSG_05_L,R8),MSG_05
         USING MSG_05,R8
         ED    MSG_05_NAME,SMFPSJNM           JOURNAL NAME
         DROP  R8

         B     EXIT0000

*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
MAIN0020 DS    0H
         ITRACE ID=MONITOR
         MVC   0(MSG_06_L,R8),MSG_06
         A     R8,SPF_VWIDTH
         B     EXIT0000

*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
MAIN0030 DS    0H
         ITRACE ID=STATS
         MVC   0(MSG_07_L,R8),MSG_07
         A     R8,SPF_VWIDTH
         B     EXIT0000

*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
MAIN0040 DS    0H
         ITRACE ID=TS
         MVC   0(MSG_08_L,R8),MSG_08
         A     R8,SPF_VWIDTH
         B     EXIT0000

*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
MAIN0050 DS    0H
         ITRACE ID=CFDT
         MVC   0(MSG_09_L,R8),MSG_09
         A     R8,SPF_VWIDTH
         B     EXIT0000

*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
MAIN0060 DS    0H
         ITRACE ID=NAMED
         MVC   0(MSG_10_L,R8),MSG_10
         A     R8,SPF_VWIDTH
         B     EXIT0000

*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
EXIT0000 DS    0H
         ITRACE ID=EXIT
         L     R13,4(R13)                     RESTORE SAVE AREA ADDRESS
         L     R14,12(,R13)                   RESTORE R14
         LM    R14,R12,12(R13)                RESTORE REGISTERS
         SR    R15,R15
         BR    R14                            RETURN
* ------------------------------------------------------------------- *
*        CONSTANTS                                                    *
* ------------------------------------------------------------------- *
DXDSTART       DC    Q(WK0110)

MSG_01         DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'Journalling'
MSG_01_L       EQU   *-MSG_01

MSG_02         DS    0C
               DC    AL1($SCREEN_ATTR_HIGH_TURQUOISE)
               DC    C'Journal ID           '
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_02_JOURNAL DC    X'40202120'
MSG_02_L       EQU   *-MSG_02

MSG_03         DS    0C
               DC    AL1($SCREEN_ATTR_HIGH_TURQUOISE)
               DC    C'Record within journal '
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_03_RECORD  DC    X'4020202020202120'
MSG_03_L       EQU   *-MSG_03

MSG_04         DS    0C
               DC    AL1($SCREEN_ATTR_HIGH_TURQUOISE)
               DC    C'Record within data set  '
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_04_RECORD  DC    X'402020202120'
MSG_04_L       EQU   *-MSG_04

MSG_05         DS    0C
               DC    AL1($SCREEN_ATTR_HIGH_TURQUOISE)
               DC    C'Journal name          '
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_05_NAME    DC    X'402020202120'
MSG_05_L       EQU   *-MSG_05

MSG_06         DS    0C
               DC    AL1($SCREEN_ATTR_HIGH_TURQUOISE)
               DC    C'Monitoring            '
MSG_06_L       EQU   *-MSG_06

MSG_07         DS    0C
               DC    AL1($SCREEN_ATTR_HIGH_TURQUOISE)
               DC    C'Statistics            '
MSG_07_L       EQU   *-MSG_07

MSG_08         DS    0C
               DC    AL1($SCREEN_ATTR_HIGH_TURQUOISE)
               DC    C'TS datasharing        '
MSG_08_L       EQU   *-MSG_08

MSG_09         DS    0C
               DC    AL1($SCREEN_ATTR_HIGH_TURQUOISE)
               DC    C'CFDT server stats     '
MSG_09_L       EQU   *-MSG_09

MSG_10         DS    0C
               DC    AL1($SCREEN_ATTR_HIGH_TURQUOISE)
               DC    C'Named ctr server      '
MSG_10_L       EQU   *-MSG_10

* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
WK0110         DSECT
               COPY     DXDPREF
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               SESSION
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               BPXYSTAT DSECT=YES,LIST=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               OSSPFD   TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
SMF_RECORD     DSECT
               COPY     DFHSMFDS
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY     ATTRS
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY     REGEQU
               END      SMF0110
