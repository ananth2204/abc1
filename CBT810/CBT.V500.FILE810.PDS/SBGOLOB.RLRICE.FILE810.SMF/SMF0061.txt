*---------------------------------------------------------------------*
*                                                                     *
*         Format SMF record type 61                                   *
*         ICF define activity                                         *
*                                                                     *
*   At entry                                                          *
*       R15    program base address                                   *
*       R14    return address                                         *
*       R9     OSSPFD ADDRESS                                         *
*       R8     VDATA buffer address                                   *
*       R7     SMF RECORD ADDRESS +4 (DOES NOT INCLUDE LENGTH)        *
*                                                                     *
* ------------------------------------------------------------------- *
SMF0061  CSECT
SMF0061  AMODE 31
SMF0061  RMODE ANY
         USING SESSION,R11
         USING SMF0061,R15
         B     INIT0000
MODID    DC    CL8'SMF0061'
         DC    CL8'&SYSDATE'
         DC    CL7'&SYSTIME'
INIT0000 DS    0H
         USING OSCOMM,R12                     DEFINE COMMON AREA BASE
         STM   R14,R12,12(R13)                SAVE REGS
         LR    R10,R15                        COPY ENTRY POINT
         USING SMF0061,R10                    DEFINE BASE
         DROP  R15
         L     R1,SESS_DXD_ADDR
         A     R1,DXDSTART
         ST    R13,4(R1)                      SAVE R13
         ST    R1,8(R13)                      CHAIN SAVE AREA
         LR    R13,R1                         COPY WORK AREA ADDRESS
         USING WK0061,R13                     DEFINE BASE
         MVC   DXD_CSECT,MODID
         USING OSSPFD,R9                      DEFINE BASE
         USING SMF61SYS,R7                    DEFINE BASE
         ITRACE ID=ENTRY
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
         ICM   R3,15,SMF61DOF                 OFFSET TO DATA
         AR    R3,R7
         SH    R3,H4
         USING SMF61DAT,R3                    DEFINE BASE
MAIN0010 DS    0H
         A     R8,SPF_VWIDTH                  NEXT LINE
         MVC   0(MSG_01_L,R8),MSG_01
         USING MSG_01,R8
         MVC   MSG_01_JOB,SMF61JNM            COPY JOB NAME
         MVC   MSG_01_USER,SMF61UID           COPY USER ID
         MVC   MSG_01_CATALOG,SMF61CNM        COPY CATALOG NAME
         A     R8,SPF_VWIDTH                  NEXT LINE
         A     R8,SPF_VWIDTH                  NEXT LINE
         DROP  R8
         LA    R1,TYPES                       FIRST COMPONENT TYPE
         USING TYPE_DSECT,R1
MAIN0020 DS    0H
         CLI   TYPE_TYPE,X'FF'                END OF TABLE?
         BE    MAIN0030                       YES
         CLC   TYPE_TYPE,SMF61TYP             FOUND THE TYPE
         BE    MAIN0030                       YES
         LA    R1,TYPE_L(,R1)                 NEXT TYPE
         B     MAIN0020
MAIN0030 DS    0H
         MVC   0(MSG_02_L,R8),MSG_02
         USING MSG_02,R8
         MVC   MSG_02_TYPE,TYPE_DESC          COPY DESCRIPTION
         MVC   MSG_02_NAME,SMF61ENM           COPY ENTRY NAME
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
EXIT0000 DS    0H
         ITRACE ID=EXIT
         L     R13,4(R13)                     RESTORE SAVE AREA ADDRESS
         L     R14,12(,R13)                   RESTORE R14
         LM    R14,R12,12(R13)                RESTORE REGISTERS
         SR    R15,R15
         BR    R14                            RETURN
* ------------------------------------------------------------------- *
*        CONSTANTS                                                    *
* ------------------------------------------------------------------- *
DXDSTART       DC    Q(WK0061)
H4             DC    H'4'

MSG_01         DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C' JOB'
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_01_JOB     DC    CL8' '
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C' User'
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_01_USER    DC    CL8' '
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C' CAT'
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_01_CATALOG DC    CL44' '
MSG_01_L       EQU   *-MSG_01

MSG_02         DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C' Type'
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_02_TYPE    DC    CL20' '
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C' Name'
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_02_NAME    DC    CL44' '
MSG_02_L       EQU   *-MSG_02


TYPES          DS    0C
               DC    C'A',CL20'Non_VSAM data set'
               DC    C'B',CL20'GDG base'
               DC    C'C',CL20'VSAM Cluster'
               DC    C'D',CL20'Data set'
               DC    C'G',CL20'Alternate index'
               DC    C'H',CL20'GDG member'
               DC    C'I',CL20'VSAM Index'
               DC    C'M',CL20'Master catalog'
               DC    C'P',CL20'Page space'
               DC    C'R',CL20'Path'
               DC    C'U',CL20'User catalog'
               DC    C'V',CL20'Volume'
               DC    C'X',CL20'Alias'
               DC    C'Y',CL20'Upgrade'
               DC    X'FF',CL20'Unknown'
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
WK0061         DSECT
               COPY     DXDPREF
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               SESSION
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               BPXYSTAT DSECT=YES,LIST=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               OSSPFD   TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
SMF_RECORD     DSECT
               IFASMFI6 61
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
TYPE_DSECT     DSECT
TYPE_TYPE      DS       X
TYPE_DESC      DS       CL20
TYPE_L         EQU      *-TYPE_DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY     ATTRS
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY     REGEQU
               END      SMF0061
