* ------------------------------------------------------------------- *
*                                                                     *
*  MODULE NAME: MAIN                                                  *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN     CSECT
MAIN     AMODE 31
MAIN     RMODE ANY
         USING OSCOMM,R12
         USING SESSION,R11
         USING MAIN,R15
         B     INIT0000
MODID    DC    CL8'MAIN '
         DC    CL8'&SYSDATE'
         DC    CL7'&SYSTIME'
INIT0000 DS    0H
         STM   R14,R12,12(R13)                SAVE REGS
         LR    R10,R15                        COPY ENTRY POINT ADDR
         DROP  R15
         USING MAIN,R10                       DEFINE BASE
         LR    R9,R1                          COPY PARM ADDRESS
         USING DSPCREQ,R9                     DEFINE BASE
         L     R8,DSPCREQ_DATASPACE           DATASPACE INFO ADDRESS
         USING SCONTROL_DATASPACE,R8
         ICM   R15,15,COMM_OSDSPACE_DXD       DXD GETMAIN'D?
         BNZ   INIT0010                       YES
         L     R0,DXD_SIZE                    WORK AREA SIZE
         ST    R0,COMM_OSDSPACE_DXD_LENGTH    SAVE LENGTH
         GETMAIN RU,                                                   +
               LV=(0),                                                 +
               LOC=ANY
         ST    R1,COMM_OSDSPACE_DXD           SAVE ADDRESS
         LR    R0,R1                          COPY ADDRESS
         L     R1,COMM_OSDSPACE_DXD_LENGTH
         SR    R14,R14
         SR    R15,R15
         MVCL  R0,R14                         INITIALIZE WORK AREA
         L     R15,COMM_OSDSPACE_DXD
INIT0010 DS    0H
         A     R15,DXD_START                  DISPLACEMENT TO OUR PART
         ST    R13,4(R15)
         ST    R15,8(R13)
         LR    R13,R15
         USING DXDMAIN,R13                    DEFINE BSE
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY,                                              +
               DATA1=(DSPCREQ_FUNC,1),                                 +
               DATA2=(DSPCREQ_DATASPACE,4)
         MVC   COMM_MSG_CSECT,OSDSPACE  <---- THIS IS A BIT CONFUSING
         LA    R7,COMMAND_TABLE               COMMAND TABLE
         USING COMMAND_DSECT,R7
DSPC0030 DS    0H
         CLI   COMMAND,X'FF'                  END OF TABLE
         BE    DSPC0040                       YES
         CLC   DSPCREQ_FUNC,COMMAND           FUNCTION LOCATED?
         BE    DSPC0050                       YES
         LA    R7,COMMAND_L(,R7)              NEXT IN TABLE
         B     DSPC0030
DSPC0040 DS    0H
         ITRACE ID=BAD_CMD                    COMMAND NOT FOUND
         ABEND 1,DUMP,,USER
DSPC0050 DS    0H
         ICM   R15,15,COMMAND_ADDRESS         COMMAND ROUTINE ADDRESS
         ITRACE ID=LINK,                                               +
               RDATA1=R15,RDATA2=R7
         BALR  R14,R15                        PROCESS THE COMMAND
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         L     R1,4(,R13)
         L     R1,12(,R1)
         ITRACE ID=EXIT,                                               +
               DATA1=(DSPCREQ_RC,1),                                   +
               RDATA2=R1
         L     R13,4(R13)                     RESTORE SAVE AREA ADDRESS
         LM    R14,R12,12(R13)                RESTORE REGISTERS
         BR    R14                            RETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXD_SIZE       CXD
DXD_START      DC   Q(DXDMAIN)

OSDSPACE       DC   CL8'OSDSPACE'    MESSAGES 'CSECT' NAME

COMMAND_TABLE  DS   0C
               DC   AL1($DSPCREQ_CREATE),VL4(CREATE)
               DC   AL1($DSPCREQ_CLEAR),VL4(CLEAR)
               DC   AL1($DSPCREQ_DELETE),VL4(DELETE)
               DC   AL1($DSPCREQ_DUMP),VL4(DUMP)
               DC   AL1($DSPCREQ_FIND_MEMBER_G),VL4(FINDMEM)
               DC   AL1($DSPCREQ_FIND_MEMBER_S),VL4(FINDMEM)
               DC   AL1($DSPCREQ_FIND_DSN_G),VL4(FINDDSN)
               DC   AL1($DSPCREQ_FIND_DSN_S),VL4(FINDDSN)
               DC   AL1($DSPCREQ_FIND_CCHHR),VL4(FINDCCHH)
               DC   AL1($DSPCREQ_FIND_FILE_G),VL4(FINDFILE)
               DC   AL1($DSPCREQ_FIND_FILE_S),VL4(FINDFILE)
               DC   AL1($DSPCREQ_SORT_NAME),VL4(SORT)
               DC   AL1($DSPCREQ_SORT_TTR),VL4(SORT)
               DC   AL1($DSPCREQ_SORT_CDATE),VL4(SORT)
               DC   AL1($DSPCREQ_SORT_MDATE),VL4(SORT)
               DC   AL1($DSPCREQ_SORT_SIZE),VL4(SORT)
               DC   AL1($DSPCREQ_SORT_USER),VL4(SORT)
               DC   AL1($DSPCREQ_SORT_DSN),VL4(SORT)
               DC   AL1($DSPCREQ_SORT_CCHHR),VL4(SORT)
               DC   AL1($DSPCREQ_RETRIEVE),VL4(RETRIEVE)
               DC   AL1($DSPCREQ_STORE),VL4(STORE)
               DC   AL1($DSPCREQ_UPDATE),VL4(UPDATE)
               DC   X'FF'
               LTORG
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDMAIN           DSECT
                  COPY      DXDPREF
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                  COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                  SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                  BPXYSTAT DSECT=YES,LIST=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                  SCONTROL SCONTROL
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                  DSPCREQ  DSECT=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
COMMAND_DSECT     DSECT
COMMAND           DS       AL1
COMMAND_ADDRESS   DS       VL4
COMMAND_L         EQU      *-COMMAND_DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                  COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                  COPY     REGEQU
                  END      MAIN
