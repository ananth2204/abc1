* ------------------------------------------------------------------- *
*                                                                     *
*  MODULE NAME: SORT                                                  *
*                                                                     *
*  SORT a dataspace                                                   *
*                                                                     *
* ------------------------------------------------------------------- *
SORT     CSECT
SORT     AMODE 31
SORT     RMODE ANY
         USING OSCOMM,R12
         USING SESSION,R11
         USING SORT,R15
         B     INIT0000
MODID    DC    CL8'SORT'
         DC    CL8'&SYSDATE'
         DC    CL7'&SYSTIME'
INIT0000 DS    0H
         STM   R14,R12,12(R13)                SAVE REGS
         LR    R10,R15                        COPY ENTRY POINT ADDR
         DROP  R15
         USING SORT,R10                       DEFINE BASE
         USING DSPCREQ,R9                     DEFINE BASE
         L     R8,DSPCREQ_DATASPACE           DATASPACE INFO ADDRESS
         USING SCONTROL_DATASPACE,R8
         L     R15,COMM_OSDSPACE_DXD          WORK AREA BASE
         A     R15,DXD_START                  PLUS DISPLACEMENT
         ST    R13,4(R15)                     SAVE R13
         ST    R15,8(R13)                     CHAIN SAVE AREA
         LR    R13,R15                        COPY WORK AREA ADDRESS
         USING DXDSORT,R13
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         ITRACE ID=SORT,                                               +
               DATA1=SCONTROL_DATASPACE_NAME
         CLI   DSPCREQ_FUNC,$DSPCREQ_SORT_NAME
         BE    SORT0010
         CLI   DSPCREQ_FUNC,$DSPCREQ_SORT_TTR
         BE    SORT0020
         CLI   DSPCREQ_FUNC,$DSPCREQ_SORT_CDATE
         BE    SORT0030
         CLI   DSPCREQ_FUNC,$DSPCREQ_SORT_MDATE
         BE    SORT0040
         CLI   DSPCREQ_FUNC,$DSPCREQ_SORT_SIZE
         BE    SORT0050
         CLI   DSPCREQ_FUNC,$DSPCREQ_SORT_USER
         BE    SORT0060
         CLI   DSPCREQ_FUNC,$DSPCREQ_SORT_DSN
         BE    SORT0070
         CLI   DSPCREQ_FUNC,$DSPCREQ_SORT_CCHHR
         BE    SORT0080
         DC    H'0'
         BE    SORT0010
         CLI   DSPCREQ_FUNC,$DSPCREQ_SORT_NAME
         BE    SORT0010
         CLI   DSPCREQ_FUNC,$DSPCREQ_SORT_NAME
         BE    SORT0010
SORT0010 DS    0H
         ITRACE ID=SORTNAME
         CLI   SESS_DATA_TYPE,$DATA_TYPE_PDS_DIR
         BNE   ERR0010
         CLI   SESS_SORT,$SORT_NAME           ALREADY SORTED BY NAME?
         BE    EXIT0000                       YES
         LA    R2,NAME_CLC                    ADDRESS OF CLC TO USE
         MVI   SESS_SORT,$SORT_NAME           SET SORT INDICATOR
         B     SORT0100
SORT0020 DS    0H
         ITRACE ID=SORTTTR
         CLI   SESS_DATA_TYPE,$DATA_TYPE_PDS_DIR
         BNE   ERR0010
         CLI   SESS_SORT,$SORT_TTR            ALREADY SORTED BY TTR?
         BE    EXIT0000                       YES
         LA    R2,TTR_CLC                     ADDRESS OF CLC TO USE
         MVI   SESS_SORT,$SORT_TTR            SET SORT INDICATOR
         B     SORT0100
SORT0030 DS    0H
         ITRACE ID=SORTCDAT
         CLI   SESS_DATA_TYPE,$DATA_TYPE_PDS_DIR
         BNE   ERR0010
         CLI   SESS_SORT,$SORT_CDATE          ALREADY SORTED BY CDATE?
         BE    EXIT0000                       YES
         LA    R2,CDATE_CLC                   ADDRESS OF CLC TO USE
         MVI   SESS_SORT,$SORT_CDATE          SET SORT INDICATOR
         B     SORT0100
SORT0040 DS    0H
         ITRACE ID=SORTMDAT
         CLI   SESS_DATA_TYPE,$DATA_TYPE_PDS_DIR
         BNE   ERR0010
         CLI   SESS_SORT,$SORT_MDATE          ALREADY SORTED BY MDATE?
         BE    EXIT0000                       YES
         LA    R2,MDATE_CLC                   ADDRESS OF CLC TO USE
         MVI   SESS_SORT,$SORT_MDATE          SET SORT INDICATOR
         B     SORT0100
SORT0050 DS    0H
         ITRACE ID=SORTSIZE
         CLI   SESS_DATA_TYPE,$DATA_TYPE_PDS_DIR
         BNE   ERR0010
         CLI   SESS_SORT,$SORT_SIZE           ALREADY SORTED BY SIZE?
         BE    EXIT0000                       YES
         LA    R2,SIZE_CLC                    ADDRESS OF CLC TO USE
         MVI   SESS_SORT,$SORT_SIZE           SET SORT INDICATOR
         B     SORT0100
SORT0060 DS    0H
         ITRACE ID=SORTUSER
         CLI   SESS_DATA_TYPE,$DATA_TYPE_PDS_DIR
         BNE   ERR0010
         CLI   SESS_SORT,$SORT_USER           ALREADY SORTED BY USER
         BE    EXIT0000                       YES
         LA    R2,USER_CLC                    ADDRESS OF CLC TO USE
         MVI   SESS_SORT,$SORT_USER           SET SORT INDICATOR
         B     SORT0100
SORT0070 DS    0H
         ITRACE ID=SORTDSN
         CLI   SESS_DATA_TYPE,$DATA_TYPE_VTOC
         BNE   ERR0020
         LA    R2,SORT_DSN_CLC                ADDRESS OF CLC TO USE
         B     SORT0100
SORT0080 DS    0H
         ITRACE ID=SORT_ADR
         CLI   SESS_DATA_TYPE,$DATA_TYPE_VTOC
         BNE   ERR0020
         LA    R2,SORT_CCHHR_CLC              ADDRESS OF CLC TO USE
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
SORT0100 DS    0H
         L     R3,SCONTROL_DATASPACE_LAST_RECORD  HIGHEST RECORD NUMBER
         S     R3,SCONTROL_DATASPACE_FIRST_RECORD MINUS LOWEST
         A     R3,F1                              PLUS 1
         LAM   R4,R4,SCONTROL_DATASPACE_ALET      LOAD ALET
         LAM   R5,R5,SCONTROL_DATASPACE_ALET      LOAD ALET
         LAM   R6,R6,SCONTROL_DATASPACE_ALET      LOAD ALET
         LAM   R7,R7,SCONTROL_DATASPACE_ALET      LOAD ALET
         SAC   512                                TURN ON AR MODE
         SR    R4,R4                              DATA STARTS AT ZERO
I1       USING DATASPACE_INDEX,R4
SORT0110 DS    0H
         C     R3,F1                          DOWN TO 1 RECORD?
         BE    SORT0140                       YES
         LA    R5,DATASPACE_INDEX_L(,R4)
I2       USING DATASPACE_INDEX,R5
         LR    R15,R3                         NUMBER OF ENTRIES LEFT
         BCTR  R15,0                          1 LOOP LESS
SORT0120 DS    0H
         ICM   R6,15,I1.DATASPACE_INDEX_ADDR
D1       USING DIR_RECORD,R6
         ICM   R7,15,I2.DATASPACE_INDEX_ADDR
D2       USING DIR_RECORD,R7
         EX    R0,0(R2)                       COMPARE THE TWO
         BL    SORT0130
         XC    0(DATASPACE_INDEX_L,R4),0(R5)
         XC    0(DATASPACE_INDEX_L,R5),0(R4)
         XC    0(DATASPACE_INDEX_L,R4),0(R5)
SORT0130 DS    0H
         LA    R5,DATASPACE_INDEX_L(,R5)
         BCT   R15,SORT0120
         LA    R4,DATASPACE_INDEX_L(,R4)
         BCT   R3,SORT0110
SORT0140 DS    0H
         SAC   0
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0010  DS    0H
         MVI   COMM_MSG_ID+1,17               SET MESSAGE ID
         B     ERR1000
ERR0020  DS    0H
         MVI   COMM_MSG_ID+1,15               SET MESSAGE ID
ERR1000  DS    0H
         L     R14,COMM_V_OSMSG
         BALR  R14,R15                        BUILD MESSAGE
         MVI   DSPCREQ_RC,$DSPCREQ_ERROR
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=EXIT,                                               +
               DATA1=(DSPCREQ_RC,1)
         LAM   R0,R15,F0
         L     R13,4(R13)                     REESTORE ADDRESS
         LM    R14,R12,12(R13)                RESTORE REGISTERS
         BR    R14                            RETURN

* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
CDATE_CLC       CLC  D1.DIR_SPF_C_DATE,D2.DIR_SPF_C_DATE
MDATE_CLC       CLC  D1.DIR_SPF_M_DATE(7),D2.DIR_SPF_M_DATE
NAME_CLC        CLC  D1.DIR_NAME,D2.DIR_NAME
SIZE_CLC        CLC  D1.DIR_SPF_SIZE,D2.DIR_SPF_SIZE
TTR_CLC         CLC  D1.DIR_TTR,D2.DIR_TTR
USER_CLC        CLC  D1.DIR_SPF_USER,D2.DIR_SPF_USER
SORT_DSN_CLC    CLC  0(44,R6),0(R7)
SORT_CCHHR_CLC  CLC  I1.DATASPACE_INDEX_CCHHR,I2.DATASPACE_INDEX_CCHHR

                DROP I1,I2,D1,D2
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXD_START       DC    Q(DXDSORT)
F0              DC    16F'0'
F1              DC    F'1'

                LTORG
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDSORT         DSECT
                COPY      DXDPREF
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                BPXYSTAT DSECT=YES,LIST=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                DSPCREQ  DSECT=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
SCONTROL        DSECT
                SCONTROL SCONTROL
SCONTROL_L      EQU      *-SCONTROL
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                COPY     SINDEX
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                COPY     DIR
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                COPY     REGEQU
                END      SORT
