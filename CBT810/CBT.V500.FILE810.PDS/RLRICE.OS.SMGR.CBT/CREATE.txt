* ------------------------------------------------------------------- *
*                                                                     *
*  MODULE NAME: CREATE                                                *
*                                                                     *
*  Create a new data space                                            *
*                                                                     *
* ------------------------------------------------------------------- *
CREATE   CSECT
CREATE   AMODE 31
CREATE   RMODE ANY
         USING OSCOMM,R12
         USING SESSION,R11
         USING CREATE,R15
         B     INIT0000
MODID    DC    CL8'CREATE'
         DC    CL8'&SYSDATE'
         DC    CL7'&SYSTIME'
INIT0000 DS    0H
         STM   R14,R12,12(R13)                SAVE REGS
         LR    R10,R15                        COPY ENTRY POINT ADDR
         DROP  R15
         USING CREATE,R10                     DEFINE BASE
         USING DSPCREQ,R9                     DEFINE BASE
         L     R8,DSPCREQ_DATASPACE           DATASPACE INFO ADDRESS
         USING SCONTROL_DATASPACE,R8
         L     R15,COMM_OSDSPACE_DXD          WORK AREA BASE
         A     R15,DXD_START                  PLUS DISPLACEMENT
         ST    R13,4(R15)                     SAVE R13
         ST    R15,8(R13)                     CHAIN SAVE AREA
         LR    R13,R15                        COPY WORK AREA ADDRESS
         USING DXDCRE,R13
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         ICM   R1,15,SCONTROL_DATASPACE_BLOCKS
         BNZ   CRE0000
         LA    R1,1000
         STCM  R1,15,SCONTROL_DATASPACE_BLOCKS
CRE0000  DS    0H
         ITRACE ID=CREATE,                                             +
               DATA1=(SCONTROL_DATASPACE_BLOCKS,4),                    +
               DATA2=(SCONTROL_DATASPACE_FIRST_RECORD,4)
         MVC   DXD_DSPSERV(DSPSERV_L),DSPSERV_I
         ITRACE ID=DSPSERV
         AP    COMM_DATASPACE_NUMBER,P1
         MVC   SCONTROL_DATASPACE_NAME,EDIT_WORD
         ED    SCONTROL_DATASPACE_NAME,COMM_DATASPACE_NUMBER
         MVI   SCONTROL_DATASPACE_NAME,C'D'
         DSPSERV CREATE,                                               +
               NAME=SCONTROL_DATASPACE_NAME,                           +
               STOKEN=SCONTROL_DATASPACE_TOKEN,                        +
               BLOCKS=SCONTROL_DATASPACE_BLOCKS,                       +
               NUMBLKS=DXD_MAX_BLOCKS,                                 +
               TYPE=BASIC,                                             +
               MF=(E,DXD_DSPSERV)
         LTR   R15,R15                        SUCCESSFUL?
         BNZ   ERR0020                        NO
         ITRACE ID=ALESERV
         MVC   DXD_ALESERV(ALESERV_L),ALESERV_I
         ALESERV ADD,                                                  +
               STOKEN=SCONTROL_DATASPACE_TOKEN,                        +
               ALET=SCONTROL_DATASPACE_ALET,                           +
               MF=(E,DXD_ALESERV)
         LTR   R15,R15                        SUCCESSFUL?
         BNZ   ERR0010                        NO
         ITRACE ID=ALET,                                               +
               DATA1=SCONTROL_DATASPACE_NAME,                          +
               DATA2=(SCONTROL_DATASPACE_ALET,4)
         SR    R0,R0                          CLEAR REGISTER
         SR    R1,R1                          CLEAR REGISTER
         SR    R14,R14                        CLEAR REGISTER
         L     R15,DXD_MAX_BLOCKS             DATA SPACE SIZE IN BLOCKS
         MH    R15,H4096                      SIZE OF DATA SPACE
         LAM   R14,R14,SCONTROL_DATASPACE_ALET  SET ACCESS REGISTER
         SAC   512                            TURN ON AR MODE
         MVCL  R14,R0                         INITIALIZE THE DATA SPACE
         SAC   0                              TURN OFF AR MODE
         LAM   R14,R14,F0                     RESET ACCESS REGISTER
         XC    SCONTROL_DATASPACE_LAST_RECORD,SCONTROL_DATASPACE_LAST_R+
               ECORD
         XC    SCONTROL_DATASPACE_BYTES,SCONTROL_DATASPACE_BYTES
         MVC   SCONTROL_DATASPACE_BLOCKS,DXD_MAX_BLOCKS
         L     R1,SCONTROL_DATASPACE_BLOCKS   SIZE IN 4K BLOCKS
         MH    R1,H4096                       TIMES SIZE OF A BLOCK
         BCTR  R1,0                           MINUS 1
         ST    R1,SCONTROL_DATASPACE_LAST_ADDRESS  INITIALIZE ADDR
         ITRACE ID=CREATED,                                            +
               DATA1=(DXD_MAX_BLOCKS,4),                               +
               DATA2=(SCONTROL_DATASPACE_ALET,4)
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0010  DS    0H
         MVI   COMM_MSG_ID+1,1           ALESERV FAILED
         B     ERR1000
ERR0020  DS    0H
         MVI   COMM_MSG_ID+1,2           DSPSERV FAILED
         B     ERR1000
ERR1000  DS    0H
         STCM  R15,15,COMM_INFO_01
         STCM  R0,15,COMM_INFO_01+4
         MVC   COMM_INFO_02,SCONTROL_DATASPACE_NAME
         L     R15,COMM_V_OSMSG
         BALR  R14,R15                   BUILD MESSAGE
         MVI   DSPCREQ_RC,$DSPCREQ_ERROR
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=EXIT,                                               +
               DATA1=(DSPCREQ_RC,1)
         L     R13,4(R13)                RESTORE SAVE AREA ADDRESS
         LM    R14,R12,12(R13)           RESTORE REMAINING REGISTERS
         BR    R14                       RETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXD_START     DC    Q(DXDCRE)
DSPSERV_I     DSPSERV MF=(L,DSP)
DSPSERV_L     EQU   *-DSPSERV_I
ALESERV_I     ALESERV MF=L
ALESERV_L     EQU   *-ALESERV_I
F0            DC    16F'0'
H4096         DC    H'4096'

P1            DC    P'1'

EDIT_WORD     DC    X'F020202020202120'


               LTORG
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDCRE            DSECT
                  COPY      DXDPREF
DXD_DSPSERV       DS        0F,(DSPSERV_L)X
DXD_ALESERV       DS        0F,(ALESERV_L)X
DXD_MAX_BLOCKS    DS        F
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                  COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                  SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                  BPXYSTAT DSECT=YES,LIST=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                  DSPCREQ  DSECT=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
SCONTROL          DSECT
                  SCONTROL SCONTROL
SCONTROL_L        EQU      *-SCONTROL
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                  COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                  COPY     REGEQU
                  END      CREATE
