* ------------------------------------------------------------------- *
*                                                                     *
*  MODULE NAME: FINDMEM                                               *
*                                                                     *
*  FIND a member                                                      *
*                                                                     *
* ------------------------------------------------------------------- *
FINDMEM  CSECT
FINDMEM  AMODE 31
FINDMEM  RMODE ANY
         USING OSCOMM,R12
         USING SESSION,R11
         USING FINDMEM,R15
         B     INIT0000
MODID    DC    CL8'FINDMEM'
         DC    CL8'&SYSDATE'
         DC    CL7'&SYSTIME'
INIT0000 DS    0H
         STM   R14,R12,12(R13)            SAVE REGS
         LR    R10,R15                    COPY ENTRY POINT ADDR
         DROP  R15
         USING FINDMEM,R10                DEFINE BASE
         USING DSPCREQ,R9                 DEFINE BASE
         L     R8,DSPCREQ_DATASPACE       DATASPACE INFO ADDRESS
         USING SCONTROL_DATASPACE,R8
         L     R15,COMM_OSDSPACE_DXD      WORK AREA BASE
         A     R15,DXD_START              PLUS DISPLACEMENT
         ST    R13,4(R15)                 SAVE R13
         ST    R15,8(R13)                 CHAIN SAVE AREA
         LR    R13,R15                    COPY WORK AREA ADDRESS
         USING DXDMEM,R13
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY,                                              +
               DATA1=DSPCREQ_MEMBER
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         CLI   SESS_DATA_TYPE,$DATA_TYPE_PDS_DIR
         BNE   ERR0010                        DATA IS NOT FROM PDS DIR
         ITRACE ID=FINDMEM,                                            +
               DATA1=SCONTROL_DATASPACE_NAME,                          +
               DATA2=DSPCREQ_MEMBER
         LA    R1,1                           INITIALIZE LOOP COUNTER
         LA    R14,7                          LENGTH (INSTRUCTION TYPE)
         LA    R15,DSPCREQ_MEMBER+7           END OF MEMBER NAME
         CLI   DSPCREQ_FUNC,$DSPCREQ_FIND_MEMBER_G
         BE    FIND0010                       GENERIC
         L     R7,SCONTROL_DATASPACE_FIRST_RECORD
         B     FIND0030
FIND0010 DS    0H
         CLI   0(R15),C' '                    BLANK?
         BNE   FIND0020                       NO
         LA    R1,1(,R1)                      ADD TO LOOP COUNTER
         BCTR  R15,0                          BACK UP 1
         BCT   R14,FIND0010                   LOOP
FIND0020 DS    0H
         L     R7,DSPCREQ_RECORD_NBR          STARTING RECORD
FIND0030 DS    0H
         STH   R1,DXD_SEARCH_LOOPS            SAVE NUMBER OF LOOPS
         STH   R14,DXD_SEARCH_LENGTH          SAVE MEMBER NAME LENGTH
         LR    R6,R7                          COPY RECORD NUMBER
         BCTR  R6,0                           MINUS 1
         MH    R6,=Y(DATASPACE_INDEX_L)       MULTIPLY BY LENGTH EACH
         USING DATASPACE_INDEX,R6             DEFINE BASE
         LAM   R6,R6,SCONTROL_DATASPACE_ALET  LOAD AR
         LAM   R4,R4,SCONTROL_DATASPACE_ALET  LOAD AR
         LAM   R1,R1,SCONTROL_DATASPACE_ALET  LOAD AR
         SAC   512                            TURN ON AR MODE
FIND0040 DS    0H
         ICM   R4,15,DATASPACE_INDEX_ADDR     ADDRESS OF STORED RECORD
         BZ    FIND0060                       NOT STORED
         USING DIR_RECORD,R4
         MVC   DXD_MEMBER,DIR_NAME
         LA    R1,DIR_NAME                    1ST CHARACTER OF NAME

*
*        Uncomment these if needed for debugging
*
*        SAC   0
*        ITRACE ID=XX,DATA1=DXD_MEMBER,RDATA2=R7
*        SAC   512

         LH    R14,DXD_SEARCH_LOOPS           NUMBER OF LOOPS
         LH    R15,DXD_SEARCH_LENGTH          LENGTH OF SEARCH ARG
FIND0050 DS    0H
         EX    R15,MEMBER_SEARCH_CLC          STRING LOCATED?
         BE    FIND0070                       YES
         LA    R1,1(,R1)                      NEXT ADDRESS
         BCT   R14,FIND0050                   LOOP
FIND0060 DS    0H
         C     R7,SCONTROL_DATASPACE_LAST_RECORD AT LAST RECORD?
         BE    FIND0080                       YES
         LA    R6,DATASPACE_INDEX_L(,R6)      NEXT INDEX ENTRY
         A     R7,F1                          ADD 1 TO RECORD NUMBER
         B     FIND0040                       CHECK THIS ONE
FIND0070 DS    0H
         L     R5,DATASPACE_INDEX_LENGTH      LENGTH OF STORED RECORD
         ST    R5,DSPCREQ_RECORD_LENGTH       RETURN LENGTH TO USER
         L     R14,DSPCREQ_RECORD_ADDR        USER'S I/O AREA
         LR    R15,R5                         COPY LENGTH TO 'R2'
         MVCL  R14,R4                         COPY RECORD TO USER
         SAC   0                              TURN OFF AR MODE
         ITRACE ID=FOUND,                                              +
               RDATA1=R7
         ST    R7,DSPCREQ_RECORD_NBR          RETURN RECORD NUMBER
         MVI   DSPCREQ_RC,$DSPCREQ_OK         FOUND
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
FIND0080 DS    0H
         SAC   0
         ITRACE ID=NOT_FND
         MVI   DSPCREQ_RC,$DSPCREQ_ERROR      NOT FOUND
         MVI   COMM_MSG_ID+1,12
         MVC   COMM_INFO_01(L'DSPCREQ_MEMBER),DSPCREQ_MEMBER
         L     R15,COMM_V_OSMSG
         BALR  R14,R15
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0010  DS    0H
         MVI   COMM_MSG_ID+1,17               SET MESSAGE ID
         L     R14,COMM_V_OSMSG
         BALR  R14,R15                        BUILD MESSAGE
         MVI   DSPCREQ_RC,$DSPCREQ_ERROR
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=EXIT,                                               +
               DATA1=(DSPCREQ_RC,1)
         LAM   R6,R6,F0                       RESET AR
         LAM   R4,R4,F0                       RESET AR
         LAM   R1,R1,F0                       RESET AR
         L     R13,4(R13)                     RESTORE SAVE AREA ADDRESS
         LM    R14,R12,12(R13)                RESTORE REGISTERS
         BR    R14                            RETURN

* ------------------------------------------------------------------- *
MEMBER_SEARCH_CLC   CLC DSPCREQ_MEMBER(0),0(R1)
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXD_START     DC    Q(DXDMEM)
F0            DC    F'0'
F1            DC    F'1'

              LTORG
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDMEM            DSECT
                  COPY      DXDPREF
DXD_SEARCH_LOOPS  DS        H
DXD_SEARCH_LENGTH DS        H
DXD_MEMBER        DS        CL8
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                  COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                  SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                  BPXYSTAT DSECT=YES,LIST=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                  DSPCREQ  DSECT=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
SCONTROL          DSECT
                  SCONTROL SCONTROL
SCONTROL_L        EQU      *-SCONTROL
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                  COPY     SINDEX
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                  COPY     DIR
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                  COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                  COPY     REGEQU
                  END      FINDMEM
