* ------------------------------------------------------------------- *
*                                                                     *
*  MODULE NAME: FMTFLD                                                *
*                                                                     *
*  Process FIELD FMAT block during a 'format'                         *
*                                                                     *
* ------------------------------------------------------------------- *
FMTFLD   CSECT
FMTFLD   AMODE 31
FMTFLD   RMODE ANY
         USING OSCOMM,R12
         USING SESSION,R11
         USING FMTFLD,R15
         B     INIT0000
MODID    DC    CL8'FMTFLD'
         DC    CL8'&SYSDATE'
         DC    CL7'&SYSTIME'
INIT0000 DS    0H
         STM   R14,R12,12(R13)               SAVE REGS
         LR    R10,R15                       COPY ENTRY POINT
         DROP  R15
         USING FMTFLD,R10                    DEFINE BASE
         L     R9,SESS_DXD_ADDR              WORK AREA
         LR    R15,R9                        COPY WORK AREA ADDRESS
         USING FMT_COMMON,R9
         L     R15,SESS_DXD_ADDR             WORK AREA BASE
         A     R15,DXD_START                 PLUS DISP
         ST    R13,4(R15)                    SAVE R13
         ST    R15,8(R13)                    CHAIN SAVE AREA
         LR    R13,R15                       COPY WORK AREA ADDRESS
         USING DXDFFLD,R13                   DEFINE WORK AREA BASE
         MVC   DXD_CSECT,MODID
         L     R7,DXD_FMAT_FMAT              FMAT BEING PROCESSED
         USING FMAT,R7
         ITRACE ID=ENTRY,                                              +
               RDATA1=R7
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         CLI   FMAT_DATA_TYPE,$FMAT_BINARY   BINARY?
         BE    FLD0010                       YES
         CLI   FMAT_DATA_TYPE,$FMAT_CHARACTER
         BE    FLD0020                       YES
         CLI   FMAT_DATA_TYPE,$FMAT_HEADING  HEADING?
         BE    FLD0030                       YES
         CLI   FMAT_DATA_TYPE,$FMAT_HEX      HEX?
         BE    FLD0040                       YES
         CLI   FMAT_DATA_TYPE,$FMAT_PACKED   PACKED DECIMAL?
         BE    FLD0050                       YES
         DC    H'0'
FLD0010  DS    0H
         ITRACE ID=BINARY                    BINARY FIELD
         L     R15,V_FMTBIN
         B     FLD0060
FLD0020  DS    0H
         ITRACE ID=CHARACTER
         L     R15,V_FMTCHAR                 CHARACTER FIELD
         B     FLD0060
FLD0030  DS    0H
         ITRACE ID=HEADING
         L     R15,V_FMTHEAD                 HEADING FIELD
         B     FLD0060
FLD0040  DS    0H
         ITRACE ID=HEX
         L     R15,V_FMTHEX                  HEX FIELD
         B     FLD0060
FLD0050  DS    0H
         ITRACE ID=PACKED
         L     R15,V_FMTPACK                 PACKED DECIMAL FIELD
FLD0060  DS    0H
         ITRACE ID=LINK,                                               +
               RDATA1=R15
         BALR  R14,R15
         ITRACE ID=LINKX
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         MVC   DXD_FMAT_FMAT,FMAT_NEXT       SET NEXT FMAT
         ITRACE ID=EXIT,                                               +
               DATA1=(DXD_FMAT_FMAT,4)
         LH    R15,DXD_RC                    SET RC
         L     R13,4(,R13)                   RESTORE R13
         L     R14,12(,R13)                  RESTORE R14
         LM    R0,R12,20(R13)                RESTORE REGISTERS
         BR    R14                           RETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXD_START      DC      Q(DXDFFLD)
V_FMTBIN       DC      V(FMTBIN)
V_FMTCHAR      DC      V(FMTCHAR)
V_FMTHEAD      DC      V(FMTHEAD)
V_FMTHEX       DC      V(FMTHEX)
V_FMTPACK      DC      V(FMTPACK)

               LTORG
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDFFLD        DSECT
               COPY    DXDPREF

DXD_RC         DS      H

* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               SESSION
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               BPXYSTAT DSECT=YES,LIST=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
FMT_COMMON     DSECT
               DS      (DXD_PREFIX_L)X
               COPY    FMTCOMM
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY    FMAT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY    TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY    REGEQU
               END     FMTFLD
