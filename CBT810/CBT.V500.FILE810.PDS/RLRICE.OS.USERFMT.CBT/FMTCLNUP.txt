* ------------------------------------------------------------------- *
*                                                                     *
*  MODULE NAME: FMTCLNUP                                              *
*                                                                     *
*  Free the FMAT blocks                                               *
*                                                                     *
* ------------------------------------------------------------------- *
FMTCLNUP CSECT
FMTCLNUP AMODE 31
FMTCLNUP RMODE ANY
         USING OSCOMM,R12
         USING SESSION,R11
         USING FMTCLNUP,R15
         B     INIT0000
MODID    DC    CL8'FMTCLNUP'
         DC    CL8'&SYSDATE'
         DC    CL7'&SYSTIME'
INIT0000 DS    0H
         STM   R14,R12,12(R13)               SAVE REGS
         LR    R10,R15                       COPY ENTRY POINT
         DROP  R15
         USING FMTCLNUP,R10                  DEFINE BASE
         L     R9,SESS_DXD_ADDR              WORK AREA BASE
         USING FMT_COMMON,R9                 FORMATTER COMMON
         LR    R15,R9
         L     R15,SESS_DXD_ADDR             DXD BASE
         A     R15,DXD_START                 PLUS DISP
         ST    R13,4(R15)                    SAVE R13
         ST    R15,8(R13)                    CHAIN SAVE AREA
         LR    R13,R15                       COPY WORK AREA ADDRESS
         USING DXDCLNUP,R13                  DEFINE WORK AREA BASE
         MVC   DXD_CSECT,MODID
         XC    DXD_IF_LEVEL,DXD_IF_LEVEL     RESET IF LEVEL
         ICM   R7,15,SESS_FUNCTION_DATA      FIRST FMAT BLOCK
         BZ    EXIT0000                      NO FMAT BLOCKS
         USING FMAT,R7
FREE0010 DS    0H
         ITRACE ID=FMAT,                     TRACE EACH BLOCK          +
               RDATA1=R7,                                              +
               DATA2=(FMAT_FIELDS,4)
         ICM   R2,15,FMAT_FIELDS             ANY DEPENDANT FIELDS?
         BZ    FREE0020                      NO
         LH    R15,DXD_IF_LEVEL              CURRENT IF LEVEL
         ITRACE ID=DOWN,                                               +
               RDATA1=R15,                                             +
               RDATA2=R2
         SLL   R15,2                         MULTIPLY BY 4
         LA    R15,DXD_IF_TABLE(R15)         COMPUTE ADDRESS
         ST    R7,0(R15)                     SAVE IF FMAT BLOCK ADDRESS
         LH    R15,DXD_IF_LEVEL              IF LEVEL (AGAIN)
         LA    R15,1(,R15)                   PLUS 1
         STH   R15,DXD_IF_LEVEL              UPDATE IF LEVEL
         LR    R7,R2                         COPY DEPENDANT'S ADDRESS
FREE0020 DS    0H
         L     R2,FMAT_NEXT                  NEXT FMAT BLOCK'S ADDRESS
FREE0030 DS    0H
         ITRACE ID=FREEFMAT,                 FREEMAIN THIS FMAT BLOCK  +
               RDATA1=R7,                                              +
               RDATA2=R2
         FREEMAIN RU,                                                  +
               A=(R7),                                                 +
               LV=FMAT_L
         LTR   R7,R2                         COPY NEXT ON CHAIN
         BNZ   FREE0010                      FREE IT
         SR    R15,R15                       CLEAR R15
         ICM   R15,3,DXD_IF_LEVEL            CURRENT IF LEVEL
         BZ    FREE0040                      ZERO.. ALL DONE
         BCTR  R15,0                         MINUS 1
         STH   R15,DXD_IF_LEVEL              UPDATE IF LEVEL
         SLL   R15,2                         MULTIPLY BY 4
         LA    R15,DXD_IF_TABLE(R15)         COMPUTE ADDRESS
         L     R7,0(R15)                     RESTORE SAVED FMAT ADDRESS
         ITRACE ID=UP,                                                 +
               RDATA1=R15,                                             +
               RDATA2=R7
         B     FREE0020                      FREE IT
FREE0040 DS    0H
         XC    SESS_FUNCTION_DATA,SESS_FUNCTION_DATA
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         L     R13,4(R13)                    RESTORE R13
         LM    R14,R12,12(R13)               RESTORE REGISTERS
         SR    R15,R15                       SET RC
         BR    R14                           RETURN

* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXD_START      DC      Q(DXDCLNUP)
               LTORG
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDCLNUP       DSECT
               COPY    DXDPREF
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
FMT_COMMON     DSECT
               DS      (DXD_PREFIX_L)X
               COPY    FMTCOMM
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               SESSION
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               BPXYSTAT DSECT=YES,LIST=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY    FORMAT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY    FMAT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY    TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY    REGEQU
               END     FMTCLNUP
