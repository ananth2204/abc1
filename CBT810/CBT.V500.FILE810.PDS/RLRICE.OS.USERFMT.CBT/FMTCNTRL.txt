* ------------------------------------------------------------------- *
*                                                                     *
*  Module name: FMTCNTRL                                              *
*                                                                     *
*  Process control statements                                         *
*                                                                     *
*                                                                     *
*  At time of entry R1 is the address of the control statement        *
*                                                                     *
* ------------------------------------------------------------------- *
FMTCNTRL CSECT
FMTCNTRL AMODE 31
FMTCNTRL RMODE ANY
         USING OSCOMM,R12
         USING SESSION,R11
         USING FMTCNTRL,R15
         B     INIT0000
MODID    DC    CL8'FMTCNTRL'
         DC    CL8'&SYSDATE'
         DC    CL7'&SYSTIME'
INIT0000 DS    0H
         STM   R14,R12,12(R13)               SAVE REGS
         LR    R10,R15                       COPY ENTRY POINT
         DROP  R15
         USING FMTCNTRL,R10                  DEFINE BASE
         L     R9,SESS_DXD_ADDR              WORK AREA BASE
         USING FMT_COMMON,R9                 FORMATTER COMMON
         LR    R15,R9
         A     R15,DXD_START                 PLUS DISP
         ST    R13,4(R15)                    SAVE R13
         ST    R15,8(R13)                    CHAIN SAVE AREA
         LR    R13,R15                       COPY WORK AREA ADDRESS
         USING DXDCNTRL,R13                  DEFINE WORK AREA BASE
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY,                                              +
               RDATA1=R13,                                             +
               RDATA2=R9
         XC    DXD_RC,DXD_RC                 SET RC
         USING FORMAT,R1                     DEFINE BASE
         ITRACE ID=TYPE,                                               +
               RDATA1=R1,                                              +
               DATA2=FORMAT_TYPE
         CLC   ENDIF,FORMAT_TYPE             ENDIF STATEMENT?
         BE    ENDIF000                      YES
         CLC   FIELD,FORMAT_TYPE             FIELD STATEMENT?
         BE    FIELD000                      YES
         CLC   IF,FORMAT_TYPE                IF STATEMENT?
         BE    IF0000                        YES
         B     ERR0010
ENDIF000 DS    0H
         ITRACE ID=ENDIF
         L     R15,V_CNTRLEIF                CNTRLEIF ENTRY POINT
         BALR  R14,R15                       LINK TO CNTRLEIF
         ITRACE ID=ENDIFX
         B     EXIT0000
IF0000   DS    0H
         ITRACE ID=IF
         L     R15,V_CNTRLIF                 CNTRLIF ENTRY POINT
         BALR  R14,R15                       LINK TO CNTRLIF
         ITRACE ID=IFX
         B     EXIT0000
FIELD000 DS    0H
         ITRACE ID=FIELD
         L     R15,V_CNTRLFLD                CNTRLFLD ENTRY POINT
         BALR  R14,R15                       LINK TO CNTRLFLD
         ITRACE ID=FLDX
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0010  DS    0H
         MVI   COMM_MSG_ID+1,1               UNRECOGNIZED RECORD TYPE
         MVC   COMM_MSG_CSECT,MODID          SET MODULE NAME
         L     R15,COMM_V_OSMSG              MESSAGE BUILDER
         BALR  R14,R15                       BUILD MESSAGE
         MVI   DXD_RC+1,8                    SET RETURN CODE
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         LH    R15,DXD_RC                    SET RETURN CODE
         ITRACE ID=EXIT,                                               +
               RDATA1=R15
         L     R13,4(R13)                    RESTORE R13
         L     R14,12(,R13)                  RESTORE R14
         LM    R0,R12,20(R13)                RESTORE REGISTERS
         BR    R14                           RETURN

* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXD_START      DC      Q(DXDCNTRL)
V_CNTRLEIF     DC      V(CNTRLEIF)
V_CNTRLFLD     DC      V(CNTRLFLD)
V_CNTRLIF      DC      V(CNTRLIF)
ENDIF          DC      CL8'ENDIF'
FIELD          DC      CL8'FIELD'
IF             DC      CL8'IF'
               LTORG
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDCNTRL       DSECT
               COPY    DXDPREF
DXD_RC         DS      H
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
FMT_COMMON     DSECT
               DS      (DXD_PREFIX_L)X
               COPY    FMTCOMM
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               SESSION
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               BPXYSTAT DSECT=YES,LIST=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY    FORMAT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY    FMAT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY    TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY    REGEQU
               END     FMTCNTRL
