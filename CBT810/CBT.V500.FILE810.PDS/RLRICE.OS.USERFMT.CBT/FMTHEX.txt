* ------------------------------------------------------------------- *
*                                                                     *
*  MODULE NAME: FMTHEX                                                *
*                                                                     *
*  Process heading (constant) fields during a 'format'                *
*                                                                     *
* ------------------------------------------------------------------- *
FMTHEX   CSECT
FMTHEX   AMODE 31
FMTHEX   RMODE ANY
         USING OSCOMM,R12
         USING SESSION,R11
         USING FMTHEX,R15
         B     INIT0000
MODID    DC    CL8'FMTHEX'
         DC    CL8'&SYSDATE'
         DC    CL7'&SYSTIME'
INIT0000 DS    0H
         STM   R14,R12,12(R13)               SAVE REGS
         LR    R10,R15                       COPY ENTRY POINT
         DROP  R15
         USING FMTHEX,R10                    DEFINE BASE
         L     R9,SESS_DXD_ADDR              WORK AREA
         LR    R15,R9                        COPY WORK AREA ADDRESS
         USING FMT_COMMON,R9
         L     R15,SESS_DXD_ADDR             WORK AREA BASE
         A     R15,DXD_START                 PLUS DISP
         ST    R13,4(R15)                    SAVE R13
         ST    R15,8(R13)                    CHAIN SAVE AREA
         LR    R13,R15                       COPY WORK AREA ADDRESS
         USING FMTFHEX,R13                   DEFINE WORK AREA BASE
         MVC   DXD_CSECT,MODID
         L     R7,DXD_FMAT_FMAT              FMAT BEING PROCESSED
         USING FMAT,R7
         ITRACE ID=ENTRY,                                              +
               RDATA1=R7
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         L     R8,COMM_OSSPFD
         USING OSSPFD,R8
         LH    R1,FMAT_START                 STARTING POSITION
         LH    R2,FMAT_LENGTH                LENGTH
         CH    R2,=Y(L'COMM_INFO_01/2)       TOO LONG?
         BL    HEX0000                       NO
         LH    R2,=Y(L'COMM_INFO_01/2)       LIMIT LENGTH
HEX0000  DS    0H
         LR    R3,R2                         SAVE LENGTH USED
         A     R1,DXD_RECORD_ADDR            PLUS RECORD ADDRESS
         BCTR  R1,0
         LA    R15,COMM_INFO_01              WORK AREA FOR UNPACKING
HEX0010  DS    0H
         UNPK  0(3,R15),0(2,R1)              UNPACK DATA
         TR    0(2,R15),COMM_HEXCHAR         TRANSLATE
         LA    R1,1(,R1)                     NEXT SOURCE
         LA    R15,2(,R15)                   NEXT OUTPUT
         BCT   R2,HEX0010                    LOOP
         LA    R0,COMM_INFO_01               SOURCE IS UNPACKED DATA
         ST    R0,DXD_SOURCE_START
         SLL   R3,1                          DOUBLE LENGTH
         ST    R3,DXD_DISPLAY_LENGTH
         AR    R0,R3                         PLUS LENGTH
         BCTR  R0,0                          LAST BYTE
         ST    R0,DXD_SOURCE_END
         LH    R0,FMAT_ROW                   ROW ON DISPLAY
         LH    R1,FMAT_COLUMN                COLUMN ON DISPLAY
         BCTR  R0,0                          RELATIVE TO ZERO
         MH    R0,SPF_VWIDTH+2               MULTIPLY BY LINE LENGTH
         BCTR  R1,0                          COLUMN RELATIVE TO ZERO
         AR    R1,R0                         PLUS COLUMN
         A     R1,DXD_VDATA_ADDR             PLUS VDATA ADDRESS
         ST    R1,DXD_DISPLAY_START          START ADDR IN VDATA
         AR    R1,R3                         PLUS LENGTH
         ST    R3,DXD_DISPLAY_END            END ADDRESS IN VDATA
         ITRACE ID=STRT_END,                                           +
               DATA1=DXD_SOURCE_START,                                 +
               DATA2=DXD_DISPLAY_START
         L     R0,DXD_SOURCE_START           UNPACKED DATA ADDRESS
         LR    R1,R3                         DISPLAY LENGTH
         L     R14,DXD_DISPLAY_START         ADDRESS IN VDATA
         LR    R15,R1
         MVCL  R14,R0                        COPY SOURCE TO DISPLAY
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0010  DS    0H
         MVI   COMM_MSG_ID+1,1
         B     ERR1000
ERR0020  DS    0H
         MVI   COMM_MSG_ID+1,2
ERR1000  DS    0H
         MVC   COMM_MSG_CSECT,MODID
         L     R15,COMM_V_OSMSG
         BALR  R14,R15
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=EXIT
         LH    R15,DXD_RC                    SET RC
         L     R13,4(,R13)                   RESTORE R13
         L     R14,12(,R13)                  RESTORE R14
         LM    R0,R12,20(R13)                RESTORE REGISTERS
         BR    R14                           RETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXD_START      DC      Q(FMTFHEX)

               LTORG
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
FMTFHEX              DSECT
                     COPY     DXDPREF

DXD_RC               DS   H

DXD_SOURCE_START     DS   A
DXD_SOURCE_END       DS   A
DXD_DISPLAY_START    DS   A
DXD_DISPLAY_END      DS   A
DXD_DISPLAY_LENGTH   DS   F
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               SESSION
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               BPXYSTAT DSECT=YES,LIST=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
FMT_COMMON     DSECT
               DS      (DXD_PREFIX_L)X
               COPY    FMTCOMM
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY    FMAT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               OSSPFD  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY    TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY    REGEQU
               END     FMTHEX
