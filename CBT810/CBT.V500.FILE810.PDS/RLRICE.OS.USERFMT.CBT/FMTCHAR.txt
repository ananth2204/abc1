* ------------------------------------------------------------------- *
*                                                                     *
*  MODULE NAME: FMTCHAR                                               *
*                                                                     *
*  Process heading (constant) fields during a 'format'                *
*                                                                     *
* ------------------------------------------------------------------- *
FMTCHAR  CSECT
FMTCHAR  AMODE 31
FMTCHAR  RMODE ANY
         USING OSCOMM,R12
         USING SESSION,R11
         USING FMTCHAR,R15
         B     INIT0000
MODID    DC    CL8'FMTCHAR'
         DC    CL8'&SYSDATE'
         DC    CL7'&SYSTIME'
INIT0000 DS    0H
         STM   R14,R12,12(R13)               SAVE REGS
         LR    R10,R15                       COPY ENTRY POINT
         DROP  R15
         USING FMTCHAR,R10                   DEFINE BASE
         L     R9,SESS_DXD_ADDR              WORK AREA
         LR    R15,R9                        COPY WORK AREA ADDRESS
         USING FMT_COMMON,R9
         L     R15,SESS_DXD_ADDR             WORK AREA BASE
         A     R15,DXD_START                 PLUS DISP
         ST    R13,4(R15)                    SAVE R13
         ST    R15,8(R13)                    CHAIN SAVE AREA
         LR    R13,R15                       COPY WORK AREA ADDRESS
         USING DXDFCHAR,R13                  DEFINE WORK AREA BASE
         MVC   DXD_CSECT,MODID
         L     R7,DXD_FMAT_FMAT              FMAT BEING PROCESSED
         USING FMAT,R7
         ITRACE ID=ENTRY,                                              +
               RDATA1=R7
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         L     R8,COMM_OSSPFD
         USING OSSPFD,R8
         LH    R1,FMAT_START                 STARTING POSITION
         LH    R2,FMAT_LENGTH                LENGTH
         LH    R3,FMAT_ROW                   DISPLAY ROW
         LH    R4,FMAT_COLUMN                DISPLAY COLUMN
         LR    R0,R1                         COPY START
         A     R0,DXD_RECORD_ADDR            PLUS BASE ADDRESS
         BCTR  R0,0                          RELATIVE TO ZERO
         ST    R0,DXD_SOURCE_START           DATA SOURCE START ADDRESS
         AR    R0,R2                         PLUS LENGTH
         ST    R0,DXD_SOURCE_END             DATA SOURCE END ADDRESS
         BCTR  R3,0                          ROW RELATIVE TO ZERO
         MH    R3,SPF_VWIDTH+2               MULTIPLY BY LINE LENGTH
         BCTR  R4,0                          COLUMN RELATIVE TO ZERO
         AR    R3,R4                         PLUS COLUMN
         A     R3,DXD_VDATA_ADDR             PLUS VDATA ADDRESS
         ST    R3,DXD_DISPLAY_START          START ADDR IN VDATA
         AR    R3,R2                         PLUS LENGTH
         ST    R3,DXD_DISPLAY_END            END ADDRESS IN VDATA
         ITRACE ID=STRT_END,                                           +
               DATA1=DXD_SOURCE_START,                                 +
               DATA2=DXD_DISPLAY_START
         LR    R0,R1                         COPY START POSITION
         AR    R0,R2                         PLUS LENGTH
         BCTR  R0,0
         C     R0,DXD_RECORD_LENGTH          BEYOND END OF RECORD?
         BH    ERR0010                       YES
         CLC   FMAT_ROW,SPF_VDEPTH+2         BELOW BOTTOM OF SCREEN?
         BH    ERR0020                       YES
         L     R0,DXD_SOURCE_START
         LH    R1,FMAT_LENGTH
         L     R14,DXD_DISPLAY_START
         LR    R15,R1
         MVCL  R14,R0                        COPY SOURCE TO DISPLAY
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0010  DS    0H
         MVI   COMM_MSG_ID+1,1
         B     ERR1000
ERR0020  DS    0H
         MVI   COMM_MSG_ID+1,2
ERR1000  DS    0H
         MVC   COMM_MSG_CSECT,MODID
         L     R15,COMM_V_OSMSG
         BALR  R14,R15
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=EXIT
         LH    R15,DXD_RC                    SET RC
         L     R13,4(,R13)                   RESTORE R13
         L     R14,12(,R13)                  RESTORE R14
         LM    R0,R12,20(R13)                RESTORE REGISTERS
         BR    R14                           RETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXD_START      DC      Q(DXDFCHAR)

               LTORG
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDFCHAR             DSECT
                     COPY     DXDPREF

DXD_RC               DS   H

DXD_SOURCE_START     DS   A
DXD_SOURCE_END       DS   A
DXD_DISPLAY_START    DS   A
DXD_DISPLAY_END      DS   A
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               SESSION
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               BPXYSTAT DSECT=YES,LIST=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
FMT_COMMON     DSECT
               DS      (DXD_PREFIX_L)X
               COPY    FMTCOMM
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY    FMAT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               OSSPFD  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY    TRENTRY
* ------------------------------------------------------------------- *
*              EQUATES                                                *
* ------------------------------------------------------------------- *
               COPY    REGEQU
               END     FMTCHAR
