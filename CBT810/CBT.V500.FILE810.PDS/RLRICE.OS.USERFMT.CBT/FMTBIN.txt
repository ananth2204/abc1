* ------------------------------------------------------------------- *
*                                                                     *
*  MODULE NAME: FMTBIN                                                *
*                                                                     *
*  Process binary fields during 'format'                              *
*                                                                     *
*                                                                     *
*  If no "edit word" is specified, the number is displayed in vanilla *
*  99999 type format with leading zeros displayed, no plus or minus   *
*  sign.  1-byte fields are displayed as 3 digits, 2-byte fields are  *
*  displayed as 5 digits, 3-byte fields are displayed as 8 digits,    *
*  4-byte fields are displayed as 10 digits.                          *
*                                                                     *
*  Rather than use hardware edit instruction (ED), the editing is     *
*  done more 'manually'.  "Edit words" are much like "PICTURE"        *
*  values valid to COBOL.                                             *
*                                                                     *
* ------------------------------------------------------------------- *
FMTBIN   CSECT
FMTBIN   AMODE 31
FMTBIN   RMODE ANY
         USING OSCOMM,R12
         USING SESSION,R11
         USING FMTBIN,R15
         B     INIT0000
MODID    DC    CL8'FMTBIN'
         DC    CL8'&SYSDATE'
         DC    CL7'&SYSTIME'
INIT0000 DS    0H
         STM   R14,R12,12(R13)               SAVE REGS
         LR    R10,R15                       COPY ENTRY POINT
         DROP  R15
         USING FMTBIN,R10                    DEFINE BASE
         L     R9,SESS_DXD_ADDR              WORK AREA
         LR    R15,R9                        COPY WORK AREA ADDRESS
         USING FMT_COMMON,R9
         L     R15,SESS_DXD_ADDR             WORK AREA BASE
         A     R15,DXD_START                 PLUS DISP
         ST    R13,4(R15)                    SAVE R13
         ST    R15,8(R13)                    CHAIN SAVE AREA
         LR    R13,R15                       COPY WORK AREA ADDRESS
         USING DXDFPACK,R13                  DEFINE WORK AREA BASE
         MVC   DXD_CSECT,MODID
         L     R7,DXD_FMAT_FMAT              FMAT BEING PROCESSED
         USING FMAT,R7
         ITRACE ID=ENTRY,                                              +
               RDATA1=R7
         L     R8,COMM_OSSPFD
         USING OSSPFD,R8
* ------------------------------------------------------------------- *
*        Convert binary to packed decimal                             *
* ------------------------------------------------------------------- *
         LH    R1,FMAT_START                 STARTING COLUMN
         A     R1,DXD_RECORD_ADDR            PLUS RECORD ADDRESS
         BCTR  R1,0
         SR    R0,R0                         INITIALIZE
         CLI   FMAT_LENGTH+1,1               1 BYTE?
         BE    BIN0010                       YES
         CLI   FMAT_LENGTH+1,2               2 BYTES?
         BE    BIN0020                       YES
         CLI   FMAT_LENGTH+1,3               3 BYTES?
         BE    BIN0030                       YES
         ICM   R0,15,0(R1)                   INSERT 4 BYTES
         LA    R2,COMM_INFO_02+2             1ST BYTE TO DISPLAY
         LA    R3,11                         LENGTH TO DISPLAY
         B     BIN0040
BIN0010  DS    0H
         IC    R0,0(,R1)                     INSERT 1 BYTE
         LA    R2,COMM_INFO_02+9             1ST BYTE TO DISPLAY
         LA    R3,3                          LENGTH TO DISPLAY
         B     BIN0040
BIN0020  DS    0H
         ICM   R0,3,0(R1)                    INSERT 2 BYTES
         LA    R2,COMM_INFO_02+7             1ST BYTE TO DISPLAY
         LA    R3,5                          LENGTH TO DISPLAY
         B     BIN0040
BIN0030  DS    0H
         ICM   R0,7,0(R1)                    INSERT 3 BYTE
         LA    R2,COMM_INFO_02+4             1ST BYTE TO DISPLAY
         LA    R3,8                          LENGTH TO DISPLAY
BIN0040  DS    0H
         CVD   R0,COMM_DWORD                 CONVERT TO DECIMAL
         MVC   COMM_INFO_02(L'EDITWORD1),EDITWORD1
         ED    COMM_INFO_02(L'EDITWORD1),COMM_DWORD+2
         TM    FMAT_OPTIONS,$FMAT_EDITWORD   EDIT WORD SPECIFIED?
         BO    BIN0050                       YES
         ITRACE ID=NOWORD
         LH    R0,FMAT_ROW                   DISPLAY ROW
         LH    R1,FMAT_COLUMN                DISPLAY COLUMN
         BCTR  R0,0                          ROW RELATIVE TO ZERO
         MH    R0,SPF_VWIDTH+2               MULTIPLY BY LINE LENGTH
         BCTR  R1,0                          COLUMN RELATIVE TO ZERO
         AR    R0,R1                         PLUS COLUMN
         A     R0,DXD_VDATA_ADDR             PLUS VDATA ADDRESS
         B     BIN0060
BIN0050  DS    0H
         LA    R0,COMM_INFO_01               COPY TO COMM_INFO_01
BIN0060  DS    0H
         LR    R1,R3                         LENGTH
         MVCL  R0,R2                         COPY TO VDATA BUFFER
         TM    FMAT_OPTIONS,$FMAT_EDITWORD   EDIT WORD SPECIFIED?
         BNO   EXIT0000                      NO
         ITRACE ID=PRE_EDIT,                                           +
               DATA1=COMM_INFO_01+0,                                   +
               DATA2=COMM_INFO_01+8
         ITRACE ID=CALLEDIT
         L     R15,V_FMTEDIT                 FMTEDIT ENTRY POINT
         BALR  R14,R15                       CALL FMTEDIT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=EXIT
         LH    R15,DXD_RC                    SET RC
         L     R13,4(,R13)                   RESTORE R13
         L     R14,12(,R13)                  RESTORE R14
         LM    R0,R12,20(R13)                RESTORE REGISTERS
         BR    R14                           RETURN
* ------------------------------------------------------------------- *
*              CONSTANTS                                              *
* ------------------------------------------------------------------- *
DXD_START      DC      Q(DXDFPACK)
V_FMTEDIT      DC      V(FMTEDIT)

EDITWORD1      DC      X'F02020202020202020202020'

               LTORG
* ------------------------------------------------------------------- *
*              WORK AREAS                                             *
* ------------------------------------------------------------------- *
DXDFPACK             DSECT
                     COPY     DXDPREF

DXD_RC               DS   H

DXD_SIGN_ADDRESS     DS   A
* ------------------------------------------------------------------- *
*              COMMON AREA                                            *
* ------------------------------------------------------------------- *
               COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               SESSION
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               BPXYSTAT DSECT=YES,LIST=YES
* ------------------------------------------------------------------- *
*              FMT COMMON                                             *
* ------------------------------------------------------------------- *
FMT_COMMON     DSECT
               DS      (DXD_PREFIX_L)X
               COPY    FMTCOMM
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY    FMAT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               OSSPFD  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY    TRENTRY
* ------------------------------------------------------------------- *
*              EQUATES                                                *
* ------------------------------------------------------------------- *
               COPY    REGEQU
               END     FMTBIN
