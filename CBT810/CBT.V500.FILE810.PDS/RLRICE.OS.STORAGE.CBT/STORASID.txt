* ------------------------------------------------------------------- *
*                                                                     *
*  Module name: STORASID                                              *
*                                                                     *
*  Set current address to selected address space's ASID               *
*                                                                     *
* ------------------------------------------------------------------- *
STORASID CSECT
STORASID AMODE 31
STORASID RMODE ANY
         USING OSCOMM,R12
         USING SESSION,R11
         USING STORASID,R15
         B     INIT0000
MODID    DC    CL8'STORASID'
         DC    CL8'&SYSDATE'
         DC    CL7'&SYSTIME'
INIT0000 DS    0H
         STM   R14,R12,12(R13)                SAVE REGS
         LR    R10,R15                        COPY ENTRY POINT
         DROP  R15
         USING STORASID,R10                   DEFINE BASE
         USING STORCOMM,R9                    DEFINE BASE
         L     R15,SESS_DXD_ADDR              WORK AREA BASE
         A     R15,DXD_START                  PLUS DISP
         ST    R13,4(,R15)                    SAVE R13
         ST    R15,8(,R13)                    CHAIN SAVE AREA
         LR    R13,R15
         USING DXDASID,R13                    DEFINE WORK AREA BASE
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY
         L     R8,COMM_OSSPFD
         USING OSSPFD,R8
         MVC   STORCOMM_FUNCTION_ADDR,V_STORSTOR
ASID0000 DS    0H
         CLC   OPERAND_02_LENGTH,H4           CORRECT LENGTH
         BNE   ERR0010                        NO
         ICM   R1,15,OPERAND_02_ADDRESS       ASID ADDRESS
         LA    R2,4                           LENGTH
         BAL   R7,HEX0000                     VERIFY HEX
         ICM   R1,15,OPERAND_02_ADDRESS       OPERAND ADDRESS
         TR    0(4,R1),CHARHEX                PREPARE FOR PACKING
         PACK  COMM_DWORD(3),0(5,R1)          PACK SPECIFIED ASID
         ITRACE ID=NEW_ASID,                                           +
               DATA1=(COMM_DWORD,2)
         L     R1,CVTPTR                      CVT ADDRESS
         USING CVT,R1                         DEFINE BASE
         L     R1,CVTASVT                     ASVT ADDRESS
         USING ASVT,R1
         CLC   COMM_DWORD(2),ASVTMAXU+2       TOO HIGH?
         BH    ERR0020                        YES
         SR    R2,R2                          CLEAR REGISTER
         ICM   R2,3,COMM_DWORD                ASID
         SLL   R2,2                           MULTIPLY BY 4
         LA    R2,ASVTFRST(R2)                PLUS ADDR OF FIRST ENTRY
         ICM   R5,15,0(R2)                    ADDR OF ASCB
         BM    ERR0030                        NOT IN USE
         USING ASCB,R5
         ITRACE ID=ASCB,                                               +
               RDATA1=R5,                                              +
               DATA2=(ASCBASID,4)
         MVC   SPF_ZCMD,COMM_BLANKS
         ST    R5,STORCOMM_ASCB               SAVE ASCB ADDRESS
         ST    R5,STORCOMM_DATA_ADDR          SET DATA ADDRESS
         MVC   STORCOMM_ASID,ASCBASID         SAVE ASID
         ICM   R1,15,ASCBJBNI                 JOB NAME?
         BNZ   ASID0040                       NO
         ICM   R1,15,ASCBJBNS                 OTHER JOB NAME
ASID0040 DS    0H
         MVC   STORCOMM_SPACE_NAME,0(R1)      SAVE JOB NAME
         MVC   SPF_SPACE_NAME,0(R1)           SAVE JOB NAME
         UNPK  COMM_DWORD(5),STORCOMM_ASID(3)
         MVC   SPF_SPACE_ASID,COMM_DWORD
         TR    SPF_SPACE_ASID,COMM_HEXCHAR
         XC    STORCOMM_DATA_DISP,STORCOMM_DATA_DISP
         CLC   SESS_DISP_PANEL,STOR_PANEL
         BE    ASID0050
         MVC   STORCOMM_FUNCTION_ADDR,V_STORCB
         L     R1,V_ASCBTBL                   FORMATTING CONTROL INFO
         ST    R1,STORCOMM_CB_TABLE           SET ADDRESS
         MVC   STORCOMM_CB_NAME,0(R1)         COPY CONTROL BLOCK NAME
         MVC   STORCOMM_CB_DESCRIPTION,8(R1)  COPY CONTROL BLOCK DESC
         MVC   SESS_DISP_PANEL,48(R1)         COPY PANEL NAME
         MVC   STORCOMM_PANEL_NAME,48(R1)
         LA    R1,56(,R1)                     FIRST FIELD
         ST    R1,STORCOMM_CB_FIRST           SET ADDRESS
         B     EXIT0000
ASID0050 DS    0H
         MVC   STORCOMM_CB_NAME,COMM_BLANKS
         MVC   STORCOMM_CB_DESCRIPTION,COMM_BLANKS
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
*        VERIFY A CHARACTER STRING IS ALL VALID HEX CHARACTERS.       *
*                                                                     *
*        R1  ADDRESS OF CHARACTER STRING                              *
*        R2  LENGTH OF STRING                                         *
*        R7  RETURN ADDRESS                                           *
*                                                                     *
*        IF THE STRING IS NOT ALL HEX, CONTROL IS NOT RETURNED        *
*        TO THE CALLER.                                               *
*                                                                     *
* ------------------------------------------------------------------- *
HEX0000  DS    0H
         CLI   0(R1),C'A'                      VALID?
         BL    ERR0040                         NO
         CLI   0(R1),C'G'                      VALID?
         BL    HEX0010                         YES
         CLI   0(R1),C'0'                      VALID?
         BL    ERR0040                         NO
         CLI   0(R1),C'9'                      VALID?
         BH    ERR0040                         NO
HEX0010  DS    0H
         LA    R1,1(,R1)                       NEXT
         BCT   R2,HEX0000                      LOOP
         BR    R7                              RETURN.. ALL VALID
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0010  DS    0H
         MVC   SPF_MSG_1(MSG01_L),MSG01
         B     EXIT0000
ERR0020  DS    0H
         MVC   SPF_MSG_1(MSG02_L),MSG02
         B     EXIT0000
ERR0030  DS    0H
         MVC   SPF_MSG_1(MSG03_L),MSG03
         B     EXIT0000
ERR0040  DS    0H
         MVC   SPF_MSG_1(MSG04_L),MSG04
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=EXIT
         L     R13,4(,R13)                     RESTORE R13
         LM    R14,R12,12(R13)                 RESTORE REGISTERS
         SR    R15,R15                         SET RC
         BR    R14
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXD_START      DC       Q(DXDASID)
V_ASCBTBL      DC       V(ASCBTBL)
V_STORCB       DC       V(STORCB)
V_STORSTOR     DC       V(STORSTOR)

H4             DC       H'4'

CB_ASCB        DC       CL8'ASCB'
ASCB_PANEL     DC       CL8'STORASCB'
STOR_PANEL     DC       CL8'STORSTOR'

CHARHEX        DC       256C' '
               ORG      CHARHEX+C'A'
               DC       X'0A0B0C0D0E0F'
               ORG      CHARHEX+C'0'
               DC       X'00010203040506070809'
               ORG      CHARHEX+256

MSG01          DS       0C
               DC       C'STORASID01E '
               DC       C'ASID must be four characters'
MSG01_L        EQU      *-MSG01

MSG02          DS       0C
               DC       C'STORASID02E '
               DC       C'ASID too high'
MSG02_L        EQU      *-MSG02

MSG03          DS       0C
               DC       C'STORASID03E '
               DC       C'ASID is not active'
MSG03_L        EQU      *-MSG03

MSG04          DS       0C
               DC       C'STORASIDXXE '
               DC       C'Invalid hex digit'
MSG04_L        EQU      *-MSG04

               LTORG

* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDASID        DSECT
               COPY     DXDPREF
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               BPXYSTAT DSECT=YES,LIST=NO
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               STORCOMM
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               IBMMAC   CVT=YES
               IBMMAC   ASCB=YES
               IHAASVT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY     OPERAND
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY     FIELDD
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               OSSPFD   TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY     REGEQU
               END      STORASID
