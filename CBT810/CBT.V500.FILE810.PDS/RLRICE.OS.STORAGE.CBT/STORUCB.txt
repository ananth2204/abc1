* ------------------------------------------------------------------- *
*                                                                     *
*  Module name: STORUCB                                               *
*                                                                     *
*  Set current address to UCB                                         *
*                                                                     *
* ------------------------------------------------------------------- *
STORUCB  CSECT
STORUCB  AMODE 31
STORUCB  RMODE ANY
         USING OSCOMM,R12
         USING SESSION,R11
         USING STORUCB,R15
         B     INIT0000
MODID    DC    CL8'STORUCB'
         DC    CL8'&SYSDATE'
         DC    CL7'&SYSTIME'
INIT0000 DS    0H
         STM   R14,R12,12(R13)                 SAVE REGS
         LR    R10,R15                         COPY ENTRY POINT
         DROP  R15
         USING STORUCB,R10                     DEFINE BASE
         USING STORCOMM,R9                     DEFINE BASE
         L     R15,SESS_DXD_ADDR               WORK AREA BASE
         A     R15,DXD_START                   PLUS DISP
         ST    R13,4(,R15)                     SAVE R13
         ST    R15,8(,R13)                     CHAIN SAVE AREA
         LR    R13,R15
         USING DXDUCB,R13                      DEFINE WORK AREA BASE
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY
         L     R8,COMM_OSSPFD
         USING OSSPFD,R8
         TM    OPERAND_02_TYPE,$OPERAND_PRESENT
         BNO   UCB0010
         ICM   R1,15,OPERAND_02_ADDRESS       DEVICE NUMBER
         CLC   OPERAND_02_LENGTH,H3           THREE BYTE DEVICE NBR?
         BE    UCB0010                        YES
         CLC   OPERAND_02_LENGTH,H4           FOUR BYTE DEVICE NBR?
         BNE   ERR0020                        NO
         TR    0(4,R1),CHARHEX                PREPARE FOR PACKING
         PACK  COMM_DWORD(3),0(5,R1)          PACK DEVICE NBR
         B     UCB0020
UCB0010  DS    0H
         TR    0(3,R1),CHARHEX                PREPARE FOR PACKING
         PACK  COMM_DWORD(3),0(4,R1)          PACK DEVICE NBR
UCB0020  DS    0H
         MVC   DXD_DEVICE_NBR,COMM_DWORD      COPY DEVICE NBR
         XC    DXD_UCBSCAN_WORK,DXD_UCBSCAN_WORK
         OI    COMM_FLAGS,$COMM_AUTH_ON
         L     R15,COMM_V_OSAUTH
         BALR  R14,R15                        TURN ON APF AUTH
         MODESET MODE=SUP
         ITRACE ID=UCBSCAN,                                            +
               DATA1=(DXD_DEVICE_NBR,2)
         UCBSCAN ADDRESS,                     ISSUE UCBSCAN            +
               WORKAREA=DXD_UCBSCAN_WORK,     .. WORK AREA             +
               UCBPTR=DXD_UCBSCAN_UCB,        .. RETURNED UCB ADDRESS  +
               DEVN=DXD_DEVICE_NBR,           .. DEVICE NBR TO LOCATE  +
               NOPIN,                         .. DON'T PIN             +
               RANGE=ALL,                     .. 3 AND 4 DIGIT DEVICES +
               DYNAMIC=YES,                   .. INCLUDE DYNAMIC       +
               LOC=ANY,                       .. 24 OR 31 BIT          +
               MF=(E,DXD_UCBSCAN)
         LR    R2,R15                         SAVE UCBSCAN RC
         MODESET MODE=PROB
         NI    COMM_FLAGS,255-$COMM_AUTH_ON
         L     R15,COMM_V_OSAUTH
         BALR  R14,R15                        TURN OFF APF AUTH
         ITRACE ID=UCB_RC,                    TRACE UCBSCAN RC         +
               RDATA1=R2,                                              +
               DATA2=(DXD_UCBSCAN_UCB,4)
         LTR   R2,R2                          SUCCESSFUL?
         BNZ   ERR0030                        NO
         L     R3,DXD_UCBSCAN_UCB             UCB ADDRESS
         USING UCBOB,R3
         CLC   DXD_DEVICE_NBR,UCBCHAN         UCB WE WANTED?
         BNE   ERR0030                        NO
         MVC   SPF_ZCMD,COMM_BLANKS
         MVI   OPERAND_01_TYPE,0              FAKE OUT STORCB
         MVI   OPERAND_02_TYPE,0              DITTO
         MVC   SPF_ZCMD,COMM_BLANKS
         MVC   STORCOMM_DATA_ADDR,DXD_UCBSCAN_UCB
         XC    STORCOMM_DATA_DISP,STORCOMM_DATA_DISP
         CLC   SESS_DISP_PANEL,STOR_PANEL
         BE    UCB0080
         MVC   STORCOMM_FUNCTION_ADDR,V_STORCB
         L     R1,V_UCBTBL                    FORMATTING INFO TABLE
         ST    R1,STORCOMM_CB_TABLE           SET ADRESS
         MVC   STORCOMM_CB_NAME,0(R1)         COPY CONTROL BLOCK NAME
         MVC   STORCOMM_CB_DESCRIPTION,8(R1)  COPY CONTROL BLOCK DESC
         MVC   SESS_DISP_PANEL,48(R1)         COPY PANEL NAME
         MVC   STORCOMM_PANEL_NAME,48(R1)     COPY PANEL NAME
         LA    R1,56(,R1)                     FIRST FIELD
         ST    R1,STORCOMM_CB_FIRST           SET ADDRESS
         B     EXIT0000
UCB0080  DS    0H
         MVC   STORCOMM_FUNCTION_ADDR,V_STORSTOR
         MVC   STORCOMM_CB_NAME,COMM_BLANKS
         MVC   STORCOMM_CB_DESCRIPTION,COMM_BLANKS
         XC    STORCOMM_CB_TABLE,STORCOMM_CB_TABLE
         XC    STORCOMM_CB_FIRST,STORCOMM_CB_FIRST
         XC    STORCOMM_CB_LAST,STORCOMM_CB_LAST
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
*        VERIFY A CHARACTER STRING IS ALL VALID HEX CHARACTERS.       *
*                                                                     *
*        R1  ADDRESS OF CHARACTER STRING                              *
*        R2  LENGTH OF STRING                                         *
*        R7  RETURN ADDRESS                                           *
*                                                                     *
*        IF THE STRING IS NOT ALL HEX, CONTROL IS NOT RETURNED        *
*        TO THE CALLER.                                               *
*                                                                     *
* ------------------------------------------------------------------- *
HEX0000  DS    0H
         CLI   0(R1),C'A'                      VALID?
         BL    ERR0030                         NO
         CLI   0(R1),C'G'                      VALID?
         BL    HEX0010                         YES
         CLI   0(R1),C'0'                      VALID?
         BL    ERR0030                         NO
         CLI   0(R1),C'9'                      VALID?
         BH    ERR0030                         NO
HEX0010  DS    0H
         LA    R1,1(,R1)                       NEXT
         BCT   R2,HEX0000                      LOOP
         BR    R7                              RETURN.. ALL VALID
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0010  DS    0H
         MVC   SPF_MSG_1(MSG01_L),MSG01
         B     EXIT0000
ERR0020  DS    0H
         MVC   SPF_MSG_1(MSG02_L),MSG02
         B     EXIT0000
ERR0030  DS    0H
         MVC   SPF_MSG_1(MSG03_L),MSG03
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=EXIT
         L     R13,4(,R13)                     RESTORE R13
         LM    R14,R12,12(R13)                 RESTORE REGISTERS
         SR    R15,R15                         SET RC
         BR    R14
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXD_START      DC       Q(DXDUCB)
V_STORCB       DC       V(STORCB)
V_STORSTOR     DC       V(STORSTOR)
V_UCBTBL       DC       V(UCBTBL)

H3             DC       H'3'
H4             DC       H'4'

STOR_PANEL     DC       CL8'STORSTOR'

CHARHEX        DC       256C' '
               ORG      CHARHEX+C'A'
               DC       X'0A0B0C0D0E0F'
               ORG      CHARHEX+C'0'
               DC       X'00010203040506070809'
               ORG      CHARHEX+256

MSG01          DS       0C
               DC       C'STORUCB01E '
               DC       C'UCB address is required'
MSG01_L        EQU      *-MSG01

MSG02          DS       0C
               DC       C'STORUCB02E '
               DC       C'Unit number must be 3 or 4 hex digits'
MSG02_L        EQU      *-MSG02

MSG03          DS       0C
               DC       C'STORUCB03E '
               DC       C'Device not found' '
MSG03_L        EQU      *-MSG03

               LTORG

* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDUCB           DSECT
                 COPY     DXDPREF
DXD_UCBSCAN      UCBSCAN  MF=(L,UCBSCAN_PARM)

DXD_UCBSCAN_WORK DS       XL100
DXD_UCBSCAN_UCB  DS       XL4
DXD_DEVICE_NBR   DS       XL2
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               IEFUCBOB
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY     FIELDD
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               BPXYSTAT DSECT=YES,LIST=NO
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               STORCOMM
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY     OPERAND
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               OSSPFD   TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY     REGEQU
               END      STORUCB
