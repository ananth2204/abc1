* ------------------------------------------------------------------- *
*                                                                     *
*        Process the RBTMFLD flags                                    *
*                                                                     *
* ------------------------------------------------------------------- *
RBEX1    CSECT
RBEX1    AMODE 31
RBEX1    RMODE ANY
         USING OSCOMM,R12
         USING SESSION,R11
         USING RBEX1,R15
         B     INIT0000
MODID    DC    CL8'RBEX1'
         DC    CL8'&SYSDATE'
         DC    CL7'&SYSTIME'
INIT0000 DS    0H
         STM   R14,R12,12(R13)                SAVE REGS
         LR    R10,R15                        COPY ENTRY POINT
         DROP  R15
         USING RBEX1,R10                      DEFINE BASE
         USING STORCOMM,R9
         L     R15,SESS_DXD_ADDR              WORK AREA BASE
         A     R15,DXD_START                  PLUS DISP
         ST    R13,4(,R15)                    SAVE R13
         ST    R15,8(,R13)                    CHAIN SAVE AREA
         LR    R13,R15
         USING DXDRB1,R13                     DEFINE WORK AREA BASE
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY
         CLI   STORCOMM_FUNCTION,$STORCOMM_COMMAND
         BE    EXIT0000
         L     R5,STORCOMM_DATA_ADDR          RB ADDRESS
         USING RBBASIC,R5
         L     R3,FIELD_EXIT_PNS              CURRENT POINT-AND-SHOOT
         L     R6,FIELD_EXIT_VDATA
         LH    R7,FIELD_EXIT_LINES
         MVI   STORCOMM_STATUS,$STORCOMM_OK   SET DEFAULT STATUS
         L     R8,COMM_OSSPFD
         USING OSSPFD,R8

         ITRACE ID=ZERO
         OI    COMM_FLAGS,$COMM_AUTH_ON       REQUEST APF AUTHORIZATION
         L     R15,COMM_V_OSAUTH
         BALR  R14,R15                        CALL OSAUTH
         MODESET KEY=ZERO                     ENTER KEY ZERO
         NI    COMM_FLAGS,255-$COMM_AUTH_ON   REQUEST APF OFF
         L     R15,COMM_V_OSAUTH
         BALR  R14,R15                        CALL OSAUTH
         LAM   R5,R5,F1                       SET AR
         SAC   512                            ENTER AR MODE
         TM    RBTMFLD,RBTMQUE                FLAG ON?
         BNO   MAIN0010                       NO
         MVC   0(MSG01_L,R6),MSG01
V        USING MSG01,R6
         MVI   V.MSG01_ATTR,$SCREEN_ATTR_HIGH_YELLOW
         MVC   V.MSG01_DESC,MSG02
         BAL   R1,LINE0000
MAIN0010 DS    0H
         TM    RBTMFLD,RBTMTOD                FLAG ON?
         BNO   MAIN0020                       NO
         MVC   0(MSG01_L,R6),MSG01
         MVC   V.MSG01_DESC,MSG03
         BAL   R1,LINE0000
MAIN0020 DS    0H
         TM    RBTMFLD,RBWLIM                 FLAG ON?
         BNO   MAIN0030                       NO
         MVC   0(MSG01_L,R6),MSG01
         MVC   V.MSG01_DESC,MSG04
         BAL   R1,LINE0000
MAIN0030 DS    0H
         TM    RBTMFLD,RBTMCMP                FLAG ON?
         BNO   MAIN0040                       NO
         MVC   0(MSG01_L,R6),MSG01
         MVC   V.MSG01_DESC,MSG05
         BAL   R1,LINE0000
MAIN0040 DS    0H
         TM    RBTMFLD,RBTMIND2               FLAG ON?
         BNO   MAIN0050                       NO
         MVC   0(MSG01_L,R6),MSG01
         MVC   V.MSG01_DESC,MSG06
         BAL   R1,LINE0000
MAIN0050 DS    0H
         MVC   0(MSG01_L,R6),MSG01
         TM    RBTMFLD,RBRREQ                 REAL REQUEST?
         BO    MAIN0060                       YES
         TM    RBTMFLD,RBTREQ                 TASK REQUEST?
         BO    MAIN0070                       YES
         TM    RBTMFLD,RBTREQ                 WAIT REQUEST?
         BO    MAIN0070                       YES
         B     EXIT0000
MAIN0060 DS    0H
         MVC   V.MSG01_DESC,MSG07             REAL
         B     MAIN0090
MAIN0070 DS    0H
         MVC   V.MSG01_DESC,MSG08             TASK
         B     MAIN0090
MAIN0080 DS    0H
         MVC   V.MSG01_DESC,MSG09             WAIT
MAIN0090 DS    0H
         BAL   R1,LINE0000
         B     EXIT0000
* ------------------------------------------------------------------- *
*        Next line on display                                         *
* ------------------------------------------------------------------- *
LINE0000 DS    0H
         LA    R3,PNS_L(,R3)                  NEXT IN PNS TABLE
         A     R6,SPF_VWIDTH                  NEXT LINE
         BCT   R7,LINE0010                    MINUS 1 AVAILABLE LINE
         B     EXIT0000                       SCREEN FULL.. EXIT
LINE0010 DS    0H
         BR    R1
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         SAC   0
         LAM   R5,R5,F0                       RESET AR
         OI    COMM_FLAGS,$COMM_AUTH_ON       REQUEST APF AUTHORIZATION
         L     R15,COMM_V_OSAUTH
         BALR  R14,R15                        CALL OSAUTH
         ITRACE ID=NZERO
         MODESET KEY=NZERO                    RESET STORAGE PROTECT KEY
         NI    COMM_FLAGS,255-$COMM_AUTH_ON   REQUEST APF OFF
         L     R15,COMM_V_OSAUTH
         BALR  R14,R15                        CALL OSAUTH
         ITRACE ID=EXIT
         ST    R3,FIELD_EXIT_PNS
         ST    R6,FIELD_EXIT_VDATA
         STH   R7,FIELD_EXIT_LINES
         L     R13,4(,R13)                    RESTORE R13
         LM    R14,R12,12(R13)                RESTORE REGISTERS
         SR    R15,R15
         BR    R14                            RETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXD_START    DC     Q(DXDRB1)

F0           DC     F'0'
F1           DC     F'1'

MSG01        DS     0C
             DC     AL1($SCREEN_ATTR_HIGH_TURQUOISE)
MSG01_DISP   DC     CL04' '
             DC     AL01($SCREEN_ATTR_HIGH)
MSG01_NAME   DC     CL08' '
             DC     AL01($SCREEN_ATTR_NORMAL)
MSG01_DATA   DC     CL20' '
             DC     CL04' '
MSG01_ATTR   DC     AL01($SCREEN_ATTR_HIGH_GREEN)
MSG01_DESC   DC     CL40' '
MSG01_L      EQU    *-MSG01

MSG02        DC     CL40'Timer element not on queue'
MSG03        DC     CL40'Local time of day option'
MSG04        DC     CL40'WAIT limit exceeded'
MSG05        DC     CL40'Interval has expired'
MSG06        DC     CL40'Exit specified with TASK or REAL'
MSG07        DC     CL40'REAL'
MSG08        DC     CL40'TASK'
MSG09        DC     CL40'WAIT'
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDRB1       DSECT
             COPY     DXDPREF
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
             COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
             SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
             IHARB    DSECT=YES,LIST=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
             BPXYSTAT DSECT=YES,LIST=NO
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
             STORCOMM
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
             COPY     PNS
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
             OSSPFD   TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
             COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
             COPY     ATTRS
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
             COPY     REGEQU
             END      RBEX1
