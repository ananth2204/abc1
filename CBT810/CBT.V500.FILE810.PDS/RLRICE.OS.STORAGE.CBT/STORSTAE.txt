* ------------------------------------------------------------------- *
*                                                                     *
*        ESTAE exit                                                   *
*                                                                     *
*                                                                     *
*     Parm list needed on ESTAEX                                      *
*         1    list of addresses where abends are allowed to occur    *
*         2    OSSPFD address                                         *
*         3    STORCOMM address                                       *
*         4    ADDRESS of CSECT that issued the ESTAEX                *
*         5    SESSION block address                                  *
*         6    OSCOMM block address                                   *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
STORSTAE CSECT
STORSTAE AMODE 31
STORSTAE RMODE ANY
         USING STORSTAE,R15
         B     STAE0000
MODID    DC    CL8'STORSTAE'
         DC    CL8'&SYSDATE'
         DC    CL8'&SYSTIME'
         DC    A(ESTAEEND-STORSTAE)
STAE0000 DS    0H
         CH    R0,H12                         SDWA PRESENT?
         BE    STAE0030                       NO
         STM   R14,R12,12(R13)                SAVE REGISTERS
         LR    R6,R15                         COPY BASE ADDRESS
         DROP  R15
         USING STORSTAE,R6                    DEFINE BASE
         SAC   0                              MAKE SURE AR MODE IS OFF
         LR    R5,R1                          COPY SDWA ADDRESS
         USING SDWA,R5                        DEFINE BASE
         L     R1,SDWAPARM                    PARM LIST ADDRESS
         L     R2,0(R1)
         LM    R7,R12,0(R2)
         USING OSCOMM,R12                     DEFINE COMM BASE
         USING SESSION,R11                    DEFINE SESSION BASE
         USING STORCOMM,R9                    DEFINE STORCOMM BASE
         L     R15,COMM_DXD                   WORK AREA BASE
         A     R15,DXD_START                  PLUS DISP TO OUR SECTION
         ST    R13,4(,R15)                    SAVE CALLER'S REG 13
         ST    R15,8(,R13)                    CHAIN TO CALLER
         LR    R13,R15
         USING WKESTAE,R13
         MVC   DXD_CSECT,MODID
         ITRACE ID=ABEND,                                              +
               DATA1=(SDWANXT1,4)
         L     R3,SDWANXT1                    FAILING ADDRESS
         LA    R3,0(,R3)                      CLEAR AMODE BIT
STAE0010 DS    0H
         C     R3,0(,R7)                      ALLOWED AT THIS ADDR?
         BE    STAE0020                       YES
         CLI   0(R7),X'FF'                    END OF TABLE?
         BE    STAE0030                       YES
         LA    R7,4(,R7)                      NEXT IN ADDRESS LIST
         B     STAE0010
STAE0020 DS    0H
         L     R4,SESS_RETRY                  RETRY POINT
         ITRACE ID=RETRY,                                              +
               RDATA1=R4
         LR    R1,R5                          RESTORE R1
         SETRP RETADDR=(R4),                  SET RECOVERY PARAMETERS  +
               FRESDWA=YES,                   .. FREE THE SDWA         +
               DUMP=NO,                       .. DON'T CREATE A DUMP   +
               RC=4                           .. RETRY
         L     R13,4(,R13)                    RESTORE R13
         LM    R14,R12,12(R13)                RESTORE REGISTERS
         BR    R14
STAE0030 DS    0H
         LR    R1,R5                          RESTORE R1
         SETRP RC=0                           DON'T TRY TO RECOVER
         BR    R14
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXD_START DC    Q(WKESTAE)
H12       DC    H'12'
          LTORG
ESTAEEND  EQU   *
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COMMON TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         STORCOMM
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         BPXYSTAT DSECT=YES,LIST=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY   TRENTRY
* ------------------------------------------------------------------- *
WKESTAE  DSECT
         COPY   DXDPREF
* ------------------------------------------------------------------- *
         IBMMAC SDWA=YES
* ------------------------------------------------------------------- *
         COPY   REGEQU
         END    STORSTAE
