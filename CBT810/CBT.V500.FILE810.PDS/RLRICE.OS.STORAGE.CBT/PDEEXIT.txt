* ------------------------------------------------------------------- *
*                                                                     *
*        Process PDE elements of the IPA (initialization parameters)  *
*                                                                     *
* ------------------------------------------------------------------- *
PDEEXIT  CSECT
PDEEXIT  AMODE 31
PDEEXIT  RMODE ANY
         USING OSCOMM,R12
         USING SESSION,R11
         USING PDEEXIT,R15
         B     INIT0000
MODID    DC    CL8'PDEEXIT'
         DC    CL8'&SYSDATE'
         DC    CL7'&SYSTIME'
INIT0000 DS    0H
         STM   R14,R12,12(R13)                SAVE REGS
         LR    R10,R15                        COPY ENTRY POINT
         DROP  R15
         USING PDEEXIT,R10                    DEFINE BASE
         USING STORCOMM,R9
         L     R15,SESS_DXD_ADDR              WORK AREA BASE
         A     R15,DXD_START                  PLUS DISP
         ST    R13,4(,R15)                    SAVE R13
         ST    R15,8(,R13)                    CHAIN SAVE AREA
         LR    R13,R15
         USING DXDPDE,R13                     DEFINE WORK AREA BASE
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY
         L     R8,COMM_OSSPFD
         USING OSSPFD,R8
         MVI   STORCOMM_STATUS,$STORCOMM_OK   SET DEFAULT STATUS
         CLI   STORCOMM_FUNCTION,$STORCOMM_COMMAND
         BE    EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         L     R4,FIELD_EXIT_FIELD
         USING FIELD_DSECT,R4
         SR    R5,R5
         ICM   R5,3,FIELD_DISP                DISP TO FIELD
         A     R5,STORCOMM_DATA_ADDR          PLUS BASE ADDRESS
         USING IPAPDE,R5
         SR    R2,R2
         ICM   R2,3,IPAPDESL                  DATA LENGTH
         BZ    EXIT0000                       NO DATA
         L     R6,FIELD_EXIT_VDATA            CURRENT VDATA BUFFER ADDR
         MVC   0(MSG01_L,R6),MSG01            INITIALIZE MESSAGE
V        USING MSG01,R6
         MVC   V.MSG01_NAME,FIELD_DESC        COPY FIELD DESCRIPTION
         ICM   R3,15,IPAPDESA                 PARAMETER'S ADDRESS
         CH    R2,=Y(L'MSG01_DATA)            TOO LONG?
         BNH   MAIN0010                       NO
         LH    R2,=Y(L'MSG01_DATA)            LIMIT LENGTH
MAIN0010 DS    0H
         BCTR  R2,0
         EX    R2,DATA_MVC                    COPY DATA
         CLI   IPAPDEDO+1,0                   DEFAULT?
         BE    MAIN0020                       YES
         CLI   IPAPDEDO+1,X'FF'               OPERATOR SUPPLIED?
         BE    MAIN0030                       YES
         MVC   V.MSG01_SUFFIX,IPAPDESS        COPY SUFFIX
         B     MAIN0040
MAIN0020 DS    0H
         MVC   V.MSG01_SOURCE,DEFAULT
         B     MAIN0040
MAIN0030 DS    0H
         MVC   V.MSG01_SOURCE,OPERATOR
MAIN0040 DS    0H
         A     R6,SPF_VWIDTH                  NEXT LINE
         ST    R6,FIELD_EXIT_VDATA            UPDATE VDATA ADDRESS
         LH    R1,FIELD_EXIT_LINES            NBR OF LINES AVAILABLE
         BCTR  R1,R0                          MINUS 1 LINE
         STH   R1,FIELD_EXIT_LINES            UPDATE LINES REMAINING
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=EXIT
         L     R13,4(,R13)                    RESTORE R13
         LM    R14,R12,12(R13)                RESTORE REGISTERS
         SR    R15,R15
         BR    R14                            RETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DATA_MVC MVC   V.MSG01_DATA(0),0(R3)          COPY DATA
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXD_START    DC     Q(DXDPDE)

DEFAULT      DC     CL8'Default'
OPERATOR     DC     CL8'Operator'

MSG01        DS     0C
             DC     AL1($SCREEN_ATTR_HIGH)
MSG01_NAME   DC     CL08' '
             DC     AL01($SCREEN_ATTR_HIGH_YELLOW)
MSG01_SOURCE DC     CL08' '
             DC     CL04' '
             DC     AL01($SCREEN_ATTR_HIGH_GREEN)
MSG01_SUFFIX DC     CL02' '
             DC     AL01($SCREEN_ATTR_HIGH_TURQUOISE)
MSG01_DATA   DC     CL40' '
MSG01_L      EQU    *-MSG01
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDPDE       DSECT
             COPY     DXDPREF
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
             COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
             SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
             IHAIPA   DSECT=YES,LIST=YES,IPAPDE=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
             BPXYSTAT DSECT=YES,LIST=NO
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
             STORCOMM
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
             COPY     FIELDD
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
             OSSPFD   TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
             COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
             COPY     ATTRS
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
             COPY     REGEQU
             END      PDEEXIT
