* ------------------------------------------------------------------- *
*                                                                     *
*  Module name: STORTASK                                              *
*                                                                     *
*  Display the TCBs and their status in the current address space     *
*                                                                     *
* ------------------------------------------------------------------- *
STORTASK CSECT
STORTASK AMODE 31
STORTASK RMODE ANY
         USING OSCOMM,R12
         USING SESSION,R11
         USING STORCOMM,R9
         USING STORTASK,R15
         B     INIT0000
MODID    DC    CL8'STORTASK'
         DC    CL8'&SYSDATE'
         DC    CL7'&SYSTIME'
INIT0000 DS    0H
         STM   R14,R12,12(R13)                  SAVE REGS
         LR    R10,R15                          COPY ENTRY POINT
         DROP  R15
         USING STORTASK,R10                     DEFINE BASE
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
INIT0010 DS    0H
         L     R15,SESS_DXD_ADDR
         A     R15,DXD_START
         ST    R13,4(,R15)
         ST    R15,8(,R13)
         LR    R13,R15
         USING DXDTASK,R13
         MVC   DXD_CSECT,MODID
         L     R8,COMM_OSSPFD                 SPF DATA AREA
         USING OSSPFD,R8
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0000 DS    0H
         ITRACE ID=MAIN0000
         CLI   STORCOMM_FUNCTION,$STORCOMM_FORMAT
         BE    MAIN0010                       YES
         CLI   STORCOMM_FUNCTION,$STORCOMM_COMMAND
         BE    MAIN0200                       YES
         CLI   STORCOMM_FUNCTION,$STORCOMM_CLEANUP
         BE    MAIN0400                       YES
         DC    H'0'
* ------------------------------------------------------------------- *
*        Build list of TCBs                                           *
* ------------------------------------------------------------------- *
MAIN0010 DS    0H
         ITRACE ID=FORMAT
         MVC   SPF_ZCMD,COMM_BLANKS
         ZAP   DXD_LEVEL,P1
         XC    DXD_CHAIN,DXD_CHAIN
         MVC   SESS_DISP_PANEL,PANEL_NAME

         L     R0,STORCOMM_VDATA_ADDR
         L     R1,STORCOMM_VDATA_LENGTH
         SR    R14,R14
         SR    R15,R15
         MVCL  R0,R14                         INITIALIZE VDATA BUFFER

         L     R0,STORCOMM_PNS_TABLE_ADDR
         ST    R0,DXD_CURRENT_PNS
         L     R1,STORCOMM_PNS_TABLE_LENGTH
         SR    R14,R14
         SR    R15,R15
         MVCL  R0,R14                         INITIALIZE PNS TABLE

         L     R5,STORCOMM_ASCB               ASCB'S ADDRESS
         USING ASCB,R5
         L     R5,ASCBRCTP                    RCT'S TCB ADDRESS
         USING TCB,R5
         ITRACE ID=ZERO
         OI    COMM_FLAGS,$COMM_AUTH_ON       REQUEST APF AUTHORIZATION
         L     R15,COMM_V_OSAUTH
         BALR  R14,R15                        CALL OSAUTH
         MODESET KEY=ZERO                     ENTER KEY ZERO
         NI    COMM_FLAGS,255-$COMM_AUTH_ON   REQUEST APF OFF
         L     R15,COMM_V_OSAUTH
         BALR  R14,R15                        CALL OSAUTH
         LH    R1,STORCOMM_ASID
         ITRACE ID=SSAR,                                               +
               RDATA1=R1
         SSAR  (R1)
         LAM   R5,R5,F1                       SET AR
         SAC   512                            AR MODE ON
         L     R6,STORCOMM_VDATA_ADDR         VDATA ADDRESS
         L     R7,STORCOMM_VDEPTH             SCREEN DEPTH
MAIN0020 DS    0H
         L     R1,DXD_CURRENT_PNS             CURRENT POINT AND SHOOT
         ST    R5,0(,R1)                      SAVE ADDRESS OF TCB
         LA    R1,4(,R1)                      NEXT POINT AND SHOOT
         ST    R1,DXD_CURRENT_PNS             UPDATE ADDRESS
         MVC   0(MSG01_L,R6),MSG01
V        USING MSG01,R6
         ED    V.MSG01_LEVEL,DXD_LEVEL        EDIT TASK LEVEL
         ST    R5,COMM_DWORD
         UNPK  V.MSG01_TCB(9),COMM_DWORD(5)   UNPACK TCB ADDRESS
         TR    V.MSG01_TCB,COMM_HEXCHAR       TRANSLATE
         MVI   V.MSG01_ATTR3,$SCREEN_ATTR_HIGH_GREEN
         UNPK  V.MSG01_CC(9),TCBCMP(5)        UNPACK COMPLETION CODE
         TR    V.MSG01_CC,COMM_HEXCHAR
         SR    R0,R0
         ICM   R0,7,TCBCMPC                   ALL ZERO?
         BZ    MAIN0030                       YES
         MVI   V.MSG01_ATTR3,$SCREEN_ATTR_HIGH_YELLOW
         TM    TCBCMPF,TCBCSTEP               ABEND?
         BNO   MAIN0030                       NO
         MVI   V.MSG01_ATTR3,$SCREEN_ATTR_HIGH_RED
MAIN0030 DS    0H
         MVI   V.MSG01_ATTR4,$SCREEN_ATTR_NORMAL
         ICM   R4,15,TCBRBP                   RB ADDRESS
         BZ    MAIN0040                       ZERO..
         LAM   R4,R4,F1                       SET AR
         USING RBBASIC,R4
         SR    R3,R3
         ICM   R3,7,RBCDE1                    CDE ADDRESS
         LAM   R3,R3,F1                       SET AR
         USING CDENTRY,R3
         MVC   V.MSG01_PROGRAM,CDNAME         COPY PROGRAM NAME
MAIN0040 DS    0H
         LAM   R3,R3,F0                       RESET AR
         LAM   R4,R4,F0                       RESET AR
         DROP  R3,R4
         LA    R4,STATUS00
         USING STATUS_DSECT,R4
MAIN0050 DS    0H
         EX    R0,STATUS_TM                   BIT ON?
         BO    MAIN0060                       YES
         LA    R4,STATUS_DSECT_L(,R4)         NEXT STATUS
         CLI   0(R4),X'FF'                    END OF TABLE?
         BE    MAIN0070                       YES
         B     MAIN0050
MAIN0060 DS    0H
         EX    R0,STATUS_MVI                  SET ATTRIBUTE
         EX    R0,STATUS_MVC                  COPY STATUS
         B     MAIN0070
MAIN0070 DS    0H
         A     R6,STORCOMM_VWIDTH             NEXT LINE
         BCT   R7,MAIN0080
         B     EXIT0000                       SCREEN IS FULL
MAIN0080 DS    0H
         ICM   R2,15,TCBLTC                   THIS TASK HAVE A SUBTASK?
         BZ    MAIN0090                       NO
         AP    DXD_LEVEL,P1                   ADD TO LEVEL
         STORAGE OBTAIN,                      GETMAIN CHAIN STORAGE    +
               LENGTH=8,                                               +
               LOC=31,                                                 +
               LINKAGE=SYSTEM
         MVC   0(4,R1),DXD_CHAIN              PREVIOUS BLOCK'S ADDRESS
         ST    R5,4(,R1)                      SAVE TCB ADDRESS
         ST    R1,DXD_CHAIN                   ADD NEW BLOCK TO CHAIN
         LR    R5,R2                          COPY SUBTASK TCB ADDR
         B     MAIN0020                       FORMAT DATA
MAIN0090 DS    0H
         ICM   R5,15,TCBNTC                   SISTER TASK?
         BNZ   MAIN0020                       YES.. FORMAT IT
         ICM   R1,15,DXD_CHAIN                ANYTHING ON CHAIN?
         BZ    EXIT0000                       NO.. ALL DONE
         MVC   DXD_CHAIN,0(R1)                PREVIOUS BLOCK'S ADDR
         L     R5,4(,R1)                      RESTORE TCB ADDRESS
         STORAGE RELEASE,                     FREE CHAIN BLOCK         +
               ADDR=(1),                                               +
               LENGTH=8,                                               +
               LINKAGE=SYSTEM
         SP    DXD_LEVEL,P1                   SUBTRACT FROM LEVEL
         B     MAIN0090
* ------------------------------------------------------------------- *
*        Check for line command(s)                                    *
* ------------------------------------------------------------------- *
MAIN0200 DS    0H
         ITRACE ID=CMDS
         L     R6,STORCOMM_VDATA_ADDR         VDATA BUFFER
         L     R7,STORCOMM_VDEPTH             NBR OF LINES
         L     R4,STORCOMM_PNS_TABLE_ADDR     FIRST IN POINT-AND-SHOOT
MAIN0210 DS    0H
         ITRACE ID=LINE_CMD,                                           +
               DATA1=(V.MSG01_SELECTION,1),                            +
               DATA2=V.MSG01_TCB
         OI    V.MSG01_SELECTION,C' '
         CLI   V.MSG01_SELECTION,C' '         BLANK?
         BNE   MAIN0220                       NO
         LA    R4,4(,R4)                      NEXT IN POINT AND SHOOT
         A     R6,STORCOMM_VWIDTH             NEXT LINE
         BCT   R7,MAIN0210                    LOOP
         B     EXIT0000                       NOTHING SELECTED
MAIN0220 DS    0H
         ITRACE ID=SELECTED,                                           +
               DATA1=(0(R4),4)
         MVC   STORCOMM_DATA_ADDR,0(R4)       COPY TCB ADDRESS
         XC    STORCOMM_DATA_DISP,STORCOMM_DATA_DISP
         MVC   STORCOMM_FUNCTION_ADDR,V_STORCB
         L     R1,V_TCBTBL                    TCB FORMAT INFO
         ST    R1,STORCOMM_CB_TABLE           SET TABLE ADDRESS
         MVC   STORCOMM_CB_NAME,0(R1)         COPY CONTROL BLOCK NAME
         MVC   STORCOMM_CB_DESCRIPTION,8(R1)  COPY CONTROL BLOCK DESC
         MVC   SESS_DISP_PANEL,48(R1)         COPY PANEL NAME
         MVC   STORCOMM_PANEL_NAME,48(R1)     COPY PANEL NAME
         LA    R1,56(,R1)                     FIRST FIELD
         ST    R1,STORCOMM_CB_FIRST           SET ADDRESS
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0400 DS    0H
         ITRACE ID=CLEAN_UP
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         SAC   0
         LAM   R5,R5,F0
         LAM   R4,R4,F0
         LAM   R3,R3,F0
         EPAR  (R1)
         ITRACE ID=RESET,                                              +
               RDATA1=R1
         SSAR  (R1)
         OI    COMM_FLAGS,$COMM_AUTH_ON       REQUEST APF AUTHORIZATION
         L     R15,COMM_V_OSAUTH
         BALR  R14,R15                        CALL OSAUTH
         ITRACE ID=NZERO
         MODESET KEY=NZERO                    ENTER KEY ZERO
         NI    COMM_FLAGS,255-$COMM_AUTH_ON   REQUEST APF OFF
         L     R15,COMM_V_OSAUTH
         BALR  R14,R15                        CALL OSAUTH
         ITRACE ID=EXIT
         LH    R15,DXD_RC                     RETURN CODE
         L     R13,4(,R13)                    RESTORE R13
         L     R14,12(,R13)                   RESTORE R14
         LM    R0,R12,20(R13)                 RESTORE REGISTERS
         BR    R14                            RETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
STATUS00 DS    0H
         TM    TCBFLGS2,TCBFOINP              TASK IS ABENDING
         MVI   V.MSG01_ATTR5,$SCREEN_ATTR_HIGH_RED
         MVC   V.MSG01_STATUS(STATUS01_L),STATUS01

         TM    TCBFLGS2,TCBFSMC               MUST COMPLETE
         MVI   V.MSG01_ATTR5,$SCREEN_ATTR_HIGH_RED
         MVC   V.MSG01_STATUS(STATUS02_L),STATUS02

         TM    TCBFLGS2,TCBFJMC               MUST COMPLETE
         MVI   V.MSG01_ATTR5,$SCREEN_ATTR_HIGH_RED
         MVC   V.MSG01_STATUS(STATUS02_L),STATUS02

         TM    TCBXSCT1,TCBACTIV              ACTIVE ON A CPU
         MVI   V.MSG01_ATTR5,$SCREEN_ATTR_HIGH_GREEN
         MVC   V.MSG01_STATUS(STATUS03_L),STATUS03

         TM    TCBFLGS4,TCBRQENA              I/O RQE's exhausted
         MVI   V.MSG01_ATTR5,$SCREEN_ATTR_HIGH_GREEN
         MVC   V.MSG01_STATUS(STATUS04_L),STATUS04

         TM    TCBFLGS4,TCBHNDSP              FROZEN
         MVI   V.MSG01_ATTR5,$SCREEN_ATTR_HIGH_YELLOW
         MVC   V.MSG01_STATUS(STATUS05_L),STATUS05

         TM    TCBFLGS5,TCBUXNDV              USER EXIT EXECUTING
         MVI   V.MSG01_ATTR5,$SCREEN_ATTR_HIGH_YELLOW
         MVC   V.MSG01_STATUS(STATUS06_L),STATUS06

         TM    TCBFLGS4,TCBRBWF               TOP RB IN WAIT
         MVI   V.MSG01_ATTR5,$SCREEN_ATTR_HIGH_YELLOW
         MVC   V.MSG01_STATUS(STATUS07_L),STATUS07

         TM    TCBFLGS5,TCBABWF               ABNORMAL WAIT
         MVI   V.MSG01_ATTR5,$SCREEN_ATTR_HIGH_RED
         MVC   V.MSG01_STATUS(STATUS08_L),STATUS08

         TM    TCBFLGS5,TCBPAGE               HIGH PAGING RATE
         MVI   V.MSG01_ATTR5,$SCREEN_ATTR_HIGH_YELLOW
         MVC   V.MSG01_STATUS(STATUS09_L),STATUS09

         TM    TCBFLGS5,TCBSYS                OTHER TASK MUST COMPLETE
         MVI   V.MSG01_ATTR5,$SCREEN_ATTR_HIGH_YELLOW
         MVC   V.MSG01_STATUS(STATUS10_L),STATUS10

         DC    X'FFFF'

* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXD_START       DC       Q(DXDTASK)
V_STORCB        DC       V(STORCB)
V_TCBTBL        DC       V(TCBTBL)

F0              DC       F'0'
F1              DC       F'1'
P1              DC       P'1'

PANEL_NAME      DC       CL8'STORTASK'

STATUS01        DS       0C
                DC       C'Abending'
STATUS01_L      EQU      *-STATUS01

STATUS02        DS       0C
                DC       C'Must complete'
STATUS02_L      EQU      *-STATUS02

STATUS03        DS       0C
                DC       C'Active on a CPU'
STATUS03_L      EQU      *-STATUS03

STATUS04        DS       0C
                DC       C'I/O RQE''s exhausted'
STATUS04_L      EQU      *-STATUS04

STATUS05        DS       0C
                DC       C'Temporarily frozen'
STATUS05_L      EQU      *-STATUS05

STATUS06        DS       0C
                DC       C'Time limit or SYSOUT limit exit running'
STATUS06_L      EQU      *-STATUS06

STATUS07        DS       0C
                DC       C'Top RB in WAIT'
STATUS07_L      EQU      *-STATUS07

STATUS08        DS       0C
                DC       C'Abnormal WAIT'
STATUS08_L      EQU      *-STATUS08

STATUS09        DS       0C
                DC       C'Not dispatchable due to high page rate'
STATUS09_L      EQU      *-STATUS09

STATUS10        DS       0C
                DC       C'Another task is in must complete'
STATUS10_L      EQU      *-STATUS10

MSG01           DS       0C
MSG01_ATTR1     DC       AL1($SCREEN_ATTR_INPUT)
MSG01_SELECTION DC       C' '
                DC       AL1($SCREEN_ATTR_NORMAL)
MSG01_LEVEL     DC       X'40202120'
                DC       AL1($SCREEN_ATTR_HIGH_TURQUOISE)
                DC       C' '
MSG01_TCB       DC       CL8' '
MSG01_ATTR3     DC       AL1($SCREEN_ATTR_HIGH_YELLOW)
MSG01_CC        DC       CL8' '
MSG01_ATTR4     DC       AL1($SCREEN_ATTR_NORMAL)
MSG01_PROGRAM   DC       CL8' '
MSG01_ATTR5     DC       AL1($SCREEN_ATTR_NORMAL)
MSG01_STATUS    DC       CL40' '
MSG01_L         EQU      *-MSG01
* ------------------------------------------------------------------- *
*                                                                     *
*               LOCAL COMMANDS                                        *
*                                                                     *
* ------------------------------------------------------------------- *
LOCAL_COMMANDS  DS       0C
                DC       X'FF'

                LTORG
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
STATUS_DSECT    DSECT
STATUS_TM       TM      *,0
STATUS_MVI      TM      *,0
STATUS_MVC      MVC     *,*
STATUS_DSECT_L  EQU     *-STATUS_DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDTASK         DSECT
                COPY     DXDPREF

DXD_CHAIN       DS       A                  FIRST ON CHAIN
DXD_CURRENT_PNS DS       A                  CURRENT POINT-AND-SHOOT

DXD_RC          DS       XL2

DXD_LEVEL       DS       PL2
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                IBMMAC   ASCB=LIST
                IBMMAC   TCB=LIST
                IHARB    DSECT=YES,LIST=YES
                IHACDE
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                BPXYSTAT DSECT=YES,LIST=NO
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                IHAASVT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                STORCOMM
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                OSSPFD   TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                COPY     ATTRS
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                COPY     REGEQU
                END      STORTASK
