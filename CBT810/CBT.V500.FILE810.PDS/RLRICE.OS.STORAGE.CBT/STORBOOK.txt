* ------------------------------------------------------------------- *
*                                                                     *
*  Process name (.), redisplay (..), and list commands                *
*                                                                     *
*                                                                     *
*  Maybe someone else can figure out a better way of wording this...  *
*                                                                     *
*  While searching storage, you can "bookmark" the current display.   *
*  That is give a name to the current address, displacment,           *
*  and if using a control block format, the format in use.  Then      *
*  later you can return to bookmarks.                                 *
*                                                                     *
*  "Bookmark" the current display, enter ".name" where the "." is a   *
*  keyword and "name" is any 1-8 character name you want to use.      *
*  To return to a bookmark, enter "..name" where ".." is a keyword    *
*  and "name" is the name used earlier on a "." command.              *
*                                                                     *
*  You can list the bookmarks you have created by entering "bookmark".*
*                                                                     *
* ------------------------------------------------------------------- *
STORBOOK CSECT
STORBOOK AMODE 31
STORBOOK RMODE ANY
         USING OSCOMM,R12
         USING SESSION,R11
         USING STORCOMM,R9
         USING STORBOOK,R15
         B     INIT0000
MODID    DC    CL8'STORBOOK'
         DC    CL8'&SYSDATE'
         DC    CL7'&SYSTIME'
INIT0000 DS    0H
         STM   R14,R12,12(R13)                SAVE REGS
         LR    R10,R15                        COPY ENTRY POINT
         DROP  R15
         USING STORBOOK,R10                   DEFINE BASE
         USING BOOKMARK_DSECT,R5              DEFINE BASE
         L     R1,SESS_DXD_ADDR
         A     R1,DXD_START
         ST    R1,8(,R13)
         ST    R13,4(,R1)
         LR    R13,R1
         USING DXDBOOK,R13
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY
         L     R8,COMM_OSSPFD
         USING OSSPFD,R8
         ICM   R1,15,OPERAND_01_ADDRESS       OPERAND'S ADDRESS
         BZ    EXIT0008                       NOT PRESENT
         SR    R2,R2
         ICM   R2,3,OPERAND_01_LENGTH         OPERAND LENGTH
         CLC   =C'..',0(R1)                   RESTORE?
         BE    MAIN0100                       YES
         CLC   =C'BOOKMARK',0(R1)             LIST BOOKMARKS?
         BE    MAIN0200                       YES
* ------------------------------------------------------------------- *
*        Create or replace bookmark                                   *
* ------------------------------------------------------------------- *
         ITRACE ID=BOOKMARK
         CH    R2,H9                          TOO LONG?
         BH    ERR0010                        YES
         BCTR  R2,R0                          MINUS 1 FOR "."
         BCTR  R2,R0                          MINUS 1 FOR EX
         MVC   DXD_NAME,COMM_BLANKS           INITIALIZE NAME
         EX    R1,NAMEMVC1                    COPY NAME
         ITRACE ID=CHECKDUP
         LA    R5,STORCOMM_BOOKMARK_CHAIN     BOOKMARK CHAIN ANCHOR
         B     MAIN0020
MAIN0010 DS    0H
         CLC   BOOKMARK_NAME,DXD_NAME         ALREADY ON CHAIN?
         BE    MAIN0030                       YES
MAIN0020 DS    0H
         LR    R2,R5                          SAVE THIS BLOCK'S ADDRESS
         ICM   R5,15,BOOKMARK_NEXT            NEXT ON CHAIN
         BNZ   MAIN0010
         MVC   SPF_MSG_1(MSG05_L),MSG05
PREV     USING BOOKMARK_DSECT,R2
         GETMAIN RU,                                                   +
               LV=BOOKMARK_DSECT_L,                                    +
               LOC=ANY
         LR    R5,R1                          COPY NEW BLOCK'S ADDR
         ST    R5,PREV.BOOKMARK_NEXT          CHAIN TO PREVIOUS
         DROP  PREV
         ITRACE ID=NEW_BK,                                             +
               RDATA1=R5,                                              +
               RDATA2=R2
         B     MAIN0040
MAIN0030 DS    0H
         ITRACE ID=REP_BOOK
         MVC   SPF_MSG_1(MSG02_L),MSG02
MAIN0040 DS    0H
         XC    BOOKMARK_DSECT(BOOKMARK_DSECT_L),BOOKMARK_DSECT
         MVC   BOOKMARK_NAME,DXD_NAME
         MVC   BOOKMARK_DATA_ADDR,STORCOMM_DATA_ADDR
         MVC   BOOKMARK_DATA_DISP,STORCOMM_DATA_DISP
         MVC   BOOKMARK_CB_NAME,STORCOMM_CB_NAME
         MVC   BOOKMARK_CB_DESC,STORCOMM_CB_DESCRIPTION
         MVC   BOOKMARK_CB_TABLE,STORCOMM_CB_TABLE
         MVC   BOOKMARK_CB_FIRST,STORCOMM_CB_FIRST
         MVC   BOOKMARK_FUNCTION_ADDR,STORCOMM_FUNCTION_ADDR
         MVC   BOOKMARK_ASID,STORCOMM_ASID
         MVC   BOOKMARK_SPACE_NAME,STORCOMM_SPACE_NAME
         MVC   BOOKMARK_PANEL,SESS_DISP_PANEL
         MVC   SPF_ZCMD,COMM_BLANKS
         B     EXIT0008                       <-- NOT REALLY AN ERROR
NAMEMVC1 MVC   DXD_NAME(0),1(R1)
* ------------------------------------------------------------------- *
*        Restore previously named (bookmarked) display                *
* ------------------------------------------------------------------- *
MAIN0100 DS    0H
         ITRACE ID=RESTORE
         CH    R2,H10                         TOO LONG?
         BH    ERR0010                        YES
         BCTR  R2,R0                          MINUS 1 FOR 1ST "."
         BCTR  R2,R0                          MINUS 1 FOR 2ND "."
         BCTR  R2,R0                          MINUS 1 FOR EX
         MVC   DXD_NAME,COMM_BLANKS           INITIALIZE NAME
         EX    R1,NAMEMVC2                    COPY NAME
         ICM   R5,15,STORCOMM_BOOKMARK_CHAIN  1ST BOOKMARK
         BZ    ERR0020                        NO BOOKMARKS
MAIN0110 DS    0H
         CLC   BOOKMARK_NAME,DXD_NAME         BOOKMARK LOCATED?
         BE    MAIN0120                       YES
         ICM   R5,15,BOOKMARK_NEXT            NEXT BOOKMARK
         BNZ   MAIN0110                       LOOP
         B     ERR0020
MAIN0120 DS    0H
         MVC   STORCOMM_DATA_ADDR,BOOKMARK_DATA_ADDR
         MVC   STORCOMM_DATA_DISP,BOOKMARK_DATA_DISP
         MVC   STORCOMM_CB_NAME,BOOKMARK_CB_NAME
         MVC   STORCOMM_CB_DESCRIPTION,BOOKMARK_CB_DESC
         MVC   STORCOMM_CB_TABLE,BOOKMARK_CB_TABLE
         MVC   STORCOMM_CB_FIRST,BOOKMARK_CB_FIRST
         MVC   STORCOMM_NEW_FUNCTION,BOOKMARK_FUNCTION_ADDR
         MVC   STORCOMM_ASID,BOOKMARK_ASID
         MVC   STORCOMM_SPACE_NAME,BOOKMARK_SPACE_NAME
         MVC   SESS_DISP_PANEL,BOOKMARK_PANEL
         MVC   SPF_ZCMD,COMM_BLANKS
         ITRACE ID=RESTORED
         B     EXIT0000
NAMEMVC2 MVC   DXD_NAME(0),2(R1)
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0200 DS    0H
         ITRACE ID=LIST
         MVC   SPF_ZCMD,COMM_BLANKS
         MVC   DXD_PANEL,SESS_DISP_PANEL
         MVC   SESS_DISP_PANEL,PANEL_NAME
         MVC   DXD_FIRST_BOOKMARK,STORCOMM_BOOKMARK_CHAIN
MAIN0210 DS    0H
         L     R0,STORCOMM_VDATA_ADDR
         L     R1,STORCOMM_VDATA_LENGTH
         SR    R14,R14
         SR    R15,R15
         MVCL  R0,R14                         INITIALIZE VDATA
         L     R6,STORCOMM_VDATA_ADDR
         LH    R7,SPF_VDEPTH                  NBR OF LINES
         ICM   R5,15,DXD_FIRST_BOOKMARK       FIRST BOOKMARK
         BZ    MAIN0350
MAIN0220 DS    0H
         MVC   0(DETAIL_L,R6),DETAIL
V        USING DETAIL,R6
         MVC   V.DETAIL_NAME,BOOKMARK_NAME
         MVC   V.DETAIL_AS_NAME,BOOKMARK_SPACE_NAME
         UNPK  COMM_DWORD(5),BOOKMARK_ASID(3)
         MVC   V.DETAIL_AS_ID,COMM_DWORD
         TR    V.DETAIL_AS_ID,COMM_HEXCHAR
         UNPK  COMM_DWORD(9),BOOKMARK_DATA_ADDR(5)
         MVC   V.DETAIL_ADDR,COMM_DWORD
         TR    V.DETAIL_ADDR,COMM_HEXCHAR
         UNPK  COMM_DWORD(9),BOOKMARK_DATA_DISP(5)
         MVC   V.DETAIL_DISP,COMM_DWORD
         TR    V.DETAIL_DISP,COMM_HEXCHAR
         A     R6,SPF_VWIDTH                  NEXT LINE
         BCT   R7,MAIN0230                    ACCOUNT FOR USED LINE
         B     MAIN0240
MAIN0230 DS    0H
         ICM   R5,15,BOOKMARK_NEXT            ANOTHER BOOKMARK?
         BNZ   MAIN0220                       YES
MAIN0240 DS    0H
         ITRACE ID=DISPLAY
         L     R15,COMM_V_OSDISP              OSDISP ENTRY POINT
         BALR  R14,R15                        CALL OSDISP
         OC    SESS_RC,SESS_RC                RC = 0?
         BNZ   MAIN0400                       NO
         CLI   SPF_ZVERB,C'D'                 SCROLL DOWN?
         BE    MAIN0250                       YES
         CLI   SPF_ZVERB,C'U'                 SCROLL UP?
         BE    MAIN0300
         B     MAIN0240                       DISPLAY AGAIN
MAIN0250 DS    0H
         L     R5,DXD_FIRST_BOOKMARK          FIRST BOOKMARK ON SCREEN
         CLI   SPF_ZSCROLLA,C'M'              MAX?
         BE    MAIN0290
         L     R1,SPF_ZSCROLLN                NBR OF LINES
         B     MAIN0270
MAIN0260 DS    0H
         ST    R5,DXD_FIRST_BOOKMARK
         ICM   R5,15,BOOKMARK_NEXT            NEXT BOOKMARK
         BZ    MAIN0210                       END OF CHAIN
MAIN0270 DS    0H
         BCT   R1,MAIN0260                    LOOP
         B     MAIN0210
MAIN0280 DS    0H
         ST    R5,DXD_FIRST_BOOKMARK
MAIN0290 DS    0H
         ICM   R5,15,BOOKMARK_NEXT
         BNZ   MAIN0290
         B     MAIN0210
MAIN0300 DS    0H
         MVC   DXD_FIRST_BOOKMARK,STORCOMM_BOOKMARK_CHAIN
         B     MAIN0210
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0350 DS    0H
         MVC   SPF_MSG_1(MSG04_L),MSG04
         B     EXIT0008
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0400 DS    0H
         MVC   SESS_DISP_PANEL,DXD_PANEL
         B     EXIT0008
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0010  DS    0H
         ITRACE ID=TOO_LONG
         MVC   SPF_MSG_1(MSG01_L),MSG01
         B     EXIT0008
ERR0020  DS    0H
         ITRACE ID=NOT_FND
         MVC   SPF_MSG_1(MSG03_L),MSG03
         B     EXIT0008
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         SR    R15,R15
         B     EXITEXIT
EXIT0008 DS    0H
         LA    R15,8
EXITEXIT DS    0H
         ITRACE ID=EXIT,                                               +
               RDATA1=R15
         L     R13,4(,R13)                     RESTORE R13
         L     R14,12(,R13)                    RESTORE R14
         LM    R0,R12,20(R13)                  RESTORE REGISTERS
         BR    R14                             RETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXD_START      DC       Q(DXDBOOK)
V_STORSTOR     DC       V(STORSTOR)
H9             DC       H'9'
H10            DC       H'10'

PANEL_NAME     DC       CL8'STORBOOK'

MSG01          DS       0C
               DC       C'STORBOOK01E '
               DC       C'Bookmark name too long'
MSG01_L        EQU      *-MSG01

MSG02          DS       0C
               DC       C'STORBOOK02I '
               DC       C'Bookmark replaced'
MSG02_L        EQU      *-MSG02

MSG03          DS       0C
               DC       C'STORBOOK03E '
               DC       C'Not found'
MSG03_L        EQU      *-MSG03

MSG04          DS       0C
               DC       C'STORBOOK04I '
               DC       C'No bookmarks'
MSG04_L        EQU      *-MSG04

MSG05          DS       0C
               DC       C'STORBOOK05I '
               DC       C'New bookmark created'
MSG05_L        EQU      *-MSG05

               LTORG

DETAIL         DS       0C
               DC       AL1($SCREEN_ATTR_HIGH)
DETAIL_NAME    DC       CL8' '
               DC       AL1($SCREEN_ATTR_HIGH_TURQUOISE)
DETAIL_AS_NAME DC       CL8' '
               DC       AL1($SCREEN_ATTR_NORMAL)
DETAIL_AS_ID   DC       CL4' '
               DC       AL1($SCREEN_ATTR_HIGH_GREEN)
DETAIL_ADDR    DC       CL8' '
               DC       AL1($SCREEN_ATTR_NORMAL)
DETAIL_DISP    DC       CL8' '
               DC       AL1($SCREEN_ATTR_HIGH_YELLOW)
DETAIL_CB      DC       CL8' '
DETAIL_L       EQU      *-DETAIL
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDBOOK            DSECT
                   COPY    DXDPREF
DXD_FIRST_BOOKMARK DS      A
DXD_NAME           DS      CL8
DXD_PANEL          DS      CL8
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY     BOOKMARK
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               BPXYSTAT DSECT=YES,LIST=NO
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               STORCOMM
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               OSSPFD   TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY     ATTRS
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY     REGEQU
               END      STORBOOK
