* ------------------------------------------------------------------- *
*                                                                     *
*                                                                     *
*    Note:  This exit does NOT need to switch to AR mode              *
*           or key zero.  The RCVT is in common storage.              *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
RCVTEX02 CSECT
RCVTEX02 AMODE 31
RCVTEX02 RMODE ANY
         USING OSCOMM,R12
         USING SESSION,R11
         USING RCVTEX02,R15
         B     INIT0000
MODID    DC    CL8'RCVTEX02'
         DC    CL8'&SYSDATE'
         DC    CL7'&SYSTIME'
INIT0000 DS    0H
         STM   R14,R12,12(R13)                SAVE REGS
         LR    R10,R15                        COPY ENTRY POINT
         DROP  R15
         USING RCVTEX02,R10                   DEFINE BASE
         USING STORCOMM,R9
         L     R15,SESS_DXD_ADDR              WORK AREA BASE
         A     R15,DXD_START                  PLUS DISP
         ST    R13,4(,R15)                    SAVE R13
         ST    R15,8(,R13)                    CHAIN SAVE AREA
         LR    R13,R15
         USING DXDRCVT2,R13                   DEFINE WORK AREA BASE
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY,                                              +
               DATA1=(STORCOMM_FUNCTION,1)
         CLI   STORCOMM_FUNCTION,$STORCOMM_COMMAND
         BE    EXIT0000
         L     R5,STORCOMM_DATA_ADDR          RCVT ADDRESS
         USING RCVT,R5
         L     R3,FIELD_EXIT_PNS
         L     R6,FIELD_EXIT_VDATA
         LH    R7,FIELD_EXIT_LINES
         MVI   STORCOMM_STATUS,$STORCOMM_OK   SET DEFAULT STATUS
         L     R8,COMM_OSSPFD
         USING OSSPFD,R8
         TM    RCVTFLG1,RCVTFPDS              FAST PATH FOR DATA SETS?
         BNO   MAIN0010
         MVC   0(MSG01_L,R6),MSG01
V        USING MSG01,R6
         MVI   V.MSG01_ATTR,$SCREEN_ATTR_HIGH_YELLOW
         MVC   V.MSG01_DESC,MSG02
         BAL   R1,LINE0000
MAIN0010 DS    0H
         TM    RCVTFLG1,RCVTTDSN              TAPE DATA SET PROTECTION?
         BNO   MAIN0020
         MVC   0(MSG01_L,R6),MSG01
         MVI   V.MSG01_ATTR,$SCREEN_ATTR_HIGH_YELLOW
         MVC   V.MSG01_DESC,MSG03
         BAL   R1,LINE0000
MAIN0020 DS    0H
         TM    RCVTFLG1+1,RCVTPRO             PROTECT ALL?
         BNO   MAIN0050
         MVC   0(MSG01_L,R6),MSG01
         MVI   V.MSG01_ATTR,$SCREEN_ATTR_HIGH_YELLOW
         TM    RCVTFLG1+1,RCVTPROF            PROTECT ALL FAIL?
         BNO   MAIN0030                       NO
         MVC   V.MSG01_DESC,MSG04
         B     MAIN0040
MAIN0030 DS    0H
         MVC   V.MSG01_DESC,MSG05
MAIN0040 DS    0H
         BAL   R1,LINE0000
         BAL   R1,LINE0000
MAIN0050 DS    0H
         TM    RCVTFLG1+1,RCVTEOS             ERASE ON SCRATCH?
         BNO   MAIN0070                       NO
         TM    RCVTFLG1+1,RCVTEOSL            ERASE ON SCRATCH SECLEVEL
         BNO   MAIN0060                       NO
         MVC   0(MSG01_L,R6),MSG01
         MVI   V.MSG01_ATTR,$SCREEN_ATTR_HIGH_YELLOW
         MVC   V.MSG01_DESC,MSG06
         BAL   R1,LINE0000
MAIN0060 DS    0H
         TM    RCVTFLG1+1,RCVTEOSA            ERASE ON SCRATCH ALL?
         BNO   MAIN0070                       NO
         MVC   0(MSG01_L,R6),MSG01
         MVI   V.MSG01_ATTR,$SCREEN_ATTR_HIGH_YELLOW
         MVC   V.MSG01_DESC,MSG07
         BAL   R1,LINE0000
MAIN0070 DS    0H
         TM    RCVTFLG1+2,RCVTPROG            ACCESS CONTROL BY PROGRAM
         BNO   EXIT0000                       NO
         MVC   0(MSG01_L,R6),MSG01
         MVI   V.MSG01_ATTR,$SCREEN_ATTR_HIGH_YELLOW
         MVC   V.MSG01_DESC,MSG08
         BAL   R1,LINE0000
         B     EXIT0000
* ------------------------------------------------------------------- *
*        Next line on display                                         *
* ------------------------------------------------------------------- *
LINE0000 DS    0H
         ITRACE ID=NEXTLINE,                                           +
               RDATA1=R6,                                              +
               RDATA2=R7
         LA    R3,PNS_L(,R3)
         A     R6,SPF_VWIDTH                  NEXT LINE
         BCT   R7,LINE0010                    MINUS 1 AVAILABLE LINE
         B     EXIT0000                       SCREEN FULL.. EXIT
LINE0010 DS    0H
         BR    R1
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=EXIT
         ST    R3,FIELD_EXIT_PNS
         ST    R6,FIELD_EXIT_VDATA
         STH   R7,FIELD_EXIT_LINES
         L     R13,4(,R13)                    RESTORE R13
         LM    R14,R12,12(R13)                RESTORE REGISTERS
         SR    R15,R15
         BR    R14                            RETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXD_START    DC       Q(DXDRCVT2)

MSG01        DS       0C
             DC       AL1($SCREEN_ATTR_HIGH_TURQUOISE)
MSG01_DISP   DC       CL04' '
             DC       AL01($SCREEN_ATTR_HIGH)
MSG01_NAME   DC       CL08' '
             DC       AL01($SCREEN_ATTR_NORMAL)
MSG01_DATA   DC       CL20' '
             DC       CL04' '
MSG01_ATTR   DC       AL01($SCREEN_ATTR_HIGH_GREEN)
MSG01_DESC   DC       CL40' '
MSG01_L      EQU      *-MSG01

MSG02        DC       CL40'Fast path for data sets'
MSG03        DC       CL40'Tape data set protection'
MSG04        DC       CL40'Protect-all FAIL'
MSG05        DC       CL40'Protect-all WARN'
MSG06        DC       CL40'Erase on scratch by SECLEVEL'
MSG07        DC       CL40'Erase on scratch ALL'
MSG08        DC       CL40'Access control by program'
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDRCVT2          DSECT
                  COPY   DXDPREF
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                  COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                  SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                  ICHPRCVT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                  BPXYSTAT DSECT=YES,LIST=NO
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                  STORCOMM
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                  COPY     PNS
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                  OSSPFD   TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                  COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                  COPY     ATTRS
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                  COPY     REGEQU
                  END      RCVTEX02
