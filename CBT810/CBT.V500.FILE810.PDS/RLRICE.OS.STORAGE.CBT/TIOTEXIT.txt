* ------------------------------------------------------------------- *
*                                                                     *
*  Module name: TIOTEXIT                                              *
*                                                                     *
*                                                                     *
*  Displays ASCBs                                                     *
*                                                                     *
*  ASID            STORCOMM_ASID                                      *
*  ASCB address    STORCOMM_DATA_ADDR                                 *
*                                                                     *
* ------------------------------------------------------------------- *
TIOTEXIT CSECT
TIOTEXIT AMODE 31
TIOTEXIT RMODE ANY
         USING OSCOMM,R12
         USING SESSION,R11
         USING TIOTEXIT,R15
         B     INIT0000
MODID    DC    CL8'TIOTEXIT'
         DC    CL8'&SYSDATE'
         DC    CL7'&SYSTIME'
INIT0000 DS    0H
         STM   R14,R12,12(R13)                  SAVE REGS
         LR    R10,R15                          COPY ENTRY POINT
         DROP  R15
         USING TIOTEXIT,R10                     DEFINE BASE
         USING STORCOMM,R9
         L     R15,SESS_DXD_ADDR                WORK AREA BASE
         A     R15,DXD_START                    PLUS DISP
         ST    R13,4(,R15)                      SAVE R13
         ST    R15,8(,R13)                      CHAIN SAVE AREA
         LR    R13,R15
         USING DXDTIOT,R13                      DEFINE WORK AREA BASE
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY,                                              +
               DATA1=(STORCOMM_FUNCTION,1)
         L     R8,COMM_OSSPFD
         USING OSSPFD,R8
         MVI   STORCOMM_STATUS,$STORCOMM_OK
         CLI   STORCOMM_FUNCTION,$STORCOMM_COMMAND
         BE    CMD0000
         CLI   STORCOMM_FUNCTION,$STORCOMM_PNS
         BE    PNS0000
* ------------------------------------------------------------------- *
*        Initialize vdata buffer                                      *
* ------------------------------------------------------------------- *
         L     R0,STORCOMM_VDATA_ADDR
         L     R1,STORCOMM_VDATA_LENGTH
         SR    R14,R14
         SR    R15,R15
         MVCL  R0,R14
* ------------------------------------------------------------------- *
*        Initialize point and shoot table                             *
* ------------------------------------------------------------------- *
         L     R0,STORCOMM_PNS_TABLE_ADDR
         L     R1,STORCOMM_PNS_TABLE_LENGTH
         SR    R14,R14
         SR    R15,R15
         MVCL  R0,R14
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0000 DS    0H
         OI    COMM_FLAGS,$COMM_AUTH_ON
         L     R15,COMM_V_OSAUTH
         BALR  R14,R15                         TURN ON APF AUTH
         MODESET KEY=ZERO
         NI    COMM_FLAGS,255-$COMM_AUTH_ON
         L     R15,COMM_V_OSAUTH
         BALR  R14,R15                         TURN OFF APF AUTH
         L     R3,STORCOMM_PNS_TABLE_ADDR
         USING PNS,R3
         LH    R7,FIELD_EXIT_LINES
         L     R6,FIELD_EXIT_VDATA
         LAM   R5,R5,F1                        SET SECONDARY ALET
         SAC   512
         L     R5,STORCOMM_DATA_ADDR           CURRENT DATA ADDRESS
         USING TIOT,R5
         LH    R1,STORCOMM_ASID
         ITRACE ID=SSAR,                                               +
               RDATA1=R1
         SSAR  (R1)
* ------------------------------------------------------------------- *
*                                                                     *
*                                                                     *
*        Format TIOT                                                  *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
         MVC   SESS_DISP_PANEL,PANEL_NAME
         CLC   TIOCNJOB,STORCOMM_SPACE_NAME   MATCH JOB NAME?
         BE    MAIN0010                       YES
         CLC   TIOCSTEP,STORCOMM_SPACE_NAME   MATCH STEP NAME?
         BNE   ERR0010                        NO
MAIN0010 DS    0H
         MVC   0(MSG01_L,R6),MSG01
         A     R6,SPF_VWIDTH                  NEXT LINE
         BCTR  R7,0
         LA    R5,TIOENTRY                    1ST DD
         DROP  R5
         USING TIOENTRY,R5
MAIN0020 DS    0H
         SR    R4,R4
         ICM   R4,1,TIOELNGH                  LENGTH OF THIS ENTRY
         BZ    EXIT0000                       ZERO.. END OF TIOT
         MVC   0(MSG02_L,R6),MSG02
V        USING MSG02,R6
         CLI   TIOEDDNM,C' '                  PRINTABLE?
         BNH   MAIN0030                       NO
         MVC   V.MSG02_DD,TIOEDDNM            COPY DD NAME
MAIN0030 DS    0H
         CVD   R4,COMM_DWORD                  CONVERT TO DECIMAL
         ED    V.MSG02_LENGTH,COMM_DWORD+6    EDIT LENGTH
         UNPK  COMM_INFO_01(3),TIOESTTA(2)    UNPACK STATUS A
         MVC   V.MSG02_STATUSA,COMM_INFO_01   COPY TO VDATA
         TR    V.MSG02_STATUSA,COMM_HEXCHAR   TRANSLATE TO PRINTABLE
         UNPK  COMM_INFO_01(3),TIOESTTB(2)    UNPACK STATUS B
         MVC   V.MSG02_STATUSB,COMM_INFO_01   COPY TO VDATA
         TR    V.MSG02_STATUSB,COMM_HEXCHAR   TRANSLATE TO PRINTABLE
         UNPK  COMM_INFO_01(3),TIOESTTC(2)    UNPACK STATUS C
         MVC   V.MSG02_STATUSC,COMM_INFO_01   COPY TO VDATA
         TR    V.MSG02_STATUSC,COMM_HEXCHAR   TRANSLATE TO PRINTABLE
         TM    TIOELINK,TIOESYOT              SYSOUT?
         BO    MAIN0040                       YES
         SR    R1,R1
         ICM   R1,7,TIOEFSRT                  UCB ADDRESS
         ST    R1,PNS_DATA_ADDR               SAVE UCB ADDRESS
         BZ    MAIN0060
         USING UCBOB,R1
         UNPK  COMM_INFO_01(5),UCBCHAN(3)     UNPACK DEVICE NBR
         MVC   V.MSG02_UCB_ADDR,COMM_INFO_01  COPY TO VDATA
         TR    V.MSG02_UCB_ADDR,COMM_HEXCHAR  TRANSLATE
         DROP  R1
         B     MAIN0050
MAIN0040 DS    0H
         MVI   V.MSG02_ATTR,$SCREEN_ATTR_HIGH_TURQUOISE
         MVC   V.MSG02_VOLSER,C_JES
MAIN0050 DS    0H
         SR    R1,R1
MAIN0060 DS    0H
         LA    R2,DXD_EPA
         ST    R2,DXD_SWEPAPTR
         USING ZB505,R2
         XC    SWAEPA,SWAEPA
         MVC   SWVA,TIOEJFCB
         EPAR  (R1)
         ITRACE ID=RESET,                                              +
               RDATA1=R1
         SSAR  (R1)
         SAC   0
         ITRACE ID=SWAREQ
         SWAREQ FCODE=RL,                                              +
               EPA=DXD_SWEPAPTR,                                       +
               UNAUTH=YES,                                             +
               MF=(E,DXD_SWAPARMS)
         LH    R1,STORCOMM_ASID
         ITRACE ID=SSAR,                                               +
               RDATA1=R1
         SSAR  (R1)
         SAC   512
         L     R1,SWBLKPTR
         USING JFCB,R1
         LAM   R1,R1,F1
         MVC   INFMJFCB(JFCBLGTH),0(R1)
         LAM   R1,R1,F0
         MVC   V.MSG02_DSN,JFCBDSNM           COPY DATA SET NAME
         LA    R2,JFCBVOLS                    1ST VOLSER
         SR    R1,R1
         ICM   R1,1,JFCBNVOL                  NBR OF VOLSERS
         BZ    MAIN0090                       NO VOLSERS
MAIN0070 DS    0H
         MVC   V.MSG02_VOLSER,0(R2)           COPY VOLSER
         A     R6,SPF_VWIDTH                  NEXT LINE
         LA    R3,PNS_L(,R3)                  NEXT IN PNS TABLE
         BCT   R7,MAIN0080
         B     EXIT0000                       SCREEN FULL
MAIN0080 DS    0H
         LA    R2,6(,R2)                      NEXT VOLSER
         BCT   R1,MAIN0070
         B     MAIN0100
MAIN0090 DS    0H
         A     R6,SPF_VWIDTH                  NEXT LINE
         LA    R3,PNS_L(,R3)                  NEXT IN PNS TABLE
         BCT   R7,MAIN0100
         B     EXIT0000                       SCREEN FULL
MAIN0100 DS    0H
         AR    R5,R4                          NEXT TIOT ENTRY
         B     MAIN0020
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
CMD0000  DS    0H
         ITRACE ID=COMMAND
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
PNS0000  DS    0H
         L     R3,SPF_ROW                     ROW CURSOR WAS ON
         ITRACE ID=PNS,                                                +
               RDATA1=R7
         CH    R3,H7
         BL    EXIT0000
         SH    R3,H7
         MH    R3,=Y(PNS_L)                   MULTIPLY BY LENGTH EACH
         A     R3,STORCOMM_PNS_TABLE_ADDR     PLUS BASE
         MVC   STORCOMM_DATA_ADDR,PNS_DATA_ADDR
         XC    STORCOMM_DATA_DISP,STORCOMM_DATA_DISP
         MVC   STORCOMM_NEW_FUNCTION,V_STORCB
         L     R1,V_UCBTBL
         ST    R1,STORCOMM_CB_TABLE           SET CB TABLE ADDR
         MVC   STORCOMM_CB_NAME,0(R1)         COPY CONTROL BLOCK NAME
         MVC   STORCOMM_CB_DESCRIPTION,8(R1)  COPY CONTROL BLOCK DESC
         MVC   SESS_DISP_PANEL,48(R1)         COPY PANEL NAME
         MVC   STORCOMM_PANEL_NAME,48(R1)     COPY PANEL NAME
         LA    R1,56(,R1)                     FIRST FIELD
         ST    R1,STORCOMM_CB_FIRST           SET ADDRESS
         MVI   STORCOMM_STATUS,$STORCOMM_NEW
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0010  DS    0H
         SAC   0
         LAM   R5,R5,F0
         MVI   COMM_MSG_ID,1
         MVC   COMM_MSG_CSECT,MODID
         L     R15,COMM_V_OSMSG
         BALR  R14,R15
         MVC   SPF_MSG_1,COMM_MSG_1           COPY MESSAGE
         MVC   SPF_MSG_2,COMM_MSG_2           COPY MESSAGE
         MVC   SPF_MSG_3,COMM_MSG_3           COPY MESSAGE
         MVC   SPF_MSG_4,COMM_MSG_4           COPY MESSAGE
         MVC   SPF_MSG_5,COMM_MSG_5           COPY MESSAGE
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         SAC   0                              EXIT AR MODE
         LAM   R5,R5,F0                       RESET AR 5
         EPAR  (R1)
         ITRACE ID=RESET,                                              +
               RDATA1=R1
         SSAR  (R1)
         OI    COMM_FLAGS,$COMM_AUTH_ON
         L     R15,COMM_V_OSAUTH
         BALR  R14,R15                        TURN ON APF AUTH
         MODESET KEY=NZERO
         NI    COMM_FLAGS,255-$COMM_AUTH_ON
         L     R15,COMM_V_OSAUTH
         BALR  R14,R15                        TURN OFF APF AUTH
         ST    R6,FIELD_EXIT_VDATA            UPDATE VDATA BUFFER ADDR
         STH   R7,FIELD_EXIT_LINES            UPDATES LINES LEFT
         ITRACE ID=EXIT
         LH    R15,DXD_RC                     RETURN CODE
         L     R13,4(,R13)                    RESTORE R13
         L     R14,12(,R13)                   RESTORE R14
         LM    R0,R12,20(R13)                 RESTORE REGISTERS
         BR    R14                            RETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXD_START       DC       Q(DXDTIOT)
V_STORCB        DC       V(STORCB)
V_UCBTBL        DC       V(UCBTBL)

SWAPARM_I       SWAREQ   MF=L
SWAPARM_L       EQU      *-SWAPARM_I

F0              DC       F'0'
F1              DC       F'1'

H7              DC       H'7'
H8              DC       H'8'

C_JES           DC       CL6'JES   '
C_UCB           DC       CL8'UCB   '
PANEL_NAME      DC       CL8'STORTIOT'

MSG01           DS       0C
                DC       AL1($SCREEN_ATTR_HIGH_TURQUOISE)
                DC       C'DD name    ln A__B__C_ Dev  VOLSER DSN'
MSG01_L         EQU      *-MSG01

MSG02           DS       0C
                DC       AL1($SCREEN_ATTR_HIGH)
MSG02_DD        DC       CL8' '
                DC       AL1($SCREEN_ATTR_NORMAL)
MSG02_LENGTH    DC       X'40202120'
                DC       C' '
MSG02_STATUSA   DC       CL2' '
                DC       C' '
MSG02_STATUSB   DC       CL2' '
                DC       C' '
MSG02_STATUSC   DC       CL2' '
                DC       C' '
MSG02_UCB_ADDR  DC       CL4' '
MSG02_ATTR      DC       AL1($SCREEN_ATTR_HIGH_YELLOW)
MSG02_VOLSER    DC       CL6' '
                DC       AL1($SCREEN_ATTR_HIGH_GREEN)
MSG02_DSN       DC       CL44' '
MSG02_L         EQU      *-MSG02

* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDTIOT           DSECT
                  COPY   DXDPREF

DXD_UCB_TABLE     DS     50A
DXD_UCB_TABLE_L   EQU    *-DXD_UCB_TABLE

DXD_SWAPARMS      DS    0F,(SWAPARM_L)X

DXD_SWEPAPTR      DS    A
DXD_EPA           DS    CL28


DXD_RC            DS     H
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                  IBMMAC   CVT=YES
                  IBMMAC   JFCB=YES
                  IEFJESCT
                  IEFZB505 LOCEPAX=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                  COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                  SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
TIOT              DSECT
                  IBMMAC   TIOT=YES
                  IBMMAC   UCB=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                  BPXYSTAT DSECT=YES,LIST=NO
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                  STORCOMM
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                  COPY     PNS
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                  OSSPFD   TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                  COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                  COPY     ATTRS
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                  COPY     REGEQU
                  END      TIOTEXIT
