* ------------------------------------------------------------------- *
*                                                                     *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
UCBEXIT  CSECT
UCBEXIT  AMODE 31
UCBEXIT  RMODE ANY
         USING OSCOMM,R12
         USING SESSION,R11
         USING UCBEXIT,R15
         B     INIT0000
MODID    DC    CL8'UCBEXIT'
         DC    CL8'&SYSDATE'
         DC    CL7'&SYSTIME'
INIT0000 DS    0H
         STM   R14,R12,12(R13)                SAVE REGS
         LR    R10,R15                        COPY ENTRY POINT
         DROP  R15
         USING UCBEXIT,R10                    DEFINE BASE
         USING STORCOMM,R9
         L     R15,SESS_DXD_ADDR              WORK AREA BASE
         A     R15,DXD_START                  PLUS DISP
         ST    R13,4(,R15)                    SAVE R13
         ST    R15,8(,R13)                    CHAIN SAVE AREA
         LR    R13,R15
         USING DXDUCB,R13                     DEFINE WORK AREA BASE
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY
         CLI   STORCOMM_FUNCTION,$STORCOMM_COMMAND
         BE    EXIT0000
         L     R5,STORCOMM_DATA_ADDR          UCB ADDRESS
         USING UCBOB,R5
         L     R3,FIELD_EXIT_PNS
         L     R6,FIELD_EXIT_VDATA
         LH    R7,FIELD_EXIT_LINES
         MVI   STORCOMM_STATUS,$STORCOMM_OK   SET DEFAULT STATUS
         L     R8,COMM_OSSPFD
         USING OSSPFD,R8
         TM    UCBTBYT3,UCB3DACC              DASD?
         BO    DASD0000                       YES
         B     EXIT0000
DASD0000 DS    0H
         MVC   0(MSG01_L,R6),MSG01
V        USING MSG01,R6
         UNPK  COMM_DWORD(5),UCBVOLI_DISP(3)  UNPACK DISP
         MVC   V.MSG01_DISP,COMM_DWORD        COPY FIELD DISP
         TR    V.MSG01_DISP,COMM_HEXCHAR      TRANSLATE DISP
         MVC   V.MSG01_NAME,UCBVOLI_NAME      COPY FIELD NAME
         MVC   V.MSG01_DATA(L'UCBVOLI),UCBVOLI
         MVC   V.MSG01_DESC,MSG02
         BAL   R1,LINE0000                    NEXT LINE
         MVC   0(MSG01_L,R6),MSG01
         UNPK  COMM_DWORD(5),UCBSTAB_DISP(3)  UNPACK DISP
         MVC   V.MSG01_DISP,COMM_DWORD        COPY FIELD DISP
         TR    V.MSG01_DISP,COMM_HEXCHAR      TRANSLATE DISP
         MVC   V.MSG01_NAME,UCBSTAB_NAME      COPY FIELD NAME
         UNPK  COMM_DWORD(3),UCBSTAB(2)       UNPACK STATUS
         MVC   V.MSG01_DATA(2),COMM_DWORD     COPY STATUS
         TR    V.MSG01_DATA(2),COMM_HEXCHAR   TRANSLATE STATUS
         MVC   V.MSG01_DESC,MSG03
         BAL   R1,LINE0000                    NEXT LINE
         TM    UCBSTAB,UCBBSVL                NOT DEMOUNTABLE?
         BNO   DASD0010                       NO (TWO NO'S..?)
         MVI   V.MSG01_ATTR,$SCREEN_ATTR_HIGH_YELLOW
         MVC   V.MSG01_DESC,MSG04
         BAL   R1,LINE0000                    NEXT LINE
DASD0010 DS    0H
         TM    UCBSTAB,UCBPGFL                USED FOR PAGING?
         BNO   DASD0020                       NO
         MVI   V.MSG01_ATTR,$SCREEN_ATTR_HIGH_YELLOW
         MVC   V.MSG01_DESC,MSG05
         BAL   R1,LINE0000                    NEXT LINE
DASD0020 DS    0H
         TM    UCBSTAB,UCBPRSRS               PERMANENTLY RESIDENT?
         BNO   DASD0030                       NO
         MVI   V.MSG01_ATTR,$SCREEN_ATTR_HIGH_YELLOW
         MVC   V.MSG01_DESC,MSG06
         BAL   R1,LINE0000                    NEXT LINE
DASD0030 DS    0H
         TM    UCBSTAB,UCBBPRV                PRIVATE VOLUME?
         BNO   DASD0040                       NO
         MVI   V.MSG01_ATTR,$SCREEN_ATTR_HIGH_YELLOW
         MVC   V.MSG01_DESC,MSG07
         BAL   R1,LINE0000                    NEXT LINE
DASD0040 DS    0H
         TM    UCBSTAB,UCBBPUB                PUBLIC VOLUME?
         BNO   DASD0050                       NO
         MVI   V.MSG01_ATTR,$SCREEN_ATTR_HIGH_YELLOW
         MVC   V.MSG01_DESC,MSG08
         BAL   R1,LINE0000                    NEXT LINE
DASD0050 DS    0H
         TM    UCBSTAB,UCBBSTR                STORAGE VOLUME?
         BNO   DASD0060                       NO
         MVI   V.MSG01_ATTR,$SCREEN_ATTR_HIGH_YELLOW
         MVC   V.MSG01_DESC,MSG09
         BAL   R1,LINE0000                    NEXT LINE
DASD0060 DS    0H
         TM    UCBSTAB,UCBSHAR                SHARABLE?
         BNO   DASD0070                       NO
         MVI   V.MSG01_ATTR,$SCREEN_ATTR_HIGH_YELLOW
         MVC   V.MSG01_DESC,MSG10
         BAL   R1,LINE0000                    NEXT LINE
DASD0070 DS    0H
         TM    UCBSTAB,UCBBNUL                CATALOG ON THIS VOLUME?
         BNO   EXIT0000                       NO
         MVI   V.MSG01_ATTR,$SCREEN_ATTR_HIGH_YELLOW
         MVC   V.MSG01_DESC,MSG11
         BAL   R1,LINE0000                    NEXT LINE
         B     EXIT0000
* ------------------------------------------------------------------- *
*        Next line on display                                         *
* ------------------------------------------------------------------- *
LINE0000 DS    0H
         LA    R3,PNS_L(,R3)
         A     R6,SPF_VWIDTH                  NEXT LINE
         BCT   R7,LINE0010                    MINUS 1 AVAILABLE LINE
         B     EXIT0000                       SCREEN FULL.. EXIT
LINE0010 DS    0H
         BR    R1
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=EXIT
         ST    R3,FIELD_EXIT_PNS
         ST    R6,FIELD_EXIT_VDATA
         STH   R7,FIELD_EXIT_LINES
         L     R13,4(,R13)                    RESTORE R13
         LM    R14,R12,12(R13)                RESTORE REGISTERS
         SR    R15,R15
         BR    R14                            RETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXD_START    DC     Q(DXDUCB)

UCBVOLI_DISP DC     AL2(UCBVOLI-UCBOB)
UCBVOLI_NAME DC     CL8'UCBVOLI'
UCBSTAB_DISP DC     AL2(UCBSTAB-UCBOB)
UCBSTAB_NAME DC     CL8'UCBSTAB'

MSG01        DS     0C
             DC     AL1($SCREEN_ATTR_HIGH_TURQUOISE)
MSG01_DISP   DC     CL04' '
             DC     AL01($SCREEN_ATTR_HIGH)
MSG01_NAME   DC     CL08' '
             DC     AL01($SCREEN_ATTR_NORMAL)
MSG01_DATA   DC     CL20' '
             DC     CL04' '
MSG01_ATTR   DC     AL01($SCREEN_ATTR_HIGH_GREEN)
MSG01_DESC   DC     CL40' '
MSG01_L      EQU    *-MSG01

MSG02        DC     CL40'VOLSER'
MSG03        DC     CL40'Volume status'
MSG04        DC     CL40'Not demountable'
MSG05        DC     CL40'Volume used for paging'
MSG06        DC     CL40'Permanently resident'
MSG07        DC     CL40'Private'
MSG08        DC     CL40'Public'
MSG09        DC     CL40'Storage'
MSG10        DC     CL40'Sharable amoung JOB steps'
MSG11        DC     CL40'Catalog on volume'
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDUCB       DSECT
             COPY     DXDPREF
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
             COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
             SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
UCB          DSECT
             IEFUCBOB LIST=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
             BPXYSTAT DSECT=YES,LIST=NO
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
             STORCOMM
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
             COPY     PNS
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
             OSSPFD   TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
             COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
             COPY     ATTRS
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
             COPY     REGEQU
             END      UCBEXIT
