* ------------------------------------------------------------------- *
*                                                                     *
*     Process master trace table                                      *
*                                                                     *
*                                                                     *
*   What makes processing the master trace table a bit wierd is that  *
*   the table is a "bottom up" not a "top down" table, so it "wraps"  *
*   at the top, not the bottom.  The "last used before wrap" is the   *
*   last entry used at the top before "wrapping" back to the bottom.  *
*                                                                     *
* ------------------------------------------------------------------- *
MTEXIT   CSECT
MTEXIT   AMODE 31
MTEXIT   RMODE ANY
         USING OSCOMM,R12
         USING SESSION,R11
         USING MTEXIT,R15
         B     INIT0000
MODID    DC    CL8'MTEXIT'
         DC    CL8'&SYSDATE'
         DC    CL7'&SYSTIME'
INIT0000 DS    0H
         STM   R14,R12,12(R13)                SAVE REGS
         LR    R10,R15                        COPY ENTRY POINT
         DROP  R15
         USING MTEXIT,R10                     DEFINE BASE
         USING STORCOMM,R9
         L     R15,SESS_DXD_ADDR              WORK AREA BASE
         A     R15,DXD_START                  PLUS DISP
         ST    R13,4(,R15)                    SAVE R13
         ST    R15,8(,R13)                    CHAIN SAVE AREA
         LR    R13,R15
         USING DXDMT,R13                      DEFINE WORK AREA BASE
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY
         L     R8,COMM_OSSPFD
         USING OSSPFD,R8
         MVI   STORCOMM_STATUS,$STORCOMM_OK   SET DEFAULT STATUS
         CLI   STORCOMM_FUNCTION,$STORCOMM_COMMAND
         BE    EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         L     R5,STORCOMM_DATA_ADDR          PLUS BASE ADDRESS
         USING MTTABLE,R5
         OI    COMM_FLAGS,$COMM_AUTH_ON
         L     R15,COMM_V_OSAUTH
         BALR  R14,R15                        TURN ON APF AUTH
         MODESET KEY=ZERO
         LH    R1,STORCOMM_ASID
         ITRACE ID=SSAR,                                               +
               RDATA1=R1
         SSAR  (R1)
         LAM   R5,R5,F1                       SET AR
         SAC   512                            ENTER AR MODE
         L     R4,MTTWRPPT                    FIRST ENTRY
         USING MTENTRY,R4
         LAM   R4,R4,F1
         L     R6,FIELD_EXIT_VDATA            CURRENT VDATA BUFFER ADDR
         LH    R7,FIELD_EXIT_LINES            NBR OF LINES AVAILABLE
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0010 DS    0H
         MVC   0(MSG01_L,R6),MSG01            INITIALIZE MESSAGE
V        USING MSG01,R6
         SR    R2,R2                          CLEAR REGISTER
         ICM   R2,3,MTENTLEN                  LENGTH OF USERS DATA
         CH    R2,=Y(L'MSG01_DATA)            TOO LONG?
         BNH   MAIN0020
         LH    R2,=Y(L'MSG01_DATA)            LIMIT LENGTH
MAIN0020 DS    0H
         BCTR  R2,0
         EX    R2,DATA_MVC                    COPY DATA
         A     R6,STORCOMM_VWIDTH             NEXT LINE
         BCT   R7,MAIN0030                    ONE LESS LINE
         B     EXIT0000                       SCREEN FULL
MAIN0030 DS    0H
         ICM   R2,3,MTENTLEN                  FULL DATA LENGTH
         LA    R4,L'MTENTHDR(R2,R4)           NEXT ENTRY
         C     R4,MTTENDPT                    BEYOND END OF TABLE?
         BL    MAIN0010                       NO
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         SAC   0
         LAM   R4,R4,F0
         LAM   R5,R5,F0
         EPAR  (R1)
         ITRACE ID=RESET,                                              +
               RDATA1=R1
         SSAR  (R1)
         MODESET KEY=NZERO
         NI    COMM_FLAGS,255-$COMM_AUTH_ON
         L     R15,COMM_V_OSAUTH
         BALR  R14,R15                        TURN OFF APF AUTH
         ST    R6,FIELD_EXIT_VDATA            UPDATE VDATA ADDR
         STH   R7,FIELD_EXIT_LINES            UPDATE LINES LEFT
         ITRACE ID=EXIT
         L     R13,4(,R13)                    RESTORE R13
         LM    R14,R12,12(R13)                RESTORE REGISTERS
         SR    R15,R15
         BR    R14                            RETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DATA_MVC MVC   V.MSG01_DATA(0),MTENTDAT       COPY DATA
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXD_START    DC     Q(DXDMT)

F0           DC     F'0'
F1           DC     F'1'

MSG01            DS     0C
                 DC     AL01($SCREEN_ATTR_NORMAL)
MSG01_DATA       DC     CL79' '
MSG01_L          EQU    *-MSG01
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDMT        DSECT
             COPY     DXDPREF
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
             COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
             SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
             IEEZB806
             IEZMTPRM
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
             BPXYSTAT DSECT=YES,LIST=NO
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
             STORCOMM
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
             COPY     FIELDD
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
             OSSPFD   TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
             COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
             COPY     ATTRS
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
             COPY     REGEQU
             END      MTEXIT
