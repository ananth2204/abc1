* ------------------------------------------------------------------- *
*                                                                     *
*  Module name: STORMAIN                                              *
*                                                                     *
*                                                                     *
*  Storage display mainline.                                          *
*                                                                     *
*  Displays a list of currently active address spaces.                *
*                                                                     *
*  When an address space is selected, set the ASID and display        *
*  the ASCB for the address space.                                    *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
STORMAIN CSECT
STORMAIN AMODE 31
STORMAIN RMODE ANY
         USING OSCOMM,R12
         USING SESSION,R11
         USING STORMAIN,R15
         B     INIT0000
MODID    DC    CL8'STORMAIN'
         DC    CL8'&SYSDATE'
         DC    CL7'&SYSTIME'
INIT0000 DS    0H
         STM   R14,R12,12(R13)                  SAVE REGS
         LR    R10,R15                          COPY ENTRY POINT
         DROP  R15
         USING STORMAIN,R10                     DEFINE BASE
         CLI   SESS_FORMAT_FLAGS,$FORMAT_INITIALIZE
         BNE   MAIN0000                         NO
* ------------------------------------------------------------------- *
*        Initialization                                               *
* ------------------------------------------------------------------- *
INIT0010 DS    0H
         L     R0,DXD_SIZE
         GETMAIN RU,                                                   +
               LV=(R0),                                                +
               LOC=ANY
         ST    R1,SESS_DXD_ADDR                 SET WORK AREA ADDRESS
         LR    R2,R1
         LR    R0,R1
         L     R1,DXD_SIZE
         SR    R14,R14
         SR    R15,R15
         MVCL  R0,R14                           INITIALIZE WORK AREA
         A     R2,DXD_START
         ST    R13,4(,R2)                       SAVE R13
         ST    R2,8(,R13)                       CHAIN SAVE AREA
         LR    R13,R2                           COPY WORK AREA ADDRESS
         USING DXDMAIN,R13                      DEFINE WORK AREA BASE
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY
         OI    COMM_FLAGS,$COMM_NO_FORMAT       MAY NOT CHANGE FORMAT
         GETMAIN RU,                                                   +
               LV=STORCOMM_L,                                          +
               LOC=ANY
         LR    R9,R1                            COPY ADDRESS
         USING STORCOMM,R9
         LR    R0,R1
         LA    R1,STORCOMM_L
         SR    R14,R14
         SR    R15,R15
         MVCL  R0,R14                           INITIALIZE STORCOMM
         ST    R9,SESS_COMM                     SAVE STORCOMM ADDRESS
         ST    R9,DXD_STORCOMM                  SAVE STORCOMM ADDRESS
         MVC   STORCOMM_EYE,STORCOMM_ID         IDENTIFY CONTROL BLOCK
         L     R8,COMM_OSSPFD                   SPF DATA AREA
         USING OSSPFD,R8                        DEFINE BASE
         MVC   SESS_DSN(L'STOR_DSN),STOR_DSN
* ------------------------------------------------------------------- *
*        GETMAIN VDATA buffer                                         *
* ------------------------------------------------------------------- *
         ITRACE ID=GETVDATA
         LA    R1,STOR_PANEL
         MVC   STORCOMM_PANEL_NAME,STOR_PANEL
         MVI   COMM_VDATA_FUNC,$VDATA_GETMAIN
         L     R15,COMM_V_OSVDATA
         BALR  R14,R15                          GETMAIN VDATA STORAGE
         ST    R0,STORCOMM_VDATA_LENGTH         SAVE LENGTH
         ST    R1,STORCOMM_VDATA_ADDR           SAVE ADDRESS
         ITRACE ID=VDATA,                                              +
               RDATA1=R1,                                              +
               RDATA2=R0
         MVC   STORCOMM_VDEPTH,SPF_VDEPTH       SAVE DEPTH
         MVC   STORCOMM_VWIDTH,SPF_VWIDTH       SAVE WIDTH
* ------------------------------------------------------------------- *
*        GETMAIN point and shoot table                                *
* ------------------------------------------------------------------- *
         L     R0,STORCOMM_VDEPTH               SCREEN DEPTH
         MH    R0,=Y(PNS_L)                     COMPUTE PNS TABLE SIZE
         ST    R0,STORCOMM_PNS_TABLE_LENGTH     SAVE LENGTH
         GETMAIN RU,                                                   +
               LV=(0),                                                 +
               LOC=ANY
         ST    R1,STORCOMM_PNS_TABLE_ADDR       SAVE PNS TABLE ADDR
         ITRACE ID=PNS,                                                +
               RDATA1=R1,                                              +
               DATA2=(STORCOMM_PNS_TABLE_LENGTH,4)
         MVC   SESS_MESSAGES,V_STORMSGS
* ------------------------------------------------------------------- *
*        Initial function is STOR, home address space, address 0      *
* ------------------------------------------------------------------- *
         MVC   STORCOMM_FUNCTION_ADDR,V_STORSTOR
         USING PSA,R0
         L     R1,PSAAOLD                        OUR ASCB ADDRESS
         USING ASCB,R1
         ST    R1,STORCOMM_ASCB                  SET ADDRESS
         ICM   R2,15,ASCBJBNS                    JOB NAME ADDRESS
         MVC   STORCOMM_SPACE_NAME,0(R2)
         MVC   STORCOMM_ASID,ASCBASID            SET ASID
         XC    STORCOMM_DATA_ADDR,STORCOMM_DATA_ADDR
         XC    STORCOMM_DATA_DISP,STORCOMM_DATA_DISP
* ------------------------------------------------------------------- *
*        Authorize ourselves to all other address spaces              *
* ------------------------------------------------------------------- *
         TM    COMM_FLAGS,$COMM_AXSET           AXSET ISSUED?
         BO    EXIT0010                         YES
         OI    COMM_FLAGS,$COMM_AUTH_ON
         L     R15,COMM_V_OSAUTH
         BALR  R14,R15                          TURN ON APF AUTH
         OI    COMM_FLAGS,$COMM_NO_FORMAT
         ITRACE ID=AXSET                        DOING AXSET
         MODESET MODE=SUP                       ENTER SUPERVISOR STATE
         LA    R1,1                             SUPERVISOR'S AIX
         AXSET AX=(R1)
         MODESET MODE=PROB                      RESET STATE
         B     EXIT0010
* ------------------------------------------------------------------- *
*                                                                     *
*                                                                     *
*        Start or resume STOR function                                *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0000 DS    0H
         L     R1,SESS_DXD_ADDR
         A     R1,DXD_START
         ST    R1,8(,R13)
         ST    R13,4(,R1)
         LR    R13,R1
         ITRACE ID=ENTRY2
* ------------------------------------------------------------------- *
*        Re-establish STORCOMM and OSSPFD addresses                   *
* ------------------------------------------------------------------- *
         L     R8,COMM_OSSPFD                   SPF DATA AREA
         L     R9,DXD_STORCOMM                  STORCOMM ADDRESS
* ------------------------------------------------------------------- *
*        Determine reason we were called                              *
* ------------------------------------------------------------------- *
         CLI   SESS_FORMAT_FLAGS,$FORMAT_CONTROL
         BE    EXIT0010                         YES
         CLI   SESS_FORMAT_FLAGS,$FORMAT_CLEANUP
         BE    EXIT0020                         YES
* ------------------------------------------------------------------- *
*        Call current function with FORMAT function                   *
* ------------------------------------------------------------------- *
MAIN0020 DS    0H
         L     R15,STORCOMM_FUNCTION_ADDR     CURRENT FUNCTION EP
         NI    STORCOMM_STATUS,255-$STORCOMM_NEW
         MVI   STORCOMM_FUNCTION,$STORCOMM_FORMAT
         ITRACE ID=FORMAT,                                             +
               RDATA1=R15
         BALR  R14,R15                        CALL DATA FORMATTER
* ------------------------------------------------------------------- *
*        Call OSDISP to display data                                  *
* ------------------------------------------------------------------- *
MAIN0030 DS    0H
         ITRACE ID=DISP
         MVC   SPF_SPACE_NAME,STORCOMM_SPACE_NAME
         UNPK  COMM_DWORD(5),STORCOMM_ASID(3)
         MVC   SPF_SPACE_ASID,COMM_DWORD
         MVC   SPF_CB_NAME,STORCOMM_CB_NAME
         MVC   SPF_CB_DESCRIPTION,STORCOMM_CB_DESCRIPTION
         TR    SPF_SPACE_ASID,COMM_HEXCHAR
         NI    STORCOMM_DATA_ADDR,X'7F'       FORCE HIGH BIT OFF
         UNPK  COMM_DWORD(9),STORCOMM_DATA_ADDR(5)
         MVC   SPF_ADDRESS,COMM_DWORD
         TR    SPF_ADDRESS,COMM_HEXCHAR
         MVC   SESS_DISP_VDATA_ADDR,STORCOMM_VDATA_ADDR
         MVC   SESS_DISP_VDATA_LENGTH,STORCOMM_VDATA_LENGTH
         MVC   SESS_DISP_CURSOR,STORCOMM_CURSOR
         L     R15,COMM_V_OSDISP
         BALR  R14,R15                        CALL OSDISP
         ITRACE ID=DISP_RC,                                            +
               DATA1=(SESS_RC,2),                                      +
               DATA2=COMM_NEW_FORMAT
         CLC   COMM_NEW_FORMAT,COMM_BLANKS    NEW FORMAT WANTED?
         BNE   EXIT0010                       YES
         CLI   COMM_SESS_FUNC,0               SESSION FUNCTION?
         BNE   EXIT0010                       YES
         OC    SESS_RC,SESS_RC                RC=0
         BNZ   EXIT0000                       NO
         CLC   =C'BOOKMARK ',SPF_ZCMD         <-- SPECIAL CASE COMMAND
         BE    MAIN0270                       PROCESS BOOKMARK COMMAND
* ------------------------------------------------------------------- *
*        Call current function to allow it to check for commands      *
* ------------------------------------------------------------------- *
         ITRACE ID=COMMAND,                                            +
               RDATA1=R15
         MVI   STORCOMM_FUNCTION,$STORCOMM_COMMAND
         L     R15,STORCOMM_FUNCTION_ADDR     CURRENT FUNCTION EP
         BALR  R14,R15                        LINK AGAIN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         ITRACE ID=CMD_STAT,                                           +
               DATA1=(STORCOMM_STATUS,1)
         CLI   STORCOMM_STATUS,$STORCOMM_NEW  NEW STOR FUNCTION?
         BE    MAIN0170                       YES
         CLI   STORCOMM_STATUS,$STORCOMM_PNS  CALL FOR POINT-AND-SHOOT?
         BE    MAIN0160                       YES
         CLI   STORCOMM_STATUS,$STORCOMM_ERROR
         BE    MAIN0030                       YES
* ------------------------------------------------------------------- *
*                                                                     *
*        If there was a command in ZCMD and the current fuction       *
*        'recoginzed' it, it will have processed the command and      *
*        set SPF_ZCMD back to blanks.                                 *
*                                                                     *
*        If the command was not recognized, SPF_ZCMD will still       *
*        be non-blank.                                                *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0040 DS    0H
         CLC   SPF_ZCMD,COMM_BLANKS           COMMAND PRESENT?
         BE    MAIN0020                       NO
         ITRACE ID=SEARCH,                                             +
               DATA1=SPF_ZCMD
         MVC   SESS_COMMAND_ADDRESS,OPERAND_01_ADDRESS
         MVC   SESS_COMMAND_LENGTH,OPERAND_01_LENGTH
         LA    R0,COMMAND_TABLE
         ST    R0,COMM_MAIN_COMMAND_TABLE
         XC    COMM_SUB_COMMAND_TABLE,COMM_SUB_COMMAND_TABLE
         MVI   SESS_COMMAND_FLAGS,$SESS_COMMAND_SEARCH
         L     R15,COMM_V_OSCMD               OSCMD ENTRY POINT
         BALR  R14,R15                        SEARCH COMMANDS
         TM    SESS_COMMAND_FLAGS,$SESS_COMMAND_FOUND
         BO    MAIN0150                       YES
* ------------------------------------------------------------------- *
*        Command not in table.  It could be an address                *
* ------------------------------------------------------------------- *
         CLC   COMM_OPERANDS_NBR,H1           ONE OPERAND?
         BNE   MAIN0050                       NO
         ITRACE ID=CALLADDR
         L     R15,V_STORADDR                 STORADDR EP
         BALR  R14,R15                        CALL STORADDR
         LTR   R15,R15                        WAS IT AN ADDRESS?
         BZ    MAIN0170                       YES
* ------------------------------------------------------------------- *
*        Not an address, check for "+" or "-" displacment             *
* ------------------------------------------------------------------- *
MAIN0050 DS    0H
         ICM   R1,15,OPERAND_01_ADDRESS
         CLI   0(R1),C'+'                     PLUS SIGN?
         BE    MAIN0060                       YES
         CLI   0(R1),C'-'                     MINUS SIGN?
         BNE   MAIN0070                       NO
MAIN0060 DS    0H
         ITRACE ID=CALLDISP
         L     R15,V_STORDISP                 STORDISP ENTRY POINT
         BALR  R14,R15                        CALL STORDISP
         ITRACE ID=DISP_RC,                                            +
               RDATA1=R15
         LTR   R15,R15                        VALID DISP?
         BZ    MAIN0170                       YES
* ------------------------------------------------------------------- *
*        Last chance.. check for bookmark command                     *
* ------------------------------------------------------------------- *
MAIN0070 DS    0H
         ICM   R1,15,OPERAND_01_ADDRESS
         CLI   0(R1),C'.'                     BOOKMARK COMMAND?
         BE    MAIN0270                       YES
         B     ERR0010                        OTHERWISE NOT RECOGNIZED
* ------------------------------------------------------------------- *
*        Primary command has been located                             *
* ------------------------------------------------------------------- *
MAIN0150 DS    0H
         ITRACE ID=CMD_FND,                                            +
               RDATA1=R15
         C     R15,=A(MAIN0290)               DISPLAY COMMANDS?
         BER   R15                            YES.. BYPASS
         C     R15,=A(MAIN0315)               FIND?
         BER   R15                            YES.. BYPASS
         ST    R15,STORCOMM_NEW_FUNCTION      SET NEW FUNCTION ADDR
         B     MAIN0170
* ------------------------------------------------------------------- *
*        Call current function for point-and-shoot                    *
* ------------------------------------------------------------------- *
MAIN0160 DS    0H
         ITRACE ID=PNS
         MVI   STORCOMM_FUNCTION,$STORCOMM_PNS
         L     R15,STORCOMM_FUNCTION_ADDR     CURRENT FUNCTION
         BALR  R14,R15                        LINK FOR POINT-AND-SHOOT
* ------------------------------------------------------------------- *
*        New function.  Allow current function to 'clean up'          *
* ------------------------------------------------------------------- *
MAIN0170 DS    0H
         ITRACE ID=CLEAN_UP
         MVI   STORCOMM_FUNCTION,$STORCOMM_CLEANUP
         ICM   R15,15,STORCOMM_FUNCTION_ADDR  CURRENT FUNCTION
         BZ    MAIN0180                       NONE..
         BALR  R14,R15                        CLEAN UP
         XC    COMM_SUB_COMMAND_TABLE,COMM_SUB_COMMAND_TABLE
MAIN0180 DS    0H
         OC    STORCOMM_NEW_FUNCTION,STORCOMM_NEW_FUNCTION
         BZ    MAIN0020
         ITRACE ID=NEW_FUNC,                                           +
               DATA1=(STORCOMM_NEW_FUNCTION,4),                        +
               DATA2=(STORCOMM_DATA_ADDR,4)
         MVC   STORCOMM_FUNCTION_ADDR,STORCOMM_NEW_FUNCTION
         XC    STORCOMM_NEW_FUNCTION,STORCOMM_NEW_FUNCTION
         B     MAIN0020
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0200 DS    0H
         ITRACE ID=ACEE
         L     R15,V_STORACEE
         BALR  R14,R15
         B     MAIN0020
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0210 DS    0H
         ITRACE ID=ACT
         MVC   STORCOMM_FUNCTION_ADDR,V_STORACT
         MVC   SPF_ZCMD,COMM_BLANKS
         B     MAIN0020
* ------------------------------------------------------------------- *
*        Process "ASCB" commands                                      *
* ------------------------------------------------------------------- *
MAIN0220 DS    0H
         ITRACE ID=ASCB
         L     R15,V_STORASCB
         BALR  R14,R15
         B     MAIN0020
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0230 DS    0H
         ITRACE ID=ASID
         L     R15,V_STORASID
         BALR  R14,R15
         B     MAIN0020
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0240 DS    0H
         ITRACE ID=ASSB
         L     R15,V_STORASSB
         BALR  R14,R15
         B     MAIN0020
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0250 DS    0H
         ITRACE ID=ASXB
         L     R15,V_STORASXB
         BALR  R14,R15
         B     MAIN0020
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0260 DS    0H
         ITRACE ID=BASEA
         L     R15,V_STORBASE
         BALR  R14,R15
         B     MAIN0020
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0270 DS    0H
         ITRACE ID=BOOKMARK
         L     R15,V_STORBOOK                 BOOKMARK EP
         BALR  R14,R15                        CALL BOOKMARK
         ITRACE ID=NAME_RC,                                            +
               RDATA1=R15
         LTR   R15,R15                        SUCCESSFUL?
         BZ    MAIN0170                       YES
         B     MAIN0020                       DISPLAY WITH MESSAGE
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0280 DS    0H
         ITRACE ID=CB
         MVC   STORCOMM_FUNCTION_ADDR,V_STORCB
         B     MAIN0020
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0290 DS    0H
         ITRACE ID=LISTCMD
         MVC   SPF_ZCMD,COMM_BLANKS
         MVI   SESS_COMMAND_FLAGS,$SESS_COMMAND_DISPLAY
         LA    R0,COMMAND_TABLE
         ST    R0,COMM_MAIN_COMMAND_TABLE
         XC    COMM_SUB_COMMAND_TABLE,COMM_SUB_COMMAND_TABLE
         L     R15,COMM_V_OSCMD
         BALR  R14,R15                        DISPLAY COMMANDS
         ITRACE ID=CMD_RC
         B     MAIN0020
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0300 DS    0H
         ITRACE ID=CVT
         L     R15,V_STORCVT
         BALR  R14,R15
         B     MAIN0020
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0310 DS    0H
         ITRACE ID=CSCB
         L     R15,V_STORCSCB
         BALR  R14,R15
         B     MAIN0020
* ------------------------------------------------------------------- *
*                                                                     *
*                                                                     *
*     Find is an unusual command.                                     *
*                                                                     *
*     The user can enter "F ?" or "FIND ?" to see what the current    *
*     "find arguement" is.  When the user enters "F ?" or "FIND ?",   *
*     The current function will be called which will in turn call     *
*     OSFIND.  OSFIND will have re-created the "FIND" command and     *
*     put it in SPF_ZCMD.  Normally when a command is recognized      *
*     and processed, it is "cleared" to blanks.  "F ?" or "FIND ?"    *
*     is that one exeception that makes programming difficult.        *
*                                                                     *
*     All we need to do is display the same data with the             *
*     reconstructed "FIND" command.                                   *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0315 DS    0H
         ITRACE ID=FIND
         B     MAIN0030
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0320 DS    0H
         ITRACE ID=IPA
         L     R15,V_STORIPA
         BALR  R14,R15
         B     MAIN0020
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0330 DS    0H
         ITRACE ID=JESCT
         L     R15,V_STORJES
         BALR  R14,R15
         B     MAIN0020
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0340 DS    0H
         ITRACE ID=JSCB
         L     R15,V_STORJSCB
         BALR  R14,R15
         B     MAIN0020
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0350 DS    0H
         ITRACE ID=MT
         L     R15,V_STORMT
         BALR  R14,R15
         B     MAIN0020
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0360 DS    0H
         ITRACE ID=MTT
         L     R15,V_STORMTT
         BALR  R14,R15
         B     MAIN0040                 <-- THIS ONE IS DIFFERENT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0370 DS    0H
         ITRACE ID=OUCB
         L     R15,V_STOROUCB
         BALR  R14,R15
         B     MAIN0020
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0380 DS    0H
         ITRACE ID=OUXB
         L     R15,V_STOROUXB
         BALR  R14,R15
         B     MAIN0020
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0390 DS    0H
         ITRACE ID=PSA
         L     R15,V_STORPSA
         BALR  R14,R15
         B     MAIN0020
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0400 DS    0H
         ITRACE ID=RCVT
         L     R15,V_STORRCVT
         BALR  R14,R15
         B     MAIN0020
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0410 DS    0H
         ITRACE ID=TCT
         L     R15,V_STORTCT
         BALR  R14,R15
         B     MAIN0020
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0420 DS    0H
         ITRACE ID=TIOT
         L     R15,V_STORTIOT
         BALR  R14,R15
         B     MAIN0020
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0430 DS    0H
         ITRACE ID=UCB
         L     R15,V_STORUCB
         BALR  R14,R15
         B     MAIN0020
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0010  DS    0H
         MVI   COMM_MSG_ID+1,1
         B     ERR0100
ERR0100  DS    0H
         MVI   COMM_MSG_ID,0
         MVC   COMM_MSG_CSECT,MODID
         L     R15,COMM_V_OSMSG
         BALR  R14,R15
         MVC   SPF_MSG_1,COMM_MSG_1            COPY MESSAGE
         MVC   SPF_MSG_2,COMM_MSG_2            COPY MESSAGE
         MVC   SPF_MSG_3,COMM_MSG_3            COPY MESSAGE
         MVC   SPF_MSG_4,COMM_MSG_4            COPY MESSAGE
         MVC   SPF_MSG_5,COMM_MSG_5            COPY MESSAGE
         B     MAIN0030
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=ENDING
         MVI   COMM_SESS_FUNC,$SESS_REMOVE     SET FUNCTION
         MVI   STORCOMM_FUNCTION,$STORCOMM_CLEANUP
         MVI   STORCOMM_STATUS,$STORCOMM_OK    RESET STATUS
         ICM   R15,15,STORCOMM_FUNCTION_ADDR   CURRENT FUNCTION
         BZ    EXIT0010                        NONE..
         BALR  R14,R15                         CLEAN UP
EXIT0010 DS    0H
         ITRACE ID=EXIT0010
         NI    COMM_FLAGS,255-$COMM_AUTH_ON
         L     R15,COMM_V_OSAUTH
         BALR  R14,R15                         TURN OFF APF AUTH
         NI    COMM_FLAGS,255-$COMM_NO_FORMAT
         XC    SESS_MESSAGES,SESS_MESSAGES
         L     R13,4(,R13)                     RESTORE R13
         LM    R14,R12,12(R13)                 RESTORE REGISTERS
         SR    R15,R15                         SET RC
         BR    R14
EXIT0020 DS    0H
         ITRACE ID=EXIT0020
         ICM   R1,15,STORCOMM_VDATA_ADDR       VDATA BUFFER ADDRESS
         BZ    EXIT0030                        NOT GETMAINED
         L     R0,STORCOMM_VDATA_LENGTH
         MVI   COMM_VDATA_FUNC,$VDATA_FREEMAIN
         L     R15,COMM_V_OSVDATA
         BALR  R14,R15                         FREE VDATA
         XC    STORCOMM_VDATA_ADDR,STORCOMM_VDATA_ADDR
         XC    STORCOMM_VDATA_LENGTH,STORCOMM_VDATA_LENGTH
EXIT0030 DS    0H
         ICM   R1,15,STORCOMM_PNS_TABLE_ADDR   POINT AND SHOOT TABLE
         BZ    EXIT0040                        NOT GETMAINED
         L     R0,STORCOMM_PNS_TABLE_LENGTH    POINT AND SHOOT LENGTH
         ITRACE ID=FREE_PNS,                                           +
               RDATA1=R1,                                              +
               RDATA2=R0
         FREEMAIN RU,                          FREE POINT AND SHOOT    +
               A=(1),                                                  +
               LV=(0)
         XC    STORCOMM_PNS_TABLE_ADDR,STORCOMM_PNS_TABLE_ADDR
         XC    STORCOMM_PNS_TABLE_LENGTH,STORCOMM_PNS_TABLE_LENGTH
EXIT0040 DS    0H
         ICM   R1,15,STORCOMM_BOOKMARK_CHAIN   FIRST BOOKMARK BLOCK
         BZ    EXIT0060                        NONE ON CHAIN
         USING BOOKMARK_DSECT,R1
EXIT0050 DS    0H
         L     R2,BOOKMARK_NEXT                NEXT BOOKMARK BLOCK
         ITRACE ID=FREEBOOK,                                           +
               RDATA1=R1
         FREEMAIN RU,                          FREE THIS BOOKMARK BLOCK++
               A=(1),                                                  +
               LV=BOOKMARK_DSECT_L
         LTR   R1,R2                           ANOTHER BOOKMARK BLOCK?
         BNZ   EXIT0050                        YES
EXIT0060 DS    0H
         NI    COMM_FLAGS,255-$COMM_AUTH_ON
         L     R15,COMM_V_OSAUTH
         BALR  R14,R15                         TURN OFF APF AUTH
         NI    COMM_FLAGS,255-$COMM_NO_FORMAT
         XC    SESS_MESSAGES,SESS_MESSAGES
         MVI   COMM_SESS_FUNC,$SESS_REMOVE     SET SESSION FUNCTION
         L     R13,4(,R13)                     RESTORE R13
         L     R1,SESS_DXD_ADDR                COPY WORK AREA ADDR
         L     R0,DXD_SIZE
         FREEMAIN RU,                          FREEMAIN WORK AREA      +
               A=(R1),                                                 +
               LV=(0)
         LM    R14,R12,12(R13)                 RESTORE REGISTERS
         SR    R15,R15                         SET RC
         BR    R14                             RETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXD_SIZE       CXD
DXD_START      DC       Q(DXDMAIN)
V_STORACEE     DC       V(STORACEE)
V_STORACT      DC       V(STORACT)
V_STORADDR     DC       V(STORADDR)
V_STORASCB     DC       V(STORASCB)
V_STORASID     DC       V(STORASID)
V_STORASSB     DC       V(STORASSB)
V_STORASXB     DC       V(STORASXB)
V_STORBASE     DC       V(STORBASE)
V_STORBOOK     DC       V(STORBOOK)
V_STORCB       DC       V(STORCB)
V_STORCSCB     DC       V(STORCSCB)
V_STORCVT      DC       V(STORCVT)
V_STORDISP     DC       V(STORDISP)
V_STORIPA      DC       V(STORIPA)
V_STORJES      DC       V(STORJES)
V_STORJSCB     DC       V(STORJSCB)
V_STORMSGS     DC       V(STORMSGS)
V_STORMT       DC       V(STORMT)
V_STORMTT      DC       V(STORMTT)
V_STOROUCB     DC       V(STOROUCB)
V_STOROUXB     DC       V(STOROUXB)
V_STORPSA      DC       V(STORPSA)
V_STORRCVT     DC       V(STORRCVT)
V_STORSTOR     DC       V(STORSTOR)
V_STORTCT      DC       V(STORTCT)
V_STORTIOT     DC       V(STORTIOT)
V_STORUCB      DC       V(STORUCB)

H1             DC       H'1'

STOR_PANEL     DC       CL8'STORSTOR'
STORCOMM_ID    DC       CL8'STORCOMM'

STOR_DSN       DC       C'Storge display'


               LTORG

* ------------------------------------------------------------------- *
*                                                                     *
*                   Primary commands                                  *
*                                                                     *
* ------------------------------------------------------------------- *
COMMAND_TABLE   DS       0C
                CMD      xxxx,ERR0010,'Where xxxx is an hex address'
                CMD      +xxx,ERR0010,'Where xxx is a hex displacement'
                CMD      -xxx,ERR0010,'Where xxx is a hex displacement'
                CMD      .name,MAIN0270,'Create bookmark'
                CMD      ..name,MAIN0270,'Restore bookmark'
                CMD      ACEE,MAIN0200,'Display ACEE'
                CMD      ACT,MAIN0210,'Display active regions'
                CMD      ASCB,MAIN0220,'Set current addr to ASCB'
                CMD      ASID,MAIN0230,'Set address space by ID'
                CMD      ASSB,MAIN0240,'Set current addr to ASSB'
                CMD      ASXB,MAIN0250,'Set current addr to ASXB'
                CMD      BASEA,MAIN0260,'Master scheduler control area'
                CMD      BOOKMARK,MAIN0270,'List bookmarks'
                CMD      CB,MAIN0280,'Format control block'
                CMD      CSCB,MAIN0310,'Set current addr to CSCB'
                CMD      CVT,MAIN0300,'Set current addr to CVT'
                CMD      DEV,MAIN0430,'Display UCB for device xxx'
                CMD      F,MAIN0315,'Find a character or hex string'
                CMD      FIND,MAIN0315,'Find a character or hex string'
                CMD      IPA,MAIN0320,'IPL initialization parameters'
                CMD      JESCT,MAIN0330,'Set current addr to JESCT'
                CMD      JSCB,MAIN0340,'Set current addr to JSCB'
                CMD      MT,MAIN0350,'Set current addr to master trace'
                CMD      MTT,MAIN0360,'Display master trace table'
                CMD      OUCB,MAIN0370,'Set current addr to OUCB'
                CMD      OUXB,MAIN0380,'Set current addr to OUXB'
                CMD      PSA,MAIN0390,'Set current addr to PSA'
                CMD      RCVT,MAIN0400,'Set current addr to RACF RCVT'
                CMD      STOR,STORSTOR,'Display storage unformatted'
                CMD      TASK,STORTASK,'Display tasks (TCB''s)'
                CMD      TCT,MAIN0410,'Set current addr to TCT'
                CMD      TIOT,MAIN0420,'Set current addr to TIOT'
                CMD      UCB,MAIN0430,'Display UCB xxx'
                CMD      UNIT,MAIN0430,'Display UCB for device xxx'
                CMD      ??,MAIN0290,'List available commands'
                DC       X'FF'
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDMAIN          DSECT
                 COPY     DXDPREF
DXD_STORCOMM     DS       A

DXD_UCBSCAN      UCBSCAN  MF=(L,UCBSCAN_PARM)

DXD_UCBSCAN_WORK DS       XL100
DXD_UCBSCAN_UCB  DS       XL4
DXD_DEVICE_NBR   DS       XL2

DXD_RC           DS       H

                 LOADREQ  DSECT=NO
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ZCMD_DSECT       DSECT
ZCMD_COMMAND     DS       CL10
ZCMD_LENGTH      DS       XL2
ZCMD_ADDRESS     DS       XL4
ZCMD_DSECT_L     EQU      *-ZCMD_DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 BPXYSTAT DSECT=YES,LIST=NO
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 STORCOMM
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COPY     PNS
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COPY     BOOKMARK
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COPY     OPERAND
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 IBMMAC   CVT=YES
                 IBMMAC   ASCB=YES
                 IBMMAC   PSA=YES
                 IBMMAC   TCB=YES
                 IBMMAC   TIOT=YES
                 IBMMAC   UCB=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 OSSPFD   TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COPY     ATTRS
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COPY     REGEQU
                 END      STORMAIN
