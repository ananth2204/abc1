* ------------------------------------------------------------------- *
*                                                                     *
*  Module name: STORASXB                                              *
*                                                                     *
*  Set current address to selected address space's ASXB               *
*                                                                     *
* ------------------------------------------------------------------- *
STORASXB CSECT
STORASXB AMODE 31
STORASXB RMODE ANY
         USING OSCOMM,R12
         USING SESSION,R11
         USING STORASXB,R15
         B     INIT0000
MODID    DC    CL8'STORASXB'
         DC    CL8'&SYSDATE'
         DC    CL7'&SYSTIME'
INIT0000 DS    0H
         STM   R14,R12,12(R13)                 SAVE REGS
         LR    R10,R15                         COPY ENTRY POINT
         DROP  R15
         USING STORASXB,R10                    DEFINE BASE
         USING STORCOMM,R9                     DEFINE BASE
         L     R15,SESS_DXD_ADDR               WORK AREA BASE
         A     R15,DXD_START                   PLUS DISP
         ST    R13,4(,R15)                     SAVE R13
         ST    R15,8(,R13)                     CHAIN SAVE AREA
         LR    R13,R15
         USING DXDASXB,R13                     DEFINE WORK AREA BASE
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY
         L     R8,COMM_OSSPFD
         USING OSSPFD,R8
         MVC   SPF_ZCMD,COMM_BLANKS
         L     R5,STORCOMM_ASCB                ASCB ADDRESS
         USING ASCB,R5
         CLC   ASCBASCB,C_ASCB                 ASCB LOOK OK?
         BNE   ERR0010                         NO
         MVC   STORCOMM_DATA_ADDR,ASCBASXB     ASXB ADDRESS
         ITRACE ID=ASXBADDR,                                           +
               DATA1=(STORCOMM_DATA_ADDR,4)
         XC    STORCOMM_DATA_DISP,STORCOMM_DATA_DISP
         CLC   SESS_DISP_PANEL,STOR_PANEL
         BE    ASXB0010
         MVC   STORCOMM_FUNCTION_ADDR,V_STORCB
         L     R1,V_ASXBTBL                    FORMATTING INFO TABLE
         ST    R1,STORCOMM_CB_TABLE            SET ADRESS
         MVC   STORCOMM_CB_NAME,0(R1)          COPY CB NAME
         MVC   STORCOMM_CB_DESCRIPTION,8(R1)   COPY CB DESCRIPTION
         MVC   SESS_DISP_PANEL,48(R1)          COPY PANEL NAME
         MVC   STORCOMM_PANEL_NAME,48(R1)      COPY PANEL NAME
         LA    R1,56(,R1)                      FIRST FIELD
         ST    R1,STORCOMM_CB_FIRST            SET ADDRES
         B     EXIT0000
ASXB0010 DS    0H
         MVC   STORCOMM_FUNCTION_ADDR,V_STORSTOR
         MVC   STORCOMM_CB_NAME,COMM_BLANKS
         MVC   STORCOMM_CB_DESCRIPTION,COMM_BLANKS
         XC    STORCOMM_CB_TABLE,STORCOMM_CB_TABLE
         XC    STORCOMM_CB_FIRST,STORCOMM_CB_FIRST
         XC    STORCOMM_CB_LAST,STORCOMM_CB_LAST
         B     EXIT0000
ERR0010  DS    0H
         ITRACE ID=BAD_ASCB,                                           +
               RDATA1=R5
         DC    H'0'
EXIT0000 DS    0H
         ITRACE ID=EXIT
         L     R13,4(,R13)                     RESTORE R13
         LM    R14,R12,12(R13)                 RESTORE REGISTERS
         SR    R15,R15                         SET RC
         BR    R14
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXD_START      DC       Q(DXDASXB)
V_ASXBTBL      DC       V(ASXBTBL)
V_STORCB       DC       V(STORCB)
V_STORSTOR     DC       V(STORSTOR)

C_ASCB         DC       CL4'ASCB'
STOR_PANEL     DC       CL8'STORSTOR'

               LTORG

* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDASXB        DSECT
               COPY     DXDPREF
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               BPXYSTAT DSECT=YES,LIST=NO
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               STORCOMM
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY     OPERAND
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               IBMMAC   ASCB=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY     FIELDD
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               OSSPFD   TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY     REGEQU
               END      STORASXB
