* ------------------------------------------------------------------- *
*                                                                     *
*  Module name: STORCSCB                                              *
*                                                                     *
*  Set current address to selected address space's CSCB               *
*                                                                     *
* ------------------------------------------------------------------- *
STORCSCB CSECT
STORCSCB AMODE 31
STORCSCB RMODE ANY
         USING OSCOMM,R12
         USING SESSION,R11
         USING STORCSCB,R15
         B     INIT0000
MODID    DC    CL8'STORCSCB'
         DC    CL8'&SYSDATE'
         DC    CL7'&SYSTIME'
INIT0000 DS    0H
         STM   R14,R12,12(R13)                 SAVE REGS
         LR    R10,R15                         COPY ENTRY POINT
         DROP  R15
         USING STORCSCB,R10                    DEFINE BASE
         USING STORCOMM,R9                     DEFINE BASE
         L     R15,SESS_DXD_ADDR               WORK AREA BASE
         A     R15,DXD_START                   PLUS DISP
         ST    R13,4(,R15)                     SAVE R13
         ST    R15,8(,R13)                     CHAIN SAVE AREA
         LR    R13,R15
         USING DXDCSCB,R13                     DEFINE WORK AREA BASE
         ITRACE ID=ENTRY
         MVC   DXD_CSECT,MODID
         L     R8,COMM_OSSPFD
         USING OSSPFD,R8
         MVC   SPF_ZCMD,COMM_BLANKS
         OI    COMM_FLAGS,$COMM_AUTH_ON
         L     R15,COMM_V_OSAUTH
         BALR  R14,R15                         TURN ON APF AUTH
         MODESET KEY=ZERO
         LH    R1,STORCOMM_ASID
         ITRACE ID=SSAR,                                               +
               RDATA1=R1
         SSAR  (R1)
         LAM   R5,R5,F1                        SET AR
         SAC   512                             ENTER AR MODE
         MVC   SPF_ZCMD,COMM_BLANKS
         L     R5,STORCOMM_ASCB                ASCB ADDRESS
         USING ASCB,R5
         MVC   STORCOMM_DATA_ADDR,ASCBCSCB     CSCB ADDRESS
         ITRACE ID=CSCB,                                               +
               DATA1=(STORCOMM_DATA_ADDR,4)
         SAC   0                               TURN OFF AR MODE
         LAM   R5,R5,F0                        RESET AR
         EPAR  (R1)
         ITRACE ID=RESET,                                              +
               RDATA1=R1
         SSAR  (R1)
         MODESET KEY=NZERO                     REQUEST NORMAL KEY
         NI    COMM_FLAGS,255-$COMM_AUTH_ON
         L     R15,COMM_V_OSAUTH
         BALR  R14,R15                         TURN OFF APF AUTH
         ITRACE ID=CSCBADDR,                                           +
               DATA1=(STORCOMM_DATA_ADDR,4)
         XC    STORCOMM_DATA_DISP,STORCOMM_DATA_DISP
         CLC   SESS_DISP_PANEL,STOR_PANEL
         BE    CSCB0010
         MVC   STORCOMM_FUNCTION_ADDR,V_STORCB
         L     R1,V_CSCBTBL                    FORMATTING INFO TABLE
         ST    R1,STORCOMM_CB_TABLE            SET ADDRESS
         MVC   STORCOMM_CB_NAME,0(R1)          COPY CB NAME
         MVC   STORCOMM_CB_DESCRIPTION,8(R1)   COPY CB DESCRIPTION
         MVC   SESS_DISP_PANEL,48(R1)          COPY PANEL ANME
         MVC   STORCOMM_PANEL_NAME,48(R1)      COPY PANEL NAME
         LA    R1,56(,R1)                      FIRST FIELD
         ST    R1,STORCOMM_CB_FIRST            SET ADDRESS
         B     EXIT0000
CSCB0010 DS    0H
         MVC   STORCOMM_FUNCTION_ADDR,V_STORSTOR
         MVC   STORCOMM_CB_NAME,COMM_BLANKS
         MVC   STORCOMM_CB_DESCRIPTION,COMM_BLANKS
         XC    STORCOMM_CB_TABLE,STORCOMM_CB_TABLE
         XC    STORCOMM_CB_FIRST,STORCOMM_CB_FIRST
         XC    STORCOMM_CB_LAST,STORCOMM_CB_LAST
EXIT0000 DS    0H
         ITRACE ID=EXIT
         L     R13,4(,R13)                     RESTORE R13
         LM    R14,R12,12(R13)                 RESTORE REGISTERS
         SR    R15,R15                         SET RC
         BR    R14
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXD_START      DC       Q(DXDCSCB)
V_CSCBTBL      DC       V(CSCBTBL)
V_STORCB       DC       V(STORCB)
V_STORSTOR     DC       V(STORSTOR)

F0             DC       F'0'
F1             DC       F'1'

STOR_PANEL     DC       CL8'STORSTOR'

               LTORG

* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDCSCB        DSECT
               COPY     DXDPREF
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               BPXYSTAT DSECT=YES,LIST=NO
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               STORCOMM
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY     OPERAND
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               IBMMAC   ASCB=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY     FIELDD
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               OSSPFD   TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY     REGEQU
               END      STORCSCB
