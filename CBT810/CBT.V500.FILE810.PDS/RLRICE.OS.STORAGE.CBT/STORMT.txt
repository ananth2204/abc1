* ------------------------------------------------------------------- *
*                                                                     *
*  Module name: STORMT                                                *
*                                                                     *
*  Set current address to master trace table                          *
*                                                                     *
* ------------------------------------------------------------------- *
STORMT   CSECT
STORMT   AMODE 31
STORMT   RMODE ANY
         USING OSCOMM,R12
         USING SESSION,R11
         USING STORMT,R15
         B     INIT0000
MODID    DC    CL8'STORMT'
         DC    CL8'&SYSDATE'
         DC    CL7'&SYSTIME'
INIT0000 DS    0H
         STM   R14,R12,12(R13)                 SAVE REGS
         LR    R10,R15                         COPY ENTRY POINT
         DROP  R15
         USING STORMT,R10                      DEFINE BASE
         USING STORCOMM,R9                     DEFINE BASE
         L     R15,SESS_DXD_ADDR               WORK AREA BASE
         A     R15,DXD_START                   PLUS DISP
         ST    R13,4(,R15)                     SAVE R13
         ST    R15,8(,R13)                     CHAIN SAVE AREA
         LR    R13,R15
         USING DXDMT,R13                       DEFINE WORK AREA BASE
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY
         L     R8,COMM_OSSPFD
         USING OSSPFD,R8
         MVC   SPF_ZCMD,COMM_BLANKS
         L     R1,CVTPTR
         USING CVT,R1
         CLC   STORCOMM_ASID,H1                ALREADY IN MASTER A/S?
         BE    INIT0010                        YES
         MVC   STORCOMM_ASID,H1                FORCE ASID 1
         MVC   STORCOMM_SPACE_NAME,MASTER_NAME NAME IS '*MASTER'
         L     R2,CVTASVT                      ASVT ADDRESS
         USING ASVT,R2
         MVC   STORCOMM_ASCB,ASVTFRST+4        COPY MASTER'S ASCB ADDR
         MVC   SPF_MSG_3(MSG01_L),MSG01
INIT0010 DS    0H
         L     R2,CVTMSER                      MASTER DATA AREA
         USING BASE,R2
         MVC   STORCOMM_DATA_ADDR,BAMTTBL      MASTER TRACE TABLE ADDR
         ITRACE ID=BASE,                                               +
               DATA1=(STORCOMM_DATA_ADDR,4)
         XC    STORCOMM_DATA_DISP,STORCOMM_DATA_DISP
         CLC   SESS_DISP_PANEL,STOR_PANEL
         BE    BASE0010
         MVC   STORCOMM_FUNCTION_ADDR,V_STORCB
         L     R1,V_MTTBL                      FORMATTING INFO TABLE
         ST    R1,STORCOMM_CB_TABLE            SET ADRESS
         MVC   STORCOMM_CB_NAME,0(R1)          COPY CONTROL BLOCK NAME
         MVC   STORCOMM_CB_DESCRIPTION,8(R1)   COPY CONTROL BLOCK DESC
         MVC   SESS_DISP_PANEL,48(R1)          COPY PANEL NAME
         MVC   STORCOMM_PANEL_NAME,48(R1)      COPY PANEL NAME
         LA    R1,56(,R1)                      FIRST FIELD
         ST    R1,STORCOMM_CB_FIRST            SET ADDRESS
         B     EXIT0000
BASE0010 DS    0H
         MVC   STORCOMM_FUNCTION_ADDR,V_STORSTOR
         MVC   STORCOMM_CB_NAME,COMM_BLANKS
         MVC   STORCOMM_CB_DESCRIPTION,COMM_BLANKS
         XC    STORCOMM_CB_TABLE,STORCOMM_CB_TABLE
         XC    STORCOMM_CB_FIRST,STORCOMM_CB_FIRST
         XC    STORCOMM_CB_LAST,STORCOMM_CB_LAST
EXIT0000 DS    0H
         ITRACE ID=EXIT
         L     R13,4(,R13)                     RESTORE R13
         LM    R14,R12,12(R13)                 RESTORE REGISTERS
         SR    R15,R15                         SET RC
         BR    R14
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXD_START      DC       Q(DXDMT)
V_MTTBL        DC       V(MTTBL)
V_STORCB       DC       V(STORCB)
V_STORSTOR     DC       V(STORSTOR)

H1             DC       H'1'

MSG01          DS       0C
               DC       C'STORBASE01I '
               DC       C'Address space forced to *MASTER*'
MSG01_L        EQU      *-MSG01

MASTER_NAME    DC       CL8'*MASTER*'
STOR_PANEL     DC       CL8'STORSTOR'

               LTORG

* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDMT          DSECT
               COPY     DXDPREF
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               IBMMAC   CVT=YES
               IBMMAC   ASCB=YES
               IHAASVT
               IEEBASEA
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               BPXYSTAT DSECT=YES,LIST=NO
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               STORCOMM
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY     OPERAND
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               OSSPFD   TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY     REGEQU
               END      STORMT
