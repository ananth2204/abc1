* ------------------------------------------------------------------- *
*                                                                     *
*                                                                     *
*    Note:  This exit does NOT need to switch to AR mode              *
*           or key zero.  The RCVT is in common storage.              *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
RCVTEX03 CSECT
RCVTEX03 AMODE 31
RCVTEX03 RMODE ANY
         USING OSCOMM,R12
         USING SESSION,R11
         USING RCVTEX03,R15
         B     INIT0000
MODID    DC    CL8'RCVTEX03'
         DC    CL8'&SYSDATE'
         DC    CL7'&SYSTIME'
INIT0000 DS    0H
         STM   R14,R12,12(R13)                SAVE REGS
         LR    R10,R15                        COPY ENTRY POINT
         DROP  R15
         USING RCVTEX03,R10                   DEFINE BASE
         USING STORCOMM,R9
         L     R15,SESS_DXD_ADDR              WORK AREA BASE
         A     R15,DXD_START                  PLUS DISP
         ST    R13,4(,R15)                    SAVE R13
         ST    R15,8(,R13)                    CHAIN SAVE AREA
         LR    R13,R15
         USING DXDRCVT3,R13                   DEFINE WORK AREA BASE
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY,                                              +
               DATA1=(STORCOMM_FUNCTION,1)
         CLI   STORCOMM_FUNCTION,$STORCOMM_COMMAND
         BE    EXIT0000
         L     R3,FIELD_EXIT_PNS
         L     R5,STORCOMM_DATA_ADDR          RCVT ADDRESS
         USING RCVT,R5
         L     R6,FIELD_EXIT_VDATA
         LH    R7,FIELD_EXIT_LINES
         MVI   STORCOMM_STATUS,$STORCOMM_OK   SET DEFAULT STATUS
         L     R8,COMM_OSSPFD
         USING OSSPFD,R8
         TM    RCVTMFL1,RCVTXUSP              EXTENDED INITUSP?
         BNO   MAIN0010                       NO
         MVC   0(MSG01_L,R6),MSG01
V        USING MSG01,R6
         MVI   V.MSG01_ATTR,$SCREEN_ATTR_HIGH_YELLOW
         MVC   V.MSG01_DESC,MSG02
         BAL   R1,LINE0000
MAIN0010 DS    0H
         TM    RCVTMFL1,RCVTXFAR              FASTAUTH?
         BNO   MAIN0020                       NO
         MVC   0(MSG01_L,R6),MSG01
         MVI   V.MSG01_ATTR,$SCREEN_ATTR_HIGH_YELLOW
         MVC   V.MSG01_DESC,MSG03
         BAL   R1,LINE0000
MAIN0020 DS    0H
         TM    RCVTMFL1,RCVTENVS              ENVIRONMENT SERVICES?
         BNO   MAIN0030                       NO
         MVC   0(MSG01_L,R6),MSG01
         MVI   V.MSG01_ATTR,$SCREEN_ATTR_HIGH_YELLOW
         MVC   V.MSG01_DESC,MSG04
         BAL   R1,LINE0000
MAIN0030 DS    0H
         TM    RCVTMFL1,RCVTX500              X500NAME?
         BNO   MAIN0040                       NO
         MVC   0(MSG01_L,R6),MSG01
         MVI   V.MSG01_ATTR,$SCREEN_ATTR_HIGH_YELLOW
         MVC   V.MSG01_DESC,MSG05
         BAL   R1,LINE0000
MAIN0040 DS    0H
         MVC   0(MSG01_L,R6),MSG01
         MVI   V.MSG01_ATTR,$SCREEN_ATTR_HIGH_YELLOW
         TM    RCVTMFL1,RCVTPSEN              ENHANCED PGMSECURITY?
         BO    MAIN0050                       YES
         MVC   V.MSG01_DESC,MSG06
         B     MAIN0060
MAIN0050 DS    0H
         MVC   V.MSG01_DESC,MSG07
MAIN0060 DS    0H
         BAL   R1,LINE0000
         MVC   0(MSG01_L,R6),MSG01
         MVI   V.MSG01_ATTR,$SCREEN_ATTR_HIGH_YELLOW
         TM    RCVTMFL1,RCVTPSWR              WARNING MODE?
         BO    MAIN0070                       YES
         MVC   V.MSG01_DESC,MSG08
         B     MAIN0080
MAIN0070 DS    0H
         MVC   V.MSG01_DESC,MSG09
MAIN0080 DS    0H
         BAL   R1,LINE0000
         MVC   0(MSG01_L,R6),MSG01
         MVI   V.MSG01_ATTR,$SCREEN_ATTR_HIGH_YELLOW
         TM    RCVTMFL1,RCVTLPAP              ATTCH1ST REQUIRED?
         BO    MAIN0090                       YES
         MVC   V.MSG01_DESC,MSG10
         B     MAIN0100
MAIN0090 DS    0H
         MVC   V.MSG01_DESC,MSG11
MAIN0100 DS    0H
         BAL   R1,LINE0000
         TM    RCVTMFL1,RCVTFGSG              FILE.GROUPOWNER.SETGID?
         BNO   exit0000                       NO
         MVC   0(MSG01_L,R6),MSG01
         MVI   V.MSG01_ATTR,$SCREEN_ATTR_HIGH_YELLOW
         MVC   V.MSG01_DESC,MSG12
         BAL   R1,LINE0000
         B     EXIT0000
* ------------------------------------------------------------------- *
*        Next line on display                                         *
* ------------------------------------------------------------------- *
LINE0000 DS    0H
         ITRACE ID=NEXTLINE,                                           +
               RDATA1=R6,                                              +
               RDATA2=R7
         LA    R3,PNS_L(,R3)
         A     R6,SPF_VWIDTH                  NEXT LINE
         BCT   R7,LINE0010                    MINUS 1 AVAILABLE LINE
         B     EXIT0000                       SCREEN FULL.. EXIT
LINE0010 DS    0H
         BR    R1
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=EXIT
         ST    R3,FIELD_EXIT_PNS
         ST    R6,FIELD_EXIT_VDATA
         STH   R7,FIELD_EXIT_LINES
         L     R13,4(,R13)                    RESTORE R13
         LM    R14,R12,12(R13)                RESTORE REGISTERS
         SR    R15,R15
         BR    R14                            RETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXD_START    DC       Q(DXDRCVT3)

MSG01        DS       0C
             DC       AL1($SCREEN_ATTR_HIGH_TURQUOISE)
MSG01_DISP   DC       CL04' '
             DC       AL01($SCREEN_ATTR_HIGH)
MSG01_NAME   DC       CL08' '
             DC       AL01($SCREEN_ATTR_NORMAL)
MSG01_DATA   DC       CL20' '
             DC       CL04' '
MSG01_ATTR   DC       AL01($SCREEN_ATTR_HIGH_GREEN)
MSG01_DESC   DC       CL40' '
MSG01_L      EQU      *-MSG01

MSG02        DC       CL40'Extended INITSUP support'
MSG03        DC       CL40'FASTAUTH RACLIST SUPPORT'
MSG04        DC       CL40'ENVIRONMENT SERVICES AVAILABLE'
MSG05        DC       CL40'X500NAME SUPPORT AVAILABLE'
MSG06        DC       CL40'BASIC PGMSECURITY'
MSG07        DC       CL40'ENHANCED PGMSECURITY'
MSG08        DC       CL40'PGMSECURITY IN FAIL MODE'
MSG09        DC       CL40'PGMSECURITY IN WARN MODE'
MSG10        DC       CL40'RACF REQ=FAE ATTCH1ST NOT REQUIRED'
MSG11        DC       CL40'RACF REQ=FAE ATTCH1ST REQUIRED'
MSG12        DC       CL40'FILE.GROUPOWNER.SETGID EXISTS'
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDRCVT3     DSECT
             COPY   DXDPREF
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
             COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
             SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
             ICHPRCVT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
             BPXYSTAT DSECT=YES,LIST=NO
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
             STORCOMM
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
             COPY     PNS
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
             OSSPFD   TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
             COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
             COPY     ATTRS
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
             COPY     REGEQU
             END      RCVTEX03
