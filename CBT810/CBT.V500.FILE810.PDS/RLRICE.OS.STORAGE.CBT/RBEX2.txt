* ------------------------------------------------------------------- *
*                                                                     *
*                                                                     *
*    Note:  This exit does NOT need to switch to AR mode              *
*           or key zero.  The RCVT is in common storage.              *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
RBEX2    CSECT
RBEX2    AMODE 31
RBEX2    RMODE ANY
         USING OSCOMM,R12
         USING SESSION,R11
         USING RBEX2,R15
         B     INIT0000
MODID    DC    CL8'RBEX2'
         DC    CL8'&SYSDATE'
         DC    CL7'&SYSTIME'
INIT0000 DS    0H
         STM   R14,R12,12(R13)                SAVE REGS
         LR    R10,R15                        COPY ENTRY POINT
         DROP  R15
         USING RBEX2,R10                      DEFINE BASE
         USING STORCOMM,R9
         L     R15,SESS_DXD_ADDR              WORK AREA BASE
         A     R15,DXD_START                  PLUS DISP
         ST    R13,4(,R15)                    SAVE R13
         ST    R15,8(,R13)                    CHAIN SAVE AREA
         LR    R13,R15
         USING DXDRB2,R13                     DEFINE WORK AREA BASE
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY,                                              +
               DATA1=(STORCOMM_FUNCTION,1)
         MVI   STORCOMM_STATUS,$STORCOMM_OK   SET DEFAULT STATUS
         CLI   STORCOMM_FUNCTION,$STORCOMM_COMMAND
         BE    EXIT0000
         L     R4,FIELD_EXIT_FIELD
         USING FIELD_DSECT,R4
         L     R5,STORCOMM_DATA_ADDR          RB OR TCB ADDRESS
         USING RBBASIC,R5
         ITRACE ID=ZERO
         OI    COMM_FLAGS,$COMM_AUTH_ON       REQUEST APF AUTHORIZATION
         L     R15,COMM_V_OSAUTH
         BALR  R14,R15                        CALL OSAUTH
         MODESET KEY=ZERO                     ENTER KEY ZERO
         NI    COMM_FLAGS,255-$COMM_AUTH_ON   REQUEST APF OFF
         L     R15,COMM_V_OSAUTH
         BALR  R14,R15                        CALL OSAUTH
         LAM   R5,R5,F1                       SET AR
         SAC   512                            ENTER AR MODE
         SR    R3,R3                          CLEAR R3
         ICM   R3,3,RBLINKB                   PREV RB OR TCB ADDRESS
         LAM   R3,R3,F1                       SET AR 3 TO SECONDARY
         USING TCB,R3
         C     R5,TCBRBP                      POINT BACK HERE?
         BE    MAIN0010                       YES
         MVC   FIELD_TABLE,V_RBTBL            USE RB TABLE
         B     EXIT0000
MAIN0010 DS    0H
         MVC   FIELD_TABLE,V_TCBTBL           USE TCB TABLE
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         SAC   0
         LAM   R3,R3,F0                       RESET AR
         LAM   R5,R5,F0                       RESET AR
         OI    COMM_FLAGS,$COMM_AUTH_ON       REQUEST APF AUTHORIZATION
         L     R15,COMM_V_OSAUTH
         BALR  R14,R15                        CALL OSAUTH
         ITRACE ID=NZERO
         MODESET KEY=NZERO                    RESET STORAGE PROTECT KEY
         NI    COMM_FLAGS,255-$COMM_AUTH_ON   REQUEST APF OFF
         L     R15,COMM_V_OSAUTH
         BALR  R14,R15                        CALL OSAUTH
         ITRACE ID=EXIT
         L     R13,4(,R13)                    RESTORE R13
         LM    R14,R12,12(R13)                RESTORE REGISTERS
         SR    R15,R15
         BR    R14                            RETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXD_START    DC     Q(DXDRB2)

V_RBTBL      DC     V(RBTBL)
V_TCBTBL     DC     V(TCBTBL)

F0           DC     F'0'
F1           DC     F'1'

* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDRB2       DSECT
             COPY     DXDPREF
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
             COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
             SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
             IHARB    DSECT=YES,LIST=YES
             IBMMAC   TCB=LIST
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
             COPY     FIELDD
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
             BPXYSTAT DSECT=YES,LIST=NO
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
             STORCOMM
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
             OSSPFD   TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
             COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
             COPY     ATTRS
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
             COPY     REGEQU
             END      RBEX2
