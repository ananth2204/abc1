.* ------------------------------------------------------------------*
.*                                                                   *
.*                        TRACE MACRO                                *
.*                                                                   *
.*       The trace function always captures the last three bytes of  *
.*       the active task's TCB address and bytes 3-7 of the active   *
.*       CSECT's name, and a trace ID.                               *
.*                                                                   *
.*       One or two data elements (8 bytes each) may be captured at  *
.*       any trace point.  The data to be captured may be specified  *
.*       in either of two ways: DATA1 or RDATA1 and DATA2 or RDATA2. *
.*       DATA1 and RDATA1 are mutually exclusive as are DATA2 and    *
.*       RDATA2.  DATA1 and DATA2 specify the address of data to be  *
.*       captured, RDATA1 and RDATA2 specify a register whose        *
.*       contents are to be captured.                                *
.*                                                                   *
.*       For example:                                                *
.*                                                                   *
.*                 ITRACE ID=POINTA,DATA1=VALUE1,RDATA2=R4           *
.*                                                                   *
.*          In this case the trace entry would include the TCB       *
.*          address, CSECT name, ID "POINTA", 8 bytes of data        *
.*          beginning at value1's address and the contents of        *
.*          register 4.                                              *
.*                                                                   *
.* ------------------------------------------------------------------*
         MACRO
&NAME    ITRACE &X,&ID=,&DATA1=,&DATA2=,&RDATA1=,&RDATA2=
         LCLA  &L
         AIF   (T'&X NE 'O').ERR0030
         AIF   (T'&DATA1 NE 'O' AND T'&RDATA1 NE 'O').ERR0010
         AIF   (T'&DATA2 NE 'O' AND T'&RDATA2 NE 'O').ERR0020
         AIF   ('&SYSPARM' EQ 'NOTRACE').MEXIT
         AIF   (T'&DATA1 EQ 'O').DATA030
         AIF   ('&DATA1'(1,1) EQ '(').DATA010
         LA    R14,&DATA1                   DATA ADDRESS
         MVC   COMM_TRACE_ITEM_1,0(R14)     MOVE DATA
         AGO   .DATA040
.DATA010 ANOP
         AIF   (N'&DATA1 EQ 1).DATA020
&L       SETA  &DATA1(2)
         AIF   (&L GT 8).ERR0040
         MVC   COMM_TRACE_ITEM_1(&L),&DATA1(1)
         AGO   .DATA040
.DATA020 ANOP
         MVC   COMM_TRACE_ITEM_1,0&DATA1    MOVE DATA
         AGO   .DATA040
.DATA030 ANOP
         AIF   (T'&RDATA1 EQ 'O').DATA040
         STCM  &RDATA1,15,COMM_TRACE_ITEM_1 SAVE REGISTER CONTENTS
.DATA040 ANOP
         AIF   (T'&DATA2 EQ 'O').DATA070
         AIF   ('&DATA2'(1,1) EQ '(').DATA050
         LA    R14,&DATA2                   DATA ADDRESS
         MVC   COMM_TRACE_ITEM_2,0(R14)     MOVE DATA
         AGO   .DATA080
.DATA050 ANOP
         AIF   (N'&DATA2 EQ 1).DATA060
&L       SETA  &DATA2(2)
         AIF   (&L GT 8).ERR0050
         MVC   COMM_TRACE_ITEM_2(&L),&DATA2(1)
         AGO   .DATA080
.DATA060 ANOP
         MVC   COMM_TRACE_ITEM_2,0&DATA2    MOVE DATA
         AGO   .DATA080
.DATA070 ANOP
         AIF   (T'&RDATA2 EQ 'O').DATA080
         STCM  &RDATA2,15,COMM_TRACE_ITEM_2 SAVE REGISTER CONTENTS
.DATA080 ANOP
         BAL   R14,TRACE000                 ENTER TRACE ROUTINE
         AIF      (T'&ID EQ 'O').NOID
&NAME    DC    CL8'&ID'                     TRACE ID
         AGO   .ID
.NOID    ANOP
&NAME    DC    CL8'**NOID**'
.ID      ANOP
         MEXIT
.ERR0010 ANOP
         MNOTE 12,'DATA1 AND RDATA1 ARE MUTUALLY EXCLUSIVE'
         MEXIT
.ERR0020 ANOP
         MNOTE 12,'DATA2 AND RDATA2 ARE MUTUALLY EXCLUSIVE'
         MEXIT
.ERR0030 ANOP
         MNOTE 4,'POSITIONAL PARM FOUND'
         MEXIT
.ERR0040 ANOP
         MNOTE 12,'LENGTH FOR ITEM 1 MUST BE 8 OR LESS'
         MEXIT
.ERR0050 ANOP
         MNOTE 12,'LENGTH FOR ITEM 2 MUST BE 8 OR LESS'
.MEXIT   ANOP
         MEXIT
         MEND
