         MACRO
.* ------------------------------------------------------------------ *
.*                                                                    *
.*    DEFINE OPCODES                                                  *
.*      MACH    OPCODE IN MACHINE HEX                                 *
.*      MNEM    MNEMONIC FOR THE INSTRUCTION                          *
.*      LENGTH  LENGTH OF INSTRUCTION                                 *
.*      COMMENT ANY COMMENT (PRINTED IN COMMENT AREA OF GENNED SOURCE)*
.*      FLAGS                                                         *
.*                - OPCODE_EXT    USE EXTENDED NMEMONIC IF AVAILABLE  *
.*                - OPCODE_SVC    THIS INSTRUCTION IS AN SVC          *
.*                - OPCODE_REF    THIS INSTRUCTION CAUSES REFERENCE(S)*
.*                - OPCODE_CCA    SETS "ARITHMETIC" CONDITION CODE    *
.*                - OPCODE_CC     SETS "COMPARE" CONDITION CODE       *
.*                - OPCODE_CCL    SETS "LOGICAL" CONDITION CODE       *
.*                - OPCODE_BRANCH THIS IS A "BRANCHING" INSTRUCTION   *
.*      REGS    REGISTER CONSTRAINTS                                  *
.*                - OPCODE_1EVEN  R1 MUST BE EVEN NUMBER              *
.*                - OPCODE_104    R1 MUST BE 0 OR 4                   *
.*                - OPCODE_10246  R1 MUST BE 0, 2, 4, OR 6            *
.*                - OPCODE_2EVEN  R2 MUST BE EVEN NUMBER              *
.*                - OPCODE_204    R2 MUST BE 0 OR 4                   *
.*                - OPCODE_20246  R2 MUST BE 0, 2, 4, OR 6            *
.*                                                                    *
.*                                                                    *
.*                                                                    *
.*                                                                    *
.* ------------------------------------------------------------------ *
         OPCODE &MACH,                                                 +
               &MNEM,                                                  +
               &FORMAT,                                                +
               &LENGTH,                                                +
               &COMMENT,                                               +
               &FLAGS=,                                                +
               &REGS=,                                                 +
               &REF=,                                                  +
               &EXIT=,                                                 +
               &TYPE=DEFINE
         GBLB &OPSW
         GBLC &OPTBL(256),&HEXTBL(16)
         LCLA &SUB1,&SUB2,&SUB3
         LCLC &OP1,&OP2,&OPTS
         AIF  (&OPSW).LBL020
&OPSW    SETB 1
&SUB1    SETA 1
.LBL010  ANOP
&OPTBL(&SUB1) SETC '0'
&SUB1    SETA &SUB1+1
         AIF  (&SUB1 LE 256).LBL010
&HEXTBL(01) SETC '0'
&HEXTBL(02) SETC '1'
&HEXTBL(03) SETC '2'
&HEXTBL(04) SETC '3'
&HEXTBL(05) SETC '4'
&HEXTBL(06) SETC '5'
&HEXTBL(07) SETC '6'
&HEXTBL(08) SETC '7'
&HEXTBL(09) SETC '8'
&HEXTBL(10) SETC '9'
&HEXTBL(11) SETC 'A'
&HEXTBL(12) SETC 'B'
&HEXTBL(13) SETC 'C'
&HEXTBL(14) SETC 'D'
&HEXTBL(15) SETC 'E'
&HEXTBL(16) SETC 'F'
.LBL020  ANOP
         AIF  ('&TYPE' EQ 'INDEX').LBL150
         AIF  ('&TYPE' NE 'DEFINE').ERR70
         AIF  (T'&MACH EQ 'O').ERR010
         AIF  (K'&MACH NE 2).ERR020
         AIF  (T'&LENGTH EQ 'O').ERR080
         AIF  (&LENGTH EQ 2).LBL030
         AIF  (&LENGTH EQ 4).LBL030
         AIF  (&LENGTH NE 6).ERR090
.LBL030  ANOP
         AIF  (K'&MNEM GT 5).ERR100
&OP1     SETC '&MACH'(1,1)
&OP2     SETC '&MACH'(2,1)
&SUB1    SETA 1
.LBL040  ANOP
         AIF  ('&OP1' EQ '&HEXTBL(&SUB1)').LBL050
         AIF  (&SUB1 EQ 16).ERR030
&SUB1    SETA &SUB1+1
         AGO  .LBL040
.LBL050  ANOP
&SUB2    SETA &SUB1
&SUB1    SETA 1
.LBL060  ANOP
         AIF  ('&OP2' EQ '&HEXTBL(&SUB1)').LBL070
         AIF  (&SUB1 EQ 16).ERR030
&SUB1    SETA &SUB1+1
         AGO  .LBL060
.LBL070  ANOP
&SUB3    SETA &SUB1
&SUB1    SETA (16*(&SUB2-1))+&SUB3
&OPTBL(&SUB1) SETC 'MACH&MACH'
         AIF  (T'&MNEM EQ 'O').ERR050
         AIF  (T'&FORMAT EQ 'O').ERR060
MACH&MACH DC   CL2'&MACH'           MACHINE CODE FOR THIS INSTRUCTION
          DC   CL05'&MNEM'          MNEMONIC
          DC   AL2(&LENGTH)         INSTRUCTION LENGTH
          AIF  (T'&EXIT EQ 'O').LBL080
          DC   VL4(&EXIT)           MODIFICATION EXIT ENTRY POINT
          AGO  .LBL090
.LBL080   ANOP
          DC   AL4(0)               NO MODIFICATION EXIT
.LBL090   ANOP
          DC   AL1(&FORMAT)         FORMAT CODE
&OPTS    SETC '0'
         AIF  (T'&FLAGS EQ 'O').LBL100
&OPTS    SETC '&FLAGS'
.LBL100  ANOP
         AIF  (T'&COMMENT EQ 'O').LBL110
          DC   AL1(&OPTS)           FLAGS
         AGO   .LBL120
.LBL110  ANOP
          DC   AL1(&OPTS+$OPCODE_NO_COMMENT)
.LBL120  ANOP
         AIF   (T'&REGS EQ 'O').LBL130
          DC    AL1(&REGS)
         AGO   .LBL140
.LBL130  ANOP
          DC    AL1(0)              NO REGISTER REQUIREMENTS
.LBL140  ANOP
         AIF   (T'&REF EQ 'O').LBL14A
          DC    AL1(&REF)           BASE + DISPLACEMENT OPERANDS
         AGO   .LBL14B
.LBL14A  ANOP
          DC    AL1(0)              NO BASE + DISPLACMENT OPERANDS
.LBL14B  ANOP
         AIF   (T'&COMMENT EQ 'O').EXIT
          DC   CL25&COMMENT         COMMENT
         MEXIT
.LBL150  ANOP
&SUB1    SETA 1
.LBL160  ANOP
         DC    A(&OPTBL(&SUB1))
         AIF   (&SUB1 EQ 256).EXIT
&SUB1    SETA  &SUB1+1
         AGO   .LBL160
.ERR010  ANOP
         MNOTE 12,'MACHINE CODE IS REQUIRED'
         MEXIT
.ERR020  ANOP
         MNOTE 12,'MACHINE CODE MUST BE 2 CHARACTERS'
         MEXIT
.ERR030  ANOP
         MNOTE 12,'MACHINE CODE CONTAINS INVALID HEX CHARACTER'
         MEXIT
.ERR050  ANOP
         MNOTE 12,'MNEMONIC IS REQUIRED'
         MEXIT
.ERR060  ANOP
         MNOTE 12,'INSTRUCTION FORMAT IS REQUIRED'
         MEXIT
.ERR070  ANOP
         MNOTE 12,'INVALID TYPE PARAMETER'
         MEXIT
.ERR080  ANOP
         MNOTE 12,'LENGTH IS REQUIRED'
         MEXIT
.ERR090  ANOP
         MNOTE 12,'LENGTH MUST BE 2, 4, OR 6'
         MEXIT
.ERR100  ANOP
         MNOTE 12,'MNEMONIC IS MORE THAN 5 CHARACTERS LONG'
.EXIT    ANOP
         MEND
