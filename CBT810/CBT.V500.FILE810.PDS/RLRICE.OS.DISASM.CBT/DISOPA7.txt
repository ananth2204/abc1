* ------------------------------------------------------------------- *
*                                                                     *
*  MODULE NAME: DISOPA7                                               *
*                                                                     *
*  This exit does not extablish a work area.                          *
*  It MUST NOT call ITRACE                                            *
*                                                                     *
*                                                                     *
*                                                                     *
*  FUNCTION:                                                          *
*    Modify OPCODE table for A7 instructions                          *
*                                                                     *
*                                                                     *
*  Calling program provides two parms                                 *
*     1)   Address of OPCODE table entry to modify                    *
*     2)   Address of object text                                     *
*                                                                     *
* ------------------------------------------------------------------- *
DISOPA7  CSECT
DISOPA7  AMODE 31
DISOPA7  RMODE ANY
         USING DISOPA7,R15
         B     MOD0000                           SKIP EYECATCHER
MODID    DC    CL8'DISOPA7'
         DC    C'&SYSDATE'
         DC    C'&SYSTIME'
MOD0000  DS    0H
         STM   R14,R12,12(R13)                   SAVE REGS
         LR    R10,R15                           SET BASE REG
         DROP  R15
         USING OSCOMM,R12
         USING SESSION,R11
         USING DISOPA7,R10
         USING DISCOMM,R9
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         LM    R2,R3,0(R1)                       PARMS
         GETMAIN RU,                                                   +
               LV=WORK_L,                                              +
               LOC=ANY
         ST    R13,4(,R1)
         ST    R1,8(,R13)
         LR    R13,R1
         USING WORK,R13
         MVC   DXD_CSECT,MODID
         USING OPCODE_DSECT,R2
         MVC   COMM_INFO_01(1),1(R3)             COPY 2ND BYTE
         NI    COMM_INFO_01,X'0F'                CLEAR 1ST NIBBLE
         SR    R5,R5                             CLEAR R4
         IC    R5,COMM_INFO_01                   OPCODE MODIFIER
         MH    R5,=Y(OPCODE_TABLE_L)             MULTIPLY BY LENGTH
         LA    R6,OPCODE_TABLE(R5)               PLUS BASE
         MVC   OPCODE_MNEMONIC,0(R6)             COPY MNEMONIC
         MVC   OPCODE_FLAGS,5(R6)                COPY FLAGS
         MVC   OPCODE_REF,6(R6)                  COPY REF INDICATORS
         STCM  R10,15,OPCODE_EXIT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=EXIT,                                               +
               DATA1=(OPCODE_MNEMONIC,5),                              +
               DATA2=(OPCODE_FORMAT,4)
         LR    R1,R13
         L     R13,4(,R13)                       RESTORE R13
         FREEMAIN RU,                                                  +
               A=(1),                                                  +
               LV=WORK_L
         LM    R14,R12,12(R13)                   RESTORE REGISTERS
         SR    R15,R15
         BR    R14                               RETURN TO CALLER
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *

OPCODE_TABLE   DS      0C
               DC      CL5'TMLH '               0
               DC      AL1(0)
               DC      AL1($OPCODE_REF1)
OPCODE_TABLE_L EQU     *-OPCODE_TABLE

               DC      CL5'TMLL '               1
               DC      AL1(0)
               DC      AL1($OPCODE_REF1)

               DC      CL5'TMHH '               2
               DC      AL1(0)
               DC      AL1($OPCODE_REF1)

               DC      CL5'TMHL '               3
               DC      AL1(0)
               DC      AL1($OPCODE_REF1)

               DC      CL5'BRC  '               4
               DC      AL1($OPCODE_BRANCH+$OPCODE_RELATIVE)
               DC      AL1(0)

               DC      CL5'BRAS '               5
               DC      AL1($OPCODE_BRANCH+$OPCODE_RELATIVE)
               DC      AL1(0)

               DC      CL5'BRCT '               6
               DC      AL1($OPCODE_BRANCH+$OPCODE_RELATIVE)
               DC      AL1(0)

               DC      CL5'BRCTG'               7
               DC      AL1($OPCODE_BRANCH+$OPCODE_RELATIVE)
               DC      AL1(0)

               DC      CL5'LHI  '               8
               DC      AL1(0)
               DC      AL1(0)

               DC      CL5'LGHI '               9
               DC      AL1(0)
               DC      AL1(0)

               DC      CL5'AHI  '               A
               DC      AL1(0)
               DC      AL1(0)

               DC      CL5'AGHI '               B
               DC      AL1(0)
               DC      AL1(0)

               DC      CL5'MHI  '               C
               DC      AL1(0)
               DC      AL1(0)

               DC      CL5'MGHI '               D
               DC      AL1(0)
               DC      AL1(0)

               DC      CL5'CHI  '               E
               DC      AL1(0)
               DC      AL1(0)

               DC      CL5'CGHI '               F
               DC      AL1(0)
               DC      AL1(0)

* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
WORK          DSECT
              COPY     DXDPREF
WORK_L        EQU      *-WORK
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              DISCOMM  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              DISASMDA
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              BPXYSTAT DSECT=YES,LIST=NO
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              COPY     REGEQU
              END      DISOPA7
