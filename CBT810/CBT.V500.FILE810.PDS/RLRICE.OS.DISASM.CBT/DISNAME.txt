*--------------------------------------------------------------------*
*                                                                    *
*  Module name: DISNAME                                              *
*                                                                    *
*  Function:                                                         *
*   Obtain the section names in the LMOD                             *
*                                                                    *
*   DISBIND has already established a dialog                         *
*                                                                    *
*                                                                    *
*--------------------------------------------------------------------*
DISNAME  CSECT
DISNAME  AMODE 31
DISNAME  RMODE ANY
         USING DISNAME,R15
         B     MOD0000                          SKIP EYECATCHER
MODID    DC    CL8'DISNAME'
         DC    C'&SYSDATE'
         DC    C'&SYSTIME'
MOD0000  DS    0H
         STM   R14,R12,12(R13)                  SAVE REGS
         LR    R10,R15
         DROP  R15
         USING OSCOMM,R12
         USING SESSION,R11
         USING DISNAME,R10
         USING DISCOMM,R9
         L     R15,SESS_DXD_ADDR                WORK AREA
         A     R15,DXD_START                    PLUS DISP
         ST    R13,4(,R15)                      CHAIN SAVE AREA
         ST    R15,8(,R13)                      CHAIN SAVE AREA
         LR    R13,R15                          SET SAVE AREA ADDRESS
         USING DXDNAME,R13
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY
* ------------------------------------------------------------------- *
*        Obtain and initialize NAME buffer                            *
* ------------------------------------------------------------------- *
         ITRACE ID=GETBUF
         IEWBUFF FUNC=GETBUF,                                          +
               TYPE=NAME
         ST    R8,DXD_BUFFER
         ITRACE ID=INIT_BUF,                                           +
               RDATA1=R8,                                              +
               RDATA2=R7
         IEWBUFF FUNC=INITBUF,                                         +
               TYPE=NAME
         XC    DXD_CURSOR,DXD_CURSOR
*--------------------------------------------------------------------*
*                                                                    *
*--------------------------------------------------------------------*
NAME0000 DS    0H
         ITRACE ID=GET_NAME
         L     R8,DXD_BUFFER
         LA    R7,BNLH_END
         IEWBIND FUNC=GETN,                                            +
               VERSION=5,                                              +
               WORKMOD=DISCOMM_DIALOG_WORKMOD,                         +
               AREA=IEWBBNL,                                           +
               CURSOR=DXD_CURSOR,                                      +
               COUNT=DXD_NAME_RETURNED,                                +
               TCOUNT=DXD_TOTAL_NAMES,                                 +
               NTYPE=S,                                                +
               RETCODE=DXD_RETURN_CODE,                                +
               RSNCODE=DXD_REASON_CODE,                                +
               MF=(E,DXD_IEWBIND,COMPLETE)
         ITRACE ID=NAME_RC,                                            +
               DATA1=(DXD_RETURN_CODE,4),                              +
               DATA2=(DXD_REASON_CODE,4)
         CLC   DXD_RETURN_CODE,F4
         BH    ERR0010
         BL    NAME0010
         CLC   DXD_REASON_CODE,X_83000800
         BE    NAME0010
         CLC   DXD_REASON_CODE,X_83000801
         BE    ERR0020
NAME0010 DS    0H
         ICM   R6,15,DXD_NAME_RETURNED
         USING BNL_ENTRY,R7
NAME0020 DS    0H
         ITRACE ID=BNL,                                                +
               RDATA1=R7,                                              +
               RDATA2=R6
         L     R2,BNL_NAME_PTR                 ADDRESS OF NAME
         CLI   0(R2),C' '                      PRINTABLE NAME?
         BL    NAME0040                        NO
         L     R1,DISCOMM_NAME_NBR             NBR OF 'NAMES'
         LA    R1,1(,R1)                       PLUS 1
         ST    R1,DISCOMM_NAME_NBR             SAVE TOTAL
         GETMAIN RU,                                                   +
               LV=NAME_DSECT_L,                                        +
               LOC=ANY
         USING NAME_DSECT,R1
         MVC   NAME_EYE,NAME_ID                SET EYECATCHER
         MVC   NAME_NEXT,DISCOMM_NAME_NEXT     CHAIN EXISTING TO NEW
         ST    R1,DISCOMM_NAME_NEXT            ADD NEW NAME TO CHAIN
         MVC   NAME_NAME,COMM_BLANKS           INITIALIZE NAME
         LH    R3,BNL_NAME_CHARS               LENGTH OF NAME
         CH    R3,H8                           MORE THAN 8?
         BNH   NAME0030                        NO
         LH    R3,H8                           LIMIT TO 8
NAME0030 DS    0H
         BCTR  R3,0                            FOR EX
         EX    R3,NAME_MVC                     COPY NAME
         MVC   NAME_OFFSET,BNL_SEGM_OFF        COPY OFFSET
         ITRACE ID=NEW_NAME,                                           +
               DATA1=NAME_NAME,                                        +
               DATA2=(NAME_OFFSET,4)
NAME0040 DS    0H
         LA    R7,BNL_END                      NEXT BNL ENTRY
         BCT   R6,NAME0020                     LOOP
         CLC   DXD_REASON_CODE,X_83000800      MORE TO GO?
         BNE   NAME0000                        YES
         B     EXIT0000
NAME_MVC MVC   NAME_NAME(0),0(R2)
*--------------------------------------------------------------------*
*                                                                    *
*--------------------------------------------------------------------*
ERR0010  DS    0H
         DC    H'1'
ERR0020  DS    0H
         DC    H'2'
EXIT0000 DS    0H
         ITRACE ID=EXIT
         L     R13,4(,R13)                     RESTORE REGISTER 13      ASE01670
         LM    R14,R12,12(R13)                 RESTORE OTHER REGISTERS  ASE01680
         SR    R15,R15                         SET RETURN CODE          ASE01690
         BR    R14                             RETURN TO CALLER         ASE01700
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
DXD_START     DC   Q(DXDNAME)
F4            DC   F'4'
H8            DC   H'8'
B_NAME        DC   XL2'5',C'B_NAME'
X_83000800    DC   X'83000800'
X_83000801    DC   X'83000801'
NAME_ID       DC   CL8'NAME'

         IEWBUFF FUNC=MAPBUF,                                          +
               TYPE=NAME,                                              +
               HEADREG=R8,                                             +
               ENTRYREG=R7,                                            +
               SIZE=50,                                                +
               VERSION=5

              IEWBIND MF=(L,IEWBIND_I)
IEWBIND_L     EQU  *-IEWBIND_I
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
DXDNAME           DSECT
                  COPY     DXDPREF

DXD_RETURN_CODE   DS   F
DXD_REASON_CODE   DS   F

DXD_BUFFER        DS   A

DXD_CURSOR        DS   F
DXD_NAME_RETURNED DS   F
DXD_TOTAL_NAMES   DS   F

DXD_IEWBIND       DS   0F,(IEWBIND_L)X
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 DISCOMM  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 DISASMDA
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 BPXYSTAT DSECT=YES,LIST=NO
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COPY     OPERAND
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COPY     REGEQU
                  END     DISNAME
