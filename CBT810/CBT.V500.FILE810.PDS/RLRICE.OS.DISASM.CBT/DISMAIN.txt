* ------------------------------------------------------------------- *
*                                                                     *
*  Module name: DISMAIN                                               *
*                                                                     *
*  Disassembler mainline                                              *
*                                                                     *
* ------------------------------------------------------------------- *
DISMAIN  CSECT
DISMAIN  AMODE 31
DISMAIN  RMODE ANY
         USING OSCOMM,R12
         USING SESSION,R11
         USING DISMAIN,R15
         B     INIT0000
MODID    DC    CL8'DISMAIN'
         DC    CL8'&SYSDATE'
         DC    CL7'&SYSTIME'
INIT0000 DS    0H
         STM   R14,R12,12(R13)                  SAVE REGS
         LR    R10,R15                          COPY ENTRY POINT
         LA    R7,2048(,R10)
         LA    R7,2048(,R7)
         DROP  R15
         USING DISMAIN,R10,R7                   DEFINE BASE
         CLI   SESS_FORMAT_FLAGS,$FORMAT_INITIALIZE
         BNE   MAIN0000                         NO
* ------------------------------------------------------------------- *
*        Initialization                                               *
* ------------------------------------------------------------------- *
INIT0010 DS    0H
         L     R0,DXD_LENGTH
         ST    R0,SESS_DXD_LENGTH               SAVE WORK AREA LENGTH
         GETMAIN RU,                                                   +
               LV=(R0),                                                +
               LOC=ANY
         ST    R1,SESS_DXD_ADDR                 SET WORK AREA ADDRESS
         LR    R2,R1
         LR    R0,R1
         L     R1,DXD_LENGTH
         SR    R14,R14
         SR    R15,R15
         MVCL  R0,R14                           INITIALIZE WORK AREA
         A     R2,DXD_START
         ST    R13,4(,R2)                       SAVE R13
         ST    R2,8(,R13)                       CHAIN SAVE AREA
         LR    R13,R2                           COPY WORK AREA ADDRESS
         USING DXDMAIN,R13                      DEFINE WORK AREA BASE
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY,                                              +
               DATA1=(SESS_DXD_ADDR,4),                                +
               DATA2=(SESS_DXD_LENGTH,4)
         LOAD  EP=DISCOMM
         LR    R9,R0
         ITRACE ID=DISCOMM,                                            +
               RDATA1=R9
         USING DISCOMM,R9
         ST    R9,SESS_COMM                     SAVE DISCOMM ADDRESS
         L     R8,COMM_OSSPFD                   SPF DATA AREA
         USING OSSPFD,R8                        DEFINE BASE
         LOAD  EP=DISMSGUS
         ST    R0,SESS_MESSAGES
* ------------------------------------------------------------------- *
*        GETMAIN VDATA buffer                                         *
* ------------------------------------------------------------------- *
         ITRACE ID=GETVDATA
         LA    R1,NAMELIST_PANEL
         MVC   DISCOMM_PANEL_NAME,NAMELIST_PANEL
         MVI   COMM_VDATA_FUNC,$VDATA_GETMAIN
         L     R15,COMM_V_OSVDATA
         BALR  R14,R15                          GETMAIN VDATA STORAGE
         ST    R0,DISCOMM_VDATA_LENGTH          SAVE LENGTH
         ST    R1,DISCOMM_VDATA_ADDR            SAVE ADDRESS
         ITRACE ID=VDATA,                                              +
               RDATA1=R1,                                              +
               RDATA2=R0
         B     EXIT0050
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0000 DS    0H
         L     R15,SESS_DXD_ADDR                WORK AREA BASE
         A     R15,DXD_START                    PLUS DISP
         ST    R13,4(,R15)                      SAVE R13
         ST    R15,8(,R13)                      CHAIN SAVE AREA
         LR    R13,R15                          COPY WORK AREA ADDRESS
         L     R9,SESS_COMM                     RESTORE DISCOMM ADDR
         L     R8,COMM_OSSPFD                   SPF DATA AREA
         MVC   SPF_ZCMD,COMM_BLANKS             RESET COMMAND
         MVC   SPF_FUNCTION,COMM_BLANKS         RESET COMMAND
         CLI   SESS_FORMAT_FLAGS,$FORMAT_CONTROL
         BE    EXIT0050
         CLI   SESS_FORMAT_FLAGS,$FORMAT_CLEANUP
         BE    EXIT0000
* ------------------------------------------------------------------- *
*        Start binder dialog                                          *
* ------------------------------------------------------------------- *
         ITRACE ID=STARTD
         L     R9,SESS_COMM                     DISCOMM ADDRESS
         MVC   DISCOMM_LMOD,SESS_MEMBER         LMOD NAME = MEMBER NAME
         MVC   DISCOMM_DDNAME,SESS_DD           SET LIBARY DD NAME
         MVI   DISCOMM_BIND_FUNC,$BIND_STARTD   SET FUNCTION
         L     R15,V_DISBIND                    DISBIND ENTRY POINT
         BALR  R14,R15                          LINK TO DISIND
         ITRACE ID=START_RC,                                           +
               RDATA1=R15
         LTR   R15,R15                          SUCCESSFUL?
         BNZ   EXIT0050                         NO
* ------------------------------------------------------------------- *
*        Build section names                                          *
* ------------------------------------------------------------------- *
         ITRACE ID=GETN
         MVI   DISCOMM_BIND_FUNC,$BIND_NAMES    SET FUNCTION
         L     R15,V_DISBIND                    DISBIND ENTRY POINT
         BALR  R14,R15                          LINK TO DISIND
         ITRACE ID=NAMES_RC,                                           +
               RDATA1=R15
         LTR   R15,R15                          SUCCESSFUL?
         BNZ   EXIT0050                         NO
         CLC   DISCOMM_NAME_NBR,F1              ONE CSECT?
         BE    MAIN0020                         YES
* ------------------------------------------------------------------- *
*        List section names                                           *
* ------------------------------------------------------------------- *
MAIN0010 DS    0H
         ITRACE ID=LISTNAME
         L     R15,V_DISNLIST                   DISNLIST ENTRY POINT
         BALR  R14,R15                          LINK TO DISIND
         ITRACE ID=LIST_RC,                                            +
               RDATA1=R15
         LTR   R15,R15                          SUCCESSFUL?
         BZ    MAIN0030                         YES
         B     EXIT0040                         OTHERWIZE EXIT
* ------------------------------------------------------------------- *
*        Module has only one CSECT                                    *
* ------------------------------------------------------------------- *
MAIN0020 DS    0H
         ITRACE ID=1_CSECT
         MVC   SPF_MEMBER,SESS_MEMBER
         L     R1,DISCOMM_NAME_NEXT             1ST (AND ONLY) NAME
         USING NAME_DSECT,R1
         MVC   DISCOMM_CSECT,NAME_NAME
         MVC   SPF_CSECT,NAME_NAME
* ------------------------------------------------------------------- *
*        GET ESD and RLD info for selected (or only CSECT)            *
* ------------------------------------------------------------------- *
MAIN0030 DS    0H
         ITRACE ID=ESD_RLD
         MVI   DISCOMM_BIND_FUNC,$BIND_ESD_RLD
         L     R15,V_DISBIND                    DISBIND ENTRY POINT
         BALR  R14,R15                          LINK TO DISIND
         ITRACE ID=ESDRLDRC,                                           +
               RDATA1=R15
         LTR   R15,R15                          SUCCESSFUL?
         BNZ   EXIT0050                         NO
* ------------------------------------------------------------------- *
*        Read text                                                    *
* ------------------------------------------------------------------- *
         ITRACE ID=TEXT
         MVI   DISCOMM_BIND_FUNC,$BIND_TEXT
         L     R15,V_DISBIND                    DISBIND ENTRY POINT
         BALR  R14,R15                          LINK TO DISIND
         ITRACE ID=TEXT_RC,                                            +
               RDATA1=R15
         LTR   R15,R15                          SUCCESSFUL?
         BNZ   EXIT0050                         NO
* ------------------------------------------------------------------- *
*        End the binder dialog                                        *
* ------------------------------------------------------------------- *
         ITRACE ID=ENDD
         MVI   DISCOMM_BIND_FUNC,$BIND_ENDD
         L     R15,V_DISBIND                    DISBIND ENTRY POINT
         BALR  R14,R15                          LINK TO DISIND
         ITRACE ID=ENDD_RC,                                            +
               RDATA1=R15
         LTR   R15,R15                          SUCCESSFUL?
         BNZ   EXIT0050                         NO
* ------------------------------------------------------------------- *
*                                                                     *
*        For ADCONs the text tells us which displacement is           *
*        being referenced by the ADCON.                               *
*                                                                     *
* ------------------------------------------------------------------- *
         ICM   R5,15,DISCOMM_RLD_AREA_NEXT      FIRST RLD ITEM
         BZ    MAIN0100                         NO RLD ITEMS
         USING AREA_DSECT,R5
MAIN0050 DS    0H
         ITRACE ID=AREA,                                               +
               RDATA1=R5,                                              +
               DATA2=(AREA_DATA_TYPE,1)
         CLI   AREA_DATA_TYPE,$DATA_ACON        ADCON?
         BNE   MAIN0060                         NO
         ICM   R1,15,AREA_BEGIN                 RLD ITEM'S DISP
         LR    R0,R1                            COPY DISP
         A     R1,DISCOMM_CSECT_TEXT            PLUS BASE
         ICM   R14,15,AREA_LENGTH               RLD ITEM'S LENGTH
         BCTR  R14,0                            RELATIVE TO ZERO
         AR    R0,R14                           PLUS LENGTH
         STCM  R0,15,AREA_END
         LA    R15,AREA_REF_DISP1+3             END OF OUTPUT FIELD
         SR    R15,R14                          MINUS LENGTH
         EX    R14,ADCONMVC                     COPY TEXT
         ITRACE ID=ADCON,                                              +
               DATA1=(AREA_BEGIN,4),                                   +
               DATA2=(AREA_REF_DISP1,4)
MAIN0060 DS    0H
         ICM   R5,15,AREA_NEXT                  ANOTHER AREA?
         BNZ   MAIN0050                         YES
         B     MAIN0100
ADCONMVC MVC   0(0,R15),0(R1)                   COPY TARGET DISP
* ------------------------------------------------------------------- *
*                                                                     *
*                                                                     *
*        Display main menu                                            *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0100 DS    0H
         ITRACE ID=MAINMENU
         MVI   SPF_FUNCTION,C' '
         MVC   SESS_DISP_PANEL,MENU_PANEL       SET PANEL NAME
         XC    SESS_DISP_VDATA_ADDR,SESS_DISP_VDATA_ADDR
         XC    SESS_DISP_VDATA_LENGTH,SESS_DISP_VDATA_LENGTH
         XC    SESS_DISP_CURSOR,SESS_DISP_CURSOR
MAIN0110 DS    0H
         ITRACE ID=CALLDISP
         L     R15,COMM_V_OSDISP
         BALR  R14,R15                          DISPLAY MAIN MENU
         ITRACE ID=MENU_RC,                                            +
               RDATA1=R15
         LTR   R15,R15                          RC=0?
         BNZ   MAIN0120                         NO
         OC    SPF_FUNCTION,COMM_BLANKS         'TRANSLATE'
         ITRACE ID=SELECT,                                             +
               DATA1=(SPF_FUNCTION,1)
         CLI   SPF_FUNCTION,C' '                BLANK?
         BE    MAIN0130                         YES
         CLI   SPF_FUNCTION,C'A'                ASSEMBLER SOURCE?
         BE    MAIN0140                         YES
         CLI   SPF_FUNCTION,C'B'                LIST BASEs?
         BE    MAIN0150                         YES
         CLI   SPF_FUNCTION,C'C'                Save/Restore config?
         BE    MAIN0160                         YES
         CLI   SPF_FUNCTION,C'D'                LIST DATA AREAs?
         BE    MAIN0170                         YES
         CLI   SPF_FUNCTION,C'E'                LIST ESD info?
         BE    MAIN0180                         YES
         CLI   SPF_FUNCTION,C'G'                Generate source?
         BE    MAIN0190                         YES
         CLI   SPF_FUNCTION,C'K'                LIST KSDSes?
         BE    MAIN0300                         YES
         CLI   SPF_FUNCTION,C'O'                OPTIONS?
         BE    MAIN0310                         YES
         CLI   SPF_FUNCTION,C'R'                LIST RLD info?
         BE    MAIN0320                         YES
         CLI   SPF_FUNCTION,C'S'                LIST SYSLIBs?
         BE    MAIN0330                         YES
         CLI   SPF_FUNCTION,C'T'                Display TEXT?
         BE    MAIN0340                         YES
         CLI   SPF_FUNCTION,C'U'                Display USINGs?
         BE    MAIN0350                         YES
         CLI   SPF_FUNCTION,C'1'                TURN DEBUGGING ON?
         BE    MAIN0400                         YES
         CLI   SPF_FUNCTION,C'2'                TURN DEBUGGING OFF?
         BE    MAIN0410                         YES
         ITRACE ID=BAD_SEL
         MVC   SPF_MSG_1(MSG02_L),MSG02
         B     MAIN0100                         DISPLAY MENU AGAIN
* ------------------------------------------------------------------- *
*                                                                     *
*                                                                     *
*  Clean up current CSECT's info                                      *
*                                                                     *
*  If there is only one CSECT, just exit                              *
*  If there are multiple CSECTs                                       *
*    . Start a BINDER DIALOG again                                    *
*    . Display the list of CSECTs again                               *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0120 DS    0H
         ITRACE ID=MAIN0120
         BAL   R6,CLEAN000                      CLEAN UP
         CLC   DISCOMM_NAME_NBR,F1              ONE CSECT?
         BE    EXIT0040                         YES
         ITRACE ID=STARTD
         L     R9,SESS_COMM                     DISCOMM ADDRESS
         MVC   DISCOMM_LMOD,SESS_MEMBER         LMOD NAME = MEMBER NAME
         MVC   DISCOMM_DDNAME,SESS_DD           SET LIBARY DD NAME
         MVI   DISCOMM_BIND_FUNC,$BIND_STARTD   SET FUNCTION
         L     R15,V_DISBIND                    DISBIND ENTRY POINT
         BALR  R14,R15                          LINK TO DISIND
         ITRACE ID=START_RC,                                           +
               RDATA1=R15
         LTR   R15,R15                          SUCCESSFUL?
         BNZ   EXIT0050                         NO
         B     MAIN0010                         DISPLAY NAME LIST AGAIN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0130 DS    0H
         ITRACE ID=NO_SEL
         MVC   SPF_MSG_1(MSG01_L),MSG01
         B     MAIN0110
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0140 DS    0H
         ITRACE ID=ASSEM
         MVC   SPF_FUNCTION,COMM_BLANKS         RESET COMMAND
         L     R15,V_DISALIST                   DISALIST ENTRY POINT
         BALR  R14,R15                          CALL DISALIST
         ITRACE ID=ASSEM_X
         B     MAIN0100
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0150 DS    0H
         ITRACE ID=BASE
         MVC   SPF_FUNCTION,COMM_BLANKS         RESET COMMAND
         L     R15,V_DISBLIST                   DISBLIST ENTRY POINT
         BALR  R14,R15                          CALL DISBLIST
         ITRACE ID=BASE_X
         B     MAIN0100
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0160 DS    0H
         ITRACE ID=CONFIG
         MVC   SPF_FUNCTION,COMM_BLANKS         RESET COMMAND
         L     R15,V_DISCONF                    DISCONF ENTRY POINT
         BALR  R14,R15                          CALL DISCONF
         ITRACE ID=CONF_X
         B     MAIN0100
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0170 DS    0H
         ITRACE ID=DATA
         MVC   SPF_FUNCTION,COMM_BLANKS         RESET COMMAND
         L     R15,V_DISDLIST                   DISDLIST ENTRY POINT
         BALR  R14,R15                          CALL DISDLIST
         ITRACE ID=DATA_X
         B     MAIN0100
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0180 DS    0H
         ITRACE ID=ESD
         MVC   SPF_FUNCTION,COMM_BLANKS         RESET COMMAND
         L     R15,V_DISELIST                   DISELIST ENTRY POINT
         BALR  R14,R15                          CALL DISELIST
         ITRACE ID=ESD_X
         B     MAIN0100




* ------------------------------------------------------------------- *
*                                                                     *
*        Generate source                                              *
*                                                                     *
*        1) Verify OPCODE table has been selected                     *
*        2) Verify label prefix has been given                        *
*        3) Verify BASE and USINGs                                    *
*        4) Delete old "all areas chain"                              *
*        5) Build new "all areas chain" (merge user with RLD data)    *
*                                                                     *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0190 DS    0H
         ITRACE ID=GEN
         MVI   SPF_FUNCTION,C' '                RESET COMMAND
         OC    DISCOMM_ASM_NEXT,DISCOMM_ASM_NEXT
         BZ    MAIN019A                         NO SOURCE TO ASSEMBLE
         TM    DISCOMM_FLAGS,$DISCOMM_ASSEMBLE_SOURCE
         BNO   MAIN019A
         MVC   DISCOMM_TPUT_MSG(STAT_MSG_01_L),STAT_MSG_01
         BAL   R6,TPUT0000                      ISSUE TPUT
         ITRACE ID=ASSEM
         L     R15,V_DISASSEM                   DISASSEM ENTRY POINT
         BALR  R14,R15                          ASSEMBLE SOURCE
         CH    R15,H4                           SERIOUS ERROR?
         BH    MAIN0280                         YES
         NI    DISCOMM_FLAGS,255-$DISCOMM_ASSEMBLE_SOURCE
MAIN019A DS    0H
         CLC   DISCOMM_OUTPUT_DSN,COMM_BLANKS   HAVE OUTPUT DATA SET?
         BE    ERR0010                          NO
         OC    DISCOMM_OPCODE_TABLE_ADDR,DISCOMM_OPCODE_TABLE_ADDR
         BNZ   MAIN0210
         LOAD  EPLOC=DISCOMM_OPCODE_TABLE_NAME  LOAD OPCODE TABLE
         ST    R0,DISCOMM_OPCODE_TABLE_ADDR     SAVE ADDRESS
         ITRACE ID=OP_TABLE,                                           +
               RDATA1=R1
MAIN0210 DS    0H
         CLC   DISCOMM_PREFIX,COMM_BLANKS       LABEL PREFIX GIVEN?
         BNE   MAIN0220                         YES
         ITRACE ID=DFLTPRFX
         MVC   DISCOMM_PREFIX,DEFAULT_PREFIX    SET DEFAULT PREFIX
         MVC   DISCOMM_PREFIX_LENGTH,H4         SET LENGTH
MAIN0220 DS    0H
         MVC   DISCOMM_TPUT_MSG(STAT_MSG_02_L),STAT_MSG_02
         BAL   R6,TPUT0000                      ISSUE TPUT
         L     R15,V_DISVBU
         ITRACE ID=VBU,                                                +
               RDATA1=R15
         BALR  R14,R15                          VERIFY BASES AND USINGS
         ITRACE ID=VBU_RC,                                             +
               RDATA1=R15
         LTR   R15,R15                          ANY ERRORS DETECTED?
         BNZ   MAIN0100                         YES
* ------------------------------------------------------------------- *
*        Connect USINGS with DSECTs                                   *
* ------------------------------------------------------------------- *
         MVC   DISCOMM_TPUT_MSG(STAT_MSG_03_L),STAT_MSG_03
         BAL   R6,TPUT0000                      ISSUE TPUT
         L     R15,V_DISDSECT
         ITRACE ID=DSECT,                                              +
               RDATA1=R15
         BALR  R14,R15                          CONNECT USING TO DSECT
         ITRACE ID=DSECT_RC,                                           +
               RDATA1=R15
         LTR   R15,R15                          ANY ERRORS DETECTED?
         BNZ   MAIN0100                         YES
* ------------------------------------------------------------------- *
*        Delete old "ALL areas"                                       *
* ------------------------------------------------------------------- *
         MVC   DISCOMM_TPUT_MSG(STAT_MSG_04_L),STAT_MSG_04
         BAL   R6,TPUT0000                      ISSUE TPUT
         ICM   R5,15,DISCOMM_ALL_AREA_NEXT      1ST AREA ON ALL CHAIN
         BZ    MAIN0240                         NOTHING ON CHAIN
         USING AREA_DSECT,R5
         XC    DISCOMM_ALL_AREA_NEXT,DISCOMM_ALL_AREA_NEXT
MAIN0230 DS    0H
         LR    R1,R5                            COPY ADDR
         L     R5,AREA_NEXT                     NEXT AREA BLOCK
         ITRACE ID=FREEAREA,                                           +
               RDATA1=R1
         FREEMAIN RU,                           FREE THIS AREA BLOCK   +
               A=(1),                                                  +
               LV=AREA_DSECT_L
         LTR   R5,R5                            ANOTHER AREA?
         BNZ   MAIN0230                         YES
* ------------------------------------------------------------------- *
*        Merge RLD areas with user specified data areas               *
* ------------------------------------------------------------------- *
MAIN0240 DS    0H
         MVC   DISCOMM_TPUT_MSG(STAT_MSG_05_L),STAT_MSG_05
         BAL   R6,TPUT0000                      ISSUE TPUT
         ITRACE ID=DISDMERG
         L     R15,V_DISDMERG
         BALR  R14,R15                          MERGE USER AND RLD DATA
         ITRACE ID=MERGE_X
         TM    DISCOMM_FLAGS,$DISCOMM_DEBUG     DEBUGGING ON?
         BNO   MAIN0250                         NO
         ICM   R1,15,DISCOMM_ALL_AREA_NEXT
         ITRACE ID=DEBUG_1,                                            +
               RDATA1=R1
         L     R15,V_DISDEBUG
         BALR  R14,R15
* ------------------------------------------------------------------- *
*        Scan text to determine where instructions occur              *
* ------------------------------------------------------------------- *
MAIN0250 DS    0H
         ITRACE ID=DISSCAN
         MVC   DISCOMM_TPUT_MSG(STAT_MSG_06_L),STAT_MSG_06
         BAL   R6,TPUT0000                      INFORM USER OF PROGRESS
         L     R15,V_DISSCAN
         BALR  R14,R15                          SCAN TEXT
         ITRACE ID=SCAN_RC,                                            +
               RDATA1=R15
         TM    DISCOMM_FLAGS,$DISCOMM_DEBUG     DEBUGGING ON?
         BNO   MAIN0260                         NO
         ICM   R1,15,DISCOMM_ALL_AREA_NEXT
         ITRACE ID=DEBUG_1,                                            +
               RDATA1=R1
         L     R15,V_DISDEBUG
         BALR  R14,R15
MAIN0260 DS    0H
* ------------------------------------------------------------------- *
*        Determine data references                                    *
* ------------------------------------------------------------------- *
         ITRACE ID=DISREF
         MVC   DISCOMM_TPUT_MSG(STAT_MSG_07_L),STAT_MSG_07
         BAL   R6,TPUT0000                      INFORM USER OF PROGRESS
         L     R15,V_DISREF
         BALR  R14,R15                          DETERMINE REFERENCES
         ITRACE ID=REF_X
         TM    DISCOMM_FLAGS,$DISCOMM_DEBUG     DEBUGGING ON?
         BNO   MAIN0270                         NO
         ICM   R1,15,DISCOMM_ALL_AREA_NEXT
         ITRACE ID=DEBUG_1,                                            +
               RDATA1=R1
         L     R15,V_DISDEBUG
         BALR  R14,R15
* ------------------------------------------------------------------- *
*                                                                     *
*                                                                     *
*                                                                     *
*        Generate source                                              *
*                                                                     *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0270 DS    0H
* ------------------------------------------------------------------- *
*        Make sure DISPUNCH and DISTEMP are free                      *
* ------------------------------------------------------------------- *
         MVI   DAIR_FUNC,$DAIR_FREE             SET FUNCTION
         MVI   DAIR_OPTS,0                      RESET ALL OPTIONS
         MVC   DAIR_DD,DD_DISPUNCH
         LA    R1,DAIRREQ                       DAIR PARMS
         L     R15,COMM_V_OSDAIR                OSDAIR ENTRY POINT
         ITRACE ID=FREEPNCH,                                           +
               RDATA1=R15,                                             +
               RDATA2=R1
         BALR  R14,R15                          FREE DISPUNCH
         ITRACE ID=DAIR_RC,                                            +
               RDATA1=R15
         MVC   DAIR_DD,DD_DISTEMP
         LA    R1,DAIRREQ                       DAIR PARMS
         L     R15,COMM_V_OSDAIR                OSDAIR ENTRY POINT
         ITRACE ID=FREETEMP,                                           +
               RDATA1=R15,                                             +
               RDATA2=R1
         BALR  R14,R15                          FREE DISPUNCH
         ITRACE ID=DAIR_RC,                                            +
               RDATA1=R15
* ------------------------------------------------------------------- *
*        Allocate DISPUNCH                                            *
* ------------------------------------------------------------------- *
         MVI   DAIR_FUNC,$DAIR_ALLOC            SET FUNCTION
         MVI   DAIR_OPTS,$DAIR_USE_DD           USE SPECIFIED DDNAME
         MVC   DAIR_DSN,DISCOMM_OUTPUT_DSN      DSN
         MVC   DAIR_DD,DD_DISPUNCH              DD NAME
         MVC   DAIR_MEMBER,DISCOMM_OUTPUT_MEMBER
         MVC   DAIR_VOLSER,DISCOMM_OUTPUT_VOLSER
         MVI   DAIR_DISP,$DAIR_DISP_SHR         DISP
         LA    R1,DAIRREQ
         L     R15,COMM_V_OSDAIR                OS DAIR ENTRY POINT
         ITRACE ID=ALOCPNCH,                                           +
               RDATA1=R15,                                             +
               RDATA2=R1
         BALR  R14,R15                          ALLOCATE DISPUNCH
         ITRACE ID=DAIR_RC,                                            +
               DATA1=DAIR_R15
         OC    DAIR_R15,DAIR_R15                SUCCESSFUL?
         BNZ   ERR0110                          NO
* ------------------------------------------------------------------- *
*        OPEN DISPUNCH                                                *
* ------------------------------------------------------------------- *
         ITRACE ID=OPENPNCH
         OPEN  (DISPUNCH_DCB,OUTPUT),           OPEN DISPUNCH          +
               MODE=31,                                                +
               MF=(E,DISCOMM_OPEN)
* ------------------------------------------------------------------- *
*        Allocate DISTEMP                                             *
* ------------------------------------------------------------------- *
         MVI   DAIR_OPTS,$DAIR_USE_DD+$DAIR_TEMP
         MVC   DAIR_DD,DD_DISTEMP               DD NAME
         MVC   DAIR_MEMBER,COMM_BLANKS
         MVC   DAIR_VOLSER,COMM_BLANKS
         MVC   DAIR_PRI,NINETY                  PRIMARY 90 TRACKS
         MVC   DAIR_SEC,NINETY                  SECONDARY 90 TRACKS
         LA    R1,DAIRREQ
         L     R15,COMM_V_OSDAIR                OS DAIR ENTRY POINT
         ITRACE ID=ALOCTEMP,                                           +
               RDATA1=R15,                                             +
               RDATA2=R1
         BALR  R14,R15                          ALLOCATE DISTEMP
         ITRACE ID=DAIR_RC,                                            +
               DATA1=DAIR_R15
         OC    DAIR_R15,DAIR_R15                SUCCESSFUL?
         BNZ   ERR0110                          NO
* ------------------------------------------------------------------- *
*        OPEN DISTEMP                                                 *
* ------------------------------------------------------------------- *
         ITRACE ID=OPENTEMP
         OPEN  (DISTEMP_DCB,OUTPUT),            OPEN DISTEMP           +
               MODE=31,                                                +
               MF=(E,DISCOMM_OPEN)
* ------------------------------------------------------------------- *
*        Generate assembler source                                    *
* ------------------------------------------------------------------- *
         MVC   DISCOMM_TPUT_MSG(STAT_MSG_08_L),STAT_MSG_08
         BAL   R6,TPUT0000                      ISSUE TPUT
         ITRACE ID=CALL_GEN
         L     R15,V_DISGEN                     DISGEN ENTRY POINT
         BALR  R14,R15                          CALL DISGEN
         ITRACE ID=GEN_X
* ------------------------------------------------------------------- *
*        CLOSE DISPUNCH                                               *
* ------------------------------------------------------------------- *
         ITRACE ID=CLSEPNCH
         CLOSE DISPUNCH_DCB,                    CLOSE DISPUNCH         +
               MODE=31,                                                +
               MF=(E,DISCOMM_CLOSE)
         FREEPOOL DISPUNCH_DCB
* ------------------------------------------------------------------- *
*        FREE DISPUNCH                                                *
* ------------------------------------------------------------------- *
         MVI   DAIR_FUNC,$DAIR_FREE             SET FUNCTION
         MVC   DAIR_DD,DD_DISPUNCH
         LA    R1,DAIRREQ                       DAIR PARMS
         L     R15,COMM_V_OSDAIR                OSDAIR ENTRY POINT
         ITRACE ID=FREEPNCH,                                           +
               RDATA1=R15,                                             +
               RDATA2=R1
         BALR  R14,R15                          FREE DISPUNCH
         ITRACE ID=DAIR_RC,                                            +
               RDATA1=R15
         OC    DAIR_R15,DAIR_R15                SUCCESSFUL?
         BNZ   ERR0110                          NO
* ------------------------------------------------------------------- *
*        CLOSE DISTEMP                                                *
* ------------------------------------------------------------------- *
         ITRACE ID=CLSETEMP
         CLOSE DISTEMP_DCB,                     CLOSE DISTEMP          +
               MODE=31,                                                +
               MF=(E,DISCOMM_CLOSE)
         FREEPOOL DISTEMP_DCB
* ------------------------------------------------------------------- *
*        Inform the user of success                                   *
* ------------------------------------------------------------------- *
         MVC   DISCOMM_TPUT_MSG(STAT_MSG_09_L),STAT_MSG_09
         BAL   R6,TPUT0000                      ISSUE TPUT
* ------------------------------------------------------------------- *
*        Get DDID for DISTEMP                                         *
* ------------------------------------------------------------------- *
         ITRACE ID=LMINIT
         LA    R0,FUNCTION_LMINIT
         ST    R0,DXD_SPF_1
         LA    R0,FUNCTION_DDID               DDID (RETURNED BY LMINIT)
         ST    R0,DXD_SPF_2
         LA    R0,COMM_BLANKS
         ST    R0,DXD_SPF_3
         ST    R0,DXD_SPF_4
         ST    R0,DXD_SPF_5
         ST    R0,DXD_SPF_6
         ST    R0,DXD_SPF_7
         ST    R0,DXD_SPF_8
         ST    R0,DXD_SPF_9
         LA    R0,DD_DISTEMP                  DDNAME
         ST    R0,DXD_SPF_10                  SET DDNAME ADDR
         OI    DXD_SPF_10,X'80'               SET END-OF-LIST
         LA    R1,DXD_SPF_PARMS               SPF PARMS
         L     R15,COMM_ISPLINK               ISPLINK ENTRY POINT
         BALR  R14,R15                        OBTAIN DDID
         ITRACE ID=LMINITRC,                                           +
               RDATA1=R15,                                             +
               DATA2=SPF_DDID
         LTR   R15,R15                        SUCCESSFUL?
         BNZ   ERR0010                        NO
         ITRACE ID=DDID,                                               +
               DATA1=SPF_DDID
*---------------------------------------------------------------------*
*        INVOKE EDIT                                                  *
*---------------------------------------------------------------------*
         LA    R0,FUNCTION_EDIT
         ST    R0,DXD_SPF_1
         LA    R0,COMM_BLANKS
         ST    R0,DXD_SPF_2
         ST    R0,DXD_SPF_3
         ST    R0,DXD_SPF_4
         ST    R0,DXD_SPF_5
         ST    R0,DXD_SPF_6
         ST    R0,DXD_SPF_7
         LA    R0,SPF_DDID
         ST    R0,DXD_SPF_8
         OI    DXD_SPF_8,X'80'
         LA    R1,DXD_SPF_PARMS
         ITRACE ID=EDIT,                                               +
               DATA1=SPF_DDID
         L     R15,COMM_ISPLINK
         BALR  R14,R15                          INVOKE EDIT
         ITRACE ID=EDIT_RC,                                            +
               RDATA1=R15
         CH    R15,H4                           RC > 4?
         BH    ERR0020                          YES
* ------------------------------------------------------------------- *
*        FREE DISTEMP                                                 *
* ------------------------------------------------------------------- *
         MVI   DAIR_FUNC,$DAIR_FREE             SET FUNCTION
         MVC   DAIR_DD,DD_DISTEMP
         LA    R1,DAIRREQ                       DAIR PARMS
         L     R15,COMM_V_OSDAIR                OSDAIR ENTRY POINT
         ITRACE ID=FREETEMP,                                           +
               RDATA1=R15,                                             +
               RDATA2=R1
         BALR  R14,R15                          FREE DISTEMP
         ITRACE ID=DAIR_RC,                                            +
               RDATA1=R15
         OC    DAIR_R15,DAIR_R15                SUCCESSFUL?
         BNZ   ERR0110                          NO
         B     MAIN0100
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0280 DS    0H
         ITRACE ID=ASMERROR
         CLC   SPF_MSG_1,COMM_BLANKS            ALREADY HAVE MESSAGE?
         BNE   MAIN0100                         YES
         MVC   SPF_MSG_1(MSG05_L),MSG05
         MVC   SPF_MSG_2(MSG06_L),MSG06
         B     MAIN0100
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0300 DS    0H
         ITRACE ID=KSDS
         MVC   SPF_FUNCTION,COMM_BLANKS         RESET COMMAND
         SR    R1,R1                            DISPLAY EXISTING KSDSes
         L     R15,V_DISKLIST                   DISKLIST ENTRY POINT
         BALR  R14,R15                          CALL DISKLIST
         ITRACE ID=KSDS_X
         B     MAIN0100
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0310 DS    0H
         ITRACE ID=OPTIONS
         MVC   SPF_FUNCTION,COMM_BLANKS         RESET COMMAND
         L     R15,V_DISOPTS                    DISOPTS ENTRY POINT
         BALR  R14,R15                          CALL DISOPTS
         ITRACE ID=OPTS_X
         B     MAIN0100
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0320 DS    0H
         ITRACE ID=RLD
         MVC   SPF_FUNCTION,COMM_BLANKS         RESET COMMAND
         L     R15,V_DISRLIST                   DISRLIST ENTRY POINT
         BALR  R14,R15                          CALL DISRLIST
         ITRACE ID=RLD_X
         B     MAIN0100
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0330 DS    0H
         ITRACE ID=SYSLIB
         MVC   SPF_FUNCTION,COMM_BLANKS         RESET COMMAND
         SR    R1,R1                            DISPLAY EXISTING LIBs
         L     R15,V_DISSLIST                   DISSLIST ENTRY POINT
         BALR  R14,R15                          CALL DISSLIST
         ITRACE ID=SYSLIB_X
         B     MAIN0100
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0340 DS    0H
         ITRACE ID=TEXT
         MVC   SPF_FUNCTION,COMM_BLANKS         RESET COMMAND
         L     R15,V_DISTLIST                   DISTLIST ENTRY POINT
         BALR  R14,R15                          CALL DISTLIST
         ITRACE ID=TEXT_X
         B     MAIN0100
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0350 DS    0H
         ITRACE ID=USINGS
         MVC   SPF_FUNCTION,COMM_BLANKS         RESET COMMAND
         L     R15,V_DISULIST                   DISULIST ENTRY POINT
         BALR  R14,R15                          CALL DISULIST
         ITRACE ID=ULIST_X
         B     MAIN0100
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0400 DS    0H
         ITRACE ID=DEBUGON
         OI    DISCOMM_FLAGS,$DISCOMM_DEBUG     SET DEBUG FLAG
         MVC   SPF_MSG_2(MSG03_L),MSG03
         B     MAIN0100
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0410 DS    0H
         ITRACE ID=DEBUGOFF
         NI    DISCOMM_FLAGS,255-$DISCOMM_DEBUG  RESET DEBUG FLAG
         MVC   SPF_MSG_2(MSG04_L),MSG04
         B     MAIN0100
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
TPUT0000 DS    0H
         TPUT  DISCOMM_TPUT_MSG,80
         MVC   DISCOMM_TPUT_MSG,COMM_BLANKS
         BR    R6
         DROP  R5
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
CLEAN000 DS    0H
         ITRACE ID=CLEAN
         ICM   R2,15,DISCOMM_ESD_NEXT           ANY ESD BLOCKS?
         BZ    CLEAN020                         NO
         XC    DISCOMM_ESD_NEXT,DISCOMM_ESD_NEXT
         USING ESD_DSECT,R2
CLEAN010 DS    0H
         LR    R1,R2
         ICM   R2,15,ESD_NEXT                   NEXT ESD BLOCK
         ITRACE ID=FREE_ESD,                                           +
               RDATA1=R1
         FREEMAIN RU,                                                  +
               A=(1),                                                  +
               LV=ESD_DSECT_L
         LTR   R2,R2
         BNZ   CLEAN010
CLEAN020 DS    0H
         DROP  R2
         ICM   R2,15,DISCOMM_RLD_AREA_NEXT      ANY RLD BLOCKS?
         BZ    CLEAN040                         NO
         XC    DISCOMM_RLD_AREA_NEXT,DISCOMM_RLD_AREA_NEXT
         USING AREA_DSECT,R2
CLEAN030 DS    0H
         LR    R1,R2
         ICM   R2,15,AREA_NEXT                  NEXT RLD BLOCK
         ITRACE ID=FREE_RLD,                                           +
               RDATA1=R1
         FREEMAIN RU,                                                  +
               A=(1),                                                  +
               LV=AREA_DSECT_L
         LTR   R2,R2
         BNZ   CLEAN030
CLEAN040 DS    0H
         ICM   R2,15,DISCOMM_USER_AREA_NEXT_1   ANY USER AREA BLOCKS?
         BZ    CLEAN060                         NO
         XC    DISCOMM_USER_AREA_NEXT_1,DISCOMM_USER_AREA_NEXT_1
         USING AREA_DSECT,R2
CLEAN050 DS    0H
         LR    R1,R2
         ICM   R2,15,AREA_NEXT                  NEXT USER BLOCK
         ITRACE ID=FREEUSR1,                                           +
               RDATA1=R1
         FREEMAIN RU,                                                  +
               A=(1),                                                  +
               LV=AREA_DSECT_L
         LTR   R2,R2
         BNZ   CLEAN050
CLEAN060 DS    0H
         ICM   R2,15,DISCOMM_ALL_AREA_NEXT      ANY ALL AREA BLOCKS?
         BZ    CLEAN080                         NO
         XC    DISCOMM_ALL_AREA_NEXT,DISCOMM_ALL_AREA_NEXT
         USING AREA_DSECT,R2
CLEAN070 DS    0H
         ICM   R3,15,AREA_LABEL                 ANY LABELS?
         BZ    CLEAN07B                         NO
         USING LABEL_DSECT,R3
CLEAN07A DS    0H
         LR    R1,R3                            COPY LABEL BLOCK ADDR
         ICM   R3,15,LABEL_NEXT
         ITRACE ID=FREELABL,                                           +
               RDATA1=R1
         FREEMAIN RU,                                                  +
               A=(1),                                                  +
               LV=LABEL_DSECT_L
         LTR   R3,R3                            ANOTHER LABEL?
         BNZ   CLEAN07A                         YES
         DROP  R3
CLEAN07B DS    0H
         LR    R1,R2                            COPY AREA BLOCK ADDR
         ICM   R2,15,AREA_NEXT                  NEXT ALL AREA BLOCK
         ITRACE ID=FREE_ALL,                                           +
               RDATA1=R1
         FREEMAIN RU,                                                  +
               A=(1),                                                  +
               LV=AREA_DSECT_L
         LTR   R2,R2
         BNZ   CLEAN070
CLEAN080 DS    0H
         DROP  R2
         ICM   R2,15,DISCOMM_BASE_NEXT          ANY BASE BLOCKS?
         BZ    CLEAN100                         NO
         XC    DISCOMM_BASE_NEXT,DISCOMM_BASE_NEXT
CLEAN090 DS    0H
         USING BASE_DSECT,R2
         LR    R1,R2
         ICM   R2,15,BASE_NEXT                  NEXT BASE BLOCK
         ITRACE ID=FREEBASE,                                           +
               RDATA1=R1
         FREEMAIN RU,                                                  +
               A=(1),                                                  +
               LV=BASE_DSECT_L
         LTR   R2,R2                            ANOTHER BASE BLOCK?
         BNZ   CLEAN090                         YES
CLEAN100 DS    0H
         DROP  R2
         ICM   R2,15,DISCOMM_DSECT_NEXT         ANY DSECT BLOCKS?
         BZ    CLEAN160                         NO
         XC    DISCOMM_DSECT_NEXT,DISCOMM_DSECT_NEXT
CLEAN110 DS    0H
         USING DSECT_DSECT,R2
         ICM   R3,15,DSECT_LABEL_NEXT           ANY LABELS?
         BNZ   CLEAN150                         NO
         USING LABEL_DSECT,R3
CLEAN120 DS    0H
         ICM   R4,15,LABEL_EQU_NEXT             ANY EQUATES?
         BZ    CLEAN140                         NO
         USING EQU_DSECT,R4
CLEAN130 DS    0H
         LR    R1,R4                            COPY EQU BLOCK ADDR
         ICM   R4,15,EQU_NEXT                   NEXT EQU BLOCK
         ITRACE ID=FREEEQU1,                                           +
               RDATA1=R1
         FREEMAIN RU,                           FREE EQU BLOCK         +
               A=(1),                                                  +
               LV=EQU_DSECT_L
         LTR   R4,R4                            ANOTHER EQU BLOCK
         BNZ   CLEAN130                         YES
         DROP  R4
CLEAN140 DS    0H
         LR    R1,R3                            COPY LABEL BLOCK ADDR
         ICM   R3,15,LABEL_NEXT                 NEXT LABEL
         ITRACE ID=FREELBL1,                                           +
               RDATA1=R1
         FREEMAIN RU,                           FREE LABEL BLOCK       +
               A=(1),                                                  +
               LV=LABEL_DSECT_L
         LTR   R3,R3                            ANOTHER LABEL BLOCK?
         BNZ   CLEAN120                         YES
         DROP  R3
CLEAN150 DS    0H
         LR    R1,R2                            COPY DSECT BLOCK ADDR
         ICM   R2,15,DSECT_NEXT                 NEXT DSECT BLOCK
         ITRACE ID=FREEDSC1,                                           +
               RDATA1=R1
         FREEMAIN RU,                           FREE DSECT BLOCK       +
               A=(1),                                                  +
               LV=DSECT_DSECT_L
         LTR   R2,R2                            ANOTHER DSECT BLOCK?
         BNZ   CLEAN110                         YES
CLEAN160 DS    0H
         DROP  R2
         ICM   R2,15,DISCOMM_USING_NEXT         ANY USING BLOCKS?
         BZ    CLEAN180                         NO
         USING USING_DSECT,R2
         XC    DISCOMM_USING_NEXT,DISCOMM_USING_NEXT
CLEAN170 DS    0H
         LR    R1,R2                            COPY USING BLOCK ADDR
         ICM   R2,15,USING_NEXT                 NEXT USING BLOCK
         ITRACE ID=FREEUSNG,                                           +
               RDATA1=R1
         FREEMAIN RU,                           FREE USING BLOCK       +
               A=(1),                                                  +
               LV=USING_DSECT_L
         LTR   R2,R2                            ANOTHER USING BLOCK?
         BNZ   CLEAN170                         YES
         DROP  R2
CLEAN180 DS    0H
         ICM   R2,15,DISCOMM_KSDS_NEXT          ANY DSECT KSDS?
         BZ    CLEAN270                         NO
         XC    DISCOMM_KSDS_NEXT,DISCOMM_KSDS_NEXT
         USING KSDS_DSECT,R2
CLEAN190 DS    0H
         ICM   R3,15,KSDS_DSECT_NEXT            ANY DSECT BLOCKS?
         BZ    CLEAN250                         NO
         USING DSECT_DSECT,R3
CLEAN200 DS    0H
         ITRACE ID=DSECT,                                              +
               DATA1=DSECT_NAME+0,                                     +
               DATA2=DSECT_NAME+8
         ICM   R4,15,DSECT_LABEL_NEXT           ANY LABELS?
         BZ    CLEAN240                         NO
         USING LABEL_DSECT,R4
CLEAN210 DS    0H
         ITRACE ID=LABEL,                                              +
               DATA1=LABEL_NAME+0,                                     +
               DATA2=LABEL_NAME+8
         ICM   R5,15,LABEL_EQU_NEXT             ANY EQUATES?
         BZ    CLEAN230                         NO
         USING EQU_DSECT,R5
CLEAN220 DS    0H
         ITRACE ID=EQU,                                                +
               DATA1=EQU_LABEL+0,                                      +
               DATA2=EQU_LABEL+8
         LR    R1,R5                            COPY EQU BLOCK ADDR
         ICM   R5,15,EQU_NEXT                   NEXT EQU BLOCK
         ITRACE ID=FREEEQU2,                                           +
               RDATA1=R1
         FREEMAIN RU,                           FREE EQU BLOCK         +
               A=(1),                                                  +
               LV=EQU_DSECT_L
         LTR   R5,R5                            ANOTHER EQU BLOCK
         BNZ   CLEAN220                         YES
         DROP  R5
CLEAN230 DS    0H
         LR    R1,R4                            COPY LABEL BLOCK ADDR
         ICM   R4,15,LABEL_NEXT                 NEXT LABEL
         ITRACE ID=FREELBL2,                                           +
               RDATA1=R1
         FREEMAIN RU,                           FREE LABEL BLOCK       +
               A=(1),                                                  +
               LV=LABEL_DSECT_L
         LTR   R4,R4                            ANOTHER LABEL BLOCK?
         BNZ   CLEAN210                         YES
         DROP  R4
CLEAN240 DS    0H
         LR    R1,R3                            COPY DSECT BLOCK ADDR
         ICM   R3,15,DSECT_NEXT                 NEXT DSECT BLOCK
         ITRACE ID=FREEDSC2,                                           +
               DATA1=DSECT_NAME,                                       +
               RDATA2=R1
         FREEMAIN RU,                           FREE DSECT BLOCK       +
               A=(1),                                                  +
               LV=DSECT_DSECT_L
         LTR   R3,R3                            ANOTHER DSECT BLOCK?
         BNZ   CLEAN200                         YES
         DROP  R3
CLEAN250 DS    0H
         TM    KSDS_FLAGS,$KSDS_OPEN            KSDS OPEN?
         BNO   CLEAN260                         NO
         ITRACE ID=CLOSE,                                              +
               DATA1=KSDS_DDNAME
         CLOSE KSDS_ACB,                                               +
               MODE=31,                                                +
               MF=(E,DISCOMM_CLOSE)
CLEAN260 DS    0H
         ITRACE ID=FREE_DD,                                            +
               DATA1=KSDS_DDNAME
         MVI   DAIR_FUNC,$DAIR_FREE             SET COMMAND
         MVI   DAIR_OPTS,$DAIR_NO_MSGS          SET OPTIONS
         MVC   DAIR_DD,KSDS_DDNAME              COPY DDNAME
         LA    R1,DAIRREQ
         L     R15,COMM_V_OSDAIR                OSDAIR ENTRY POINT
         BALR  R14,R15                          CALL OSDAIR
         LR    R1,R2                            COPY DSECT BLOCK ADDR
         ICM   R2,15,KSDS_NEXT                  NEXT DSECT BLOCK
         ITRACE ID=FREEKSDS,                                           +
               DATA1=KSDS_DDNAME,                                      +
               RDATA2=R1
         FREEMAIN RU,                           FREE DSECT BLOCK       +
               A=(1),                                                  +
               LV=KSDS_DSECT_L
         LTR   R2,R2                            ANOTHER KSDS BLOCK?
         BNZ   CLEAN190                         YES
CLEAN270 DS    0H
         ICM   R2,15,DISCOMM_SYSLIB_NEXT        ANY SYSLIB BLOCKS?
         BZ    CLEAN290                         NO
         XC    DISCOMM_SYSLIB_NEXT,DISCOMM_SYSLIB_NEXT
         USING SYSLIB_DSECT,R2
CLEAN280 DS    0H
         LR    R1,R2                            COPY SYSLIB BLOCK
         ICM   R2,15,SYSLIB_NEXT
         ITRACE ID=FREESLIB,                                           +
               DATA1=SYSLIB_DDNAME,                                    +
               RDATA2=R1
         FREEMAIN RU,                           FREE SYSLIB BLOCK      +
               A=(1),                                                  +
               LV=SYSLIB_DSECT_L
         LTR   R2,R2                            ANOTHER DSECT BLOCK?
         BNZ   CLEAN280                         YES
CLEAN290 DS    0H
         DROP  R2
         ICM   R2,15,DISCOMM_ASM_NEXT           ANY ASM BLOCKS?
         BZ    CLEAN310                         NO
         XC    DISCOMM_ASM_NEXT,DISCOMM_ASM_NEXT
         USING ASM_DSECT,R2
CLEAN300 DS    0H
         LR    R1,R2                            COPY ASM BLOCK
         ICM   R2,15,ASM_NEXT
         ITRACE ID=FREE_ASM,                                           +
               RDATA1=R1
         FREEMAIN RU,                           FREE ASM BLOCK         +
               A=(1),                                                  +
               LV=ASM_DSECT_L
         LTR   R2,R2                            ANOTHER ASM BLOCK?
         BNZ   CLEAN300                         YES
CLEAN310 DS    0H
         DROP  R2
         L     R0,DISCOMM_CSECT_LENGTH
         ICM   R1,15,DISCOMM_CSECT_TEXT
         BZ    CLEAN320
         XC    DISCOMM_CSECT_TEXT,DISCOMM_CSECT_TEXT
         XC    DISCOMM_CSECT_LENGTH,DISCOMM_CSECT_LENGTH
         ITRACE ID=FREETEXT,                                           +
               RDATA1=R1,                                              +
               RDATA2=R0
         FREEMAIN RU,                           FREE CSECT TEXT        +
               A=(1),                                                  +
               LV=(0)
CLEAN320 DS    0H
         BR    R6
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0010  DS    0H
         MVI   COMM_MSG_ID+1,1
         B     ERR0100
ERR0020  DS    0H
         MVI   COMM_MSG_ID+1,2
         STH   R15,COMM_INFO_01
         B     ERR0100
ERR0100  DS    0H
         MVI   COMM_MSG_ID,0
         MVC   COMM_MSG_CSECT,MODID
         L     R15,COMM_V_OSMSG
         BALR  R14,R15
ERR0110  DS    0H
         MVC   SPF_MSG_1,COMM_MSG_1             COPY MESSAGE
         MVC   SPF_MSG_2,COMM_MSG_2             COPY MESSAGE
         MVC   SPF_MSG_3,COMM_MSG_3             COPY MESSAGE
         MVC   SPF_MSG_4,COMM_MSG_4             COPY MESSAGE
         MVC   SPF_MSG_5,COMM_MSG_5             COPY MESSAGE
         B     MAIN0100                         DISPLAY WITH MSG
* ------------------------------------------------------------------- *
*                                                                     *
*     Assuming that I ever get this thing to work....                 *
*       Free all the AREA blocks (RLD, ESD, DATA,.. )                 *
*       Free all the ASM blocks                                       *
*       Free all the BASE blocks                                      *
*       Free all the DSECT blocks (and LABELs, EQU's chained)         *
*       Free all the KSDS  blocks                                     *
*       Free all the NAME  blocks                                     *
*       Free all the SYSLIB blocks                                    *
*       Free all the USING blocks                                     *
*       Free the text                                                 *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=CLEANUP
         ICM   R2,15,DISCOMM_NAME_NEXT          ANY NAME BLOCKS?
         BZ    EXIT0020
         USING NAME_DSECT,R2
EXIT0010 DS    0H
         LR    R1,R2
         ICM   R2,15,NAME_NEXT                  NEXT NAME BLOCK
         USING NAME_DSECT,R2
         ITRACE ID=FREENAME,                                           +
               RDATA1=R1
         FREEMAIN RU,                                                  +
               A=(1),                                                  +
               LV=NAME_DSECT_L
         LTR   R2,R2
         BNZ   EXIT0010
EXIT0020 DS    0H
         L     R1,SESS_FUNCTION_DATA
         ITRACE ID=DEL_DATA,                    FREE DIRECTORY DATA    +
               RDATA1=R1
         FREEMAIN RU,                                                  +
               A=(1),                                                  +
               LV=DIR_RECORD_L
         XC    SESS_FUNCTION_DATA,SESS_FUNCTION_DATA
         ITRACE ID=DELMSGS
         DELETE EP=DISMSGUS                     DELETE MESSAGES
         XC    SESS_MESSAGES,SESS_MESSAGES
         ITRACE ID=DELCOMM
         DELETE EP=DISCOMM                      DELETE COMMON AREA
         ICM   R1,15,SESS_DXD_ADDR              COPY WORK AREA ADDR
         BZ    EXIT0030
         L     R0,SESS_DXD_LENGTH
         ITRACE ID=FREE_DXD,                                           +
               RDATA1=R1,                                              +
               RDATA2=R0
         L     R13,4(,R13)                      RESTORE R13
         FREEMAIN RU,                           FREEMAIN WORK AREA     +
               A=(R1),                                                 +
               LV=(0)
         XC    SESS_DXD_ADDR,SESS_DXD_ADDR      RESET ADDRESS
         XC    SESS_DXD_LENGTH,SESS_DXD_LENGTH  RESET LENGTH
EXIT0030 DS    0H
         LM    R14,R12,12(R13)
         SR    R15,R15
         BR    R14
EXIT0040 DS    0H
         ITRACE ID=ENDING
         MVI   COMM_SESS_FUNC,$SESS_REMOVE      SET FUNCTION
EXIT0050 DS    0H
         ITRACE ID=EXIT
         L     R13,4(,R13)                      RESTORE R13
         LM    R14,R12,12(R13)                  RESTORE REGISTERS
         SR    R15,R15                          SET RC
         BR    R14                              RETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXD_LENGTH     CXD
DXD_START      DC       Q(DXDMAIN)
V_DISALIST     DC       V(DISALIST)
V_DISASSEM     DC       V(DISASSEM)
V_DISBLIST     DC       V(DISBLIST)
V_DISBIND      DC       V(DISBIND)
V_DISCONF      DC       V(DISCONF)
V_DISDEBUG     DC       V(DISDEBUG)
V_DISDLIST     DC       V(DISDLIST)
V_DISDMERG     DC       V(DISDMERG)
V_DISDSECT     DC       V(DISDSECT)
V_DISELIST     DC       V(DISELIST)
V_DISGEN       DC       V(DISGEN)
V_DISKLIST     DC       V(DISKLIST)
V_DISNLIST     DC       V(DISNLIST)
V_DISOPTS      DC       V(DISOPTS)
V_DISREF       DC       V(DISREF)
V_DISRLIST     DC       V(DISRLIST)
V_DISSCAN      DC       V(DISSCAN)
V_DISSLIST     DC       V(DISSLIST)
V_DISTLIST     DC       V(DISTLIST)
V_DISULIST     DC       V(DISULIST)
V_DISVBU       DC       V(DISVBU)

F1             DC       F'1'
H4             DC       H'4'

DD_DISPUNCH    DC       CL8'DISPUNCH'
DD_DISTEMP     DC       CL8'DISTEMP'
DEFAULT_PREFIX DC       C'LABL'
FUNCTION_EDIT   DC      CL8'EDIT'
FUNCTION_DDID   DC      CL8'DDID'
FUNCTION_LMINIT DC      CL8'LMINIT'
MENU_PANEL     DC       CL8'DISMENU'
NAMELIST_PANEL DC       CL8'DISNLIST'
NINETY         DC       AL3(90)

MSG01          DS       0C
               DC       C'DISMAIN01I '
               DC       C' Enter a selection'
MSG01_L        EQU      *-MSG01

MSG02          DS       0C
               DC       C'DISMAIN02E '
               DC       C' Invalid function code'
MSG02_L        EQU      *-MSG02

MSG03          DS       0C
               DC       C'DISMAIN03I '
               DC       C' Debugging activated'
MSG03_L        EQU      *-MSG03

MSG04          DS       0C
               DC       C'DISMAIN04I '
               DC       C' Debugging de-activated'
MSG04_L        EQU      *-MSG04

MSG05          DS       0C
               DC       C'DISMAIN05E '
               DC       C'Error(s) assembling source'
MSG05_L        EQU      *-MSG05

MSG06          DS       0C
               DC       C'DISMAIN06E '
               DC       C'Correct source or SYSLIBs (option A or S)'
MSG06_L        EQU      *-MSG06

STAT_MSG_01    DS       0C
               DC       C'Assembling source from option A'
STAT_MSG_01_L  EQU      *-STAT_MSG_01

STAT_MSG_02    DS       0C
               DC       C'Verifying BASEs and USINGs'
STAT_MSG_02_L  EQU      *-STAT_MSG_02

STAT_MSG_03    DS       0C
               DC       C'Connecting USINGs to DSECTs'
STAT_MSG_03_L  EQU      *-STAT_MSG_03

STAT_MSG_04    DS       0C
               DC       C'Deleting old ALL areas chain'
STAT_MSG_04_L  EQU      *-STAT_MSG_04

STAT_MSG_05    DS       0C
               DC       C'Building new ALL areas chain'
STAT_MSG_05_L  EQU      *-STAT_MSG_05

STAT_MSG_06    DS       0C
               DC       C'Scanning text to determine where instructions+
                occur'
STAT_MSG_06_L  EQU      *-STAT_MSG_06

STAT_MSG_07    DS       0C
               DC       C'Determining data references'
STAT_MSG_07_L  EQU      *-STAT_MSG_07

STAT_MSG_08    DS       0C
               DC       C'Generating source'
STAT_MSG_08_L  EQU      *-STAT_MSG_08

STAT_MSG_09    DS       0C
               DC       C'Source generation complete'
STAT_MSG_09_L  EQU      *-STAT_MSG_09


               LTORG
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDMAIN          DSECT
                 COPY     DXDPREF

DXD_SPF_PARMS    DS       0A
DXD_SPF_1        DS       A
DXD_SPF_2        DS       A
DXD_SPF_3        DS       A
DXD_SPF_4        DS       A
DXD_SPF_5        DS       A
DXD_SPF_6        DS       A
DXD_SPF_7        DS       A
DXD_SPF_8        DS       A
DXD_SPF_9        DS       A
DXD_SPF_10       DS       A

DXD_RC           DS       H

                 DAIRREQ  DSECT=NO
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 DISCOMM  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 DISASMDA
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DIR_DSECT        DSECT
                 COPY     DIR
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 BPXYSTAT DSECT=YES,LIST=NO
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COPY     OPERAND
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 OSSPFD   TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COPY     REGEQU
                 END      DISMAIN
