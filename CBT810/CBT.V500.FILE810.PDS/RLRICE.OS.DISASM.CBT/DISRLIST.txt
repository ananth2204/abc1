* ------------------------------------------------------------------- *
*                                                                     *
*  MODULE NAME: DISRLIST                                              *
*                                                                     *
*  FUNCTION:                                                          *
*   PRINT RLD ITEMS                                                   *
*                                                                     *
* ------------------------------------------------------------------- *
DISRLIST CSECT
DISRLIST AMODE 31
DISRLIST RMODE ANY
         USING DISRLIST,R15
         B     MOD0000                           SKIP EYECATCHER
MODID    DC    CL8'DISRLIST'
         DC    C'&SYSDATE'
         DC    C'&SYSTIME'
MOD0000  DS    0H
         DROP  R15
         STM   R14,R12,12(R13)                   SAVE REGS
         LR    R10,R15                           SET BASE REG
         USING OSCOMM,R12
         USING SESSION,R11
         USING DISRLIST,R10
         USING DISCOMM,R9
         L     R15,SESS_DXD_ADDR                 WORK AREA
         A     R15,DXD_START                     PLUS DISP
         ST    R13,4(,R15)
         ST    R15,8(,R13)
         LR    R13,R15
         USING DXDRLIST,R13
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY
         L     R8,COMM_OSSPFD
         USING OSSPFD,R8
         ICM   R5,15,DISCOMM_RLD_AREA_NEXT       FIRST AREA
         BZ    MAIN0300                          NO RLD ITEMS
         USING AREA_DSECT,R5
MAIN0000 DS    0H
         L     R0,DISCOMM_VDATA_ADDR
         L     R1,DISCOMM_VDATA_LENGTH
         SR    R14,R14
         SR    R15,R15
         MVCL  R0,R14
         L     R6,SPF_VWIDTH                     SCREEN WIDTH
         SH    R6,=Y(MSG01_L)                    MINUS LENGTH OF DETAIL
         SRL   R6,1                              DIVIDE BY 2
         A     R6,DISCOMM_VDATA_ADDR             PLUS VDATA ADDRESS
         L     R7,SPF_VDEPTH                     NBR OF LINES
         ST    R5,DXD_FIRST_DISPLAYED            FIRST ITEM DISPLAYED
MAIN0010 DS    0H
         ST    R5,DXD_LAST_DISPLAYED             LAST ITEM DISPLAYED
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
P        USING MSG01,R6
         CLI   AREA_DATA_TYPE,$DATA_ACON         ADDRESS CONSTANT?
         BE    MAIN0020                          YES
         CLI   AREA_DATA_TYPE,$DATA_VCON         V-CONSTANT?
         BE    MAIN0030                          YES
         CLI   AREA_DATA_TYPE,$DATA_Q            Q-CONSTANT?
         BE    MAIN0040                          YES
         CLI   AREA_DATA_TYPE,$DATA_CXD          CXD?
         BE    MAIN0050                          YES
         CLI   AREA_DATA_TYPE,$DATA_USER         USER DEFINED AREA?
         BE    MAIN0060                          YES
         DC    H'0'                              SHOULD NOT HAPPEN
MAIN0020 DS    0H
         MVI   P.MSG01_ATTR,$SCREEN_ATTR_HIGH
         MVC   P.MSG01_TYPE,C_ADCON
         B     MAIN0070
MAIN0030 DS    0H
         MVI   P.MSG01_ATTR,$SCREEN_ATTR_HIGH_TURQUOISE
         MVC   P.MSG01_TYPE,C_VCON
         B     MAIN0070
MAIN0040 DS    0H
         MVI   P.MSG01_ATTR,$SCREEN_ATTR_HIGH_YELLOW
         MVC   P.MSG01_TYPE,C_Q
         B     MAIN0070
MAIN0050 DS    0H
         MVI   P.MSG01_ATTR,$SCREEN_ATTR_HIGH_RED
         MVC   P.MSG01_TYPE,C_CXD
         B     MAIN0070
MAIN0060 DS    0H
         MVC   P.MSG01_TYPE,C_USER
MAIN0070 DS    0H
         ICM   R1,15,AREA_LENGTH                 ITEM'S LENGTH
         CVD   R1,COMM_DWORD                     CONVERT TO DECIMAL
         MVC   P.MSG01_LENGTH,=X'40202120'       EDIT LENGTH
         ED    P.MSG01_LENGTH,COMM_DWORD+6
         UNPK  P.MSG01_BEGIN(9),AREA_BEGIN(5)    UNPACK BEGIN DISP
         TR    P.MSG01_BEGIN,COMM_HEXCHAR        TRANSLATE IT
         MVI   P.MSG01_BEGIN+8,C' '
         UNPK  P.MSG01_END(9),AREA_END(5)        UNPACK END DISP
         TR    P.MSG01_END,COMM_HEXCHAR          TRANSLATE IT
         MVI   P.MSG01_END+8,C' '
         TM    AREA_FLAGS,$AREA_RLD_PLUS         DIRECTION "+"?
         BO    MAIN0080                          YES
         MVI   P.MSG01_DIRECTION,C'-'            SHOW DIRECTION '-'
         B     MAIN0090
MAIN0080 DS    0H
         MVI   P.MSG01_DIRECTION,C'+'            SHOW DIRECTION '+'
MAIN0090 DS    0H
         MVC   P.MSG01_NAME,AREA_RLD_NAME        COPY NAME (Q, VCON)
         A     R6,SPF_VWIDTH                     NEXT LINE
         BCT   R7,MAIN0100
         B     MAIN0110
MAIN0100 DS    0H
         ICM   R5,15,AREA_NEXT                   NEXT AREA
         BNZ   MAIN0010
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0110 DS    0H
         ITRACE ID=DISPLAY
         MVC   SESS_DISP_PANEL,PANEL_DISRLIST    SET PANEL NAME
         MVC   SESS_DISP_VDATA_ADDR,DISCOMM_VDATA_ADDR
         MVC   SESS_DISP_VDATA_LENGTH,DISCOMM_VDATA_LENGTH
         L     R15,COMM_V_OSDISP                 DISP ENTRY POINT
         BALR  R14,R15                           DISPLAY
         ITRACE ID=DISP_RC,                                            +
               RDATA1=R15,                                             +
               DATA2=SPF_ZVERB
         LTR   R15,R15                           RC = 0?
         BNZ   EXIT0000                          NO
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         CLI   SPF_ZVERB,C'B'                    BOTTOM?
         BE    MAIN0200
         CLI   SPF_ZVERB,C'D'                    DOWN?
         BE    MAIN0220
         CLI   SPF_ZVERB,C'T'                    TOP?
         BE    MAIN0230
         CLI   SPF_ZVERB,C'U'                    UP?
         BE    MAIN0240
         B     MAIN0110
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0200 DS    0H
         ITRACE ID=BOTTOM
         ICM   R1,15,DISCOMM_RLD_AREA_NEXT       FIRST ON CHAIN
TEMP     USING AREA_DSECT,R1
         BZ    EXIT0000                          SHOULD NOT HAPPEN!
MAIN0210 DS    0H
         LR    R5,R1                             SAVE THIS ADDR
         ICM   R1,15,TEMP.AREA_NEXT              NEXT AREA
         BNZ   MAIN0210
         B     MAIN0000                          BUILD DISPLAY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0220 DS    0H
         ITRACE ID=DOWN
         CLI   SPF_ZSCROLLA,C'M'                 MAX?
         BE    MAIN0200                          YES
         L     R5,DXD_LAST_DISPLAYED             LAST ITEM DISPLAYED
         B     MAIN0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0230 DS    0H
         ITRACE ID=TOP
         L     R5,DISCOMM_RLD_AREA_NEXT          FIRST DATA ITEM
         B     MAIN0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0240 DS    0H
         ITRACE ID=UP
         CLI   SPF_ZSCROLLA,C'M'                 MAX?
         BE    MAIN0230                          YES
         ICM   R1,15,DXD_FIRST_DISPLAYED         FIRST ON DISPLAY
         BZ    EXIT0000                          SHOULD NOT HAPPEN!
         L     R7,SPF_ZSCROLLN                   LINES ON DISPLAY
MAIN0250 DS    0H
         LR    R5,R1                             COPY ADDR
         ICM   R1,15,TEMP.AREA_PREV              PREVIOUS AREA
         BZ    MAIN0000
         BCT   R7,MAIN0250                       LOOP
         B     MAIN0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0300 DS    0H
         ITRACE ID=NO_RLD
         MVC   SPF_MSG_3(MSG02_L),MSG02
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=EXIT
EXIT0010 DS    0H
         L     R13,4(,R13)                       RESTORE REGISTER 13    ASE01670
         L     R14,12(,R13)                      RESTORE REGISTERS      ASE01680
         LR    R15,R2                            SET RC
         LM    R0,R12,20(R13)                    RESTORE REGISTERS      ASE01680
         BR    R14                               RETURN TO CALLER       ASE01700
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXD_START         DC   Q(DXDRLIST)
C_ADCON           DC   CL5'ADCON'
C_VCON            DC   CL5'VCON'
C_Q               DC   CL5'Q'
C_CXD             DC   CL5'CXD'
C_USER            DC   CL5'USER'
PANEL_DISRLIST    DC   C'DISRLIST'

MSG01             DS   0C
MSG01_ATTR        DC   AL1($SCREEN_ATTR_NORMAL)
MSG01_TYPE        DC   CL5' '
                  DC   CL1' '
                  DC   C' '
MSG01_BEGIN       DC   CL8' '
                  DC   C' '
MSG01_END         DC   CL8' '
                  DC   C' '
MSG01_LENGTH      DC   X'40202120'
                  DC   C' '
MSG01_DIRECTION   DC   C' '
                  DC   CL2' '
MSG01_NAME        DC   CL8' '
MSG01_L           EQU  *-MSG01

MSG02             DC   C'DISRLIST04I  No RLD items to display'
MSG02_L           EQU  *-MSG02
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDRLIST              DSECT
                      COPY    DXDPREF

DXD_FIRST_DISPLAYED   DS  A
DXD_LAST_DISPLAYED    DS  A
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 DISCOMM  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 DISASMDA
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 BPXYSTAT DSECT=YES,LIST=NO
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 OSSPFD   TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COPY     ATTRS
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COPY     REGEQU
              END     DISRLIST
