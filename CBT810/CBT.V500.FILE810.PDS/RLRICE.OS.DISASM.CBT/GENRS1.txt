*--------------------------------------------------------------------*
*                                                                    *
*  Module name: GENRS1                                               *
*                                                                    *
*  Function:                                                         *
*   GENERATE RS TYPE INSTRUCTIONS  (SLL, SRL, SRA, SLA, ...)         *
*                                                                    *
*  At entry R7 is AREA address                                       *
*           R5 is address of instruction text                        *
*           R4 is address of OPCODE entry                            *
*                                                                    *
*--------------------------------------------------------------------*
GENRS1   CSECT
GENRS1   AMODE 31
GENRS1   RMODE ANY
         USING GENRS1,R15
         USING OSCOMM,R12
         USING SESSION,R11
         USING DISCOMM,R9
         USING AREA_DSECT,R7
         USING OPCODE_DSECT,R4
         B     MOD0000                        SKIP EYECATCHER
MODID    DC    CL8'GENRS1'
         DC    C'&SYSDATE'
         DC    C'&SYSTIME'
MOD0000  DS    0H
         STM   R14,R12,12(R13)                SAVE REGS
         LR    R10,R15                        SET BASE REG
         DROP  R15
         USING GENRS1,R10
         L     R15,SESS_DXD_ADDR              WORK AREA
         A     R15,DXD_START                  PLUS DISP
         ST    R13,4(,R15)
         ST    R15,8(,R13)
         LR    R13,R15                        SET SAVE AREA ADDRESS
         USING DXDRS1,R13
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY,                                              +
               RDATA1=R7,                                              +
               DATA2=(0(R5),4)
         MVC   DXD_BLANKS,COMM_BLANKS
         LA    R1,DISCOMM_SOURCE_OPERAND1
*--------------------------------------------------------------------*
*        GENERATE R                                                  *
*--------------------------------------------------------------------*
         SR    R3,R3                          CLEAR R3
         IC    R3,1(R5)                       INSERT R1 AND R2
         SRL   R3,4                           SHIFT OUT R2
         STC   R3,DXD_R                       SAVE R1
         LA    R1,DISCOMM_SOURCE_OPERAND1
         MVI   0(R1),C'R'                     INSERT 'R' FOR R1
         IC    R3,DXD_R                       R1
         SLL   R3,1                           MULTIPLY BY 2
         LA    R2,REGISTER_TABLE(R3)          REGISTSER NAME
         MVC   DISCOMM_SOURCE_OPERAND1+1(2),0(R2)
         BAL   R6,SCAN0000                    FIND 1ST BLANK
         MVI   0(R1),C','                     INSERT COMMA
*--------------------------------------------------------------------*
*        GENERATE "nnn"                                              *
*--------------------------------------------------------------------*
         MVC   DXD_DISP,2(R5)                 COPY
         NI    DXD_DISP,X'0F'                 FORCE ZERO
         LH    R0,DXD_DISP                    DISP
         CVD   R0,COMM_DWORD                  CONVERT TO DECIMAL
         MVC   DXD_EDIT_WORD,EDIT_WORD
         ED    DXD_EDIT_WORD,COMM_DWORD+4     EDIT DISP
         MVC   1(4,R1),DXD_EDIT_WORD+4        COPY BITS TO SHIFT
         B     EXIT0000
*--------------------------------------------------------------------*
*        Find next blank                                             *
*--------------------------------------------------------------------*
SCAN0000 DS    0H
         CLI   0(R1),C' '
         BER   R6
         LA    R1,1(,R1)
         B     SCAN0000
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
EXIT0000 DS    0H
         ITRACE ID=EXIT
         L     R13,4(,R13)                    RESTORE REGISTER 13       ASE01670
         LM    R14,R12,12(R13)                RESTORE OTHER REGISTERS   ASE01680
         SR    R15,R15                        RETURN CODE               ASE01690
         BR    R14                            RETURN TO CALLER          ASE01700
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
DXD_START      DC   Q(DXDRS1)
EDIT_WORD      DC   X'F020202020202120'

REGISTER_TABLE DS   0C
               DC   CL2'0 '
               DC   CL2'1 '
               DC   CL2'2 '
               DC   CL2'3 '
               DC   CL2'4 '
               DC   CL2'5 '
               DC   CL2'6 '
               DC   CL2'7 '
               DC   CL2'8 '
               DC   CL2'9 '
               DC   CL2'10'
               DC   CL2'11'
               DC   CL2'12'
               DC   CL2'13'
               DC   CL2'14'
               DC   CL2'15'
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
DXDRS1         DSECT
               COPY DXDPREF
DXD_DISP       DS   XL2
DXD_R          DS   X

DXD_EDIT_WORD  DS   XL8
DXD_BLANKS     DS   CL8
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               DISCOMM  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               DISASMDA
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               BPXYSTAT DSECT=YES,LIST=NO
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY     REGEQU
               END  GENRS1
