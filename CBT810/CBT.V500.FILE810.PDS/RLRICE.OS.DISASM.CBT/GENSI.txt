*--------------------------------------------------------------------*
*                                                                    *
*  Module name: GENSI                                                *
*                                                                    *
*  Function:                                                         *
*   Generate SI type instructions                                    *
*                                                                    *
*  At entry R7 is AREA address                                       *
*           R5 is address of instruction                             *
*           R4 is address of OPCODE entry                            *
*                                                                    *
*--------------------------------------------------------------------*
GENSI    CSECT
GENSI    AMODE 31
GENSI    RMODE ANY
         USING GENSI,R15
         USING OSCOMM,R12
         USING SESSION,R11
         USING DISCOMM,R9
         USING AREA_DSECT,R7
         USING OPCODE_DSECT,R4
         B     MOD0000                        SKIP EYECATCHER
MODID    DC    CL8'GENSI'
         DC    C'&SYSDATE'
         DC    C'&SYSTIME'
MOD0000  DS    0H
         STM   R14,R12,12(R13)                SAVE REGS
         LR    R10,R15                        SET BASE REG
         DROP  R15
         USING GENSI,R10
         L     R15,SESS_DXD_ADDR              WORK AREA BASE
         A     R15,DXD_START                  PLUS DISP
         ST    R13,4(,R15)
         ST    R15,8(,R13)
         LR    R13,R15                        SET SAVE AREA ADDRESS
         USING DXDSI,R13
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY,                                              +
               RDATA1=R7,                                              +
               DATA2=0(R5)
         MVC   DXD_BLANKS,COMM_BLANKS
         LA    R1,DISCOMM_SOURCE_OPERAND1
         ICM   R8,15,AREA_REF_LABEL1          LABEL REFERENCED?
         USING LABEL_DSECT,R8
         BZ    GEN0020                        NO
*--------------------------------------------------------------------*
*        Generate label and displacement                             *
*--------------------------------------------------------------------*
         ICM   R2,15,AREA_REF_USING1          USING BLOCK ADDR?
         BZ    GEN0005                        NO
         USING USING_DSECT,R2
         CLC   USING_NAME,COMM_BLANKS         NAME BLANK?
         BE    GEN0005                        YES
         MVC   0(L'USING_NAME,R1),USING_NAME  COPY NAME
         BAL   R15,SCAN0000                   FIND END OF NAME
         MVI   0(R1),C'.'                     INSERT DELIMITER
         LA    R1,1(,R1)                      UPDATE OUTPUT ADDRESS
GEN0005  DS    0H
         MVC   0(L'LABEL_NAME,R1),LABEL_NAME
         BAL   R15,SCAN0000                   FIND END OF NAME
         ICM   R0,15,AREA_REF_DISP1           DISP FROM LABEL
         BZ    GEN0010
         CVD   R0,COMM_DWORD                  CONVERT TO DECIMAL
         MVI   0(R1),C'+'                     INSERT PLUS
         MVC   DXD_EDIT_WORD,EDIT_WORD
         ED    DXD_EDIT_WORD,COMM_DWORD+4
         LA    R2,DXD_EDIT_WORD
         BAL   R15,SCAN1000
         MVC   1(4,R1),0(R2)                  COPY DISP
         BAL   R15,SCAN0000
         B     GEN0030
GEN0010  DS    0H
         BAL   R15,SCAN0000
         B     GEN0030
*--------------------------------------------------------------------*
*        Generate disp from register                                 *
*--------------------------------------------------------------------*
GEN0020  DS    0H
         MVC   DXD_WORK(2),2(R5)              COPY BASE AND DISP
         NI    DXD_WORK,X'0F'                 REMOVE BASE
         LH    R0,DXD_WORK                    DISP ONLY
         CVD   R0,COMM_DWORD                  CONVERT TO DECIMAL
         MVC   DXD_EDIT_WORD,EDIT_WORD
         ED    DXD_EDIT_WORD,COMM_DWORD+4     EDIT DISP
         LA    R2,DXD_EDIT_WORD
         BAL   R15,SCAN1000
         MVC   DISCOMM_SOURCE_OPERAND1(4),0(R2)
         LA    R1,DISCOMM_SOURCE_OPERAND1
         BAL   R15,SCAN0000
         MVI   0(R1),C'('
         MVI   1(R1),C'R'
         SR    R2,R2
         IC    R2,2(R5)                       BASE +
         SRL   R2,4                           SHIFT OUT DISP
         SLL   R2,1
         LA    R2,REGISTER_TABLE(R2)
         MVC   2(2,R1),0(R2)
         BAL   R15,SCAN0000
         MVI   0(R1),C')'
         LA    R1,1(,R1)
*--------------------------------------------------------------------*
*        Generate immediate value                                    *
*--------------------------------------------------------------------*
GEN0030  DS    0H
         CLI   OPCODE_FORMAT,$OPCODE_S        'S' FORMAT?
         BE    EXIT0000                       YES..
         MVI   0(R1),C','                     COMMAN
         LA    R1,1(,R1)
         LTR   R8,R8                          USING A LABEL?
         BZ    GEN0050                        NO
         ICM   R3,15,LABEL_EQU_NEXT           ANY EQU'S?
         BZ    GEN0050                        NO
         USING EQU_DSECT,R3
GEN0040  DS    0H
         CLC   EQU_VALUE,1(R5)                MATCH?
         BE    GEN0060                        YES
         ICM   R3,15,EQU_NEXT                 NEXT EQU
         BNZ   GEN0040
GEN0050  DS    0H
         MVC   0(2,R1),=C'X'''
         UNPK  2(3,R1),1(2,R5)                UNPACK IMMEDIATE VALUE
         TR    2(2,R1),COMM_HEXCHAR
         MVI   4(R1),C''''
         B     EXIT0000
GEN0060  DS    0H
         MVC   0(63,R1),EQU_LABEL
         B     EXIT0000
*--------------------------------------------------------------------*
*        Find next blank                                             *
*--------------------------------------------------------------------*
SCAN0000 DS    0H
         CLI   0(R1),C' '
         BER   R15
         LA    R1,1(,R1)
         B     SCAN0000
*--------------------------------------------------------------------*
*        Find first non-zero                                         *
*--------------------------------------------------------------------*
SCAN1000 DS    0H
         CLI   0(R2),C'0'
         BNER  R15
         LA    R2,1(,R2)
         B     SCAN1000
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
EXIT0000 DS    0H
         ITRACE ID=EXIT
         L     R13,4(,R13)                    RESTORE REGISTER 13       ASE01670
         LM    R14,R12,12(R13)                RESTORE OTHER REGISTERS   ASE01680
         SR    R15,R15                        RETURN CODE               ASE01690
         BR    R14                            RETURN TO CALLER          ASE01700
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
DXD_START      DC   Q(DXDSI)
EDIT_WORD      DC   X'F020202020202120'

REGISTER_TABLE DS   0C
               DC   CL2'0 '
               DC   CL2'1 '
               DC   CL2'2 '
               DC   CL2'3 '
               DC   CL2'4 '
               DC   CL2'5 '
               DC   CL2'6 '
               DC   CL2'7 '
               DC   CL2'8 '
               DC   CL2'9 '
               DC   CL2'10'
               DC   CL2'11'
               DC   CL2'12'
               DC   CL2'13'
               DC   CL2'14'
               DC   CL2'15'
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
DXDSI          DSECT
               COPY DXDPREF
DXD_WORK       DS   XL8

DXD_EDIT_WORD  DS   XL8
DXD_BLANKS     DS   CL8

* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               DISCOMM  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               DISASMDA
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               BPXYSTAT DSECT=YES,LIST=NO
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY     REGEQU
               END      GENSI
