* ------------------------------------------------------------------- *
*                                                                     *
*   Determine data set type                                           *
*                                                                     *
*   If the VOLSER was not given, call catalog service using IGGCSI00. *
*      If the data set is non-VSAM issue OBTAIN to read the FORMAT-1  *
*      using the VOLSER returned by catalog services.                 *
*                                                                     *
*   If the VOLSER was given, issue OBTAIN to read the FORMAT-1 from   *
*      the specified volume.                                          *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
DISDSINF CSECT
DISDSINF AMODE 31
DISDSINF RMODE ANY
         USING DISDSINF,R15
         USING DISCOMM,R12
         B     INIT0000
MODID    DC    CL8'DISDSINF'
         DC    CL8'&SYSDATE'
         DC    CL8'&SYSTIME'
INIT0000 DS    0H
         DROP  R15
         STM   R14,R12,12(R13)                SAVE REGISTERS
         LR    R10,R15                        COPY BASE ADDRESS
         USING OSCOMM,R12
         USING SESSION,R11
         USING DISDSINF,R10
         USING DISCOMM,R9
         L     R15,SESS_DXD_ADDR              DXD AREA BASE ADDRESS
         A     R15,DXD_START                  PLUS OFFSET
         ST    R13,4(,R15)
         ST    R15,8(,R13)
         LR    R13,R15                        COPY DXD AREA ADDRESS
         USING DXDDSINF,R13                   DEFINE WORK AREA BASE
         MVC   DXD_CSECT,MODID
         L     R8,COMM_OSSPFD
         USING OSSPFD,R8
         LR    R7,R1                          COPY PARM ADDRESS
         USING DSINFREQ,R7
         ITRACE ID=ENTRY,                                              +
               DATA1=DSINFO_DSN+00,                                    +
               DATA2=DSINFO_DSN+08
         ITRACE ID=+,                                                  +
               DATA1=DSINFO_DSN+16,                                    +
               DATA2=DSINFO_DSN+24
         LA    R0,DXD_CSI_DATA_LENGTH
         LA    R1,DXD_CSI_L
         SR    R14,R14
         SR    R15,R15
         MVCL  R0,R14                         INIT
         LA    R1,DSINFO_ERROR_INFO
         ST    R1,DXD_CSI_REASON_ADDRESS      SET ADDRESS
         LA    R6,DXD_CSI_SELECTION           CATALOG SEARCH CRITERIA
         ST    R6,DXD_CSI_SELECTION_ADDRESS   SET ADDRESS
         USING CSIFIELD,R6
         LA    R1,DXD_CSI_DATA_LENGTH         CSI AREA
         ST    R1,DXD_CSI_DATA_ADDRESS        SET ADDRESS
         MVC   DXD_CSI_DATA_LENGTH,CSI_DATA_LENGTH
         MVC   CSIFILTK,DSINFO_DSN            COPY DATA SET NAME
         MVC   CSICATNM,COMM_BLANKS           START WITH MASTER CATALOG
         MVC   CSIRESNM,COMM_BLANKS           NOT RESUMING A SEARCH
         MVC   CSIDTYPD,COMM_BLANKS           RETURN ALL ENTRY TYPES
         MVI   CSICLDI,C'Y'                   RETURN DATA AND INDEX
         MVI   CSIOPTNS,C'F'                  USE 4-BYTE LENGTHS
         MVI   CSIS1CAT,C' '                  SEARCH ALL CATALOGS
         MVI   CSIRESUM,C' '                  THIS IS INITIAL CALL
         MVC   CSINUMEN,H1                    NUMBER OF FIELDS WANTED
         MVC   CSIFLDNM,C_VOLSER              NAME OF FIELD
         LOAD  EP=IGGCSI00                    LOAD THE CSI INTERFACE
         LR    R15,R0                         COPY ENTRY POINT
         LA    R1,DXD_CSI_PARMS               PARAMETER LIST
         ITRACE ID=CALL_CSI,                                           +
               RDATA1=R1,                                              +
               RDATA2=R15
         BALR  R14,R15                        CALL CATALOG SEARCH
         LR    R2,R15                         SAVE RETURN CODE
         DELETE EP=IGGCSI00                   DELETE IGGCSI00
         ITRACE ID=CSI_RC,                                             +
               RDATA1=R2,                                              +
               DATA2=(DXD_CSI_CATALOG_FLAG,1)
         LTR   R2,R2                          LOOKUP SUCCESSFUL?
         BNZ   ERR0030                        NO
         TM    DXD_CSI_CATALOG_FLAG,$NOT_CATALOGED
         BO    ERR0020                        DATA SET NOT CATALOGED
         ITRACE ID=NAME,                                               +
               DATA1=DXD_CSI_ENTRY_NAME+00,                            +
               DATA2=DXD_CSI_ENTRY_NAME+08
         ITRACE ID=NAME+,                                              +
               DATA1=DXD_CSI_ENTRY_NAME+16,                            +
               DATA2=DXD_CSI_ENTRY_NAME+24
         ITRACE ID=TYPE,                                               +
               DATA1=(DXD_CSI_ENTRY_TYPE,1),                           +
               DATA2=DXD_VOLSERS
         CLI   DXD_CSI_ENTRY_TYPE,C'A'        NONVSAM?
         BE    INFO0060                       YES
         CLI   DXD_CSI_ENTRY_TYPE,C'C'        VSAM?
         BE    INFO0020                       YES
         CLI   DXD_CSI_ENTRY_TYPE,C'D'        DATA COMPONENT?
         BE    INFO0030                       YES
         CLI   DXD_CSI_ENTRY_TYPE,C'H'        GDG MEMBER?
         BE    INFO0040                       YES
         CLI   DXD_CSI_ENTRY_TYPE,C'I'        INDEX COMPONENT?
         BE    INFO0050                       YES
         ITRACE ID=OTHER
         MVI   DSINFO_DATASET_TYPE,$DATA_TYPE_OTHER
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
*        For VSAM do not try to OBTAIN the DSCB.  The DATA, INDEX,    *
*        or other components may not have the same name as the        *
*        CLUSTER.                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
INFO0020 DS    0H
         ITRACE ID=VSAM1
         MVI   DSINFO_DATASET_TYPE,$DATA_TYPE_VSAM
         B     EXIT0000
INFO0030 DS    0H
         ITRACE ID=VSAMDATA
         MVI   DSINFO_DATASET_TYPE,$DATA_TYPE_DATA
         B     EXIT0000
INFO0040 DS    0H
         ITRACE ID=GDG
         MVI   DSINFO_DATASET_TYPE,$DATA_TYPE_GDG
         B     EXIT0000
INFO0050 DS    0H
         ITRACE ID=VSAMINDX
         MVI   DSINFO_DATASET_TYPE,$DATA_TYPE_INDEX
         B     EXIT0000
INFO0060 DS    0H
         ITRACE ID=NON_VSAM
         MVI   DSINFO_DATASET_TYPE,$DATA_TYPE_NONVSAM
         CLC   ASTERISK,DXD_VOLSERS           CATALOGED ON SYSRES?
         BNE   INFO0070                       NO
         L     R1,CVTPTR                      CVT ADDRESS
         USING CVT,R1
         L     R1,CVTSYSAD                    SYSRES UCB ADDRESS
         USING UCBOB,R1
         MVC   DXD_VOLSERS,UCBVOLI            CHANGE VOLSER
INFO0070 DS    0H
         CLC   MIGRAT,DXD_VOLSERS             MIGRATED?
         BE    ERR0040                        YES
         MVC   DXD_CAMLST(CAMLST_L),CAMLST_I  INITIALIZE CAMLST
         MVC   DSINFO_DSCB(L'DSINFO_DSN),DSINFO_DSN
         LA    R1,DSINFO_DSN                  DSN
         ST    R1,DXD_CAMLST+4                SET ADDRESS
         LA    R1,DXD_VOLSERS                 VOLSER
         ST    R1,DXD_CAMLST+8                SET ADDRESS
         LA    R1,DSINFO_DSCB+L'DSINFO_DSN    RETURNED INFO
         ST    R1,DXD_CAMLST+12               SET ADDRESS
         LA    R1,DXD_CAMLST                  CAMLST ADDRESS
         ITRACE ID=OBTAIN,                                             +
               RDATA1=R1
         OBTAIN (R1)                          READ DSCB
         ITRACE ID=OBTN_RC,                                            +
               RDATA1=R15
         LTR   R15,R15                        SUCCESSFUL?
         BNZ   ERR0010                        NO
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0010  DS    0H
         DC    H'0'
ERR0020  DS    0H
         ITRACE ID=NOTCATLG
         MVC   COMM_MSG_1,COMM_BLANKS
         MVC   COMM_MSG_1(ERR01_L),ERR01
         LA    R2,8
         B     EXIT0000
ERR0030  DS    0H
         ITRACE ID=CAT_ERR,                                            +
               DATA1=DSINFO_ERROR_INFO
         MVC   COMM_MSG_1(ERR02_L),ERR02
         UNPK  COMM_MSG_1+(ERR02_INFO-ERR02)(9),DSINFO_ERROR_INFO(5)
         TR    COMM_MSG_1+(ERR02_INFO-ERR02)(5),COMM_HEXCHAR
         MVI   COMM_MSG_1+(ERR02_INFO-ERR02)+8,C' '
         LA    R2,8
         B     EXIT0000
ERR0040  DS    0H
         ITRACE ID=NOTCATLG
         MVC   COMM_MSG_1,COMM_BLANKS
         MVC   COMM_MSG_1(ERR03_L),ERR03
         LA    R2,8
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=EXIT,                                               +
               RDATA1=R2,                                              +
               DATA2=DSINFO_DATASET_TYPE
         L     R13,4(,R13)
         L     R14,12(,R13)
         LR    R15,R2
         LM    R0,R12,20(R13)
         BR    R14
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXD_START            DC    Q(DXDDSINF)
CSI_DATA_LENGTH      DC    A($CSI_DATA_LENGTH)

CAMLST_I             CAMLST SEARCH,*,*,*
CAMLST_L             EQU   *-CAMLST_I

H1                   DC    H'1'
C_VOLSER             DC    CL8'VOLSER'
ASTERISK             DC    C'******'
MIGRAT               DC    C'MIGRAT'

ERR01                DC    C'Not cataloged'
ERR01_L              EQU   *-ERR01

ERR02                DC    C'Catalog info error '
ERR02_INFO           DC    CL8' '
                     DC    C' '
ERR02_L              EQU   *-ERR02

ERR03                DC    C'Migrated'
ERR03_L              EQU   *-ERR03

                     LTORG

* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDDSINF                  DSECT
                          COPY  DXDPREF

DXD_CAMLST                DS    0A,(CAMLST_L)X

DXD_CSI_PARMS             DS    0A
DXD_CSI_REASON_ADDRESS    DS    A
DXD_CSI_SELECTION_ADDRESS DS    A
DXD_CSI_DATA_ADDRESS      DS    A

DXD_CSI_SELECTION         DS    (CSIFIELD_L)X

DXD_CSI_DATA_LENGTH       DS    F           LENGTH OF WORK AREA
DXD_CSI_DATA_AREA         DS    0A,($CSI_DATA_LENGTH)X
                          ORG   DXD_CSI_DATA_AREA
DXD_CSI_REQUIRED_LENGTH   DS    F
DXD_CSI_USED_LENGTH       DS    F           LENGTH USED
DXD_CSI_FIELD_NAMES       DS    H           NUMBER OF FIELD NAMES + 1

DXD_CSI_CATALOG_FLAG      DS    X
$NOT_CATALOGED            EQU   X'40'       .. DATA SET NOT CATALOGED

DXD_CSI_CATALOG_TYPE      DS    C           ALWAYS C'0'
DXD_CSI_CATALOG_NAME      DS    CL44        CATALOG NAME
                          DS    CL2         MODULE ID
                          DS    X           REASON CODE
                          DS    X           RETURN CODE

DXD_CSI_ENTRY_FLAG        DS    X
DXD_CSI_ENTRY_TYPE        DS    C
*                               C'A'        .. NONVSAM
*                               C'C'        .. CLUSTER
*                               C'D'        .. DATA COMPONENT
*                               C'H'        .. MEMBER OF A GDG
*                               C'I'        .. INDEX COMPONENT
*                               C'F'        .. HFS
*                               C'T'        .. VTOC
DXD_CSI_ENTRY_NAME        DS    CL44        ENTRY NAME (S/B DSN)
                          DS    CL2         MODULE NAME
                          DS    X           REASON CODE
                          DS    X           RETURN CODE

                          DS    XL2         TOTAL LENTH OF INFO
                          DS    XL2         RESERVED
DXD_CSI_VOLSER_LENGTH     DS    XL4         LENGTH OF FIELD S/B 6
DXD_VOLSERS               DS    16CL6
                          ORG
DXD_CSI_L                 EQU   *-DXD_CSI_DATA_LENGTH

* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COPY     IGGCSINA
CSIFIELD_L       EQU      *-CSIFIELD
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 DSINFREQ DSECT=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 DISCOMM  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 DISASMDA
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 BPXYSTAT DSECT=YES,LIST=NO
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 OSSPFD   TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 CVT      DSECT=YES,LIST=YES
                 IEFUCBOB
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COPY     TRENTRY

                 COPY     REGEQU
$CSI_DATA_LENGTH EQU      1024
                 END      DISDSINF
