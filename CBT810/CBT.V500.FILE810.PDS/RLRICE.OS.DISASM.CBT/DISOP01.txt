* ------------------------------------------------------------------- *
*                                                                     *
*  Module name: DISOP01                                               *
*                                                                     *
*  Function:                                                          *
*   Modify opcode table entry for X'01xx' (SAM) instructions          *
*                                                                     *
*  Calling program provides two parms                                 *
*     1)   Address of OPCODE table entry to modify                    *
*     2)   Address of object text                                     *
*                                                                     *
* ------------------------------------------------------------------- *
DISOP01  CSECT
DISOP01  AMODE 31
DISOP01  RMODE ANY
         USING DISOP01,R15
         USING OSCOMM,R12
         USING SESSION,R11
         USING DISCOMM,R9
         USING OPCODE_DSECT,R2
         B     MOD0000                        SKIP EYECATCHER
MODID    DC    CL8'DISOP01'
         DC    C'&SYSDATE'
         DC    C'&SYSTIME'
MOD0000  DS    0H
         STM   R14,R12,12(R13)                SAVE REGS
         LR    R10,R15                        SET BASE REG
         LM    R2,R3,0(R1)
         DROP  R15
         USING DISOP01,R10
         GETMAIN RU,                                                   +
               LV=WORK_L,                                              +
               LOC=ANY
         ST    R13,4(,R1)
         ST    R1,8(,R13)
         LR    R13,R1
         USING WORK,R13
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY,                                              +
               RDATA1=R3,                                              +
               DATA2=(0(R3),2)
         CLI   1(R3),X'01'                    PR?
         BE    OP0010                         YES
         CLI   1(R3),X'02'                    UPT
         BE    OP0020                         YES
         CLI   1(R3),X'07'                    SCKPF
         BE    OP0030                         YES
         CLI   1(R3),X'0B'                    TAM?
         BE    OP0040                         YES
         CLI   1(R3),X'0C'                    SAM24?
         BE    OP0050                         YES
         CLI   1(R3),X'0D'                    SAM31?
         BE    OP0060                         YES
         CLI   1(R3),X'0E'                    SAM64?
         BE    OP0070                         YES
         ITRACE ID=NOT_VALID
         XC    OPCODE_LENGTH,OPCODE_LENGTH    NOT A VALID OPCODE
         B     EXIT0000
OP0010   DS    0H
         MVC   OPCODE_MNEMONIC,OP_PR          SET MNEMONIC
         B     OP0080
OP0020   DS    0H
         MVC   OPCODE_MNEMONIC,OP_UPT         SET MNEMONIC
         B     OP0080
OP0030   DS    0H
         MVC   OPCODE_MNEMONIC,OP_SCKPF       SET MNEMONIC
         B     OP0080
OP0040   DS    0H
         MVC   OPCODE_MNEMONIC,OP_TAM         SET MNEMONIC
         B     OP0080
OP0050   DS    0H
         MVC   OPCODE_MNEMONIC,OP_SAM24       SET MNEMONIC
         B     OP0080
OP0060   DS    0H
         MVC   OPCODE_MNEMONIC,OP_SAM31       SET MNEMONIC
         B     OP0080
OP0070   DS    0H
         MVC   OPCODE_MNEMONIC,OP_SAM64       SET MNEMONIC
         B     OP0080
OP0080   DS    0H
         MVC   OPCODE_LENGTH,H2               SET LENGTH TO 2
EXIT0000 DS    0H
         ITRACE ID=EXIT
         LR    R1,R13
         L     R13,4(,R13)                    RESTORE REGISTER 13       ASE01670
         FREEMAIN RU,                                                  +
               A=(R1),                                                 +
               LV=WORK_L
         LM    R14,R12,12(R13)                RESTORE OTHER REGISTERS   ASE01680
         SR    R15,R15                        RETURN CODE               ASE01690
         BR    R14                            RETURN TO CALLER          ASE01700
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
H2       DC    H'2'
OP_PR    DC    CL5'PR   '
OP_UPT   DC    CL5'UPT  '
OP_SCKPF DC    CL5'SCKPF'
OP_TAM   DC    CL5'TAM  '
OP_SAM24 DC    CL5'SAM24'
OP_SAM31 DC    CL5'SAM31'
OP_SAM64 DC    CL5'SAM64'
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
WORK           DSECT
               COPY DXDPREF
WORK_L         EQU  *-WORK
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               DISCOMM  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               DISASMDA
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               BPXYSTAT DSECT=YES,LIST=NO
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY     REGEQU
               END      DISOP01
