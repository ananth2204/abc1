* ------------------------------------------------------------------- *
*                                                                     *
*   "Merge" the user identified data areas with RLD data items.       *
*                                                                     *
*   To this point, data areas may have been identified by one         *
*   of two ways.  One is the RLD data obtained from the binder.       *
*   The second is areas identified by the user. The data from         *
*   these two sources needs to be 'merged'  The end result is         *
*   a chain of AREA blocks that                                       *
*       1) is in ascending DISP sequence                              *
*       2) any duplicate or overlapping areas will have been          *
*          resolved.  That is a given displacement range              *
*          will only be referenced by one AREA block.                 *
*                                                                     *
*                                                                     *
*   The RLD items are identified by AREAs chained from                *
*   DISCOMM_RLD_AREA_NEXT. User identified areas are chained          *
*   DISCOMM_USER_AREA_NEXT_1. The merged data will be a chain         *
*   of AREA blocks chained from DISCOMM_ALL_AREA_NEXT.                *
*                                                                     *
* ------------------------------------------------------------------- *
DISDMERG CSECT
DISDMERG AMODE 31
DISDMERG RMODE ANY
         USING DISDMERG,R15
         B     MOD0000                             SKIP EYECATCHER
MODID    DC    CL8'DISDMERG'
         DC    C'&SYSDATE'
         DC    C'&SYSTIME'
MOD0000  DS    0H
         STM   R14,R12,12(R13)                     SAVE REGS
         LR    R10,R15                             SET BASE REG
         DROP  R15
         USING OSCOMM,R12
         USING SESSION,R11
         USING DISDMERG,R10
         USING DISCOMM,R9
         L     R15,SESS_DXD_ADDR                   WORK AREA
         A     R15,DXD_START                       PLUS DISP
         ST    R13,4(,R15)
         ST    R15,8(,R13)
         LR    R13,R15
         USING DXDDMERG,R13
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY
         L     R8,COMM_OSSPFD
         USING OSSPFD,R8
I        USING AREA_DSECT,R7
U        USING AREA_DSECT,R6
PREV     USING AREA_DSECT,R5
         MVI   DUMMY_AREA,X'FF'
         MVC   DUMMY_AREA+1(AREA_DSECT_L-1),DUMMY_AREA
* ------------------------------------------------------------------- *
*        Free 'area 2' chain                                          *
* ------------------------------------------------------------------- *
INIT0010 DS    0H
         ITRACE ID=FREE_2,                                             +
               DATA1=DISCOMM_USER_AREA_NEXT_2
         ICM   R7,15,DISCOMM_USER_AREA_NEXT_2      FIRST ON CHAIN
         BZ    INIT0030                            NONE ON CHAIN
         XC    DISCOMM_USER_AREA_NEXT_2,DISCOMM_USER_AREA_NEXT_2
INIT0020 DS    0H
         CLI   I.AREA_TYPE,X'FF'                   DUMMY AREA?
         BE    INIT0030                            YES
         LR    R1,R7
         L     R7,I.AREA_NEXT                      NEXT AREA ON CHAIN
         ITRACE ID=AREA,                                               +
               RDATA1=R1,                                              +
               RDATA2=R7
         FREEMAIN RU,                              FREE THIS AREA      +
               A=(1),                                                  +
               LV=AREA_DSECT_L
         LTR   R7,R7                               ANOTHER BLOCK?
         BNZ   INIT0020                            YES
* ------------------------------------------------------------------- *
*        Free 'ALL area' chain                                        *
* ------------------------------------------------------------------- *
INIT0030 DS    0H
         ITRACE ID=FREE_ALL,                                           +
               DATA1=DISCOMM_ALL_AREA_NEXT
         ICM   R7,15,DISCOMM_ALL_AREA_NEXT         FIRST ON INTERNAL
         BZ    MAIN0000                            NONE ON CHAIN
         XC    DISCOMM_ALL_AREA_NEXT,DISCOMM_ALL_AREA_NEXT
INIT0040 DS    0H
         LR    R1,R7
         L     R7,I.AREA_NEXT                      NEXT AREA ON CHAIN
         ITRACE ID=AREA,                                               +
               RDATA1=R1,                                              +
               RDATA2=R7
         FREEMAIN RU,                              FREE THIS AREA      +
               A=(1),                                                  +
               LV=AREA_DSECT_L
         LTR   R7,R7                               ANOTHER BLOCK?
         BNZ   INIT0040                            YES

* ------------------------------------------------------------------- *
*                                                                     *
*        Build internal area chain                                    *
*                                                                     *
*   The user may identify areas of the text as data via the "D"       *
*   option from the main menu.  Data areas are identified by          *
*   giving the starting and ending displacement.  The user may        *
*   identify multiple areas.  To make things easier for the user      *
*   the areas may be identified any order.  Also the user may         *
*   specify data areas that overlap.                                  *
*                                                                     *
*   .... this was hard to put into words (at least for me) ....       *
*                                                                     *
*   I wanted to preserve the data areas that were identified by       *
*   the user in the same form and order they identified them.         *
*   A dis-assembly is usually an iterative process.  The results      *
*   from one 'generate' process can often lead to a better            *
*   understanding of the code being examined.  Then data areas,       *
*   DSECTs, etc and be refined.  If the data areas identified         *
*   by the user were re-arranged, or otherwise updated for            *
*   internal use, it could confuse the user.                          *
*                                                                     *
*   ............................................................      *
*                                                                     *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *



* ------------------------------------------------------------------- *
*                                                                     *
*   Step 1                                                            *
*                                                                     *
*   Copy the user areas to the internal chain in ascending            *
*   order based on the starting displacement.                         *
*                                                                     *
*   The "U" quailifier is R6, chain 1                                 *
*   The "I" quallifier is R7, chain 2                                 *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0000 DS    0H
         ITRACE ID=STEP1
         LA    R7,DUMMY_AREA                  DUMMY AREA
         ST    R7,DISCOMM_USER_AREA_NEXT_2
         ICM   R6,15,DISCOMM_USER_AREA_NEXT_1 FIRST ON USER
         BZ    MAIN0040                       NO USER DEFINED AREAS
         LA    R1,DISCOMM_USER_AREA_PREV_2
         ST    R1,I.AREA_PREV                 SET 'PREV' IN DUMMY AREA
MAIN0010 DS    0H
         ICM   R7,15,DISCOMM_USER_AREA_NEXT_2 1ST ON INTERNAL CHAIN
MAIN0020 DS    0H
         ITRACE ID=U,                                                  +
               RDATA1=R6,                                              +
               DATA2=U.AREA_BEGIN
         ITRACE ID=I,                                                  +
               RDATA1=R7,                                              +
               DATA2=I.AREA_BEGIN
         CLC   U.AREA_BEGIN,I.AREA_BEGIN      USER LOWER THAN INTERNAL?
         BH    MAIN0030                       NO
         GETMAIN RU,                          GETMAIN AN AREA BLOCK    +
               LOC=ANY,                                                +
               LV=AREA_DSECT_L
NEW      USING AREA_DSECT,R1
         MVC   NEW.AREA_DSECT(AREA_DSECT_L),U.AREA_DSECT
         ITRACE ID=NEW,                                                +
               RDATA1=R1,                                              +
               DATA2=NEW.AREA_BEGIN
         L     R2,I.AREA_PREV
PREV     USING AREA_DSECT,R2
         ST    R1,PREV.AREA_NEXT
         ST    R2,NEW.AREA_PREV
         ST    R1,I.AREA_PREV
         ST    R7,NEW.AREA_NEXT
         ICM   R6,15,U.AREA_NEXT              NEXT ON USER AREA CHAIN
         BZ    MAIN0040                       END OF CHAIN REACHED
         B     MAIN0010                       INSERT THIS AREA
* ------------------------------------------------------------------- *
*                                                                     *
*        Remove "dummy" entry from chain                              *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0030 DS    0H
         ICM   R7,15,I.AREA_NEXT
         BNZ   MAIN0020
         DC    H'0'
         DROP  NEW,PREV
MAIN0040 DS    0H
         ICM   R7,15,DISCOMM_USER_AREA_NEXT_2
         CLI   I.AREA_BEGIN,X'FF'
         BE    MAIN0080
MAIN0050 DS    0H
         ITRACE ID=USER2,                                              +
               RDATA1=R7,                                              +
               DATA2=I.AREA_BEGIN
         ICM   R1,15,I.AREA_NEXT
         BZ    MAIN0080
NEXT     USING AREA_DSECT,R1
         CLI   NEXT.AREA_BEGIN,X'FF'
         BE    MAIN0060
         LR    R7,R1
         B     MAIN0050
MAIN0060 DS    0H
         XC    I.AREA_NEXT,I.AREA_NEXT
* ------------------------------------------------------------------- *
*                                                                     *
*                                                                     *
*                                                                     *
*   Step 2                                                            *
*                                                                     *
*   Combine areas that overlap or free areas "spanned" by another.    *
*                                                                     *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0080 DS    0H
A        USING AREA_DSECT,R7
B        USING AREA_DSECT,R6
         ICM   R7,15,DISCOMM_USER_AREA_NEXT_2      1ST ON CHAIN
         ITRACE ID=STEP2,                                              +
               RDATA1=R7
         LTR   R7,R7                               ANYTHING ON CHAIN?
         BZ    MAIN0400                            NO.. SKIP STEP 3
         CLI   A.AREA_TYPE,X'FF'                   DUMMY AREA?
         BE    MAIN0400                            NO.. SKIP STEP 3
MAIN0090 DS    0H
         ICM   R6,15,A.AREA_NEXT                   1ST BLOCK AFTER "A"
         BZ    MAIN0180                            END OF CHAIN
         CLC   A.AREA_BEGIN,B.AREA_BEGIN           BEGIN AT SAME DISP?
         BNE   MAIN0130                            NO
* ------------------------------------------------------------------- *
*   Both begin at same disp                                           *
* ------------------------------------------------------------------- *
         CLC   A.AREA_END,B.AREA_END               END AT SAME DISP?
         BH    MAIN0110                            A > B
         BL    MAIN0120                            A < B
* ------------------------------------------------------------------- *
*   "A" and "B" are identical                                         *
*   FREEMAIN the "B" block                                            *
* ------------------------------------------------------------------- *
         ITRACE ID=DUP                             DUPLICATE AREAS
         B     MAIN0160
* ------------------------------------------------------------------- *
*   "A" and "B" start at the same disp                                *
*   "A" has a larger end disp.                                        *
*   FREEMAIN the "B" block                                            *
* ------------------------------------------------------------------- *
MAIN0110 DS    0H
         ITRACE ID=SPAN1
         B     MAIN0160
* ------------------------------------------------------------------- *
*   "A" and "B" start at the same disp                                *
*   "B" has a larger end disp                                         *
*   Change "A" end disp to "B"s end disp                              *
*   Calculate size of combined area                                   *
*   FREEMAIN the "B" block                                            *
* ------------------------------------------------------------------- *
MAIN0120 DS    0H
         ITRACE ID=SPAN2
         B     MAIN0150
* ------------------------------------------------------------------- *
*                                                                     *
*   "B" block begins at a higher disp than "A" block.                 *
*   If "A" block ends at a higher disp than "B" begins,               *
*   "A" block overlaps part or all of "B" block.                      *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0130 DS    0H
         ITRACE ID=MAIN0130,                                           +
               DATA1=A.AREA_BEGIN,                                     +
               DATA2=B.AREA_BEGIN
         CLC   A.AREA_END,B.AREA_BEGIN             A END < B BEGIN?
         BL    MAIN0180                            YES.. GOOD
* ------------------------------------------------------------------- *
*                                                                     *
*   "A" block ends at the same or higher disp than "B" block begins.  *
*                                                                     *
*   If "A" block ends at or higher than "B" ends                      *
*      "A" block spans all of "B" block                               *
*   otherwise                                                         *
*      "A" block spans first portion of "B" block                     *
*                                                                     *
* ------------------------------------------------------------------- *
         ITRACE ID=A,                                                  +
               DATA1=A.AREA_BEGIN,                                     +
               DATA2=B.AREA_BEGIN
         CLC   A.AREA_END,B.AREA_END               "A" END < "B" END
         BL    MAIN0140                            YES
* ------------------------------------------------------------------- *
*                                                                     *
*   'A' block spans all of 'B' block                                  *
*                                                                     *
* ------------------------------------------------------------------- *
         ITRACE ID=SPAN3,                                              +
               DATA1=A.AREA_BEGIN,                                     +
               DATA2=B.AREA_BEGIN
         B     MAIN0160
* ------------------------------------------------------------------- *
*                                                                     *
*   'A' block overlaps first part of 'B' block.                       *
*   Change end disp in 'A' block to end disp in 'B' block.            *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0140 DS    0H
         ITRACE ID=SPAN4
MAIN0150 DS    0H
         ITRACE ID=MAIN0150,                                           +
               RDATA1=R6,                                              +
               RDATA2=R7
         MVC   A.AREA_END,B.AREA_END               COPY 'B' AREA END
         ICM   R0,15,A.AREA_BEGIN
         ICM   R1,15,A.AREA_END
         SR    R1,R0
         LA    R1,1(,R1)
         STCM  R1,15,A.AREA_LENGTH
* ------------------------------------------------------------------- *
*                                                                     *
*   Remove 'B' block from chain                                       *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0160 DS    0H
         ITRACE ID=MAIN0160,                                           +
               RDATA1=R7,                                              +
               DATA2=B.AREA_NEXT
         ICM   R4,15,B.AREA_NEXT                   2ND BLOCK FORWARD
         BZ    MAIN0170
NEXT     USING AREA_DSECT,R4
         ST    R7,NEXT.AREA_PREV                   SET PREV IN 2ND FWD
         DROP  NEXT
MAIN0170 DS    0H
         ST    R4,A.AREA_NEXT                      2ND FORWARD NOW NEXT
         ITRACE ID=FREE_B,                                             +
               RDATA1=R6
         FREEMAIN RU,                                                  +
               A=(R6),                                                 +
               LV=AREA_DSECT_L
         B     MAIN0090                            CHECK AGAIN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0180 DS    0H
         ITRACE ID=MAIN0180
         ICM   R7,15,A.AREA_NEXT                   NEXT "A" AREA
         BNZ   MAIN0090
         DROP  A,B



* ------------------------------------------------------------------- *
*                                                                     *
*                                                                     *
*   Step 3                                                            *
*                                                                     *
*   Check for RLD items that overlap or duplicate user area(s)        *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
USER     USING AREA_DSECT,R7
RLD      USING AREA_DSECT,R6
* ------------------------------------------------------------------- *
*                                                                     *
*   Check the RLD items against the user area(s)                      *
*                                                                     *
*   An RLD item can be                                                *
*                                                                     *
*     1)  "below" the data item                                       *
*                                                                     *
*     2)  "overlap" the first portion of the data area                *
*                                                                     *
*     3)  "overlap" the entire data area                              *
*                                                                     *
*     4)  "overlap" the last portion of the data area                 *
*                                                                     *
*     5)  "above" the data area                                       *
*                                                                     *
*     6)  "in the middle" of a data area                              *
*                                                                     *
*     7)  exactly the same as a data area                             *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0190 DS    0H
         ITRACE ID=STEP3
         ICM   R7,15,DISCOMM_USER_AREA_NEXT_2
         BZ    MAIN0400
MAIN0200 DS    0H
         ICM   R6,15,DISCOMM_RLD_AREA_NEXT
         BZ    MAIN0400
* ------------------------------------------------------------------- *
*                                                                     *
*     Case 1  RLD below user area                                     *
*                                                                     *
*     Action: No conflict                                             *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0220 DS    0H
         ITRACE ID=CASE,                                               +
               DATA1=RLD.AREA_BEGIN,                                   +
               DATA2=USER.AREA_BEGIN
         CLC   RLD.AREA_END,USER.AREA_BEGIN        RLD < USER BEGIN?
         BNL   MAIN0230                            NO
         ITRACE ID=CASE1                           RLD IS BELOW USER
         B     MAIN0330
* ------------------------------------------------------------------- *
*                                                                     *
*     CASE 2  RLD overlaps 1st portion of user area                   *
*                                                                     *
*        RLD begins at or lower disp than user area begins            *
*        RLD ends at or higher an user area begins                    *
*        RLD ends at lower disp than user area ends                   *
*                                                                     *
*     Action: Change start disp in user area to "RLD end disp + 1"    *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0230 DS    0H
         ITRACE ID=MAIN0230                        RLD IS BELOW USER
         CLC   RLD.AREA_BEGIN,USER.AREA_BEGIN   RLD BEGIN < USER BEGIN?
         BH    MAIN0240                         NO
         CLC   RLD.AREA_END,USER.AREA_BEGIN     RLD END > USER BEGIN?
         BL    MAIN0240                         NO
         CLC   RLD.AREA_END,USER.AREA_END       RLD END < USER END?
         BNL   MAIN0240
         ITRACE ID=CASE2
         ICM   R2,15,RLD.AREA_END               RLD ITEM END DISP
         LA    R2,1(,R2)                        PLUS 1
         STCM  R2,15,USER.AREA_BEGIN            CHANGE USER AREA BEGIN
         ICM   R3,15,USER.AREA_END              USER END DISP
         SR    R3,R2                            COMPUTE LENGTH
         LA    R3,1(,R3)                        TRUE LENGTH
         STCM  R3,15,USER.AREA_LENGTH           SET NEW LENGTH
         B     MAIN0330
* ------------------------------------------------------------------- *
*                                                                     *
*                                                                     *
*     Case 3  RLD overlaps entire user area                           *
*             or is the SAME as user area                             *
*                                                                     *
*        RLD begins at same disp as user area begins and              *
*        RLD ends at same disp that user area ends                    *
*                                                                     *
*      or                                                             *
*                                                                     *
*        RLD begins at lower disp than user area begins               *
*        RLD ends at higher disp than user area ends                  *
*                                                                     *
*     Action: "delete" the user area                                  *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0240 DS    0H
         ITRACE ID=MAIN0240
         CLC   RLD.AREA_BEGIN,USER.AREA_BEGIN   SAME START DISP?
         BNE   MAIN0250                         NO
         CLC   RLD.AREA_BEGIN,USER.AREA_BEGIN   SAME START DISP?
         BE    MAIN0260                         YES.. SAME AREA
MAIN0250 DS    0H
         ITRACE ID=MAIN0250
         CLC   RLD.AREA_BEGIN,USER.AREA_BEGIN   RLD BEGIN < USER BEGIN?
         BNL   MAIN0290                         NO
         CLC   RLD.AREA_END,USER.AREA_END       RLD END > USER END?
         BNH   MAIN0290                         NO
MAIN0260 DS    0H
         ITRACE ID=CASE3
         ICM   R15,15,USER.AREA_NEXT            NEXT AREA ON CHAIN
         BZ    MAIN0270                         NOT PRESENT
NEXT     USING AREA_DSECT,R15
         MVC   NEXT.AREA_PREV,USER.AREA_PREV    SET PREV IN NEXT
         DROP  NEXT
MAIN0270 DS    0H
         ITRACE ID=MAIN0270
         ICM   R15,15,USER.AREA_PREV            PREVIOUS BLOCK
         BZ    MAIN0280                         NOT PRESENT
PREV     USING AREA_DSECT,R15
         MVC   PREV.AREA_NEXT,USER.AREA_NEXT    SET NEXT IN PREV BLOCK
         DROP  PREV
MAIN0280 DS    0H
         ITRACE ID=MAIN0280
         FREEMAIN RU,                                                  +
               A=(7),                                                  +
               LV=AREA_DSECT_L
         B     MAIN0190                         RE-DRIVE PROCESS
* ------------------------------------------------------------------- *
*                                                                     *
*     Case 4  RLD overlaps last portion of user area                  *
*                                                                     *
*        RLD begins at higher than user begins                        *
*        RLD begins at or lower than user area ends                   *
*        RLD ends at or higher than user area ends                    *
*                                                                     *
*     Action: Change ending disp in user area to "RLD begin - 1)      *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0290 DS    0H
         ITRACE ID=MAIN0290
         CLC   RLD.AREA_BEGIN,USER.AREA_BEGIN   RLD BEGIN > USER BEGIN?
         BNH   MAIN0300                         NO
         CLC   RLD.AREA_BEGIN,USER.AREA_END     RLD BEGIN > USER END?
         BH    MAIN0300                         YES..NOT LOWER OR EQUAL
         CLC   RLD.AREA_END,USER.AREA_END       RLD END > USER END?
         BL    MAIN0300                         NO
         ITRACE ID=CASE4
         ICM   R3,15,RLD.AREA_BEGIN             RLD BEGIN DISP
         BCTR  R3,0                             MINUS 1
         STCM  R3,15,USER.AREA_END              CHANGE USER AREA END
         ICM   R2,15,USER.AREA_BEGIN            USER BEGIN DISP
         SR    R3,R2                            COMPUTE NEW LENGTH
         LA    R3,1(,R3)                        TRUE LENGTH
         STCM  R3,15,USER.AREA_LENGTH           CHANGE LENGTH
         B     MAIN0330
* ------------------------------------------------------------------- *
*                                                                     *
*     CASE 5  RLD is at higher disp than user area                    *
*                                                                     *
*        RLD begins at higher disp than user area ends                *
*                                                                     *
*     Action: no conflict                                             *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0300 DS    0H
         ITRACE ID=MAIN0300
         CLC   RLD.AREA_BEGIN,USER.AREA_END     RLD BEGIN > USER END?
         BNH   MAIN0310                         NO
         ITRACE ID=CASE5
         B     MAIN0330                         GO TO NEXT RLD ITEM
* ------------------------------------------------------------------- *
*                                                                     *
*     CASE 6 (RLD is "in the middle of" a user area)                  *
*                                                                     *
*        RLD begins at higher disp than user area begins              *
*        RLD ends at lower disp than user area ends                   *
*                                                                     *
*     Action: "SPLIT" the user area into two parts                    *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0310 DS    0H
         ITRACE ID=MAIN0310
         CLC   RLD.AREA_BEGIN,USER.AREA_BEGIN   RLD BEGIN > USER BEGIN?
         BNH   MAIN0330                         NO
         CLC   RLD.AREA_END,USER.AREA_END       RLD END < USER END?
         BNL   MAIN0330                         NO
         GETMAIN RU,                                                   +
               LV=AREA_DSECT_L,                                        +
               LOC=ANY
         ITRACE ID=MIDDLE,                                             +
               RDATA1=R1
NEW      USING AREA_DSECT,R1
         MVC   NEW.AREA_DSECT(AREA_DSECT_L),USER.AREA_DSECT
         MVC   NEW.AREA_NEXT,USER.AREA_NEXT     CHAIN NEXT TO NEW
         MVI   NEW.AREA_TYPE,$AREA_DATA         AREA IS DATA
         ICM   R15,15,USER.AREA_NEXT            NEXT ON USER CHAIN
         BZ    MAIN0320
NEXT     USING AREA_DSECT,R15
         ST    R1,NEXT.AREA_PREV                CHAIN NEW TO NEXT
MAIN0320 DS    0H
         ITRACE ID=MAIN0320
         ST    R7,NEW.AREA_PREV                 SET PREV IN NEW
         ST    R1,USER.AREA_NEXT                SET NEXT IN CURRENT
         ICM   R3,15,RLD.AREA_BEGIN             RLD ITEM BEGIN
         BCTR  R3,0                             MINUS 1
         STCM  R3,15,USER.AREA_END              SET END DISP
         ICM   R2,15,USER.AREA_BEGIN            BEGIN DISP
         SR    R3,R2                            COMPUTE LENGTH
         LA    R3,1(,R3)                        TRUE LENGTH
         STCM  R3,15,USER.AREA_LENGTH           SET LENGTH
         ICM   R2,15,RLD.AREA_END               RLD ITEM END
         LA    R2,1(,R2)                        PLUS 1
         STCM  R2,15,NEW.AREA_BEGIN             SET BEGIN DISP
         ICM   R3,15,NEW.AREA_END               END DISP
         SR    R3,R2                            COMPUTE LENGTH
         LA    R3,1(,R3)                        TRUE LENGTH
         STCM  R3,15,NEW.AREA_LENGTH            SET LENGTH
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0330 DS    0H
         ITRACE ID=MAIN0330
         ICM   R6,15,RLD.AREA_NEXT               NEXT RLD ITEM
         BNZ   MAIN0220
         ICM   R7,15,USER.AREA_NEXT              NEXT USER AREA
         BNZ   MAIN0200


* ------------------------------------------------------------------- *
*                                                                     *
*                                                                     *
*                                                                     *
*  Step 4                                                             *
*                                                                     *
*  Merge RLD and user data into "ALL" data                            *
*                                                                     *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0400 DS    0H
         ITRACE ID=STEP4
ALL      USING AREA_DSECT,R5
         LA    R5,DISCOMM_ALL_AREA_PREV
         ICM   R6,15,DISCOMM_RLD_AREA_NEXT
         BNZ   MAIN0410
         LA    R6,DUMMY_AREA
MAIN0410 DS    0H
         ICM   R7,15,DISCOMM_USER_AREA_NEXT_2
         BNZ   MAIN0420
         LA    R7,DUMMY_AREA
MAIN0420 DS    0H
         CLC   RLD.AREA_BEGIN,USER.AREA_BEGIN       COMPARE DISPS
         BL    MAIN0430                             RLD LOW
         BH    MAIN0440
         CLI   RLD.AREA_BEGIN,X'FF'                 AT DUMMY RECORD?
         BE    EXIT0000                             YES
         DC    H'0'                                 INTERNAL ERROR
MAIN0430 DS    0H
         GETMAIN RU,                                                   +
               LV=AREA_DSECT_L,                                        +
               LOC=ANY
         ITRACE ID=COPY_RLD,                                           +
               RDATA1=R1,                                              +
               RDATA2=R6
         MVC   NEW.AREA_DSECT(AREA_DSECT_L),RLD.AREA_DSECT
         ST    R1,ALL.AREA_NEXT
         XC    NEW.AREA_NEXT,NEW.AREA_NEXT
         ST    R5,NEW.AREA_PREV
         LR    R5,R1
         ICM   R6,15,RLD.AREA_NEXT
         BNZ   MAIN0420
         LA    R6,DUMMY_AREA
         B     MAIN0420
MAIN0440 DS    0H
         GETMAIN RU,                                                   +
               LV=AREA_DSECT_L,                                        +
               LOC=ANY
         ITRACE ID=COPYUSER,                                           +
               RDATA1=R1,                                              +
               RDATA2=R7
         MVC   NEW.AREA_DSECT(AREA_DSECT_L),USER.AREA_DSECT
         ST    R1,ALL.AREA_NEXT
         ST    R5,NEW.AREA_PREV
         LR    R5,R1
         ICM   R7,15,USER.AREA_NEXT
         BNZ   MAIN0420
         LA    R7,DUMMY_AREA
         B     MAIN0420
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=EXIT
         ICM   R7,15,DISCOMM_USER_AREA_NEXT_2      FIRST ON CHAIN
         BZ    EXIT0020                            NOTHING ON CHAIN
EXIT0010 DS    0H
         XC    USER.AREA_PREV,USER.AREA_PREV       NO PREV IN 1ST BLOCK
EXIT0020 DS    0H
         ICM   R7,15,DISCOMM_ALL_AREA_NEXT         FIRST ON CHAIN
         BZ    EXIT0030                            NOTHING ON CHAIN
         XC    USER.AREA_PREV,USER.AREA_PREV       NO PREV IN 1ST BLOCK
EXIT0030 DS    0H
         L     R13,4(,R13)                         RESTORE REGISTER 13
         LM    R14,R12,12(R13)                     RESTORE REGISTERS
         SR    R15,R15                             RETURN CODE
         BR    R14                                 RETURN TO CALLER
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXD_START     DC       Q(DXDDMERG)
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDDMERG      DSECT
              COPY     DXDPREF

DUMMY_AREA    DS       (AREA_DSECT_L)X
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              DISCOMM  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              DISASMDA
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              BPXYSTAT DSECT=YES,LIST=NO
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              OSSPFD   TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              COPY     REGEQU
         END  DISDMERG
