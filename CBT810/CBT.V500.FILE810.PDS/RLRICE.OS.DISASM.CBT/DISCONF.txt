*--------------------------------------------------------------------*
*                                                                    *
*  Module name: DISCONF                                              *
*                                                                    *
*  Function:                                                         *
*                                                                    *
*   Save or restore saved config info                                *
*                                                                    *
*--------------------------------------------------------------------*
DISCONF  CSECT
DISCONF  AMODE 31
DISCONF  RMODE ANY
         USING DISCONF,R15
         B     MOD0000                        SKIP EYECATCHER
MODID    DC    CL8'DISCONF'
         DC    C'&SYSDATE'
         DC    C'&SYSTIME'
MOD0000  DS    0H
         STM   R14,R12,12(R13)                SAVE REGS
         LR    R10,R15                        SET BASE REG
         LA    R7,2048(,R10)
         LA    R7,2048(,R7)
         DROP  R15
         USING OSCOMM,R12
         USING SESSION,R11
         USING DISCONF,R10,R7
         USING DISCOMM,R9
         L     R15,SESS_DXD_ADDR              WORK AREA
         A     R15,DXD_START                  PLUS DISP
         ST    R13,4(,R15)
         ST    R15,8(,R13)
         LR    R13,R15
         USING DXDCONF,R13
         MVC   DXD_CSECT,MODID
         USING IECSDSL1,DSINFO_DSCB
         ITRACE ID=ENTRY
         L     R8,COMM_OSSPFD
         USING OSSPFD,R8
         MVI   SPF_FUNCTION,C' '              RESET FUNCTION
         GETMAIN RU,                                                   +
               LV=$BUFF_SIZE,                                          +
               LOC=BELOW
         ST    R1,DXD_IO_AREA                 SAVE I/O AREA ADDR
         ITRACE ID=IO_AREA,                                            +
               RDATA1=R1
CONF     USING IHADCB,DISCONF_DCB
         LA    R1,MAIN0810                    EOD POINT
CONFDCBE USING DCBE,DISCONF_DCBE
         ST    R1,CONFDCBE.DCBEEODA           SET EOD ADDRES
         LA    R0,VGET
         ST    R0,DXD_SPF_1
         LA    R0,NAMELIST
         ST    R0,DXD_SPF_2
         LA    R0,PROFILE
         ST    R0,DXD_SPF_3
         OI    DXD_SPF_3,X'80'
         LA    R1,DXD_SPF_PARMS
         ITRACE ID=VGET,                                               +
               RDATA1=R1
         L     R15,COMM_ISPLINK
         BALR  R14,R15                        VGET PROFILE VARIABLES
         ITRACE ID=VGET_RC,                                            +
               RDATA1=R15
         LTR   R15,R15                        SUCCESSFUL?
         BNZ   ERR0100                        NO
         LA    R0,VPUT
         ST    R0,DXD_SPF_1
         LA    R0,NAMELIST
         ST    R0,DXD_SPF_2
         LA    R0,SHARED
         ST    R0,DXD_SPF_3
         OI    DXD_SPF_3,X'80'
         LA    R1,DXD_SPF_PARMS
         ITRACE ID=VPUTSHAR
         L     R15,COMM_ISPLINK
         BALR  R14,R15                        VPUT TO SHARED POOL
         ITRACE ID=VPUT_RC,                                            +
               RDATA1=R15
         LTR   R15,R15                        SUCCESSFUL?
         BNZ   ERR0120                        NO
MAIN0010 DS    0H
         ITRACE ID=DISPLAY
         MVC   SESS_DISP_PANEL,PANEL_DISCONF
         L     R15,COMM_V_OSDISP
         BALR  R14,R15                        DISPLAY CONFIG
         MVC   SPF_MSG_1,COMM_BLANKS          CLEAR MESSAGE
         MVC   SPF_MSG_2,COMM_BLANKS          CLEAR MESSAGE
         MVC   SPF_MSG_3,COMM_BLANKS          CLEAR MESSAGE
         MVC   SPF_MSG_4,COMM_BLANKS          CLEAR MESSAGE
         MVC   SPF_MSG_5,COMM_BLANKS          CLEAR MESSAGE
         ITRACE ID=DISP_RC,                                            +
               RDATA1=R15
         LTR   R15,R15                        RC = 0?
         BNZ   EXIT0000                       NO
         OC    SPF_CONFIG_DSN,COMM_BLANKS     'TRANSLATE' NAME
         OC    SPF_CONFIG_MEMBER,COMM_BLANKS
         OC    SPF_CONFIG_VOLSER,COMM_BLANKS
         CLC   SPF_CONFIG_DSN,COMM_BLANKS     BLANK?
         BE    ERR0010                        YES
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
         MVC   DSINFO_DSN,SPF_CONFIG_DSN      COPY DSN
         MVC   DSINFO_VOLSER,SPF_CONFIG_VOLSER
         MVI   DSINFO_OPTIONS,$DSINFO_VOLSER  USE SPECIFIED VOLSER
         LA    R1,DSINFREQ
         L     R15,V_DISDSINF
         BALR  R14,R15                        DETERMINE DS INFO
         LTR   R15,R15                        SUCCESSFUL?
         BZ    MAIN0020                       YES
         MVC   SPF_MSG_1,COMM_MSG_1           COPY MESSAGE
         B     MAIN0010                       DISPLAY W/MSG
MAIN0020 DS    0H
         ITRACE ID=DS_TYPE,                                            +
               DATA1=(DS1DSORG,1)
         TM    DS1DSORG,DS1DSGPO              PDS?
         BNO   ERR0020                        NO
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
         OI    SPF_FUNCTION,C' '
         CLI   SPF_FUNCTION,C' '              BLANK?
         BE    ERR0030                        YES
         CLI   SPF_FUNCTION,C'R'              RESTORE SAVED INFO?
         BE    MAIN0030                       YES
         CLI   SPF_FUNCTION,C'S'              SAVE INFO?
         BE    MAIN1000                       YES
         B     ERR0040                        FIRST ON CHAIN
*---------------------------------------------------------------------*
*                                                                     *
*        Restore a config                                             *
*                                                                     *
*        DO NOT RESTORE                                               *
*         Load module library                                         *
*         LMOD                                                        *
*         CSECT                                                       *
*                                                                     *
*---------------------------------------------------------------------*
MAIN0030 DS    0H
         ITRACE ID=RESTORE
         OI    DAIR_OPTS,$DAIR_NO_MSGS
         BAL   R6,FREE0000
         NI    DAIR_OPTS,255-$DAIR_NO_MSGS
         BAL   R6,ALLOC000
         NI    DXD_FLAGS,255-$DXD_SEARCH      RESET FLAG
         CLC   SPF_CONFIG_MEMBER,COMM_BLANKS  MEMBER NAME BLANK?
         BE    MAIN0040                       YES
         OI    DXD_FLAGS,$DXD_SEARCH          SEARCH FOR NAMED MEMBER
MAIN0040 DS    0H
         ITRACE ID=OPEN
         OPEN  (DISCONF_DCB,INPUT),           OPEN CONFIG DATA SET     +
               MODE=31,                                                +
               MF=(E,DISCOMM_OPEN)
         L     R15,V_DISCDIR                  CONF DIRECTORY DISP/SRCH
         ITRACE ID=CALLCDIR,                                           +
               DATA1=SPF_CONFIG_MEMBER,                                +
               RDATA2=R15
         BALR  R14,R15                        CALL DISCDIR
         ITRACE ID=CDIR_RC,                                            +
               RDATA1=R15
         LTR   R2,R15                         SUCCESSFUL?
         BZ    MAIN0050                       YES
         CLOSE DISCONF_DCB,                   CLOSE CONFIG DCB         +
               MODE=31,                                                +
               MF=(E,DISCOMM_CLOSE)
         FREEPOOL DISCONF_DCB
         OI    DAIR_OPTS,$DAIR_NO_MSGS
         BAL   R6,FREE0000                    FREE CONFIG DATA SET
         NI    DAIR_OPTS,255-$DAIR_NO_MSGS
         TM    DXD_FLAGS,$DXD_SEARCH          SEARCH FOR NAMED MEMBER?
         BO    ERR0060                        YES.. NOT FOUND
         B     EXIT0000                       OTHERWISE JUST EXIT
*---------------------------------------------------------------------*
*        Issue FIND to position for READ                              *
*---------------------------------------------------------------------*
MAIN0050 DS    0H
         FIND  DISCONF_DCB,                   FIND THE MEMBER          +
               SPF_CONFIG_MEMBER,                                      +
               D
         ITRACE ID=FIND_RC,                                            +
               RDATA1=R15
         LTR   R15,R15                        SUCCESSFUL?
         BNZ   ERR0060                        NO
*---------------------------------------------------------------------*
*        Save config DSN, member, volser in profile                   *
*---------------------------------------------------------------------*
         DS    0H
         LA    R0,VPUT
         ST    R0,DXD_SPF_1
         LA    R0,NAMELIST
         ST    R0,DXD_SPF_2
         LA    R0,PROFILE
         ST    R0,DXD_SPF_3
         OI    DXD_SPF_3,X'80'
         LA    R1,DXD_SPF_PARMS
         ITRACE ID=VPUTPROF
         L     R15,COMM_ISPLINK
         BALR  R14,R15                        VPUT PCDSN, PCMEM, PCVOL
         ITRACE ID=VPUT_RC,                                            +
               RDATA1=R15
         LTR   R15,R15
         BNZ   ERR0110
*---------------------------------------------------------------------*
*        Free user defined data area(s)                               *
*---------------------------------------------------------------------*
         ICM   R2,15,DISCOMM_USER_AREA_NEXT_1 FIRST AREA
         BZ    MAIN0090
         ITRACE ID=FREEDATA
         USING AREA_DSECT,R2
         XC    DISCOMM_USER_AREA_NEXT_1,DISCOMM_USER_AREA_NEXT_1
MAIN0060 DS    0H
         ICM   R1,15,AREA_REF_LABEL1          LABEL ASSIGNED?
         BZ    MAIN0070
         ITRACE ID=FREEREF1
         FREEMAIN RU,                         FREE LABEL BLOCK         +
               A=(1),                                                  +
               LV=LABEL_DSECT_L
MAIN0070 DS    0H
         ICM   R1,15,AREA_REF_LABEL2          LABEL ASSIGNED?
         BZ    MAIN0080
         ITRACE ID=FREEREF2
         FREEMAIN RU,                         FREE LABEL BLOCK         +
               A=(1),                                                  +
               LV=LABEL_DSECT_L
MAIN0080 DS    0H
         LR    R1,R2
         ICM   R2,15,AREA_NEXT
         FREEMAIN RU,                         FREE AREA BLOCK          +
               A=(R1),                                                 +
               LV=AREA_DSECT_L
         LTR   R2,R2                          ANOTHER AREA?
         BNZ   MAIN0060                       YES
         DROP  R2
*---------------------------------------------------------------------*
*        Free Base(s)                                                 *
*---------------------------------------------------------------------*
MAIN0090 DS    0H
         ICM   R2,15,DISCOMM_BASE_NEXT        FIRST BASE
         BZ    MAIN0150
         ITRACE ID=FREEBASE
         USING BASE_DSECT,R2
         XC    DISCOMM_BASE_NEXT,DISCOMM_BASE_NEXT
MAIN0100 DS    0H
         LR    R1,R2
         ICM   R2,15,BASE_NEXT
         FREEMAIN RU,                                                  +
               A=(1),                                                  +
               LV=BASE_DSECT_L
         LTR   R2,R2
         BNZ   MAIN0100
         DROP  R2
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
MAIN0150 DS    0H
         ICM   R2,15,DISCOMM_KSDS_NEXT        FIRST KSDS BLOCK
         BZ    MAIN0300                       NO KSDS BLOCKS
         USING KSDS_DSECT,R2
         XC    DISCOMM_KSDS_NEXT,DISCOMM_KSDS_NEXT
MAIN0160 DS    0H
         ITRACE ID=KSDS,                                               +
               DATA1=KSDS_DDNAME
         ICM   R3,15,KSDS_DSECT_NEXT          FIRST DSECT BLOCK
         BZ    MAIN0220                       NO DSECT BLOCKS
         USING DSECT_DSECT,R3
MAIN0170 DS    0H
         ITRACE ID=DSECT,                                              +
               DATA1=DSECT_NAME
         ICM   R4,15,DSECT_LABEL_NEXT         FIRST LABEL BLOCK
         BZ    MAIN0210                       NO LABEL BLOCKS
         USING LABEL_DSECT,R4
MAIN0180 DS    0H
         ICM   R5,15,LABEL_EQU_NEXT           FIRST EQU
         BZ    MAIN0200                       NO EQU
         USING EQU_DSECT,R5
MAIN0190 DS    0H
         LR    R1,R5                          COPY EQU BLOCK ADDR
         ICM   R5,15,EQU_NEXT                 NEXT EQU BLOCK
         ITRACE ID=FREE_EQU,                                           +
               RDATA1=R1
         FREEMAIN RU,                         FREE THE EQU BLOCK       +
               A=(1),                                                  +
               LV=EQU_DSECT_L
         LTR   R5,R5                          ANOTHER EQU BLOCK?
         BNZ   MAIN0190                       YES
MAIN0200 DS    0H
         LR    R1,R4                          COPY LABEL BLOCK ADDR
         ICM   R4,15,LABEL_NEXT               NEXT LABEL BLOCK
         ITRACE ID=FREELABL,                                           +
               RDATA1=R1
         FREEMAIN RU,                         FREE THE LABEL BLOCK     +
               A=(1),                                                  +
               LV=LABEL_DSECT_L
         LTR   R4,R4                          ANOTHER LABEL BLOCK?
         BNZ   MAIN0180                       YES.. CHECK FOR EQU'S
MAIN0210 DS    0H
         LR    R1,R3                          COPY DSECT BLOCK ADDR
         ICM   R3,15,DSECT_NEXT               NEXT DSECT BLOCK
         ITRACE ID=FREEDSCT,                                           +
               RDATA1=R1
         FREEMAIN RU,                                                  +
               A=(1),                                                  +
               LV=DSECT_DSECT_L
         LTR   R3,R3                          ANOTHER DSECT?
         BNZ   MAIN0170                       YES.. CHECK FOR LABELS
MAIN0220 DS    0H
         TM    KSDS_FLAGS,$KSDS_OPEN          KSDS OPEN?
         BNO   MAIN0230                       NO
         ITRACE ID=CLOSE,                                              +
               DATA1=KSDS_DDNAME
         CLOSE KSDS_ACB,                                               +
               MODE=31,                                                +
               MF=(E,DISCOMM_CLOSE)
MAIN0230 DS    0H
         OI    DAIR_OPTS,$DAIR_NO_MSGS
         MVI   DAIR_FUNC,$DAIR_FREE
         MVC   DAIR_DD,KSDS_DDNAME
         LA    R1,DAIRREQ
         L     R15,COMM_V_OSDAIR
         BALR  R14,R15                        FREE THE DD
         NI    DAIR_OPTS,255-$DAIR_NO_MSGS
         LR    R1,R2                          COPY KSDS BLOCK ADDR
         ICM   R2,15,KSDS_NEXT                NEXT KSDS
         ITRACE ID=FREEKSDS,                                           +
               RDATA1=R1
         FREEMAIN RU,                         FREE THE KSDS BLOCK      +
               A=(1),                                                  +
               LV=KSDS_DSECT_L
         LTR   R2,R2                          ANOTHER KSDS BLOCK?
         BNZ   MAIN0160                       YES.. CHECK FOR DSECTS
         DROP  R2,R3,R4,R5
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
MAIN0300 DS    0H
         ICM   R2,15,DISCOMM_SYSLIB_NEXT      FIRST SYSLIB BLOCK
         BZ    MAIN0320
         USING SYSLIB_DSECT,R2
         XC    DISCOMM_SYSLIB_NEXT,DISCOMM_SYSLIB_NEXT
MAIN0310 DS    0H
         LR    R1,R2                          COPY SYSLIB BLOCK ADDR
         ICM   R2,15,SYSLIB_NEXT              NEXT SYSLIB
         ITRACE ID=FREE_LIB,                                           +
               RDATA1=R1
         FREEMAIN RU,                         FREE SYSLIB BLOCK        +
               A=(1),                                                  +
               LV=SYSLIB_DSECT_L
         LTR   R2,R2                          ANOTHER SYSLIB BLOCK?
         BNZ   MAIN0310                       YES
         DROP  R2
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
MAIN0320 DS    0H
         ICM   R2,15,DISCOMM_USING_NEXT       FIRST USING BLOCK
         BZ    MAIN0340
         ITRACE ID=FREEUSNG
         USING USING_DSECT,R2
         XC    DISCOMM_USING_NEXT,DISCOMM_USING_NEXT
MAIN0330 DS    0H
         LR    R1,R2
         ICM   R2,15,USING_NEXT
         FREEMAIN RU,                                                  +
               A=(1),                                                  +
               LV=USING_DSECT_L
         LTR   R2,R2
         BNZ   MAIN0330
         DROP  R2
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
MAIN0340 DS    0H
         ICM   R2,15,DISCOMM_ASM_NEXT         FIRST ASSEMBLER SOURCE
         BZ    MAIN0400
         ITRACE ID=FREE_ASM
         USING ASM_DSECT,R2
         XC    DISCOMM_ASM_NEXT,DISCOMM_ASM_NEXT
MAIN0350 DS    0H
         LR    R1,R2
         ICM   R2,15,ASM_NEXT
         FREEMAIN RU,                                                  +
               A=(1),                                                  +
               LV=ASM_DSECT_L
         LTR   R2,R2
         BNZ   MAIN0350
         DROP  R2
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
MAIN0400 DS    0H
         ITRACE ID=RESET
         XC    DXD_FIRST_DATAAREA,DXD_FIRST_DATAAREA
         XC    DXD_LAST_DATAAREA,DXD_LAST_DATAAREA
         XC    DXD_FIRST_BASE,DXD_FIRST_BASE
         XC    DXD_LAST_BASE,DXD_LAST_BASE
         XC    DXD_FIRST_USING,DXD_FIRST_USING
         XC    DXD_LAST_USING,DXD_LAST_USING
         XC    DXD_FIRST_KSDS,DXD_FIRST_KSDS
         XC    DXD_LAST_KSDS,DXD_LAST_KSDS
         XC    DXD_FIRST_SYSLIB,DXD_FIRST_SYSLIB
         XC    DXD_LAST_SYSLIB,DXD_LAST_SYSLIB
         XC    DXD_FIRST_ASM,DXD_FIRST_ASM
         XC    DXD_LAST_ASM,DXD_LAST_ASM
*---------------------------------------------------------------------*
*                                                                     *
*        R5   I/O area address                                        *
*        R4   Data length                                             *
*                                                                     *
*---------------------------------------------------------------------*
MAINX140 DS    0H
         ITRACE ID=READ
         XC    DISCOMM_DECB(DISCOMM_DECB_L),DISCOMM_DECB
         L     R5,DXD_IO_AREA                    I/O AREA ADDRESS
         USING CONFIG_RECORD,R5
         READ  DISCOMM_DECB,                     READ                  +
               SF,                                                     +
               DISCONF_DCB,                      .. DCB ADDR           +
               (R5),                             .. I/O AREA ADDR      +
               'S',                                                    +
               MF=E
         ITRACE ID=WAIT
         CHECK DISCOMM_DECB
         ITRACE ID=RUNNING
         USING DECB,DISCOMM_DECB
         L     R1,DECIOBPT                       IOB ADDRESS
         USING IOBSTDRD,R1                       DEFINE IOB BASE
         LH    R4,CONF.DCBBLKSI                  BLOCK SIZE
         SR    R0,R0                             CLEAR R1
         ICM   R0,3,IOBSTDRD+14                  RESIDUAL LENGTH
         SR    R4,R0                             LENGTH READ
*---------------------------------------------------------------------*
*                                                                     *
*        Determine record type                                        *
*                                                                     *
*---------------------------------------------------------------------*
MAIN0410 DS    0H
         ITRACE ID=RECORD,                                             +
               RDATA1=R5,                                              +
               DATA2=CONFIG_TYPE
         CLI   CONFIG_TYPE,$CONFIG_DATAAREA      DATA AREA?
         BE    MAIN0420                          YES
         CLI   CONFIG_TYPE,$CONFIG_BASE          BASE?
         BE    MAIN0500                          YES
         CLI   CONFIG_TYPE,$CONFIG_USING         USING?
         BE    MAIN0530                          YES
         CLI   CONFIG_TYPE,$CONFIG_DSECT_KSDS    DSECT DATA KSDS?
         BE    MAIN0600                          YES
         CLI   CONFIG_TYPE,$CONFIG_SYSLIB        SYSLIB?
         BE    MAIN0650                          YES
         CLI   CONFIG_TYPE,$CONFIG_PREFIX_LENGTH LABEL PREFIX LENGTH?
         BE    MAIN0700                          YES
         CLI   CONFIG_TYPE,$CONFIG_PREFIX        LABEL PREFIX?
         BE    MAIN0710                          YES
         CLI   CONFIG_TYPE,$CONFIG_ASM_SOURCE    ASSEMBLER SOURCE?
         BE    MAIN0720                          YES
         CLI   CONFIG_TYPE,$CONFIG_OUTPUT_DSN    OUTPUT DSN?
         BE    MAIN0750                          YES
         CLI   CONFIG_TYPE,$CONFIG_OUTPUT_MEMBER OUTPUT MEMBER?
         BE    MAIN0760                          YES
         CLI   CONFIG_TYPE,$CONFIG_OUTPUT_VOLSER OUTPUT VOLSER?
         BE    MAIN0770                          YES
         DC    H'0'
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
MAIN0420 DS    0H
         ITRACE ID=DATAAREA
         CLC   DISCOMM_CSECT_LENGTH,CONFIG_DATA+(AREA_BEGIN-AREA_EYE)
         BL    MAIN0450
         GETMAIN RU,                                                   +
               LV=AREA_DSECT_L,                                        +
               LOC=ANY
NEW      USING AREA_DSECT,R1
         ICM   R3,15,DXD_LAST_DATAAREA           LAST DATA AREA
         BZ    MAIN0430
PREV     USING AREA_DSECT,R3
         ST    R1,PREV.AREA_NEXT                 SET NEXT
MAIN0430 DS    0H
         ST    R3,NEW.AREA_PREV                  SET PREV
         XC    NEW.AREA_NEXT,NEW.AREA_NEXT
         MVC   NEW.AREA_EYE(AREA_DSECT_L-(AREA_EYE-AREA_DSECT)),CONFIG_+
               DATA
         MVI   NEW.AREA_TYPE,$AREA_DATA
         XC    NEW.AREA_OPCODE,NEW.AREA_OPCODE
         XC    NEW.AREA_REF_LABEL1,NEW.AREA_REF_LABEL1
         XC    NEW.AREA_REF_DISP1,NEW.AREA_REF_DISP1
         XC    NEW.AREA_REF_LABEL2,NEW.AREA_REF_LABEL2
         XC    NEW.AREA_REF_DISP2,NEW.AREA_REF_DISP2
         OC    DXD_FIRST_DATAAREA,DXD_FIRST_DATAAREA
         BNZ   MAIN0440
         ST    R1,DXD_FIRST_DATAAREA
MAIN0440 DS    0H
         ST    R1,DXD_LAST_DATAAREA              SAVE ADDRESS
         CLC   NEW.AREA_END,DISCOMM_CSECT_LENGTH BEYOND END OF CSECT?
         BNH   MAIN0800                          NO
         MVC   NEW.AREA_END,DISCOMM_CSECT_LENGTH LIMIT TO CSECT LENGTH
         MVC   COMM_MSG_1(MSG06_L),MSG06
         UNPK  COMM_INFO_01(9),CONFIG_DATA+(AREA_BEGIN-AREA_EYE)(5)
         TR    COMM_INFO_01(8),COMM_HEXCHAR
         MVC   COMM_MSG_1+(MSG06_B-MSG06)(8),COMM_INFO_01
         UNPK  COMM_INFO_01(9),CONFIG_DATA+(AREA_END-AREA_EYE)(5)
         TR    COMM_INFO_01(8),COMM_HEXCHAR
         MVC   COMM_MSG_1+(MSG06_E-MSG06)(8),COMM_INFO_01
         B     MAIN0800
MAIN0450 DS    0H
         MVC   COMM_MSG_1(MSG07_L),MSG07
         UNPK  COMM_INFO_01(9),CONFIG_DATA+(AREA_BEGIN-AREA_EYE)(5)
         TR    COMM_INFO_01(8),COMM_HEXCHAR
         MVC   COMM_MSG_1+(MSG07_B-MSG07)(8),COMM_INFO_01
         UNPK  COMM_INFO_01(9),CONFIG_DATA+(AREA_END-AREA_EYE)(5)
         TR    COMM_INFO_01(8),COMM_HEXCHAR
         MVC   COMM_MSG_1+(MSG07_E-MSG07)(8),COMM_INFO_01
         BAL   R6,TPUT0000
         B     MAIN0800
         DROP  PREV,NEW
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
MAIN0500 DS    0H
         ITRACE ID=BASE
         GETMAIN RU,                                                   +
               LV=BASE_DSECT_L,                                        +
               LOC=ANY
NEW      USING BASE_DSECT,R1
         ICM   R3,15,DXD_LAST_BASE               LAST BASE AREA
         BZ    MAIN0510
PREV     USING BASE_DSECT,R3
         ST    R1,PREV.BASE_NEXT                 SET NEXT
MAIN0510 DS    0H
         ST    R3,NEW.BASE_PREV                  SET PREV
         XC    NEW.BASE_NEXT,NEW.BASE_NEXT
         MVC   NEW.BASE_EYE(BASE_DSECT_L-(BASE_EYE-BASE_DSECT)),CONFIG_+
               DATA
         MVI   NEW.BASE_FLAGS,0                  RESET FLAGS
         OC    DXD_FIRST_BASE,DXD_FIRST_BASE     ANY ON CHAIN NOW?
         BNZ   MAIN0520                          YES
         ST    R1,DXD_FIRST_BASE                 SET FIRST ON CHAIN
MAIN0520 DS    0H
         ST    R1,DXD_LAST_BASE                  SAVE LAST ON NEW CHAIN
         B     MAIN0800
         DROP  PREV,NEW
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
MAIN0530 DS    0H
         ITRACE ID=USING
         GETMAIN RU,                                                   +
               LV=USING_DSECT_L,                                       +
               LOC=ANY
NEW      USING USING_DSECT,R1
         ICM   R3,15,DXD_LAST_USING              LAST USING AREA
         BZ    MAIN0540
PREV     USING USING_DSECT,R3
         ST    R1,PREV.USING_NEXT                SET NEXT
MAIN0540 DS    0H
         ST    R3,NEW.USING_PREV                 SET PREV
         XC    NEW.USING_NEXT,NEW.USING_NEXT
         MVC   NEW.USING_EYE(USING_DSECT_L-(USING_EYE-USING_DSECT)),CON+TA
               FIG_DATA
         MVI   NEW.USING_FLAG,0                  RESET FLAGS
         XC    NEW.USING_DSECT_ADDR,NEW.USING_DSECT_ADDR
         XC    NEW.USING_LABEL_ADDR,NEW.USING_LABEL_ADDR
         OC    DXD_FIRST_USING,DXD_FIRST_USING   SOMETHING ON CHAIN?
         BNZ   MAIN0550                          YES
         ST    R1,DXD_FIRST_USING                SET FIRST ON CHAIN
MAIN0550 DS    0H
         ST    R1,DXD_LAST_USING                 SAVE LAST ON CHAIN
         B     MAIN0800
         DROP  PREV,NEW
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
MAIN0600 DS    0H
         ITRACE ID=KSDS
         GETMAIN RU,                                                   +
               LV=KSDS_DSECT_L,                                        +
               LOC=ANY
NEW      USING KSDS_DSECT,R1
         ICM   R3,15,DXD_LAST_KSDS               LAST KSDS AREA
         BZ    MAIN0610
PREV     USING KSDS_DSECT,R3
         ST    R1,PREV.KSDS_NEXT                 SET NEXT
MAIN0610 DS    0H
         ST    R3,NEW.KSDS_PREV                  SET PREV
         XC    NEW.KSDS_NEXT,NEW.KSDS_NEXT
         MVC   NEW.KSDS_EYE(KSDS_DSECT_L-(KSDS_EYE-KSDS_DSECT)),CONFIG_+
               DATA
         XC    NEW.KSDS_DSECT_NEXT,NEW.KSDS_DSECT_NEXT
         MVC   NEW.KSDS_ACB(ACB_L),ACB_I         COPY ACB
         MVI   NEW.KSDS_FLAGS,0
         OC    DXD_FIRST_KSDS,DXD_FIRST_KSDS     ANY ON CHAIN NOW?
         BNZ   MAIN0620                          NO
         ST    R1,DXD_FIRST_KSDS                 SET 1ST ON NEW CHAIN
MAIN0620 DS    0H
         ST    R1,DXD_LAST_KSDS                  SAVE LAST ON NEW CHAIN
         B     MAIN0800
         DROP  PREV,NEW
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
MAIN0650 DS    0H
         ITRACE ID=SYSLIB
         GETMAIN RU,                                                   +
               LV=SYSLIB_DSECT_L,                                      +
               LOC=ANY
NEW      USING  SYSLIB_DSECT,R1
         ICM   R3,15,DXD_LAST_SYSLIB             LAST SYSLIB AREA
         BZ    MAIN0660
PREV     USING  SYSLIB_DSECT,R3
         ST    R1,PREV.SYSLIB_NEXT               SET NEXT
MAIN0660 DS    0H
         ST    R3,NEW.SYSLIB_PREV                SET PREV
         XC    NEW.SYSLIB_NEXT,NEW.SYSLIB_NEXT
         MVC   NEW.SYSLIB_EYE(SYSLIB_DSECT_L-(SYSLIB_EYE-SYSLIB_DSECT))+
               ,CONFIG_DATA
         MVC   NEW.SYSLIB_DDNAME,COMM_BLANKS
         OC    DXD_FIRST_SYSLIB,DXD_FIRST_SYSLIB ANY ON CHAIN NOW?
         BNZ   MAIN0670                          NO
         ST    R1,DXD_FIRST_SYSLIB               SET 1ST ON NEW CHAIN
MAIN0670 DS    0H
         ST    R1,DXD_LAST_SYSLIB                SAVE LAST ON NEW CHAIN
         B     MAIN0800
         DROP  PREV,NEW
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
MAIN0700 DS    0H
         ITRACE ID=PRE_LEN
         MVC   DISCOMM_PREFIX_LENGTH,CONFIG_DATA
         B     MAIN0800
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
MAIN0710 DS    0H
         ITRACE ID=PREFIX
         MVC   DISCOMM_PREFIX,CONFIG_DATA
         B     MAIN0800
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
MAIN0720 DS    0H
         ITRACE ID=ASM_SRC
         OI    DISCOMM_FLAGS,$DISCOMM_ASSEMBLE_SOURCE
         GETMAIN RU,                                                   +
               LV=ASM_DSECT_L,                                         +
               LOC=ANY
NEW      USING ASM_DSECT,R1
         ICM   R3,15,DXD_LAST_ASM                LAST ASM AREA
         BZ    MAIN0730
PREV     USING ASM_DSECT,R3
         ST    R1,PREV.ASM_NEXT                  SET NEXT
MAIN0730 DS    0H
         ST    R3,NEW.ASM_PREV                   SET PREV
         XC    NEW.ASM_NEXT,NEW.ASM_NEXT
         MVC   NEW.ASM_EYE(ASM_DSECT_L-(ASM_EYE-ASM_DSECT)),CONFIG_DATA
         OC    DXD_FIRST_ASM,DXD_FIRST_ASM
         BNZ   MAIN0740                          NO
         ST    R1,DXD_FIRST_ASM                  SET 1ST ON NEW CHAIN
MAIN0740 DS    0H
         ST    R1,DXD_LAST_ASM                   SAVE LAST ON NEW CHAIN
         B     MAIN0800
         DROP  PREV,NEW
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
MAIN0750 DS    0H
         ITRACE ID=OUT_DSN
         MVC   DISCOMM_OUTPUT_DSN,CONFIG_DATA    RESTORE OUTPUT DSN
         B     MAIN0800
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
MAIN0760 DS    0H
         ITRACE ID=OUT_MEM
         MVC   DISCOMM_OUTPUT_MEMBER,CONFIG_DATA RESTORE OUTPUT MEMBER
         B     MAIN0800
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
MAIN0770 DS    0H
         ITRACE ID=OUT_VOL
         MVC   DISCOMM_OUTPUT_VOLSER,CONFIG_DATA RESTORE OUTPUT VOLSER
         B     MAIN0800
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
MAIN0800 DS    0H
         AH    R5,H256                           PLUS ONE RECORD
         SH    R4,H256                           MINUS ONE RECORD
         BH    MAIN0410
         B     MAINX140
*---------------------------------------------------------------------*
*                                                                     *
*        EOF on CONFIG file                                           *
*         . CLOSE CONFIG                                              *
*         . De-allocate CONFIG file                                   *
*         . display pre-processed DSECT KSDS libraries                *
*         . display Assembler SYSLIBs                                 *
*         . assemble assembler source                                 *
*                                                                     *
*---------------------------------------------------------------------*
MAIN0810 DS    0H
         ITRACE ID=EOF
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
         CLOSE DISCONF_DCB,                      CLOSE CONFIG DCB      +
               MODE=31,                                                +
               MF=(E,DISCOMM_CLOSE)
         FREEPOOL DISCONF_DCB
         BAL   R6,FREE0000
         LTR   R15,R15                           SUCCESSFUL?
         BNZ   ERR0050                           NO
*---------------------------------------------------------------------*
*        Display pre-processed DSECT KSDSes                           *
*---------------------------------------------------------------------*
         ITRACE ID=DISPKSDS,                                           +
               RDATA1=R1
         MVC   SPF_MSG_3(MSG02_L),MSG02
         L     R1,DXD_FIRST_KSDS
         L     R15,V_DISKLIST                    DISPLAY KSDS(S)
         BALR  R14,R15
*---------------------------------------------------------------------*
*        Display Assembler SYSLIBs                                    *
*---------------------------------------------------------------------*
         ITRACE ID=SYSLIB,                                             +
               RDATA1=R1
         MVC   SPF_MSG_3(MSG03_L),MSG03
         L     R1,DXD_FIRST_SYSLIB
         L     R15,V_DISSLIST
         BALR  R14,R15                           DISPLAY SYSLIBS
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
         MVC   DISCOMM_USER_AREA_NEXT_1,DXD_FIRST_DATAAREA
         MVC   DISCOMM_BASE_NEXT,DXD_FIRST_BASE
         MVC   DISCOMM_USING_NEXT,DXD_FIRST_USING
         MVC   DISCOMM_ASM_NEXT,DXD_FIRST_ASM
         MVC   SPF_MSG_3(MSG01_L),MSG01
         B     EXIT0000                          ALL DONE
*---------------------------------------------------------------------*
*                                                                     *
*                                                                     *
*            Save config info                                         *
*                                                                     *
*                                                                     *
*---------------------------------------------------------------------*
MAIN1000 DS    0H
         ITRACE ID=SAVE
         BAL   R6,FREE0000
         OI    DAIR_OPTS,$DAIR_NO_MSGS
         BAL   R6,ALLOC000
         NI    DAIR_OPTS,255-$DAIR_NO_MSGS
         ITRACE ID=OPEN
         OPEN  (DISCONF_DCB,OUTPUT),             OPEN CONFIG DATA SET  +
               MODE=31,                                                +
               MF=(E,DISCOMM_OPEN)
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
MAIN1020 DS    0H
         L     R0,DXD_IO_AREA
         LH    R1,=Y($BUFF_SIZE)
         SR    R14,R14
         SR    R15,R15
         MVCL  R0,R14
         L     R5,DXD_IO_AREA                    I/O AREA
         SR    R4,R4                             INIT AMOUNT OF DATA
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
MAIN1030 DS    0H
         ICM   R2,15,DISCOMM_USER_AREA_NEXT_1    FIRST USER AREA
         BZ    MAIN1050
         USING AREA_DSECT,R2
MAIN1040 DS    0H
         ITRACE ID=DATAAREA,                                           +
               RDATA1=R2
         MVI   CONFIG_TYPE,$CONFIG_DATAAREA      DATA TYPE
         MVC   CONFIG_DATA(AREA_DSECT_L-(AREA_EYE-AREA_DSECT)),AREA_EYE
         BAL   R6,WRITE000                       WRITE TO CONFIG FILE
         ICM   R2,15,AREA_NEXT                   NEXT AREA BLOCK
         BNZ   MAIN1040
         DROP  R2
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
MAIN1050 DS    0H
         ICM   R2,15,DISCOMM_BASE_NEXT           FIRST BASE
         BZ    MAIN1070
         USING BASE_DSECT,R2
MAIN1060 DS    0H
         ITRACE ID=BASE,                                               +
               RDATA1=R2
         MVI   CONFIG_TYPE,$CONFIG_BASE          DATA TYPE
         MVC   CONFIG_DATA(BASE_DSECT_L-(BASE_EYE-BASE_DSECT)),BASE_EYE
         BAL   R6,WRITE000                       WRITE TO CONFIG FILE
         ICM   R2,15,BASE_NEXT                   NEXT BASE BLOCK
         BNZ   MAIN1060
         DROP  R2
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
MAIN1070 DS    0H
         ICM   R2,15,DISCOMM_USING_NEXT          FIRST USING
         BZ    MAIN1090
         USING USING_DSECT,R2
MAIN1080 DS    0H
         ITRACE ID=USING,                                              +
               RDATA1=R2
         MVI   CONFIG_TYPE,$CONFIG_USING         DATA TYPE
         MVC   CONFIG_DATA(USING_DSECT_L-(USING_EYE-USING_DSECT)),USING+
               _EYE
         BAL   R6,WRITE000                       WRITE TO CONFIG FILE
         ICM   R2,15,USING_NEXT                  NEXT USING BLOCK
         BNZ   MAIN1080
         DROP  R2
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
MAIN1090 DS    0H
         ICM   R2,15,DISCOMM_KSDS_NEXT           FIRST KSDS
         BZ    MAIN1110
         USING KSDS_DSECT,R2
MAIN1100 DS    0H
         ITRACE ID=KSDS,                                               +
               RDATA1=R2
         MVI   CONFIG_TYPE,$CONFIG_DSECT_KSDS    DATA TYPE
         MVC   CONFIG_DATA(KSDS_DSECT_L-(KSDS_EYE-KSDS_DSECT)),KSDS_EYE
         BAL   R6,WRITE000                       WRITE TO CONFIG FILE
         ICM   R2,15,KSDS_NEXT                   NEXT KSDS BLOCK
         BNZ   MAIN1100
         DROP  R2
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
MAIN1110 DS    0H
         ICM   R2,15,DISCOMM_SYSLIB_NEXT         FIRST SYSLIB
         BZ    MAIN1130
         USING SYSLIB_DSECT,R2
MAIN1120 DS    0H
         ITRACE ID=SYSLIB,                                             +
               RDATA1=R2
         MVI   CONFIG_TYPE,$CONFIG_SYSLIB        DATA TYPE
         MVC   CONFIG_DATA(SYSLIB_DSECT_L-(SYSLIB_EYE-SYSLIB_DSECT)),SY+
               SLIB_EYE
         BAL   R6,WRITE000                       WRITE TO CONFIG FILE
         ICM   R2,15,SYSLIB_NEXT                 NEXT SYSLIB BLOCK
         BNZ   MAIN1120
         DROP  R2
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
MAIN1130 DS    0H
         ITRACE ID=PRFX_LEN
         MVI   CONFIG_TYPE,$CONFIG_PREFIX_LENGTH DATA TYPE
         MVC   CONFIG_DATA(2),DISCOMM_PREFIX_LENGTH
         BAL   R6,WRITE000                       WRITE TO CONFIG FILE
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
         ITRACE ID=PREFIX
         MVI   CONFIG_TYPE,$CONFIG_PREFIX        DATA TYPE
         MVC   CONFIG_DATA(4),DISCOMM_PREFIX
         BAL   R6,WRITE000                       WRITE TO CONFIG FILE
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
         ICM   R2,15,DISCOMM_ASM_NEXT            FIRST ASSEMBLER SOURCE
         BZ    MAIN1150
         USING ASM_DSECT,R2
MAIN1140 DS    0H
         ITRACE ID=ASM,                                                +
               RDATA2=R2
         MVI   CONFIG_TYPE,$CONFIG_ASM_SOURCE    DATA TYPE
         MVC   CONFIG_DATA(ASM_DSECT_L-(ASM_EYE-ASM_DSECT)),ASM_EYE
         BAL   R6,WRITE000                       WRITE TO CONFIG FILE
         ICM   R2,15,ASM_NEXT                    NEXT ASM BLOCK
         BNZ   MAIN1140
         DROP  R2
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
MAIN1150 DS    0H
         MVI   CONFIG_TYPE,$CONFIG_OUTPUT_DSN
         MVC   CONFIG_DATA(44),DISCOMM_OUTPUT_DSN
         BAL   R6,WRITE000
         MVI   CONFIG_TYPE,$CONFIG_OUTPUT_MEMBER
         MVC   CONFIG_DATA(8),DISCOMM_OUTPUT_MEMBER
         BAL   R6,WRITE000
         MVI   CONFIG_TYPE,$CONFIG_OUTPUT_VOLSER
         MVC   CONFIG_DATA(6),DISCOMM_OUTPUT_VOLSER
         BAL   R6,WRITE000
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
         ITRACE ID=LAST,                                               +
               RDATA1=R4
         LTR   R4,R4                             DATA IN LAST BLOCK?
         BZ    MAIN1160                          NO
         BAL   R6,WRITE010                       WRITE LAST BUFFER
MAIN1160 DS    0H
         ITRACE ID=STOW,                                               +
               DATA1=SPF_CONFIG_MEMBER
         GETMAIN RU,                                                   +
               LV=STOW_DATA_L,                                         +
               LOC=BELOW
         LR    R5,R1
         USING STOW_DATA,R5
         MVC   STOW_NAME,SPF_CONFIG_MEMBER       MEMBER NAME
         XC    STOW_TT,STOW_TT                   TRACK
         MVI   STOW_R,0                          RECORD
         MVI   STOW_C,31                         USER DATA LENGTH
         MVC   STOW_DESC,SPF_CONFIG_DESC         COPY DESCRIPTION
         ITRACE ID=STOW,                                               +
               DATA1=SPF_CONFIG_MEMBER
         STOW  DISCONF_DCB,                                            +
               (R5),                                                   +
               R
         ITRACE ID=STOW_RC,                                            +
               RDATA1=R15
         LR    R2,R15                            SAVE STOW RC
         FREEMAIN RU,                                                  +
               A=(R5),                                                 +
               LV=STOW_DATA_L
         CH    R2,H8                             RC = 8?
         BE    MAIN1170                          YES.. ACCEPTABLE
         LTR   R2,R2                             SUCCESSFUL?
         BNZ   ERR0070                           NO
MAIN1170 DS    0H
         ITRACE ID=CLOSCONF
         CLOSE DISCONF_DCB,                      CLOSE CONFIG DCB      +
               MODE=31,                                                +
               MF=(E,DISCOMM_CLOSE)
         FREEPOOL DISCONF_DCB
         BAL   R6,FREE0000                       FREE CONFIG DATA SET
         LTR   R15,R15                           SUCCESSFUL?
         BNZ   ERR0050                           NO
         MVC   SPF_MSG_3(MSG05_L),MSG05
         B     EXIT0000
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
ALLOC000 DS    0H
         ITRACE ID=ALOCCONF
         MVI   DAIR_FUNC,$DAIR_ALLOC
         MVC   DAIR_DD,PANEL_DISCONF
         MVC   DAIR_DSN,SPF_CONFIG_DSN
         MVC   DAIR_MEMBER,COMM_BLANKS           ALLOC PDS, NOT MEMBER
         MVC   DAIR_UNIT,COMM_BLANKS
         MVC   DAIR_VOLSER,SPF_CONFIG_VOLSER
         MVI   DAIR_DISP,$DAIR_DISP_SHR
         MVI   DAIR_OPTS,$DAIR_USE_DD
         LA    R1,DAIRREQ
         L     R15,COMM_V_OSDAIR
         BALR  R14,R15                           ALLOCATE DISCONF
         ITRACE ID=DAIR_RC,                                            +
               RDATA1=R15
         LTR   R15,R15                           SUCCESSFUL?
         BNZ   ERR0050                           NO
         TM    DS1RECFM,DS1RECFV                 RECFM V OR U?
         BO    ERR0080
         CLC   DS1LRECL,H256                     LRECL 256?
         BNE   ERR0090                           NO
         BR    R6                                OTHERWIZE OK
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
FREE0000 DS    0H
         ITRACE ID=FREECONF
         MVI   DAIR_FUNC,$DAIR_FREE
         MVC   DAIR_DD,PANEL_DISCONF
         LA    R1,DAIRREQ
         L     R15,COMM_V_OSDAIR
         BALR  R14,R15                           FREE DISCONF
         ITRACE ID=FREE_RC,                                            +
               RDATA1=R15
         BR    R6
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
WRITE000 DS    0H
         ITRACE ID=WRITE000,                                           +
               RDATA1=R5,                                              +
               RDATA2=R4
         USING CONFIG_RECORD,R5
         AH    R5,H256                           UPDATE BUFFER ADDR
         AH    R4,H256                           UPDATE LENGTH
         CH    R4,=Y($BUFF_SIZE)                 BUFFER FULL?
         BNE   WRITE020                          NO
WRITE010 DS    0H
         L     R5,DXD_IO_AREA                    RESET R5
         ITRACE ID=WRITE010,                                           +
               RDATA1=R5,                                              +
               RDATA2=R4
         XC    DISCOMM_DECB(DISCOMM_DECB_L),DISCOMM_DECB
         STH   R4,CONF.DCBBLKSI                  SET BLOCK SIZE
         WRITE DISCOMM_DECB,                                           +
               SF,                                                     +
               DISCONF_DCB,                      .. DCB ADDR           +
               (R5),                             .. I/O AREA ADDR      +
               (R4),                             .. LENGTH             +
               MF=E
         ITRACE ID=WAIT
         CHECK DISCOMM_DECB
         ITRACE ID=RUNNING
         SR    R4,R4                             RESET LENGTH
         L     R0,DXD_IO_AREA
         LH    R1,=Y($BUFF_SIZE)
         SR    R14,R14
         SR    R15,R15
         MVCL  R0,R14                            INITIALIZE I/O AREA
WRITE020 DS    0H
         MVC   CONFIG_RECORD(CONFIG_RECORD_L),COMM_BLANKS
         BR    R6
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
TPUT0000 DS    0H
         ITRACE ID=TPUT
         TPUT  COMM_MSG_1,L'COMM_MSG_1
         MVC   COMM_MSG_1,COMM_BLANKS
         BR    R6
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
ERR0010  DS    0H
         MVI   COMM_MSG_ID+1,1                   DSN IS BLANK
         B     ERR0200
ERR0020  DS    0H
         MVI   COMM_MSG_ID+1,2                   CONFIG DATASET NOT PDS
         B     ERR0200
ERR0030  DS    0H
         MVI   COMM_MSG_ID+1,3                   FUNCTION IS BLANK
         B     ERR0200
ERR0040  DS    0H
         MVI   COMM_MSG_ID+1,4                   BAD FUNCTION
         B     ERR0200
ERR0050  DS    0H
         ITRACE ID=DAIR_ERR                      DAIR ERROR
         B     ERR0210                           MESSAGES ALREADY BUILT
ERR0060  DS    0H
         ITRACE ID=FIND_ERR
         MVI   COMM_MSG_ID+1,6                   FIND FAILED
         MVC   COMM_INFO_01(L'SPF_CONFIG_MEMBER),SPF_CONFIG_MEMBER
         STH   R15,COMM_INFO_02
         B     ERR0200
ERR0070  DS    0H
         MVI   COMM_MSG_ID+1,7                   STOW FAILED
         MVC   COMM_INFO_01(L'SPF_CONFIG_MEMBER),SPF_CONFIG_MEMBER
         STC   R2,COMM_INFO_01                   STOW RC
         B     ERR0200
ERR0080  DS    0H
         MVI   COMM_MSG_ID+1,8                   CONFIG RECFM V OR U
         B     ERR0200
ERR0090  DS    0H
         MVI   COMM_MSG_ID+1,9                   CONFIG LRECL NOT 256
         B     ERR0200
ERR0100  DS    0H
         MVI   COMM_MSG_ID+1,10                  VGET FAILED
         STH   R15,COMM_INFO_01
         B     ERR0200
ERR0110  DS    0H
         MVI   COMM_MSG_ID+1,11                  VPUT TO PROFILE FAILED
         STH   R15,COMM_INFO_01
         B     ERR0200
ERR0120  DS    0H
         MVI   COMM_MSG_ID+1,12                  VPUT TO SHARED FAILED
         STH   R15,COMM_INFO_01
ERR0200  DS    0H
         MVI   COMM_MSG_ID,0
         MVC   COMM_MSG_CSECT,MODID              SET CSECT ID
         L     R15,COMM_V_OSMSG
         BALR  R14,R15                           BUILD MESSAGE
ERR0210  DS    0H
         ITRACE ID=ERROR,                                              +
               DATA1=(COMM_MSG_ID,2)
         MVC   SPF_MSG_1,COMM_MSG_1
         MVC   SPF_MSG_2,COMM_MSG_2
         MVC   SPF_MSG_3,COMM_MSG_3
         MVC   SPF_MSG_4,COMM_MSG_4
         MVC   SPF_MSG_5,COMM_MSG_5
         B     MAIN0010
ERR0300  DS    0H
         ITRACE ID=LM_ERR,                                             +
               RDATA1=R15
         LA    R0,DISPLAY
         ST    R0,DXD_SPF_1
         LA    R1,PANEL_DIS0000
         ST    R1,DXD_SPF_2
         OI    DXD_SPF_2,X'80'
         LA    R1,DXD_SPF_PARMS
         L     R14,COMM_ISPLINK
         BALR  R14,R15                           DISPLAY CONFIG
         DC    H'0'
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
EXIT0000 DS    0H
         ITRACE ID=EXIT
         L     R1,DXD_IO_AREA                    I/O AREA ADDR
         FREEMAIN RU,                                                  +
               A=(1),                                                  +
               LV=$BUFF_SIZE
         L     R13,4(,R13)                       RESTORE REGISTER 13    ASE01670
         L     R14,12(,R13)                      RESTORE REGISTERS      ASE01680
         LR    R15,R2                            SET RC
         LM    R0,R12,20(R13)                    RESTORE REGISTERS      ASE01680
         BR    R14                               RETURN TO CALLER       ASE01700
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
DXD_START     DC   Q(DXDCONF)
V_DISASSEM    DC   V(DISASSEM)
V_DISCDIR     DC   V(DISCDIR)
V_DISDSINF    DC   V(DISDSINF)
V_DISKLIST    DC   V(DISKLIST)
V_DISSLIST    DC   V(DISSLIST)

HALF_SECOND   DC   F'50'

H4       DC   H'4'
H8       DC   H'8'
H256     DC   H'256'

DISPLAY  DC   CL8'DISPLAY'
VGET     DC   CL8'VGET'
VPUT     DC   CL8'VPUT'
NAMELIST DC   C'(PCDSN PCMEM PCVOL)'
PROFILE  DC   CL8'PROFILE'
SHARED   DC   CL8'SHARED'

PANEL_DISCONF DC CL8'DISCONF'
PANEL_DIS0000 DC CL8'DIS0000'

MSG01    DS   0C
         DC   C'Configuration data restored'
MSG01_L  EQU  *-MSG01

MSG02    DS   0C
         DC   C'Verify pre-processed DSECT KSDSes'
MSG02_L  EQU  *-MSG02

MSG03    DS   0C
         DC   C'Verify assembler SYSLIBs'
MSG03_L  EQU  *-MSG03

MSG04    DS   0C
         DC   C'Re-assembling DSECT(s)'
MSG04_L  EQU  *-MSG04

MSG05    DS   0C
         DC   C'Configuration data saved'
MSG05_L  EQU  *-MSG05

MSG06    DS   0C
         DC   C'Ending disp of data area at '
MSG06_B  DC   CL8' '
         DC   C' to '
MSG06_E  DC   CL8' '
         DC   C' limited to CSECT length'
MSG06_L  EQU  *-MSG06

MSG07    DS   0C
         DC   C'Data area beyond end of CSECT.  Area from '
MSG07_B  DC   CL8' '
         DC   C' to '
MSG07_E  DC   CL8' '
         DC   C' ignored'
MSG07_L  EQU  *-MSG07

         LTORG

*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
DXDCONF               DSECT
                      COPY DXDPREF

DXD_SPF_PARMS         DS   0A
DXD_SPF_1             DS   A
DXD_SPF_2             DS   A
DXD_SPF_3             DS   A
DXD_SPF_4             DS   A
DXD_SPF_5             DS   A
DXD_SPF_6             DS   A
DXD_SPF_7             DS   A
DXD_SPF_8             DS   A
DXD_SPF_9             DS   A
DXD_SPF_10            DS   A

DXD_IO_AREA           DS   A

DXD_FIRST_DATAAREA    DS   A
DXD_LAST_DATAAREA     DS   A

DXD_FIRST_BASE        DS   A
DXD_LAST_BASE         DS   A

DXD_FIRST_USING       DS   A
DXD_LAST_USING        DS   A

DXD_FIRST_KSDS        DS   A
DXD_LAST_KSDS         DS   A

DXD_FIRST_SYSLIB      DS   A
DXD_LAST_SYSLIB       DS   A

DXD_FIRST_ASM         DS   A
DXD_LAST_ASM          DS   A

DXD_FLAGS             DS   X
$DXD_SEARCH           EQU  X'80'

                      DSINFREQ   DSECT=NO
                      DAIRREQ    DSECT=NO

*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
DIR_DATA      DSECT
STOW_DATA     DS      0C
STOW_NAME     DS      CL8               MEMBER NAME
STOW_TT       DS      XL2               TRACK NBR
STOW_R        DS      X                 RECORD NUMBER
STOW_C        DS      X                 FLAGS, NBR OF USER DATA BYTES
STOW_DESC     DS      CL62              USER DATA
STOW_DATA_L   EQU     *-STOW_DATA

* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              DISCOMM  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              DISASMDA
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              BPXYSTAT DSECT=YES,LIST=NO
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              OSSPFD   TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              COPY     TRENTRY
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
              DCBD     DSORG=PO
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
              IHADCBE
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
              IEZIOB   DSECT=YES
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
              IHADECB  DSECT=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
FORMAT_1      DSECT
              IECSDSL1 1
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              COPY     REGEQU
$BUFF_SIZE    EQU      CONFIG_RECORD_L*16
              END      DISCONF
