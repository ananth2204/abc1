*--------------------------------------------------------------------*
*                                                                    *
*  Module name: DISOPC0                                              *
*                                                                    *
*  Function:                                                         *
*   Modify opcode table for 'C0_x' instructions                      *
*                                                                    *
*  Calling program provides two parms                                 *
*     1)   Address of OPCODE table entry to modify                    *
*     2)   Address of object text                                     *
*                                                                    *
*--------------------------------------------------------------------*
DISOPC0  CSECT
DISOPC0  AMODE 31
DISOPC0  RMODE ANY
         USING DISOPC0,R15
         USING OSCOMM,R12
         USING SESSION,R11
         USING DISCOMM,R9
         USING OPCODE_DSECT,R2
         B     MOD0000                        SKIP EYECATCHER
MODID    DC    CL8'DISOPC0'
         DC    C'&SYSDATE'
         DC    C'&SYSTIME'
MOD0000  DS    0H
         STM   R14,R12,12(R13)                SAVE REGS
         LR    R10,R15                        SET BASE REG
         DROP  R15
         USING DISOPC0,R10
         LM    R2,R3,0(R1)                    PARM ADDRESSES
         GETMAIN RU,                                                   +
               LV=WORK_L,                                              +
               LOC=ANY
         ST    R13,4(,R1)
         ST    R1,8(,R13)
         LR    R13,R1
         USING WORK,R13
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY,                                              +
               RDATA1=R2,                                              +
               RDATA2=R3
         MVC   COMM_INFO_01(1),1(R3)          COPY 2ND BYTE
         NI    COMM_INFO_01,X'0F'             LEAVE ONLY MODIFIER
         CLI   COMM_INFO_01,X'00'             LARL?
         BE    REL0010                        YES
         CLI   COMM_INFO_01,X'04'             BRCL?
         BE    REL0020                        YES
         CLI   COMM_INFO_01,X'05'             BRASL?
         BE    REL0030                        YES
         ITRACE ID=NOTVALID
         XC    OPCODE_LENGTH,OPCODE_LENGTH    SET LENGTH TO ZERO
         B     EXIT0000
REL0010  DS    0H
         MVC   OPCODE_MNEMONIC,OP_LARL        CHANGE OPCODE TO 'LARL'
         MVI   OPCODE_FORMAT,$OPCODE_RIL      USE RIL FORMAT
         B     REL0040
REL0020  DS    0H
         MVC   OPCODE_MNEMONIC,OP_BRCL        CHANGE OPCODE TO 'BRCL'
         MVI   OPCODE_FORMAT,$OPCODE_REL1     USE REL1 FORMAT
         B     REL0040
REL0030  DS    0H
         MVC   OPCODE_MNEMONIC,OP_BRASL       CHANGE OPCODE TO 'BRASL'
         MVI   OPCODE_FORMAT,$OPCODE_RIL      USE RIL FORMAT
REL0040  DS    0H
         MVC   OPCODE_LENGTH,H6               SET LENGTH TO 6
EXIT0000 DS    0H
         ITRACE ID=EXIT,                                               +
               DATA1=(OPCODE_MNEMONIC,5)
         LR    R1,R13
         L     R13,4(,R13)                    RESTORE REGISTER 13       ASE01670
         FREEMAIN RU,                                                  +
               A=(R1),                                                 +
               LV=WORK_L
         LM    R14,R12,12(R13)                RESTORE OTHER REGISTERS   ASE01680
         SR    R15,R15                        RETURN CODE               ASE01690
         BR    R14                            RETURN TO CALLER          ASE01700
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
H6       DC    H'6'
OP_LARL  DC    CL5'LARL'
OP_BRCL  DC    CL5'BRCL'
OP_BRASL DC    CL5'BRASL'
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
WORK           DSECT
               COPY DXDPREF
WORK_L         EQU  *-WORK
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               DISCOMM  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               DISASMDA
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               BPXYSTAT DSECT=YES,LIST=NO
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY     REGEQU
               END      DISOPC0
