* ------------------------------------------------------------------- *
*                                                                     *
*  Module name: DISALIST                                              *
*                                                                     *
*  Function:                                                          *
*   Allow the user to enter assembler source.                         *
*                                                                     *
*   The disassembler allows the user to use actual assembler source   *
*   source to define DSECTs and LABELs.                               *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
DISALIST CSECT
DISALIST AMODE 31
DISALIST RMODE ANY
         USING DISALIST,R15
         B     MOD0000                        SKIP EYECATCHER
MODID    DC    CL8'DISALIST'
         DC    C'&SYSDATE'
         DC    C'&SYSTIME'
MOD0000  DS    0H
         STM   R14,R12,12(R13)                SAVE REGS
         LR    R10,R15                        SET BASE REG
         DROP  R15
         USING OSCOMM,R12
         USING SESSION,R11
         USING DISALIST,R10
         USING DISCOMM,R9
         L     R15,SESS_DXD_ADDR
         A     R15,DXD_START
         ST    R13,4(,R15)
         ST    R15,8(,R13)
         LR    R13,R15                        SET SAVE AREA ADDRESS
         USING DXDALIST,R13
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY
         L     R8,COMM_OSSPFD
         USING OSSPFD,R8
         USING DCBE,SYSIN_DCBE
         XC    DISCOMM_DSECT_NEXT,DISCOMM_DSECT_NEXT
* ------------------------------------------------------------------- *
*        Allocate SYSIN                                               *
* ------------------------------------------------------------------- *
         ITRACE ID=ALOC_IN
         MVI   DAIR_FUNC,$DAIR_FREE           FREE
         MVC   DAIR_DD,DD_SYSIN               SET DDNAME
         LA    R1,DAIRREQ                     DAIR PARMS
         L     R15,COMM_V_OSDAIR              OSDAIR ENTRY POINT
         BALR  R14,R15                        FREE SYSIN
         MVI   DAIR_FUNC,$DAIR_ALLOC          ALLOCATE
         MVI   DAIR_OPTS,$DAIR_TEMP+$DAIR_USE_DD
         LA    R1,DAIRREQ                     DAIR PARMS
         L     R15,COMM_V_OSDAIR              OSDAIR ENTRY POINT
         BALR  R14,R15                        ALLOCATE SYSIN
         LTR   R15,R15                        SUCCESSFUL?
         BNZ   ERR0100                        NO
* ------------------------------------------------------------------- *
*        Copy existing source to SYSIN                                *
* ------------------------------------------------------------------- *
         ITRACE ID=COPY_SRC
         OPEN  (SYSIN_DCB,OUTPUT),                                     +
               MODE=31,                                                +
               MF=(E,DISCOMM_OPEN)
         ICM   R3,15,DISCOMM_ASM_NEXT         FIRST ASSEMBLER SOURCE
         BZ    INIT0020
         USING ASM_DSECT,R3
INIT0010 DS    0H
         PUT   SYSIN_DCB,ASM_SOURCE           WRITE TO SYSIN
         LR    R1,R3
         ICM   R3,15,ASM_NEXT                 NEXT SOURCE
         FREEMAIN RU,                         FREE THE ASM BLOCK       +
               A=(1),                                                  +
               LV=ASM_DSECT_L
         LTR   R3,R3                          ANOTHER SOURCE STATEMENT?
         BNZ   INIT0010                       YES
INIT0020 DS    0H
         XC    DISCOMM_ASM_NEXT,DISCOMM_ASM_NEXT
         CLOSE SYSIN_DCB,                                              +
               MODE=31,                                                +
               MF=(E,DISCOMM_CLOSE)
         FREEPOOL SYSIN_DCB
* ------------------------------------------------------------------- *
*        Obtain data set ID                                           *
* ------------------------------------------------------------------- *
         ITRACE ID=LMINIT
         LA    R0,FUNCTION_LMINIT
         ST    R0,DXD_SPF_1
         LA    R0,FUNCTION_DDID               DDID (RETURNED BY LMINIT)
         ST    R0,DXD_SPF_2
         LA    R0,COMM_BLANKS
         ST    R0,DXD_SPF_3
         ST    R0,DXD_SPF_4
         ST    R0,DXD_SPF_5
         ST    R0,DXD_SPF_6
         ST    R0,DXD_SPF_7
         ST    R0,DXD_SPF_8
         ST    R0,DXD_SPF_9
         LA    R0,DD_SYSIN                    DDNAME
         ST    R0,DXD_SPF_10                  SET DDNAME ADDR
         OI    DXD_SPF_10,X'80'               SET END-OF-LIST
         LA    R1,DXD_SPF_PARMS               SPF PARMS
         L     R15,COMM_ISPLINK               ISPLINK ENTRY POINT
         BALR  R14,R15                        OBTAIN DDID
         ITRACE ID=LMINITRC,                                           +
               RDATA1=R15,                                             +
               DATA2=SPF_DDID
         LTR   R15,R15                        SUCCESSFUL?
         BNZ   ERR0010                        NO
         MVC   DXD_DDID,SPF_DDID              SAVE SYSIN DATASET ID
         ITRACE ID=IN_DDID,                                            +
               DATA1=DXD_DDID
* ------------------------------------------------------------------- *
*        Call EDIT                                                    *
* ------------------------------------------------------------------- *
MAIN0000 DS    0H
         ITRACE ID=CALLEDIT,                                           +
               DATA1=DXD_DDID
         LA    R0,FUNCTION_EDIT
         ST    R0,DXD_SPF_1
         LA    R0,COMM_BLANKS
         ST    R0,DXD_SPF_2                   .. DATASET NAME
         ST    R0,DXD_SPF_3                   .. VOLSER
         ST    R0,DXD_SPF_4                   .. PASSWORD
         ST    R0,DXD_SPF_5                   .. PANEL NAME
         ST    R0,DXD_SPF_6                   .. MACRO NAME
         ST    R0,DXD_SPF_7                   .. PROFILE NAME
         LA    R0,DXD_DDID                    DDID
         ST    R0,DXD_SPF_8                   SET DATA ID ADDR
         OI    DXD_SPF_8,X'80'                FLAG END-OF-LIST
         LA    R1,DXD_SPF_PARMS               PARMS FOR EDIT
         L     R15,COMM_ISPLINK
         BALR  R14,R15                        LINK TO EDIT
* ------------------------------------------------------------------- *
*        Save updated source                                          *
* ------------------------------------------------------------------- *
         LA    R1,MAIN0020                    EOD ADDRESS
         ST    R1,DCBEEODA                    SET EOD ADDRESS
         ITRACE ID=OPEN
         OPEN  (SYSIN_DCB,INPUT),                                      +
               MODE=31,                                                +
               MF=(E,DISCOMM_OPEN)
         LA    R3,DISCOMM_ASM_PREV
MAIN0010 DS    0H
         ITRACE ID=GET
         GET   SYSIN_DCB,DXD_IO               READ A RECORD
         ITRACE ID=SOURCE
         GETMAIN RU,                                                   +
               LV=ASM_DSECT_L,                                         +
               LOC=BELOW
NEW      USING ASM_DSECT,R1
         XC    NEW.ASM_DSECT(ASM_DSECT_L),NEW.ASM_DSECT
         ST    R1,ASM_NEXT                    SET NEXT BLOCK IN PREV
         ST    R3,NEW.ASM_PREV                SET PREV BLOCK IN NEW
         MVC   NEW.ASM_EYE,ASM_ID             IDENTIFY THE BLOCK
         MVC   NEW.ASM_SOURCE,DXD_IO          COPY DATA
         LR    R3,R1
         B     MAIN0010                       READ NEXT RECORD
         DROP  NEW
MAIN0020 DS    0H
         ITRACE ID=CLOSE
         CLOSE SYSIN_DCB,                                              +
               MODE=31,                                                +
               MF=(E,DISCOMM_CLOSE)
         FREEPOOL SYSIN_DCB
* ------------------------------------------------------------------- *
*        Free SYSIN                                                   *
* ------------------------------------------------------------------- *
         ITRACE ID=FREE
         MVI   DAIR_FUNC,$DAIR_FREE
         LA    R1,DAIRREQ
         L     R15,COMM_V_OSDAIR
         BALR  R14,R15
         OI    DISCOMM_FLAGS,$DISCOMM_ASSEMBLE_SOURCE
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0010  DS    0H
         ITRACE ID=LM_ERR,                                             +
               RDATA1=R15
         MVI   COMM_MSG_ID+1,1
         STH   R15,COMM_INFO_01
         MVI   COMM_MSG_ID,0
         MVC   COMM_MSG_CSECT,MODID
         L     R15,COMM_V_OSMSG
         BALR  R14,R15                        CREATE MESSAGE(S)
         MVC   SPF_MSG_1,COMM_MSG_1           COPY MESSAGE(S)
         MVC   SPF_MSG_2,COMM_MSG_2
         MVC   SPF_MSG_3,COMM_MSG_3
         MVC   SPF_MSG_4,COMM_MSG_4
         MVC   SPF_MSG_5,COMM_MSG_5
ERR0100  DS    0H
         XC    SESS_DISP_VDATA_ADDR,SESS_DISP_VDATA_ADDR
         MVC   SESS_DISP_PANEL,PANEL_DIS0000  SET PANEL NAME
         L     R15,COMM_V_OSDISP
         BALR  R14,R15                        DISPLAY PANEL
         B     EXIT0000                       EXIT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=EXIT
         L     R13,4(,R13)                    RESTORE REGISTER 13       ASE01670
         LM    R14,R12,12(R13)                RESTORE OTHER REGISTERS   ASE01680
         SR    R15,R15                        GIVE GOOD RETURN CODE     ASE01690
         BR    R14                            RETURN TO CALLER          ASE01700
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXD_START         DC   Q(DXDALIST)

H4                DC   H'4'

ASM_ID            DC   CL8'ASM'
DD_SYSIN          DC   CL8'SYSIN'
FUNCTION_DDID     DC   CL8'DDID'
FUNCTION_EDIT     DC   CL8'EDIT'
FUNCTION_LMINIT   DC   CL8'LMINIT'
PANEL_DIS0000     DC   CL8'DIS0000'
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDALIST          DSECT
                  COPY DXDPREF

DXD_SPF_PARMS     DS   0A
DXD_SPF_1         DS   A
DXD_SPF_2         DS   A
DXD_SPF_3         DS   A
DXD_SPF_4         DS   A
DXD_SPF_5         DS   A
DXD_SPF_6         DS   A
DXD_SPF_7         DS   A
DXD_SPF_8         DS   A
DXD_SPF_9         DS   A
DXD_SPF_10        DS   A

DXD_DDID          DS   CL8
DXD_IO            DS   CL80

                  DAIRREQ DSECT=NO
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 DISCOMM  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 DISASMDA
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 IHADCBE
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 BPXYSTAT DSECT=YES,LIST=NO
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COPY     OPERAND
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 OSSPFD   TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COPY     REGEQU
                 END  DISALIST
