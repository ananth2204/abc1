*--------------------------------------------------------------------*
*                                                                    *
*  Module name: DISNLIST                                             *
*                                                                    *
*  Display section names as a scrollable selection list              *
*                                                                    *
*--------------------------------------------------------------------*
DISNLIST CSECT
DISNLIST AMODE 31
DISNLIST RMODE ANY
         USING DISNLIST,R15
         B     MOD0000                           SKIP EYECATCHER
MODID    DC    CL8'DISNLIST'
         DC    C'&SYSDATE'
         DC    C'&SYSTIME'
MOD0000  DS    0H
         STM   R14,R12,12(R13)                   SAVE REGS
         LR    R10,R15                           SET BASE REG
         DROP  R15
         USING OSCOMM,R12
         USING SESSION,R11
         USING DISNLIST,R10
         USING DISCOMM,R9
         L     R15,SESS_DXD_ADDR                 WORK AREA
         A     R15,DXD_START                     PLUS DISP
         ST    R13,4(,R15)
         ST    R15,8(,R13)
         LR    R13,R15
         USING DXDNLIST,R13
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY
         L     R8,COMM_OSSPFD                    DISSPFD ADDR
         USING OSSPFD,R8                         DEFINE BASE
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
MAIN0000 DS    0H
         ICM   R5,15,DISCOMM_NAME_NEXT           FIRST SECTION NAME
         USING NAME_DSECT,R5
         BZ    ERR0010
MAIN0010 DS    0H
         ITRACE ID=BUILD
         L     R0,DISCOMM_VDATA_ADDR             VDATA ADDRESS
         L     R1,DISCOMM_VDATA_LENGTH           VDATA SIZE
         SR    R14,R14
         SR    R15,R15
         MVCL  R0,R14                            INITIALIZE VDATA
         L     R6,DISCOMM_VDATA_ADDR
         USING DETAIL,R6
         L     R7,SPF_VDEPTH                     SCREEN DEPTH
         ST    R5,DXD_FIRST_DISPLAYED            SAVE FIRST DISPLAYED
         XC    DXD_LINES_BUILT,DXD_LINES_BUILT
MAIN0020 DS    0H
         ITRACE ID=NAME,                                               +
               DATA1=NAME_NAME,                                        +
               DATA2=(NAME_OFFSET,4)
         ST    R5,DXD_LAST_DISPLAYED             SAVE LAST DISPLAYED
         MVI   DETAIL_ATTR_1,$SCREEN_ATTR_INPUT  SET ATTRIBUTE
         MVI   DETAIL_SELECT,C' '                INITIALIZE SELECT
         MVI   DETAIL_ATTR_2,$SCREEN_ATTR_HIGH_TURQUOISE
         MVC   DETAIL_NAME,NAME_NAME             COPY NAME
         MVC   COMM_DWORD(4),NAME_OFFSET         COPY TO WORK AREA
         UNPK  DETAIL_OFFSET(9),COMM_DWORD(5)    .. PREVENTS S0C4'S
         TR    DETAIL_OFFSET,COMM_HEXCHAR
         MVI   DETAIL_OFFSET+8,C' '
         LA    R1,1                              CONSTANT 1
         AH    R1,DXD_LINES_BUILT                PLUS TOTAL SO FAR
         STH   R1,DXD_LINES_BUILT                UPDATE TOTAL
         A     R6,SPF_VWIDTH                     NEXT LINE
         BCT   R7,MAIN0030
         B     MAIN0040
MAIN0030 DS    0H
         ICM   R5,15,NAME_NEXT                   NEXT SECTION
         BNZ   MAIN0020                          LOOP
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
MAIN0040 DS    0H
         ITRACE ID=REFRDSCB
         L     R15,COMM_V_OSDSCB
         BALR  R14,R15                           REFRESH DSCB INFO
         MVC   SESS_DISP_PANEL,PANEL_NAME        SET PANEL NAME
         MVC   SESS_DISP_VDATA_ADDR,DISCOMM_VDATA_ADDR
         MVC   SESS_DISP_VDATA_LENGTH,DISCOMM_VDATA_LENGTH
         XC    SESS_DISP_CURSOR,SESS_DISP_CURSOR
*---------------------------------------------------------------------*
*                                                                     *
*        Display                                                      *
*                                                                     *
*---------------------------------------------------------------------*
MAIN0050 DS    0H
         L     R15,COMM_V_OSDISP                 OSDISP ENTRY POINT
         BALR  R14,R15                           DISPLAY PANEL
         ITRACE ID=DISP_RC,                                            +
               RDATA1=R15
         LTR   R2,R15                            RC = ZERO?
         BNZ   EXIT0000                          NO
*---------------------------------------------------------------------*
*        Check for selection from command line                        *
*---------------------------------------------------------------------*
         OC    COMM_OPERANDS_NBR,COMM_OPERANDS_NBR
         BZ    MAIN0090                          NO OPERANDS, TRY LINE
         CLC   COMM_OPERANDS_NBR,H2              NBR CORRECT?
         BNE   ERR0020                           NO
         ICM   R1,15,OPERAND_01_ADDRESS          1ST OPERAND ADDR
         CLC   =C'S ',0(R1)                      "S "?
         BE    MAIN0060                          YES
         CLC   =C'SEL ',0(R1)                    "SEL "?
         BE    MAIN0060                          YES
         CLC   =C'SELECT ',0(R1)                 "SELECT "?
         BNE   ERR0030                           NO
MAIN0060 DS    0H
         ICM   R1,15,OPERAND_02_ADDRESS          2ND OPERAND ADDR
         SR    R2,R2
         ICM   R2,3,OPERAND_02_LENGTH            OPERAND 2 LENGTH
         CH    R2,H8                             TOO LONG?
         BH    ERR0040                           YES
         MVC   DXD_NAME,COMM_BLANKS              INITIALIZE NAME
         BCTR  R2,0
         EX    R2,NAME_MVC                       COPY NAME
         ITRACE ID=NAME,                                               +
               DATA1=DXD_NAME
         ICM   R5,15,DISCOMM_NAME_NEXT           ANY NAMES?
         BZ    ERR0050                           NO
MAIN0070 DS    0H
         ITRACE ID=A,DATA1=NAME_NAME,DATA2=DXD_NAME
         CLC   NAME_NAME,DXD_NAME                NAME LOCATED?
         BE    MAIN0080
         ICM   R5,15,NAME_NEXT                   NEXT NAME
         BNZ   MAIN0070
         B     ERR0050                           NOT FOUND
MAIN0080 DS    0H
         ITRACE ID=GOODNAME
         MVC   SPF_ZCMD,COMM_BLANKS
         MVC   DISCOMM_CSECT,DXD_NAME            COPY NAME
         MVC   SPF_CSECT,DXD_NAME                COPY NAME
         SR    R2,R2
         B     EXIT0000
NAME_MVC MVC   DXD_NAME(0),0(R1)
*---------------------------------------------------------------------*
*        Check for selection                                          *
*---------------------------------------------------------------------*
MAIN0090 DS    0H
         ITRACE ID=LINE_SEL
         L     R6,DISCOMM_VDATA_ADDR             VDATA ADDR
         LH    R7,DXD_LINES_BUILT
MAIN0100 DS    0H
         CLI   DETAIL_SELECT,C' '                SELECTED?
         BNE   MAIN0200                          YES
         A     R6,SPF_VWIDTH                     NEXT LINE
         BCT   R7,MAIN0100                       CHECK NEXT LINE
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
         CLI   SPF_ZVERB,C'B'                    BOTTOM?
         BE    MAIN0130                          YES
         CLI   SPF_ZVERB,C'D'                    DOWN?
         BE    MAIN0150                          YES
         CLI   SPF_ZVERB,C'T'                    TOP?
         BE    MAIN0000                          YES
         CLI   SPF_ZVERB,C'U'                    UP?
         BE    MAIN0160                          YES
         B     MAIN0050                          DISPLAY DATA AGAIN
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
MAIN0130 DS    0H
         ITRACE ID=BOTTOM
         ICM   R5,15,DISCOMM_NAME_NEXT
         BZ    EXIT0000
MAIN0140 DS    0H
         ICM   R1,15,NAME_NEXT                   NEXT NAME
         BZ    MAIN0010                          END OF LIST
         LR    R5,R1                             COPY NEXT
         B     MAIN0140                          LOOP
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
MAIN0150 DS    0H
         ITRACE ID=DOWN
         CLI   SPF_ZSCROLLA,C'M'                 MAX?
         BE    MAIN0130                          YES
         L     R5,DXD_LAST_DISPLAYED             LAST ON DISPLAY
         B     MAIN0010                          BUILD DISPLAY
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
MAIN0160 DS    0H
         ITRACE ID=UP
         CLI   SPF_ZSCROLLA,C'M'                 MAX?
         BE    MAIN0000                          YES
         L     R5,DXD_FIRST_DISPLAYED            FIRST ON DISPLAY
         L     R7,SPF_ZSCROLLN                   NBR OF LINES TO SCROLL
MAIN0170 DS    0H
         ICM   R1,15,NAME_PREV                   PREVIOUS ON CHAIN
         BZ    MAIN0010
         LR    R5,R1
         BCT   R7,MAIN0170
         B     MAIN0010
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
MAIN0200 DS    0H
         ITRACE ID=SELECTED,                                           +
               DATA1=DETAIL_NAME
         MVC   DISCOMM_CSECT,DETAIL_NAME
         MVC   SPF_CSECT,DETAIL_NAME
         B     EXIT0000
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
ERR0010  DS    0H
         MVI   COMM_MSG_ID+1,1                   SET MESSAGE ID
         B     ERR0100
ERR0020  DS    0H
         ITRACE ID=BADOPERS
         MVI   COMM_MSG_ID+1,2                   SET MESSAGE ID
         B     ERR0100
ERR0030  DS    0H
         ITRACE ID=BADCMD
         MVI   COMM_MSG_ID+1,3                   SET MESSAGE ID
         B     ERR0100
ERR0040  DS    0H
         ITRACE ID=LONGNAME
         MVI   COMM_MSG_ID+1,4                   SET MESSAGE ID
         B     ERR0100
ERR0050  DS    0H
         ITRACE ID=BAD_NAME
         MVI   COMM_MSG_ID+1,5                   SET MESSAGE ID
         MVC   COMM_INFO_01(8),DXD_NAME
ERR0100  DS    0H
         MVI   COMM_MSG_ID,0
         ITRACE ID=ERROR,                                              +
               DATA1=(COMM_MSG_ID,2)
         MVC   COMM_MSG_CSECT,MODID
         L     R15,COMM_V_OSMSG
         BALR  R14,R15
         MVC   SPF_MSG_1,COMM_MSG_1
         MVC   SPF_MSG_2,COMM_MSG_2
         MVC   SPF_MSG_3,COMM_MSG_3
         MVC   SPF_MSG_4,COMM_MSG_4
         MVC   SPF_MSG_5,COMM_MSG_5
         B     MAIN0040
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
EXIT0000 DS    0H
         ITRACE ID=EXIT
         L     R13,4(,R13)                       RESTORE REGISTER 13    ASE01670
         L     R14,12(,R13)                      RESTORE REGISTERS      ASE01680
         LR    R15,R2                            SET RC
         LM    R0,R12,20(R13)                    RESTORE REGISTERS      ASE01680
         BR    R14                               RETURN TO CALLER       ASE01700
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
DXD_START            DC   Q(DXDNLIST)

H2                   DC   H'2'
H8                   DC   H'8'

PANEL_NAME           DC   CL8'DISNLIST'

                     LTORG
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
DXDNLIST             DSECT
                     COPY DXDPREF

DXD_FIRST_DISPLAYED  DS   A
DXD_LAST_DISPLAYED   DS   A

DXD_LINES_BUILT      DS   H

DXD_NAME             DS   CL8
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
DETAIL               DSECT
                     DS   CL10
DETAIL_ATTR_1        DS   X
DETAIL_SELECT        DS   X
DETAIL_ATTR_2        DS   X
DETAIL_NAME          DS   CL8
DETAIL_ATTR_3        DS   X
DETAIL_OFFSET        DS   CL8
                     DS   X
DETAIL_L             EQU  *-DETAIL
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 DISCOMM  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 DISASMDA
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 BPXYSTAT DSECT=YES,LIST=NO
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COPY     OPERAND
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 OSSPFD   TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COPY     ATTRS
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COPY     REGEQU
              END     DISNLIST
