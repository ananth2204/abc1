* ------------------------------------------------------------------- *
*                                                                     *
*  MODULE NAME: DISLREAD                                              *
*                                                                     *
*  FUNCTION:                                                          *
*    Read LABELS and EQUATEs for a DSECT from a pre-processed         *
*    data KSDS                                                        *
*                                                                     *
* ------------------------------------------------------------------- *
DISLREAD CSECT
DISLREAD AMODE 31
DISLREAD RMODE ANY
         USING DISLREAD,R15
         B     MOD0000                        SKIP EYECATCHER
MODID    DC    CL8'DISLREAD'
         DC    C'&SYSDATE'
         DC    C'&SYSTIME'
MOD0000  DS    0H
         STM   R14,R12,12(R13)                SAVE REGS
         LR    R10,R15                        SET BASE REG
         DROP  R15
         USING OSCOMM,R12
         USING SESSION,R11
         USING DISLREAD,R10
         USING DISCOMM,R9
         L     R15,SESS_DXD_ADDR              WORK AREA
         A     R15,DXD_START                  PLUS DISP
         ST    R13,4(,R15)
         ST    R15,8(,R13)
         LR    R13,R15
         USING DXDLREAD,R13
         MVC   DXD_CSECT,R13
         ITRACE ID=ENTRY,                                              +
               DATA1=DISCOMM_USING_KSDS,                               +
               DATA2=DISCOMM_USING_DSECT
         L     R6,DISCOMM_USING_KSDS          KSDS BLOCK ADDR
         L     R5,DISCOMM_USING_DSECT         DSECT BLOCK ADDR
         USING KSDS_DSECT,R6
         USING DSECT_DSECT,R5
         MVC   DXD_RPL(RPL_L),RPL_I
         USING IFGRPL,DXD_RPL
* ------------------------------------------------------------------- *
*                                                                     *
*                                                                     *
*        READ the LABELs (and EQUATES) for a DSECT from a KSDS        *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0010 DS    0H
         TM    KSDS_FLAGS,$KSDS_OPEN          KSDS OPEN?
         BO    MAIN0020                       YES
         OPEN  (KSDS_ACB,INPUT),              OPEN THE KSDS            +
               MODE=31,                                                +
               MF=(E,DISCOMM_OPEN)
         OI    KSDS_FLAGS,$KSDS_OPEN          SET FLAG
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0020 DS    0H
         ITRACE ID=MODCB1
         MVC   DXD_MODCB(MODCB1_L),MODCB1_I
         LA    R2,DXD_RPL
         LA    R3,KSDS_ACB
         LA    R7,PPD
         LA    R8,PPD_KEY
         MODCB RPL=(R2),                                               +
               ACB=(R3),                                               +
               AREA=(R7),                                              +
               AREALEN=PPD_L,                                          +
               ARG=(R8),                                               +
               KEYLEN=L'PPD_KEY,                                       +
               OPTCD=(KEY,KEQ,DIR,SYN,MVE),                            +
               MF=(G,DXD_MODCB)
         ITRACE ID=MODCB_RC,                                           +
               RDATA1=R15,                                             +
               RDATA2=R0
         LTR   R15,R15
         BNZ   ERR0010
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         XC    PPD_SEQ,PPD_SEQ                FORCE SEQ TO ZERO
         MVC   PPD_NAME,DSECT_NAME            SET DSECT NAME
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         LA    R4,DSECT_LABEL_PREV            "FIRST" LABEL ON CHAIN
         USING LABEL_DSECT,R4
         SR    R3,R3
         USING EQU_DSECT,R3
MAIN0030 DS    0H
         ITRACE ID=GET
         GET   RPL=DXD_RPL                    READ A RECORD
         ITRACE ID=GET_RC,                                             +
               RDATA1=R15,                                             +
               DATA2=RPLFDBK
         LTR   R15,R15                        SUCCESSFUL?
         BNZ   EXIT0000                       NO
         CLC   LABEL_ID,PPD_DATA+8            LABEL?
         BE    MAIN0040                       YES
         CLC   EQU_ID,PPD_DATA+8              EQUATE?
         BE    MAIN0050                       YES
         B     MAIN0060                       OTHERWISE
MAIN0040 DS    0H
         GETMAIN RU,                                                   +
               LV=LABEL_DSECT_L,                                       +
               LOC=ANY
NEW      USING LABEL_DSECT,R1
         XC    NEW.LABEL_DSECT(LABEL_DSECT_L),NEW.LABEL_DSECT
KSDS     USING LABEL_DSECT,PPD_DATA
         ST    R1,LABEL_NEXT
         ST    R4,LABEL_PREV
         MVC   NEW.LABEL_EYE,KSDS.LABEL_EYE
         MVC   NEW.LABEL_NAME,KSDS.LABEL_NAME
         MVC   NEW.LABEL_DISP,KSDS.LABEL_DISP
         MVC   NEW.LABEL_TYPE,KSDS.LABEL_TYPE
         LA    R3,LABEL_EQU_PREV
         LR    R4,R1
         ITRACE ID=LABEL,                                              +
               RDATA1=R4,                                              +
               DATA2=LABEL_NAME
         DROP  NEW,KSDS
         B     MAIN0060
MAIN0050 DS    0H
         GETMAIN RU,                                                   +
               LV=EQU_DSECT_L,                                         +
               LOC=ANY
NEW      USING EQU_DSECT,R1
         XC    NEW.EQU_DSECT(EQU_DSECT_L),NEW.EQU_DSECT
KSDS     USING EQU_DSECT,PPD_DATA
         ST    R1,EQU_NEXT
         ST    R3,NEW.EQU_PREV
         MVC   NEW.EQU_EYE,KSDS.EQU_EYE
         MVC   NEW.EQU_LABEL,KSDS.EQU_LABEL
         MVC   NEW.EQU_VALUE,KSDS.EQU_VALUE
         LR    R3,R1
         ITRACE ID=EQU,                                                +
               RDATA1=R3,                                              +
               DATA2=EQU_LABEL
         DROP  NEW,KSDS
MAIN0060 DS    0H
         ICM   R1,15,PPD_SEQ                  CURRENT SEQ NBR
         LA    R1,1(,R1)                      PLUS 1
         STCM  R1,15,PPD_SEQ                  UPDATE SEQ NBR
         B     MAIN0030                       READ NEXT RECORD
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0010  DS    0H
         ITRACE ID=MOD_ERR,                                            +
               RDATA1=R15
         DC    H'0'
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=EXIT
         L     R13,4(,R13)                    RESTORE REGISTER 13
         L     R14,12(,R13)                   RESTORE REGISTERS
         SR    R15,R15                        SET RC
         LM    R0,R12,20(R13)                 RESTORE REGISTERS
         BR    R14                            RETURN TO CALLER
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXD_START     DC   Q(DXDLREAD)

RPL_I         RPL  AM=VSAM
RPL_L         EQU  *-RPL_I

MODCB1_I MODCB RPL=*,                                                  +
               ACB=*,                                                  +
               AREA=*,                                                 +
               AREALEN=PPD_L,                                          +
               ARG=*,                                                  +
               KEYLEN=L'PPD_KEY,                                       +
               OPTCD=(KEY,KGE,DIR,SYN,MVE),                            +
               MF=L
MODCB1_L EQU  *-MODCB1_I

LABEL_ID DC   CL8'LABEL'
EQU_ID   DC   CL8'EQU  '
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDLREAD      DSECT
              COPY    DXDPREF

DXD_MODCB     DS      0F,(MODCB1_L)X
DXD_RPL       DS      0F,(RPL_L)X

              COPY    PPDDATA
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 DISCOMM  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 DISASMDA
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 BPXYSTAT DSECT=YES,LIST=NO
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 IFGRPL   AM=VSAM
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COPY     REGEQU
              END     DISLREAD
