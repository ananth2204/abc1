* ------------------------------------------------------------------- *
*                                                                     *
*  MODULE NAME: DISDEBUG                                              *
*                                                                     *
*  FUNCTION:                                                          *
*    Display an AREA chain                                            *
*                                                                     *
* ------------------------------------------------------------------- *
DISDEBUG CSECT
DISDEBUG AMODE 31
DISDEBUG RMODE ANY
         USING DISDEBUG,R15
         B     MOD0000                           SKIP EYECATCHER
MODID    DC    CL8'DISDEBUG'
         DC    C'&SYSDATE'
         DC    C'&SYSTIME'
MOD0000  DS    0H
         STM   R14,R12,12(R13)                   SAVE REGS
         LR    R10,R15                           SET BASE REG
         DROP  R15
         USING OSCOMM,R12
         USING SESSION,R11
         USING DISDEBUG,R10
         USING DISCOMM,R9
         L     R15,SESS_DXD_ADDR                 WORK AREA
         A     R15,DXD_START                     PLUS DISP
         ST    R13,4(,R15)
         ST    R15,8(,R13)
         LR    R13,R15
         USING DXDXLIST,R13
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY,                                              +
               RDATA1=R1
         L     R8,COMM_OSSPFD
         USING OSSPFD,R8
         LR    R5,R1
         USING AREA_DSECT,R5
         ST    R5,DXD_FIRST_AREA
         ST    R5,DXD_FIRST_DISPLAYED
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0010 DS    0H
         ITRACE ID=BUILD
         L     R0,DISCOMM_VDATA_ADDR
         L     R1,DISCOMM_VDATA_LENGTH
         SR    R14,R14
         SR    R15,R15
         MVCL  R0,R14                            INITIALIZE VDATA
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         L     R6,DISCOMM_VDATA_ADDR             VDATA ADDRESS
         L     R7,SPF_VDEPTH                     NBR OF LINES
         ICM   R5,15,DXD_FIRST_DISPLAYED         1ST AREA DISPLAYED
         BZ    MAIN0300
         CLI   AREA_TYPE-AREA_DSECT(R5),X'FF'    DUMMY ENTRY?
         BE    MAIN0300                          YES
MAIN0020 DS    0H
         ITRACE ID=AREA,                                               +
               DATA1=AREA_BEGIN,                                       +
               DATA2=(AREA_TYPE,1)
         MVC   0(MSG01_L,R6),MSG01               INITIALIZE
         USING MSG01,R6
         ST    R5,DXD_LAST_DISPLAYED             SAVE LAST DISPLAYED
         UNPK  DXD_WORK(9),DXD_LAST_DISPLAYED(5) UNPACK ADDR
         MVC   MSG01_ADDR,DXD_WORK               COPY UNPACKED ADDR
         TR    MSG01_ADDR,HEXCHAR                TRANSLATE ADDR
         UNPK  DXD_WORK(9),AREA_PREV(5)          UNPACK ADDR
         MVC   MSG01_PREV,DXD_WORK               COPY UNPACKED ADDR
         TR    MSG01_PREV,HEXCHAR                TRANSLATE ADDR
         UNPK  DXD_WORK(9),AREA_NEXT(5)          UNPACK ADDR
         MVC   MSG01_NEXT,DXD_WORK               COPY UNPACKED ADDR
         TR    MSG01_NEXT,HEXCHAR                TRANSLATE ADDR
         UNPK  DXD_WORK(9),AREA_BEGIN(5)         UNPACK BEGIN DISP
         MVC   MSG01_BEGIN,DXD_WORK              COPY UNPACKED DISP
         TR    MSG01_BEGIN,HEXCHAR               TRANSLATE DISP
         UNPK  DXD_WORK(9),AREA_END(5)           UNPACK END DISP
         MVC   MSG01_END,DXD_WORK                COPY UNPACKED ADDR
         TR    MSG01_END,HEXCHAR                 TRANSLATE DISP
         UNPK  DXD_WORK(9),AREA_LENGTH(5)        UNPACK END DISP
         MVC   MSG01_LENGTH,DXD_WORK             COPY UNPACKED ADDR
         TR    MSG01_LENGTH,HEXCHAR              TRANSLATE DISP
         CLI   AREA_TYPE,$AREA_DATA              DATA AREA?
         BE    MAIN0030                          YES
         CLI   AREA_TYPE,$AREA_INSTRUCTION       INSTRUCTION?
         BE    MAIN0090                          YES
         ITRACE ID=BAD_TYPE
         DC    H'0'
MAIN0030 DS    0H
         MVI   MSG01_ATTR,$SCREEN_ATTR_HIGH_TURQUOISE
         CLI   AREA_DATA_TYPE,$DATA_ACON         ADCON?
         BE    MAIN0040                          YES
         CLI   AREA_DATA_TYPE,$DATA_VCON         VCON?
         BE    MAIN0050                          YES
         CLI   AREA_DATA_TYPE,$DATA_CXD          CXD?
         BE    MAIN0060                          YES
         CLI   AREA_DATA_TYPE,$DATA_Q            Q?
         BE    MAIN0070                          YES
         CLI   AREA_DATA_TYPE,$DATA_USER         USER?
         BE    MAIN0080                          YES
         MVI   MSG01_ATTR,$SCREEN_ATTR_HIGH_RED
         MVC   MSG01_TYPE,T_DATA                 DATA
         B     MAIN0100
MAIN0040 DS    0H
         MVC   MSG01_TYPE,T_ADCON                ADCON
         B     MAIN0100
MAIN0050 DS    0H
         MVC   MSG01_TYPE,T_VCON                 VCON
         B     MAIN0100
MAIN0060 DS    0H
         MVC   MSG01_TYPE,T_CXD                  CXD
         B     MAIN0100
MAIN0070 DS    0H
         MVC   MSG01_TYPE,T_Q                    Q
         B     MAIN0100
MAIN0080 DS    0H
         MVC   MSG01_TYPE,T_USER                 USER
         B     MAIN0100
MAIN0090 DS    0H
         MVC   MSG01_TYPE,T_INSTR
         ICM   R1,15,AREA_OPCODE                 OPCODE ADDR
         BZ    MAIN0100                          NOT DETERMINED
         USING OPCODE_DSECT,R1
         MVI   MSG01_ATTR,$SCREEN_ATTR_HIGH_YELLOW
         MVC   MSG01_TYPE,OPCODE_MNEMONIC        COPY MNEMONIC
         B     MAIN0100
MAIN0100 DS    0H
         MVC   MSG01_RLD_NAME,AREA_RLD_NAME
         DROP  R6
         A     R6,SPF_VWIDTH                     NEXT LINE
         BCT   R7,MAIN0110
         B     MAIN0120                          SCREEN IS FULL
MAIN0110 DS    0H
         ICM   R5,15,AREA_NEXT                   NEXT AREA
         BNZ   MAIN0020
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0120 DS    0H
         ITRACE ID=DISPLAY
         MVC   SESS_DISP_PANEL,PANEL_NAME        SET PANEL NAME
         MVC   SESS_DISP_VDATA_ADDR,DISCOMM_VDATA_ADDR
         MVC   SESS_DISP_VDATA_LENGTH,DISCOMM_VDATA_LENGTH
         XC    SESS_DISP_CURSOR,SESS_DISP_CURSOR
         OI    COMM_FLAGS,$COMM_NO_FORMAT+$COMM_NO_SESSION
         L     R15,COMM_V_OSDISP                 DISP ENTRY POINT
         BALR  R14,R15                           DISPLAY
         NI    COMM_FLAGS,255-$COMM_NO_FORMAT-$COMM_NO_SESSION
         ITRACE ID=DISP_RC,                                            +
               RDATA1=R15,                                             +
               DATA2=SPF_ZVERB
         LTR   R15,R15                           RC = 0?
         BNZ   EXIT0000                          NO
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         ITRACE ID=SCROLL,                                             +
               DATA1=SPF_ZVERB,                                        +
               DATA2=SPF_ZSCROLLA
         CLI   SPF_ZVERB,C'B'                    BOTTOM?
         BE    MAIN0200
         CLI   SPF_ZVERB,C'D'                    DOWN?
         BE    MAIN0220
         CLI   SPF_ZVERB,C'T'                    TOP?
         BE    MAIN0250
         CLI   SPF_ZVERB,C'U'                    UP?
         BE    MAIN0260
         B     MAIN0120
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0200 DS    0H
         ITRACE ID=BOTTOM
         ICM   R5,15,DXD_FIRST_AREA              FIRST ON CHAIN
MAIN0210 DS    0H
         LR    R1,R5
         ICM   R5,15,AREA_NEXT                   NEXT AREA
         BNZ   MAIN0210
         ST    R1,DXD_FIRST_DISPLAYED
         B     MAIN0010                          BUILD DISPLAY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0220 DS    0H
         ITRACE ID=DOWN
         CLI   SPF_ZSCROLLA,C'M'                 MAX?
         BE    MAIN0200                          YES
         L     R2,SPF_ZSCROLLN                   NBR OF LINES TO SCROLL
         L     R5,DXD_FIRST_DISPLAYED            1ST ON DISPLAY
MAIN0230 DS    0H
         LR    R1,R5
         ICM   R5,15,AREA_NEXT                   NEXT ON CHAIN
         BZ    MAIN0240                          AT END OF CHAIN
         BCT   R2,MAIN0230
MAIN0240 DS    0H
         ST    R1,DXD_FIRST_DISPLAYED
         B     MAIN0010
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0250 DS    0H
         ITRACE ID=TOP
         MVC   DXD_FIRST_DISPLAYED,DXD_FIRST_AREA
         B     MAIN0010
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0260 DS    0H
         ITRACE ID=UP
         CLI   SPF_ZSCROLLA,C'M'                 MAX?
         BE    MAIN0250                          YES
         ICM   R5,15,DXD_FIRST_DISPLAYED         FIRST ON DISPLAY
         BZ    EXIT0000                          SHOULD NOT HAPPEN!
         L     R2,SPF_ZSCROLLN                   LINES ON DISPLAY
MAIN0270 DS    0H
         LR    R1,R5
         ICM   R5,15,AREA_PREV                   PREVIOUS AREA
         BZ    MAIN0280
         BCT   R2,MAIN0270                       LOOP
MAIN0280 DS    0H
         ST    R1,DXD_FIRST_DISPLAYED            COPY ADDR
         B     MAIN0010
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0300 DS    0H
         ITRACE ID=NOTHING
         MVC   SPF_MSG_5(MSG02_L),MSG02
         MVC   SESS_DISP_PANEL,DIS0000
         XC    SESS_DISP_VDATA_ADDR,SESS_DISP_VDATA_ADDR
         XC    SESS_DISP_VDATA_LENGTH,SESS_DISP_VDATA_LENGTH
         XC    SESS_DISP_CURSOR,SESS_DISP_CURSOR
         L     R15,COMM_V_OSDISP
         BALR  R14,R15
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=EXIT
         MVC   SPF_MSG_1,COMM_BLANKS
         MVC   SPF_MSG_2,COMM_BLANKS
         MVC   SPF_MSG_3,COMM_BLANKS
         MVC   SPF_MSG_4,COMM_BLANKS
         MVC   SPF_MSG_5,COMM_BLANKS
         L     R13,4(,R13)                       RESTORE REGISTER 13    ASE01670
         L     R14,12(,R13)                      RESTORE REGISTERS      ASE01680
         LR    R15,R2                            SET RC
         LM    R0,R12,20(R13)                    RESTORE REGISTERS      ASE01680
         BR    R14                               RETURN TO CALLER       ASE01700
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXD_START     DC   Q(DXDXLIST)

DIS0000       DC   CL8'DIS0000'
PANEL_NAME    DC   CL8'DISXLIST'

T_INSTR       DC   CL5'INSTR'
T_ADCON       DC   CL5'ADCON'
T_VCON        DC   CL5'VCON'
T_CXD         DC   CL5'CXD'
T_Q           DC   CL5'Q'
T_USER        DC   CL5'DATA'
T_DATA        DC   CL5'DATA'

              LTORG

HEXCHAR       EQU  *-C'0'
              DC   C'0123456789ABCDEF'

MSG01         DS   0C
              DC   AL1($SCREEN_ATTR_HIGH_TURQUOISE)
MSG01_ADDR    DC   CL8' '
              DC   AL1($SCREEN_ATTR_NORMAL)
MSG01_PREV    DC   CL8' '
              DC   CL1' '
MSG01_NEXT    DC   CL8' '
              DC   CL1' '
MSG01_BEGIN   DC   CL8' '
              DC   CL1' '
MSG01_END     DC   CL8' '
              DC   CL1' '
MSG01_LENGTH  DC   CL8' '
MSG01_ATTR    DC   AL1(0)
MSG01_TYPE    DC   CL5' '
              DC   AL1($SCREEN_ATTR_HIGH_GREEN)
MSG01_RLD_NAME DC  CL8' '
MSG01_L       EQU  *-MSG01

MSG02         DS   0C
              DC   C'Nothing on chain'
MSG02_L       EQU  *-MSG02


* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDXLIST              DSECT
                      COPY DXDPREF

DXD_FIRST_AREA        DS  A             FIRST DATA ITEM ON CHAIN

DXD_FIRST_DISPLAYED   DS  A             FIRST DATA ITEM DISPLAYED
DXD_LAST_DISPLAYED    DS  A             LAST DATA ITEM DISPLAYED

DXD_WORK              DS  CL9
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 DISCOMM  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 DISASMDA
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 BPXYSTAT DSECT=YES,LIST=NO
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 OSSPFD   TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COPY     ATTRS
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COPY     REGEQU
              END     DISDEBUG
