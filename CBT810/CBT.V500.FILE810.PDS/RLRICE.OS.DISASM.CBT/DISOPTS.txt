*--------------------------------------------------------------------*
*                                                                    *
*  Module name: DISOPTS                                              *
*                                                                    *
*  Function:                                                         *
*                                                                    *
*   Allow the user to choose                                         *
*     .  prefix for generated labels                                 *
*     .  output dataset (and member) for generated source            *
*                                                                    *
*--------------------------------------------------------------------*
DISOPTS  CSECT
DISOPTS  AMODE 31
DISOPTS  RMODE ANY
         USING DISOPTS,R15
         B     MOD0000                           SKIP EYECATCHER
MODID    DC    CL8'DISOPTS'
         DC    C'&SYSDATE'
         DC    C'&SYSTIME'
MOD0000  DS    0H
         STM   R14,R12,12(R13)                   SAVE REGS
         LR    R10,R15                           SET BASE REG
         DROP  R15
         USING OSCOMM,R12
         USING SESSION,R11
         USING DISOPTS,R10
         USING DISCOMM,R9
         USING ESD_DSECT,R6
         L     R15,SESS_DXD_ADDR                 WORK AREA
         A     R15,DXD_START                     PLUS DISP
         ST    R13,4(R15)
         ST    R15,8(R13)
         LR    R13,R15
         USING DXDOPTS,R13
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY
         L     R8,COMM_OSSPFD
         USING OSSPFD,R8
         MVC   SPF_PREFIX,DISCOMM_PREFIX
         MVC   SPF_OUTPUT_DSN,DISCOMM_OUTPUT_DSN
         MVC   SPF_OUTPUT_MEMBER,DISCOMM_OUTPUT_MEMBER
*--------------------------------------------------------------------*
*                                                                    *
*--------------------------------------------------------------------*
MAIN0010 DS    0H
         ITRACE ID=DISPLAY
         MVC   SESS_DISP_PANEL,PANEL_NAME        SET PANEL NAME
         XC    SESS_DISP_VDATA_ADDR,SESS_DISP_VDATA_ADDR
         XC    SESS_DISP_VDATA_LENGTH,SESS_DISP_VDATA_LENGTH
         XC    SESS_DISP_CURSOR,SESS_DISP_CURSOR
         L     R15,COMM_V_OSDISP
         BALR  R14,R15                           DISPLAY ESD INFO
         ITRACE ID=DISP_RC,                                            +
               RDATA1=R15
         LTR   R2,R15                            RC=0?
         BNZ   MAIN0200                          NO
*--------------------------------------------------------------------*
*                                                                    *
*--------------------------------------------------------------------*
         NI    DXD_FLAGS,255-$ERROR              RESET ERROR FLAG
*---------------------------------------------------------------------*
*        VERIFY OUTPUT DSN                                            *
*---------------------------------------------------------------------*
         CLC   SPF_OUTPUT_DSN,COMM_BLANKS        OUTPUT DSN BLANK?
         BE    ERR0010                           YES
         MVI   DSINFO_OPTIONS,0
         MVC   DSINFO_DSN,SPF_OUTPUT_DSN         COPY DSN
         MVC   DSINFO_VOLSER,COMM_BLANKS
         LA    R1,DSINFREQ                       DSINFO REQ PARMS
         L     R15,COMM_V_OSDSINFO               OSDSINFO ENTRY POINT
         ITRACE ID=DSINFO,                                             +
               RDATA1=R15,                                             +
               RDATA2=R1
         BALR  R14,R15                           CALL OSDSINFO
         ITRACE ID=INFO_RC,                                            +
               RDATA1=R15,                                             +
               DATA2=(DSINFO_ERROR_INFO,4)
         LTR   R15,R15                           PROBLEM?
         BNZ   ERR0210                           YES
         OC    DSINFO_ERROR_INFO,DSINFO_ERROR_INFO
         BNZ   ERR0020
         USING DS1FMTID,DSINFO_DSCB              DEFINE BASE
         TM    DS1DSORG,DS1DSGPS                 SEQUENTIAL?
         BO    MAIN0020                          YES
         TM    DS1DSORG,DS1DSGPO                 PDS?
         BO    MAIN0030                          YES
         B     ERR0030                           NOT ALLOWED
MAIN0020 DS    0H
         CLC   SPF_OUTPUT_MEMBER,COMM_BLANKS     MEMBER NAME BLANK?
         BNE   ERR0040                           NO
         B     MAIN0040
MAIN0030 DS    0H
         CLC   SPF_OUTPUT_MEMBER,COMM_BLANKS     MEMBER NAME BLANK?
         BE    ERR0050
MAIN0040 DS    0H
         TM    DS1RECFM,DS1RECFV                 RECFM=V OR U?
         BO    ERR0060                           YES.. NOT ALLOWED
         CLC   DS1LRECL,H80                      LRECL=80?
         BNE   ERR0070                           NO
*---------------------------------------------------------------------*
*        VERIFY PREFIX                                                *
*---------------------------------------------------------------------*
         OC    SPF_PREFIX,COMM_BLANKS            TRANSLATE
         CLI   SPF_PREFIX,C'@'                   AT-SIGN?
         BE    MAIN0080                          YES
         CLI   SPF_PREFIX,C'#'                   POUND SIGN?
         BE    MAIN0080                          YES
         CLI   SPF_PREFIX,C'$'                   DOLLAR SIGN?
         BE    MAIN0080                          YES
         CLI   SPF_PREFIX,C'_'                   UNDERSCORE?
         BE    MAIN0080                          YES
         CLI   SPF_PREFIX,C'A'                   ALPHA?
         BL    ERR0080                           NO
         CLI   SPF_PREFIX,C'Z'                   ALPHA?
         BH    ERR0080                           NO
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
MAIN0080 DS    0H
         TM    DXD_FLAGS,$ERROR                  ERRORS PENDING?
         BO    ERR0100                           YES
         MVC   DISCOMM_PREFIX,SPF_PREFIX         SET PREFIX
         LA    R1,4                              MAX LENGTH
         LA    R2,DISCOMM_PREFIX+3               END OF PREFIX
MAIN0090 DS    0H
         CLI   0(R2),C' '                        END OF PREFIX?
         BNE   MAIN0100                          YES
         BCTR  R2,0
         BCT   R1,MAIN0090
MAIN0100 DS    0H
         STH   R1,DISCOMM_PREFIX_LENGTH          SET PREFIX LENGTH
         ITRACE ID=PREFIX,                                             +
               DATA1=DISCOMM_PREFIX,                                   +
               RDATA2=R1
         MVC   DISCOMM_OUTPUT_DSN,SPF_OUTPUT_DSN
         MVC   DISCOMM_OUTPUT_MEMBER,SPF_OUTPUT_MEMBER
         B     EXIT0000
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
MAIN0200 DS    0H
         ITRACE ID=ENDING
         CLC   SPF_OUTPUT_DSN,DISCOMM_OUTPUT_DSN
         BNE   MAIN0210
         CLC   SPF_OUTPUT_MEMBER,DISCOMM_OUTPUT_MEMBER
         BNE   MAIN0210
         CLC   SPF_PREFIX,DISCOMM_PREFIX
         BE    EXIT0000
MAIN0210 DS    0H
         ITRACE ID=NOT_UPDT
         MVC   SPF_MSG_1(MSG1_L),MSG1
         B     EXIT0000
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
ERR0010  DS    0H
         MVI   COMM_MSG_ID+1,1                   OUTPUT DSN IS BLANK
         B     ERR0200
ERR0020  DS    0H
         MVI   COMM_MSG_ID+1,2                   DSINFO FAILED
         B     ERR0200
ERR0030  DS    0H
         MVI   COMM_MSG_ID+1,3                   DSORG NOT PS OR PO
         B     ERR0200
ERR0040  DS    0H
         MVI   COMM_MSG_ID+1,4                   MEMBER NAME WITH PS
         B     ERR0200
ERR0050  DS    0H
         MVI   COMM_MSG_ID+1,5                   NO MEMBER NAME WITH PO
         B     ERR0200
ERR0060  DS    0H
         MVI   COMM_MSG_ID+1,6                   RECFM NOT F OR FB
         B     ERR0200
ERR0070  DS    0H
         MVI   COMM_MSG_ID+1,7                   LRECL NOT 80
         B     ERR0200
ERR0080  DS    0H
         MVI   COMM_MSG_ID+1,8                   INVALID LABEL PREFIX
         B     ERR0200
ERR0100  DS    0H
         MVI   COMM_MSG_ID+1,10                  ERRORS PENDING
ERR0200  DS    0H
         OI    DXD_FLAGS,$ERROR                  SET ERROR FLAG
         MVI   COMM_MSG_ID,0
         MVC   COMM_MSG_CSECT,MODID              SET CSECT NAME
         L     R15,COMM_V_OSMSG
         BALR  R14,R15                           BUILD MESSAGE(S)
ERR0210  DS    0H
         MVC   SPF_MSG_1,COMM_MSG_1              COPY MESSAGE(S)
         MVC   SPF_MSG_2,COMM_MSG_2
         MVC   SPF_MSG_3,COMM_MSG_3
         MVC   SPF_MSG_4,COMM_MSG_4
         MVC   SPF_MSG_5,COMM_MSG_5
         B     MAIN0010
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
EXIT0000 DS    0H
         TM    DXD_FLAGS,$ERROR                  ERROR FLAG ON?
         BO    ERR0100                           YES..
         ITRACE ID=EXIT
         L     R13,4(R13)                        RESTORE REGISTER 13    ASE01670
         L     R14,12(,R13)                      RESTORE REGISTERS      ASE01680
         LR    R15,R2                            SET RC
         LM    R0,R12,20(R13)                    RESTORE REGISTERS      ASE01680
         BR    R14                               RETURN TO CALLER       ASE01700

*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
DXD_START     DC      Q(DXDOPTS)

H80           DC      H'80'

PANEL_NAME    DC      CL8'DISOPTS '

MSG1          DC      C'DISOPTS01W  Last updates discarded'
MSG1_L        EQU     *-MSG1
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
DXDOPTS       DSECT
              COPY    DXDPREF

DXD_FLAGS     DS      X
$ERROR        EQU     X'80'

              DSINFREQ DSECT=NO
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              DISCOMM  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              DISASMDA
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
FORMAT1       DSECT
              IECSDSL1 1
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              BPXYSTAT DSECT=YES,LIST=NO
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              OSSPFD   TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              COPY     REGEQU
              END      DISOPTS
