*--------------------------------------------------------------------*
*                                                                    *
*  Module name: DISESD                                               *
*                                                                    *
*  Function:                                                         *
*   Obtain the requested LMOD'S ESD info                             *
*                                                                    *
*   DISBIND has already established a dialog                         *
*                                                                    *
*                                                                    *
*--------------------------------------------------------------------*
DISESD   CSECT
DISESD   AMODE 31
DISESD   RMODE ANY
         USING DISESD,R15
         B     MOD0000                        SKIP EYECATCHER
MODID    DC    CL8'DISESD'
         DC    C'&SYSDATE'
         DC    C'&SYSTIME'
MOD0000  DS    0H
         DROP  R15
         STM   R14,R12,12(R13)                SAVE REGS
         LR    R10,R15                        SET BASE REG
         USING OSCOMM,R12
         USING SESSION,R11
         USING DISESD,R10
         USING DISCOMM,R9
         L     R15,SESS_DXD_ADDR
         A     R15,DXD_START                  PLUS DISP
         ST    R13,4(,R15)                    CHAIN
         ST    R15,8(,R13)                    CHAIN
         LR    R13,R15                        SET SAVE AREA ADDRESS
         USING DXDESD,R13
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY
* ------------------------------------------------------------------- *
*        OBTAIN AND INITIALIZE ESD BUFFER                             *
* ------------------------------------------------------------------- *
         ITRACE ID=GET_SECT
         IEWBUFF FUNC=GETBUF,                                          +
               PREFIX=IBMESD,                                          +
               TYPE=ESD
         ST    R8,DXD_BUFFER
         ITRACE ID=INIT_ESD,                                           +
               RDATA1=R8,                                              +
               RDATA2=R7
         IEWBUFF FUNC=INITBUF,                                         +
               PREFIX=IBMESD,                                          +
               TYPE=ESD
         XC    DXD_CURSOR,DXD_CURSOR
*--------------------------------------------------------------------*
*                                                                    *
*--------------------------------------------------------------------*
ESD0000  DS    0H
         ITRACE ID=GETD_ESD,                                           +
               DATA1=(DISCOMM_CSECT_NAME_LENGTH,2),                    +
               DATA2=DISCOMM_CSECT
         L     R8,DXD_BUFFER
         LA    R7,IBMESD_ESDH_END
         IEWBIND FUNC=GETD,                                            +
               VERSION=5,                                              +
               WORKMOD=DISCOMM_DIALOG_WORKMOD,                         +
               CLASS=B_ESD,                                            +
               SECTION=DISCOMM_CSECT_NAME_LENGTH,                      +
               AREA=IBMESD_IEWBESD,                                    +
               CURSOR=DXD_CURSOR,                                      +
               COUNT=DXD_ESD_RETURNED,                                 +
               RETCODE=DXD_RETURN_CODE,                                +
               RSNCODE=DXD_REASON_CODE,                                +
               MF=(E,DXD_IEWBIND,COMPLETE)
         ITRACE ID=GETD_RC,                                            +
               DATA1=(DXD_RETURN_CODE,4),                              +
               DATA2=(DXD_REASON_CODE,4)
         CLC   DXD_RETURN_CODE,F8
         BH    ERR0010
         BE    ERR0020                   NO DATA
         CLC   DXD_RETURN_CODE,F4
         BL    ESD0020
         CLC   DXD_REASON_CODE,X_83000800
         BE    ESD0020
         CLC   DXD_REASON_CODE,X_83000801
         BE    EXIT0000
ESD0020  DS    0H
         ICM   R6,15,DXD_ESD_RETURNED
         LA    R7,IBMESD_ESDH_END
         USING IBMESD_ESD_ENTRY,R7
ESD0030  DS    0H
         ITRACE ID=ESD,                                                +
               RDATA1=R7,                                              +
               RDATA2=R6
         GETMAIN RU,                                                   +
               LV=ESD_DSECT_L,                                         +
               LOC=ANY
         LR    R5,R1
         USING ESD_DSECT,R5
         L     R1,DISCOMM_ESD_NBR
         LA    R1,1(,R1)
         ST    R1,DISCOMM_ESD_NBR
         MVC   ESD_EYE,ESD_ID
         MVC   ESD_NAME,COMM_BLANKS
         MVC   ESD_RESIDENT,COMM_BLANKS
         MVC   ESD_TARGET,COMM_BLANKS
         SR    R1,R1
         ICM   R1,3,IBMESD_ESD_NAME_CHARS
         BZ    ESD0050
         L     R2,IBMESD_ESD_NAME_PTR
         C     R1,F8
         BNH   ESD0040
         L     R1,F8
ESD0040  DS    0H
         BCTR  R1,0
         EX    R1,NAME_MVC
ESD0050  DS    0H
         MVC   ESD_TYPE,IBMESD_ESD_TYPE
         MVC   ESD_TYPE_QUAL,IBMESD_ESD_TYPE_QUAL
         SR    R1,R1
         ICM   R1,3,IBMESD_ESD_RESIDENT_CHARS
         BZ    ESD0070
         L     R2,IBMESD_ESD_RESIDENT_PTR
         C     R1,F8
         BNH   ESD0060
         L     R1,F8
ESD0060  DS    0H
         BCTR  R1,0
         EX    R1,RES_MVC
ESD0070  DS    0H
         ICM   R1,3,IBMESD_ESD_TARGET_CHARS
         BZ    ESD0090
         C     R1,F8
         BNH   ESD0080
         L     R1,F8
ESD0080  DS    0H
         BCTR  R1,0
         L     R2,IBMESD_ESD_TARGET_PTR
         EX    R1,TARG_MVC
ESD0090  DS    0H
         MVC   ESD_LENGTH,IBMESD_ESD_LENG
         MVC   ESD_ELEM_OFFSET,IBMESD_ESD_ELEM_OFFSET
         MVC   ESD_CLASS_OFFSET,IBMESD_ESD_CLASS_OFFSET
         CLC   ESD_NAME,B_TEXT
         BNE   ESD0100
         MVC   DISCOMM_CSECT_LENGTH,IBMESD_ESD_LENG
         ITRACE ID=LENGTH,                                             +
               DATA1=(DISCOMM_CSECT_LENGTH,4)
         UNPK  COMM_INFO_01(9),DISCOMM_CSECT_LENGTH(5)
         TR    COMM_INFO_01(8),COMM_HEXCHAR
         L     R1,COMM_OSSPFD
         USING OSSPFD,R1
         MVC   SPF_LENGTH,COMM_INFO_01
ESD0100  DS    0H
         CLC   DISCOMM_CSECT,ESD_NAME
         BNE   ESD0110
         MVC   DISCOMM_CSECT_EP,ESD_ELEM_OFFSET
ESD0110  DS    0H
         ICM   R1,15,DISCOMM_ESD_NEXT
         BZ    ESD0120
PREV     USING ESD_DSECT,R1
         ST    R5,PREV.ESD_PREV
         DROP  PREV
ESD0120  DS    0H
         MVC   ESD_NEXT,DISCOMM_ESD_NEXT
         ST    R5,DISCOMM_ESD_NEXT
         LA    R7,IBMESD_ESD_END
         BCT   R6,ESD0030
         CLC   DXD_REASON_CODE,X_83000800
         BNE   ESD0000
*--------------------------------------------------------------------*
*                                                                    *
*--------------------------------------------------------------------*
         L     R8,DXD_BUFFER
         IEWBUFF FUNC=FREEBUF,                                         +
               PREFIX=IBMESD,                                          +
               TYPE=ESD
         B     EXIT0000
*--------------------------------------------------------------------*
*                                                                    *
*--------------------------------------------------------------------*
NAME_MVC MVC   ESD_NAME(0),0(R2)
RES_MVC  MVC   ESD_RESIDENT(0),0(R2)
TARG_MVC MVC   ESD_TARGET(0),0(R2)
*--------------------------------------------------------------------*
*                                                                    *
*--------------------------------------------------------------------*
ERR0010  DS    0H
         DC    H'1'
ERR0020  DS    0H
         DC    H'2'
*--------------------------------------------------------------------*
*                                                                    *
*--------------------------------------------------------------------*
EXIT0000 DS    0H
         ITRACE ID=EXIT
         L     R13,4(,R13)                    RESTORE REGISTER 13       ASE01670
         LM    R14,R12,12(R13)                RESTORE OTHER REGISTERS   ASE01680
         SR    R15,R15                        GIVE GOOD RETURN CODE     ASE01690
         BR    R14                            RETURN TO CALLER          ASE01700
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
DXD_START     DC   Q(DXDESD)
F4            DC   F'4'
F8            DC   F'8'
B_ESD         DC   XL2'5',C'B_ESD'
B_TEXT        DC   CL8'B_TEXT'
X_83000800    DC   X'83000800'
X_83000801    DC   X'83000801'
EDIT_WORD     DC   X'40202020202020202120'
ESD_ID        DC   CL8'ESD'

         IEWBUFF FUNC=MAPBUF,                                          +
               TYPE=ESD,                                               +
               PREFIX=IBMESD,                                          +
               HEADREG=R8,                                             +
               ENTRYREG=R7,                                            +
               SIZE=50,                                                +
               VERSION=5

              IEWBIND MF=(L,IEWBIND_I)
IEWBIND_L     EQU  *-IEWBIND_I

*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
DXDESD           DSECT
                 COPY DXDPREF
DXD_RETURN_CODE  DS   F
DXD_REASON_CODE  DS   F

DXD_BUFFER       DS   A

DXD_CURSOR       DS   F
DXD_ESD_RETURNED DS   F

DXD_IEWBIND      DS   0F,(IEWBIND_L)X
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 DISCOMM  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 DISASMDA
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 BPXYSTAT DSECT=YES,LIST=NO
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COPY     OPERAND
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 OSSPFD   TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COPY     REGEQU
         END  DISESD
