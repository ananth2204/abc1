* ------------------------------------------------------------------- *
*                                                                     *
*  MODULE NAME: DISSLIST                                              *
*                                                                     *
*  FUNCTION:                                                          *
*    Display/allow definition of assembler SYSLIB data sets.          *
*                                                                     *
* ------------------------------------------------------------------- *
DISSLIST CSECT
DISSLIST AMODE 31
DISSLIST RMODE ANY
         USING DISSLIST,R15
         B     MOD0000                           SKIP EYECATCHER
MODID    DC    CL8'DISSLIST'
         DC    C'&SYSDATE'
         DC    C'&SYSTIME'
MOD0000  DS    0H
         DROP  R15
         STM   R14,R12,12(R13)                   SAVE REGS
         LR    R10,R15                           SET BASE REG
         USING OSCOMM,R12
         USING SESSION,R11
         USING DISSLIST,R10
         USING DISCOMM,R9
         L     R15,SESS_DXD_ADDR                 WORK AREA
         A     R15,DXD_START                     PLUS DISP
         ST    R13,4(,R15)
         ST    R15,8(,R13)
         LR    R13,R15
         USING DXDSLIST,R13
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY
         L     R8,COMM_OSSPFD
         USING OSSPFD,R8
         USING SYSLIB_DSECT,R5
         USING IHADCB,SYSLIB_DSECT
         USING IECSDSL1,DSINFO_DSCB
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         MVI   DXD_FLAGS,0
* ------------------------------------------------------------------- *
*                                                                     *
*        If R1 is zero display existing KSDS list                     *
*        If R1 not zero, display list passed in R1                    *
*                                                                     *
* ------------------------------------------------------------------- *
         ST    R1,DXD_SYSLIB_NEXT
         ST    R1,DXD_FIRST_DISPLAYED
         LTR   R1,R1                             R1 ZERO?
         BNZ   INIT0020                          NO
* ------------------------------------------------------------------- *
*                                                                     *
*        In order to allow the user to "cancel" changes,              *
*        first "copy" the existing SYSLIB chain.                      *
*                                                                     *
* ------------------------------------------------------------------- *
         XC    DXD_SYSLIB_NEXT,DXD_SYSLIB_NEXT
         XC    DXD_FIRST_DISPLAYED,DXD_FIRST_DISPLAYED
         ITRACE ID=COPY
         LA    R2,DXD_SYSLIB_PREV
PREV     USING SYSLIB_DSECT,R2
         ICM   R5,15,DISCOMM_SYSLIB_NEXT         FIRST LIB
         BZ    INIT0020
INIT0010 DS    0H
         GETMAIN RU,                             GETMAIN NEW LIB BLOCK +
               LV=SYSLIB_DSECT_L,                                      +
               LOC=ANY
NEW      USING SYSLIB_DSECT,R1
         MVC   NEW.SYSLIB_DSECT(SYSLIB_DSECT_L),SYSLIB_DSECT
         ST    R1,PREV.SYSLIB_NEXT
         LR    R2,R1
         ICM   R5,15,SYSLIB_NEXT                 ANOTHER LIB?
         BNZ   INIT0010                          YES.. PROCESS IT
         MVC   DXD_FIRST_DISPLAYED,DXD_SYSLIB_NEXT
         DROP  NEW,PREV
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
INIT0020 DS    0H
         L     R0,SPF_VDEPTH                     NBR OF LINES
         SLL   R0,2                              MULTIPLY BY 4
         ST    R0,DXD_SYSLIB_TABLE_SIZE
         GETMAIN RU,                                                   +
               LV=(0),                                                 +
               LOC=ANY
         ST    R1,DXD_SYSLIB_TABLE               SAVE ADDRESS
         ITRACE ID=A_TABLE,                                            +
               RDATA1=R1
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0010 DS    0H
         L     R0,DISCOMM_VDATA_ADDR
         L     R1,DISCOMM_VDATA_LENGTH
         SR    R14,R14
         SR    R15,R15
         MVCL  R0,R14                            INITIALIZE VDATA
         L     R0,DXD_SYSLIB_TABLE
         L     R1,DXD_SYSLIB_TABLE_SIZE
         SR    R14,R14
         SR    R15,R15
         MVCL  R0,R14                            INIT AREA TABLE
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         L     R6,SPF_VWIDTH                     SCREEN WIDTH
         SH    R6,=Y(MSG01_L)                    MINUS WIDTH IN USE
         SRL   R6,1                              DIVIDE BY 2
         A     R6,DISCOMM_VDATA_ADDR             PLUS VDATA ADDR
         L     R7,SPF_VDEPTH                     NBR OF LINES
MAIN0020 DS    0H
         MVC   0(MSG01_L,R6),MSG01               INITIALIZE
         A     R6,SPF_VWIDTH                     NEXT LINE
         BCT   R7,MAIN0020                       LOOP
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         L     R6,SPF_VWIDTH                     SCREEN WIDTH
         SH    R6,=Y(MSG01_L)                    MINUS WIDTH IN USE
         SRL   R6,1                              DIVIDE BY 2
         A     R6,DISCOMM_VDATA_ADDR             PLUS VDATA ADDR
         USING MSG01,R6
         L     R7,SPF_VDEPTH
MAIN0030 DS    0H
         L     R3,DXD_SYSLIB_TABLE               FIRST 'SLOT'
         XC    DXD_NBR_DISPLAYED,DXD_NBR_DISPLAYED
         ICM   R5,15,DXD_FIRST_DISPLAYED         FIRST TO DISPLAY
         BZ    MAIN0060                          NOTHING DEFINED YET
MAIN0040 DS    0H
         ST    R5,DXD_LAST_DISPLAYED             SAVE ADDR
         ST    R5,0(R3)                          SAVE ADDR
         MVC   MSG01_DSN,SYSLIB_DSNAME           COPY DATA SET NAME
         LA    R1,1                              CONSTANT 1
         AH    R1,DXD_NBR_DISPLAYED              PLUS TOTAL SO FAR
         STH   R1,DXD_NBR_DISPLAYED              SAVE TOTAL
         LA    R3,4(,R3)                         NEXT IN ADDR TABLE
         A     R6,SPF_VWIDTH                     NEXT LINE
         BCT   R7,MAIN0050
         B     MAIN0060
MAIN0050 DS    0H
         ICM   R5,15,SYSLIB_NEXT
         BNZ   MAIN0040
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0060 DS    0H
         ITRACE ID=DISPLAY
         MVC   SESS_DISP_PANEL,PANEL_DISSLIST    SET PANEL NAME
         MVC   SESS_DISP_VDATA_ADDR,DISCOMM_VDATA_ADDR
         MVC   SESS_DISP_VDATA_LENGTH,DISCOMM_VDATA_LENGTH
         XC    SESS_DISP_CURSOR,SESS_DISP_CURSOR
         L     R15,COMM_V_OSDISP                 DISP ENTRY POINT
         BALR  R14,R15                           DISPLAY
         ITRACE ID=DISP_RC,                                            +
               RDATA1=R15,                                             +
               DATA2=SPF_ZVERB
         LTR   R2,R15                            RC = 0?
         BNZ   MAIN0500                          NO
         CLC   CANCEL,SPF_ZCMD                   CANCEL?
         BE    MAIN1000                          YES
         CLC   CAN,SPF_ZCMD                      CAN(CEL)?
         BE    MAIN1000                          YES
* ------------------------------------------------------------------- *
*                                                                     *
*        Verify all DSNs                                              *
*          cataloged                                                  *
*          are PDSES                                                  *
*                                                                     *
* ------------------------------------------------------------------- *
         L     R6,SPF_VWIDTH                     SCREEN WIDTH
         SH    R6,=Y(MSG01_L)                    MINUS WIDTH IN USE
         SRL   R6,1                              DIVIDE BY 2
         A     R6,DISCOMM_VDATA_ADDR             PLUS VDATA ADDR
         L     R7,SPF_VDEPTH                     NBR OF LINES
         NI    DXD_FLAGS,255-$DXD_ERROR          RESET FLAG
MAIN0070 DS    0H
         MVC   MSG01_MSG,COMM_BLANKS             RESET MSG
         OC    MSG01_DSN,COMM_BLANKS             TRANSLATE TO UPPER
         CLC   MSG01_DSN,COMM_BLANKS             BLANK?
         BE    MAIN0090                          YES
         MVC   DSINFO_DSN,MSG01_DSN              COPY DSN
         LA    R1,DSINFREQ
         L     R15,V_DISDSINF                    OSDSINFO ENTRY POINT
         BALR  R14,R15
         ITRACE ID=DINFO_RC,                                           +
               RDATA1=R15
         LTR   R15,R15                           ERROR?
         BZ    MAIN0080                          NO
         MVC   MSG01_MSG,COMM_MSG_1              COPY MESSAGE
         OI    DXD_FLAGS,$DXD_ERROR              SET ERROR FLAG
         B     MAIN0090
MAIN0080 DS    0H
         ITRACE ID=DS_TYPE,                                            +
               DATA1=DSINFO_DATASET_TYPE
         TM    DS1DSORG,DS1DSGPO                 PDS?
         BO    MAIN0090                          YES
         OI    DXD_FLAGS,$DXD_ERROR              SET ERROR FLAG
         MVC   MSG01_MSG(ERR01_L),ERR01
MAIN0090 DS    0H
         A     R6,SPF_VWIDTH                     NEXT LINE
         BCT   R7,MAIN0070
         TM    DXD_FLAGS,$DXD_ERROR              ANY ERRORS?
         BO    MAIN0060                          DISPLAY W/MSG(S)
* ------------------------------------------------------------------- *
*                                                                     *
*        Determine if any libs were                                   *
*          updated                                                    *
*          removed                                                    *
*          added                                                      *
*                                                                     *
* ------------------------------------------------------------------- *
         L     R6,SPF_VWIDTH                     SCREEN WIDTH
         SH    R6,=Y(MSG01_L)                    MINUS WIDTH IN USE
         SRL   R6,1                              DIVIDE BY 2
         A     R6,DISCOMM_VDATA_ADDR             PLUS VDATA ADDR
         L     R7,SPF_VDEPTH                     NBR OF LINES
         L     R3,DXD_SYSLIB_TABLE               FIRST LIB
MAIN0130 DS    0H
         ITRACE ID=MAIN0130
         L     R5,0(,R3)                         LIB BLOCK IF USED
         OC    MSG01_DSN,COMM_BLANKS             'TRANSLATE'
         CLC   MSG01_DSN,COMM_BLANKS             BLANK?
         BE    MAIN0180                          YES
         LTR   R5,R5                             LIB BLOCK EXIST?
         BZ    MAIN0150                          NO
         CLC   MSG01_DSN,SYSLIB_DSNAME           SAME DSN?
         BE    MAIN0220                          YES
MAIN0140 DS    0H
         ITRACE ID=UPDTLIB
         OI    DXD_FLAGS,$DXD_UPDATE             LIB HAS BEEN UPDATED
         MVC   SYSLIB_DSNAME,MSG01_DSN           CHANGE DSN
         B     MAIN0220
MAIN0150 DS    0H
         ITRACE ID=NEW_LIB
         OI    DXD_FLAGS,$DXD_UPDATE
         GETMAIN RU,                                                   +
               LV=SYSLIB_DSECT_L,                                      +
               LOC=ANY
NEW      USING SYSLIB_DSECT,R1
         XC    NEW.SYSLIB_DSECT(SYSLIB_DSECT_L),NEW.SYSLIB_DSECT
         MVC   NEW.SYSLIB_EYE,SYSLIB_ID          IDENTIFY BLOCK
         MVC   NEW.SYSLIB_DSNAME,MSG01_DSN       SET DSN
         LA    R5,DXD_SYSLIB_PREV                FIRST LIB
MAIN0160 DS    0H
         ICM   R2,15,SYSLIB_NEXT                 ANOTHER ON CHAIN?
         BZ    MAIN0170                          YES
         LR    R5,R2                             COPY ADDRESS
         B     MAIN0160
MAIN0170 DS    0H
         ST    R1,SYSLIB_NEXT                    SET 'NEXT' IN PREVIOUS
         ST    R5,NEW.SYSLIB_PREV                SET PREV IN NEW
         OC    DXD_FIRST_DISPLAYED,DXD_FIRST_DISPLAYED
         BNZ   MAIN0220                          BLOCK PRESENT
         ST    R1,DXD_FIRST_DISPLAYED            SET FIRST ON CHAIN
         B     MAIN0220
         DROP  NEW
MAIN0180 DS    0H
         LTR   R5,R5                             LIB BLOCK EXIST?
         BZ    MAIN0220                          NO
         OI    DXD_FLAGS,$DXD_UPDATE             A LIB WAS UPDATED
         ITRACE ID=DEL_LIB,                      DELETING A LIB BLOCK  +
               RDATA1=R5
         ICM   R1,15,SYSLIB_NEXT                 NEXT LIB ON CHAIN
         BZ    MAIN0190
NEXT     USING SYSLIB_DSECT,R1
         MVC   NEXT.SYSLIB_PREV,SYSLIB_PREV      SET PREV IN NEXT
         DROP  NEXT
MAIN0190 DS    0H
         ICM   R1,15,SYSLIB_PREV                 PREVIOUS ON CHAIN
         BZ    MAIN0200                          NO PREVIOUS BLOCK
PREV     USING SYSLIB_DSECT,R1
         MVC   PREV.SYSLIB_NEXT,SYSLIB_NEXT      SET NEXT IN PREVIOUS
MAIN0200 DS    0H
         C     R5,DXD_FIRST_DISPLAYED            FIRST LIB DISPLAYED?
         BNE   MAIN0210                          NO
         MVC   DXD_FIRST_DISPLAYED,SYSLIB_NEXT   CHANGE TO NEXT BLOCK
MAIN0210 DS    0H
         FREEMAIN RU,                            FREEMAIN LIB BLOCK    +
               A=(R5),                                                 +
               LV=SYSLIB_DSECT_L
         XC    0(4,R3),0(R3)                     CLEAR ADDR IN TABLE
MAIN0220 DS    0H
         ITRACE ID=NEXT
         A     R6,SPF_VWIDTH                     NEXT LINE
         LA    R3,4(,R3)                         NEXT IN TABLE
         BCT   R7,MAIN0130                       LOOP
         TM    DXD_FLAGS,$DXD_UPDATE             ANY UPDATES?
         BO    MAIN0010                          YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         CLI   SPF_ZVERB,C'B'                    BOTTOM?
         BE    MAIN0300
         CLI   SPF_ZVERB,C'D'                    DOWN?
         BE    MAIN0320
         CLI   SPF_ZVERB,C'T'                    TOP?
         BE    MAIN0330
         CLI   SPF_ZVERB,C'U'                    UP?
         BE    MAIN0340
         B     MAIN0050
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0300 DS    0H
         ITRACE ID=BOTTOM
         ICM   R1,15,DXD_SYSLIB_NEXT             FIRST ON CHAIN
TEMP     USING SYSLIB_DSECT,R1
         BZ    EXIT0000                          SHOULD NOT HAPPEN!
MAIN0310 DS    0H
         LR    R5,R1                             SAVE THIS ADDR
         ICM   R1,15,TEMP.SYSLIB_NEXT            NEXT LIB
         BNZ   MAIN0310
         B     MAIN0010                          BUILD DISPLAY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0320 DS    0H
         ITRACE ID=DOWN
         CLI   SPF_ZSCROLLA,C'M'                 MAX?
         BE    MAIN0300                          YES
         L     R5,DXD_LAST_DISPLAYED             LAST ITEM DISPLAYED
         B     MAIN0010
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0330 DS    0H
         ITRACE ID=TOP
         L     R5,DXD_SYSLIB_NEXT                FIRST DATA ITEM
         B     MAIN0010
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0340 DS    0H
         ITRACE ID=UP
         CLI   SPF_ZSCROLLA,C'M'                 MAX?
         BE    MAIN0330                          YES
         ICM   R1,15,DXD_FIRST_DISPLAYED         FIRST ON DISPLAY
         BZ    EXIT0000                          SHOULD NOT HAPPEN!
         L     R7,SPF_ZSCROLLN                   LINES ON DISPLAY
MAIN0350 DS    0H
         LR    R5,R1                             COPY ADDR
         ICM   R1,15,TEMP.SYSLIB_PREV            PREVIOUS LIB
         BZ    MAIN0010
         BCT   R7,MAIN0350                       LOOP
         B     MAIN0010
         DROP  TEMP
* ------------------------------------------------------------------- *
*                                                                     *
*      Replace the existing LIB chain with the updated chain          *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0500 DS    0H
         ITRACE ID=TERM
         TM    DXD_FLAGS,$DXD_ERROR              ERROR FLAG ON?
         BO    MAIN0700
* ------------------------------------------------------------------- *
*        Free current chain                                           *
* ------------------------------------------------------------------- *
MAIN0580 DS    0H
         ITRACE ID=FREECURR
         ICM   R5,15,DISCOMM_SYSLIB_NEXT
         BZ    MAIN0600
MAIN0590 DS    0H
         LR    R1,R5
         ICM   R5,15,SYSLIB_NEXT
         FREEMAIN RU,                                                  +
               A=(1),                                                  +
               LV=SYSLIB_DSECT_L
         LTR   R5,R5
         BNZ   MAIN0590
* ------------------------------------------------------------------- *
*        Replace old chain with new                                   *
* ------------------------------------------------------------------- *
MAIN0600 DS    0H
         ITRACE ID=SWITCH,                                             +
               DATA1=DISCOMM_SYSLIB_NEXT,                              +
               DATA2=DXD_SYSLIB_NEXT
         OI    DISCOMM_FLAGS,$DISCOMM_ASSEMBLE_SOURCE
         MVC   DISCOMM_SYSLIB_NEXT,DXD_SYSLIB_NEXT
         ICM   R5,15,DISCOMM_SYSLIB_NEXT         1ST SYSLIB
         BZ    EXIT0000                          NO SYSLIB'S
         XC    SYSLIB_PREV,SYSLIB_PREV           NO PREV BLOCK
         B     EXIT0000
* ------------------------------------------------------------------- *
*        User has requested exit with error(s) pending                *
* ------------------------------------------------------------------- *
MAIN0700 DS    0H
         ITRACE ID=PURGE
         MVI   COMM_MSG_ID,0
         MVI   COMM_MSG_ID+1,1
         BAL   R4,MSG0000
         MVC   SPF_MSG_1,COMM_MSG_1
         MVC   SPF_MSG_2,COMM_MSG_2
         MVC   SPF_MSG_3,COMM_MSG_3
         MVC   SPF_MSG_4,COMM_MSG_4
         MVC   SPF_MSG_5,COMM_MSG_5
* ------------------------------------------------------------------- *
*                                                                     *
*        User has requested all changes to be cancelled.              *
*                                                                     *
*        Free any blocks on new chain                                 *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN1000 DS    0H
         ITRACE ID=CANCEL
         MVC   SPF_ZCMD,COMM_BLANKS              RESET COMMAND
         ICM   R5,15,DXD_SYSLIB_NEXT             FIRST ON NEW CHAIN
         BZ    EXIT0000
         XC    DXD_SYSLIB_NEXT,DXD_SYSLIB_NEXT   CLEAR FOR NEXT TIME
MAIN1010 DS    0H
         LR    R1,R5                             COPY ADDRESS
         ICM   R5,15,SYSLIB_NEXT                 NEXT LIB
         ITRACE ID=FREELIB,                                            +
               RDATA1=R5,                                              +
               DATA2=SYSLIB_DDNAME
         FREEMAIN RU,                            FREEMAIN THE BLOCK    +
               A=(1),                                                  +
               LV=SYSLIB_DSECT_L
         LTR   R5,R5                             ANOTHER ON CHAIN?
         BNZ   MAIN1010                          YES
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MSG0000  DS    0H
         ITRACE ID=MSG,                                                +
               DATA1=COMM_MSG_ID
         CLC   SPF_MSG_1,COMM_BLANKS             ALREADY HAVE A MSG?
         BNER  R4                                YES
         MVC   COMM_MSG_CSECT,MODID
         L     R15,COMM_V_OSMSG
         BALR  R14,R15
         MVC   SPF_MSG_1,COMM_MSG_1
         MVC   SPF_MSG_2,COMM_MSG_2
         MVC   SPF_MSG_3,COMM_MSG_3
         MVC   SPF_MSG_4,COMM_MSG_4
         MVC   SPF_MSG_5,COMM_MSG_5
         BR    R4
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=EXIT
         L     R0,DXD_SYSLIB_TABLE_SIZE          ADDR TABLE SIZE
         L     R1,DXD_SYSLIB_TABLE               ADDR TABLE ADDR
         FREEMAIN RU,                            FREEMAIN ADDR TABLE   +
               A=(1),                                                  +
               LV=(0)
         L     R13,4(,R13)                       RESTORE REGISTER 13    ASE01670
         L     R14,12(,R13)                      RESTORE REGISTERS      ASE01680
         LR    R15,R2                            SET RC
         LM    R0,R12,20(R13)                    RESTORE REGISTERS      ASE01680
         BR    R14                               RETURN TO CALLER       ASE01700
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXD_START     DC   Q(DXDSLIST)
V_DISDSINF    DC   V(DISDSINF)

SYSLIB_ID     DC   CL8'SYSLIB'
CAN           DC   C'CAN '
CANCEL        DC   C'CANCEL '
PANEL_DISSLIST DC  CL8'DISSLIST'
PANEL_DIS0000  DC  CL8'DIS0000'

ERR01         DC   C'Not a PDS'
ERR01_L       EQU  *-ERR01

              LTORG

MSG01         DS   0C
              DC   AL1($SCREEN_ATTR_INPUT)
MSG01_DSN     DC   CL44' '
              DC   AL1($SCREEN_ATTR_HIGH_YELLOW)
MSG01_MSG     DC   CL30' '
MSG01_L       EQU  *-MSG01

* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDSLIST              DSECT
                      COPY DXDPREF

DXD_SYSLIB_PREV       DS   A        DUMMY FOR 'PREV' ON CHAIN
DXD_SYSLIB_NEXT       DS   A        FIRST DATA ITEM ON CHAIN

DXD_FIRST_DISPLAYED   DS   A        FIRST DATA ITEM DISPLAYED
DXD_LAST_DISPLAYED    DS   A        LAST DATA ITEM DISPLAYED

DXD_SYSLIB_TABLE      DS   A
DXD_SYSLIB_TABLE_SIZE DS   F

DXD_NBR_DISPLAYED     DS   H        NBR OF DATA ITEMS DISPLAYED

DXD_FLAGS             DS   X        FLAGS
$DXD_ERROR            EQU  X'80'    .. SERIOUS ERROR
$DXD_UPDATE           EQU  X'40'    .. AREA(S) UPDATED

                      DSINFREQ      DSECT=NO
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 DISCOMM  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 DISASMDA
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 BPXYSTAT DSECT=YES,LIST=NO
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 OSSPFD   TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 DCBD     DSORG=PS
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
FORMAT_1         DSECT
                 IECSDSL1 1
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COPY     ATTRS
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COPY     REGEQU
              END     DISSLIST
