*--------------------------------------------------------------------*
*                                                                    *
*  Module name: DISELIST                                             *
*                                                                    *
*  Function:                                                         *
*                                                                    *
*   Display ESD items                                                *
*                                                                    *
*--------------------------------------------------------------------*
DISELIST CSECT
DISELIST AMODE 31
DISELIST RMODE ANY
         USING DISELIST,R15
         B     MOD0000                           SKIP EYECATCHER
MODID    DC    CL8'DISELIST'
         DC    C'&SYSDATE'
         DC    C'&SYSTIME'
MOD0000  DS    0H
         DROP  R15
         USING ESD_DSECT,R5
         STM   R14,R12,12(R13)                   SAVE REGS
         LR    R10,R15                           SET BASE REG
         USING OSCOMM,R12
         USING SESSION,R11
         USING DISELIST,R10
         USING DISCOMM,R9
         L     R15,SESS_DXD_ADDR                 WORK AREA
         A     R15,DXD_START                     PLUS DISP
         ST    R13,4(,R15)
         ST    R15,8(,R13)
         LR    R13,R15
         USING DXDELIST,R13
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY
         L     R8,COMM_OSSPFD
         USING OSSPFD,R8
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
MAIN0020 DS    0H
         ICM   R5,15,DISCOMM_ESD_NEXT            1ST ESD
         BZ    MAIN0300                          NO ESD ITEMS
*---------------------------------------------------------------------*
*                                                                     *
*        Build data for ESD display                                   *
*                                                                     *
*---------------------------------------------------------------------*
MAIN0030 DS    0H
         L     R0,DISCOMM_VDATA_ADDR             VDATA ADDR
         L     R1,DISCOMM_VDATA_LENGTH           VDATA LENGTH
         SR    R14,R14
         SR    R15,R15
         MVCL  R0,R14                            INITIALIZE VDATA
         ST    R5,DXD_FIRST_DISPLAYED            SAVE 1ST DISPLAYED
         L     R7,SPF_VDEPTH                     SCREEN DEPTH
         L     R6,DISCOMM_VDATA_ADDR             VDATA ADDR
MAIN0040 DS    0H
         ST    R5,DXD_LAST_DISPLAYED             SAVE LAST ON DISPLAY
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
P        USING MSG01,R6
         MVI   P.MSG01_ATTR,$SCREEN_ATTR_NORMAL
         MVC   P.MSG01_NAME,ESD_NAME
         MVC   P.MSG01_TYPE,ESD_TYPE
         MVC   P.MSG01_TYPE_QUAL,ESD_TYPE_QUAL
         MVC   P.MSG01_RESIDENT,ESD_RESIDENT
         MVC   P.MSG01_TARGET,ESD_TARGET
         L     R0,ESD_LENGTH                     LENGTH
         MVC   P.MSG01_LENGTH,=X'40202020202020202120'
         CVD   R0,COMM_DWORD
         ED    P.MSG01_LENGTH,COMM_DWORD+3       EDIT LENGTH
         UNPK  P.MSG01_LENGTH_H(9),ESD_LENGTH(5)
         TR    P.MSG01_LENGTH_H,COMM_HEXCHAR
         MVI   P.MSG01_LENGTH_H-1,C'('
         MVI   P.MSG01_LENGTH_H+8,C')'
         UNPK  P.MSG01_ELEM_OFFSET(9),ESD_ELEM_OFFSET(5)
         TR    P.MSG01_ELEM_OFFSET(8),COMM_HEXCHAR
         MVI   P.MSG01_ELEM_OFFSET+8,C' '
         UNPK  P.MSG01_CLASS_OFFSET(9),ESD_CLASS_OFFSET(5)
         TR    P.MSG01_CLASS_OFFSET(8),COMM_HEXCHAR
         MVI   P.MSG01_CLASS_OFFSET+8,C' '
         CLC   P.MSG01_TYPE_QUAL,=C'PR'
         BE    MAIN0050
         CLC   P.MSG01_TYPE,=C'ED'
         BNE   MAIN0060
MAIN0050 DS    0H
         MVI   P.MSG01_ATTR,$SCREEN_ATTR_HIGH_YELLOW
MAIN0060 DS    0H
         DROP  P
         A     R6,SPF_VWIDTH                     NEXT LINE
         BCT   R7,MAIN0070
         B     MAIN0080
MAIN0070 DS    0H
         ICM   R5,15,ESD_NEXT                    NEXT ESD ITEM
         BNZ   MAIN0040                          PROCESS THIS ESD ITEM
*---------------------------------------------------------------------*
*                                                                     *
*        Display the ESD items                                        *
*                                                                     *
*---------------------------------------------------------------------*
MAIN0080 DS    0H
         ITRACE ID=DISPLAY
         MVC   SESS_DISP_PANEL,PANEL_DISELIST SET PANEL NAME
         MVC   SESS_DISP_VDATA_ADDR,DISCOMM_VDATA_ADDR
         MVC   SESS_DISP_VDATA_LENGTH,DISCOMM_VDATA_LENGTH
         L     R15,COMM_V_OSDISP
         BALR  R14,R15                           DISPLAY ESD INFO
         ITRACE ID=DISP_RC,                                            +
               RDATA1=R15,                                             +
               DATA2=SPF_ZVERB
         LTR   R15,R15                           RC = 0?
         BNZ   EXIT0000                          NO
*---------------------------------------------------------------------*
*        Process scrolling                                            *
*---------------------------------------------------------------------*
         CLI   SPF_ZVERB,C'B'                    BOTTOM?
         BE    MAIN0100
         CLI   SPF_ZVERB,C'D'                    DOWN?
         BE    MAIN0120
         CLI   SPF_ZVERB,C'T'                    TOP?
         BE    MAIN0130
         CLI   SPF_ZVERB,C'U'                    UP?
         BE    MAIN0140
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
MAIN0100 DS    0H
         ITRACE ID=BOTTOM
         ICM   R5,15,DISCOMM_ESD_NEXT            FIRST ON CHAIN
         BZ    EXIT0000                          SHOULD NOT HAPPEN!
MAIN0110 DS    0H
         ICM   R1,15,ESD_NEXT                    NEXT ON CHAIN?
         BZ    MAIN0030                          NO..
         LR    R5,R1                             COPY ADDR
         B     MAIN0110
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
MAIN0120 DS    0H
         ITRACE ID=DOWN
         L     R5,DXD_LAST_DISPLAYED             LAST ITEM DISPLAYED
         B     MAIN0030
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
MAIN0130 DS    0H
         ITRACE ID=TOP
         L     R5,DISCOMM_ESD_NEXT               FIRST ESD ITEM
         B     MAIN0030
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
MAIN0140 DS    0H
         ITRACE ID=UP
         ICM   R5,15,DXD_FIRST_DISPLAYED         FIRST ON DISPLAY
         BZ    EXIT0000                          SHOULD NOT HAPPEN!
         L     R7,SPF_ZSCROLLN                   LINES ON DISPLAY
MAIN0150 DS    0H
         ICM   R1,15,ESD_PREV                    PREV ON CHAIN?
         BZ    MAIN0030                          NO..
         LR    R5,R1                             COPY ADDR
         BCT   R7,MAIN0150                       LOOP
         B     MAIN0030
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
MAIN0300 DS    0H
         ITRACE ID=NO_ESD
         MVC   SPF_MSG_3(MSG_02_L),MSG_02
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
EXIT0000 DS    0H
         ITRACE ID=EXIT
         L     R13,4(,R13)                       RESTORE REGISTER 13    ASE01670
         L     R14,12(,R13)                      RESTORE REGISTERS      ASE01680
         LR    R15,R2                            SET RC
         LM    R0,R12,20(R13)                    RESTORE REGISTERS      ASE01680
         BR    R14                               RETURN TO CALLER       ASE01700
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
DXD_START           DC   Q(DXDELIST)

PANEL_DISELIST      DC   CL8'DISELIST'

MSG01               DS   0C
MSG01_ATTR          DC   AL1($SCREEN_ATTR_NORMAL)
MSG01_NAME          DC   CL8' '
                    DC   CL1' '
MSG01_TYPE          DC   CL2' '
                    DC   C' '
MSG01_TYPE_QUAL     DC   CL2' '
                    DC   C' '
MSG01_LENGTH        DC   X'40202020202020202120'
                    DC   C' (HEX '
MSG01_LENGTH_H      DC   CL8' '
                    DC   C') '
MSG01_ELEM_OFFSET   DC   CL8' '
                    DC   C'  '
MSG01_CLASS_OFFSET  DC   CL8' '
                    DC   C' '
MSG01_RESIDENT      DC   CL8' '
                    DC   CL2' '
MSG01_TARGET        DC   CL8' '
MSG01_L             EQU  *-MSG01

MSG_02              DC   C'No ESD items to display'
MSG_02_L            EQU  *-MSG_02

*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
DXDELIST              DSECT
                      COPY DXDPREF

DXD_FIRST_DISPLAYED   DS   A
DXD_LAST_DISPLAYED    DS   A
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 DISCOMM  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 DISASMDA
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 BPXYSTAT DSECT=YES,LIST=NO
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 OSSPFD   TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COPY     ATTRS
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COPY     REGEQU
              END     DISELIST
