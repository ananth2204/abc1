* ------------------------------------------------------------------- *
*                                                                     *
*  MODULE NAME: DISULSTL                                              *
*                                                                     *
*  FUNCTION:                                                          *
*    Display LABELs in a DSECT for USING definitions                  *
*                                                                     *
* ------------------------------------------------------------------- *
DISULSTL CSECT
DISULSTL AMODE 31
DISULSTL RMODE ANY
         USING DISULSTL,R15
         B     MOD0000                        SKIP EYECATCHER
MODID    DC    CL8'DISULSTL'
         DC    C'&SYSDATE'
         DC    C'&SYSTIME'
MOD0000  DS    0H
         STM   R14,R12,12(R13)                SAVE REGS
         LR    R10,R15                        SET BASE REG
         DROP  R15
         USING OSCOMM,R12
         USING SESSION,R11
         USING DISULSTL,R10
         USING DISCOMM,R9
         L     R15,SESS_DXD_ADDR              WORK AREA
         A     R15,DXD_START                  PLUS DISP
         ST    R13,4(,R15)
         ST    R15,8(,R13)
         LR    R13,R15
         USING DXDULSTL,R13
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY,                                              +
               DATA1=DISCOMM_USING_KSDS,      .. RELATED KSDS          +
               DATA2=DISCOMM_USING_DSECT      .. RELATED DSECT
         L     R8,COMM_OSSPFD
         USING OSSPFD,R8
         L     R5,DISCOMM_USING_DSECT
         USING DSECT_DSECT,R5
         USING LABEL_DSECT,R4
* ------------------------------------------------------------------- *
*                                                                     *
*                                                                     *
*        Display labels for selected DSECT                            *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
         MVC   DXD_FIRST_LABEL,DSECT_LABEL_NEXT
         MVC   DXD_FIRST_LABEL_DISPLAYED,DSECT_LABEL_NEXT
         XC    DISCOMM_USING_LABEL,DISCOMM_USING_LABEL
MAIN0010 DS    0H
         L     R0,DISCOMM_VDATA_ADDR
         L     R1,DISCOMM_VDATA_LENGTH
         SR    R14,R14
         SR    R15,R15
         MVCL  R0,R14
         L     R6,DISCOMM_VDATA_ADDR
         L     R7,SPF_VDEPTH
         ICM   R4,15,DXD_FIRST_LABEL_DISPLAYED
         BZ    MAIN0210
MAIN0020 DS    0H
         ITRACE ID=LABEL,                                              +
               RDATA1=R4,                                              +
               DATA2=LABEL_NAME
         ST    R4,DXD_LAST_LABEL_DISPLAYED
         MVC   0(LINE01_L,R6),LINE01
         USING LINE01,R6
         UNPK  DXD_WORK(9),LABEL_DISP(5)      UNPACK DISP
         MVI   LINE01_SELECT,C' '
         MVC   LINE01_DISP,DXD_WORK
         TR    LINE01_DISP,COMM_HEXCHAR
         MVC   LINE01_LABEL,LABEL_NAME
         A     R6,SPF_VWIDTH                  NEXT LINE
         BCT   R7,MAIN0030
         B     MAIN0040
MAIN0030 DS    0H
         ICM   R4,15,LABEL_NEXT               NEXT LABEL
         BNZ   MAIN0020
* ------------------------------------------------------------------- *
*                                                                     *
*        Display labels                                               *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0040 DS    0H
         ITRACE ID=DISPLAY
         MVC   SESS_DISP_PANEL,PANEL_NAME     SET PANEL NAME
         MVC   SESS_DISP_VDATA_ADDR,DISCOMM_VDATA_ADDR
         MVC   SESS_DISP_VDATA_LENGTH,DISCOMM_VDATA_LENGTH
         XC    SESS_DISP_CURSOR,SESS_DISP_CURSOR
         L     R15,COMM_V_OSDISP              DISP ENTRY POINT
         BALR  R14,R15                        DISPLAY
         ITRACE ID=DISP_RC,                                            +
               RDATA1=R15,                                             +
               DATA2=SPF_ZVERB
         LTR   R2,R15                         RC = 0?
         BNZ   MAIN0220                       NO
         CLC   ABEND,SPF_ZCMD                 ABEND?
         BE    ABEND000                       YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         L     R6,DISCOMM_VDATA_ADDR          VDATA ADDR
         L     R7,SPF_VDEPTH
         L     R4,DXD_FIRST_LABEL_DISPLAYED
MAIN0050 DS    0H
         CLI   LINE01_SELECT,C' '             SELECTED?
         BNE   MAIN0200                       YES
         A     R6,SPF_VWIDTH                  NEXT LINE
         BCT   R7,MAIN0060
         B     MAIN0070
MAIN0060 DS    0H
         ICM   R4,15,LABEL_NEXT               NEXT LABEL
         BNZ   MAIN0050                       LOOP
* ------------------------------------------------------------------- *
*        Nothing selected, check for scrolling                        *
* ------------------------------------------------------------------- *
MAIN0070 DS    0H
         ITRACE ID=SCROLL,                                             +
               DATA1=SPF_ZVERB,                                        +
               DATA2=SPF_ZSCROLLA
         CLI   SPF_ZVERB,C'B'                 BOTTOM?
         BE    MAIN0080                       YES
         CLI   SPF_ZVERB,C'D'                 DOWN?
         BE    MAIN0100
         CLI   SPF_ZVERB,C'T'                 TOP?
         BE    MAIN0120
         CLI   SPF_ZVERB,C'U'                 UP?
         BE    MAIN0130
         B     MAIN0010
MAIN0080 DS    0H
         ITRACE ID=BOTTOM
         L     R4,DXD_FIRST_LABEL             FIRST LABEL
MAIN0090 DS    0H
         ST    R4,DXD_FIRST_LABEL_DISPLAYED   SAVE LABEL BLOCK ADDR
         ICM   R4,15,LABEL_NEXT               ANOTHER LABEL?
         BNZ   MAIN0090                       YES
         B     MAIN0010
MAIN0100 DS    0H
         ITRACE ID=DOWN
         CLI   SPF_ZSCROLLA,C'M'              BOTTOM?
         BE    MAIN0080                       YES
         L     R4,DXD_FIRST_LABEL_DISPLAYED   FIRST LABEL DISPLAYED
         L     R2,SPF_ZSCROLLN                NBR TO SCROLL
MAIN0110 DS    0H
         ST    R4,DXD_FIRST_LABEL_DISPLAYED   SAVE LABEL BLOCK ADDR
         ICM   R4,15,LABEL_NEXT               ANOTHER LABEL?
         BZ    MAIN0010                       NO.. DISPLAY
         BCT   R2,MAIN0110                    LOOP
         B     MAIN0010
MAIN0120 DS    0H
         ITRACE ID=TOP
         MVC   DXD_FIRST_LABEL_DISPLAYED,DXD_FIRST_LABEL
         B     MAIN0010
MAIN0130 DS    0H
         ITRACE ID=UP
         CLI   SPF_ZSCROLLA,C'M'              TOP?
         BE    MAIN0120                       YES
         L     R4,DXD_FIRST_LABEL_DISPLAYED   FIRST ON LAST DISPLAY
         L     R2,SPF_ZSCROLLN                NBR TO SCROLL
MAIN0140 DS    0H
         ST    R4,DXD_FIRST_LABEL_DISPLAYED   SAVE LABEL BLOCK ADDR
         ICM   R4,15,LABEL_PREV               ANOTHER BLOCK?
         BZ    MAIN0010                       NO
         BCT   R2,MAIN0140                    LOOP
         B     MAIN0010
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0200 DS    0H
         ITRACE ID=SELECTED
         ST    R4,DISCOMM_USING_LABEL         SAVE LABEL BLOCK ADDR
         SR    R15,R15                        SET RC
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0210 DS    0H
         ITRACE ID=NONE
         XC    DISCOMM_USING_LABEL,DISCOMM_USING_LABEL
         LA    R15,4                          SET RC
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0220 DS    0H
         ITRACE ID=ABORT
         XC    DISCOMM_USING_LABEL,DISCOMM_USING_LABEL
         LA    R15,8                          SET RC
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=EXIT,                                               +
               RDATA1=R15
         L     R13,4(,R13)                    RESTORE REGISTER 13
         L     R14,12(,R13)                   RESTORE R14
         LM    R0,R12,20(R13)                 RESTORE REGISTERS
         BR    R14                            RETURN TO CALLER
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ABEND000 DS    0H
         ITRACE ID=ABEND
         DC    H'0'
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXD_START     DC   Q(DXDULSTL)

PANEL_NAME    DC   CL8'DISLMENU'
ABEND         DC   C'ABEND'

              LTORG

LINE01        DS   0C
              DC   CL2' '
              DC   AL1($SCREEN_ATTR_INPUT)
LINE01_SELECT DC   C' '
              DC   AL1($SCREEN_ATTR_HIGH_YELLOW)
LINE01_DISP   DC   CL8' '
              DC   AL1($SCREEN_ATTR_HIGH_TURQUOISE)
LINE01_LABEL  DC   CL63' '
LINE01_L      EQU  *-LINE01

* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDULSTL                  DSECT
                          COPY DXDPREF

DXD_FIRST_LABEL           DS  A               FIRST LABEL ON CHAIN
DXD_FIRST_LABEL_DISPLAYED DS  A               FIRST LABEL DISPLAYED
DXD_LAST_LABEL_DISPLAYED  DS  A               LAST LABEL DISPLAYED

DXD_WORK                  DS  XL9
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 DISCOMM  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 DISASMDA
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 BPXYSTAT DSECT=YES,LIST=NO
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COPY     ATTRS
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 OSSPFD   TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COPY     REGEQU
              END     DISULSTL
