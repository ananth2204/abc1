*--------------------------------------------------------------------*
*                                                                    *
*  Module name: GENRRE                                               *
*                                                                    *
*  Function:                                                         *
*   Generate RR type instructions                                    *
*                                                                    *
*  At entry R7 is address of AREA                                    *
*           R5 is address of instruction                             *
*           R4 is address of OPCODE entry                            *
*                                                                    *
*--------------------------------------------------------------------*
GENRRE   CSECT
GENRRE   AMODE 31
GENRRE   RMODE ANY
         USING GENRRE,R15
         USING OSCOMM,R12
         USING SESSION,R11
         USING DISCOMM,R9
         B     MOD0000                        SKIP EYECATCHER
MODID    DC    CL8'GENRRE'
         DC    C'&SYSDATE'
         DC    C'&SYSTIME'
MOD0000  DS    0H
         STM   R14,R12,12(R13)                SAVE REGS
         LR    R10,R15                        SET BASE REG
         DROP  R15
         USING GENRRE,R10
         L     R15,SESS_DXD_ADDR              WORK AREA
         A     R15,DXD_START                  PLUS DISP
         ST    R13,4(,R15)
         ST    R15,8(,R13)
         LR    R13,R15                        SET SAVE AREA ADDRESS
         USING DXDB2RRE,R13
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY,                                              +
               RDATA1=R7,                                              +
               DATA2=0(R5)
         MVI   DISCOMM_SOURCE_OPERAND1,C'R'   INSERT 'R'
         SR    R1,R1                          CLEAR REGISTER
         IC    R1,3(,R5)                      OPERANDS
         SRL   R1,4                           SHIFT OUT OPERAND 2
         SLL   R1,1                           MULTIPLY BY 2
         LA    R1,REGISTER_TABLE(R1)
         MVC   DISCOMM_SOURCE_OPERAND1+1(2),0(R1) COPY REGISTER
         LA    R2,DISCOMM_SOURCE_OPERAND1
GEN0010  DS    0H
         CLI   0(R2),C' '
         BE    GEN0020
         LA    R2,1(,R2)
         B     GEN0010
GEN0020  DS    0H
         CLI   1(R5),X'25'                    SSAR?
         BE    GEN0030                        YES
         CLI   1(R5),X'26'                    EPAR?
         BE    GEN0030                        YES
         CLI   1(R5),X'27'                    ESAR?
         BE    GEN0030                        YES
         MVI   0(R2),C','                     INSERT COMMA
         MVI   1(R2),C'R'                     INSERT 'R'
         SR    R1,R1                          CLEAR REGISTER
         IC    R1,3(,R5)                      OPERANDS (AGAIN)
         SLL   R1,28                          SHIFT OUT ALL BUT OPERAND
         SRL   R1,27                          SHIFT BACK
         LA    R1,REGISTER_TABLE(R1)
         MVC   2(2,R2),0(R1)                  COPY REGISTER
         B     EXIT0000
GEN0030  DS    0H
         MVI   DISCOMM_SOURCE_OPERAND1+0,C'('
         MVI   DISCOMM_SOURCE_OPERAND1+1,C'R'
         MVC   DISCOMM_SOURCE_OPERAND1+2(2),0(R1)
         LA    R2,DISCOMM_SOURCE_OPERAND1+3
GEN0040  DS    0H
         CLI   0(R2),C' '
         BE    GEN0050
         LA    R2,1(,R2)
         B     GEN0040
GEN0050  DS    0H
         MVI   0(R2),C')'
EXIT0000 DS    0H
         ITRACE ID=EXIT
         L     R13,4(,R13)                    RESTORE REGISTER 13       ASE01670
         LM    R14,R12,12(R13)                RESTORE OTHER REGISTERS   ASE01680
         SR    R15,R15                        RETURN CODE               ASE01690
         BR    R14                            RETURN TO CALLER          ASE01700
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
DXD_START      DC   Q(DXDB2RRE)

REGISTER_TABLE DS   0C
               DC   CL2'0 '
               DC   CL2'1 '
               DC   CL2'2 '
               DC   CL2'3 '
               DC   CL2'4 '
               DC   CL2'5 '
               DC   CL2'6 '
               DC   CL2'7 '
               DC   CL2'8 '
               DC   CL2'9 '
               DC   CL2'10'
               DC   CL2'11'
               DC   CL2'12'
               DC   CL2'13'
               DC   CL2'14'
               DC   CL2'15'
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
DXDB2RRE       DSECT
               COPY DXDPREF
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               DISCOMM  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               DISASMDA
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               BPXYSTAT DSECT=YES,LIST=NO
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               OSSPFD   TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY     REGEQU
               END      GENRRE
