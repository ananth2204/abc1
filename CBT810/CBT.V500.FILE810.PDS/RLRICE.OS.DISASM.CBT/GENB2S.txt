*--------------------------------------------------------------------*
*                                                                    *
*  Module name: GENB2S                                               *
*                                                                    *
*  Function:                                                         *
*   Generate B2 "S" format instructions                              *
*                                                                    *
*  At entry R7 is AREA address                                       *
*           R5 is address of instruction                             *
*           R4 is address of OPCODE entry                            *
*                                                                    *
*--------------------------------------------------------------------*
GENB2S   CSECT
GENB2S   AMODE 31
GENB2S   RMODE ANY
         USING GENB2S,R15
         USING OSCOMM,R12
         USING SESSION,R11
         USING DISCOMM,R9
         USING AREA_DSECT,R7
         USING OPCODE_DSECT,R4
         B     MOD0000                        SKIP EYECATCHER
MODID    DC    CL8'GENB2S'
         DC    C'&SYSDATE'
         DC    C'&SYSTIME'
MOD0000  DS    0H
         STM   R14,R12,12(R13)                SAVE REGS
         LR    R10,R15                        SET BASE REG
         DROP  R15
         USING GENB2S,R10
         L     R15,SESS_DXD_ADDR              WORK AREA BASE
         A     R15,DXD_START                  PLUS DISP
         ST    R13,4(,R15)
         ST    R15,8(,R13)
         LR    R13,R15                        SET SAVE AREA ADDRESS
         USING DXDSI,R13
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY,                                              +
               RDATA1=R7,                                              +
               DATA2=0(R5)
         MVC   DXD_BLANKS,COMM_BLANKS
         LA    R8,DISCOMM_SOURCE_OPERAND1
         ICM   R1,15,AREA_REF_LABEL1          LABEL REFERENCED?
         USING LABEL_DSECT,R1
         BZ    GEN0030                        NO
*--------------------------------------------------------------------*
*        Generate label and displacement                             *
*--------------------------------------------------------------------*
         ICM   R2,15,AREA_REF_USING1          USING BLOCK
         BZ    GEN0020                        NO
         USING USING_DSECT,R2
         CLC   USING_NAME,COMM_BLANKS         NAME BLANK?
         BE    GEN0020                        YES
         MVC   0(L'USING_NAME,R8),USING_NAME
         BAL   R15,SCAN0000                   FIND END OF NAME
         MVI   0(R8),C'.'                     INSERT DELIMITER
         LA    R8,1(,R8)
GEN0020  DS    0H
         MVC   0(L'LABEL_NAME,R8),LABEL_NAME
         BAL   R15,SCAN0000                   FIND END OF NAME
         ICM   R0,15,AREA_REF_DISP1           DISP FROM LABEL
         BZ    EXIT0000                       ZERO..
         CVD   R0,COMM_DWORD                  CONVERT TO DECIMAL
         MVI   0(R1),C'+'                     INSERT PLUS
         MVC   DXD_EDIT_WORD,EDIT_WORD
         ED    DXD_EDIT_WORD,COMM_DWORD+4
         LA    R2,DXD_EDIT_WORD
         BAL   R15,SCAN1000
         MVC   1(4,R8),0(R2)                  COPY DISP
         B     EXIT0000
*--------------------------------------------------------------------*
*        Generate disp from register                                 *
*--------------------------------------------------------------------*
GEN0030  DS    0H
         MVC   DXD_WORK(2),2(R5)              COPY BASE AND DISP
         NI    DXD_WORK,X'0F'                 REMOVE BASE
         SR    R0,R0
         ICM   R0,3,DXD_WORK                  DISP ONLY
         BZ    GEN0040
         CVD   R0,COMM_DWORD                  CONVERT TO DECIMAL
         MVC   DXD_EDIT_WORD,EDIT_WORD
         ED    DXD_EDIT_WORD,COMM_DWORD+4     EDIT DISP
         LA    R2,DXD_EDIT_WORD
         BAL   R15,SCAN1000
         MVC   DISCOMM_SOURCE_OPERAND1(4),0(R2)
         B     GEN0050
GEN0040  DS    0H
         MVI   DISCOMM_SOURCE_OPERAND1,C'0'
GEN0050  DS    0H
         LA    R8,DISCOMM_SOURCE_OPERAND1
         BAL   R15,SCAN0000
         MVI   0(R8),C'('
         MVI   1(R8),C'R'
         SR    R2,R2
         IC    R2,2(R5)                       BASE +
         SRL   R2,4                           SHIFT OUT DISP
         SLL   R2,1
         LA    R2,REGISTER_TABLE(R2)
         MVC   2(2,R8),0(R2)
         BAL   R15,SCAN0000
         MVI   0(R8),C')'
         B     EXIT0000
*--------------------------------------------------------------------*
*        Find next blank                                             *
*--------------------------------------------------------------------*
SCAN0000 DS    0H
         CLI   0(R8),C' '
         BER   R15
         LA    R8,1(,R8)
         B     SCAN0000
*--------------------------------------------------------------------*
*        Find first non-zero                                         *
*--------------------------------------------------------------------*
SCAN1000 DS    0H
         CLI   0(R2),C'0'
         BNER  R15
         LA    R2,1(,R2)
         B     SCAN1000
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
EXIT0000 DS    0H
         ITRACE ID=EXIT
         L     R13,4(,R13)                    RESTORE REGISTER 13       ASE01670
         LM    R14,R12,12(R13)                RESTORE OTHER REGISTERS   ASE01680
         SR    R15,R15                        RETURN CODE               ASE01690
         BR    R14                            RETURN TO CALLER          ASE01700
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
DXD_START      DC   Q(DXDSI)
EDIT_WORD      DC   X'F020202020202120'

REGISTER_TABLE DS   0C
               DC   CL2'0 '
               DC   CL2'1 '
               DC   CL2'2 '
               DC   CL2'3 '
               DC   CL2'4 '
               DC   CL2'5 '
               DC   CL2'6 '
               DC   CL2'7 '
               DC   CL2'8 '
               DC   CL2'9 '
               DC   CL2'10'
               DC   CL2'11'
               DC   CL2'12'
               DC   CL2'13'
               DC   CL2'14'
               DC   CL2'15'
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
DXDSI          DSECT
               COPY DXDPREF
DXD_WORK       DS   XL8

DXD_EDIT_WORD  DS   XL8
DXD_BLANKS     DS   CL8

* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               DISCOMM  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               DISASMDA
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               BPXYSTAT DSECT=YES,LIST=NO
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY     REGEQU
               END      GENB2S
