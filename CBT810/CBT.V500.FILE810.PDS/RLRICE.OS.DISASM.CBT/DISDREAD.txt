* ------------------------------------------------------------------- *
*                                                                     *
*  MODULE NAME: DISDREAD                                              *
*                                                                     *
*  FUNCTION:                                                          *
*    Read DSECTS (only) from a KSDS of pre-processed DSECTS/LABELS    *
*                                                                     *
* ------------------------------------------------------------------- *
DISDREAD CSECT
DISDREAD AMODE 31
DISDREAD RMODE ANY
         USING DISDREAD,R15
         B     MOD0000                           SKIP EYECATCHER
MODID    DC    CL8'DISDREAD'
         DC    C'&SYSDATE'
         DC    C'&SYSTIME'
MOD0000  DS    0H
         STM   R14,R12,12(R13)                   SAVE REGS
         LR    R10,R15                           SET BASE REG
         DROP  R15
         USING OSCOMM,R12
         USING SESSION,R11
         USING DISDREAD,R10
         USING DISCOMM,R9
         L     R15,SESS_DXD_ADDR                 WORK AREA
         A     R15,DXD_START                     PLUS DISP
         ST    R13,4(,R15)
         ST    R15,8(,R13)
         LR    R13,R15
         USING DXDDREAD,R13
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY,                                              +
               DATA1=DISCOMM_USING_KSDS
         L     R6,DISCOMM_USING_KSDS
         USING KSDS_DSECT,R6
         USING DSECT_DSECT,R5
         MVC   DXD_RPL(RPL_L),RPL_I
         USING IFGRPL,DXD_RPL
* ------------------------------------------------------------------- *
*                                                                     *
*                                                                     *
*        READ the DSECTs in a KSDS                                    *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0010 DS    0H
         TM    KSDS_FLAGS,$KSDS_OPEN             KSDS OPEN?
         BO    MAIN0020                          YES
         ITRACE ID=OPEN,                                               +
               DATA1=KSDS_DDNAME
         OPEN  (KSDS_ACB,INPUT),                 OPEN THE KSDS         +
               MODE=31,                                                +
               MF=(E,DISCOMM_OPEN)
         OI    KSDS_FLAGS,$KSDS_OPEN             SET FLAG
MAIN0020 DS    0H
         ITRACE ID=MODCB1
         MVC   DXD_MODCB(MODCB1_L),MODCB1_I
         LA    R2,DXD_RPL
         LA    R3,KSDS_ACB
         LA    R7,PPD
         LA    R8,PPD_KEY
         MODCB RPL=(R2),                                               +
               ACB=(R3),                                               +
               AREA=(R7),                                              +
               AREALEN=PPD_L,                                          +
               ARG=(R8),                                               +
               KEYLEN=L'PPD_KEY,                                       +
               OPTCD=(KEY,KGE,DIR,SYN,MVE),                            +
               MF=(G,DXD_MODCB)
         ITRACE ID=MODCB_RC,                                           +
               RDATA1=R15,                                             +
               RDATA2=R0
         LTR   R15,R15
         BNZ   ERR0010
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         XC    PPD_SEQ,PPD_SEQ                   INITIALIZE KEY
         MVC   PPD_NAME,COMM_BLANKS
         ITRACE ID=POINT,                                              +
               DATA1=PPD_KEY+0,                                        +
               DATA2=PPD_KEY+8
         POINT RPL=DXD_RPL                       ISSUE POINT
         ITRACE ID=POINT_RC,                                           +
               RDATA1=R8
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         ITRACE ID=MODCB2
         LA    R2,DXD_RPL
         MODCB RPL=(R2),                         CHANGE TO SEQUENTIAL  +
               OPTCD=(SEQ),                                            +
               MF=(G,DXD_MODCB)
         ITRACE ID=MODCB_RC,                                           +
               RDATA1=R15,                                             +
               RDATA2=R0
         LTR   R15,R15
         BNZ   ERR0010
         LA    R5,KSDS_DSECT_PREV                "FIRST" ON CHAIN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0030 DS    0H
         ITRACE ID=GET
         GET   RPL=DXD_RPL                       READ A RECORD
         ITRACE ID=GET_RC,                                             +
               RDATA1=R15,                                             +
               DATA2=RPLFDBK
         LTR   R15,R15                           SUCCESSFUL?
         BNZ   MAIN0040                          NO
         ITRACE ID=PPD_DATA,                                           +
               DATA1=PPD_DATA+0,                                       +
               DATA2=PPD_DATA+8
         CLC   DSECT_ID,PPD_DATA+8               DSECT?
         BNE   MAIN0030                          NO
         GETMAIN RU,                                                   +
               LV=DSECT_DSECT_L,                                       +
               LOC=ANY
NEW      USING DSECT_DSECT,R1
PPX      USING DSECT_DSECT,PPD_DATA
         XC    NEW.DSECT_DSECT(DSECT_DSECT_L),NEW.DSECT_DSECT
         ST    R1,DSECT_NEXT                     CHAIN TO PREVIOUS
         ST    R5,NEW.DSECT_PREV                 CHAIN TO NEW
         MVC   NEW.DSECT_EYE,PPX.DSECT_EYE       COPY EYECATCHER
         MVC   NEW.DSECT_NAME,PPX.DSECT_NAME     COPY DSECT NAME
         ITRACE ID=DSECT,                                              +
               RDATA1=R1,                                              +
               DATA2=NEW.DSECT_NAME
         LR    R5,R1
         B     MAIN0030                          READ NEXT RECORD
MAIN0040 DS    0H
         ICM   R5,15,KSDS_DSECT_NEXT             FIRST DSECT
         BZ    EXIT0000                          NO DSECTS
         XC    DSECT_PREV,DSECT_PREV             FORCE PREV TO ZERO
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0010  DS    0H
         ITRACE ID=MOD_ERR,                                            +
               RDATA1=R15
         DC    H'0'
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=EXIT
         L     R13,4(,R13)                       RESTORE REGISTER 13
         L     R14,12(,R13)                      RESTORE REGISTERS
         SR    R15,R15                           SET RC
         LM    R0,R12,20(R13)                    RESTORE REGISTERS
         BR    R14                               RETURN TO CALLER
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXD_START     DC   Q(DXDDREAD)

RPL_I         RPL  AM=VSAM
RPL_L         EQU  *-RPL_I

MODCB1_I MODCB RPL=*,                                                  +
               ACB=*,                                                  +
               AREA=*,                                                 +
               AREALEN=PPD_L,                                          +
               ARG=*,                                                  +
               KEYLEN=L'PPD_KEY,                                       +
               OPTCD=(KEY,KGE,DIR,SYN,MVE),                            +
               MF=L
MODCB1_L EQU  *-MODCB1_I

DSECT_ID DC   CL8'DSECT'
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDDREAD      DSECT
              COPY    DXDPREF

DXD_MODCB     DS      0F,(MODCB1_L)X
DXD_RPL       DS      0F,(RPL_L)X

              COPY     PPDDATA
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              DISCOMM  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              DISASMDA
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              BPXYSTAT DSECT=YES,LIST=NO
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              IFGRPL   AM=VSAM
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              COPY     REGEQU
              END      DISDREAD
