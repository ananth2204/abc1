* ------------------------------------------------------------------- *
*                                                                     *
*  MODULE NAME: DISCDIR                                               *
*                                                                     *
*  Find a specific member or display list of members                  *
*  for restore config                                                 *
*                                                                     *
* ------------------------------------------------------------------- *
DISCDIR  CSECT
DISCDIR  AMODE 31
DISCDIR  RMODE ANY
         USING OSCOMM,R12
         USING SESSION,R11
         USING DISCOMM,R9
         USING DISCDIR,R15
         B     INIT0000
MODID    DC    CL8'DISCDIR'
         DC    CL8'&SYSDATE'
         DC    CL7'&SYSTIME'
INIT0000 DS    0H
         STM   R14,R12,12(R13)                  SAVE REGS
         LR    R10,R15                          COPY ENTRY POINT
         DROP  R15
         USING DISCDIR,R10                      DEFINE BASE
         ICM   R15,15,SESS_DXD_ADDR
         A     R15,DXD_START                    PLUS DISPLACEMENT
         ST    R13,4(R15)                       SAVE R13
         ST    R15,8(R13)                       CHAIN SAVE AREA
         LR    R13,R15                          COPY WORK AREA ADDRESS
         USING DXDCDIR,R13                      DEFINE WORK AREA BASE
         MVC   DXD_CSECT,MODID
         L     R8,COMM_OSSPFD
         USING OSSPFD,R8
         ITRACE ID=ENTRY,                                              +
               DATA1=SPF_CONFIG_MEMBER
         MVI   DXD_RC,0                         INITIALIZE RC
         MVI   DXD_FLAGS,$DXD_FIND              ASSUME 'FIND' MEMBER
         MVC   DXD_DESERV(DESERV_L),DESERV_I
         XC    DXD_VDATA_ADDR,DXD_VDATA_ADDR
         XC    DXD_VDATA_LENGTH,DXD_VDATA_LENGTH
         XC    DXD_ADDR,DXD_ADDR
         CLC   SPF_CONFIG_MEMBER,COMM_BLANKS    MEMBER NAME GIVEN?
         BNE   INIT0010                         YES
         MVI   DXD_FLAGS,$DXD_DISPLAY           DISPLAY MEMBERS
         XC    DSPCREQ_RECORD_NBR,DSPCREQ_RECORD_NBR
         LA    R0,DXD_IO
         ST    R0,DSPCREQ_RECORD_ADDR
         LA    R0,CONFIG_L
         ST    R0,DSPCREQ_RECORD_LENGTH
         LA    R0,DATASPACE_2
         ST    R0,DSPCREQ_DATASPACE
         MVI   DSPCREQ_FUNC,$DSPCREQ_CREATE     SET FUNCTION
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE                OSDSPACE ENTRY POINT
         BALR  R14,R15                          CREATE DATA SPACE
         CLI   DSPCREQ_RC,$DSPCREQ_OK           SUCCESSFUL?
         BNE   ERR0100                          NO
         OI    DXD_FLAGS,$DXD_CREATED
* ------------------------------------------------------------------- *
*        Issue DESERV to obtain directory info for all members        *
* ------------------------------------------------------------------- *
INIT0010 DS    0H
         ITRACE ID=GET_ALL
         DESERV FUNC=GET_ALL,                   ALL DIRECTORY ENTRIES  +
               AREAPTR=DXD_ADDR,                .. BUFFER FOR DATA     +
               CONN_INTENT=NONE,                .. NO CONNECTION INTENT+
               DCB=DISCONF_DCB,                 .. DCB ADDRESS         +
               RETCODE=DXD_RETURN_CODE,                                +
               RSNCODE=DXD_REASON_CODE,                                +
               MF=(E,DXD_DESERV,COMPLETE)
         ITRACE ID=DE_RC,                                              +
               DATA1=(DXD_RETURN_CODE,4),                              +
               DATA2=(DXD_REASON_CODE,4)
         OC    DXD_RETURN_CODE,DXD_RETURN_CODE
         BNZ   ERR0100
* ------------------------------------------------------------------- *
*        Process the data returned by DESERV                          *
* ------------------------------------------------------------------- *
         L     R5,DXD_ADDR                      ADDR OF RETURNED DATA
         USING DESB,R5                          DEFINE BASE
DIR0000  DS    0H
         L     R7,DESB_COUNT                    NUMBER OF SMDES
         LA    R6,DESB_DATA                     FIRST SMDE
         USING SMDE,R6                          DEFINE BASE
DIR0010  DS    0H
         ITRACE ID=SMDE,                                               +
               RDATA1=R6,                                              +
               RDATA2=R7
         MVC   DXD_CONFIG_MEMBER,COMM_BLANKS
         MVC   DXD_CONFIG_DESC,COMM_BLANKS
         SR    R14,R14
         ICM   R14,3,SMDE_NAME_OFF              OFFSET TO NAME
         AR    R14,R6                           PLUS BASE
         SR    R15,R15
         ICM   R15,3,0(R14)                     LENGTH OF NAME
         CH    R15,=Y(L'DXD_CONFIG_MEMBER)      TOO LONG?
         BNH   DIR0020                          NO
         LH    R15,=Y(L'DXD_CONFIG_MEMBER)      LIMIT LENGTH
DIR0020  DS    0H
         BCTR  R15,0                            MINUS 1
         EX    R15,NAME_MVC                     COPY NAME
         CLC   SPF_CONFIG_MEMBER,COMM_BLANKS    MEMBER NAME GIVEN?
         ICM   R15,3,SMDE_USRD_LEN              USER DATA LENGTH
         BZ    DIR0040                          NO USER DATA
         CH    R15,=Y(L'DXD_CONFIG_DESC)        TOO LONG?
         BNH   DIR0030                          NO
         LH    R15,=Y(L'DXD_CONFIG_DESC)        LIMIT LENGTH
DIR0030  DS    0H
         SR    R14,R14
         ICM   R14,3,SMDE_USRD_OFF              OFFSET TO USER DATA
         AR    R14,R6                           PLUS BASE
         BCTR  R15,0
         EX    R15,DESC_MVC                     COPY DESCRIPTION
DIR0040  DS    0H
         ITRACE ID=MEMBER,                                             +
               DATA1=DXD_CONFIG_MEMBER
         TM    DXD_FLAGS,$DXD_DISPLAY           CALLED TO DISPLAY LIST?
         BO    DIR0050                          YES
         CLC   SPF_CONFIG_MEMBER,DXD_CONFIG_MEMBER
         BE    DIR0410                          MEMBER LOCATED
         B     DIR0060                          GO TO NEXT MEMBER
DIR0050  DS    0H
         L     R1,DSPCREQ_RECORD_NBR            RECORDS STORED SO FAR
         A     R1,F1                            PLUS 1
         ST    R1,DSPCREQ_RECORD_NBR            SET RECORD NUMBER
         ITRACE ID=STORE,                                              +
               DATA1=(DSPCREQ_RECORD_NBR,4),                           +
               DATA2=(DSPCREQ_RECORD_LENGTH,4)
         MVI   DSPCREQ_FUNC,$DSPCREQ_STORE
         LA    R1,DSPCREQ                       DSPCREQ BLOCK ADDRESS
         L     R15,COMM_OSDSPACE                OSDSPACE ENTRY POINT
         BALR  R14,R15                          STORE THE RECORD
         CLI   DSPCREQ_RC,$DSPCREQ_OK           SUCCESSFUL?
         BNE   ERR0100                          NO
DIR0060  DS    0H
         ICM   R0,15,SMDE_LEN                   LENGTH OF THIS SMDE
         AR    R6,R0                            NEXT SMDE
         BCT   R7,DIR0010                       LOOP
* ------------------------------------------------------------------- *
*        All entries in this block processed.                         *
*        FREEMAIN this block and continue with next block.            *
* ------------------------------------------------------------------- *
         ICM   R0,15,DESB_LEN                   DESB LENGTH
         ICM   R0,8,DESB_SUBPOOL                INSERT SUBPOOL
         LR    R1,R5                            COPY DESB ADDRESS
         ICM   R5,15,DESB_NEXT                  NEXT DESB
         ITRACE ID=FREEDESB,                                           +
               RDATA1=R1,                                              +
               RDATA2=R0
         FREEMAIN RU,                                                  +
               A=(1),                                                  +
               LV=(0)
         LTR   R5,R5                            ADDITIONAL DESB'S?
         BNZ   DIR0000                          YES
* ------------------------------------------------------------------- *
*        All data processed.                                          *
* ------------------------------------------------------------------- *
         ITRACE ID=EOF_DIR,                                            +
               DATA1=(DXD_FLAGS,1)
         TM    DXD_FLAGS,$DXD_FIND              CALLED TO FIND MEMBER?
         BO    ERR0010                          YES.. NOT FOUND
         MVC   DXD_DISPLAY_RECORD,F1            START WITH RECORD 1
* ------------------------------------------------------------------- *
*                                                                     *
*        Display the members as a selection list                      *
*                                                                     *
* ------------------------------------------------------------------- *
DIR0100  DS    0H
         ITRACE ID=GETVDATA,                                           +
               DATA1=PANEL_NAME
         LA    R1,PANEL_NAME                    PANEL NAME ADDR
         MVI   COMM_VDATA_FUNC,$VDATA_GETMAIN   SET FUNCTION
         L     R15,COMM_V_OSVDATA
         BALR  R14,R15                          GETMAIN VDATA
         ST    R0,DXD_VDATA_LENGTH              SAVE LENGTH
         ST    R1,DXD_VDATA_ADDR                SAVE ADDRESS
* ------------------------------------------------------------------- *
*        initialize VDATA buffer                                      *
* ------------------------------------------------------------------- *
DIR0130  DS    0H
         ITRACE ID=STRT_BLD
         L     R0,DXD_VDATA_ADDR
         L     R1,DXD_VDATA_LENGTH
         SR    R14,R14
         SR    R15,R15
         MVCL  R0,R14                           INITIALIZE VDATA
         XC    DXD_LINES,DXD_LINES              LINES BUILT
         L     R7,DXD_VDATA_ADDR                VDATA BUFFER
         L     R6,SPF_VDEPTH                    NUMBER OF LINES
         MVI   DSPCREQ_FUNC,$DSPCREQ_RETRIEVE   SET FUNCTION
         MVC   DSPCREQ_RECORD_NBR,DXD_DISPLAY_RECORD
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DIR0140  DS    0H
         ITRACE ID=RETRIEVE,                                           +
               DATA1=(DSPCREQ_RECORD_NBR,4)
         LA    R1,DSPCREQ                       DATA SPACE PARMS
         L     R15,COMM_OSDSPACE                SPACE MANAGER
         BALR  R14,R15                          RETRIEVE THE RECORD
         CLI   DSPCREQ_RC,$DSPCREQ_OK           SUCCESSFUL?
         BNE   ERR0100                          NO
         ITRACE ID=DETAIL,                                             +
               DATA1=DXD_CONFIG_MEMBER
         MVC   0(MSG01_L,R7),MSG01              COPY
         USING MSG01,R7
         MVC   MSG01_MEMBER,DXD_CONFIG_MEMBER   COPY MEMBER NAME
         MVC   MSG01_DESC,DXD_CONFIG_DESC       COPY DESCRIPTION
         A     R7,SPF_VWIDTH                    NEXT LINE
         BCT   R6,DIR0150
         B     DIR0200                          SCREEN FULL
DIR0150  DS    0H
         CLC   DSPCREQ_RECORD_NBR,DATASPACE_2_LAST_RECORD
         BE    DIR0200                          AT HIGH RECORD.. STOP
         L     R1,DSPCREQ_RECORD_NBR            CURRENT RECORD NBR
         LA    R1,1(,R1)                        NEXT RECORD
         ST    R1,DSPCREQ_RECORD_NBR            UPDATE RECORD NBR
         B     DIR0140
* ------------------------------------------------------------------- *
*        Display                                                      *
* ------------------------------------------------------------------- *
DIR0200  DS    0H
         ITRACE ID=DISPLAY
         MVC   SESS_DISP_PANEL,PANEL_NAME
         MVC   SESS_DISP_VDATA_ADDR,DXD_VDATA_ADDR
         MVC   SESS_DISP_VDATA_LENGTH,DXD_VDATA_LENGTH
         XC    SESS_DISP_CURSOR,SESS_DISP_CURSOR
         L     R15,COMM_V_OSDISP                DISPLAY ENTRY POINT
         BALR  R14,R15                          DISPLAY DATA
         MVC   DXD_RC,SESS_RC+1                 SET RETURN CODE
         ITRACE ID=DISP_RC,                                            +
               DATA1=(DXD_RC,1)
         CLI   DXD_RC,0                         ZERO?
         BNE   EXIT0000                         NO
* ------------------------------------------------------------------- *
*                                                                     *
*        Check for line command(s)                                    *
*                                                                     *
*                                                                     *
*     Both LMOD and non_LMOD display lines have the same format       *
*     through the selection and member name.  This uses "S_1"         *
*     for both.                                                       *
*                                                                     *
* ------------------------------------------------------------------- *
         L     R7,DXD_VDATA_ADDR                VDATA ADDRESS
         LH    R6,DXD_LINES                     LINES BUILT
DIR0250  DS    0H
         OI    MSG01_SELECT,C' '                'TRANSLATE' TO UPPER
         CLI   MSG01_SELECT,C' '                BLANK?
         BNE   DIR0400                          NO
         A     R7,SPF_VWIDTH                    NEXT LINE
         BCT   R6,DIR0250                       LOOP
* ------------------------------------------------------------------- *
*        Process scrolling                                            *
* ------------------------------------------------------------------- *
         ITRACE ID=ZVERB,                                              +
               DATA1=SPF_ZVERB,                                        +
               DATA2=(SPF_ZSCROLLN,4)
         CLI   SPF_ZVERB,C'B'                   BOTTOM?
         BE    DIR0360                          YES
         CLI   SPF_ZVERB,C'D'                   SCROLL DOWN?
         BE    DIR0350                          YES
         CLI   SPF_ZVERB,C'T'                   TOP?
         BE    DIR0310                          YES
         CLI   SPF_ZVERB,C'U'                   SCROLL UP?
         BE    DIR0300                          YES
         MVC   DSPCREQ_RECORD_NBR,DXD_DISPLAY_RECORD
         B     DIR0130                          RE-BUILD DATA
* ------------------------------------------------------------------- *
*        SCROLL UP                                                    *
* ------------------------------------------------------------------- *
DIR0300  DS    0H
         ITRACE ID=UP
         MVC   SPF_ZCMD,COMM_BLANKS             CLEAR ZCMD
         CLI   SPF_ZSCROLLA,C'M'                MAX?
         BE    DIR0310                          YES
         L     R2,DXD_DISPLAY_RECORD            FIRST RECORD DISPLAYED
         S     R2,SPF_ZSCROLLN                  MINUS SCROLL AMOUNT
         BH    DIR0320
* ------------------------------------------------------------------- *
*        SCROLL TOP                                                   *
* ------------------------------------------------------------------- *
DIR0310  DS    0H
         ITRACE ID=TOP
         LA    R2,1                             DEFAULT TO 1
DIR0320  DS    0H
         ST    R2,DSPCREQ_RECORD_NBR            SET RECORD TO DISPLAY
         B     DIR0130                          REBUILD EVERYTHING
* -------------------------------------------------------------------
*        SCROLL DOWN
* -------------------------------------------------------------------
DIR0350  DS    0H
         ITRACE ID=DOWN
         MVC   SPF_ZCMD,COMM_BLANKS             CLEAR ZCMD
         CLI   SPF_ZSCROLLA,C'M'                MAX?
         BE    DIR0360                          YES
         L     R2,DXD_DISPLAY_RECORD            FIRST RECORD DISPLAYED
         A     R2,SPF_ZSCROLLN                  PLUS RECORDS TO SCROLL
         C     R2,DATASPACE_2_LAST_RECORD       BEYOND LAST MEMBER?
         BH    DIR0360                          YES
         ST    R2,DSPCREQ_RECORD_NBR            SET 1ST TO DISPLAY
         B     DIR0130                          REBUILD EVERYTHING
* -------------------------------------------------------------------
*        SCROLL BOTTOM
* -------------------------------------------------------------------
DIR0360  DS    0H
         ITRACE ID=BOTTOM
         L     R2,DATASPACE_2_LAST_RECORD       HIGHEST RECORD STORED
         B     DIR0130                          REBUILD EVERYTHING
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DIR0400  DS    0H
         ITRACE ID=SELECTED,                                           +
               DATA1=MSG01_MEMBER               MEMBER SELECTED
         MVC   SPF_CONFIG_MEMBER,MSG01_MEMBER   COPY MEMBER NAME
         MVC   SPF_CONFIG_DESC,MSG01_DESC       COPY MEMBER DESCRIPTION
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DIR0410  DS    0H
         ITRACE ID=LOCATED
         MVC   SPF_CONFIG_DESC,DXD_CONFIG_DESC  COPY MEMBER DESCRIPTION
         ICM   R0,15,DESB_LEN                   DESB LENGTH
         ICM   R0,8,DESB_SUBPOOL                INSERT SUBPOOL
         ITRACE ID=FREEDESB,                                           +
               RDATA1=R5,                                              +
               RDATA2=R0
         FREEMAIN RU,                           FREEMAIN DESB          +
               A=(R5),                                                 +
               LV=(0)
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0010  DS    0H
         ITRACE ID=NO_DATA
         MVI   SESS_STATUS,$STATUS_NO_DATA
         MVC   COMM_MSG_CSECT,MODID             SET OUR CSECT'S NAME
         MVI   COMM_MSG_ID,0                    FORCE 1ST BYTE TO ZERO
         MVI   COMM_MSG_ID+1,1                  SET MESSAGE NUMBER
         MVC   COMM_INFO_01(4),DXD_RETURN_CODE
         MVC   COMM_INFO_02(4),DXD_REASON_CODE
         ITRACE ID=MESSAGE,                                            +
               DATA1=(COMM_MSG_ID,2)
         L     R15,COMM_V_OSMSG                 OSMSG ENTRY POINT
         BALR  R14,R15                          CALL OSMSG
ERR0100  DS    0H
         MVC   SPF_MSG_1,COMM_MSG_1
         MVC   SPF_MSG_2,COMM_MSG_2
         MVC   SPF_MSG_3,COMM_MSG_3
         MVC   SPF_MSG_4,COMM_MSG_4
         MVC   SPF_MSG_5,COMM_MSG_5
         MVI   DXD_RC,8                         SET RC
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=EXIT
         TM    DXD_FLAGS,$DXD_CREATED           DATASPACE CREATED?
         BNO   EXIT0010                         NO
         MVI   DSPCREQ_FUNC,$DSPCREQ_DELETE     DELETE THE DATA SPACE
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE
         BALR  R14,R15                          DELETE DATA SPACE 2
EXIT0010 DS    0H
         ICM   R1,15,DXD_VDATA_ADDR             VDATA GETMAINED?
         BZ    EXIT0020                         NO
         L     R0,DXD_VDATA_LENGTH
         MVI   COMM_VDATA_FUNC,$VDATA_FREEMAIN  SET FUNCTION
         L     R15,COMM_V_OSVDATA
         BALR  R14,R15                          FREE VDATA
EXIT0020 DS    0H
         SR    R2,R2
         IC    R2,DXD_RC                        SAVE RETURN CODE
         L     R13,4(R13)                       RESTORE R13
         LR    R15,R2
         L     R14,12(R13)                      RESTORE R14
         LM    R0,R12,20(R13)                   RESTORE REGISTERS
         BR    R14                              RETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
NAME_MVC MVC   DXD_CONFIG_MEMBER(0),2(R14)      COPY MEMBER NAME
DESC_MVC MVC   DXD_CONFIG_DESC(0),0(R14)        COPY DESCRIPTION
* ------------------------------------------------------------------- *
*        CONSTANTS                                                    *
* ------------------------------------------------------------------- *
DXD_START      DC    Q(DXDCDIR)
DESERV_I       DESERV FUNC=GET_ALL,                                    +
               MF=L
DESERV_L       EQU   *-DESERV_I

F1             DC    F'1'

PANEL_NAME     DC    CL8'DISCDIR'

MSG01          DS    0C
               DC    AL1($SCREEN_ATTR_INPUT)
MSG01_SELECT   DC    C' '
               DC    AL1($SCREEN_ATTR_HIGH)
MSG01_MEMBER   DC    CL8' '
               DC    AL1($SCREEN_ATTR_HIGH_TURQUOISE)
MSG01_DESC     DC    CL62' '
MSG01_L        EQU   *-MSG01

                LTORG
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDCDIR            DSECT
                   COPY  DXDPREF
DXD_DESERV         DS    0F,(DESERV_L)X

DXD_ADDR           DS    A
DXD_RETURN_CODE    DS    F
DXD_REASON_CODE    DS    F

DXD_VDATA_ADDR     DS    A
DXD_VDATA_LENGTH   DS    F
DXD_DISPLAY_RECORD DS    F                 FIRST RECORD DISPLAYED

DXD_LINES          DS    H

DXD_RC             DS    X

DXD_FLAGS          DS    X
$DXD_CREATED       EQU   X'80'             DATASPACE CREATED
$DXD_DISPLAY       EQU   X'40'             DISPLAY LIST OF MEMBERS
$DXD_FIND          EQU   X'20'             FIND A SPECIFIC MEMBER

DXD_IO             DS    0C
DXD_CONFIG_MEMBER  DS    CL8
DXD_CONFIG_DESC    DS    CL62
CONFIG_L           EQU   *-DXD_IO

                   DSPCREQ  DSECT=NO
DISCDIR_DXD_L      EQU      *-DXDCDIR
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         IBMMAC DES=YES
         IBMMAC SMDE=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         DISCOMM  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         DISASMDA
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         OSSPFD   TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         BPXYSTAT DSECT=YES,LIST=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY     SPFSTATS
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY     ATTRS
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY     REGEQU
         END      DISCDIR
