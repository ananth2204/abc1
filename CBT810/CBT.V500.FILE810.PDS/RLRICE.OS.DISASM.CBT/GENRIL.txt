*--------------------------------------------------------------------*
*                                                                    *
*  Module name: GENRIL                                               *
*                                                                    *
*  Function:                                                         *
*   Generate "relative" instructions (C0_x)                          *
*                                                                    *
*  At entry R7 is address of AREA                                    *
*           R5 is address of instruction                             *
*           R4 is address of OPCODE entry                            *
*                                                                    *
*--------------------------------------------------------------------*
GENRIL   CSECT
GENRIL   AMODE 31
GENRIL   RMODE ANY
         USING GENRIL,R15
         USING OSCOMM,R12
         USING SESSION,R11
         USING DISCOMM,R9
         USING AREA_DSECT,R7
         USING OPCODE_DSECT,R4
         B     MOD0000                        SKIP EYECATCHER
MODID    DC    CL8'GENRIL'
         DC    C'&SYSDATE'
         DC    C'&SYSTIME'
MOD0000  DS    0H
         STM   R14,R12,12(R13)                SAVE REGS
         LR    R10,R15                        SET BASE REG
         DROP  R15
         USING GENRIL,R10
         GETMAIN RU,                                                   +
               LV=WORK_L,                                              +
               LOC=ANY
         ST    R13,4(,R1)
         ST    R1,8(,R13)
         LR    R13,R1
         USING WORK,R13
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY,                                              +
               RDATA1=R7,                                              +
               DATA2=(0(R5),4)
         LA    R3,DISCOMM_SOURCE_OPERAND1     INITIALIZE OUTPUT ADDR
         SR    R1,R1
         IC    R1,1(R5)                       2ND BYTE
         SRL   R1,4                           SIFT REGISTER TO LOW BITS
         MH    R1,H3                          MULTIPLPY BY 3
         LA    R1,REGISTER_TABLE(R1)          REGISTER NAME
         MVC   0(3,R3),0(R1)                  COPY REGISTER NAME
         LA    R2,3
RIL0010  DS    0H
         CLI   0(R3),C' '                     BLANK?
         BE    RIL0020                        YES
         LA    R3,1(,R3)                      NEXT
         BCT   R2,RIL0010                     LOOP
RIL0020  DS    0H
         MVI   0(R3),C','                     INSERT COMMA
         ICM   R6,15,AREA_REF_LABEL2          LABEL ADDRESS
         BNZ   RIL0030
         DC    H'0'                           SHOULD NOT HAPPEN
RIL0030  DS    0H
         USING LABEL_DSECT,R6
         MVC   1(L'LABEL_NAME,R3),LABEL_NAME  COPY LABEL
EXIT0000 DS    0H
         ITRACE ID=EXIT
         LR    R1,R13
         L     R13,4(,R13)                    RESTORE REGISTER 13       ASE01670
         FREEMAIN RU,                                                  +
               A=(R1),                                                 +
               LV=WORK_L
         LM    R14,R12,12(R13)                RESTORE OTHER REGISTERS   ASE01680
         SR    R15,R15                        RETURN CODE               ASE01690
         BR    R14                            RETURN TO CALLER          ASE01700
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
H3             DC   H'3'

REGISTER_TABLE DS   0C
               DC   CL3'R0 '
               DC   CL3'R1 '
               DC   CL3'R2 '
               DC   CL3'R3 '
               DC   CL3'R4 '
               DC   CL3'R5 '
               DC   CL3'R6 '
               DC   CL3'R7 '
               DC   CL3'R8 '
               DC   CL3'R9 '
               DC   CL3'R10'
               DC   CL3'R11'
               DC   CL3'R12'
               DC   CL3'R13'
               DC   CL3'R14'
               DC   CL3'R15'
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
WORK           DSECT
               COPY DXDPREF
WORK_L         EQU  *-WORK
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               DISCOMM  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               DISASMDA
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               BPXYSTAT DSECT=YES,LIST=NO
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY     REGEQU
               END      GENRIL
