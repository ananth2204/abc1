*--------------------------------------------------------------------*
*                                                                    *
*  Module name: DISBIND                                              *
*                                                                    *
*  Function:                                                         *
*   Obtain the requested module's RLD, ESD, text, etc using          *
*   a BINDER dialog.                                                 *
*                                                                    *
*  The function being requested is in DISCOMM_BIND_FUNC.             *
*                                                                    *
*--------------------------------------------------------------------*
DISBIND  CSECT
DISBIND  AMODE 31
DISBIND  RMODE ANY
         USING DISBIND,R15
         B     MOD0000                        SKIP EYECATCHER
MODID    DC    CL8'DISBIND'
         DC    C'&SYSDATE'
         DC    C'&SYSTIME'
MOD0000  DS    0H
         STM   R14,R12,12(R13)                SAVE REGS
         LR    R10,R15                        SET BASE REG
         DROP  R15
         USING OSCOMM,R12
         USING SESSION,R11
         USING DISBIND,R10
         USING DISCOMM,R9
         L     R15,SESS_DXD_ADDR              WORK AREA
         A     R15,DXD_START                  PLUS DISP
         ST    R13,4(,R15)                    CHAIN  SAVE AREA
         ST    R15,8(,R13)                    CHAIN SAVE AREA
         LR    R13,R15                        SET SAVE AREA ADDRESS
         USING DXDBIND,R13
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY,                                              +
               DATA1=(DISCOMM_BIND_FUNC,1)
         L     R8,COMM_OSSPFD
         USING OSSPFD,R8
         CLI   DISCOMM_BIND_FUNC,$BIND_STARTD  START DIALOG?
         BE    BIND0010                        YES
         CLI   DISCOMM_BIND_FUNC,$BIND_NAMES   BUILD NAME CHAIN?
         BE    BIND0020
         CLI   DISCOMM_BIND_FUNC,$BIND_ESD_RLD BUILD ESD/RLD CHAINS?
         BE    BIND0030
         CLI   DISCOMM_BIND_FUNC,$BIND_TEXT    READ TEXT?
         BE    BIND0040
         CLI   DISCOMM_BIND_FUNC,$BIND_ENDD    END DIALOG?
         BE    BIND0050
         DC    H'0'
*--------------------------------------------------------------------*
*        Start the dialog                                            *
*--------------------------------------------------------------------*
BIND0010 DS    0H
         ITRACE ID=STARTD
         IEWBIND FUNC=STARTD,                                          +
               VERSION=5,                                              +
               DIALOG=DISCOMM_DIALOG_TOKEN,                            +
               RETCODE=DXD_RETURN_CODE,                                +
               RSNCODE=DXD_REASON_CODE,                                +
               MF=(E,DXD_IEWBIND,COMPLETE)
         ITRACE ID=STARTDRC,                                           +
               DATA1=(DXD_RETURN_CODE,4),                              +
               DATA2=(DXD_REASON_CODE,4)
         OC    DXD_RETURN_CODE,DXD_RETURN_CODE
         BNZ   ERR0010
*--------------------------------------------------------------------*
*        Create a WORKMOD                                            *
*--------------------------------------------------------------------*
         ITRACE ID=CREATEW
         IEWBIND FUNC=CREATEW,                                         +
               VERSION=5,                                              +
               DIALOG=DISCOMM_DIALOG_TOKEN,                            +
               WORKMOD=DISCOMM_DIALOG_WORKMOD,                         +
               INTENT=ACCESS,                                          +
               RETCODE=DXD_RETURN_CODE,                                +
               RSNCODE=DXD_REASON_CODE,                                +
               MF=(E,DXD_IEWBIND,COMPLETE)
         ITRACE ID=CREATWRC,                                           +
               DATA1=(DXD_RETURN_CODE,4),                              +
               DATA2=(DXD_REASON_CODE,4)
         OC    DXD_RETURN_CODE,DXD_RETURN_CODE
         BNZ   ERR0020
*--------------------------------------------------------------------*
*        Set list options                                            *
*--------------------------------------------------------------------*
         ITRACE ID=SET_ALL
         IEWBIND FUNC=SETO,                                            +
               VERSION=5,                                              +
               WORKMOD=DISCOMM_DIALOG_WORKMOD,                         +
               OPTION=LIST,                                            +
               OPTVAL=ALL,                                             +
               RETCODE=DXD_RETURN_CODE,                                +
               RSNCODE=DXD_REASON_CODE,                                +
               MF=(E,DXD_IEWBIND,COMPLETE)
         ITRACE ID=SET_RC,                                             +
               DATA1=(DXD_RETURN_CODE,4),                              +
               DATA2=(DXD_REASON_CODE,4)
         OC    DXD_RETURN_CODE,DXD_RETURN_CODE
         BNZ   ERR0030
*--------------------------------------------------------------------*
*        INCLUDE the LMOD                                            *
*--------------------------------------------------------------------*
         ITRACE ID=INCLUDE,                                            +
               DATA1=DISCOMM_DDNAME,                                   +
               DATA2=DISCOMM_LMOD
         IEWBIND FUNC=INCLUDE,                                         +
               VERSION=5,                                              +
               WORKMOD=DISCOMM_DIALOG_WORKMOD,                         +
               INTYPE=NAME,                                            +
               DDNAME=DISCOMM_DDNAME_LENGTH,                           +
               MEMBER=DISCOMM_LMOD_NAME_LENGTH,                        +
               RETCODE=DXD_RETURN_CODE,                                +
               RSNCODE=DXD_REASON_CODE,                                +
               MF=(E,DXD_IEWBIND,COMPLETE)
         ITRACE ID=INCL_RC,                                            +
               DATA1=(DXD_RETURN_CODE,4),                              +
               DATA2=(DXD_REASON_CODE,4)
         OC    DXD_REASON_CODE,DXD_REASON_CODE
         BNZ   ERR0040
         B     EXIT0000
*--------------------------------------------------------------------*
*        Determine section names in module                           *
*--------------------------------------------------------------------*
BIND0020 DS    0H
         ITRACE ID=CALLNAME
         L     R15,V_DISNAME
         BALR  R14,R15
         B     EXIT0000
*--------------------------------------------------------------------*
*        Process ESD info                                            *
*--------------------------------------------------------------------*
BIND0030 DS    0H
         ITRACE ID=CALLESD
         L     R15,V_DISESD
         BALR  R14,R15
*--------------------------------------------------------------------*
*        Process RLD items                                           *
*--------------------------------------------------------------------*
         ITRACE ID=CALLRLD
         L     R15,V_DISRLD
         BALR  R14,R15
         B     EXIT0000
*--------------------------------------------------------------------*
*        Load the text                                               *
*--------------------------------------------------------------------*
BIND0040 DS    0H
         ITRACE ID=CALLTEXT
         L     R15,V_DISTEXT
         BALR  R14,R15
         B     EXIT0000
*---------------------------------------------------------------------*
*        Delete the WORKMOD                                           *
*---------------------------------------------------------------------*
BIND0050 DS    0H
         ITRACE ID=DELETEW,                                            +
               DATA1=DISCOMM_DIALOG_WORKMOD
         IEWBIND FUNC=DELETEW,                                         +
               VERSION=5,                                              +
               WORKMOD=DISCOMM_DIALOG_WORKMOD,                         +
               PROTECT=NO,                                             +
               RETCODE=DXD_RETURN_CODE,                                +
               RSNCODE=DXD_REASON_CODE,                                +
               MF=(E,DXD_IEWBIND,COMPLETE)
         ITRACE ID=DELETERC,                                           +
               DATA1=DXD_RETURN_CODE,                                  +
               DATA2=DXD_REASON_CODE
         OC    DXD_REASON_CODE,DXD_REASON_CODE
         BNZ   ERR0050
*---------------------------------------------------------------------*
*        End the DIALOG                                               *
*---------------------------------------------------------------------*
         ITRACE ID=ENDD,                                               +
               DATA1=DISCOMM_DIALOG_TOKEN
         IEWBIND FUNC=ENDD,                                            +
               VERSION=5,                                              +
               DIALOG=DISCOMM_DIALOG_TOKEN,                            +
               RETCODE=DXD_RETURN_CODE,                                +
               RSNCODE=DXD_REASON_CODE,                                +
               MF=(E,DXD_IEWBIND,COMPLETE)
         ITRACE ID=ENDD_RC,                                            +
               DATA1=DXD_RETURN_CODE,                                  +
               DATA2=DXD_REASON_CODE
         OC    DXD_REASON_CODE,DXD_REASON_CODE
         BNZ   ERR0060
         B     EXIT0000
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
ERR0010  DS    0H
         MVI   COMM_MSG_ID+1,1
         B     ERR0070
ERR0020  DS    0H
         MVI   COMM_MSG_ID+1,2
         B     ERR0070
ERR0030  DS    0H
         MVI   COMM_MSG_ID+1,3
         B     ERR0070
ERR0040  DS    0H
         MVI   COMM_MSG_ID+1,4
         B     ERR0070
ERR0050  DS    0H
         MVI   COMM_MSG_ID+1,5
         B     ERR0070
ERR0060  DS    0H
         MVI   COMM_MSG_ID+1,6
ERR0070  DS    0H
         MVI   COMM_MSG_ID,0
         ITRACE ID=ERROR,                                              +
               DATA1=(COMM_MSG_ID,2)
         MVC   COMM_INFO_01(4),DXD_RETURN_CODE
         MVC   COMM_INFO_02(4),DXD_REASON_CODE
         MVC   COMM_MSG_CSECT,MODID
         L     R15,COMM_V_OSMSG
         BALR  R14,R15
         MVC   SPF_MSG_1,COMM_MSG_1
         MVC   SPF_MSG_2,COMM_MSG_2
         MVC   SPF_MSG_3,COMM_MSG_3
         MVC   SPF_MSG_4,COMM_MSG_4
         MVC   SPF_MSG_5,COMM_MSG_5
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
EXIT0000 DS    0H
         ITRACE ID=EXIT
         SR    R15,R15
         IC    R15,DXD_RC                     SET RC
         L     R13,4(,R13)                    RESTORE REGISTER 13       ASE01670
         L     R14,12(,R13)                   RESTORE R14               ASE01680
         LM    R0,R12,20(R13)                 RESTORE REGISTERS         ASE01690
         BR    R14                            RETURN TO CALLER          ASE01700
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
DXD_START     DC   Q(DXDBIND)
V_DISNAME     DC   V(DISNAME)
V_DISESD      DC   V(DISESD)
V_DISRLD      DC   V(DISRLD)
V_DISTEXT     DC   V(DISTEXT)

LIST          DC   H'4',C'LIST'
ALL           DC   H'3',C'ALL'

              IEWBIND MF=(L,IEWBIND_I)
IEWBIND_L     EQU  *-IEWBIND_I

*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
DXDBIND          DSECT
                 COPY     DXDPREF
DXD_RETURN_CODE  DS       F
DXD_REASON_CODE  DS       F

DXD_IEWBIND      DS       0F,(IEWBIND_L)X

DXD_RC           DS       X
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
                 DISCOMM  TYPE=DSECT
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
                 COMMON
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
                 SESSION  TYPE=DSECT
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
                 DISASMDA
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
                 OSSPFD   TYPE=DSECT
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
                 BPXYSTAT
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
                 COPY     TRENTRY
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
                 COPY     REGEQU
                 END      DISBIND
