         MACRO
         SVCDEF &SVCNBR,&DESC
         LCLA   &LEN
&LEN     SETA   K'&DESC-2
         DC     AL2(&LEN+5)          TOTAL ENTRY LENGTH
         DC     X'&SVCNBR'           SVC NUMBER
         DC     AL2(&LEN)            DESCRIPTION'S LENGTH
         DC     C&DESC               DESCRIPTION
         MEND
*--------------------------------------------------------------------*
*                                                                    *
*  Module name: GENRR2                                               *
*                                                                    *
*  Function:                                                         *
*   Generate RR type instructions                                    *
*                                                                    *
*  At entry R7 is address of OPCODE table entry                      *
*           R5 is address of instruction                             *
*           R4 is address of OPCODE entry                            *
*                                                                    *
*--------------------------------------------------------------------*
GENRR2   CSECT
GENRR2   AMODE 31
GENRR2   RMODE ANY
         USING GENRR2,R15
         USING OSCOMM,R12
         USING SESSION,R11
         USING DISCOMM,R9
         B     MOD0000                        SKIP EYECATCHER
MODID    DC    CL8'GENRR2'
         DC    C'&SYSDATE'
         DC    C'&SYSTIME'
MOD0000  DS    0H
         STM   R14,R12,12(R13)                SAVE REGS
         LR    R10,R15                        SET BASE REG
         DROP  R15
         USING GENRR2,R10
         L     R15,SESS_DXD_ADDR              WORK AREA
         A     R15,DXD_START                  PLUS DISP
         ST    R13,4(,R15)
         ST    R15,8(,R13)
         LR    R13,R15                        SET SAVE AREA ADDRESS
         USING DXDRR2,R13
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY,                                              +
               RDATA1=R7,                                              +
               DATA2=0(R5)
         MVC   DXD_EDIT_PAD,COMM_BLANKS
         SR    R1,R1                          CLEAR REGISTER
         IC    R1,1(,R5)                      OPERAND
         CVD   R1,COMM_DWORD                  CONVERT TO DECIMAL
         MVC   DXD_EDIT_WORK,EDITWORD         INITIALIZE EDIT WORD
         ED    DXD_EDIT_WORK,COMM_DWORD+6     EDIT
         LA    R1,DXD_EDIT_WORK
GEN0010  DS    0H
         CLI   0(R1),C' '                     BLANK?
         BNE   GEN0020                        NO
         LA    R1,1(,R1)                      NEXT
         B     GEN0010
GEN0020  DS    0H
         MVC   DISCOMM_SOURCE_OPERAND1(3),0(R1) COPY NBR
         CLI   0(R5),X'0A'                    SVC?
         BNE   EXIT0000                       NO
         LA    R2,SVC_DESC                    1ST DESCRIPTION
         USING SVC_DSECT,R2
GEN0030  DS    0H
         CLC   SVC_NUMBER,1(R5)               SVC LOCATED?
         BE    GEN0040                        YES
         AH    R2,SVC_LENGTH                  NEXT DESCRIPTION
         CLI   0(R2),X'FF'                    END OF TABLE?
         BNE   GEN0030                        NO
         B     EXIT0000
GEN0040  DS    0H
         SR    R1,R1
         ICM   R1,3,SVC_COMMENT_LENGTH        LENGTH OF COMMENT
         BCTR  R1,0
         EX    R1,MVC_01
EXIT0000 DS    0H
         ITRACE ID=EXIT
         L     R13,4(,R13)                    RESTORE REGISTER 13       ASE01670
         LM    R14,R12,12(R13)                RESTORE OTHER REGISTERS   ASE01680
         SR    R15,R15                        RETURN CODE               ASE01690
         BR    R14                            RETURN TO CALLER          ASE01700
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
MVC_01   MVC   DISCOMM_SOURCE_OPERAND1+20(0),SVC_COMMENT
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
DXD_START      DC   Q(DXDRR2)
EDITWORD       DC   X'40202120'

SVC_DESC       DS   0C
               SVCDEF 00,'EXCP/XDAP'
               SVCDEF 01,'WAIT/WAITR/PRTOV'
               SVCDEF 02,'POST'
               SVCDEF 03,'EXIT'
               SVCDEF 04,'GETMAIN'
               SVCDEF 05,'FREEMAIN'
               SVCDEF 06,'LINK/LINKX'
               SVCDEF 07,'XCTL/XCTLX'
               SVCDEF 08,'LOAD'
               SVCDEF 09,'DELETE'
               SVCDEF 0A,'GETMAIN/FREEMAIN'
               SVCDEF 0B,'TIME'
               SVCDEF 0C,'SYNCH/SYNCHX'
               SVCDEF 0D,'ABEND'
               SVCDEF 0E,'SPIE'
               SVCDEF 0F,'ERREXCP'
               SVCDEF 10,'PURGE'
               SVCDEF 11,'RESTORE'
               SVCDEF 12,'BLDL/FIND (TYPE D)'
               SVCDEF 13,'OPEN'
               SVCDEF 14,'CLOSE'
               SVCDEF 15,'STOW'
               SVCDEF 16,'OPEN (TYPE=J)'
               SVCDEF 17,'CLOSE (TYPE=T)'
               SVCDEF 18,'DEVTYPE'
               SVCDEF 19,'TRKBAL'
               SVCDEF 1A,'CATALOG/INDEX/LOCATE'
               SVCDEF 1B,'OBTAIN'
               SVCDEF 1D,'SCRATCH'
               SVCDEF 1E,'RENAME'
               SVCDEF 1F,'FEOV'
               SVCDEF 20,'ALLOC'
               SVCDEF 21,'IOHALT'
               SVCDEF 22,'MGCR/QEDIT'
               SVCDEF 23,'WTO/WTOR'
               SVCDEF 24,'WTL'
               SVCDEF 25,'SEGLD/SEGWT'
               SVCDEF 25,'SEGLD/SEGWT'
               SVCDEF 27,'LABEL'
               SVCDEF 28,'EXTRACT'
               SVCDEF 29,'IDENTIFY'
               SVCDEF 2A,'ATTACH/ATTACHX'
               SVCDEF 2B,'CIRB'
               SVCDEF 2C,'CHAP'
               SVCDEF 2D,'OVLYBRCH'
               SVCDEF 2E,'TIMER'
               SVCDEF 2F,'STIMER'
               SVCDEF 30,'DEQ'
               SVCDEF 33,'SNAP/SNAPX/SDUMP/SDUMPX'
               SVCDEF 34,'RESTART'
               SVCDEF 35,'RELEX'
               SVCDEF 36,'DISABLE'
               SVCDEF 37,'EOV'
               SVCDEF 38,'ENQ/RESERVE'
               SVCDEF 39,'FREEDBUF'
               SVCDEF 3A,'RELBUF/REQBUF'
               SVCDEF 3B,'OLTEP'
               SVCDEF 3C,'STAE/STAI-ESTAE/ESTAI'
               SVCDEF 3D,'IKJEGS6A'
               SVCDEF 3E,'DETACH'
               SVCDEF 3F,'CHKPT'
               SVCDEF 40,'RDJFCB'
               SVCDEF 42,'BTAMTEST'
               SVCDEF 44,'SYNADF/SYNADRLS'
               SVCDEF 45,'BSP'
               SVCDEF 46,'GSERV'
               SVCDEF 47,'ASGNBFR/BUFINQ/RLSEBFR'
               SVCDEF 49,'SPAR'
               SVCDEF 4A,'DAR'
               SVCDEF 4B,'DQUEUE'
               SVCDEF 4C,'IFBSTAT'
               SVCDEF 4E,'LSPACE'
               SVCDEF 4F,'STATUS'
               SVCDEF 51,'SETPRT'
               SVCDEF 53,'SMFWTM'
               SVCDEF 54,'GRAPHICS'
               SVCDEF 55,'DDRSWAP'
               SVCDEF 56,'ATLAS'
               SVCDEF 57,'DOM'
               SVCDEF 5B,'VOLSTAT'
               SVCDEF 5C,'TCPEXCP'
               SVCDEF 5D,'TGET/TPUT'
               SVCDEF 5E,'TGET/TPUT'
               SVCDEF 5F,'SYSEVENT'
               SVCDEF 60,'STAX'
               SVCDEF 61,'IKJEGS9G'
               SVCDEF 62,'PROTECT'
               SVCDEF 63,'DYNALLOC'
               SVCDEF 64,'IKJEFFIB'
               SVCDEF 65,'QTIP'
               SVCDEF 66,'AQCTL'
               SVCDEF 67,'XLATE'
               SVCDEF 68,'TOPCTL'
               SVCDEF 69,'IMGLIB'
               SVCDEF 6B,'MODESET'
               SVCDEF 70,'PGRLSE'
               SVCDEF 71,'PGFIX/PGFREE/PGLOAD/PGOUT'
               SVCDEF 72,'EXCPVR'
               SVCDEF 75,'DEBCHK'
               SVCDEF 77,'TESTAUTH'
               SVCDEF 78,'GETMAIN/FREEMAIN'
               SVCDEF 79,'VSAM'
               SVCDEF 7B,'PURGEDQ'
               SVCDEF 7C,'TPIO'
               SVCDEF 7D,'EVENTS'
               SVCDEF 7E,'MSS(ICB2SVC)'
               SVCDEF 82,'RACHECK'
               SVCDEF 83,'RACINIT'
               SVCDEF 84,'RACLIST'
               SVCDEF 85,'RACDEF'
               SVCDEF 89,'ESR'
               SVCDEF 8A,'PGSER'
               SVCDEF 8B,'CVAFDIR'
               DC     X'FF'
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
DXDRR2              DSECT
                    COPY DXDPREF
DXD_EDIT_WORK       DS   CL4
DXD_EDIT_PAD        DS   CL4
*---------------------------------------------------------------------*
*                                                                     *
*              SVC DSECT                                              *
*                                                                     *
*---------------------------------------------------------------------*
SVC_DSECT           DSECT
SVC_LENGTH          DS   XL2         LENGTH OF THIS SVC DEFINITION
SVC_NUMBER          DS   X           SVC NUMBER
SVC_COMMENT_LENGTH  DS   XL2         LENGTH OF DESCRIPTION
SVC_COMMENT         DS   0C          DESCRIPTION
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 DISCOMM  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 DISASMDA
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 BPXYSTAT DSECT=YES,LIST=NO
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 OSSPFD   TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COPY     REGEQU
               END  GENRR2
