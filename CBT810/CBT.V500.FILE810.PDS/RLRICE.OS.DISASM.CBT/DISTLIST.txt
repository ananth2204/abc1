* ------------------------------------------------------------------- *
*                                                                     *
*  Module name: DISTLIST                                              *
*                                                                     *
*  Function:                                                          *
*   Display the module text                                           *
*                                                                     *
* ------------------------------------------------------------------- *
DISTLIST CSECT
DISTLIST AMODE 31
DISTLIST RMODE ANY
         USING DISTLIST,R15
         B     MOD0000                        SKIP EYECATCHER
MODID    DC    CL8'DISTLIST'
         DC    C'&SYSDATE'
         DC    C'&SYSTIME'
MOD0000  DS    0H
         STM   R14,R12,12(R13)                SAVE REGS
         LR    R10,R15                        SET BASE REG
         DROP  R15
         USING OSCOMM,R12
         USING SESSION,R11
         USING DISTLIST,R10
         USING DISCOMM,R9
         L     R15,SESS_DXD_ADDR
         A     R15,DXD_START
         ST    R13,4(,R15)
         ST    R15,8(,R13)
         LR    R13,R15                        SET SAVE AREA ADDRESS
         USING DXDTLIST,R13
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY
         L     R8,COMM_OSSPFD
         USING OSSPFD,R8
         USING V_DETAIL,R6
         XC    DXD_START_DISP,DXD_START_DISP
* ------------------------------------------------------------------- *
*        Build text                                                   *
* ------------------------------------------------------------------- *
LIST0100 DS    0H
         ITRACE ID=BUILD
         L     R0,DISCOMM_VDATA_ADDR          VDATA ADDR
         L     R1,DISCOMM_VDATA_LENGTH        VDATA LENGTH
         SR    R14,R14
         SR    R15,R15
         MVCL  R0,R14                         INITIALIZE VDATA
         L     R6,DISCOMM_VDATA_ADDR          VDATA ADDR
         L     R7,SPF_VDEPTH                  NBR OF LINES
         MVC   DXD_DISP,DXD_START_DISP        COPY STARTING DISP
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
LIST0110 DS    0H
         ITRACE ID=DETAIL,                                             +
               DATA1=(DXD_DISP,4),                                     +
               DATA2=(DISCOMM_CSECT_LENGTH,4)
         MVI   V_ATTR1,$SCREEN_ATTR_HIGH      SET ATTRIBUTE
         MVI   V_ATTR2,$SCREEN_ATTR_NORMAL    SET ATTRIBUTE
         MVI   V_ATTR3,$SCREEN_ATTR_HIGH      SET ATTRIBUTE
         MVI   V_ATTR4,$SCREEN_ATTR_NORMAL    SET ATTRIBUTE
         MVI   V_ATTR5,$SCREEN_ATTR_HIGH      SET ATTRIBUTE
         MVI   V_ASTERISK_1,C'*'
         MVI   V_ASTERISK_2,C'*'
         L     R5,DISCOMM_CSECT_TEXT          TEXT
         A     R5,DXD_DISP                    PLUS DISP
         L     R2,DISCOMM_CSECT_LENGTH        CSECT LENGTH
         S     R2,DXD_DISP                    LENGTH REMAINING
         CH    R2,H16                         MORE THAN 16?
         BNH   LIST0120                       NO
         LH    R2,H16                         LIMIT TO 16
LIST0120 DS    0H
         BCTR  R2,0                           CONVERT TO INSTR LENGTH
         MVC   DXD_HEX(DXD_HEX_L),COMM_BLANKS
         XC    COMM_INFO_01(32),COMM_INFO_01  INITIALIZE WORK AREA
         UNPK  V_DISP(9),DXD_DISP(5)          UNPACK DISP
         TR    V_DISP,COMM_HEXCHAR            TRANSLATE
         MVI   V_DISP+8,C' '
         EX    R2,MVC_01                      COPY TEXT
         EX    R2,MVC_02                      COPY TEXT
         EX    R2,TR_01                       TRANSLATE UNPRINTABLES
         UNPK  COMM_INFO_02(9),COMM_INFO_01+00(5)
         TR    COMM_INFO_02(8),COMM_HEXCHAR
         MVC   DXD_HEX_01,COMM_INFO_02
         UNPK  COMM_INFO_02(9),COMM_INFO_01+04(5)
         TR    COMM_INFO_02(8),COMM_HEXCHAR
         MVC   DXD_HEX_02,COMM_INFO_02
         UNPK  COMM_INFO_02(9),COMM_INFO_01+08(5)
         TR    COMM_INFO_02(8),COMM_HEXCHAR
         MVC   DXD_HEX_03,COMM_INFO_02
         UNPK  COMM_INFO_02(9),COMM_INFO_01+12(5)
         TR    COMM_INFO_02(8),COMM_HEXCHAR
         MVC   DXD_HEX_04,COMM_INFO_02
         LA    R1,LENGTH_TABLE(R2)
         SR    R3,R3
         IC    R3,0(R1)
         EX    R3,MVC_03
         A     R6,SPF_VWIDTH                  NEXT LINE
         BCT   R7,LIST0130
         B     LIST0150                       SCREEN IS FULL
LIST0130 DS    0H
         L     R2,DXD_DISP
         LA    R2,16(,R2)
         ST    R2,DXD_DISP
         C     R2,DISCOMM_CSECT_LENGTH
         BL    LIST0110
* ------------------------------------------------------------------- *
*        Display                                                      *
* ------------------------------------------------------------------- *
LIST0150 DS    0H
         ITRACE ID=DISPLAY
         MVC   SESS_DISP_PANEL,PANEL_DISTLIST SET PANEL NAME
         MVC   SESS_DISP_VDATA_ADDR,DISCOMM_VDATA_ADDR
         MVC   SESS_DISP_VDATA_LENGTH,DISCOMM_VDATA_LENGTH
         L     R15,COMM_V_OSDISP              DISDISP ENTRY POINT
         BALR  R14,R15                        DISPLAY DATA
         ITRACE ID=DISP_RC,                                            +
               RDATA1=R15
         LTR   R15,R15                        RC = 0?
         BNZ   EXIT0000                       NO
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         ITRACE ID=ZCMD,                                               +
               DATA1=SPF_ZCMD
         CLC   SPF_ZCMD,COMM_BLANKS           ZCMD BLANK?
         BNE   ERR0010                        NO
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         ITRACE ID=ZVERB,                                              +
               DATA1=SPF_ZVERB,                                        +
               DATA2=SPF_ZSCROLLN
         CLI   SPF_ZVERB,C'B'                 BOTTOM?
         BE    LIST0200                       YES
         CLI   SPF_ZVERB,C'D'                 DOWN?
         BE    LIST0210                       YES
         CLI   SPF_ZVERB,C'T'                 TOP?
         BE    LIST0230                       YES
         CLI   SPF_ZVERB,C'U'                 UP?
         BE    LIST0240                       YES
         B     ERR0020
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
LIST0200 DS    0H
         ITRACE ID=BOTTOM
         L     R1,DISCOMM_CSECT_LENGTH        CSECT'S LENGTH
         CH    R1,H16                         MORE THAN 16 BYTES?
         BL    LIST0230                       NO
         SH    R1,H16                         MINUS 1 LINE WORTH
         SRL   R1,4                           SHIFT
         SLL   R1,4                           SHIFT
         ST    R1,DXD_START_DISP              SET START DISP
         B     LIST0100                       REBUILD DISPLAY
LIST0210 DS    0H
         ITRACE ID=DOWN
         CLI   SPF_ZSCROLLA,C'M'              MAX?
         BE    LIST0200                       YES
         L     R1,SPF_ZSCROLLN                NBR OF LINES
         SLL   R1,4                           MULTIPLY BY 16
         A     R1,DXD_START_DISP              PLUS START DISP
         C     R1,DISCOMM_CSECT_LENGTH        AT OR BEYOND END?
         BH    LIST0200                       BEYOND END
         BNE   LIST0220                       NOT AT END
         SH    R1,H16                         MINUS 1 LINE WORTH
LIST0220 DS    0H
         ST    R1,DXD_START_DISP              SET START DISP
         B     LIST0100                       REBUILD DISPLAY
LIST0230 DS    0H
         ITRACE ID=TOP
         XC    DXD_START_DISP,DXD_START_DISP  SET START DISP
         B     LIST0100                       REBUILD DISPLAY
LIST0240 DS    0H
         ITRACE ID=UP
         CLI   SPF_ZSCROLLA,C'M'              MAX?
         BE    LIST0230                       YES
         L     R0,DXD_START_DISP              STARTING DISP
         L     R1,SPF_ZSCROLLN                NBR OF LINES
         SLL   R1,4                           MULTIPLY BY 16
         SR    R0,R1                          SUBTRACT SCROLL AMOUNT
         BM    LIST0230                       NEGATIVE.. LIMIT TO TOP
         ST    R0,DXD_START_DISP              SET STARTING DISP
         B     LIST0100                       REBUILD DISPLAY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0010  DS    0H
         MVI   COMM_MSG_ID+1,1
         B     ERR0100
ERR0020  DS    0H
         MVI   COMM_MSG_ID+1,2
ERR0100  DS    0H
         MVI   COMM_MSG_ID,0
         MVC   COMM_MSG_CSECT,MODID
         L     R15,COMM_V_OSMSG
         BALR  R14,R15
         MVC   SPF_MSG_1,COMM_MSG_1
         MVC   SPF_MSG_2,COMM_MSG_2
         MVC   SPF_MSG_3,COMM_MSG_3
         MVC   SPF_MSG_4,COMM_MSG_4
         MVC   SPF_MSG_5,COMM_MSG_5
         B     LIST0150
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=EXIT
         L     R13,4(,R13)                    RESTORE REGISTER 13       ASE01670
         LM    R14,R12,12(R13)                RESTORE OTHER REGISTERS   ASE01680
         SR    R15,R15                        GIVE GOOD RETURN CODE     ASE01690
         BR    R14                            RETURN TO CALLER          ASE01700
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MVC_01   MVC   V_CHAR(0),0(R5)                COPY TEXT
MVC_02   MVC   COMM_INFO_01(0),0(R5)          COPY TEXT
MVC_03   MVC   V_HEX(0),DXD_HEX               COPY HEX
TR_01    TR    V_CHAR(0),PRINTABLE
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXD_START         DC   Q(DXDTLIST)

H16               DC   H'16'

PANEL_DISTLIST    DC   CL8'DISTLIST'

LENGTH_TABLE      DS   0C
                  DC   AL1(001)       1
                  DC   AL1(003)       2
                  DC   AL1(005)       3
                  DC   AL1(007)       4
                  DC   AL1(010)       5
                  DC   AL1(012)       6
                  DC   AL1(014)       7
                  DC   AL1(016)       8
                  DC   AL1(019)       9
                  DC   AL1(021)      10
                  DC   AL1(023)      11
                  DC   AL1(025)      12
                  DC   AL1(028)      13
                  DC   AL1(030)      14
                  DC   AL1(032)      15
                  DC   AL1(034)      16

PRINTABLE         DC   256C'.'
                  ORG  PRINTABLE+X'40'
                  DC   C' '
                  ORG  PRINTABLE+X'4A'
                  DC   C'¢.<(+¦'
                  DC   X'50'
                  ORG  PRINTABLE+X'5A'
                  DC   C'!$*);¬-/'
                  ORG  PRINTABLE+X'6B'
                  DC   C',%_>?'
                  ORG  PRINTABLE+X'7A'
                  DC   C':#@'
                  DC   X'7D'
                  DC   C'="'
                  ORG  PRINTABLE+X'C0'
                  DC   C'{ABCDEFGHI'
                  ORG  PRINTABLE+X'D0'
                  DC   C'}JKLMNOPQR'
                  ORG  PRINTABLE+X'E2'
                  DC   C'STUVWXYZ'
                  ORG  PRINTABLE+X'F0'
                  DC   C'0123456789'
                  ORG  PRINTABLE+256
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDTLIST          DSECT
                  COPY DXDPREF

DXD_START_DISP    DS   F
DXD_DISP          DS   F

DXD_HEX           DS   0C
DXD_HEX_01        DS   CL8
                  DS   X
DXD_HEX_02        DS   CL8
                  DS   X
DXD_HEX_03        DS   CL8
                  DS   X
DXD_HEX_04        DS   CL8
DXD_HEX_L         EQU  *-DXD_HEX
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
V_DETAIL          DSECT
V_ATTR1           DS   X
V_DISP            DS   CL8
                  DS   CL3
V_ATTR2           DS   X
V_HEX             DS   CL35
                  DS   CL3
V_ATTR3           DS   X
V_ASTERISK_1      DS   C
V_ATTR4           DS   X
V_CHAR            DS   CL16
V_ATTR5           DS   X
V_ASTERISK_2      DS   C

* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 DISCOMM  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 DISASMDA
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 BPXYSTAT DSECT=YES,LIST=NO
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 OSSPFD   TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COPY     ATTRS
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COPY     REGEQU
                 END  DISTLIST
