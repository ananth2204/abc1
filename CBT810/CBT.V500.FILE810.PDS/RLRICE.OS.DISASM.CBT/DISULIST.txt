* ------------------------------------------------------------------- *
*                                                                     *
*  MODULE NAME: DISULIST                                              *
*                                                                     *
*  FUNCTION:                                                          *
*    Display/allow definition of USINGs                               *
*                                                                     *
*                                                                     *
*  There are two ways for DSECT information to be made available.     *
*  One is via the batch utility DISASMU1, the other is by supplying   *
*  assembler source (via the "A" main menu option).                   *
*                                                                     *
*  The batch utility stores the information from a processing         *
*  DSECTs in VSAM KSDSes.                                             *
*                                                                     *
*  Information from DSECTs processed via the "A" main menu option     *
*  is stored in in-storage control block chains.                      *
*                                                                     *
*                                                                     *
*                                                                     *
*  When the user wants to define a USING                              *
*   They will be shown a list of the pre-processed DSECT info         *
*   data sets (from the 'K' main menu option)                         *
*                                                                     *
*   They may select one of the pre-processed DSECT info KSDSes        *
*   or the DSECTs defined via the 'A' main menu option.               *
*                                                                     *
*                                                                     *
*   When the user selects the KSDS or to use DSECTs from the 'A'      *
*   option, they will be shown the names of the DSECTs available.     *
*                                                                     *
*   The user may choose to use the DSECT itself (the base register    *
*   for the DSECT references the first byte of the DSECT) or they     *
*   may select a label within the DSECT (in case the base register    *
*   does not refer to the first byte of the DSECT.                    *
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
DISULIST CSECT
DISULIST AMODE 31
DISULIST RMODE ANY
         USING DISULIST,R15
         B     MOD0000                           SKIP EYECATCHER
MODID    DC    CL8'DISULIST'
         DC    C'&SYSDATE'
         DC    C'&SYSTIME'
MOD0000  DS    0H
         STM   R14,R12,12(R13)                   SAVE REGS
         LR    R10,R15                           SET BASE REG
         DROP  R15
         USING OSCOMM,R12
         USING SESSION,R11
         USING DISULIST,R10
         USING DISCOMM,R9
         USING USING_DSECT,R7
         USING KSDS_DSECT,R5
         USING DSECT_DSECT,R4
         USING LABEL_DSECT,R3
         L     R15,SESS_DXD_ADDR                 WORK AREA
         A     R15,DXD_START                     PLUS DISP
         ST    R13,4(,R15)
         ST    R15,8(,R13)
         LR    R13,R15
         USING DXDULIST,R13
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY
         L     R8,COMM_OSSPFD
         USING OSSPFD,R8
         SR    R0,R0
         L     R1,SPF_VDEPTH                     NBR OF LINES
         LA    R1,1(,R1)                         PLUS 1
         D     R0,F5                             DIVIDE BY 5
         STH   R1,DXD_USINGS_PER_SCREEN          NBR OF USINGS PER
         ITRACE ID=PER_SCRN,                                           +
               RDATA1=R1
* ------------------------------------------------------------------- *
*                                                                     *
*        In order to allow the user to "cancel" changes,              *
*        first "copy" the existing USING chain.                       *
*                                                                     *
* ------------------------------------------------------------------- *
         XC    DXD_USING_NEXT,DXD_USING_NEXT
         XC    DXD_FIRST_USING_DISPLAYED,DXD_FIRST_USING_DISPLAYED
         LA    R2,DXD_USING_PREV
PREV     USING USING_DSECT,R2
         ICM   R7,15,DISCOMM_USING_NEXT          FIRST USING
         BZ    MAIN0000                          NO USINGS
INIT0010 DS    0H
         GETMAIN RU,                                                   +
               LV=USING_DSECT_L,                                       +
               LOC=ANY
         ITRACE ID=COPY_1,                                             +
               RDATA1=R1
NEW      USING USING_DSECT,R1
         MVC   NEW.USING_DSECT(USING_DSECT_L),USING_DSECT
         ST    R1,PREV.USING_NEXT
         ST    R2,NEW.USING_PREV
         LR    R2,R1
         ICM   R7,15,USING_NEXT
         BNZ   INIT0010
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0000 DS    0H
         ITRACE ID=MAIN0000
         MVC   DXD_FIRST_USING_DISPLAYED,DXD_USING_NEXT
         DROP  NEW,PREV
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0010 DS    0H
         ITRACE ID=INIT_V
         L     R0,DISCOMM_VDATA_ADDR
         L     R1,DISCOMM_VDATA_LENGTH
         SR    R14,R14
         SR    R15,R15
         MVCL  R0,R14                            INITIALIZE VDATA
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         L     R6,DISCOMM_VDATA_ADDR             VDATA ADDR
         LH    R2,DXD_USINGS_PER_SCREEN          NBR PER SCREEN
         XC    DXD_NBR_DISPLAYED,DXD_NBR_DISPLAYED
         ICM   R7,15,DXD_FIRST_USING_DISPLAYED   FIRST TO DISPLAY
         ITRACE ID=BUILD,                                              +
               RDATA1=R7,                                              +
               RDATA2=R2
         LTR   R7,R7                             ANY USINGS?
         BZ    MAIN0100                          NOTHING TO DISPLAY
MAIN0020 DS    0H
         ITRACE ID=USING,                                              +
               RDATA1=R7,                                              +
               DATA2=USING_PREV
         ST    R7,DXD_LAST_USING_DISPLAYED       SAVE ADDR
         LA    R1,1                              CONSTANT 1
         AH    R1,DXD_NBR_DISPLAYED              PLUS TOTAL SO FAR
         STH   R1,DXD_NBR_DISPLAYED              SAVE TOTAL
         MVC   0(LINE01_L,R6),LINE01
         USING LINE01,R6
         MVC   LINE01_NAME,USING_NAME
         DROP  R6
         A     R6,SPF_VWIDTH                     NEXT LINE

         MVC   0(LINE02_L,R6),LINE02
         USING LINE02,R6
         MVC   LINE02_BASE,USING_CHAR_REGISTER   REGISTER IN CHAR FORM
         UNPK  DXD_WORK(9),USING_BEGIN(5)
         MVC   LINE02_BEGIN,DXD_WORK
         TR    LINE02_BEGIN,COMM_HEXCHAR
         UNPK  DXD_WORK(9),USING_END(5)
         MVC   LINE02_END,DXD_WORK
         TR    LINE02_END,COMM_HEXCHAR
         A     R6,SPF_VWIDTH                     NEXT LINE
         DROP  R6

         MVC   0(LINE03_L,R6),LINE03
         USING LINE03,R6
         MVC   LINE03_DSECT(63),USING_DSECT_NAME DSECT NAME
         CLC   USING_DSECT_LABEL,COMM_BLANKS     LABEL IN DSECT?
         BNE   MAIN0050                          YES
         OC    USING_DISP,USING_DISP             DISP ZERO?
         BZ    MAIN0050                          YES
         UNPK  DXD_WORK(9),USING_DISP(5)         UNPACK DISP
         LA    R1,LINE03_DSECT+L'LINE03_DSECT
MAIN0030 DS    0H
         CLI   0(R1),C' '                        BLANK?
         BNE   MAIN0040                          NO
         BCT   R1,MAIN0030
MAIN0040 DS    0H
         MVI   1(R1),C'+'
         MVC   2(8,R1),DXD_WORK                  COPY UNPACKED DISP
         TR    2(8,R1),COMM_HEXCHAR              TRANSLATE
         DROP  R6

MAIN0050 DS    0H
         A     R6,SPF_VWIDTH                     NEXT LINE
         MVC   0(LINE04_L,R6),LINE04
         USING LINE04,R6
         CLC   USING_DSECT_LABEL,COMM_BLANKS     LABEL BLANK?
         BE    MAIN0080
         MVC   LINE04_LABEL(63),USING_DSECT_LABEL    LABEL NAME
         OC    USING_DISP,USING_DISP             DISP ZERO?
         BZ    MAIN0080
         UNPK  DXD_WORK(9),USING_DISP(5)         UNPACK DISP
         LA    R1,LINE04_LABEL+L'LINE04_LABEL
MAIN0060 DS    0H
         CLI   0(R1),C' '                        BLANK?
         BNE   MAIN0070                          NO
         BCT   R1,MAIN0060
MAIN0070 DS    0H
         MVI   1(R1),C'+'
         MVC   2(8,R1),DXD_WORK                  COPY UNPACKED DISP
         TR    2(8,R1),COMM_HEXCHAR              TRANSLATE
         DROP  R6

MAIN0080 DS    0H
         A     R6,SPF_VWIDTH

         A     R6,SPF_VWIDTH                     NEXT LINE
         BCT   R2,MAIN0090
         B     MAIN0100
MAIN0090 DS    0H
         ICM   R7,15,USING_NEXT
         BNZ   MAIN0020
* ------------------------------------------------------------------- *
*                                                                     *
*                                                                     *
*        Display the list of USING's                                  *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0100 DS    0H
         ITRACE ID=DISPLAY
         MVC   SESS_DISP_PANEL,PANEL_NAME1       SET PANEL NAME
         MVC   SESS_DISP_VDATA_ADDR,DISCOMM_VDATA_ADDR
         MVC   SESS_DISP_VDATA_LENGTH,DISCOMM_VDATA_LENGTH
         XC    SESS_DISP_CURSOR,SESS_DISP_CURSOR
         L     R15,COMM_V_OSDISP                 DISP ENTRY POINT
         BALR  R14,R15                           DISPLAY
         ITRACE ID=DISP_RC,                                            +
               RDATA1=R15,                                             +
               DATA2=SPF_ZVERB
         LTR   R15,R15                           RC = 0?
         BNZ   MAIN0600                          NO
         CLC   ABEND,SPF_ZCMD                    ABEND?
         BE    ABEND000                          YES
         CLC   CANCEL,SPF_ZCMD                   CANCEL?
         BE    MAIN9000                          YES
         CLC   CAN,SPF_ZCMD                      CAN(CEL)?
         BE    MAIN9000                          YES
         CLC   =C'NEW',SPF_ZCMD                  NEW?
         BE    MAIN0300                          YES
* ------------------------------------------------------------------- *
*        Check for delete request(s)                                  *
* ------------------------------------------------------------------- *
         L     R6,DISCOMM_VDATA_ADDR             VDATA ADDR
V        USING LINE01,R6                         DEFINE BASE
         LH    R2,DXD_USINGS_PER_SCREEN          NBR PER SCREEN
         ICM   R7,15,DXD_FIRST_USING_DISPLAYED   FIRST TO DISPLAY
         BZ    MAIN0100
         NI    DXD_FLAGS,255-$DXD_DELETE         RESET FLAG
MAIN0110 DS    0H
         ITRACE ID=CHK_DEL,                                            +
               RDATA1=R6,                                              +
               RDATA2=R2
         OI    V.LINE01_DELETE,C' '
         CLI   V.LINE01_DELETE,C' '              DELETE THIS USING?
         BE    MAIN0150                          NO
         MVI   V.LINE01_DELETE,C' '              RESET COMMAND
         LR    R1,R7                             1ST LINE IN VDATA
         L     R15,V_DISUDEL                     DISUDEL ENTRY POINT
         BALR  R14,R15                           CALL DISUDEL
         LTR   R15,R15                           CONTINUE WITH DELETE?
         BNZ   MAIN0150                          NO
         OI    DXD_FLAGS,$DXD_DELETE             SET FLAG
         ICM   R1,15,USING_PREV                  PREVIOUS USING?
         BZ    MAIN0120                          NO
PREV     USING USING_DSECT,R1                    DEFINE BASE
         MVC   PREV.USING_NEXT,USING_NEXT        CHAIN PREV TO NEXT
         DROP  PREV
MAIN0120 DS    0H
         ICM   R1,15,USING_NEXT                  NEXT USING?
         BZ    MAIN0130                          NO
NEXT     USING USING_DSECT,R1                    DEFINE BASE
         MVC   NEXT.USING_PREV,USING_PREV        CHAIN NEXT TO PREV
         DROP  NEXT
MAIN0130 DS    0H
         C     R7,DXD_USING_NEXT                 FIRST ON CHAIN?
         BNE   MAIN0140                          NO
         MVC   DXD_USING_NEXT,USING_PREV         CHANGE ADDRESS
         OC    DXD_USING_NEXT,DXD_USING_NEXT     PREVIOUS EXIST?
         BNZ   MAIN0140                          YES
         MVC   DXD_USING_NEXT,USING_NEXT         USE NEXT INSTEAD
MAIN0140 DS    0H
         ITRACE ID=FREEUSNG,                                           +
               RDATA1=R7,                                              +
               DATA2=USING_PREV
         LR    R1,R7                             COPY ADDRESS
         L     R7,USING_NEXT                     NEXT ON CHAIN
         FREEMAIN RU,                            FREE THIS USING BLOCK +
               A=(1),                                                  +
               LV=USING_DSECT_L
         B     MAIN0160
MAIN0150 DS    0H
         L     R7,USING_NEXT                     NEXT USING
MAIN0160 DS    0H
         A     R6,SPF_VWIDTH                     NEXT LINE (LINE02)
         A     R6,SPF_VWIDTH                     NEXT LINE (LINE03)
         A     R6,SPF_VWIDTH                     NEXT LINE (LINE04)
         A     R6,SPF_VWIDTH                     NEXT LINE (BLANK LINE)
         A     R6,SPF_VWIDTH                     NEXT LINE (LINE01)
         BCT   R2,MAIN0110
         TM    DXD_FLAGS,$DXD_DELETE             ANY DELETIONS?
         BO    MAIN0000                          YES
         ITRACE ID=NODELETE
         DROP  V
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         ITRACE ID=SCROLL,                                             +
               DATA1=SPF_ZVERB,                                        +
               DATA2=SPF_ZSCROLLA
         CLI   SPF_ZVERB,C'B'                    BOTTOM?
         BE    MAIN0200
         CLI   SPF_ZVERB,C'D'                    DOWN?
         BE    MAIN0220
         CLI   SPF_ZVERB,C'T'                    TOP?
         BE    MAIN0240
         CLI   SPF_ZVERB,C'U'                    UP?
         BE    MAIN0250
         B     MAIN0100
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0200 DS    0H
         ITRACE ID=BOTTOM
         ICM   R7,15,DXD_USING_NEXT              FIRST ON CHAIN
MAIN0210 DS    0H
         ST    R7,DXD_FIRST_USING_DISPLAYED      SAVE ADDRESS
         ICM   R7,15,USING_NEXT                  ANOTHER USING?
         BNZ   MAIN0210                          YES
         B     MAIN0010                          BUILD DISPLAY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0220 DS    0H
         ITRACE ID=DOWN
         CLI   SPF_ZSCROLLA,C'M'                 MAX?
         BE    MAIN0200                          YES
         L     R2,SPF_ZSCROLLN                   NBR OF LINES TO SCROLL
         L     R7,DXD_FIRST_USING_DISPLAYED      1ST ON DISPLAY
MAIN0230 DS    0H
         ST    R7,DXD_FIRST_USING_DISPLAYED      SAVE USING BLOCK ADDR
         ICM   R7,15,USING_NEXT                  ANOTHER USING?
         BZ    MAIN0010                          NO
         BCT   R2,MAIN0230                       LOOP
         B     MAIN0010
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0240 DS    0H
         ITRACE ID=TOP
         L     R7,DXD_USING_NEXT                 FIRST DATA ITEM
         B     MAIN0010
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0250 DS    0H
         ITRACE ID=UP1
         CLI   SPF_ZSCROLLA,C'M'                 MAX?
         BE    MAIN0240                          YES
         ICM   R7,15,DXD_FIRST_USING_DISPLAYED   FIRST ON DISPLAY
         BZ    EXIT0000                          SHOULD NOT HAPPEN!
         L     R2,SPF_ZSCROLLN                   LINES ON DISPLAY
MAIN0260 DS    0H
         ST    R7,DXD_FIRST_USING_DISPLAYED      COPY ADDR
         ICM   R7,15,USING_PREV                  PREVIOUS USING?
         BZ    MAIN0010                          NO
         BCT   R2,MAIN0260                       LOOP
         B     MAIN0010
* ------------------------------------------------------------------- *
*                                                                     *
*                                                                     *
*      Define new USING                                               *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0300 DS    0H
         ITRACE ID=NEW
         MVC   SPF_ZCMD,COMM_BLANKS
* ------------------------------------------------------------------- *
*                                                                     *
*      Display selection                                              *
*        1) User source (specified by option 'A')                     *
*        2) List of KSDSes (specified by option 'K')                  *
*                                                                     *
*      If RC=0, DISCOMM_USING_KSDS will be set to the KSDS selected   *
*      If RC=4, user selected the user defined (via option 'A')       *
*                                                                     *
* ------------------------------------------------------------------- *
         ITRACE ID=ULSTK
         L     R15,V_DISULSTK                    DISULSTK ENTRY POINT
         BALR  R14,R15                           DISPLAY LIST OF KSDSES
         ITRACE ID=ULSTK_RC,                                           +
               RDATA1=R15
         LTR   R15,R15                           KSDS SELECTED?
         BZ    MAIN0310                          YES
         CH    R15,H4                            ASSEMBLED DSECTS?
         BE    MAIN0330                          YES
         B     MAIN9000                          ABORT
* ------------------------------------------------------------------- *
*        User has selected a KSDS                                     *
* ------------------------------------------------------------------- *
MAIN0310 DS    0H
         ITRACE ID=KSDS,                         KSDS WAS SELECTED     +
               RDATA1=R5
         L     R5,DISCOMM_USING_KSDS             SELECTED KSDS
         USING KSDS_DSECT,R5                     DEFINE BASE
         TM    KSDS_FLAGS,$KSDS_DSECTS_READ      DSECTS READ?
         BO    MAIN0320                          YES
         L     R15,V_DISDREAD                    DSECT READER
         ITRACE ID=DREAD                         CALLING DSECT READER
         BALR  R14,R15                           READ DSECTS
         ITRACE ID=DREAD_RC,                                           +
               RDATA1=R15
MAIN0320 DS    0H
         MVC   DISCOMM_USING_DSECT,KSDS_DSECT_NEXT 1ST DSECT
         B     MAIN0340
* ------------------------------------------------------------------- *
*        Use user supplied DSECTs (from option A)                     *
* ------------------------------------------------------------------- *
MAIN0330 DS    0H
         SR    R5,R5                             NOT FROM KSDS
         TM    DISCOMM_FLAGS,$DISCOMM_ASSEMBLE_SOURCE
         BNO   MAIN0335                          SOURCE NOT CHANGED
         NI    DISCOMM_FLAGS,255-$DISCOMM_ASSEMBLE_SOURCE
         L     R15,V_DISASSEM                    DISASSEM ENTRY POINT
         ITRACE ID=CALL_ASM,                                           +
               RDATA1=R15
         BALR  R14,R15                           CALL DISASSEM
         ITRACE ID=ASM_RC,                                             +
               RDATA1=R15
         CH    R15,H4                            ERROR IN ASSEMBLY?
         BNH   MAIN0335                          NO
         MVC   SPF_MSG_1(MSG01_L),MSG01
         MVC   SPF_MSG_2(MSG02_L),MSG02
         B     EXIT0000
* ------------------------------------------------------------------- *
*        Display list of DSECTs                                       *
* ------------------------------------------------------------------- *
MAIN0335 DS    0H
         MVC   DISCOMM_USING_DSECT,DISCOMM_DSECT_NEXT 1ST DSECT
MAIN0340 DS    0H
         L     R15,V_DISULSTD                    DISPLAY DSECTS
         ITRACE ID=ULSTD
         BALR  R14,R15                           CALLING ULSTD
         ITRACE ID=ULSTD_RC,                                           +
               RDATA1=R15
         LTR   R15,R15                           DSECT SELECTED?
         BNZ   MAIN9000                          NO
         L     R4,DISCOMM_USING_DSECT
         LTR   R5,R5                             DATA FROM A KSDS?
         BZ    MAIN0350                          NO
         TM    DSECT_FLAGS,$DSECT_LABELS_READ    LABELS READ?
         BO    MAIN0350                          YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         L     R15,V_DISLREAD                    DISLREAD ENTRY POINT
         ITRACE ID=LREAD
         BALR  R14,R15                           CALL DISLREAD
         ITRACE ID=LREAD_RC,                                           +
               RDATA1=R15
         OI    DSECT_FLAGS,$DSECT_LABELS_READ    SET FLAG
* ------------------------------------------------------------------- *
*        Display list of labels in selected DSECT                     *
* ------------------------------------------------------------------- *
MAIN0350 DS    0H
         L     R15,V_DISULSTL                    DISPLAY LABELS
         ITRACE ID=ULSTL
         BALR  R14,R15                           CALLING ULSTL
         ITRACE ID=ULSTL_RC,                                           +
               RDATA1=R15
* ------------------------------------------------------------------- *
*        Display registers, start and end disp's                      *
* ------------------------------------------------------------------- *
MAIN0360 DS    0H
         MVC   SPF_REGISTER,COMM_BLANKS          INITIALIZE REGISTER
         MVC   SPF_BEGIN,C_ZERO                  INITIALIZE BEGIN DISP
         MVC   SPF_END,C_ZERO                    INITIALIZE END DISP
MAIN0370 DS    0H
         ITRACE ID=DISUREGD
         MVC   SPF_LABEL1,COMM_BLANKS            INITIALIZE LABEL NAME
         MVC   SESS_DISP_PANEL,PANEL_NAME2       SET PANEL NAME
         XC    SESS_DISP_VDATA_ADDR,SESS_DISP_VDATA_ADDR
         XC    SESS_DISP_VDATA_LENGTH,SESS_DISP_VDATA_LENGTH
         XC    SESS_DISP_CURSOR,SESS_DISP_CURSOR
         L     R15,COMM_V_OSDISP                 DISP ENTRY POINT
         BALR  R14,R15                           DISPLAY
         ITRACE ID=DISP_RC,                                            +
               RDATA1=R15
         LTR   R15,R15                           RC = 0?
         BNZ   MAIN0010                          NO
         CLC   ABEND,SPF_ZCMD                    ABEND?
         BE    ABEND000                          YES
* ------------------------------------------------------------------- *
*        Verify base register                                         *
* ------------------------------------------------------------------- *
         OC    SPF_REGISTER,COMM_BLANKS          REGISTER BLANK?
         ITRACE ID=VER_REG,                                            +
               DATA1=SPF_REGISTER
         CLC   SPF_REGISTER,COMM_BLANKS          REGISTER BLANK?
         BE    MAIN0440                          YES
         LA    R1,L'SPF_REGISTER-1               LOOP LIMITER
MAIN0380 DS    0H
         CLI   SPF_REGISTER,C' '                 BLANK?
         BNE   MAIN0390                          NO
         MVC   SPF_REGISTER(L'SPF_REGISTER-1),SPF_REGISTER+1
         BCT   R1,MAIN0380                       LOOP
MAIN0390 DS    0H
         LA    R1,REGISTER_TABLE
         USING REGISTER_DSECT,R1
MAIN0400 DS    0H
         CLI   0(R1),X'FF'                       END OF TABLE?
         BE    MAIN0450                          YES
         CLC   SPF_REGISTER,REGISTER_NAME        REGISTER LOCATED?
         BE    MAIN0410                          YES
         LA    R1,REGISTER_DSECT_L(,R1)          NEXT
         B     MAIN0400
MAIN0410 DS    0H
         MVC   DXD_REGISTER,REGISTER_REGISTER    SAVE REGISTER
         MVC   DXD_REGISTER_NAME,REGISTER_NAME   SAVE REGISTER NAME
* ------------------------------------------------------------------- *
*        Verify begin and end disps                                   *
* ------------------------------------------------------------------- *
         ITRACE ID=VER_BEG,                                            +
               DATA1=SPF_BEGIN
         LA    R1,SPF_BEGIN                      BEGIN DISP
         ST    R1,DXD_CONV_SOURCE
         LA    R1,DXD_BEGIN                      CONVERSION OUTPUT
         ST    R1,DXD_CONV_OUT
         LA    R1,DXD_CONV_PARMS                 CONVERSION PARMS
         L     R15,V_DISDCONV                    CONVERTER ENTRY POINT
         BALR  R14,R15
         LTR   R15,R15                           ERRORS?
         BNZ   MAIN0460                          YES
         ITRACE ID=VER_END,                                            +
               DATA1=SPF_END
         LA    R1,SPF_END                        END DISP
         ST    R1,DXD_CONV_SOURCE
         LA    R1,DXD_END                        CONVERSION OUTPUT
         ST    R1,DXD_CONV_OUT
         LA    R1,DXD_CONV_PARMS                 CONVERSION PARMS
         L     R15,V_DISDCONV                    CONVERTER ENTRY POINT
         BALR  R14,R15
         LTR   R15,R15                           ERRORS?
         BNZ   MAIN0470                          YES
         OC    DXD_END,DXD_END                   END DISP LEFT ZERO?
         BNZ   MAIN0415                          NO
         MVC   DXD_END,DISCOMM_CSECT_LENGTH
MAIN0415 DS    0H
         CLC   DXD_END,DXD_BEGIN                 END LESS THAN BEGIN?
         BL    MAIN0480
* ------------------------------------------------------------------- *
*        We've put it off long enough, build a new USING block        *
* ------------------------------------------------------------------- *
         GETMAIN RU,                             GET MAIN STORAGE      +
               LV=USING_DSECT_L,                                       +
               LOC=ANY
         LR    R7,R1                             COPY ADDRESS
         ICM   R1,15,DXD_USING_NEXT              FIRST ON CHAIN NOW
         BZ    MAIN0420                          NONE ON CHAIN
FIRST    USING USING_DSECT,R1
         ST    R7,FIRST.USING_PREV               SET PREV IN OLD 1ST
         DROP  FIRST
MAIN0420 DS    0H
         XC    USING_DSECT(USING_DSECT_L),USING_DSECT
         MVC   USING_NEXT,DXD_USING_NEXT         COPY FIRST ON CHAIN
         ST    R7,DXD_USING_NEXT                 ADD NEW TO CHAIN
         MVC   USING_EYE,USING_ID                IDENTIFY THE BLOCK
         MVC   USING_NAME,SPF_LABEL1             COPY NAME
         L     R4,DISCOMM_USING_DSECT            DSECT BLOCK
         ST    R4,USING_DSECT_ADDR               CHAIN USING TO DSECT
         MVC   USING_DSECT_NAME,DSECT_NAME       COPY DSECT NAME
         MVC   USING_DSECT_LABEL,COMM_BLANKS     INITIALIZE LABEL
         ICM   R3,15,DISCOMM_USING_LABEL         LABEL?
         BZ    MAIN0430                          NO
         ST    R3,USING_LABEL_ADDR               CHAIN USING TO LABEL
         MVC   USING_DSECT_LABEL,LABEL_NAME      COPY LABEL NAME
MAIN0430 DS    0H
         MVC   USING_BEGIN,DXD_BEGIN             COPY BEGIN DISP
         MVC   USING_END,DXD_END                 COPY END DISP
         MVC   USING_REGISTER,DXD_REGISTER       COPY REGISTER
         MVC   USING_CHAR_REGISTER,DXD_REGISTER_NAME
         ITRACE ID=NEW_USNG,                                           +
               RDATA1=R7
         B     MAIN0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0440 DS    0H
         ITRACE ID=NO_REG
         MVI   COMM_MSG_ID+1,1
         B     MAIN0500
MAIN0450 DS    0H
         ITRACE ID=BAD_REG
         MVI   COMM_MSG_ID+1,2
         B     MAIN0500
MAIN0460 DS    0H
         ITRACE ID=BAD_BEG
         MVI   COMM_MSG_ID+1,3
         B     MAIN0510
MAIN0470 DS    0H
         ITRACE ID=BAD_END
         MVI   COMM_MSG_ID+1,4
         B     MAIN0510
MAIN0480 DS    0H
         ITRACE ID=END_LOW
         MVI   COMM_MSG_ID+1,5
MAIN0500 DS    0H
         MVC   COMM_MSG_CSECT,MODID
         L     R15,COMM_V_OSMSG
         BALR  R14,R15
MAIN0510 DS    0H
         MVC   SPF_MSG_1,COMM_MSG_1
         B     MAIN0370
* ------------------------------------------------------------------- *
*                                                                     *
*        Normal exit..                                                *
*        .  Free the old USING chain                                  *
*        .  Replace old chain with new                                *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0600 DS    0H
         ITRACE ID=SWITCH
         ICM   R7,15,DISCOMM_USING_NEXT       FIRST ON 'OLD' CHAIN
         BZ    MAIN0620
MAIN0610 DS    0H
         LR    R1,R7                          COPY ADDRESS
         L     R7,USING_NEXT                  NEXT USING BLOCK
         FREEMAIN RU,                         FREE USING BLOCK         +
               A=(1),                                                  +
               LV=USING_DSECT_L
         LTR   R7,R7                          ANOTHER USING BLOCK?
         BNZ   MAIN0610                       YES
MAIN0620 DS    0H
         ITRACE ID=SWAP
         MVC   DISCOMM_USING_NEXT,DXD_USING_NEXT
         XC    DXD_USING_NEXT,DXD_USING_NEXT
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN9000 DS    0H
         ITRACE ID=ABORT
         ICM   R7,15,DXD_USING_NEXT           FIRST USING
         BZ    EXIT0000
MAIN9010 DS    0H
         LR    R1,R7                          COPY USING BLOCK ADDR
         L     R7,USING_NEXT                  NEXT USING ON CHAIN
         ITRACE ID=FREEUSNG,                                           +
               RDATA1=R1
         FREEMAIN RU,                                                  +
               A=(1),                                                  +
               LV=USING_DSECT_L
         LTR   R7,R7                          ANOTHER USING?
         BNZ   MAIN9010                       YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=EXIT
         L     R13,4(,R13)                    RESTORE REGISTER 13
         L     R14,12(,R13)                   RESTORE REGISTERS
         SR    R15,R15                        SET RC
         LM    R0,R12,20(R13)                 RESTORE REGISTERS
         BR    R14                            RETURN TO CALLER
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ABEND000 DS    0H
         ITRACE ID=ABEND
         DC    H'0'
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXD_START     DC   Q(DXDULIST)
V_DISASSEM    DC   V(DISASSEM)
V_DISDCONV    DC   V(DISDCONV)
V_DISDREAD    DC   V(DISDREAD)
V_DISLREAD    DC   V(DISLREAD)
V_DISUDEL     DC   V(DISUDEL)
V_DISULSTD    DC   V(DISULSTD)
V_DISULSTK    DC   V(DISULSTK)
V_DISULSTL    DC   V(DISULSTL)

F5            DC   F'5'
H4            DC   H'4'

ABEND         DC   C'ABEND '
CAN           DC   C'CAN '
CANCEL        DC   C'CANCEL '
C_ZERO        DC   CL8'00000000'
PANEL_NAME1   DC   CL8'DISULIST'
PANEL_NAME2   DC   CL8'DISUREGD'
USING_ID      DC   CL8'USING'

MSG01         DS   0C
              DC   C'DISULIST01E '
              DC   C'Error(s) assembling source'
MSG01_L       EQU  *-MSG01

MSG02         DS   0C
              DC   C'DISULIST02E '
              DC   C'Correct assembler source or SYSLIBs'
MSG02_L       EQU  *-MSG02

              LTORG

LINE01        DS   0C
              DC   AL1($SCREEN_ATTR_INPUT)
LINE01_DELETE DC   C' '
              DC   AL1($SCREEN_ATTR_HIGH)
              DC   C'Name '
              DC   AL1($SCREEN_ATTR_HIGH_TURQUOISE)
LINE01_NAME   DC   CL63' '
LINE01_L      EQU  *-LINE01

LINE02        DS   0C
              DC   AL1($SCREEN_ATTR_HIGH)
              DC   C'  Base '
              DC   AL1($SCREEN_ATTR_HIGH_TURQUOISE)
LINE02_BASE   DC   CL3' '
              DC   AL1($SCREEN_ATTR_HIGH)
              DC   C'       In effect from '
              DC   AL1($SCREEN_ATTR_HIGH_GREEN)
LINE02_BEGIN  DC   CL8' '
              DC   AL1($SCREEN_ATTR_HIGH)
              DC   C' to '
              DC   AL1($SCREEN_ATTR_HIGH_GREEN)
LINE02_END    DC   CL8' '
LINE02_L      EQU  *-LINE02

LINE03        DS   0C
              DC   AL1($SCREEN_ATTR_HIGH)
              DC   C'  DSECT'
              DC   AL1($SCREEN_ATTR_HIGH_TURQUOISE)
LINE03_DSECT  DC   CL71' '
LINE03_L      EQU  *-LINE03

LINE04        DS   0C
              DC   AL1($SCREEN_ATTR_HIGH)
              DC   C'  LABEL'
              DC   AL1($SCREEN_ATTR_HIGH_TURQUOISE)
LINE04_LABEL  DC   CL71' '
LINE04_L      EQU  *-LINE04

REGISTER_TABLE  DS    0C
                DC    CL3'R0 ',X'00'
                DC    CL3'R1 ',X'01'
                DC    CL3'R2 ',X'02'
                DC    CL3'R3 ',X'03'
                DC    CL3'R4 ',X'04'
                DC    CL3'R5 ',X'05'
                DC    CL3'R6 ',X'06'
                DC    CL3'R7 ',X'07'
                DC    CL3'R8 ',X'08'
                DC    CL3'R9 ',X'09'
                DC    CL3'R10',X'0A'
                DC    CL3'R11',X'0B'
                DC    CL3'R12',X'0C'
                DC    CL3'R13',X'0D'
                DC    CL3'R14',X'0E'
                DC    CL3'R15',X'0F'
                DC    CL3'R00',X'00'
                DC    CL3'R01',X'01'
                DC    CL3'R02',X'02'
                DC    CL3'R03',X'03'
                DC    CL3'R04',X'04'
                DC    CL3'R05',X'05'
                DC    CL3'R06',X'06'
                DC    CL3'R07',X'07'
                DC    CL3'R08',X'08'
                DC    CL3'R09',X'09'
                DC    CL3'RA ',X'0A'
                DC    CL3'RB ',X'0B'
                DC    CL3'RC ',X'0C'
                DC    CL3'RD ',X'0D'
                DC    CL3'RE ',X'0E'
                DC    CL3'RF ',X'0F'
                DC    CL3'0  ',X'00'
                DC    CL3'1  ',X'01'
                DC    CL3'2  ',X'02'
                DC    CL3'3  ',X'03'
                DC    CL3'4  ',X'04'
                DC    CL3'5  ',X'05'
                DC    CL3'6  ',X'06'
                DC    CL3'7  ',X'07'
                DC    CL3'8  ',X'08'
                DC    CL3'9  ',X'09'
                DC    CL3'10 ',X'0A'
                DC    CL3'11 ',X'0B'
                DC    CL3'12 ',X'0C'
                DC    CL3'13 ',X'0D'
                DC    CL3'14 ',X'0E'
                DC    CL3'15 ',X'0F'
                DC    CL3'A  ',X'0A'
                DC    CL3'B  ',X'0B'
                DC    CL3'C  ',X'0C'
                DC    CL3'D  ',X'0D'
                DC    CL3'E  ',X'0E'
                DC    CL3'F  ',X'0F'
                DC    X'FF'
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDULIST                  DSECT
                          COPY DXDPREF

DXD_USING_PREV            DS   A          DUMMY FOR 'PREV' ON CHAIN
DXD_USING_NEXT            DS   A          FIRST USING ON CHAIN

DXD_FIRST_USING_DISPLAYED DS   A          FIRST USING DISPLAYED
DXD_LAST_USING_DISPLAYED  DS   A          LAST USING DISPLAYED

DXD_CONV_PARMS            DS   0A
DXD_CONV_SOURCE           DS   A
DXD_CONV_OUT              DS   A

DXD_USINGS_PER_SCREEN     DS   H          USINGS PER SCREEN
DXD_NBR_DISPLAYED         DS   H          NBR OF USINGS DISPLAYED

DXD_WORK                  DS   CL9
DXD_REGISTER              DS   X
DXD_REGISTER_NAME         DS   CL3
DXD_BEGIN                 DS   XL4
DXD_END                   DS   XL4

DXD_FLAGS                 DS   X
$DXD_DELETE               EQU  X'80'      .. USING BLOCK DELETED
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
REGISTER_DSECT            DSECT
REGISTER_NAME             DS   CL3
REGISTER_REGISTER         DS   X
REGISTER_DSECT_L          EQU  *-REGISTER_DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 DISCOMM  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 DISASMDA
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 BPXYSTAT DSECT=YES,LIST=NO
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 OSSPFD   TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COPY     ATTRS
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COPY     REGEQU
              END     DISULIST
