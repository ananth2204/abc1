*--------------------------------------------------------------------*
*                                                                    *
*  Module name: DISTEXT                                              *
*                                                                    *
*  Function:                                                         *
*   LOAD the requested CSECT's text                                  *
*                                                                    *
*   DISBIND has already established a dialog                         *
*   DISCOMM_CSECT_LENGTH has been set by DISESD                      *
*                                                                    *
*                                                                    *
*   32 bytes of "pad" are added to the CSECT text.  This was to      *
*   simplify the code in DISSCAN.  DISSCAN tries to identify areas   *
*   that are instructions.  To do this, it "looks" for two           *
*   consecutive instructions.  An exception is a branch or SVC.      *
*   Some character strings "look" like instructions.  If these       *
*   occur at the very end of a CSECT, DISSCAN will attempt to        *
*   access data beyond the end of the CSECT which can cause S0C4s.   *
*   The 16 byte "pad" is to prevent these S0C4 abends.               *
*                                                                    *
*   To give the last bytes of the program the benefit of the doubt,  *
*   the pad is valid instructions (BR instructions).  Therefore if   *
*   the last bytes of a CSECT are an instruction, it will appear to  *
*   DISSCAN that the instruction is followed by another instruction. *
*                                                                    *
*   Got it?                                                          *
*                                                                    *
*                                                                    *
*--------------------------------------------------------------------*
DISTEXT  CSECT
DISTEXT  AMODE 31
DISTEXT  RMODE ANY
         USING DISTEXT,R15
         B     MOD0000                        SKIP EYECATCHER
MODID    DC    CL8'DISTEXT'
         DC    C'&SYSDATE'
         DC    C'&SYSTIME'
MOD0000  DS    0H
         STM   R14,R12,12(R13)                SAVE REGS
         LR    R10,R15                        SET BASE REG
         DROP  R15
         USING OSCOMM,R12
         USING SESSION,R11
         USING DISTEXT,R10
         USING DISCOMM,R9
         L     R15,SESS_DXD_ADDR
         A     R15,DXD_START                  PLUS DISP
         ST    R13,4(,R15)                    CHAIN
         ST    R15,8(,R13)                    CHAIN
         LR    R13,R15                        SET SAVE AREA ADDRESS
         USING DXDTEXT,R13
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY
* ------------------------------------------------------------------- *
*        Obtain and initialize buffer for binder                      *
* ------------------------------------------------------------------- *
         ITRACE ID=GET_BUFF
         IEWBUFF FUNC=GETBUF,                                          +
               TYPE=TEXT
         ST    R8,DXD_BUFFER
         ITRACE ID=INITBUFF
         IEWBUFF FUNC=INITBUF,                                         +
               TYPE=TEXT
         ITRACE ID=BUFF,                                               +
               RDATA1=R8,                                              +
               RDATA2=R7
*--------------------------------------------------------------------*
*        GETMAIN storage for text                                    *
*--------------------------------------------------------------------*
         L     R0,DISCOMM_CSECT_LENGTH        CSECT'S LENGTH
         AH    R0,=Y(2*PAD_L)                 PLUS 'FUDGE FACTOR'
         ITRACE ID=GET_TEXT,                                           +
               RDATA1=R0
         GETMAIN RU,                          GETMAIN AREA FOR TEXT    +
               LV=(0),                                                 +
               LOC=ANY
         ST    R1,DISCOMM_CSECT_TEXT          SAVE ADDRESS
         ST    R1,DXD_TEXT_ADDR               SAVE ADDRESS
         A     R1,DISCOMM_CSECT_LENGTH        ADD LENGTH
         BCTR  R1,0                           RELATIVE TO ZERO
         ST    R1,DISCOMM_CSECT_TEXT_END      TEXT ENDING ADDRESS
         ITRACE ID=TEXT,                                               +
               DATA1=(DISCOMM_CSECT_TEXT,4),                           +
               DATA2=(DISCOMM_CSECT_LENGTH,4)
*--------------------------------------------------------------------*
*                                                                    *
*--------------------------------------------------------------------*
         XC    DXD_CURSOR,DXD_CURSOR
         MVC   DXD_LENGTH_REMAINING,DISCOMM_CSECT_LENGTH
*--------------------------------------------------------------------*
*                                                                    *
*--------------------------------------------------------------------*
TEXT0000 DS    0H
         ITRACE ID=GETD
         L     R8,DXD_BUFFER
         IEWBIND FUNC=GETD,                                            +
               VERSION=5,                                              +
               WORKMOD=DISCOMM_DIALOG_WORKMOD,                         +
               CLASS=B_TEXT,                                           +
               SECTION=DISCOMM_CSECT_NAME_LENGTH,                      +
               COUNT=DISCOMM_CSECT_LENGTH,                             +
               AREA=IEWBTXT,                                           +
               CURSOR=DXD_CURSOR,                                      +
               RETCODE=DXD_RETURN_CODE,                                +
               RSNCODE=DXD_REASON_CODE,                                +
               MF=(E,DXD_IEWBIND,COMPLETE)
         ITRACE ID=TEXT_RC,                                            +
               DATA1=(DXD_RETURN_CODE,4),                              +
               DATA2=(DXD_REASON_CODE,4)
         CLC   DXD_RETURN_CODE,F8
         BH    ERR0010
         BE    ERR0020
         CLC   DXD_RETURN_CODE,F4
         BL    TEXT0010
         CLC   DXD_REASON_CODE,X_83000800
         BE    TEXT0010
         CLC   DXD_REASON_CODE,X_83000801
         BE    EXIT0000
TEXT0010 DS    0H
         L     R2,TXTH_ENTRY_COUNT            SLOTS AVAILABLE
         C     R2,DXD_LENGTH_REMAINING        MORE THAN LEN REMAINING?
         BNH   TEXT0020                       NO
         L     R2,DXD_LENGTH_REMAINING        LIMIT TO LENGTH REMAINING
TEXT0020 DS    0H
         L     R0,DXD_TEXT_ADDR               CURRENT TEXT ADDRESS
         LR    R1,R2                          COPY LENGTH
         LA    R14,TXTH_END                   NEW TEXT
         LR    R15,R2                         COPY LENGTH
         MVCL  R0,R14                         COPY TEXT
         ITRACE ID=X,                                                  +
               DATA1=(DXD_TEXT_ADDR,4),                                +
               DATA2=(TXTH_ENTRY_COUNT,4)
         L     R0,DXD_TEXT_ADDR               CURRENT TEXT ADDRESS
         AR    R0,R2                          PLUS NEW LENGTH
         ST    R0,DXD_TEXT_ADDR               UPDATE ADDR FOR NEXT TIME
         L     R0,DXD_LENGTH_REMAINING        LENGTH REMAINING
         SR    R0,R2                          PLUS NEW LENGTH
         ST    R0,DXD_LENGTH_REMAINING        LENGTH LEFT
         ITRACE ID=Y,                                                  +
               DATA1=(DXD_TEXT_ADDR,4),                                +
               DATA2=(DXD_LENGTH_REMAINING,4)
         LTR   R0,R0                          ANY DATA LEFT?
         BNZ   TEXT0000                       YES
         L     R8,DXD_BUFFER                  BUFFER ADDRESS
         IEWBUFF FUNC=FREEBUF,                FREE BUFFER              +
               TYPE=TEXT
         L     R1,DXD_TEXT_ADDR               NEXT BYTE IN TEXT
         ITRACE ID=ADD_PAD,                                            +
               RDATA1=R1
         MVC   0(PAD_L,R1),PAD                COPY PAD
*--------------------------------------------------------------------*
*                                                                    *
*--------------------------------------------------------------------*
EXIT0000 DS    0H
         ITRACE ID=EXIT
         L     R13,4(,R13)                    RESTORE REGISTER 13       ASE01670
         LM    R14,R12,12(R13)                RESTORE REGISTERS         ASE01680
         SR    R15,R15                        GIVE GOOD RETURN CODE     ASE01690
         BR    R14                            RETURN TO CALLER          ASE01700
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
ERR0010  DS    0H
         DC    H'0'
ERR0020  DS    0H
         DC    H'0'
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
DXD_START     DC   Q(DXDTEXT)
F4            DC   F'4'
F8            DC   F'8'
H16           DC   H'16'

B_TEXT        DC   XL2'6',C'B_TEXT'
X_83000800    DC   X'83000800'
X_83000801    DC   X'83000801'

PAD           DS   0C                         PAD
              NOPR R0
              NOPR R0
              NOPR R0
              NOPR R0
              NOPR R0
              NOPR R0
              NOPR R0
              NOPR R0
PAD_L         EQU  *-PAD

         IEWBUFF FUNC=MAPBUF,                                          +
               TYPE=TEXT,                                              +
               HEADREG=R8,                                             +
               ENTRYREG=R7,                                            +
               SIZE=32768,                                             +
               VERSION=5

              IEWBIND MF=(L,IEWBIND_I)
IEWBIND_L     EQU  *-IEWBIND_I

*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
DXDTEXT           DSECT
                  COPY DXDPREF

DXD_RETURN_CODE      DS   F
DXD_REASON_CODE      DS   F

DXD_BUFFER           DS   A
DXD_TEXT_ADDR        DS   A
DXD_LENGTH_REMAINING DS   F

DXD_CURSOR           DS   F

DXD_IEWBIND          DS   0F,(IEWBIND_L)X
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 DISCOMM  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 DISASMDA
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 BPXYSTAT DSECT=YES,LIST=NO
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COPY     REGEQU
         END  DISTEXT
