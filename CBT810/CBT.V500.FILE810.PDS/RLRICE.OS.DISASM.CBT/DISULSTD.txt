* ------------------------------------------------------------------- *
*                                                                     *
*  MODULE NAME: DISULSTD                                              *
*                                                                     *
*  FUNCTION:                                                          *
*    Display DSECTs for USING definitions                             *
*                                                                     *
* ------------------------------------------------------------------- *
DISULSTD CSECT
DISULSTD AMODE 31
DISULSTD RMODE ANY
         USING DISULSTD,R15
         B     MOD0000                           SKIP EYECATCHER
MODID    DC    CL8'DISULSTD'
         DC    C'&SYSDATE'
         DC    C'&SYSTIME'
MOD0000  DS    0H
         STM   R14,R12,12(R13)                   SAVE REGS
         LR    R10,R15                           SET BASE REG
         DROP  R15
         USING OSCOMM,R12
         USING SESSION,R11
         USING DISULSTD,R10
         USING DISCOMM,R9
         L     R15,SESS_DXD_ADDR                 WORK AREA
         A     R15,DXD_START                     PLUS DISP
         ST    R13,4(,R15)
         ST    R15,8(,R13)
         LR    R13,R15
         USING DXDULSTD,R13
         MVC   DXD_CSECT,MODID
         L     R8,COMM_OSSPFD
         USING OSSPFD,R8
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         ITRACE ID=ENTRY,                                              +
               DATA1=DISCOMM_USING_KSDS,                               +
               DATA2=DISCOMM_USING_DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         MVC   DXD_FIRST_DSECT,DISCOMM_USING_DSECT
         MVC   DXD_FIRST_DSECT_DISPLAYED,DISCOMM_USING_DSECT
MAIN0010 DS    0H
         ITRACE ID=BUILD
         L     R0,DISCOMM_VDATA_ADDR
         L     R1,DISCOMM_VDATA_LENGTH
         SR    R14,R14
         SR    R15,R15
         MVCL  R0,R14
         L     R6,DISCOMM_VDATA_ADDR
         L     R7,SPF_VDEPTH
         ICM   R5,15,DXD_FIRST_DSECT_DISPLAYED
         USING DSECT_DSECT,R5
         BZ    MAIN0210
MAIN0020 DS    0H
         ST    R5,DXD_LAST_DSECT_DISPLAYED
         MVC   0(LINE01_L,R6),LINE01
         USING LINE01,R6
         MVC   LINE01_DSECT,DSECT_NAME           COPY NAME
         A     R6,SPF_VWIDTH
         BCT   R7,MAIN0030
         B     MAIN0040
MAIN0030 DS    0H
         ICM   R5,15,DSECT_NEXT                  NEXT DSECT
         BNZ   MAIN0020
* ------------------------------------------------------------------- *
*        Display list of DSECTS                                       *
* ------------------------------------------------------------------- *
MAIN0040 DS    0H
         ITRACE ID=DISPLAY
         MVC   SESS_DISP_PANEL,PANEL_NAME        SET PANEL NAME
         MVC   SESS_DISP_VDATA_ADDR,DISCOMM_VDATA_ADDR
         MVC   SESS_DISP_VDATA_LENGTH,DISCOMM_VDATA_LENGTH
         XC    SESS_DISP_CURSOR,SESS_DISP_CURSOR
         L     R15,COMM_V_OSDISP                 DISP ENTRY POINT
         BALR  R14,R15                           DISPLAY
         ITRACE ID=DISP_RC,                                            +
               RDATA1=R15,                                             +
               DATA2=SPF_ZVERB
         LTR   R2,R15                            RC = 0?
         BNZ   MAIN0210                          NO
         CLC   ABEND,SPF_ZCMD                    ABEND?
         BE    ABEND000                          YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         L     R6,DISCOMM_VDATA_ADDR
         L     R7,SPF_VDEPTH
         L     R5,DXD_FIRST_DSECT_DISPLAYED
MAIN0050 DS    0H
         CLI   LINE01_SELECT,C' '                SELECTED?
         BNE   MAIN0200                          YES
         A     R6,SPF_VWIDTH                     NEXT LINE
         BCT   R7,MAIN0060
         B     MAIN0070
MAIN0060 DS    0H
         ICM   R5,15,DSECT_NEXT                  NEXT DSECT
         BNZ   MAIN0050
* ------------------------------------------------------------------- *
*        Nothing selected, check for scrolling                        *
* ------------------------------------------------------------------- *
MAIN0070 DS    0H
         ITRACE ID=SCROLL,                                             +
               DATA1=SPF_ZVERB,                                        +
               DATA2=SPF_ZSCROLLA
         CLI   SPF_ZVERB,C'B'                    BOTTOM?
         BE    MAIN0080
         CLI   SPF_ZVERB,C'D'                    DOWN?
         BE    MAIN0100
         CLI   SPF_ZVERB,C'T'                    TOP?
         BE    MAIN0120
         CLI   SPF_ZVERB,C'U'                    UP?
         BE    MAIN0130
         B     MAIN0010
MAIN0080 DS    0H
         ITRACE ID=BOTTOM
         L     R5,DXD_FIRST_DSECT                FIRST DSECT
MAIN0090 DS    0H
         ST    R5,DXD_FIRST_DSECT_DISPLAYED      SAVE DSECT BLOCK ADDR
         ICM   R5,15,DSECT_NEXT                  ANOTHER DSECT?
         BNZ   MAIN0090                          YES
         B     MAIN0010
MAIN0100 DS    0H
         ITRACE ID=DOWN
         CLI   SPF_ZSCROLLA,C'M'                 BOTTOM?
         BE    MAIN0080                          YES
         L     R5,DXD_FIRST_DSECT_DISPLAYED      FIRST DSECT DISPLAYED
         L     R2,SPF_ZSCROLLN                   NBR TO SCROLL
MAIN0110 DS    0H
         ST    R5,DXD_FIRST_DSECT_DISPLAYED      SAVE DSECT BLOCK ADDR
         ICM   R5,15,DSECT_NEXT                  ANOTHER DSECT?
         BZ    MAIN0010                          NO.. DISPLAY
         BCT   R2,MAIN0110                       LOOP
         B     MAIN0010
MAIN0120 DS    0H
         ITRACE ID=TOP
         MVC   DXD_FIRST_DSECT_DISPLAYED,DXD_FIRST_DSECT
         B     MAIN0010
MAIN0130 DS    0H
         CLI   SPF_ZSCROLLA,C'M'                 TOP?
         BE    MAIN0120                          YES
         L     R5,DXD_FIRST_DSECT_DISPLAYED      FIRST ON LAST DISPLAY
         L     R2,SPF_ZSCROLLN                   NBR TO SCROLL
MAIN0140 DS    0H
         ITRACE ID=UP,                                                 +
               RDATA1=R5,                                              +
               RDATA2=R2
         ST    R5,DXD_FIRST_DSECT_DISPLAYED      SAVE DSECT BLOCK ADDR
         ICM   R5,15,DSECT_PREV                  ANOTHER BLOCK?
         BZ    MAIN0010                          NO
         BCT   R2,MAIN0140                       LOOP
         B     MAIN0010
         DROP  R6
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0200 DS    0H
         ITRACE ID=SELECTED,                                           +
               RDATA1=R5,                                              +
               DATA2=DSECT_NAME
         ST    R5,DISCOMM_USING_DSECT            SAVE ADDR
         SR    R15,R15                           INDICATE 'SELECTED'
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0210 DS    0H
         ITRACE ID=ABORT
         XC    DISCOMM_USING_DSECT,DISCOMM_USING_DSECT NO DSECT
         LA    R15,4                             SET RC
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=EXIT,                                               +
               RDATA1=R15
         L     R13,4(,R13)                       RESTORE REGISTER 13
         L     R14,12(,R13)                      RESTORE R14
         LM    R0,12,20(R13)                     RESTORE REGISTERS
         BR    R14                               RETURN TO CALLER
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ABEND000 DS    0H
         ITRACE ID=ABEND
         DC    H'0'
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXD_START     DC   Q(DXDULSTD)

ABEND         DC   C'ABEND'
PANEL_NAME    DC   CL8'DISDMENU'

LINE01        DS   0C
              DC   CL2' '
              DC   AL1($SCREEN_ATTR_INPUT)
LINE01_SELECT DC   C' '
              DC   AL1($SCREEN_ATTR_HIGH_TURQUOISE)
LINE01_DSECT  DC   CL63' '
LINE01_L      EQU  *-LINE01

* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDULSTD                  DSECT
                          COPY DXDPREF

DXD_FIRST_DSECT           DS  A          FIRST DSECT ON CHAIN
DXD_FIRST_DSECT_DISPLAYED DS  A          FIRST DSECT DISPLAYED
DXD_LAST_DSECT_DISPLAYED  DS  A          LAST  DSECT DISPLAYED

* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 DISCOMM  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 DISASMDA
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 BPXYSTAT DSECT=YES,LIST=NO
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 OSSPFD   TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COPY     ATTRS
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COPY     REGEQU
              END     DISULSTD
