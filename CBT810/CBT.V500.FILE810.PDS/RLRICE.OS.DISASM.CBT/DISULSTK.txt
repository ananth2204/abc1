* ------------------------------------------------------------------- *
*                                                                     *
*  MODULE NAME: DISULSTK                                              *
*                                                                     *
*  FUNCTION:                                                          *
*    Display the list of pre-processed DSECT KSDSes for USINGs.       *
*                                                                     *
* ------------------------------------------------------------------- *
DISULSTK CSECT
DISULSTK AMODE 31
DISULSTK RMODE ANY
         USING DISULSTK,R15
         B     MOD0000                           SKIP EYECATCHER
MODID    DC    CL8'DISULSTK'
         DC    C'&SYSDATE'
         DC    C'&SYSTIME'
MOD0000  DS    0H
         STM   R14,R12,12(R13)                   SAVE REGS
         LR    R10,R15                           SET BASE REG
         DROP  R15
         USING OSCOMM,R12
         USING SESSION,R11
         USING DISULSTK,R10
         USING DISCOMM,R9
         L     R15,SESS_DXD_ADDR                 WORK AREA
         A     R15,DXD_START                     PLUS DISP
         ST    R13,4(,R15)
         ST    R15,8(,R13)
         LR    R13,R15
         USING DXDULSTK,R13
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY
         L     R8,COMM_OSSPFD
         USING OSSPFD,R8
         USING KSDS_DSECT,R5
         USING DSECT_DSECT,R4
         USING LABEL_DSECT,R3
         L     R0,DISCOMM_VDATA_ADDR
         L     R1,DISCOMM_VDATA_LENGTH
         SR    R14,R14
         SR    R15,R15
         MVCL  R0,R14                            INITIALIZE VDATA
* ------------------------------------------------------------------- *
*                                                                     *
*                                                                     *
*      Display list of DSECT KSDSes                                   *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0100 DS    0H
         ITRACE ID=NEW
         MVC   SPF_ZCMD,COMM_BLANKS
         ICM   R1,15,DISCOMM_KSDS_NEXT           ANY KSDSES?
         BZ    MAIN0140                          NO
         ST    R1,DXD_FIRST_KSDS_DISPLAYED       SAVE FIRST KSDS
MAIN0110 DS    0H
         L     R0,DISCOMM_VDATA_ADDR
         L     R1,DISCOMM_VDATA_LENGTH
         SR    R14,R14
         SR    R15,R15
         MVCL  R0,R14                            INITIALIZE VDATA
         ICM   R5,15,DXD_FIRST_KSDS_DISPLAYED
         BZ    MAIN0140                          NONE TO DISPLAY
         L     R6,DISCOMM_VDATA_ADDR
         L     R2,SPF_VDEPTH                     NBR OF LINES
MAIN0120 DS    0H
         ST    R5,DXD_LAST_KSDS_DISPLAYED        SAVE LAST DISPLAYED
         MVC   0(LINE01_L,R6),LINE01
         USING LINE01,R6
         MVC   LINE01_DSNAME,KSDS_DSNAME
         DROP  R6
         A     R6,SPF_VWIDTH                     NEXT LINE
         BCT   R2,MAIN0130
         B     MAIN0140
MAIN0130 DS    0H
         ICM   R5,15,KSDS_NEXT                   NEXT KSDS
         BNZ   MAIN0120
* ------------------------------------------------------------------- *
*                                                                     *
*                                                                     *
*        Display the list of KSDS's                                   *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0140 DS    0H
         MVC   SPF_ZCMD,COMM_BLANKS              INITIALIZE FUNCTION
         ITRACE ID=DISPLAY
         MVC   SESS_DISP_PANEL,PANEL_NAME        SET PANEL NAME
         MVC   SESS_DISP_VDATA_ADDR,DISCOMM_VDATA_ADDR
         MVC   SESS_DISP_VDATA_LENGTH,DISCOMM_VDATA_LENGTH
         XC    SESS_DISP_CURSOR,SESS_DISP_CURSOR
         L     R15,COMM_V_OSDISP                 DISP ENTRY POINT
         BALR  R14,R15                           DISPLAY
         ITRACE ID=DISP_RC,                                            +
               RDATA1=R15,                                             +
               DATA2=SPF_ZVERB
         LTR   R15,R15                           RC = 0?
         BNZ   MAIN0330                          NO
* ------------------------------------------------------------------- *
*                                                                     *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
         CLI   SPF_FUNCTION,C' '                 'A' OPTION DSECTS?
         BNE   MAIN0300                          YES
         L     R5,DXD_FIRST_KSDS_DISPLAYED       FIRST KSDS DISPLAYED
         L     R6,DISCOMM_VDATA_ADDR
         L     R2,SPF_VDEPTH
         USING LINE01,R6
MAIN0150 DS    0H
         OI    LINE01_SELECT,C' '                'TRANSLATE'
         CLI   LINE01_SELECT,C' '                THIS DATASET SELECTED?
         BNE   MAIN0310                          YES
         ICM   R5,15,KSDS_NEXT                   NEXT KSDS
         BZ    MAIN0140                          END OF CHAIN
         A     R6,SPF_VWIDTH                     NEXT LINE
         BCT   R2,MAIN0150                       LOOP
         DROP  R6
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         ITRACE ID=SCROLL,                                             +
               DATA1=SPF_ZVERB,                                        +
               DATA2=SPF_ZSCROLLA
         CLI   SPF_ZVERB,C'B'                    BOTTOM?
         BE    MAIN0160
         CLI   SPF_ZVERB,C'D'                    DOWN?
         BE    MAIN0180
         CLI   SPF_ZVERB,C'T'                    TOP?
         BE    MAIN0200
         CLI   SPF_ZVERB,C'U'                    UP?
         BE    MAIN0210
         B     MAIN0140                          NO SCROLL REQUESTED
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0160 DS    0H
         ITRACE ID=BOTTOM
         ICM   R5,15,DISCOMM_KSDS_NEXT           FIRST KSDS
         BZ    MAIN0110                          NO KSDS(ES) DEFINED
MAIN0170 DS    0H
         ST    R5,DXD_FIRST_KSDS_DISPLAYED       SAVE KSDS BLOCK ADDR
         ICM   R5,15,KSDS_NEXT                   ANOTHER KSDS?
         BNZ   MAIN0170                          YES
         B     MAIN0110
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0180 DS    0H
         ITRACE ID=DOWN
         ICM   R5,15,DXD_FIRST_KSDS_DISPLAYED    FIRST KSDS ON DISPLAY
         BZ    MAIN0110                          NO KSDS(ES)
         L     R2,SPF_ZSCROLLN                   NBR TO SCROLL
MAIN0190 DS    0H
         ST    R5,DXD_FIRST_KSDS_DISPLAYED       SAVE KSDS BLOCK ADDR
         ICM   R5,15,KSDS_NEXT                   ANOTHER KSDS?
         BZ    MAIN0110                          NO
         BCT   R2,MAIN0190                       LOOP
         B     MAIN0110                          BUILD DISPLAY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0200 DS    0H
         ITRACE ID=TOP
         MVC   DXD_FIRST_KSDS_DISPLAYED,DISCOMM_KSDS_NEXT
         B     MAIN0110
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0210 DS    0H
         ITRACE ID=UP
         ICM   R5,15,DXD_FIRST_KSDS_DISPLAYED    FIRST KSDS ON DISPLAY
         BZ    MAIN0220
         L     R2,SPF_ZSCROLLN                   NBR TO SCROLL
MAIN0220 DS    0H
         ST    R5,DXD_FIRST_KSDS_DISPLAYED       SAVE KSDS BLOCK ADDR
         ICM   R5,15,KSDS_PREV                   ANOTHER KSDS?
         BZ    MAIN0110                          NO
         BCT   R2,MAIN0220                       LOOP
         B     MAIN0110                          BUILD DISPLAY
* ------------------------------------------------------------------- *
*        Display DSECTs from option 'A'                               *
* ------------------------------------------------------------------- *
MAIN0300 DS    0H
         ITRACE ID=A_DSECTS
         SR    R5,R5                             NOT A KSDS
         LA    R15,4                             SET RC
         B     MAIN0320
* ------------------------------------------------------------------- *
*        A KSDS was selected                                          *
* ------------------------------------------------------------------- *
MAIN0310 DS    0H
         ITRACE ID=SELECTED,                                           +
               RDATA1=R5,                                              +
               DATA2=KSDS_DDNAME
         ST    R5,DISCOMM_USING_KSDS
         SR    R15,R15                           SET RC
MAIN0320 DS    0H
         XC    DISCOMM_USING_DSECT,DISCOMM_USING_DSECT
         XC    DISCOMM_USING_LABEL,DISCOMM_USING_LABEL
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0330 DS    0H
         ITRACE ID=ABORT
         XC    DISCOMM_USING_KSDS,DISCOMM_USING_KSDS
         LA    R15,8
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=EXIT,                                               +
               RDATA1=R15
         L     R13,4(,R13)                       RESTORE REGISTER 13
         L     R14,12(,R13)                      RESTORE R14
         LM    R0,12,20(R13)                     RESTORE REGISTERS
         BR    R14                               RETURN TO CALLER
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ABEND000 DS    0H
         ITRACE ID=ABEND
         DC    H'0'
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXD_START     DC   Q(DXDULSTK)

PANEL_NAME    DC   CL8'DISKMENU'

              LTORG


LINE01        DS   0C
              DC   CL2' '
              DC   AL1($SCREEN_ATTR_INPUT)
LINE01_SELECT DC   C' '
              DC   AL1($SCREEN_ATTR_HIGH_TURQUOISE)
LINE01_DSNAME DC   CL44' '
LINE01_L      EQU  *-LINE01

* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDULSTK                  DSECT
                          COPY DXDPREF

                          DS  A          DUMMY FOR 'PREV' ON CHAIN

DXD_FIRST_KSDS_DISPLAYED  DS  A          FIRST KSDS DISPLAYED
DXD_LAST_KSDS_DISPLAYED   DS  A          LAST KSDS DISPLAYED

DXD_DSECT_ADDR            DS  A

                          COPY PPDDATA
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 DISCOMM  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 DISASMDA
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 BPXYSTAT DSECT=YES,LIST=NO
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 OSSPFD   TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COPY     ATTRS
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COPY     REGEQU
              END     DISULSTK
