*--------------------------------------------------------------------*
*                                                                    *
*  Module name: GENRS3                                               *
*                                                                    *
*  Function:                                                         *
*   GENERATE RS TYPE INSTRUCTIONS  (ICM, STCM, ...)                  *
*                                                                    *
*  At entry R7 is AREA address                                       *
*           R5 is address of instruction                             *
*           R4 is address of OPCODE entry                            *
*                                                                    *
*--------------------------------------------------------------------*
GENRS3   CSECT
GENRS3   AMODE 31
GENRS3   RMODE ANY
         USING GENRS3,R15
         USING OSCOMM,R12
         USING SESSION,R11
         USING DISCOMM,R9
         USING AREA_DSECT,R7
         USING OPCODE_DSECT,R4
         B     MOD0000                        SKIP EYECATCHER
MODID    DC    CL8'GENRS3'
         DC    C'&SYSDATE'
         DC    C'&SYSTIME'
MOD0000  DS    0H
         STM   R14,R12,12(R13)                SAVE REGS
         LR    R10,R15                        SET BASE REG
         DROP  R15
         USING GENRS3,R10
         L     R15,SESS_DXD_ADDR              WORK AREA BASE
         A     R15,DXD_START                  PLUS DISP
         ST    R13,4(,R15)
         ST    R15,8(,R13)
         LR    R13,R15                        SET SAVE AREA ADDRESS
         USING DXDRS3,R13
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY,                                              +
               RDATA1=R7,                                              +
               DATA2=0(R5)
         MVC   DXD_BLANKS,COMM_BLANKS
         LA    R1,DISCOMM_SOURCE_OPERAND1
*--------------------------------------------------------------------*
*        GENERATE R1 AND MASK                                        *
*--------------------------------------------------------------------*
         SR    R3,R3                          CLEAR R3
         IC    R3,1(R5)                       INSERT R1 AND R2
         SRL   R3,4                           SHIFT OUT R2
         STC   R3,DXD_R1                      SAVE R1
         MVC   DXD_MASK,1(R5)                 COPY R1 AND R1
         NI    DXD_MASK,X'0F'                 REMOVE R1
         LA    R1,DISCOMM_SOURCE_OPERAND1
         MVI   0(R1),C'R'                     INSERT 'R' FOR R1
         IC    R3,DXD_R1                      R1
         SLL   R3,1                           MULTIPLY BY 2
         LA    R2,REGISTER_TABLE(R3)          REGISTSER NAME
         MVC   DISCOMM_SOURCE_OPERAND1+1(2),0(R2)
         BAL   R15,SCAN0000                   FIND 1ST BLANK
         MVI   0(R1),C','                     INSERT COMMA
         IC    R3,DXD_MASK                    MASK
         SLL   R3,1                           MULTIPLY BY 2
         LA    R2,REGISTER_TABLE(R3)          USE REGISTER NAMES
         MVC   1(2,R1),0(R2)                  COPY REGISTER NAME
         BAL   R15,SCAN0000                   FIND BLANK
         MVI   0(R1),C','                     INSERT COMMA
         LA    R1,1(,R1)
         ICM   R8,15,AREA_REF_LABEL1          LABEL PRESENT?
         BZ    GEN0030                        NO
         USING LABEL_DSECT,R8
*--------------------------------------------------------------------*
*        GENERATE LABEL                                              *
*--------------------------------------------------------------------*
         ICM   R2,15,AREA_REF_USING1          USING BLOCK ADDRESS?
         BZ    GEN0020                        NO
         USING USING_DSECT,R2
         CLC   USING_NAME,COMM_BLANKS         USING NAME BLANK?
         BE    GEN0020                        YES
         MVC   0(L'USING_NAME,R1),USING_NAME  COPY USING NAME
         BAL   R15,SCAN0000                   FIND END OF NAME
         MVI   0(R1),C'.'                     INSERT DELIMITER
         LA    R1,1(,R1)                      UPDATE OUTPUT ADDRESS
GEN0020  DS    0H
         MVC   0(L'LABEL_NAME,R1),LABEL_NAME
         BAL   R15,SCAN0000                   FIND END OF NAME
         ICM   R0,15,AREA_REF_DISP1           DISP FROM LABEL
         BZ    EXIT0000                       ZERO.. EXIT
         CVD   R0,COMM_DWORD                  CONVERT TO DECIMAL
         MVI   0(R1),C'+'                     INSERT PLUS
         MVC   DXD_EDIT_WORD,EDIT_WORD
         ED    DXD_EDIT_WORD,COMM_DWORD+4     EDIT DISP
         LA    R2,DXD_EDIT_WORD
         BAL   R15,SCAN1000                   FIND FIRST NON ZERO
         MVC   1(4,R1),0(R2)                  COPY DISP
         B     EXIT0000
*--------------------------------------------------------------------*
*        GENERATE "DDD(R)"                                           *
*--------------------------------------------------------------------*
GEN0030  DS    0H
         SR    R3,R3
         IC    R3,2(R5)                       BASE AND PART OF DISP
         SRL   R3,4                           SHIFT OUT DISP
         STC   R3,DXD_BASE
         MVC   DXD_DISP,2(R5)                 COPY BASE & DISP
         NI    DXD_DISP,X'0F'                 LEAVE ONLY DISP
         LH    R0,DXD_DISP                    DISP
         CVD   R0,COMM_DWORD                  CONVERT TO DECIMAL
         MVC   DXD_EDIT_WORD,EDIT_WORD
         ED    DXD_EDIT_WORD,COMM_DWORD+4     EDIT DISP
         LA    R2,DXD_EDIT_WORD
         BAL   R15,SCAN1000                   FIND 1ST NON ZERO
         MVC   0(4,R1),0(R2)                  COPY DISP
         ICM   R3,1,DXD_BASE                  BASE ZERO?
         BZ    EXIT0000                       YES
         BAL   R15,SCAN0000                   FIND BLANK
         MVI   0(R1),C'('                     OPEN PAREN
         MVI   1(R1),C'R'
         SLL   R3,1                           MULTIPLY BY 2
         LA    R2,REGISTER_TABLE(R3)          REGISTER NAME
         MVC   2(2,R1),0(R2)                  COPY TO SOURCE
         BAL   R15,SCAN0000                   FIND BLANK
         MVI   0(R1),C')'                     INSERT CLOSING PAREN
         B     EXIT0000
*--------------------------------------------------------------------*
*        Find next blank                                             *
*--------------------------------------------------------------------*
SCAN0000 DS    0H
         CLI   0(R1),C' '
         BER   R15
         LA    R1,1(,R1)
         B     SCAN0000
*--------------------------------------------------------------------*
*        Find first non-zero                                         *
*--------------------------------------------------------------------*
SCAN1000 DS    0H
         CLI   0(R2),C'0'
         BNER  R15
         LA    R2,1(,R2)
         B     SCAN1000
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
EXIT0000 DS    0H
         ITRACE ID=EXIT
         L     R13,4(,R13)                    RESTORE REGISTER 13       ASE01670
         LM    R14,R12,12(R13)                RESTORE OTHER REGISTERS   ASE01680
         SR    R15,R15                        RETURN CODE               ASE01690
         BR    R14                            RETURN TO CALLER          ASE01700
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
DXD_START      DC   Q(DXDRS3)
EDIT_WORD      DC   X'F020202020202120'

REGISTER_TABLE DS   0C
               DC   CL2'0 '
               DC   CL2'1 '
               DC   CL2'2 '
               DC   CL2'3 '
               DC   CL2'4 '
               DC   CL2'5 '
               DC   CL2'6 '
               DC   CL2'7 '
               DC   CL2'8 '
               DC   CL2'9 '
               DC   CL2'10'
               DC   CL2'11'
               DC   CL2'12'
               DC   CL2'13'
               DC   CL2'14'
               DC   CL2'15'
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
DXDRS3         DSECT
               COPY DXDPREF
DXD_DISP       DS   XL2
DXD_R1         DS   X
DXD_MASK       DS   X
DXD_BASE       DS   X

DXD_EDIT_WORD  DS   XL8
DXD_BLANKS     DS   CL8
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               DISCOMM  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               DISASMDA
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               BPXYSTAT DSECT=YES,LIST=NO
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY     REGEQU
               END      GENRS3
