* ------------------------------------------------------------------- *
*                                                                     *
*  Module name: DISRLD                                                *
*                                                                     *
*  Function:                                                          *
*   Obtain the requested CSECT's RLD info                             *
*                                                                     *
*   DISBIND has already established a dialog                          *
*                                                                     *
* ------------------------------------------------------------------- *
DISRLD   CSECT
DISRLD   AMODE 31
DISRLD   RMODE ANY
         USING DISRLD,R15
         B     MOD0000                        SKIP EYECATCHER
MODID    DC    CL8'DISRLD'
         DC    C'&SYSDATE'
         DC    C'&SYSTIME'
MOD0000  DS    0H
         DROP  R15
         STM   R14,R12,12(R13)                SAVE REGS
         LR    R10,R15                        SET BASE REG
         USING OSCOMM,R12
         USING SESSION,R11
         USING DISRLD,R10
         USING DISCOMM,R9
         L     R15,SESS_DXD_ADDR
         A     R15,DXD_START                  PLUS DISP
         ST    R13,4(,R15)                    CHAIN
         ST    R15,8(,R13)                    CHAIN
         LR    R13,R15                        SET SAVE AREA ADDRESS
         USING DXDRLD,R13
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY
* ------------------------------------------------------------------- *
*        Obtain and initialize RLD buffer                             *
* ------------------------------------------------------------------- *
         ITRACE ID=GET_SECT,                                           +
               DATA1=DISCOMM_CSECT
         IEWBUFF FUNC=GETBUF,                                          +
               PREFIX=IBMRLD,                                          +
               TYPE=RLD
         ST    R8,DXD_BUFFER
         ITRACE ID=INIT_RLD,                                           +
               RDATA1=R8,                                              +
               RDATA2=R7
         IEWBUFF FUNC=INITBUF,                                         +
               PREFIX=IBMRLD,                                          +
               TYPE=RLD
         XC    DXD_BINDER_CURSOR,DXD_BINDER_CURSOR
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
RLD0000  DS    0H
         ITRACE ID=GETD_RLD,                                           +
               DATA1=DISCOMM_CSECT
         L     R8,DXD_BUFFER
         LA    R7,IBMRLD_RLDH_END
         IEWBIND FUNC=GETD,                                            +
               VERSION=5,                                              +
               WORKMOD=DISCOMM_DIALOG_WORKMOD,                         +
               CLASS=B_RLD,                                            +
               SECTION=DISCOMM_CSECT_NAME_LENGTH,                      +
               AREA=IBMRLD_IEWBRLD,                                    +
               CURSOR=DXD_BINDER_CURSOR,                               +
               COUNT=DXD_RLD_RETURNED,                                 +
               RETCODE=DXD_RETURN_CODE,                                +
               RSNCODE=DXD_REASON_CODE,                                +
               MF=(E,DXD_IEWBIND,COMPLETE)
         ITRACE ID=GETD_RC,                                            +
               DATA1=(DXD_RETURN_CODE,4),                              +
               DATA2=(DXD_REASON_CODE,4)
         CLC   DXD_RETURN_CODE,F8
         BH    ERR0010
         BE    ERR0020                             NO DATA
         CLC   DXD_RETURN_CODE,F4
         BL    RLD0020
         CLC   DXD_REASON_CODE,X_83000800
         BE    RLD0020
         CLC   DXD_REASON_CODE,X_83000801
         BE    EXIT0000
RLD0020  DS    0H
         ICM   R6,15,DXD_RLD_RETURNED
         LA    R7,IBMRLD_RLDH_END
         USING IBMRLD_RLD_ENTRY,R7
RLD0030  DS    0H
         ITRACE ID=RLD,                                                +
               RDATA1=R7,                                              +
               RDATA2=R6
         MVC   DXD_CSECT_NAME,COMM_BLANKS          CLEAR CSECT NAME
         LH    R1,IBMRLD_RLD_RESIDENT_CHARS
         L     R2,IBMRLD_RLD_RESIDENT_PTR
         BCTR  R1,0
         EX    R1,MVC_01                           COPY SECTION NAME
         CLC   DISCOMM_CSECT,DXD_CSECT_NAME        CORRECT CSECT?
         BNE   RLD0140                             NO
         GETMAIN RU,                                                   +
               LV=AREA_DSECT_L,                                        +
               LOC=ANY
         LR    R5,R1                               COPY ADDRESS
         ITRACE ID=NEW_RLD,                                            +
               RDATA1=R5
NEW      USING AREA_DSECT,R5
         XC    NEW.AREA_DSECT(AREA_DSECT_L),NEW.AREA_DSECT
         MVC   NEW.AREA_EYE,AREA_ID
         MVI   NEW.AREA_TYPE,$AREA_DATA            INDICATE 'DATA' AREA
         CLI   IBMRLD_RLD_TYPE,X'10'               VCON?
         BE    RLD0040                             YES
         CLI   IBMRLD_RLD_TYPE,X'20'               ACON?
         BE    RLD0050                             YES
         CLI   IBMRLD_RLD_TYPE,X'30'               PSEUDO AREA DISP (Q)
         BE    RLD0060                             YES
         CLI   IBMRLD_RLD_TYPE,X'40'               PSEUDO SIZE (CXD)
         BE    RLD0070                             YES
         DC    H'0'
RLD0040  DS    0H
         MVI   NEW.AREA_DATA_TYPE,$DATA_VCON       ITEM IS A VCON
         B     RLD0080
RLD0050  DS    0H
         MVI   NEW.AREA_DATA_TYPE,$DATA_ACON       ITEM IS AN ADCON
         B     RLD0080
RLD0060  DS    0H
         MVI   NEW.AREA_DATA_TYPE,$DATA_Q          ITEM IS A "Q"
         B     RLD0080
RLD0070  DS    0H
         MVI   NEW.AREA_DATA_TYPE,$DATA_CXD        ITEM IS A "CXD"
RLD0080  DS    0H
         MVC   NEW.AREA_LENGTH+2(2),IBMRLD_RLD_ADCON_LENG
         MVC   NEW.AREA_BEGIN,IBMRLD_RLD_RES_OFFSET
         ICM   R0,15,NEW.AREA_BEGIN
         ICM   R1,15,NEW.AREA_LENGTH
         AR    R1,R0
         BCTR  R1,0
         STCM  R1,15,NEW.AREA_END
         TM    IBMRLD_RLD_BIND_ATTR,IBMRLD_RLD_DIRECTION
         BO    RLD0090
         OI    NEW.AREA_FLAGS,$AREA_RLD_PLUS
         B     RLD0100
RLD0090  DS    0H
         OI    NEW.AREA_FLAGS,$AREA_RLD_MINUS
RLD0100  DS    0H
         LH    R1,IBMRLD_RLD_TARGET_CHARS
         CH    R1,=Y(L'AREA_RLD_NAME)
         BNH   RLD0110
         LH    R1,=Y(L'AREA_RLD_NAME)
RLD0110  DS    0H
         L     R2,IBMRLD_RLD_TARGET_PTR
         BCTR  R1,0
         MVC   NEW.AREA_RLD_NAME,COMM_BLANKS
         EX    R1,MVC_02
* ------------------------------------------------------------------- *
*     Add the new block to RLD area chain                             *
* ------------------------------------------------------------------- *
         LA    R1,DISCOMM_RLD_AREA_PREV            RLD ANCHOR
PREV     USING AREA_DSECT,R1
RLD0120  DS    0H
         ICM   R2,15,PREV.AREA_NEXT
         BZ    RLD0130
         LR    R1,R2                               COPY ADDRESS
         B     RLD0120                             COPY RLD BLOCK ADDR
RLD0130  DS    0H
         ST    R1,NEW.AREA_PREV                    CHAIN PREV TO NEW
         ST    R5,PREV.AREA_NEXT                   CHAIN NEW TO PREV
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
RLD0140  DS    0H
         LA    R7,IBMRLD_RLD_END
         BCT   R6,RLD0030
         CLC   DXD_REASON_CODE,X_83000800
         BNE   RLD0000
* ------------------------------------------------------------------- *
*        Free the buffer                                              *
* ------------------------------------------------------------------- *
         L     R8,DXD_BUFFER
         IEWBUFF FUNC=FREEBUF,                                         +
               PREFIX=IBMRLD,                                          +
               TYPE=RLD
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MVC_01   MVC   DXD_CSECT_NAME(0),0(R2)
MVC_02   MVC   NEW.AREA_RLD_NAME(0),0(R2)
         DROP  NEW
* --------------------------------------------------------------------*
*                                                                     *
* --------------------------------------------------------------------*
ERR0010  DS    0H
         DC    H'1'
ERR0020  DS    0H
         DC    H'2'
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=EXIT
         ICM   R1,15,DISCOMM_RLD_AREA_NEXT
         BZ    EXIT0010
         ITRACE ID=X_PREV,                                             +
               RDATA1=R5
         XC    PREV.AREA_PREV,PREV.AREA_PREV
EXIT0010 DS    0H
         L     R13,4(,R13)                    RESTORE REGISTER 13       ASE01670
         LM    R14,R12,12(R13)                RESTORE REGISTERS         ASE01680
         SR    R15,R15                        GIVE GOOD RETURN CODE     ASE01690
         BR    R14                            RETURN TO CALLER          ASE01700
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXD_START     DC   Q(DXDRLD)
F4            DC   F'4'
F8            DC   F'8'
B_RLD         DC   XL2'5',C'B_RLD'
X_83000800    DC   X'83000800'
X_83000801    DC   X'83000801'
AREA_ID       DC   CL8'AREA'
TYPE_ADCON    DC   CL5'ADCON'
TYPE_VCON     DC   CL5'VCON'
TYPE_Q        DC   CL5'Q'
TYPE_CXD      DC   CL5'CXD'

         IEWBUFF FUNC=MAPBUF,                                          +
               TYPE=RLD,                                               +
               PREFIX=IBMRLD,                                          +
               HEADREG=R8,                                             +
               ENTRYREG=R7,                                            +
               SIZE=50,                                                +
               VERSION=5

              IEWBIND MF=(L,IEWBIND_I)
IEWBIND_L     EQU  *-IEWBIND_I
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
DXDRLD            DSECT
                  COPY DXDPREF
DXD_RETURN_CODE   DS   F

DXD_REASON_CODE   DS   F

DXD_BUFFER        DS   A

DXD_BINDER_CURSOR DS   F
DXD_RLD_RETURNED  DS   F

DXD_CHAIN_POINT   DS   A

DXD_CSECT_NAME    DS   CL8

DXD_IEWBIND       DS   0F,(IEWBIND_L)X
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                  COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                  SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                  DISCOMM  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                  DISASMDA
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                  BPXYSTAT DSECT=YES,LIST=NO
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                  COPY     OPERAND
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                  COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                  COPY     REGEQU
         END  DISRLD
