KGUPEXUT TITLE 'CRYPTOGRAPHIC SERVICES FACILITY KGUP EXIT'
         TITLE 'INTERNAL MACROS'
         MACRO
         DOCPGM
         LCLC  &LABEL
.**********************************************************************
.****                                                             *****
.****    THIS MACRO DOCUMENTS THE CSECT.                          *****
.****                                                             *****
.****    CSECT NAME                                               *****
.****    ASSEMBLER NAME AND VERSION                               *****
.****    TIME AND DATE                                            *****
.****    OPERATING SYSTEM LEVEL                                   *****
.****                                                             *****
.**********************************************************************
&SKIPIT  SETC  '$$$$'.'&SYSNDX'
         USING &SYSECT,R15       TEMPORARY BASE REGISTER
         J     &SKIPIT           GO AROUND EYE CATCHER
         DC    C'&SYSSTYP &SYSECT WAS '
         DC    C'ASSEMBLED BY &SYSASM VERSION &SYSVER. '
         DC    C'AT &SYSTIME ON &SYSDATC. '
         DC    C'UNDER &SYSTEM_ID'
LOADPT   DC    A(KGUPEXIT)
SAVEX    DS    18F
&SKIPIT  DS    0H
         MEND
         TITLE 'PROGRAM ENTRY STUFF'
         PUNCH ' ENTRY KGUPEXIT'
KGUPEXIT CSECT
KGUPEXIT RMODE ANY          (ANY,24)
KGUPEXIT AMODE 31           (ANY,24,31)
*
* AUTHOR             :  MARK JACOBS
* DATE               :  11/17/2009
* INSTALLATION       :  TIME CUSTOMER SERVICE
* CALLED ENVIRONMENT :  SUPERVISOR STATE, APF-AUTHORIZED
*                    :  TCB MODE
*                    :  ADDRESS SPACE CONTROL MODE=PRIMARY
*                    :  AMODE(31), RMODE(ANY)
* PROGRAM LOGIC      :  CHECKS RACF AUTHORIZATION TO THE TARGET
*                    :  ENCRYPTION KEY (CSFKEYS GENERAL RESOURCE CLASS)
*                    :  BEFORE ALLOWING CSFKGUP TO PERFORM THE
*                    :  REQUESTED KEY FUNCTION.
*
*   BEWARE OF BUGS IN THE CODE BELOW; I HAVE ONLY PROVED IT
*   CORRECT, NOT TRIED IT.  -- DONALD KNUTH
*
*
*   MODIFICATION LOG
*
*   11/17/2009 - INITIAL VERSION
*
*
         PRINT OFF
         COPY  IEABRC
         YREGS
         PRINT ON
         SPLEVEL SET
         DOCPGM
         BAKR  R14,0            SAVE REGS ON LINKAGE STACK
         L     R12,LOADPT       LOAD BASE REGISTER
         DROP  R15              DROP TEMPORARY BASE REGISTER
         USING KGUPEXIT,R12     TELL ASSEMBLER ABOUT BASE REGISTER
         LA    R13,SAVEX        POINT R13 TO SAVE AREA
         MVC   4(4,R13),=C'F1SA' SET ACRONYM IN SAVE AREA
         L     R2,0(,R1)        LOAD ADDRESS OF KGUP EXIT CONTROL BLOCK
         USING KGXP,R2           TELL ASSEMBLER
         TM    KGXFUNCT,KGXRCDPR RECORD PRE-PROCESSING ENTRY POINT?
         JZ    EXIT              NO. GET OUT
         TM    KGXACTON,KGXADD+KGXUPDTE+KGXDELET+KGXRENAM   ACTIONS
         JZ    EXIT       ARE WE TRYING TO DO KEY WORK? NO - EXIT.
         LA    R3,KGXLABEL      GET ADDRESS OF TARGET KEY LABEL
*
*    SNAFUCATE THE FUNCTION BEING CALLED BY KGUP
*
         RACROUTE REQUEST=AUTH,CLASS='CSFKEYS',ENTITY=((R3)),          X
               ATTR=UPDATE,LOG=ASIS,SYSTEM=YES,RELEASE=7730,           X
               WORKA=RACWK
         LTR   R15,R15          TEST SAF RETURN CODE
         JZ    USERAUTH         GIVE THUMBS UP
         MVC   KGXRETCD,=F'4'  REJECT CONTROL STATEMENT. KGUP CONTINUES
USERAUTH EQU   *
EXIT     SR    R15,R15          SET ZERO RETURN CODE
         PR    ,                RETURN TO CALLER
RACWK    DS  CL512
         CSFMKGXP
         END
