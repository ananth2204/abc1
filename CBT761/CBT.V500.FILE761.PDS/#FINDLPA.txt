/* **************************** REXX ******************************* */
/*                                                                   */
/*  NAME: FINDLPA                                                    */
/*  FUNC: Scan LPA table for datasets, then search in order for a    */
/*        particular member name using SYSDSN function.              */
/*  CALL: FINDLPA member listtype                                    */
/*  SRCH: PSA+10 > CVT > CVT+4AC > SMEXT > SMEXT+38 > LPAT           */
/*  OPSYS: OS/390                                                    */
/*  Notes: listtype can be 'SHORT' or 'LONG'.  LONG lists all        */
/*         datasets regardless of whether the member is found.       */
/*         SHORT only lists the datasets the member is found in,     */
/*         along with the number in the concatenation each dataset   */
/*         is. SHORT is the default.                                 */
/*                                                                   */
/* ***************************************************************** */
/*                                                                   */
/*  FINDLPA:   EXEC to search LPA list for a specific member.        */
/*                                                                   */
/*  Syntax:    FINDLPA member {listtype}                             */
/*                                                                   */
/*  Where:     'member' is the member name to search for             */
/*             'listtype' is SHORT(default) or LONG.  LONG lists all */
/*               datasets searched, and SHORT only displays datasets */
/*               the member was found in.                            */
/*                                                                   */
/*  Functions: TSO/E SYSDSN function                                 */
/*                                                                   */
/* ***************************************************************** */
Trace O
Address TSO

ARG Member ListType
If Member = "" then
  Do
    Say "You did NOT enter a member name.  Terminating...(FINDLPA)"
    Exit
  End
If ListType = "" then ListType = "SHORT"
Select
  When COMPARE(ListType,"SHORT") = 0 then NOP
  When COMPARE(ListType,"LONG") = 0 then NOP
  Otherwise
    Do
      Say "Default listing of 'SHORT' assumed"
      ListType = "SHORT"
    End
End
Cvt = STORAGE(10,4)
CVT = C2X(Cvt)
CvtPtr = X2D(Cvt)+X2D('4AC')
CvtPtr = D2X(CvtPtr)
CvtSmext = STORAGE(CvtPtr,4)
CvtSmext = C2X(CvtSmext)
SmextPtr = X2D(CvtSmext)+X2D('38')
SmextPtr = D2X(SmextPtr)
LpatPtr = STORAGE(SmextPtr,4)
LpatPtr = C2X(LpatPtr)
LpatId = STORAGE(LpatPtr,4)
If LpatId Â¬= "LPAT" then
  Do
    Say "Bad LPAT Pointer"
    exit
  End
LpatPtr = X2D(LpatPtr) + 4
LpatPtr = D2x(LpatPtr)
LpaCount = STORAGE(LpatPtr,4)
LpaCount = C2D(LpaCount)
CALL #SYSID
Say "Searching LPA on" result "for" Member
LpatPtr = X2D(LpatPtr)+X2D(5)
LpatPtr = D2X(LpatPtr);
Found = "NO"
Do I = 1 to LpaCount
  LpaEntry = STORAGE(LpatPtr,44)
  LpaEntry = STRIP(LpaEntry)
  x = SYSDSN("'"||LpaEntry||"("||Member||")'");
  LpaEntry = LEFT(LpaEntry,36)
  Select
    When ListType = "SHORT" & x = "OK" then
      Do
        Say "  LPALST+"I-1 ":" LpaEntry X
        Found = "YES"
      End
    When ListType = "LONG" then
      Do
        Say LpaEntry X
        If x = 'OK' then Found = "YES"
      End
    otherwise
      NOP
  End
  LpatPtr = X2D(LpatPtr)+45
  LpatPtr = D2X(LpatPtr)
End
If Found = "NO" then
  Say "No match for" member "in the LPALST concatenation"
Exit
