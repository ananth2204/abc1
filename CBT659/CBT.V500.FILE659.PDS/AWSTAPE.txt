//GSIAWSTP JOB ,'SIEGEL',CLASS=A,MSGCLASS=X,REGION=0M,
//         MSGLEVEL=(1,1),NOTIFY=&SYSUID,TIME=1440
//*********************************************************************
//* CONVERT TO AWSTAPE                                                *
//*                                                                   *
//* change vvvvvv to VOLSER that was dumped                           *
//* change ffffff to VOLSER of empty volume                           *
//*********************************************************************
//ASMCLG  PROC
//IEUASM  EXEC PGM=ASMA90,PARM='NOOBJECT,DECK'
//SYSPRINT DD  SYSOUT=*
//SYSLIB   DD  DSN=SYS1.MACLIB,DISP=SHR
//         DD  DSN=SYS1.MODGEN,DISP=SHR
//SYSUT1   DD  UNIT=3390,SPACE=(CYL,(5,5))
//SYSPUNCH DD  DSN=&&OBJSET,DISP=(,PASS),UNIT=3390,
//             SPACE=(TRK,(5,5)),DCB=(RECFM=FB,LRECL=80,BLKSIZE=3120)
//IEWL    EXEC PGM=IEWL,PARM='LIST,LET,NCAL,MAP',
//             COND=(0,NE,IEUASM)
//SYSPRINT DD  SYSOUT=*
//SYSUT1   DD  UNIT=3390,SPACE=(CYL,(5,5))
//SYSLIN   DD  DSN=&&OBJSET,DISP=(OLD,DELETE)
//SYSLMOD  DD  DSN=&&GOSET(GO),DISP=(,PASS),UNIT=3390,
//             SPACE=(TRK,(5,5,5)),DCB=(RECFM=U,BLKSIZE=6144)
//GO      EXEC PGM=*.IEWL.SYSLMOD,COND=((0,NE,IEUASM),(0,NE,IEWL))
//SYSUT1   DD  DISP=(OLD,DELETE,KEEP),
//             DSN=&SYSUID..vvvvvv.VOLUME.BACKUP
//SYSUT2   DD  DSN=&SYSUID..vvvvvv.AWSTAPE,DISP=(,CATLG),
//             VOL=SER=ffffff,
//             UNIT=3390,SPACE=(TRK,(6000,6000),RLSE)
//        PEND
//ASMCLG  EXEC ASMCLG
TAPECONV TITLE 'CONVERT FILE TO AWSTAPE FORMAT'
*---------------------------------------------------------------------*
* FUNCTION:                                                           *
*        THIS PROGRAM CONVERTS A TAPE FILE TO AWSTAPE FORMAT.         *
*        IT READS UNDEFINED LENGTH BLOCKS OF DATA FROM SYSUT1 AND     *
*        WRITES EACH BLOCK, PREFIXED BY A 6-BYTE HEADER, TO SYSUT2.   *
*---------------------------------------------------------------------*
TAPECONV CSECT
         LR    R12,R15                  LOAD BASE REGISTER
         USING TAPECONV,R12             ESTABLISH ADDRESSABILITY
         OPEN  (SYSUT1,INPUT)           OPEN INPUT DCB
         TM    SYSUT1+48,X'10'          IS DCB OPEN?
         BZ    EXIT020                  NO, EXIT WITH RC=20
         OPEN  (SYSUT2,OUTPUT)          OPEN OUTPUT DCB
         TM    SYSUT2+48,X'10'          IS DCB OPEN?
         BZ    EXIT020                  NO, EXIT WITH RC=20
GENLOOP  EQU   *
         GET   SYSUT1                   GET INPUT BLOCK
         LR    R2,R1                    R2=>INPUT BLOCK
         LH    R4,SYSUT1+82             R4=ACTUAL BLOCK LENGTH
         MVC   HDRPRVLN,HDRCURLN        COPY PREVIOUS BLOCK LENGTH
         STCM  R4,B'0001',HDRCURLN      STORE LOW-ORDER LENGTH BYTE
         STCM  R4,B'0010',HDRCURLN+1    STORE HIGH-ORDER LENGTH BYTE
         MVI   HDRFLAG1,HDRF1BOR+HDRF1EOR  SET COMPLETE RECORD FLAGS
         MVC   SYSUT2+82(2),=H'6'       SET HEADER LENGTH IN DCB
         PUT   SYSUT2,HEADER            WRITE BLOCK HEADER TO SYSUT2
         STH   R4,SYSUT2+82             SET BLOCK LENGTH IN DCB
         PUT   SYSUT2,(R2)              WRITE DATA BLOCK TO SYSUT2
         B     GENLOOP                  GO BACK FOR NEXT RECORD
GENEOF   DS    0H
         MVC   HDRPRVLN,HDRCURLN        COPY PREVIOUS BLOCK LENGTH
         XC    HDRCURLN,HDRCURLN        CLEAR CURRENT BLOCK LENGTH
         MVI   HDRFLAG1,HDRF1TMK        SET TAPE MARK FLAG
         MVC   SYSUT2+82(2),=H'6'       SET HEADER LENGTH IN DCB
         PUT   SYSUT2,HEADER            WRITE BLOCK HEADER TO SYSUT2
         CLOSE (SYSUT1,,SYSUT2)         CLOSE DCBS
         SR    R15,R15                  ZEROIZE RETURN CODE
         SVC   3                        EXIT WITH RC=0
EXIT020  DS    0H
         LA    R15,20                   DD STATEMENT MISSING
         SVC   3                        EXIT WITH RC=20
         DROP  R12                      DROP BASE REGISTER
*
* AWSTAPE BLOCK HEADER
*
HEADER   DS    0CL6                     BLOCK HEADER
HDRCURLN DC    XL2'0000'                CURRENT BLOCK LENGTH
HDRPRVLN DC    XL2'0000'                PREVIOUS BLOCK LENGTH
HDRFLAG1 DC    X'00'                    FLAGS BYTE 1...
HDRF1BOR EQU   X'80'                    ...BEGINNING OF RECORD
HDRF1TMK EQU   X'40'                    ...TAPE MARK
HDRF1EOR EQU   X'20'                    ...END OF RECORD
HDRFLAG2 DC    X'00'                    FLAGS BYTE 2
*
* DATA CONTROL BLOCKS
*
         PRINT NOGEN
SYSUT1   DCB   DSORG=PS,MACRF=GL,DDNAME=SYSUT1,EODAD=GENEOF,           X
               RECFM=U,LRECL=0,BLKSIZE=27998
SYSUT2   DCB   DSORG=PS,MACRF=PM,DDNAME=SYSUT2,                        X
               RECFM=U,LRECL=0,BLKSIZE=27998
         LTORG
*
* REGISTER EQUATES
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
//*
//*********************************************************************
//* XMIT AWSTAPE FILE                                                 *
//*********************************************************************
//*
