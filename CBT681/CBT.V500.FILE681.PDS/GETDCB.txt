         MACRO
&LAB     GETDCB &DCBREG,&FCBREG,&ERRADDR
.*  THIS MACRO PUTS THE ADDRESS OF THE DCB RELATED TO A PL/I FILE
.*  IN THE REGISTER INDICATED BY &DCBREG USING THE REGISTER AT &FCBREG
.*  AS  A WORK REGISTER.  THE ADDRESS OF THE FCB IS LEFT IN &FCBREG
.*
.*  PARAMETERS:
.*  &DCBREG   THE REGISTER INTO WHICH THE ADDRESS OF THE DCB IS TO
.*            BE PLACED  1 - 15
.*  &FCBREG   A WORK REGISTER  1 -  15
.*  &ERRADDR  A LABEL SPECIFYING THE POINT TO BRANCH TO IF THE FILE
.*  VARIABLE IS NOT OPEN.
.*
.*  NOTE THAT NEITHER REGISTER MAY BE REGISTER ZERO
.*  THE FOLLOWING TWO THINGS ARE ASSUMED.
.*  1.  ADDRESSABILITY IS ESTABLISTED TO PLIFILE
.*  2.  REGISTER R12 POINTS TO THE PL/I TASK COMMUNICATIONS AREA
&LAB     L     &DCBREG,PLIFILE          POINT TO FILE LOCATOR
         L     &DCBREG,0(&DCBREG)       POINT TO OFFSET INTO PRV
         L     &DCBREG,0(&DCBREG)       LOAD OFFSET INTO PRV FOR THIS
*                                       FILE
         L     &FCBREG,4(R12)           POINT TO THE PRV
         L     &FCBREG,0(&FCBREG,&DCBREG)  LOAD  FCB ADDR
         C     &FCBREG,X'64'(R12)       IS THE FILE OPEN?
         BE    &ERRADDR
         SPACE
         L     &DCBREG,X'14'(&FCBREG)   POINT TO THE DCB
         MEND
