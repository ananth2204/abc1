HEX      TITLE 'FIXED TO HEX CHARACTER CONVERSION'
***********************************************************************
*                                                                     *
*     THIS MODULE ACCEPTS AS A PARM A FIXED BINARY(31) VALUE AND      *
*     RETURNS THE VALUE IN ITS HEX CHARACTER FORMAT.  THE RETURNED    *
*     CHARACTER STRING CAN BE FIXED LENGTH OF VARYING.  IF THE        *
*     LENGTH OF THE REQUESTED STRING IS LESS THAN 8 THE RETURNED      *
*     STRING IS TRUNCATED FROM THE LEFT.  IF A FIXED LENGTH           *
*     STRING OF LENGTH GREATER THAN 8 IS REQUESTED, THE STRING IS     *
*     FILLED TO THE RIGHT.                                            *
*                                                                     *
*     THIS MODULE OPERATES IN A PL/I ENVIONMENT AS A LIBRARY          *
*     ROUTINE.   THIS LOWERS THE OVERHEAD FOR CALLS.                  *
*                                                                     *
*     TO INVOKE THIS ROUTINE IT MUST BE DESCRIBED TO THE CALLING      *
*     PL/I PROGRAM. THE BASIC FORM IS:                                *
*                                                         |-   -|     *
*            DCL HEX ENTRY(FIXED BIN(31)) RETURNS(CHAR(N) | VAR | )   *
*                                                         |-   -|     *
*                  EXTERNAL;                                          *
*                                                                     *
*     WHERE N IS THE REQUESTED NUMBER OF CHARACTERS TO BE RETURNED    *
*     AND THE KEY WORD VAR IS OPTIONAL.                               *
*                                                                     *
*     THE ENTRY MAY BE DECLARED WITH DIFFERENT RETURNS FOR EACH       *
*     CHANGE OF SCOPE OR MODULE WITHIN A PROCEDURE AND THE CHANGES    *
*     ARE DETECTED THOUGHT THE USE OF THE STRING LOCATOR/DESCRIPTOR.  *
*                                                                     *
*     AUTHOR: R.W.CALLAGHAN, DEPT.482.61                              *
*     ISSUE DATE: MARCH 14, 1977                                      *
*                                                                     *
*     ISSUE HISTORY:                                                  *
*     REASON FOR ISSUE 02:                                            *
*     ADD SYMBAR                                                      *
*                                                                     *
***********************************************************************
         EJECT
*     PROLOG
         SPACE
HEX      RENTR BASE=3,PARM=R10,WORK=(WORKLEN,WORKAREA)
HEX1     AMODE ANY
HEX1     RMODE ANY
         SPACE 3
*     TRANSLATE FIXED TO HEX
         SPACE
         LM    R14,R15,0(R10)      OBTAIN PARMS
         MVC   WORD(4),0(R14)      CONVERT INPUT PARM TO HEX CHAR
         UNPK  DWORD(9),WORD(5)
         TR    DWORD(8),HEXSTRNG
         SPACE 3
*     RETURN HEX CHARACTER STRING
         SPACE
         L     R1,0(,R15)          OBTAIN STRING LOCATOR VALUES
         LH    R2,4(,R15)
         TM    6(R15),X'80'        VARYING STRING?
         BO    HEX0020             YES: PROCESS AS VARYING
         SPACE
*     PROCESS FIXED LENGTH STRING
         SPACE
         CH    R2,=H'8'            STRING LENGTH < 8
         BL    HEX0010             YES: TRUNCATE
         MVC   0(8,R1),DWORD       MOVE OUTPUT TO STRING
         BE    HEX0040             STRING LENGTH = 8: BYPASS FILL
         MVI   8(R1),C' '          ADD BLANK TO STRING
         SH    R2,=H'9'            CALCULATE REMAING LENGTH
         BNH   HEX0040             NONE REMAING: BYPASS REMAINING FILL
         BCTR  R2,0                EXTEND BLANKS TO REMING STRING
         EX    R2,FILL
         B     HEX0040
         SPACE
*     TRUNCATE OUTPUT STRING
         SPACE
HEX0010  LA    R14,8               CALCULATE LENGTH OF TRUNCATION
         SR    R14,R2
         LA    R14,DWORD(R14)      SET POINTER TO REMAINING OUTPUT
         BCTR  R2,0                MOVE REMAING OUTPUT TO STRING
         EX    R2,TRUNCATE
         B     HEX0040
         SPACE
*     PROCESS VARYING STRING
         SPACE
HEX0020  CH    R2,=H'8'            ALLOCATE STRING LENGTH < 8
         BL    HEX0030             YES: TRUNCATE
         MVC   2(8,R1),DWORD       MOVE OUTPUT TO STRING
         MVC   0(2,R1),=H'8'       SET CURRENT LENGTH OF STRING
         B     HEX0040
HEX0030  STH   R2,0(,R1)           STORE CURRENT LENGTH
         LA    R1,2(R1)            ADJUST STRING POINTER
         B     HEX0010
         SPACE
*     RETURN
         SPACE
HEX0040  DS    0H
         REXIT
         SPACE 3
*     CONSTANTS
         SPACE
FILL     MVC   9(*-*,R1),8(R1)     EXTEND BLANK TO FILL STRING
TRUNCATE MVC   0(*-*,R1),0(R14)    MOVE OUTPUT TO STRING
         LTORG
*FILLADDR EQU   246-(*-HEX)
*        DC    (FILLADDR)C'#'      FILL TO ALLOW ADDRESSIBILITY
HEXSTRNG EQU   *-240
         DC    C'0123456789ABCDEF' HEX CHARACTER REPRENSATION STRING
WORKAREA DSECT
         DS    26F
WORD     DS    2F
DWORD    DS    2D
WORKLEN  EQU   *-WORKAREA
         SPACE 3
         SYMBAR MODE=DEC
         END
