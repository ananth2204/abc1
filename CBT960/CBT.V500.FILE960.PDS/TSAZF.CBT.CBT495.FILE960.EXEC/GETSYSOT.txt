/******************************** REXX ********************************
  This REXX Requires TSO because it uses:
       - The PDS/PDS86 program from http://CBTTape.org
       - The SDSF REXX Interface

  This REXX also uses:
       - MVS to writes the merged output to the REPORTS DD member
 ******************************** Rexx ********************************/
TRACE("O")                     /* Set REXX TRACE for Debugging        */
MsgStat=MSG("OFF")
ARG Element .

CALL INITIALIZE_REXX

x=ISFCALLS('ON')
  ADDRESS "SDSF" "ISFEXEC DA"
  CALL CAPTURE_LIST_SYSOUT 1
x=ISFCALLS('OFF')

CALL WRITE_LIST_SYSOUT

MsgStat=MSG(MsgStat)
EXIT
/*
       Setup Various Variable to be used by this REXX
*/
INITIALIZE_REXX:
  TRUE=1; FALSE=0
  StepLst   = "MAPP030  MAPP040  COL902   ASMMAPR  LINKMAPR",
              "CICSPREC ASMPGM   LINKEDIT CICSCOPY AMPMCPRE",
              "GENPGM   DYL280   LINKDYL  PREPRCSR"
  SysoutLst = "STATOUT  STATOUT  SYSTSPRT SYSPRINT SYSPRINT",
              "SYSPRINT SYSPRINT SYSPRINT SYSPRINT SYSPRINT",
              "SYSPRINT SYSPRINT SYSPRINT LSTIN   "
  FndDds.=""; FndDds.0=0
  IsfJesName = MVSVAR("SYMDEF","JES")
  IsfOwner = USERID(); IsfPrefix = MVSVAR("SYMDEF","JOBNAME")
  IsfSysname = MVSVAR("SYMDEF","SYSNAME")
RETURN
/*
      Capture Utility SYSOUT from Current PROC Run ...
*/
CAPTURE_LIST_SYSOUT:
ARG RowNo .
  ADDRESS "SDSF" "ISFACT DA TOKEN('"Token.RowNo"') PARM(NP ?) (PREFIX Jds)"
  DO i=(JdsDdName.0-5) TO JdsDdName.0
     IF ¬(ABBREV(Element,JdsStepN.i))           THEN ITERATE i
     IF WORDPOS(JdsDdName.i,SysoutLst) = 0      THEN ITERATE i
     StepCnts = WORDPOS(JdsProcS.i,StepLst)
     IF StepCnts = 0                            THEN ITERATE i
     IF JdsDdName.i ¬= WORD(SysoutLst,StepCnts) THEN ITERATE i

     ADDRESS "SDSF" "ISFACT DA TOKEN('"JdsToken.i"') PARM(NP SA)"
     tmp = 1+FndDds.0; FndDds.0 = tmp
     ADDRESS "MVS" "EXECIO * DISKR" IsfDdName.1 "(STEM FndDds."tmp". FINIS"
  END
RETURN
/*
      Save Captured utility SYSOUTs to Specified Reports DD
*/
WRITE_LIST_SYSOUT:
  x = BPXWDYN("INFO DD(REPORTS) INRTDSN(Dsn)") /* Get MapPrm DSN      */
  x = BPXWDYN("ALLOC DD(REPORTS) DSN('"Dsn"("Element")') BUFNO(75) SHR REUSE")
  DO k = 1 TO FndDds.0
     ADDRESS "MVS" "EXECIO * DISKW REPORTS (STEM FndDds."k"."
  END
  ADDRESS "MVS" "EXECIO 0 DISKW REPORTS (FINIS"
  x = BPXWDYN("FREE DD(REPORTS)")
  DROP FndDds. ; FndDds.=""; FndDds.0=0

  ADDRESS "TSO" "PDS '"Dsn"' A" Element "ADDSTATS"
RETURN
