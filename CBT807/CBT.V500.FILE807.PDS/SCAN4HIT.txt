*   THIS PROGRAM IS A SUBROUTINE WHICH WILL SCAN A FIELD OF UP
*   TO 168 BYTES IN LENGTH FOR AN ARGUMENT OF UP TO 80 BYTES IN
*        LENGTH AND INFORM THE INVOKER OF WHETHER A MATCH WAS FOUND.
*
*        TO CALL THIS PROGRAM, DO THE FOLLOWING :
*
*           MOVE YOUR ARGUMENT TO ARGUMENT OF SCAN4HIT-I-O-AREA.
*           MOVE THE LENGTH OF YOUR ARGUMENT
*             TO ARGUMENT-LENGTH OF SCAN4HIT-I-O-AREA.
*           MOVE YOUR RECORD TO BE SCANNED
*             TO TABLE-ELEMENT OF SCAN4HIT-I-O-AREA.
*           MOVE THE LENGTH OF YOUR RECORD TO BE SCANNED
*             TO TABLE-ELEMENT-LENGTH OF SCAN4HIT-I-O-AREA.
*
*           CALL 'SCAN4HIT' USING SCAN4HIT-I-O-AREA.
*
*           AFTER THE CALL, HIT-SWITCH OF SCAN4HIT-I-O-AREA WILL
*           CONTAIN EITHER 'Y' OR 'N'.  'Y' MEANS A MATCH WAS
*           FOUND AND 'N' MEANS A MATCH WAS NOT FOUND.
*           IF HIT-SWITCH ='Y' ARG-POSITION IS BEGINNING POS OF STRNG
*
*                WHERE:
*                      01  SCAN4HIT-I-O-AREA.
*                          05  ARGUMENT              PIC X(80).
*                          05  ARGUMENT-LENGTH       PIC S9(4) COMP.
*                          05  TABLE-ELEMENT         PIC X(168).
*                          05  TABLE-ELEMENT-LENGTH  PIC S9(4) COMP.
*                          05  ARG-POSITION          PIC S9(4) COMP.
*                          05  HIT-SWITCH            PIC X.
*
*         RETURN CODES        MEANING
*                 0           MATCH WAS FOUND
*                 4           NO MATCH WAS FOUND
*                 8           INVALID PARAMETERS PASSED TO PROGRAM
SCAN4HIT CSECT
         PRINT NOGEN
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SAVE  (14,12)
         BALR  12,0
         USING *,12
         ST    13,SAVEAREA+4
         LA    13,SAVEAREA
         B     STARTIT
SAVEAREA DC    18F'0'
********************************************************************
*        SAVE REGISTER 1 IN REGISTER 11 AND LOAD PARAMETER FIELD   *
*        INTO IOAREA                                               *
********************************************************************
STARTIT  LR    R11,R1              SAVE REGISTER 1 CONTENTS IN REG 11
         L     R9,0(R11)
         MVC   IOAREA,0(R9)
********************************************************************
*        M A I N   L I N E   L O G I C                             *
********************************************************************
         LA    R10,0
         LH    R8,INARGLEN
         LH    R9,INTBLLEN
         LTR   R8,R8
         BNP   NOGOOD
         CH    R8,=H'80'
         BH    NOGOOD
         LTR   R9,R9
         BNP   NOGOOD
         CH    R9,=H'168'     FIELD IN RECORD TO BE SEARCHED @MK
         BH    NOGOOD
         CR    R8,R9
         BH    NOGOOD
         LA    R6,ARGUMENT
         LA    R7,TBLELMNT
         BCTR  R8,0            SUBTRACT 1
         AR    R9,R7
         SR    R9,R8
         BCTR  R9,0            SUBTRACT 1
LOOP     CR    R7,R9
         BH    NOHIT
         EX    R8,DOCOMPR
CHEK     BE    HIT
         LA    R7,1(R7)        ADD 1
         B     LOOP
NOHIT    MVI   HITSW,C'N'
         LA    R10,4
         B     SHUTDOWN
NOGOOD   MVI   HITSW,C' '
         LA    R10,8
         B     SHUTDOWN
HIT      MVI   HITSW,C'Y'
         LA    R5,TBLELMNT-1    GET ADDR OF RECORD (MINUS 1)   @MK
         SR    R7,R5            GET POSITION OF FOUND STRING   @MK
         STH   R7,ARGPOSIT      STORE IT IN PARAMETER LIST     @MK
         B     SHUTDOWN
DOCOMPR  CLC   0(0,R6),0(R7)
         B     CHEK
*    E N D  O F   J O B   P R O C E S S I N G                  *
SHUTDOWN LR    R15,R10
         L     R9,0(R11)
         MVC   0(255,R9),IOAREA
         L     R13,4(R13)
         RETURN (14,12),RC=(15)
*    BREAKDOWN OF IOAREA                                       *
IOAREA   DS    0CL255        @MK
ARGUMENT DS    CL80
INARGLEN DS    H
TBLELMNT DS    CL168         @MK
INTBLLEN DS    H
ARGPOSIT DS    H             @MK
HITSW    DS    CL1
         LTORG
         END
