         TITLE  'SYNADERR ROUTINE'
         PRINT  NOGEN
*        R12 - BASE REGISTER
*        AFTER ISSUING THE SYNADAF MACRO R1 HAS THE MSG BUFFER ADDR
*              AND R0 HAS THE SYNADAF RETURN CODE
*        R2 IS LOADED WITH THE MSG BUFFER ADDR (FROM R1)
*          AT OFFSET 8 OF R2 IS THE INPUT BUFFER ADDRESS
*            IF THIS ADDRESS IS BLANK-NO DATA WAS PASSED TO SYNAD
*            R3 IS LOADED WITH THIS ADDR IF ITS NOT BLANK
*          AT OFFSET 12 OF R2 IS THE NUMBER OF BYTES READ(LOADED IN R4)
*        R10 IS LOADED WITH THE RETURN CODE (FROM R0)
*        R6 - LOADED WITH 120 - LENGTH OF PRINT LINE
*        R4 - NUMBER OF BYTES REMAINING IN THE BLOCK TO BE PRINTED
*        R11- BAL REGISTER
*        SYNADERR CAN BE INVOKED IN 4 WAYS
*      1 NO EROPT PARAMETER ON THE SYSOUT DD - INVOKED 5 TIMES AT MOST
*      2 EROPT=ABE INVOKED AT MOST ONCE (SIMULATES ALL OTHER PROGRAMS)
*      3 EROPT=SKP INVOKED AT MOST 25 TIMES
*      4 EROPT=ACC INVOKED FOR I/O ERRORS
*        SAMPLE:  //SYSOUT  DD  SYSOUT=A,DCB=EROPT=ACC
*        NOTE-MESSAGE SEL076 WILL PRINT THE BAD BLOCK AT MOST 5 TIMES
SYNADERR START  0
         EXTRN  ABEND,SETRC  ADDR TO BR BACK TO AFTER 5 CALLS & SET RC
         EXTRN  RETURN3 ADDR TO BR BACK TO AFTER 5 CALLS-LIBFILE/LIBOUT
         USING  *,R15
         STM    R13,R12,SAVEAREA     SAVE REGISTERS
         BALR   R12,0               ESTABLISH R12 AS BASE REGISTER
         USING  *,R12
         SYNADAF ACSMETH=QSAM
         LR     R2,R1              LOAD MSG BUFFER ADDR INTO R2
         LR     R10,R0             LOAD RETURN CODE FROM SYNADAF MACRO
OPEN     OPEN   (SYSOUT,OUTPUT)      OPEN SYSOUT
         TM     SYSOUT+48,X'10'     IS SYSOUT IN JCL OR DYNAMIC ALLOCAT
         BNO    DYNALLOC             NO - DYNAMICALLY ALLOCATE IT
EROPTPRC NOP    SKPSETUP   SET TO BRANCH AFTER FIRST TIME THRU
         MVI    EROPTPRC+1,X'F0'
         TM     SYSOUT+84,X'80'    IS EROPT=ACC CODED ON SYSOUT DD
         BO     ALLERRS            YES - ALLOW ALL I/O ERRORS
         TM     SYSOUT+84,X'40'    IS EROPT=SKP CODED ON SYSOUT DD
         BO     ALLOW25            YES - ALLOW 25 I/O ERRORS
         TM     SYSOUT+84,X'20'    IS EROPT=ABE CODED ON SYSOUT DD
         BNO    SKPSETUP           NO  - USE DEFAULT (5)
         ZAP    ERRALLOW,=P'1'     YES - ALLOW ONLY 1 I/O ERROR
SKPSETUP AP     NUMBNVOK,=P'1'     ADD 1 TO BAD BLOCKS OUTPUT
         CP     NUMBNVOK,=P'5'     HAVE 5 BLOCKS BEEN PUT OUT BY SYNAD
         BNH    CONTINUE              NO
         MVI    NOMOPRT+1,X'F0'  YES-SET BR,NO MORE BAD BLOCKS PRINTED
CONTINUE MVC    0(8,R2),=C'SEL076E '  MOVE IN MSG NO.
         L      R7,=A(SETRC)      SET THE RETURN CODE TO 8
         MVI    3(R7),X'08'
         MVC    SAVDDNAM,75(R2)    SAVE THE DDNAME OF FILE IN ERROR
         CH     R10,=H'4'          IS RC = 4
         BE     DATAREAD        YES-DATA WAS PASSED TO BUFFER
         BL     MOVEBUF           RC=0-NO DATA WAS READ
         CLI    8(R2),C' '      RC=8,IS INPUT BUFFER ADDRESS BLANK
         BE     MOVEBUF         YES -NO DATA WAS READ
DATAREAD L      R3,8(R2)    NO- LOAD R3 WITH INPUT BUFFER ADDR
         LH     R4,12(R2)       LOAD R4 WITH NUMBER OF BYTES READ
         MVC    BLKSIZE(8),MASK RESET BLKSIZE
         CVD    R4,WORKFLD      CONVERT BLKSIZE TO DEC
         ED     BLKSIZE(8),WORKFLD+4
         MVC    8(40,R2),BLKSIMSG   MOVE IN BLKSIZE MSG
         BAL    R11,WRITE       WRITE SYNAD MSG
NOMOPRT  NOP    CLOSE  SET TO BR AFTER 5 BAD BLOCKS TO PREVENT WRITE
         MVC    8(120,R2),RULER   PLACE RULER FOLLOWING SEL076
         BAL    R11,WRITE
         LA     R6,120         LOAD R6 WITH LENGTH OF PRINT LINE
LOOP     CR     R4,R6  IS NO. OF BYTES LEFT LESS THAN PRINT LINE LENGTH
         BL     ENDBLK              YES-FINISH LAST BLOCK
         EX     R6,MOVEINPT        MOVE INPUT DATA TO MSG BUFFER
         AR     R3,R6              UP INPUT ADDR POINTER
         SR     R4,R6              SUBTRACT TO GET NO. OF BYTES LEFT
         BAL    R11,WRITE           PRINT A LINE OF THE RECORD
         B      LOOP
         SPACE  2
ENDBLK   LTR    R4,R4            IS THERE ANYMORE DATA
         BZ     NOMODATA
         BCTR   R4,0             SET LENGTH OF BYTES TO BE MOVED TO PRT
         EX     R4,MOVEINPT      MOVE DATA TO MSG BUFFER
         BAL    R11,WRITE                   WRITE
NOMODATA BAL    R11,WRITE         WRITE BLANK LINE
CLOSE    CLOSE  (SYSOUT)
         FREEPOOL SYSOUT
         SYNADRLS                      RELEASE MSG BUFFER
         CP     NUMBNVOK,ERRALLOW  HAVE MAX NO. OF BAD BLOCKS OUTPUT
         BNE    SYNADXIT     NO
         CLC    SAVDDNAM(3),=C'LIB' IS THIS THE LIBFILE OF LIBOUT DDNAM
         BE     LIBERR   YES- RETURN TO LIB CSECT AND NOT ABEND LABEL
ERROR    MVC    SAVEAREA+4(4),=V(ABEND)  CHANGE R14 TO RETURN TO ABEND
SYNADXIT LM     R13,R12,SAVEAREA          RESTORE REGISTERS
         BR     R14                  RETURN TO SYSTEM
         SPACE  2
MOVEINPT MVC    8(0,R2),0(R3)    MOVE INPUT DATA TO MSG BUFFER TO PRT
         SPACE  2
MOVEBUF  MVC    8(40,R2),BADBLK     'UNABLE TO RECOVER RECORDS' MSG
         BAL    R11,WRITE              WRITE
         B      CLOSE
         SPACE  2
WRITE    PUT    SYSOUT,(R2)
         MVI    0(R2),C' '           SPACE OUT MSG BUFFER
         MVC    1(127,R2),0(R2)
         BR     R11
         SPACE  2
ALLERRS  ZAP    ERRALLOW,=P'-1'    ALLOW INFINITE NO. OF BAD BLOCKS
         B      SKPSETUP           COMPAISON WILL NEVER BE EQUAL
         SPACE  2
ALLOW25  ZAP    ERRALLOW,=P'25'    ALLOW 25 BAD BLOCKS
         B      SKPSETUP
         SPACE  2
LIBERR   L      R7,=A(RETURN3)     SET LIB ERROR RC=8
         MVI    3(R7),X'08'
         B      SYNADXIT           RETURN VIA CONTROL PGM
         SPACE  2
DYNALLOC LA     R1,REQBLKP     DYNAMICALLY ALLOCATE SYSOUT DATASET
         DYNALLOC
         LH     R15,RBERR
         LTR    R15,R15              SUCCESSFUL?
         BNZ    ERROR                   NO-JUST GET OUT WITH ERR MSG
         B      OPEN                    YES- DO OPEN AGAIN
         SPACE  2
WORKFLD  DS     D
SAVEAREA DS     16F
SAVDDNAM DS     CL8
NUMBNVOK DC     PL8'0'    COUNTS NMBER OF TIMES SYNAD IS INVOKED
ERRALLOW DC     PL2'5'    SET DEFAULT MAX NO. OF BAD BLOCKS ALLOWED
MASK     DC     X'4020202020202120'
RULER    DC     C'----+----1----+----2----+----3----+----4----+----5'
         DC     C'----+----6----+----7----+----8----+----9----+----0'
         DC     C'----+----1----+----2'   120 BYTE RULER
BADBLK   DC     CL40'UNABLE TO RECOVER RECORDS FROM BAD BLOCK'
BLKSIMSG DS     0CL40
         DC     C'NUMBER OF BYTES READ IN BLOCK = '
BLKSIZE  DC     X'4020202020202120'
SYSOUT   DCB    DDNAME=SYSOUT,DSORG=PS,MACRF=PM,RECFM=FB,LRECL=128,    X
               BLKSIZE=1280
         SPACE  2
         DS     0F
REQBLKP  DC     X'80',AL3(REQBLK)
REQBLK   DC     X'14'
         DC     X'01'      DS ALLOC
         DC     X'1000'    FOR SYSOUT FILES
RBERR    DC     H'0',X'0000'
TXTP     DC     A(TXTUNTP)
         DC     XL8'00'
TXTUNTP  DC     A(TUDDNAM)
         DC     X'80',AL3(TUSYSOUT)
TUDDNAM  DC     X'0001'
         DC     X'0001'
         DC     X'0008'
         DC     CL8'SYSOUT'
TUSYSOUT DC     X'0018'
         DC     X'0000'
         SPACE  2
         LTORG
R0       EQU    0          SYNADAF RETURN CODE
R1       EQU    1          ADDRESS OF MSG BUFFER
R2       EQU    2           SAME AS R1
R3       EQU    3          POINTER IN THE INPUT RECORD BUFFER
R4       EQU    4           NUMBER OF BYTES LEFT IN BUFFER
R5       EQU    5
R6       EQU    6           LENGTH OF PRINT LINE
R7       EQU    7
R8       EQU    8
R9       EQU    9
R10      EQU    10         RC
R11      EQU    11         BAL REGISTER
R12      EQU    12         BASE REGISTER
R13      EQU    13
R14      EQU    14
R15      EQU    15
         END
