         TITLE 'RX3270'
*
RX3270   CSECT
RX3270   AMODE 31
RX3270   RMODE 24
*
         STM   R14,R12,12(R13)
         LR    R12,R15
         USING RX3270,R12,R11
         LA    R14,SAVEAREA
         ST    R14,8(R13)
         ST    R13,SAVEAREA+4
         LA    R13,SAVEAREA
         LA    R11,4095(R12)
*
         STFSMODE ON,INITIAL=YES
         STTMPMD  ON
         STLINENO LINE=1
*
         MVC   NAME,V1
         MVC   NL,V1L
         LINK  EP=IKJCT441,PARAM=(ECR,NP,NL,VP,VL,TK),VL=1
         LTR   R15,R15
         BNZ   ERROR01
         L     R5,VL
         L     R4,VP
         STORAGE OBTAIN,LENGTH=(R5),ADDR=(R6),LOC=BELOW
         LTR   R15,R15
         BNZ   ERROR02
         ST    R6,AREAADR
         ST    R5,AREALEN
         LR    R2,R6
         LR    R3,R5
         LR    R7,R5
         MVCL  R6,R4
         LR    R6,R2
         LR    R2,R3
MAPPUT   EQU   *
         TPUT  (R6),(R2),FULLSCR
*
         TGET  BUFIN,L'BUFIN,ASIS
*
AFTERGET EQU   *
         MVC   NAME,V2
         MVC   NL,V2L
         LA    R2,BUFIN
         ST    R2,VP
         L     R2,BUFLEN
         ST    R2,VL
         LINK  EP=IKJCT441,PARAM=(ECU,NP,NL,VP,VL,TK),VL=1
*
         BAL   R10,AIDCHECK
         MVC   NAME,V4
         MVC   NL,V4L
         LA    R2,AIDMN
         ST    R2,VP
         LA    R2,L'AIDMN
         ST    R2,VL
         LINK  EP=IKJCT441,PARAM=(ECU,NP,NL,VP,VL,TK),VL=1
*
         L     R6,AREAADR
         L     R2,AREALEN
         STORAGE RELEASE,ADDR=(R6),LENGTH=(R2)
         LTR   R15,R15
         BNZ   ERROR03
*
NOFREE   EQU   *
         STFSMODE OFF
*
         LA    R15,0
*
RETURN   L     R13,SAVEAREA+4
         LM    R14,R12,12(R13)
         XR    R15,R15
         BR    R14
*
AIDCHECK EQU   *
         CLI   BUFIN,X'60'
         BNE   AID1
         MVC   AIDMN,=CL5'NOAID'
         BR    R10
AID1     CLI   BUFIN,X'7D'
         BNE   AID2
         MVC   AIDMN,=CL5'ENTER'
         BR    R10
AID2     CLI   BUFIN,X'F1'
         BNE   AID3
         MVC   AIDMN,=CL5'PF01 '
         BR    R10
AID3     CLI   BUFIN,X'F2'
         BNE   AID4
         MVC   AIDMN,=CL5'PF02 '
         BR    R10
AID4     CLI   BUFIN,X'F3'
         BNE   AID5
         MVC   AIDMN,=CL5'PF03 '
         BR    R10
AID5     CLI   BUFIN,X'F4'
         BNE   AID6
         MVC   AIDMN,=CL5'PF04 '
         BR    R10
AID6     CLI   BUFIN,X'F5'
         BNE   AID7
         MVC   AIDMN,=CL5'PF05 '
         BR    R10
AID7     CLI   BUFIN,X'F6'
         BNE   AID8
         MVC   AIDMN,=CL5'PF06 '
         BR    R10
AID8     CLI   BUFIN,X'F7'
         BNE   AID9
         MVC   AIDMN,=CL5'PF07 '
         BR    R10
AID9     CLI   BUFIN,X'F8'
         BNE   AID10
         MVC   AIDMN,=CL5'PF08 '
         BR    R10
AID10    CLI   BUFIN,X'F9'
         BNE   AID11
         MVC   AIDMN,=CL5'PF09 '
         BR    R10
AID11    CLI   BUFIN,X'7A'
         BNE   AID12
         MVC   AIDMN,=CL5'PF10 '
         BR    R10
AID12    CLI   BUFIN,X'7B'
         BNE   AID13
         MVC   AIDMN,=CL5'PF11 '
         BR    R10
AID13    CLI   BUFIN,X'7C'
         BNE   AID14
         MVC   AIDMN,=CL5'PF12 '
         BR    R10
AID14    CLI   BUFIN,X'6C'
         BNE   AID15
         MVC   AIDMN,=CL5'PA1  '
         BR    R10
AID15    CLI   BUFIN,X'6E'
         BNE   AID16
         MVC   AIDMN,=CL5'PA2  '
         BR    R10
AID16    CLI   BUFIN,X'6B'
         BNE   AID17
         MVC   AIDMN,=CL5'PA3  '
         BR    R10
AID17    CLI   BUFIN,X'6D'
         BNE   AID99
         MVC   AIDMN,=CL5'CLEAR'
         BR    R10
AID99    MVC   AIDMN,=CL5' '
         BR    R10
*
ERROR01  EQU   *
         WTO   'RX3270: RX3270_INMAP VARIABLE IS MISSING'
         LA    R15,255
         B     RETURN
ERROR02  EQU   *
         WTO   'RX3270: GETMAIN FAIL FOR MAP AREA'
         LA    R15,255
         B     RETURN
ERROR03  EQU   *
         WTO   'RX3270: FREEMAIN FAIL FOR MAP AREA'
         LA    R15,255
         B     RETURN
*
         LTORG
IDPRG    DC    CL8'RX3270'
V0       DC    CL32' '
V0L      DC    F'0'
V1       DC    CL12'RX3270_INMAP'
V1L      DC    F'12'
V2       DC    CL13'RX3270_OUTMAP'
V2L      DC    F'13'
V3       DC    CL11'RX3270_ASIS'
V3L      DC    F'11'
V4       DC    CL10'RX3270_AID'
V4L      DC    F'10'
SAVEAREA DC    18F'0'
AIDMN    DC    CL5' '
         DS    0D
AREALEN  DS    F
AREAADR  DS    F
DBLNUM   DS    D
NAME     DS    CL32
NP       DC    A(NAME)
NL       DS    F
VL       DS    F
VP       DS    F
TK       DC    F'0'
ECR      DC    A(TSVERETR)
ECU      DC    A(TSVEUPDT)
IOAREA   DS    F
BUFLEN   DC    F'1920'
         DS    0D
BUFIN    DS    CL1920
BUFOUT   DS    CL2048
         ORG   ,
*        CONTROL BLOCKS
         PRINT NOGEN
         IKJTSVT
         YREGS
*
         END
