TSO service consumption reporting
A useful capability when performing application or program tuning or comparative
analysis in the TSO environment is that of being able to measure service
consumption characteristics quickly and easily.  In the simplest form, this may
be accomplished by executing the TIME command prior to invoking the program or
application to be measured, re-executing TIME after completion, and subtracting
the reported service value of the first display from that of the second.  What
is noticeably absent is a breakdown of the service consumed into the four
measurable categories (CPU, SRB, MSO, and IOC), without which any analysis is of
limited value.  The annoyance associated with the need to subtract the before
and after values further adds to the unattractiveness of this method.
SERVMON is a tool which addresses the deficiencies mentioned above.  It consists
of two command processors, SERVPFX (SERVMON prefix) and SERVSFX (SERVMON
suffix), which may be executed explicitly or as part of a CLIST.  SERVPFX
captures initial service values and passes them to SERVSFX.  SERVSFX captures
final service values, performs the necessary subtractions, and reports the
detailed results.
The following code illustrates a command sequence that will report the service
consumed by a LISTC of datasets catalogued under the issuer’s prefix:
FREE F(SERVMSG,SERVRPT)
ALLOC F(SERVMSG) DA(*)
ALLOC F(SERVRPT) DA(*)
SERVPFX
LISTC
SERVSFX
Both SERVPFX and SERVSFX must be authorised as command processors as described
in the MVS System Programming Library: TSO manual, or via an installation-
defined alternative.
SERVPFX
SERVPFX  CSECT
         SAVE  (14,12)                  ADDRESSABILITY
         BALR  R12,0
         USING *,R12
         ST    R13,SAVEAREA+4           SAVE AREA LINKAGE
         MVC   8(4,R13),=A(SAVEAREA)
         LA    R13,SAVEAREA
         OPEN  (SERVMSG,(OUTPUT))       OPEN MESSAGE DATA SET
         TM    SERVMSG+DCBOFLGS-IHADCB,DCBOFOPN OK?
         BO    SERV0025                 YES
         WTO   'SERVPFX00 - SERVMSG DATA SET CANNOT BE OPENED',        X
               ROUTCDE=(11),DESC=(7)
         B     SERV0100
SERV0025 L     R3,PSATOLD-PSA           CURRENT TCB
         L     R3,TCBJSTCB-TCB(R3)      JOBSTEP TCB
         CLC   TCBTCBID-TCB(L'TCBTCBID,R3),=C'TCB ' IS IT?
         BE    SERV0030                 YES
         PUT   SERVMSG,MSG01            NO
         B     SERV0090
SERV0030 ICM   R4,15,TCBUSER-TCB(R3)    GET TCB USER FIELD
         BZ    SERV0040                 IT'S AVAILABLE
         CLC   0(L'SERVID,R4),SERVID    IS IT OUR STORAGE?
         BE    SERV0060                 YES
         PUT   SERVMSG,MSG02            NO
         B     SERV0090
SERV0040 L     R0,GETMDATA              LOAD GETMAIN DATA
         GETMAIN R,LV=(0)               GET STORAGE
         LR    R4,R1                    SAVE ADDRESS
         USING SERVDSCT,R4
         MVC   SERVHDR,SERVID           STORE IDENTIFIER
         MODESET KEY=ZERO               KEY ZERO
         ST    R4,TCBUSER-TCB(R3)       STORE ADDRESS
         MODESET KEY=NZERO              PROBLEM STATE KEY
SERV0060 LA    R1,SYSEVNT1              SYSEVENT OUTPUT AREA 1
         SYSEVENT REQSVDAT,ENTRY=SVC    GET STARTING STATISTICS
         LTR   R15,R15                  SYSEVENT OK?
         BZ    SERV0090                 YES
         CVD   R15,WORK                 CONVERT TO DECIMAL
         UNPK  MSG03+37(2),WORK+6(3)    UNPACK
         OI    MSG03+38,X'F0'           DE-SIGN
         PUT   SERVMSG,MSG03            ISSUE MESSAGE
SERV0090 CLOSE SERVMSG                  CLOSE MESSAGE DATA SET
SERV0100 L     R13,4(R13)
         RETURN (14,12),RC=0            RETURN
* DATA AREA *
SAVEAREA DC    18F'0'
SERVID   DC    CL16'SERVMON STORAGE'    STORAGE IDENTIFIER
GETMDATA DC    0F'0',AL1(251),AL3(SERVLEN) GETMAIN DATA
MSG01    DC    CL80'SERVPFX01 - JOBSTEP TCB NOT FOUND'
MSG02    DC    CL80'SERVPFX02 - TCBUSER FIELD IN USE'
MSG03    DC    CL80'SERVPFX03 - SYSEVENT RETURN CODE WAS XX'
WORK     DC    D'0'                     WORK FIELD
SERVMSG  DCB   DDNAME=SERVMSG,DSORG=PS,MACRF=PM,DEVD=DA,RECFM=FB,      X
               LRECL=80,BLKSIZE=4080
* DSECTS *
SERVDSCT DSECT
SERVHDR  DC    CL16' '                  HEADER CONTAINING IDENTIFIER
SYSEVNT1 DS    0F                       SYSEVENT NO. 1
TOTSRV1  DS    F                        TOTAL SERVICE
TRNSAT1  DS    F                        TRANSACTION ACTIVE TIME
PERFGP1  DS    H                        PERFORMANCE GROUP
NOTRNS1  DS    H                        NO. OF TRANSACTIONS
RESTIM1  DS    F                        RESIDENCY TIME
CPUSRV1  DS    F                        CPU SERVICE
IOCSRV1  DS    F                        IOC SERVICE
MSOSRV1  DS    F                        MSO SERVICE
SRBSRV1  DS    F                        SRB SERVICE
SYSEVNT2 DS    0F                       SYSEVENT NO. 2
TOTSRV2  DS    F                        FIELDS SAME AS FOR 1
TRNSAT2  DS    F
PERFGP2  DS    H
NOTRNS2  DS    H
RESTIM2  DS    F
CPUSRV2  DS    F
IOCSRV2  DS    F
MSOSRV2  DS    F
SRBSRV2  DS    F
SERVLEN  EQU   *-SERVHDR
         IHAPSA
         IKJTCB  DSECT=YES
         IHADCB  DEVD=DA,DSORG=PS
         END
SERVSFX
SERVSFX  CSECT
         SAVE  (14,12)                  ADDRESSABILITY
         BALR  R12,0
         USING *,R12
         ST    R13,SAVEAREA+4           SAVE AREA LINKAGE
         MVC   8(4,R13),=A(SAVEAREA)
         LA    R13,SAVEAREA
         OPEN  (SERVMSG,(OUTPUT))       OPEN MESSAGE DATA SET
         TM    SERVMSG+DCBOFLGS-IHADCB,DCBOFOPN OK?
         BO    SERV0025                 YES
         WTO   'SERVSFX00 - SERVMSG DATA SET CANNOT BE OPENED',        X
               ROUTCDE=(11),DESC=(7)
         B     SERV5020
SERV0025 L     R3,PSATOLD-PSA           CURRENT TCB
         L     R3,TCBJSTCB-TCB(R3)      JOBSTEP TCB
         CLC   TCBTCBID-TCB(L'TCBTCBID,R3),=C'TCB ' IS IT?
         BE    SERV0030                 YES
         PUT   SERVMSG,MSG01            NO
         B     SERV5000
SERV0030 ICM   R4,15,TCBUSER-TCB(R3)    GET TCB USER FIELD
         BZ    SERV0040                 WAS NOT PLUGGED BY SERVPFX
         CLC   0(L'SERVID,R4),SERVID    IS IT OUR STORAGE?
         BE    SERV0060                 YES
         PUT   SERVMSG,MSG02            NO
         B     SERV5000
SERV0040 PUT   SERVMSG,MSG04
         B     SERV5000
SERV0060 USING SERVDSCT,R4
         LA    R1,SYSEVNT2              SYSEVENT OUTPUT AREA 2
         SYSEVENT REQSVDAT,ENTRY=SVC    GET ENDING STATISTICS
         LTR   R15,R15                  SYSEVENT OK?
         BZ    SERV0090                 YES
         CVD   R15,WORK1                CONVERT TO DECIMAL
         UNPK  MSG03+37(2),WORK1+6(2)   UNPACK
         OI    MSG03+38,X'F0'           DE-SIGN
         PUT   SERVMSG,MSG03            ISSUE MESSAGE
         B     SERV0110
SERV0090 L     R1,TOTSRV2               ENDING TOTAL SERVICE
         S     R1,TOTSRV1               STARTING/ENDING DIFFERENCE
         CVD   R1,WORK1                 CONVERT TO PACKED
         UNPK  DISPTOT(7),WORK1+4(4)    UNPACK
         OI    DISPTOT+6,X'F0'          DE-SIGN
         L     R1,CPUSRV2               ENDING CPU SERVICE
         S     R1,CPUSRV1               STARTING/ENDING DIFFERENCE
         CVD   R1,WORK1                 CONVERT TO PACKED
         UNPK  DISPCPU(7),WORK1+4(4)    UNPACK
         OI    DISPCPU+6,X'F0'          DE-SIGN
         L     R1,IOCSRV2               STARTING IOC SERVICE
         S     R1,IOCSRV1               STARTING/ENDING DIFFERENCE
         CVD   R1,WORK1                 CONVERT TO PACKED
         UNPK  DISPIOC(7),WORK1+4(4)    UNPACK
         OI    ISPIOC+6,X'F0'           DE-SIGN
         L     R1,MSOSRV2               STARTING MSO SERVICE
         S     R1,MSOSRV1               STARTING/ENDING DIFFERENCE
         CVD   R1,WORK1                 CONVERT TO PACKED
         UNPK  DISPMSO(7),WORK1+4(4)    UNPACK
         OI    DISPMSO+6,X'F0'          DE-SIGN
         L     R1,SRBSRV2               STARTING SRB SERVICE
         S     R1,SRBSRV1               STARTING/ENDING DIFFERENCE
         CVD   R1,WORK1                 CONVERT TO PACKED
         UNPK  DISPSRB(7),WORK1+4(4)    UNPACK
         OI    DISPSRB+6,X'F0'          DE-SIGN
         OPEN  (SERVRPT,(OUTPUT))       OPEN REPORT DATA SET
         TM    SERVRPT+DCBOFLGS-IHADCB,DCBOFOPN OK?
         BO    SERV0100                 YES
         PUT   SERVMSG,MSG05
         B     SERV5000
SERV0100 PUT   SERVRPT,H0               PRINT HEADINGS
         PUT   SERVRPT,H1
         PUT   SERVRPT,H2
         PUT   SERVRPT,H0
         PUT   SERVRPT,H3
         PUT   SERVRPT,H4
         PUT   SERVRPT,H5
         PUT   SERVRPT,H0
         PUT   SERVRPT,H6               PRINT RESULTS
SERV0110 L     R0,GETMDATA
         FREEMAIN R,LV=(0),A=(4)        FREE STORAGE
         MODESET KEY=ZERO
         MVC   TCBUSER-TCB(4,R3),=AL4(0) ZERO STORAGE POINTER
         MODESET KEY=NZERO
SERV5000 CLOSE (SERVMSG,,SERVRPT)       CLOSE DATA SETS
SERV5020 L     R13,4(R13)
         RETURN (14,12),RC=0            RETURN
* DATA AREA *
SAVEAREA DC    18F'0'
SERVID   DC    CL16'SERVMON STORAGE'    STORAGE IDENTIFIER
GETMDATA DC    0F'0',AL1(251),AL3(SERVLEN) GETMAIN DATA
MSG01    DC    CL80'SERVSFX01 - JOBSTEP TCB NOT FOUND'
MSG02    DC    CL80'SERVSFX02 - TCBUSER FIELD HAS BEEN USED'
MSG03    DC    CL80'SERVSFX03 - SYSEVENT RETURN CODE WAS XX'
MSG04    DC    CL80'SERVSFX04 - TCBUSER FIELD = 0 - RUN SERVPFX FIRST'
MSG05    DC    CL80'SERVSFX05 - SERVRPT DATA SET CANNOT BE OPENED'
SERVFLAG DC    X'00'                    FLAG BYTE
SERVAUTH EQU   X'80'                    WE WERE ENTERED AUTHORIZED
WORK1    DC    4D'0'                    WORK FIELD
SERVMSG  DCB   DDNAME=SERVMSG,DSORG=PS,MACRF=PM,DEVD=DA,RECFM=FB,      X
               LRECL=80,BLKSIZE=4080
SERVRPT  DCB   DDNAME=SERVRPT,DSORG=PS,MACRF=PM,DEVD=DA,RECFM=FB,      X
               LRECL=80,BLKSIZE=4080
H0 DC CL80'                                                           '
H1 DC CL80'                          S  E  R  V  M  O  N              '
H2 DC CL80'                          ===================              '
H3 DC CL80'            CPU       IOC       MSO       SRB       TOTAL  '
H4 DC CL80'            SERVICE   SERVICE   SERVICE   SERVICE   SERVICE'
H5 DC CL80'            =======   =======   =======   =======   ======='
H6 DC CL80' '
         ORG   *-80
         DC    CL12' '
DISPCPU  DS    CL7                      DISPLAY CPU SERVICE
         DC    CL3' '
DISPIOC  DS    CL7                      DISPLAY IOC SERVICE
         DC    CL3' '
DISPMSO  DS    CL7                      DISPLAY MSO SERVICE
         DC    CL3' '
DISPSRB  DS    CL7                      DISPLAY SRB SERVICE
         DC    CL3' '
DISPTOT  DS    CL7                      DISPLAY TOTAL SERVICE
         ORG
* DSECTS *
SERVDSCT DSECT
SERVHDR  DC    CL16' '                  HEADER CONTAINING IDENTIFIER
SYSEVNT1 DS    0F                       SYSEVENT NO. 1
TOTSRV1  DS    F                        TOTAL SERVICE
TRNSAT1  DS    F                        TRANSACTION ACTIVE TIME
PERFGP1  DS    H                        PERFORMANCE GROUP
NOTRNS1  DS    H                        NO. OF TRANSACTIONS
RESTIM1  DS    F                        RESIDENCY TIME
CPUSRV1  DS    F                        CPU SERVICE
IOCSRV1  DS    F                        IOC SERVICE
MSOSRV1  DS    F                        MSO SERVICE
SRBSRV1  DS    F                        SRB SERVICE
SYSEVNT2 DS    0F                       SYSEVENT NO. 2
TOTSRV2  DS    F                        FIELDS SAME AS FOR 1
TRNSAT2  DS    F
PERFGP2  DS    H
NOTRNS2  DS    H
RESTIM2  DS    F
CPUSRV2  DS    F
IOCSRV2  DS    F
MSOSRV2  DS    F
SRBSRV2  DS    F
SERVLEN  EQU   *-SERVHDR
         IHAPSA
         IKJTCB  DSECT=YES
         IHADCB  DEVD=DA,DSORG=PS
         END


     © Xephon 1988


