More about listing enqueues


BACKGROUND

As a supplement to the GRSQ program (Who has my dataset?,
MVS Update, issue 55, April 1991) of Steven St Jean , I would
like to share another program which I wrote in Assembler and
which can be used in conjunction with the GRSQ program.
Ours is an installation that caters for users of different
development projects.  There are instances when users,
unaware of the effects, code DISP=OLD in their batch JCL,
which causes a 'job waiting for datasets' condition when
someone else is using the same dataset.  There are several other
ways in which the same condition may be triggered.  On
several occasions we received complaints from users about jobs
taking too long to finish, which, when investigated, turned out
to be waiting for locked resources.  These messages can be
captured in the MVS console and operators are used to notify
the affected users via the MVS SEND command.  We find this
routine too tedious for the operator to perform too often and,
besides, there are instances when the operators may overlook
the messages.


SOLUTION

Our solution was to automate the process and this was
addressed by the DSWAIT program.  It is actually a user exit
that takes advantage of the Message Processing Facility (MPF)
of MVS.  It is triggered by MVS messages IEF861I and
IEF863I.  A table is built/refreshed for every occurrence of the
IEF861I message, extracting the user-id and saving it in the
user-id table residing in the extended common storage area
pointed to out of the CVT with CVTUSER as the anchor point.
A message is sent to the user (saved in the table) for every
occurrence of IEF863I messages.  The output would like this:

      TSG001I - Job XXXXXXX is waiting for the following datasets:
      TSG002I -     DSN = sample.dsname1
      TSG002I -     DSN = sample.dsname2
      TSG002I -     DSN = sample.dsnamen

The user can use the information from the above messages and
pass the resource name to the GRSQ program.


PROGRAM LIMITATION

The program was designed to handle an installation user-id of 7
bytes, which would prefix all batch jobs.  This code can be
revised to adapt to different installation standards.


INSTALLATION

This program is tested on an MVS/ESA 3.1.1 environment and
would run in any MVS environment that supports MPF.  The
member MPFLSTxx in library SYS1.PARMLIB should be
modified to add the following records:

      IEF861I,USEREXIT(DSWAIT)
      IEF863I,SUP(NO),USEREXIT(DSWAIT)


SOURCE CODE FOR DSWAIT

DSWAIT    CSECT
DSWAIT    AMODE 31                 31-BIT ADDRESSING MODE
DSWAIT    RMODE ANY                31-BIT RESIDENCE
CTXTPTR   EQU   5
SPINPRVT  EQU   230
SPINECSA  EQU   241
*        STANDARD ENTRY LINKAGE                                 *
         STM   R14,R12,12(R13)     SAVE CALLER'S REGISTERS
         USING DSWAIT,R12          SET UP CSECT ADDRESSABILITY
         BALR  R12,R0
         USING *,R12
         L     CTXTPTR,0(R1)       ESTABLISH ADDRESSABILITY
         USING CTXT,CTXTPTR        TO THE CTXT
*  MGCR (SVC 34) PARAMETER LIST SHOULD BE BELOW THE 16MB LINE    *
         GETMAIN RU,LV=DATAEND,SP=SPINPRVT,LOC=BELOW
         LR    R11,R1              ESTABLISH ADDRESSABILITY
         USING DATAAREA,R11        TO THE DYNAMIC STORAGE
         ST    R13,SAVEAREA+4      SET BACKWARD PTR
         LA    R15,SAVEAREA        GET ADDRESS OF SAVEAREA
         ST    R15,8(R13)          SET FWD PTR
         LR    R13,R15
PROCESS  EQU   *
         L     R2,CTXTTXPJ         ESTABLISH ADDRESSABILITY
         USING CTXTATTR,R2         TO THE MSG ATTRIBUTES
         LA    R4,CTXTTMSG
         USING MSGTXT,R4
         XC    MGCRPL(MGCRLTH),MGCRPL   CLEAR THE PARAM LIST
CHECK    EQU   *
         CLC   MSGID,IEF861I            FIRST MESSAGE?
         BNE   PNEXT                    NO,PROCESS NEXT
PFIRST   EQU   *
         BAL   R10,REFRTAB               REFRESH TABLE ENTRY
USER7    EQU   *
         MVC   LINE1+18(8),MTEXT+49      EXTRACT JOBNAME
         MVC   LINE1+75(7),MTEXT+49      AND USERID
         MVC   MGCRTEXT(L'LINE1),LINE1
         LA    R1,(MGCRTEXT-MGCRPL)+L'LINE1     GET MGCRPL LENGTH
         B     MGCREXEC
PNEXT    EQU   *
         MVC   LINE2+18(44),MTEXT         EXTRACT DSNAME AND
         BAL   R10,LOGEXT                 USERID
MGCRT    MVC   MGCRTEXT(L'LINE1),LINE2
         LA    R1,(MGCRTEXT-MGCRPL)+L'LINE2     GET MGCRPL LENGTH
MGCREXEC EQU   *
         STC   R1,MGCRLGTH         SAVE LENGTH IN THE MGCRPL
         SR    R0,R0               CLEAR R0
         MGCR  MGCRPL              ISSUE THE COMMAND
EXIT     EQU   *
         L     R13,4(R13)          RESTORE R13
         FREEMAIN RU,LV=DATAEND,A=(R11),SP=SPINPRVT    FREE STORAGE
         LM    R14,R12,12(R13)     RESTORE CALLER'S REGS
         BR    R14                 RETURN TO CALLER
REFRTAB  EQU   *
         L     R8,16               POINT TO CVT
         USING CVTMAP,R8
         L     R1,CVTUSER          GET ADDRESS OF TABLE FROM CVT
         LTR   R1,R1               IS IT THERE?
         BNZ   LOGENT              YES,THEN EXTRACT USERID
MAKETAB  EQU   *                   ONE-TIME ROUTINE FOLLOWS
         L     R0,ENTLENA
         GETMAIN RU,LV=(0),SP=SPINECSA,LOC=ANY
         ST    R1,CVTUSER          PUT ADDRESS OF TABLE INTO CVT
         DROP  R8
         LR    R2,R1
         L     R3,ENTLENA          CLEAR ROUTINE
         LR    R8,R1               GET ADDRESS OF VS
         SR    R9,R9               CLEAR PADDING BYTE
         MVCL  R2,R8               CLEAR IT
LOGENT   EQU   *
         USING ENTAREA,R1          ENTAREA
         MVC   ENTUSER(7),MTEXT+49 SAVE USERID
         DROP  R1                  DROP ADDRESSABILITY
         BR    R10
LOGEXT   EQU   *
         L     R8,16               POINT TO CVT
         USING CVTMAP,R8
         L     R1,CVTUSER          GET ADDRESS OF TABLE FROM CVT
         USING ENTAREA,R1          EST ADDRESSABILITY TO ENTAREA
         MVC   LINE2+75(7),ENTUSER
         DROP  R1,R8               DROP ADDRESSABILITY
         BR    R10
*              STORAGE DECLARATIONS                          *
ENTLENA  DC    A(ENTLEN)
IEF861I  DC    CL8'IEF861I'
LINE1    DS    0CL83
TEXT1    DC    CL83'SE ''TSG001I - Job XXXXXXXX is waiting for the foll*
               owing dataset(s):'',USER=(       )'
LINE2    DS    0CL83
TEXT2    DC    CL83'SE ''TSG002I -                                     *
                                '',USER=(       )'
DATAAREA DSECT
         DS    0F
SAVEAREA DS    18F
         DS    0F
MGCR     IEZMGCR  DSECT=NO
         ORG   MGCRTEXT
CMD      DS    CL75
USER     DS    CL7
F        DS    CL1
         ORG
DATAEND  EQU   *-DATAAREA
MSGTXT   DSECT
MSGID    DS    CL8
MTEXT    DS    CL120
ENTAREA  DSECT
ENTUSER  DS    CL7
ENTLEN   EQU   *-ENTAREA
         CVT   DSECT=YES
         IEZVX100
         END   DSWAIT


Audie T Pili
Technical Support Manager
SGV Consulting (Philippines)                         c Xephon 1991

