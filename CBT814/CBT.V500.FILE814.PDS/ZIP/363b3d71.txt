Restoring archived datasets

It is common for datasets not to remain on DASD if they are not
accessed for a stipulated number of days. After a certain number
of days the dataset is either compressed or archived to tape and
there are many products on the market which accomplish this.

Once I went on vacation for about six weeks and when I came
back to work I found that all my datasets had been archived. To
restore all the datasets manually I would have had to sit down and
access each dataset from option 3.4 list or something similar. It
would have taken hours to perform the tape mounts and restore all
the datasets.

I wanted to avoid the manual, time-consuming task of restoring
datasets individually. There is no point in struggling with a task if
the big beast can do it without any fuss. That's why I wrote a
CLIST (RESTDS) to do the task for me. It has a very simple logic
and does nothing fancy, but it is very handy if you want to restore
a number of datasets in batch, especially when you return from a
long vacation.

RESTDS CLIST

/* RESTDS - Batch restore of datasets from archive                */
/* Usage notes:                                                   */
/* Copy the CLIST RESTDS into a dataset in SYSPROC concatenation. */
/* Copy skeleton members RESTS1,RESTS2 into a dataset in ISPSLIB  */
/* concatenation.                                                 */
/* Make sure to edit the site-specific items to suit your needs.  */
/* Invoke this CLIST with the dataset qualifier as the            */
/* parameter. You can use more than 1 level of dataset qualifier. */
/* Wild cards are also supported.                                 */
/* For example: userid.DB2.TEMP*  or userid  or  userid.ISP*      */
/* are all valid parameters for the CLIST.                        */
PROC 1 LVL
  CONTROL ASIS
  ISPEXEC CONTROL ERRORS RETURN
  ISPEXEC LMDINIT LISTID(MYLD) LEVEL(&LVL)
  SET &RC = &LASTCC
  IF &RC > 0 THEN +
  DO
    SET ZEDLMSG = &STR("&LVL" was entered. &ZERRLM)
    ISPEXEC SETMSG MSG(ISRZ000)
    EXIT
  END
  /****************************************************************/
  /* Account code for submission. Change it to suit your site     */
  /* requirements. This variable is used in the skeleton member   */
  /* RESTS1.                                                      */
  /****************************************************************/
  SET &ACCI = &STR(Z000DS)
  SET &DSNV = &STR( )
  SET &CNT  = 10
  ISPEXEC LMDLIST LISTID(&MYLD) OPTION(LIST) DATASET(DSNV) STATS(YES)
  SET &RC = &LASTCC
  ISPEXEC FTOPEN TEMP
  ISPEXEC FTINCL RESTS1
  /****************************************************************/
  /* Variable ZDLVOL contains the volume name. The package which  */
  /* we use makes it MIGRAT when it is archiving the dataset.     */
  /* Change it to suit your requirements.                         */
  /****************************************************************/
  DO WHILE &RC = 0
    IF &ZDLVOL = &STR(MIGRAT) +
    OR &ZDLVOL = &STR( ) THEN +
    DO
       SET &CNT  = &CNT + 1
       ISPEXEC FTINCL RESTS2
    END
    ISPEXEC LMDLIST LISTID(&MYLD) +
            OPTION(LIST) DATASET(DSNV) +
            STATS(YES)
    SET &RC = &LASTCC
  END
  ISPEXEC FTCLOSE
  ISPEXEC LMDFREE LISTID(&MYLD)
  SET &RC = &LASTCC
  ISPEXEC VGET (ZSCREEN) ASIS
  SET &TMPDD = &STR(ISPCTL)&STR(&ZSCREEN)
  ISPEXEC CONTROL ERRORS RETURN
  ISPEXEC LMINIT DATAID(DDVAR) DDNAME(&TMPDD)
  SET &ACNT = &CNT - 10
  IF &ACNT <= 0 THEN +
    SET ZEDLMSG = &STR(No datasets found for restore +
                         with "&LVL" as dataset qualifier)
  ELSE +
    SET ZEDLMSG = &STR(&ACNT datasets found for restore +
                         with "&LVL" as dataset qualifier)
  ISPEXEC SETMSG MSG(ISRZ000)
  ISPEXEC EDIT DATAID(&DDVAR)
  ISPEXEC LMFREE DATAID(&DDVAR)
EXIT

RESTS1 SKELETON MEMBER

)CM ******************************************************************/
)CM INCLUDE IN A PDS WHICH IS IN ISPSLIB CONCATENATION. RESTS1       */
)CM ******************************************************************/
//&ZUSER.L1 JOB &ACCI.,&ZUSER.,
//          CLASS=A,MSGCLASS=V,NOTIFY=&ZUSER.,TYPRUN=HOLD
//STEP1 EXEC PGM=IEFBR14

RESTS2 SKELETON MEMBER

)CM ******************************************************************/
)CM INCLUDE IN A PDS WHICH IS IN ISPSLIB CONCATENATION. RESTS2       */
)CM ******************************************************************/
//DD&CNT  DD   DISP=SHR,DSN=&DSNV


                                                                                                                 c Xephon 1994

