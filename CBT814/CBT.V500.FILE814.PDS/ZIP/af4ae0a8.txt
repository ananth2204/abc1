An alternative to APF authorization

An earlier article (A fully secure APF authorization SVC, April
1990) showed how to code an APF authorization SVC to
minimize the risk of it being abused by checking that the caller
had the CDSYSLIB attribute bit set.  This article continues this
theme by looking at ways to further secure this SVC in certain
circumstances.

Checking the CDREN bit of the caller increases security by
ensuring that the calling module is re-entrant.  Normally when
this bit and the CDSYSLIB bit are both set the module will be
in read-only storage.  This has the benefit that the calling
module cannot then be modified by another program in an
attempt to misuse its ability to obtain APF authorization with
the SVC.  Obviously this additional check can only be
performed in the SVC if all calling modules will be re-entrant.

For those who require absolute safety there is a further
refinement.  The use of APF authorization can be replaced by
the use of Supervisor State.  Most MVS functions that require
'proof' of your authority to issue them will accept Supervisor
State, Key Zero, or APF authorization.  The problem with APF
authorization is that it applies to the entire job step and it would
be possible for a parallel sub-task to detect the change of state
and abuse it.  Using Supervisor State applies to the current task
(or TCB) only and is more secure to use.

This SVC will check that the requestor came from an
authorized library (although not running authorized), is re-
entrant and will then set the current TCB into supervisor state
or problem state by modifying the resume PSW in the caller's
request block.

IGC0023E CSECT
* SVC 235, SUP ON IF ISSUED WITH R1=1, OR SUP OFF IF R1=0
         USING  IGC0023E,R6    ADDRESS MODULE ENTRY POINT
         LA     R15,8          BAD RC 8 DEFAULT
         USING  RBBASIC,R5     ADDRESS THIS SVRB
         L      R5,RBLINK      ADDRESS PREVIOUS RB
         TM     RBSTAB,RBFTP   IS THIS A PRB?
         BNZ    EXIT           NO, NOT VALID USE OF SVC
         L      R9,RBCDE       CDE FOR CALLING MODULE
         USING  CDENTRY,R9     ADDRESS CDE
         TM     CDATTR2,CDSYSLIB  FROM AN APF AUTHORIZED LIBRARY?
         BNO    EXIT           NO, DO NOT PROCESS
         TM     CDATTR,CDREN      REENTRANT MODULE?
         BNO    EXIT           NO, DO NOT PROCESS
* CALLER IS A MODULE FROM AN APF AUTHORIZED LIBRARY & REENTRANT
         LTR    R1,R1          TEST OPTION REG 1
         BZ     SUPOFF         0 IS SUP OFF, 1 IS SUP ON
* SET SUP ON
         NI     RBOPSW+1,B'11111110' SET PROBLEM STATE OFF
         B      OUT
SUPOFF   EQU    *
         OI     RBOPSW+1,B'00000001' SET PROBLEM STATE ON
OUT      EQU    *
         LA     R15,0          RETURN CODE 0
EXIT     EQU    *
         BR     R14            EXIT
         YREGS
         LTORG
         IKJRB
         IHACDE
         END


                                                      c Reserved 1991

