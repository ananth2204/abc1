Listing the maximum number of TSO users

When we restricted the use of operator commands through TSO
at our installation, we found that we could no longer issue the
D TS,L command.  This was used regularly to check on the
number of users signed on to the system, and also to check on
the maximum number of users allowed on to TSO.  The
BTSOMAX command was written to allow this information to
be obtained using a TSO command processor.

The command locates the TCAST control block in storage by
loading the address stored at offset X'3F4' in the MVS CVT.
The TCAST is mapped by macro IKTTCAST, which resides in
SYS1.MACLIB.  The program must be link-edited using
AC=1 and should reside in an APF-authorized library.  You
should also update the IKJTSO00 member of
SYS1.PARMLIB.  This will authorize the command to TSO
and stop S047 abends occurring.

To issue the command, type BTSOMAX at the TSO READY
prompt or on the ISPF command line.  The resulting output
states the maximum number of users that can sign on to TSO
and the number of users currently signed on.  The program has
been run on an MVS/XA 2.2.3 system using TSO 2.1.


BTSOMAX SOURCE CODE
*        **************************************************************
*        * PURPOSE:      TO LIST THE MAXIMUM NUMBER OF USERS SIGNED   *
*        *               ON TO TSO USING A TSO COMMAND PROCESSOR.     *
*        *               CVT IS LOCATED AT X'10'.                     *
*        *               TCAST IS LOCATED AT X'3F4'.                  *
*        **************************************************************
BTSOMAX  CSECT
BTSOMAX  AMODE     31                  /* AMODE ABOVE THE LINE.      */
BTSOMAX  RMODE     24                  /* RMODE BELOW THE LINE.      */
         SAVE      (14,12)             /* SAVE REGISTERS.            */
         LR        R12,R15             /* LOAD ENTRY POINT.          */
         USING     BTSOMAX,R12         /* ESTABLISH A BASE.          */
         ST        R13,SAVEAREA+4      /* STORE SAVEAREA ADDRESS.    */
         LA        R15,SAVEAREA        /* CHAIN SAVEAREA.            */
         ST        R15,8(R13)          /* CHAIN SAVEAREA.            */
         LR        R13,R15             /* LOAD NEW SAVEAREA.         */
START    EQU       *
         L         R3,16               /* GET ADDRESS OF CVT.        */
         USING     CVT,R3              /* ADDRESS CVT.               */
         L         R3,CVTTCASP         /* LOAD CVT FIELD FOR TCAS.   */
         USING     TCAST,R3            /* ESTABLISH ADDR TO TCAST.   */
         MODESET   KEY=ZERO            /* INTO SUPERVISOR.           */
         LH        R4,TCASUMAX         /* LOAD USERMAX INTO REG.     */
         LH        R5,TCASUSEC         /* LOAD USER COUNT INTO REG.  */
         MODESET   KEY=NZERO           /* INTO PROBLEM.              */
         CVD       R4,TEMP             /* CONVERT VALUE TO DECIMAL.  */
         UNPK      USERACT,TEMP        /* UNPACK THE FIELD.          */
         MVZ       USERACT+3(1),USERACT+2 /* GET RID OF ZONES.       */
         CVD       R5,TEMP             /* CONVERT VALUE TO DECIMAL.  */
         UNPK      USERCNT,TEMP        /* UNPACK THE FIELD.          */
         MVZ       USERCNT+3(1),USERCNT+2 /* GET RID OF ZONES.       */
         TPUT      OUTFLD,L'OUTFLD     /* PUT OUT INFORMATION.       */
         TPUT      BLANKS,L'BLANKS     /* PUT OUT INFORMATION.       */
         TPUT      OUTFLD2,L'OUTFLD2   /* PUT OUT INFORMATION.       */
EXIT     EQU       *
         SR        R15,R15             /* ZEROIZE RETURN CODE.       */
         L         R13,4(R13)          /* RELOAD SAVEAREA ADDRESS.   */
         RETURN    (14,12),RC=(15)     /* RETURN TO TSO.             */
SAVEAREA DS        18F                 /* SAVEAREA STORAGE.          */
TEMP     DS        D                   /* TEMPORARY STORAGE FIELD.   */
BLANKS   DC        CL80' '             /* OUTPUT LINE.               */
OUTFLD   DS        0CL80               /* OUTPUT LINE.               */
TITLE    DC        CL31'MAXIMUM NUMBER OF TSO USERS = '
USERACT  DS        CL4
FILLER1  DS        CL45
OUTFLD2  DS        0CL80               /* OUTPUT LINE.               */
TITLE2   DC        CL31'TSO USERS ACTIVE AT PRESENT = '
USERCNT  DS        CL4
FILLER2  DS        CL45
         CVT       DSECT=YES           /* CVT DSECT.                 */
         IKTTCAST                      /* DSECT TO TCAST.            */
         END       BTSOMAX

MANUALS APPLICABLE TO BTSOMAX
o      MVS/XA System Initialization and Tuning - GC28-1149
o      MVS/XA Debugging Handbook Volume 2 - LC28-1165
o      MVS/XA Debugging Handbook Volume 6 - LC28-1169.

John Bradley
Technical Support Engineer (UK)                    c Xephon 1991

