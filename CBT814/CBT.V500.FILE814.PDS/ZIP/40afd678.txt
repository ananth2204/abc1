Producing SMF data summary reports

SAMPLE JCL

//SMFSUMM  EXEC PGM=SMFSUMM
//STEPLIB  DD DISP=SHR,DSN=TSOUSER.LOAD
//SMFDATA  DD DSN=TSOUSER.SMFDATA,DISP=SHR
//SYSPRINT DD SYSOUT=*
//SYSUDUMP DD SYSOUT=*
//ABENDAID DD SYSOUT=*

PROGRAM SMFSUMM SOURCE CODE

SMFSUMM  CSECT                    ESTABLISH CSECT
         SAVE  (14,12),,SMFSUMM-&SYSDATE
         YREGS                    REGISTER EQUATES
         LR    R11,R15            LOAD A(ENTRY POINT)
         USING SMFSUMM,R11,R12
         GETMAIN RU,LV=WORKLEN    GET WORK AREA
         ST    R13,4(,R1)
         ST    R1,8(,R13)
         LR    R13,R1
         USING WORKAREA,R13
         LA    R12,2048(,R11)     SET UP SECOND BASE REGISTER
         LA    R12,2048(,R12)     SET UP SECOND BASE REGISTER
         USING SMFRCD0,R3
         OPEN  (SMFDATA,,SYSPRINT,OUTPUT) OPEN FILES
GETRECRD GET   SMFDATA            READ THE TAPE
         LR    R3,R1              SAVE THE BUFFER ADDRESS
         SR    R15,R15            CLEAR REG
         ICM   R15,1,SMF0RTY      INSERT RECORD TYPE
         CH    R15,=H'90'         IS SMF TYPE GT 90
         BH    GETRECRD           IF SO, GO GET NEXT RECORD
         MVI   LINE,C' '          CLEAR OUTPUT LINE
         MVC   LINE+1(L'LINE-1),LINE CLEAR OUTPUT LINE
         MVC   LINE2,LINE         CLEAR OUTPUT LINE
         MVC   DTIME,SMF0TME      SAVE TIME
         MVC   DDATE,SMF0DTE      SAVE DATE
         BAL   R14,TIMEDATE       MAKE THEM PRINTABLE
         MVC   LDATE,TDATE+1      MOVE TO OUTPUT LINE
         MVC   LTIME,TTIME+2      MOVE TO OUTPUT LINE
         SLL   R15,2              MULTIPLY RECORD TYPE BY FOUR
         B     BRANCHES(R15)      BRANCH INTO PROCESS-RECORD-TABLE
EODAD    CLOSE (SMFDATA,DISP,SYSPRINT,DISP) CLOSE THE FILES
         LR    R1,R13             LOAD A(SAVEAREA) FOR FREEMAIN
         L     R13,4(,R13) LOAD A(CALLERS SAVEAREA)
         FREEMAIN RU,LV=WORKLEN,A=(1) FREE WORK AREA
         RETURN (14,12),RC=0      RETURN TO OS WITH RETCODE=0
BRANCHES B     IPLREC             00 IPL
         B     GETRECRD           01 --------------
         B     DUMPHDR            02 DUMP HEADER
         B     DUMPTRLR           03 DUMP TRAILER
         B     STEPTERM           04 STEP TERMINATION
         B     JOBTERM            05 JOB TERMINATION
         B     SYSOUT             06 OUTPUT WRITER
         B     SMFLOST            07 SMF DATA LOST
         B     IOCONFIG           08 I/O CONFIGURATION
         B     VONLINE            09 VARY ONLINE
         B     ALOCRCVR           10 ALLOCATION RECOVERY
         B     VOFFLINE           11 VARY OFFLINE
         B     GETRECRD           12 --------------
         B     GETRECRD           13 --------------
         B     INPUTDS            14 INPUT DATA SET ACTIVITY
         B     OUTPUTDS           15 OUTPUT DATA SET ACTIVITY
         B     GETRECRD           16 SORT/MERGE STATISTICS
         B     SCRTCHDS           17 SCRATCH DATA SET STATUS
         B     RENAMEDS           18 RENAME DATA SET STATUS
         B     DAVOLUME           19 DIRECT ACCESS VOLUME
         B     JOBINIT            20 JOB INTIATION
         B     TAPESTAT           21 ERROR STATISTICS BY VOLUME
         B     CONFIG             22 CONFIGURATION
         B     SMFSTATS           23 SMF STATUS
         B     SPOOLOFF           24 JES2 SPOOL OFFLOAD
         B     GETRECRD           25 JES3 DEVICE ALLOCATION
         B     JOBPURGE           26 JES JOB PURGE
         B     GETRECRD           27 --------------
         B     GETRECRD           28 --------------
         B     GETRECRD           29 --------------
         B     GETRECRD           30 COMMON ADDRESS SPACE WORK
         B     TIOCINIT           31 TIOC INITIALIZATION
         B     GETRECRD           32 TSO USER WORK ACCOUNTING
         B     GETRECRD           33 --------------
         B     TSTEPTRM           34 TS STEP TERMINATION
         B     LOGOFF             35 TS JOB TERMINATION (LOGOFF)
         B     ICFCATLG           36 ICF CATALOG FACILITY
         B     GETRECRD           37 NPDA
         B     GETRECRD           38 NPM
         B     GETRECRD           39 NLDM
         B     GETRECRD           40 DYNAMIC DD
         B     GETRECRD           41 DIV ACCESS/UNACCESS
         B     DFPSTATS           42 DFP STATISTICS/CONFIG
         B     JESSTART           43 JES START
         B     GETRECRD           44 --------------
         B     JESSTOP            45 JES WITHDRAWAL
         B     GETRECRD           46 --------------
         B     JESSIGON           47 JES2 BSC SIGNON
         B     JESSIGOF           48 JES2 BSC SIGNOFF
         B     JESINTGR           49 JES2 BSC INTEGRITY
         B     GETRECRD           50 ACF/VTAM TUNING STATISTICS
         B     GETRECRD           51 --------------
         B     JES2LGON           52 JES2 SNA LOGON
         B     JES2LGOF           53 JES2 SNA LOGOFF
         B     JES2INTG           54 JES2 SNA INTEGRITY
         B     GETRECRD           55 JES2 NETWORK SIGNON
         B     GETRECRD           56 JES2 NETWORK INTEGRITY
         B     GETRECRD           57 JES2 NETWORK SYSOUT TRANSMISSION
         B     GETRECRD           58 JES2 NETWORK SIGNOFF
         B     GETRECRD           59 MVS/BDT
         B     VVDSUPDT           60 VVDS UPDATE
         B     ICFDEFN            61 ICF DEFINE
         B     VSAMOPEN           62 VSAM CLUSTER OR COMP OPENED
         B     VSAMDEFN           63 VSAM CLUSTER OR COMP CATLGD
         B     GETRECRD           64 VSAM COMPONENT STATUS
         B     ICFDEL             65 ICF DELETE
         B     ICFALTER           66 ICF ALTER
         B     VSAMDEL            67 VSAM ENTRY DELETED
         B     VSAMRNAM           68 VSAM ENTRY RENAMED
         B     GETRECRD           69 VSAM DATA SPC DEFD OR DELD
         B     GETRECRD           70 CPU ACTIVITY
         B     GETRECRD           71 PAGING ACTIVITY
         B     GETRECRD           72 WORKLOAD ACTIVITY
         B     GETRECRD           73 CHANNEL ACTIVITY
         B     GETRECRD           74 DEVICE ACTIVITY
         B     GETRECRD           75 PAGE/SWAP DATASET ACTIVITY
         B     GETRECRD           76 TRACE ACTIVITY
         B     GETRECRD           77 ENQUEUE ACTIVITY
         B     GETRECRD           78 MONITOR I ACTIVITY
         B     GETRECRD           79 MONITOR II ACTIVITY
         B     GETRECRD           80 RACF PROCESSING
         B     GETRECRD           81 RACF INITIALIZATION
         B     GETRECRD           82 SECURITY
         B     GETRECRD           83 --------------
         B     GETRECRD           84 --------------
         B     GETRECRD           85 --------------
         B     GETRECRD           86 --------------
         B     GETRECRD           87 --------------
         B     GETRECRD           88 --------------
         B     GETRECRD           89 --------------
         B     SYSTATUS           90 SYSTEM STATUS RECORD
PUTGET   PUT   SYSPRINT,LINE
         B     GETRECRD
IPLREC   MVC   RECTYPE,=C'IPL             (00)' TYPE 0 (IPL)
         USING SMFRCD0,R3
         L     R15,SMF0RST
         CVD   R15,WORK
         MVC   MESG+17(7),=X'20202021202020'
         ED    MESG+17(8),WORK+4
         MVC   MESG(18),=C'REAL STORAGE SIZE='
         MVI   MESG+24,C'K'
         L     R15,SMF0JWT
         CVD   R15,WORK
         MVC   MESG+29(5),=X'2120202020'
         ED    MESG+29(6),WORK+5
         MVC   MESG+26(4),=C'JWT='
         B     PUTGET
DUMPHDR  MVC   RECTYPE,=C'DUMP HEADER     (02)' TYPE 2(DUMP HEADER)
         B     PUTGET
DUMPTRLR MVC   RECTYPE,=C'DUMP TRAILER    (03)' TYPE 3(DUMP TRAILER)
         B     PUTGET
STEPTERM MVC   RECTYPE,=C'STEP TERMINATION(04)' TYPE 4(STEP TERM)
         USING SMFRCD4,R3
         MVC   MESG(4),=C'JOB='
         MVC   MESG+13(5),=C'STEP='
         MVC   MESG+35(4),=C'PGM='
         MVC   MESG+4(8),SMF4JBN
         MVC   MESG+39(8),SMF4PGMN
         LR    R10,R3             GET RECORD PTR
         AH    R10,SMF4RLCT       GET PTR TO RELOCATE SECTION
         USING SMF4PGIN,R10
         LH    R15,SMF4PGNO       GET PERFORMANCE GROUP #
         CVD   R15,WORK           CONVERT TO DECIMAL
         MVC   MESG+28(6),=X'402120202020'
         ED    MESG+28(6),WORK+5
         MVC   MESG+27(4),=C'PGN='
         CLC   SMF4STMN,BLANKS    STEP NAME????
         BE    STEP#              NO, CHECK FOR STEP NUMBER
         MVC   MESG+18(8),SMF4STMN MOVE STEP NAME TO OUTPUT
         B     TESTA              YES, CHECK IF ABEND
STEP#    SR    R15,R15            CLEAR REGISTER
         IC    R15,SMF4STN        GET STEP NUMBER
         CVD   R15,WORK           CONVERT TO DECIMAL
         MVC   MESG+17(2),=C'#='
         MVC   MESG+19(4),=X'40212121'
         ED    MESG+19(4),WORK+6
TESTA    TM    SMF4STI,X'02'      HAS IT ABENDED?
         BO    ABEND              YES, DO ABEND
         MVC   MESG+48(15),=C'ENDED NORMALLY'
         B     PUTGET
ABEND    LH    R15,SMF4SCC        GET STEP COMPLETION CODE
         TM    SMF4SCC,X'80'      IS IT USER ABEND?
         BO    USRABND            YES, DO USER ABEND
         STH   R15,WORK
         UNPK  WORK+3(5),WORK(3)  ADD ZONES TO ABEND
         TR    WORK+4(3),CHARS-240 POINT TO RIGHT OFFSET
         MVC   MESG+54(3),WORK+4  MOVE SYSTEM ABEND
         MVC   MESG+47(7),=C'ABEND=S'
         B     PUTGET
USRABND  NI    SMF4SCC,X'80'      TURN OFF X'80' BIT
         CVD   R15,WORK           CONVERT CC TO DECIMAL
         MVC   MESG+54(4),=X'40202020'
         ED    MESG+54(4),WORK+6
         MVC   MESG+47(7),=C'ABEND=U'
         B     PUTGET
JOBTERM  MVC   RECTYPE,=C'JOB TERMINATION (05)' TYPE 5(JOB TERMINATION)
         USING SMFRCD5,R3
         MVC   MESG(4),=C'JOB='
         MVC   MESG+13(6),=C'CLASS='
         MVC   MESG+4(8),SMF5JBN
         CLI   SMF5JICL,C' '      IS IT A PRINTABLE JOB CLASS
         BL    NOCLASS            NO, SHOW UNKNOWN CLASS
         MVC   MESG+19(1),SMF5JICL MOVE JOB INPUT CLASS
         B     ARNDCLAS
NOCLASS  MVC   MESG+19(1),=C'?'
ARNDCLAS LH    R15,SMF5PGNO       GET PERFORMANCE GROUP #
         CVD   R15,WORK           CONVERT TO DECIMAL
         MVC   MESG+28(6),=X'402120202020'
         ED    MESG+28(6),WORK+5  MOVE PERF GROUP #
         MVC   MESG+27(4),=C'PGN='
         TM    SMF5JBTI,X'02'     HAS IT ABENDED?
         BO    ABEND5             YES, DO ABEND
         MVC   MESG+48(15),=C'ENDED NORMALLY'
         B     PUTGET
ABEND5   LH    R15,SMF5JCC        GET JOB COMPLETION CODE
         TM    SMF5JCC,X'80'      IS IT USER ABEND?
         BO    USER5              YES, DO USER ABEND
         STH   R15,WORK
         UNPK  WORK+3(5),WORK(3)  ADD ZONES TO CODE
         TR    WORK+4(3),CHARS-240 POINT TO RIGHT OFFSET
         MVC   MESG+54(3),WORK+4  MOVE SYSTEM ABEND
         MVC   MESG+47(7),=C'ABEND=S'
         B     PUTGET
USER5    NI    SMF5JCC,X'80'      TURN OFF X'80' BIT
         CVD   R15,WORK           CONVERT CC TO DECIMAL
         MVC   MESG+54(4),=X'40202020'
         ED    MESG+54(4),WORK+6
         MVC   MESG+47(7),=C'ABEND=U'
         B     PUTGET
SYSOUT   MVC   RECTYPE,=C'OUTPUT WRITER   (06)' TYPE 6(OUTPUT WRITER)
         USING SMFRCD6,R3
         MVC   MESG(4),=C'JOB='
         MVC   MESG+13(8),=C'SYSOUT=X'
         MVC   MESG+20(1),SMF6OWC MOVE SYSOUT CLASS
         MVC   MESG+4(8),SMF6JBN  MOVE JOB NAME
         B     PUTGET
SMFLOST  MVC   RECTYPE,=C'DATA LOST       (07)' TYPE 7(DATA LOST)
         USING SMFRCD7,R3
         MVC   MESG(13),=C'RECORDS LOST='
         LH    R15,SMF7NRO        GET SMF REC'S LOST
         CVD   R15,WORK
         MVC   MESG+13(6),=X'402020212020'
         ED    MESG+13(6),WORK+5  MOVE REC'S LOST
         MVC   DTIME,SMF7STM      TIME NO SMF DSN'S AVAIL
         MVC   DDATE,SMF7STD      DATE NO SMF DSN'S AVAIL
         BAL   R14,TIMEDATE
         MVC   MESG+23(6),=C'START='
         MVC   MESG+29(6),TDATE+1
         MVC   MESG+36(8),TTIME+2
         B     PUTGET
IOCONFIG MVC   RECTYPE,=C'I/O CONFIG      (08)' TYPE 8(I/O CONFIG)
         MVC   MESG(13),=C'MULT DEVICES '
         B     VONLINE2
VONLINE  MVC   RECTYPE,=C'VARY ONLINE     (09)' TYPE 9(VARY ONLINE)
         USING SMFRCD9,R3
VONLINE2 MVC   MESG+13(4),=C'DEV='
         MVC   MESG+21(5),=C'TYPE='
         MVC   WORK+2(2),SMF9DVAD+2 GET UCB ADDRESS
         BAL   R14,CNVTUCB
         MVC   MESG+17(3),WORK+1  MOVE UCB ADDRESS
         LA    R10,SMF9DVAD
         BAL   R14,CHKDEVT
         B     PUTGET
ALOCRCVR MVC   RECTYPE,=C'ALLOC RECOVERY  (10)' TYPE 10(ALLOC RECOVERY)
         USING SMFRCD10,R3
         MVC   MESG(4),=C'JOB='
         MVC   MESG+13(4),=C'DEV='
         MVC   MESG+21(5),=C'TYPE='
         MVC   MESG+4(8),SMF10JBN
         MVC   WORK+2(2),SMF10DEV+2
         BAL   R14,CNVTUCB
         MVC   MESG+17(3),WORK+1
         LA    R10,SMF10DEV
         BAL   R14,CHKDEVT
         B     PUTGET
VOFFLINE MVC   RECTYPE,=C'VARY OFFLINE    (11)' TYPE 11(VARY OFFLINE)
         USING SMFRCD11,R3
         MVC   MESG+13(4),=C'DEV='
         MVC   MESG+21(5),=C'TYPE='
         MVC   WORK+2(2),SMF11DEV+2
         BAL   R14,CNVTUCB
         MVC   MESG+17(3),WORK+1
         LA    R10,SMF11DEV
         BAL   R14,CHKDEVT
         B     PUTGET
INPUTDS  MVC   RECTYPE,=C'INPUT DATASET   (14)' TYPE 14(INPUT DATASET)
         USING SMF14HDR,R3
CHKTEMP  TM    SMF14RIN,SMF14TDS
         BO    GETRECRD
         MVC   MESG(4),=C'JOB='
         MVC   MESG+13(3),=C'DD='
         MVC   MESG+4(8),SMF14JBN
         MVC   MESG+48(12),=C'FORMAT4.DSCB'
         CLI   SMFJFCB1,X'04'
         BE    SKIPDSNM
         MVC   MESG+48(44),SMFJFCB1
SKIPDSNM MVC   MESG+25(4),=C'VOL='
         MVC   MESG+36(4),=C'DEV='
         MVC   MESG+44(4),=C'DSN='
         MVC   MESG+16(8),SMFTIOE5
         MVC   MESG+29(6),SMFSRTEV
         MVC   WORK+2(2),SMFUCBCH
         BAL   R14,CNVTUCB
         MVC   MESG+40(3),WORK+1
         B     PUTGET
OUTPUTDS MVC   RECTYPE,=C'OUTPUT DATASET  (15)' TYPE 15(OUTPUT DATASET)
         B     CHKTEMP
SCRTCHDS MVC   RECTYPE,=C'SCRATCH DATASET (17)' TYPE 17(SCRATCH DSN)
         USING SMF17LEN,R3
         MVC   MESG(4),=C'JOB='
         MVC   MESG+13(4),=C'VOL='
         MVC   MESG+24(4),=C'DSN='
         MVC   MESG+4(8),SMF17JBN
         MVC   MESG+28(44),SMF17DSN
         MVC   MESG+17(6),SMF17FVL
         B     PUTGET
RENAMEDS MVC   RECTYPE,=C'RENAME DATASET  (18)' TYPE 18(RENAME DATASET)
         USING SMF18HDR,R3
         MVC   MESG(4),=C'JOB='
         MVC   MESG+13(4),=C'VOL='
         MVC   MESG+24(8),=C'OLD DSN='
         MVC   MESG+4(8),SMF18JBN
         MVC   MESG+32(44),SMF18ODS
         MVC   MESG+17(6),SMF18FVL
         PUT   SYSPRINT,LINE
         MVC   MESG2+28(8),=C'NEW DSN='
         MVC   MESG2+36(44),SMF18NDS
         PUT   SYSPRINT,LINE2
         B     GETRECRD
DAVOLUME MVC   RECTYPE,=C'DASD VOLUME     (19)' TYPE 19(DASD VOLUME)
         USING SMF19HDR,R3
         MVC   MESG(4),=C'VOL='
         MVC   MESG+11(4),=C'DEV='
         MVC   MESG+4(6),SMF19VOL
         MVC   WORK+2(2),SMF19CUU
         BAL   R14,CNVTUCB
         MVC   MESG+15(3),WORK+1
         LH    R15,SMF19DSR
         CVD   R15,WORK
         MVC   MESG+34(5),=X'2120202020'
         ED    MESG+34(5),WORK+5
         LH    R15,SMF19SPC
         CVD   R15,WORK
         MVC   MESG+39(5),=X'2120202020'
         ED    MESG+39(5),WORK+5
         MVC   MESG+19(16),=C'FREE DSCBS/CYLS='
         MVI   MESG+39,C'/'
         B     PUTGET
JOBINIT  MVC   RECTYPE,=C'JOB INITIATION  (20)' TYPE 20(JOB INIT)
         USING SMFRCD20,R3
         MVC   MESG(4),=C'JOB='
         MVC   MESG+4(8),SMF20JBN
         MVC   DTIME,SMF20RST     SAVE TIME
         MVC   DDATE,SMF20RSD     SAVE DATE
         BAL   R14,TIMEDATE       MAKE THEM PRINTABLE
         MVC   MESG+13(10),=C'INIT DATE='
         MVC   MESG+23(6),TDATE+1
         MVC   MESG+31(10),=C'INIT TIME='
         MVC   MESG+41(8),TTIME+2
         B     PUTGET
TAPESTAT MVC   RECTYPE,=C'ERRORS BY VOLSER(21)' TYPE 21(ERRORS/VOLUME)
         USING SMF21HDR,R3
         MVC   MESG(4),=C'VOL='
         MVC   MESG+13(4),=C'DEV='
         MVC   MESG+4(6),SMF21VOL
         MVC   WORK+2(2),SMF21CA
         BAL   R14,CNVTUCB
         MVC   MESG+17(3),WORK+1
         SR    R15,R15
         ICM   R15,1,SMF21TR
         CVD   R15,WORK
         MVC   MESG+30(4),=X'2120202020'
         ED    MESG+30(4),WORK+5
         ICM   R15,1,SMF21TW
         CVD   R15,WORK
         MVC   MESG+39(4),=X'2120202020'
         ED    MESG+39(4),WORK+5
         ICM   R15,1,SMF21PR
         CVD   R15,WORK
         MVC   MESG+48(4),=X'2120202020'
         ED    MESG+48(4),WORK+5
         MVC   MESG+28(3),=C'TR='
         MVC   MESG+37(3),=C'TW='
         MVC   MESG+46(3),=C'PR='
         B     PUTGET
CONFIG   MVC   RECTYPE,=C'CONFIGURATION   (22)' TYPE 22(CONFIGURATION)
         USING SMFRCD22,R3
         LH    R15,SMF22IND
         CH    R15,=H'9'          CHECK FOR TYPE > 9
         BH    GETRECRD           YES, GO GET NEXT RECORD
         BCTR  R15,R0 DECREMENT FOR BRANCH TABLE
         SLL   R15,2              MULTIPLY TYPE BY FOUR
         B     CFGBRNCH(R15)      BRANCH INTO TABLE
CFGBRNCH B     CFGIPL             01 IPL
         B     CFGVON             02 CONFIG ONLINE
         B     CFGVOF             03 CONFIG OFFLINE
         B     GETRECRD           04 MSS AT IPL
         B     GETRECRD           05 CONFIG ONLINE,S
         B     GETRECRD           06 CONFIG OFFLINE,S
         B     CFGVCHP            07 CONFIG CHPID ON/OFFLINE
         B     CFGRCHP            08 CONFIG CHPID ON/OFFLINE
         B     CFGACTV            09 ACTIVATE/CONFIG ESTOR
CFGIPL   MVC   MESG(10),=C'SYSTEM IPL'
         B     CFGTYPE
CFGVON   MVC   MESG(13),=C'CONFIG ONLINE'
         B     CFGTYPE
CFGVOF   MVC   MESG(14),=C'CONFIG OFFLINE'
         B     CFGTYPE
CFGVCHP  MVC   MESG(10),=C'CONFIG CHPID'
         B     PUTGET
CFGRCHP  MVC   MESG(12),=C'RECONFIG CHPID'
         B     PUTGET
CFGACTV  MVC   MESG(8),=C'ACTIVATE'
         B     PUTGET
CFGTYPE  SR    R15,R15
         ICM   R15,1,SMF22PID
         BCTR  R15,R0
         SLL   R15,4
         LA    R1,CFGTBL(R15)
         MVC   MESG+17(16),0(R1)
         B     PUTGET
CFGTBL   DC    CL16'CPU'               1
         DC    CL16'CHANNEL'           2
         DC    CL16'CSTOR'             3
         DC    CL16'MSS IPL'           4
         DC    CL16'S'                 5
         DC    CL16'S'                 6
         DC    CL16'CHANNEL PATH'      7
         DC    CL16'RECONFIGURED CHP'  8
         DC    CL16'ESTOR'             9
         DC    CL16'STORAGE CONTROL'   10
SMFSTATS MVC   RECTYPE,=C'SMF STATUS      (23)' TYPE 23(SMF STATUS)
         USING SMFRCD23,R3
         MVC   MESG(9),=C'INTERVAL='
         LR    R15,R3
         A     R15,SMF23SOF
         USING SMF23INT,R15
         MVC   MESG+9(6),SMF23INT
         B     PUTGET
SPOOLOFF MVC   RECTYPE,=C'SPOOL OFFLOAD   (24)' TYPE 24(SPOOL OFFLOAD)
         USING SMFRCD24,R3
         MVC   MESG(7),=C'TYPE=??'
         CLI   SMF24SUB+1,X'04'
         BH    SPJOBNO
         LH    R15,SMF24SUB
         BCTR  R15,R0
         SLL   R15,1
         LA    R1,SPOOLTYP(R15)
         MVC   MESG+5(2),0(R1)
SPJOBNO  MVC   MESG+8(4),=C'JOB='
         MVC   MESG+12(8),SMF24JBN
         MVI   MESG+20,C'/'
         MVC   MESG+21(8),SMF24JID
         MVI   MESG+29,C'>'
         MVC   MESG+30(8),SMF24CJD
         CLC   SMF24CNT,=F'9999999'
         BH    SPNUMHI
         L     R15,SMF24CNT
         CVD   R15,WORK
         MVC   MESG+42(9),=X'212020202020202020'
         ED    MESG+42(10),WORK+3
SPREC    MVC   MESG+39(5),=C'RECS='
         L     R15,SMF24REC
         CVD   R15,WORK
         MVC   MESG+51(9),=X'212020202020202020'
         ED    MESG+51(10),WORK+3
         MVI   MESG+51,C'/'
         B     PUTGET
SPNUMHI  MVC   MESG+44(7),=C'*******'
         B     SPREC
SPOOLTYP DC    C'JTJRSTSR'
JOBPURGE MVC   RECTYPE,=C'JES2 JOB PURGE  (26)' TYPE 26(JOB PURGE)
         USING SMFRCD26,R3
         MVC   MESG(4),=C'JOB='
         MVC   MESG+13(6),=C'JOBID='
         MVC   MESG+4(8),SMF26JBN
         MVC   MESG+19(8),SMF26JID
         B     PUTGET
TIOCINIT MVC   RECTYPE,=C'TIOC INITIALIZE (31)' TYPE 31(TIOC INIT)
         USING TTISMF,R3
         LH    R15,TTINBF         # OF TIMESHARING BUFFERS
         CVD   R15,WORK           CONVERT TO DECIMAL
         MVC   MESG+27(4),=X'20202020'
         ED    MESG+27(4),WORK+6
         MVC   MESG(27),=C'TSO WAS STARTED, MAX USERS='
         B     PUTGET
TSTEPTRM MVC   RECTYPE,=C'TS STEP TERM    (34)' TYPE 34(TS STEP TERM)
         USING TIVSMF,R3
         MVC   MESG(4),=C'JOB='
         MVC   MESG+13(5),=C'STEP='
         MVC   MESG+4(8),TIVUIF
         LR    R15,R3             GET RECORD PTR
         AH    R15,TIVRLCT        GET PTR TO RELOCATE SECTION
         LA    R15,36(R15)        GET OFFSET TO PERF GROUP #
         LH    R15,0(R15)         GET PERF GROUP #
         CVD   R15,WORK           CONVERT TO DECIMAL
         MVC   MESG+28(6),=X'402120202020'
         ED    MESG+28(6),WORK+5
         MVC   MESG+27(4),=C'PGN='
         CLC   TIVINVNM,BLANKS    IS THERE STEP NAME?
         BE    STEP#1             NO, PRINT STEP#
         MVC   MESG+18(8),TIVINVNM
         B     TEST
STEP#1   SR    R15,R15
         IC    R15,TIVINVSQ
         CVD   R15,WORK
         MVC   MESG+17(2),=C'#='
         MVC   MESG+19(4),=X'40212121'
         ED    MESG+19(4),WORK+6
TEST     TM    TIVSTI,X'02'       HAS IT ABENDED?
         BO    ABEND2             YES, DO ABEND
         MVC   MESG+48(15),=C'ENDED NORMALLY'
         B     PUTGET
ABEND2   LH    R15,TIVSTAT
         BZ    GETRECRD
         TM    TIVSTAT,X'80'      IS IT USER ABEND?
         BO    USER2              YES, DO USER ABEND
         STH   R15,WORK
         UNPK  WORK+3(5),WORK(3)          ADD ZONES TO CODE
         TR    WORK+4(3),CHARS-240      PT TO RIGHT OFFSET
         MVC   MESG+54(3),WORK+4
         MVC   MESG+47(7),=C'ABEND=S'
         B     PUTGET
USER2    NI    TIVSTAT,X'80'      TURN OFF X'80' BIT
         CVD   R15,WORK           CONVERT CC TO DECIMAL
         MVC   MESG+54(4),=X'40202020'
         ED    MESG+54(4),WORK+6
         MVC   MESG+47(7),=C'ABEND=U'
         B     PUTGET
LOGOFF   MVC   RECTYPE,=C'TSO LOGOFF      (35)' TYPE 35(LOGOFF)
         USING TLGRLEN,R3
         MVC   MESG(4),=C'JOB='
         MVC   MESG+4(8),TLGUIF
         B     PUTGET
ICFCATLG MVC   RECTYPE,=C'ICF CATALOG EXP (36)' TYPE 36(ICF CAT EXPORT)
         USING SMFRCD36,R3
         LR    R15,R3
         AH    R15,SMF36DOF
         USING SMF36DAT,R15
         MVC   MESG(4),=C'JOB='
         MVC   MESG+13(4),=C'VOL='
         MVC   MESG+4(8),SMF36JNM
         MVC   MESG+17(6),SMF36CVS
         MVC   MESG+36(4),=C'DEV='
         MVC   WORK+2(4),SMF36CDT
         BAL   R14,CNVTUCB
         MVC   MESG+40(4),WORK+1
         PUT   SYSPRINT,LINE
         MVC   MESG2+17(4),=C'IND='
         MVC   MESG2+17(2),SMF36EIN
         MVC   MESG2+36(4),=C'CAT='
         MVC   MESG2+40(44),SMF36CNM
         PUT   SYSPRINT,LINE2
         B     GETRECRD
DFPSTATS MVC   RECTYPE,=C'DFP STATS/CONFIG(42)' TYPE 42(DFP STATISTICS)
         USING SMFRCD0,R3
         CLI   SMF0BUF+1,X'01'
         BE    DFPTYP1
         CLI   SMF0BUF+1,X'02'
         BE    DFPTYP2
         CLI   SMF0BUF+1,X'03'
         BNE   GETRECRD
DFPTYP1  MVC   MESG(12),=C'DEVICE STATS'
         B     PUTGET
DFPTYP2  MVC   MESG(12),=C'SMS CU STATS'
         B     PUTGET
DFPTYP3  MVC   MESG(17),=C'SMS CONFIG CHANGE'
         B     PUTGET
JESSTART MVC   RECTYPE,=C'JES2 START      (43)' TYPE 43(JES START)
         USING SMFRCD43,R3
         MVC   MESG+5(5),=C'START='
         TM    SMF43OPT,X'80'
         BO    FMT
         TM    SMF43OPT,X'40'
         BO    COLD
HOT      MVC   MESG(4),=C'HOT '
         B     PUTGET
FMT      MVC   MESG(4),=C'FMT '
         B     PUTGET
COLD     MVC   MESG(4),=C'COLD'
         B     PUTGET
JESSTOP  MVC   RECTYPE,=C'JES2 STOP       (45)' TYPE 45(JES STOP)
         B     PUTGET
JESSIGON MVC   RECTYPE,=C'JES2 SIGNON     (47)' TYPE 47(JES SIGNON)
         USING SMFRCD47,R3
         MVC   MESG+17(8),SMF47RMT
         MVC   MESG+40(8),SMF47LIN
         B     JESSCOMN
JESSIGOF MVC   RECTYPE,=C'JES2 SIGNOFF    (48)' TYPE 48(JES SIGNOFF)
         USING SMFRCD48,R3
         MVC   MESG+17(8),SMF48RMT
         MVC   MESG+40(8),SMF48LIN
         B     JESSCOMN
JESINTGR MVC   RECTYPE,=C'JES2 INTEGRITY  (49)' TYPE 49(JES2 INTEGRITY)
         USING SMFRCD49,R3
         MVC   MESG+17(8),SMF49RMT
         MVC   MESG+40(8),SMF49LIN
         B     JESSCOMN
JES2LGON MVC   RECTYPE,=C'JES2 INTEGRITY  (52)' TYPE 52(JES2 LOGON)
         USING SMFRCD52,R3
         MVC   MESG+17(8),SMF52RMT
         MVC   MESG+40(8),SMF52LIN
         B     JESSCOMN
JES2LGOF MVC   RECTYPE,=C'JES2 LOGOFF/SNA (53)' TYPE 53(JES2 LOGOFF)
         USING SMFRCD53,R3
         MVC   MESG+17(8),SMF53RMT
         MVC   MESG+40(8),SMF53LIN
JESSCOMN MVC   MESG+13(4),=C'RMT='
         MVC   MESG+35(5),=C'LINE='
         B     PUTGET
JES2INTG MVC   RECTYPE,=C'JES2 INTEGRITY  (54)' TYPE 54(JES2 INTEGRITY)
         USING SMFRCD54,R3
         MVC   MESG+13(4),=C'RMT='
         MVC   MESG+17(8),SMF54RMT
         B     PUTGET
VVDSUPDT MVC   RECTYPE,=C'VVDS UPDATE     (60)' TYPE 60(VVDS UPDATE)
         USING SMFRCD60,R3
         MVC   MESG(4),=C'JOB='
         MVC   MESG+13(5),=C'TYPE='
         MVC   MESG+21(4),=C'DSN='
         MVC   MESG+4(8),SMF60JNM
         MVC   MESG+18(1),SMF60TYP
         MVC   MESG+25(44),SMF60ENM
         PUT   SYSPRINT,LINE
         MVC   MESG2+17(5),=C'STAT='
         MVC   MESG2+22(2),SMF60SUB
         MVC   MESG2+25(5),=C'VVDS='
         MVC   MESG2+30(44),SMF60CNM
         PUT   SYSPRINT,LINE2
         B     GETRECRD
ICFDEFN  MVC   RECTYPE,=C'ICF DEFINE      (61)' TYPE 61(ICF DEFINE)
         USING SMFRCD61,R3
         MVC   MESG(4),=C'JOB='
         MVC   MESG+13(5),=C'TYPE='
         MVC   MESG+21(4),=C'DSN='
         MVC   MESG+4(8),SMF61JNM
         MVC   MESG+18(1),SMF61TYP
         MVC   MESG+25(44),SMF61ENM
         PUT   SYSPRINT,LINE
         MVC   MESG2+17(5),=C'STAT='
         MVC   MESG2+22(2),SMF61SUB
         MVC   MESG2+25(4),=C'CAT='
         MVC   MESG2+29(44),SMF61CNM
         PUT   SYSPRINT,LINE2
         B     GETRECRD
VSAMOPEN MVC   RECTYPE,=C'VSAM OPEN       (62)' TYPE 62(VSAM OPEN)
         USING SMFRCD62,R3
         MVC   MESG(4),=C'JOB='
         MVC   MESG+13(4),=C'DSN='
         MVC   MESG+4(8),SMF62JBN
         MVC   MESG+17(44),SMF62DNM
         PUT   SYSPRINT,LINE
         MVC   MESG2+17(8),=C'CATALOG='
         MVC   MESG2+25(44),SMF62CNM
         PUT   SYSPRINT,LINE2
         B     GETRECRD
VSAMDEFN MVC   RECTYPE,=C'VSAM DEFINE     (63)' TYPE 63(VSAM DEFINED)
         USING SMFRCD63,R3
         MVC   MESG(4),=C'JOB='
         MVC   MESG+13(4),=C'ENM='
         MVC   MESG+4(8),SMF63JBN
         MVC   MESG+17(44),SMF63ENM
         PUT   SYSPRINT,LINE
         MVC   MESG2+17(8),=C'CATALOG='
         MVC   MESG2+25(44),SMF63CNM
         PUT   SYSPRINT,LINE2
         B     GETRECRD
ICFDEL   MVC   RECTYPE,=C'ICF DELETE      (65)' TYPE 65(ICF DELETE)
         USING SMFRCD65,R3
         MVC   MESG(4),=C'JOB='
         MVC   MESG+13(5),=C'TYPE='
         MVC   MESG+21(4),=C'DSN='
         MVC   MESG+4(8),SMF65JNM
         MVC   MESG+18(1),SMF65TYP
         MVC   MESG+25(44),SMF65ENM
         PUT   SYSPRINT,LINE
         MVC   MESG2+17(5),=C'STAT='
         MVC   MESG2+22(2),SMF65SUB
         MVC   MESG2+25(4),=C'CAT='
         MVC   MESG2+29(44),SMF65CNM
         PUT   SYSPRINT,LINE2
         B     GETRECRD
ICFALTER MVC   RECTYPE,=C'ICF ALTER       (66)' TYPE 66(ICF ALTER)
         USING SMFRCD66,R3
         MVC   MESG(4),=C'JOB='
         MVC   MESG+13(5),=C'TYPE='
         MVC   MESG+21(4),=C'DSN='
         MVC   MESG+4(8),SMF66JNM
         MVC   MESG+18(1),SMF66TYP
         MVC   MESG+25(44),SMF66ENM
         PUT   SYSPRINT,LINE
         MVC   MESG2+17(5),=C'STAT='
         MVC   MESG2+22(2),SMF66SUB
         MVC   MESG2+25(4),=C'CAT='
         MVC   MESG2+29(44),SMF66CNM
         PUT   SYSPRINT,LINE2
         B     GETRECRD
VSAMDEL  MVC   RECTYPE,=C'VSAM DELETE     (67)' TYPE 67(VSAM DELETE)
         USING SMFRCD67,R3
         MVC   MESG(4),=C'JOB='
         MVC   MESG+13(4),=C'DSN='
         MVC   MESG+4(8),SMF67JBN
         MVC   MESG+17(44),SMF67DEN
         PUT   SYSPRINT,LINE
         MVC   MESG2+17(8),=C'CATALOG='
         MVC   MESG2+25(44),SMF67CNM
         PUT   SYSPRINT,LINE2
         B     GETRECRD
VSAMRNAM MVC   RECTYPE,=C'VSAM RENAME     (68)' TYPE 68(VSAM RENAME)
         USING SMFRCD68,R3
         MVC   MESG(4),=C'JOB='
         MVC   MESG+13(8),=C'OLD DSN='
         MVC   MESG+4(8),SMF68JBN
         MVC   MESG+21(44),SMF68ONM
         PUT   SYSPRINT,LINE
         MVC   MESG2+7(8),=C'NEW DSN='
         MVC   MESG2+16(44),SMF68NNM
         PUT   SYSPRINT,LINE2
         B     GETRECRD
SYSTATUS MVC   RECTYPE,=C'SYSTEM STATUS   (90)' TYPE 90(SYSTEM STATUS)
         USING SMFRCD90,R3
         LR    R1,R3              GET POINTER TO RECORD
         A     R1,SMF90DOF        GET OFFSET TO DATA SECTION
         LR    R10,R3             GET POINTER TO RECORD
         A     R10,SMF90POF       GET OFFSET TO PRODUCT SECTION
         SR    R15,R15            CLEAR REGISTER
         ICM   R15,1,1(R10)       GET RECORD SUBTYPE
         CH    R15,=H'17'         CHECK FOR SUBTYPE > 16
         BH    NO90SUBT           YES, GO PUT OUT UNKNOWN MSG
         SLA   R15,2              MULTIPLY TYPE BY FOUR
         B     COMBRNCH(R15)      BRANCH INTO TABLE
COMBRNCH B     GETRECRD           00 --------------
         B     SETCLK             01 SET TIME
         B     SETCLK             02 SET DATE
         B     SETDMN             03 SET DOMAIN
         B     SETIPS             04 SET IPS
         B     SETBSMF            05 SET SMF
         B     SWITCHSM           06 SWITCH SMF
         B     HALTEOD            07 HALT EOD
         B     IPLPRMPT           08 IPL PROMPT
         B     IPLSMF             09 IPL SMF
         B     IPLSRM             10 IPL SRM
         B     SETOPT             11 SET OPT
         B     SETICS             12 SET ICS
         B     SETSMF             13 SETSMF
         B     SETMPF             14 SETMPF
         B     NO90SUBT           15 SET SMF RESTART
         B     SETDAE             16 SET DAE
         B     SETPFK             17 SET PFK
SETCLK   MVC   MESG(8),=C'SETCLOCK'
         MVC   DTIME,0(R1)        TIME BEFORE
         MVC   DDATE,4(R1)        DATE BEFORE
         BAL   R14,TIMEDATE
         MVC   MESG+11(5),=C'FROM='
         MVC   MESG+16(6),TDATE+1
         MVC   MESG+23(8),TTIME+2
         MVC   DTIME,8(R1)        TIME AFTER
         MVC   DDATE,12(R1)       DATE AFTER
         BAL   R14,TIMEDATE
         MVC   MESG+32(3),=C'TO='
         MVC   MESG+35(6),TDATE+1
         MVC   MESG+42(8),TTIME+2
         B     PUTGET
SETDMN   SR    R15,R15
         ICM   R15,1,8(R1)        DOMAIN NUMBER
         CVD   R15,WORK
         MVC   MESG+26(7),=X'40212020202020'
         ED    MESG+26(7),WORK+5
         MVC   MESG+25(4),=C'DMN='
         MVC   MESG(16),=C'SETDMN     TIME='
         LA    R7,MESG+33
MNMPLCHK SR    R15,R15            CLEAR REGISTER
         TM    9(R1),X'80'        MIN MPL CHANGED
         BO    MINMPL
MXMPLCHK TM    9(R1),X'40'        MAX MPL CHANGED
         BO    MAXMPL
WEIGHCHK TM    9(R1),X'20'        WEIGHT CHANGED
         BO    WEIGHT
AOBJCHK  TM    9(R1),X'10'        AOBJ CHANGED
         BO    AOBJ
DOBJCHK  TM    9(R1),X'08'        DOBJ CHANGED
         BO    DOBJ
FWKLCHK  TM    9(R1),X'04'        FWKL CHANGED
         BZ    PUTGET
         ICM   R15,1,15(R1)       NEW FWKL
         BAL   R14,SETDMNIN
         MVC   0(8,R7),=C'FIX WKL='
         LA    R7,12(,R7)         ADD MESSAGE LENGTH TO POINTER
         B     PUTGET
MINMPL   ICM   R15,1,10(R1)       NEW MIN MPL VALUE
         BAL   R14,SETDMNIN
         MVC   0(8,R7),=C'MIN MPL='
         LA    R7,12(R7)          ADD MESSAGE LENGTH TO POINTER
         B     MXMPLCHK
MAXMPL   ICM   R15,1,11(R1)       NEW MAX MPL VALUE
         BAL   R14,SETDMNIN
         MVC   0(8,R7),=C'MAX MPL='
         LA    R7,12(R7)          ADD MESSAGE LENGTH TO POINTER
         B     WEIGHCHK
WEIGHT   ICM   R15,1,12(R1)       NEW WEIGHT VALUE
         BAL   R14,SETDMNIN
         MVC   0(8,R7),=C' WEIGHT='
         LA    R7,12(R7)
         B     AOBJCHK
AOBJ     ICM   R15,1,13(R1)       NEW AOBJ VALUE
         BAL   R14,SETDMNIN
         MVC   0(8,R7),=C'AVG OBJ='
         LA    R7,12(R7)
         B     DOBJCHK
DOBJ     ICM   R15,1,14(R1)       NEW DOBJ VALUE
         BAL   R14,SETDMNIN
         MVC   0(8,R7),=C'DMN OBJ='
         LA    R7,12(R7)
         B     FWKLCHK
IPLSRM   MVC   MESG(19),=C'IPL SRM    IPLTIME='
         MVC   MESG+28(18),=C'IPS=XX OPT=YY ICS='
         MVC   MESG+32(2),14(R1)  IPS SUFFIX
         MVC   MESG+39(2),22(R1)  OPT SUFFIX
         MVC   MESG+46(2),30(R1)  ICS SUFFIX
         BAL   R14,STCKCONV
         MVC   MESG+19(8),TIME
         B     PUTGET
IPLPRMPT MVC   MESG(10),=C'IPL PROMPT'
         MVC   DTIME,93(R1)       TIME OF IPL
         MVC   DDATE,97(R1)       DATE OF IPL
         BAL   R14,TIMEDATE
         MVC   MESG+11(8),=C'IPLDATE='
         MVC   MESG+19(6),TDATE+1
         MVC   MESG+26(8),=C'IPLTIME='
         MVC   MESG+34(8),TTIME+2
         CLI   0(R1),C'U'         CHECK DOWNTIME FOR DEFAULT
         BNE   LONG
         CLI   73(R1),C'U'        CHECK OPERATOR FOR DEFAULT
         BNE   LONG
         CLI   8(R1),C'U'         CHECK REASON FOR DEFAULT
         BNE   LONG
         MVC   MESG+43(26),=C'DOWNTIME=U OPER=U REASON=U'
         B     PUTGET
LONG     MVC   MESG+43(23),=C'DOWNTIME=HH:MM:SS OPER='
         MVC   MESG+52(8),0(R1)
         MVC   MESG+66(20),73(R1)
         MVC   MESG2+16(7),=C'REASON='
         MVC   MESG2+23(65),8(R1)
         PUT   SYSPRINT,LINE
         PUT   SYSPRINT,LINE2
         B     GETRECRD
SETIPS   MVC   MESG(7),=C'SET IPS'
         B     PARMSET
SETBSMF  MVC   MESG(7),=C'SET SMF'
         B     T90DATIM
SWITCHSM MVC   MESG(10),=C'SWITCH SMF'
         B     SWCHALT
HALTEOD  MVC   MESG(8),=C'HALT EOD'
         B     SWCHALT
IPLSMF   MVC   MESG(7),=C'IPL SMF'
         B     T90DATIM
SETOPT   MVC   MESG(7),=C'SET OPT'
         B     PARMSET
SETICS   MVC   MESG(7),=C'SET ICS'
         B     PARMSET
SETSMF   MVC   MESG(6),=C'SETSMF'
         B     T90DATIM
SETMPF   MVC   MESG(7),=C'SET MPF'
         B     PARMSET
SETDAE   MVC   MESG(7),=C'SET DAE'
         B     PARMSET
SETPFK   MVC   MESG(7),=C'SET PFK'
PARMSET  BAL   R14,STCKCONV
         MVC   MESG+16(8),TIME    MOVE TIME CONVERSION
         MVC   MESG+11(5),=C'TIME='
         MVC   MESG+25(11),=C'FROM=XX TO='
         MVC   MESG+30(2),14(R1)  MOVE OLD SUFFIX
         MVC   MESG+36(2),22(R1)  MOVE NEW SUFFIX
         B     PUTGET
SWCHALT  MVC   MESG+11(5),=C'FROM='
         MVC   MESG+27(3),=C'TO='
         MVC   MESG+16(10),0(R1)  OLD DSN
         MVC   MESG+30(10),10(R1) NEW DSN
         B     PUTGET
NO90SUBT MVC   MESG(8),=C'SUBTYPE='
         XC    WORK,WORK
         CVD   R9,WORK
         MVC   MESG+8(2),WORK+6
         B     PUTGET
T90DATIM LR    R10,R3             GET RECORD POINTER
         A     R10,0(R1)          OFFSET TO IPL&SET SMF
         MVC   DTIME,28(R10)      TIME OF IPL
         MVC   DDATE,32(R10)      DATE OF IPL
         BAL   R14,TIMEDATE
         MVC   MESG+11(8),=C'IPLDATE='
         MVC   MESG+26(8),=C'IPLTIME='
         MVC   MESG+19(6),TDATE+1
         MVC   MESG+34(8),TTIME+2
         B     PUTGET
TIMEDATE L     R1,DTIME
         CVD   R1,WORK
         DP    WORK,=P'100'
         DP    WORK(6),=P'3600'
         ZAP   WORK2,WORK(3)
         MP    WORK2,=P'10000'
         ZAP   WORK3,WORK+3(3)
         DP    WORK3,=P'60'
         ZAP   WORK,WORK3(6)
         MP    WORK,=P'100'
         AP    WORK2,WORK
         AP    WORK2,WORK3+6(2)
         ZAP   WORK,WORK2
         MVC   TTIME,=X'402120207A20207A2020'
         ED    TTIME,WORK+4
         MVC   TDATE,=X'4021204B202020'
         ED    TDATE(7),DDATE+1
         MVC   LSYSID,14(R3)      MOVE SYSID TO PRINTOUT
         BR    R14
CNVTUCB  LA    R6,CHARS
         L     R5,WORK
         SLL   R5,20
         SR    R4,R4
         SLDA  R4,4
         IC    R10,0(R4,R6)
         STC   R10,WORK+1
         SR    R4,R4
         SLDA  R4,4
         IC    R10,0(R4,R6)
         STC   R10,WORK+2
         SR    R4,R4
         SLDA  R4,4
         IC    R10,0(R4,R6)
         STC   R10,WORK+3
         BR    R14
SETDMNIN ST    R14,SDR14SAV
         CVD   R15,WORK
         MVC   5(6,R7),=X'402120202020'
         ED    5(6,R7),WORK+5
         BAL   R14,STCKCONV
         MVC   MESG+16(8),TIME    MOVE TIME CONVERSION
         L     R14,SDR14SAV
         BR    14
CHKDEVT  TM    0(R10),X'20'       DASD DEVICE
         BO    DASDTYPE           YES, THEN CHECK TYPE
         TM    0(R10),X'80'       TAPE DEVICE
         BO    TAPEMSG            YES, THEN PUT OUT MESG
         TM    0(R10),X'41'       CHAN-TO-CHAN ADAPTER
         BO    CTCMSG             YES, THEN PUT OUT MESG
         TM    0(R10),X'08'       UNIT RECORD
         BO    URECMSG            YES, THEN PUT OUT MESG
         TM    0(R10),X'10'       DISPLAY
         BO    DSPLYMSG           YES, THEN PUT OUT MESG
         TM    0(R10),X'40'       COMMUNICATIONS
         BO    COMMSG             YES, THEN PUT OUT MESG
         TM    0(R10),X'04'       CHARACTER READER
         BO    CHRDRMSG           YES, THEN PUT OUT MESG
         MVC   MESG+45(10),=C'NO DEVICES'
         B     CONTINUE           NO, THEN GO ON
DASDTYPE SR    R15,R15
         ICM   R15,1,1(R10)
         CH    R15,=H'16'
         BL    DASDOK
         LA    R15,1
DASDOK   BCTR  R15,R0
         SLL   R15,3
         LA    R1,DASDTBL(R15)
         MVC   MESG+26(4),0(R1)
         B     CONTINUE
DASDTBL  DC    CL4'????'      0
         DC    CL4'????'      1
         DC    CL4'????'      2
         DC    CL4'????'      3
         DC    CL4'????'      4
         DC    CL4'????'      5
         DC    CL4'2305' -1   6
         DC    CL4'2305' -2   7
         DC    CL4'2314'      8
         DC    CL4'3330' -1   9
         DC    CL4'????'      A
         DC    CL4'3350'      B
         DC    CL4'3375'      C
         DC    CL4'3330' -11  D
         DC    CL4'3380'      E
         DC    CL4'3390'      F
TAPEMSG  MVC   MESG+26(4),=C'TAPE'
         B     CONTINUE
CTCMSG   MVC   MESG+26(3),=C'CTC'
         B     CONTINUE
URECMSG  MVC   MESG+26(2),=C'UR'
         B     CONTINUE
DSPLYMSG MVC   MESG+26(7),=C'DISPLAY'
         B     CONTINUE
COMMSG   MVC   MESG+26(4),=C'COMM'
         B     CONTINUE
CHRDRMSG MVC   MESG+26(5),=C'CHRDR'
CONTINUE BR    14
STCKCONV ST    R1,R1SAVE          SAVE REGISTER
         STM   R13,R14,R13SAVE    SAVE REGISTERS
         MVC   8(8,R13),0(R1)     MOVE STORE CLOCK VALUE
         LM    0,1,8(13)          LOAD INTO WORK REGS
         L     15,16              GET CVT ADDRESS
         A     0,304(15)          ADD LOCAL TIME DEVIATION
         SRDL  0,12               ISOLATE NUMBER OF MICROSECONDS
         D     0,=F'60000000'     DIVIDE BY 60M (R1=MINUTES AFT EPOCH)
         LR    15,0               COPY REMAINDER OF MICS TO GET SECS
         SR    14,14              CLEAR FOR DIVIDE
         D     14,=F'951424'      DIVIDE TO GET REMAINING SECONDS (R5)
         LR    14,15              COPY TO WORK REG
SLB      SL    14,=F'60'          DECREMENT BY 60 SECONDS
         BM    SLS                LESS THAN SIXTY, CONTINUE
         SL    15,=F'60'          MORE THAN SIXTY, ADJUST FOR LEAP
         AL    1,=F'1'            BUMP MINUTES
         B     SLB                CHECK AGAIN
SLS      CVD   15,8(13)           CONVERT SECONDS TO PACKED FORMAT
         UNPK  24(4,13),14(2,13)  UNPACK SECONDS FOR PRINT
         OI    27(13),X'F0'       SET UP FOR PRINTING
         MVC   TIME+6(2),26(13)   MOVE THE SECONDS
         CVD   1,8(13)            CONVERT MINUTES TO PACKED FORMAT
         DP    8(8,13),=P'60'     DIVIDE INTO HOURS AND MINUTES
         UNPK  24(4,13),14(2,13)  UNPACK THE MINUTES
         OI    27(13),X'F0'       SET UP FOR PRINTING
         MVC   TIME+3(2),26(13)   MOVE THE MINUTES
         ZAP   8(8,13),8(6,13)    RESET TO FULL LENGTH
         DP    8(8,13),=P'24'     DIVIDE INTO DAYS AND HOURS
         UNPK  24(4,13),14(2,13)  UNPACK THE HOURS
         OI    27(13),X'F0'       SET UP FOR PRINTING
         MVC   TIME(2),26(13)     MOVE THE HOURS
         MVI   TIME+2,C':'        ADD COLON
         MVI   TIME+5,C':'        ADD COLON
         ZAP   8(8,13),8(6,13)    RESET TO FULL LENGTH
         DP    8(8,13),=P'7'      DIVIDE BY NUMBER OF DAYS IN A WEEK
         ZAP   8(8,13),15(1,13)   FILL DOUBLEW WITH THE REMAINDER
         CVB   0,8(13)            CONVERT RELATIVE DAY TO BINARY
         STC   0,DAY              SET RELATIVE DAY OF WEEK
         ZAP   16(8,13),8(8,13)   SAVE THE NUMBER OF DAYS
         L     1,16               GET CVT ADDRESS
         MVC   24(4,13),57(1)     MOVE CVT DATE TO WORK AREA
         ZAP   8(8,13),=P'0'      ZERO WORK AREA
         MVO   14(2,13),24(1,13)  MOVE YEAR PORTION
         UNPK  DATE+6(2),14(2,13) UNPACK INTO OUTPUT AREA
         LA    15,SLT             LOAD ADDR OF TABLE
         CVB   1,8(13)            CONVERT TO BIN
         SLL   1,30               SHIFT ALL BUT LAST TWO
         LTR   1,1                TEST RESIDUAL
         BNZ   *+8                NOT 0, BRANCH
         LA    15,SLTL            LEAP YEAR
         LR    14,15              SAVE TABLE IN USE
         ZAP   8(8,13),25(2,13)   MOVE DAY PORTION
         CVB   0,8(13)            CONVERT TO BIN
         SR    1,1                CLEAR REG
         IC    1,0(15)            LOAD CHAR FROM TABLE
         LA    15,1(15)           INDEX TABLE POINTER
         SR    0,1                SUBTRACT FROM WORK VALUE
         BP    *-10               STILL POSITIVE, DO AGAIN
         AR    0,1                ELSE, ADD IT BACK
         CVD   0,8(13)            CONVERT TO DEC
         UNPK  DATE+3(2),14(2,13) UNPACK DAY INTO OUTPUT
         SR    15,14              FIND VALUE OF CURRENT INDEX
         CVD   15,8(13)           CONVERT TO DEC
         UNPK  DATE(2),14(2,13)   UNPACK MONTH INTO OUTPUT
         OI    DATE+1,C'0'        INSURE NUMERICS
         OI    DATE+4,C'0'        INSURE NUMERICS
         OI    DATE+7,C'0'        INSURE NUMERICS
         MVI   DATE+2,C'/'        ADD SLASH
         MVI   DATE+5,C'/'        ADD SLASH
         L     R1,R1SAVE          RELOAD REGISTER
         LM    R13,R14,R13SAVE    RELOAD REGISTERS
         BR    14                 RETURN TO CALLER
SLT      DC    AL1(31,28,31,30,31,30,31,31,30,31,30,31) MONTH TABLE
SLTL     DC    AL1(31,29,31,30,31,30,31,31,30,31,30,31) LEAP YEAR
CHARS    DC    C'0123456789ABCDEF'
BLANKS   DC    CL8' '
         LTORG
SYSPRINT DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=PM,LRECL=133,RECFM=FBA,  X
               BLKSIZE=6118
SMFDATA  DCB   DDNAME=SMFDATA,DSORG=PS,MACRF=GL,EODAD=EODAD,BFTEK=A
RECDEF   DSECT
RECS     IFASMFR (0,2,3,4,5,6,7,9,10,11,14,17,18,19,20,21,22,23,24,26)
         IFASMFR (31,34,35,43,45,47,48,49,52,53,54)
         IFASMFR (62,63,64,67,68,69,90)
         IFASMFI6 36
         IFASMFI6 60
         IFASMFI6 61
         IFASMFI6 65
         IFASMFI6 66
WORKAREA DSECT
SAVEAREA DC    18F'0'
R1SAVE   DC    F'0'
R13SAVE  DC    F'0'
R14SAVE  DC    F'0'
SDR14SAV DC    F'0'
DTIME    DS    F
DDATE    DS    F
WORK     DS    D
WORK2    DS    D
WORK3    DS    D
TDATE    DC    CL7' '
TTIME    DC    CL10' '
DAY      DC    XL1'00'            RELATIVE DAY OF THE WEEK
DATE     DC    C'  /  /  '        CURRENT DATE
TIME     DC    C'  :  :  '        CURRENT TIME
LINE     DS    0CL133    -----------------------------------]
         DC    C' '                                         ]
LSYSID   DC    CL4' ',C' '                                  ]
LDATE    DC    CL6' ',C' '                                  ]
LTIME    DC    CL8' ',CL2' '                                ]
RECTYPE  DC    CL20' ',C' '                                 ]
MESG     DC    CL89' '   -----------------------------------]
LINE2    DS    0CL133    ----------------------------------------]
         DC    CL40' '                                           ]
MESG2    DC    CL93' '   ----------------------------------------]
WORKLEN  EQU   *-WORKAREA
         END


                                                                                                 c Xephon 1995

