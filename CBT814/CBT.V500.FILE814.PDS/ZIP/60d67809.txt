JES2 priority aging modification


The USERMOD

//*
//** USERMOD TO ADD CODE TO HASPMISC TO DISABLE PRIORITY AGING
//** FOR HELD JOBS.
//** USERMOD MODIFIES MODULE HASPMISC
//**
//*
//STEP01   EXEC SMPE
//SMPHOLD  DD  DUMMY
//SMPCNTL  DD  *
SET BDY(GLOBAL).
   RECEIVE S(LM00042) LIST .
SET BDY(AMVSTZN).
   APPLY   S(LM00042) .
/*
//SMPPTFIN DD  *
++USERMOD(LM00042).
++VER(Z038) FMID(HJE4430) PRE(UW01679,UW04215,UY93037).
++SRCUPD(HASPMISC) .
./ CHANGE  NAME=HASPMISC
MERCER1  DS    0H                                                MERCER 06510010
         CLI   JQEFLAG1,JQE1HLD1   SINGLE JOB HELD               MERCER 06510020
         BNE   MERCER2             NO, GO CHANGE PRIORITY        MERCER 06510030
         SLR   R7,R7               ZERO CLASS REGISTER           MERCER 06510040
         IC    R7,JQETYPE          PICKUP EXISTING CLASS         MERCER 06510050
         DS    0H                  MODIFY JOB QUEUE ELEMENT      MERCER 06510060
        $QMOD  (R6),(R7),          RESET JOB IN THE QUEUE        MERCERX06510070
               ALONE,              DON'T RESET JOB BUSY BITS     MERCERX06510080
               PCHANGE=NOTOK,      DISALLOW PHASE CHANGE         MERCERX06510090
               PRIORITY=NOCHANGE   DO NOT CHANGE PRIORITY        MERCER 06510100
         B     MERCER3             GO CHECKPOINT                 MERCER 06510110
MERCER2  DS    0H                                                MERCER 06510120
MERCER3  DS    0H                                                MERCER 06585001
./ ENDUP
/*


          .
          .
*********************************************************************** 06311000
*                                                                     * 06312000
*                 PRIORITY AGE THE JOB QUEUE                          * 06313000
*                                                                     * 06314000
*********************************************************************** 06315000
         SPACE 1                                                   @136 06316000
GPRSCAN  SLR   R3,R3               PREPARE TO SCAN ALL JOB QUEUES  @130 06320000
         SH    R3,=Y($JQHEADL)     ADJUST FOR QUEUE LOOP           @133 06330000
         OI    $CKPTFG1,$CKPTPRI   SHOW PRIO AGING OCCURRED        @220 06335000
         SPACE 1                                                        06340000
         USING JQE,R6              JOB QUEUE ADDRESSABILITY        @133 06350000
         SPACE 1                                                        06360000
GPRQLOOP LA    R3,$JQHEADL(,R3)    INCREMENT INDEX INTO QUEUE'S    @133 06370000
         CH    R3,=Y($JQTYPES*$JQHEADL)  END OF QUEUE HEADS...     @133 06380000
         BNL   GPRSTART            IF SO, START OVER               @133 06390000
         L     R6,$JQHEADS(R3)     PREPARE TO SCAN NEXT JOB QUEUE  @133 06400000
         N     R6,$ZEROFFF         END OF CHAIN...                 @133 06410000
         BZ    GPRQLOOP            BRANCH IF YES                   @133 06420000
         LA    R6,$JQHEADS(R3)     SET HEAD ADDRESS AS JQE         @133 06430000
         SH    R6,=Y(JQENEXT-JQE)      ADJUST FOR JQE CHAIN LOOP   @133 06440000
         SPACE 1                                                   @311 06445000
GPRCLOOP L     R6,JQENEXT          GET NEXT JQE ON CHAIN           @133 06450000
         N     R6,$ZEROFFF         CLEAR FLAG AND TEST CHAIN END   @133 06460000
         BZ    GPRQLOOP            BR TO NEXT QUEUE IF END         @133 06470000
         AL    R6,$JOBQPTR         EXPAND JQE OFFSET TO ABS ADDR   @133 06480000
         SPACE 1                                                 @136EM 06486000
         CLI   JQETYPE,$HARDCPY    JQE ON HARDCOPY ?             @420DJ 06500000
         BE    GPRCLOOP            YES, IGNORE JQE             @420D009 06510000
MERCER1  DS    0H                                                MERCER 06510010
         CLI   JQEFLAG1,JQE1HLD1   SINGLE JOB HELD ?             MERCER 06510020
         BNE   MERCER2             NO, GO CHANGE PRIORITY        MERCER 06510030
         SLR   R7,R7               ZERO CLASS REGISTER           MERCER 06510040
         IC    R7,JQETYPE          PICKUP EXISTING CLASS         MERCER 06510050
         DS    0H                  MODIFY JOB QUEUE ELEMENT      MERCER 06510060
        $QMOD  (R6),(R7),          RESET JOB IN THE QUEUE        MERCERX06510070
               ALONE,              DON'T RESET JOB BUSY BITS     MERCERX06510080
               PCHANGE=NOTOK,      DISALLOW PHASE CHANGE         MERCERX06510090
               PRIORITY=NOCHANGE   DO NOT CHANGE PRIORITY        MERCER 06510100
         B     MERCER3             GO CHECKPOINT THE JQE         MERCER 06510110
MERCER2  DS    0H                                                MERCER 06510120
         CLC   JQEPRIO,$PRIHIGH    COMPARE PRIO WITH UPPER LIMIT   @133 06520000
         BNL   GPRCLOOP            BRANCH IF GREATER THAN OR EQUAL @133 06530000
         CLC   JQEPRIO,$PRILOW     COMPARE PRIO WITH LOWER LIMIT   @133 06540000
         BL    GPRCLOOP            BR IF LESS -- DONE WITH JQE     @133 06550000
         IC    R2,JQEPRIO          INCREMENT                            06560000
         LA    R2,1(,R2)            JOB QUEUE                           06570000
         STC   R2,JQEPRIO            PRIORITY                           06580000
         OI    JQEFLAG6,JQE6PRAG   SET PRIORITY AGING FLAG       @420DJ 06582000
         SPACE 1                                               @420D009 06585000
MERCER3  DS    0H                                                MERCER 06585001
        $CKPT  ID=JQE,ADDR=(R6)    CHECKPOINT THE JQE              @133 06590000
         SPACE 1                                               @420D009 06595000
         B     GPRCLOOP            BR TO GET NEXT JQE              @133 06600000
         SPACE 1                                               @420D009 06610000
         DROP  R6,R12              DROP JQE AND ROUTINE        @420D009 06790000
         EJECT                                                 @420D009 06800000
GPRSEC   DC    A(5400)             NUMBER OF SECONDS IN 1/16 OF A DAY  C06810000
                                   86400 SECS(24 HRS)/16           @136 06820000
GPRINT   DC    A(540000)           TIME INTERVAL IN 100THS OF SECS @136 06820300
         LTORG                                                          06820600
         SPACE 2                                                   @136 06820900
         DROP  ,                   KILL ALL ADDRESSABILITY         @136 06821200



//CTP     EXEC JES2ASM,MODULE=HASCTP                           @420LD03
//*                                                            @430P430
//************************************************************ @430P430
//*                                                          * @430P430
//*      The following steps assemble the IBM base JES2      * @430P430
//*      product source-distributed modules that are used    * @430P430
//*      in the JES2 address space, and in FSS address       * @430P430
//*      spaces (the HASPxxxx modules).                      * @430P430
//*                                                          * @430P430
//************************************************************ @430P430
.
.
//IRPL    EXEC JES2ASM,MODULE=HASPIRPL
//IRRE    EXEC JES2ASM,MODULE=HASPIRRE
//IRSI    EXEC JES2ASM,MODULE=HASPIRSI                         @430P430
//JOS     EXEC JES2ASM,MODULE=HASPJOS
//MISC    EXEC JES2ASM,IBMSRC='SYS2.TEST.IBMSRC',MODULE=HASPMISC  <===
//MSG     EXEC JES2ASM,MODULE=HASPMSG
//NATS    EXEC JES2ASM,MODULE=HASPNATS                         @410DRVR
//NET     EXEC JES2ASM,MODULE=HASPNET                          @410DRVR
//NJT     EXEC JES2ASM,MODULE=HASPNJT                          @420LNJE
.
.


Jeff Wootton
Technical Services Manager
William M Mercer Inc (USA)                c William M Mercer Inc

