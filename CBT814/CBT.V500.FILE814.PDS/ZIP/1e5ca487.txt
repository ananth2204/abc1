Tracking personal storage


THE POP-UP PANEL
.------------------------------------------------------------.
|            STORAGE INDICATOR                               |
|            -----------------                               |
|  _____________________________________                     |
| |                                     |   amount used      |
|  -------->                                0.66     Mb below|
|  -->                                      0.97     Mb above|
|                                                            |
| Maximum allowed below the line ===> 4.06   Mb              |
| Maximum allowed above the line ===> 32.00  Mb              |
|                                                            |
| INFORMATION                                                |
|   Region asked for   ===> 4.00   Mb                        |
|   System used below  ===> 0.39   Mb                        |
|   System used above  ===> 9.73   Mb                        |
'------------------------------------------------------------'


PANEL CODE FOR REGPAN

)Attr Default(%+_)  
       %   Type(Output) Intens(High) Caps(Off)          
)Body Window(70,13) 
              +STORAGE INDICATOR
              +-----------------
 + _____________________________________________           
 +|                                             |    amount used    
 %below                                             %belowpvt+Mb below          
 %above                                             %abovepvt+Mb above          
                                                                                            
+Maximum allowed below the line ====>%maxbelow+Mb                               
+Maximum allowed above the line ====>%maxabove+Mb                               
                                                                                
+INFORMATION                                                                    
+  Region asked for   ===>%askedfor+Mb                                          
+  System used below  ===>%belowsys+Mb                                          
+  System used above  ===>%abovesys+Mb                                          
)End


INVOKING REXX

/* REXX */
/*---------------------------------*/                       
/* First define what a megabyte is */                                                                      
/*---------------------------------*/                       
megabyte=1024*1024
/*-------------------------------*/                       
/* Now call the LDA mapper       */                                                              
/*-------------------------------*/                       
CALL REGREXX
/*------------------------------------------*/                       
/* And now format the values into megabytes */                                                                    
/*------------------------------------------*/                       
askedfor=FORMAT((c2d(askedfor)/megabyte),,2)                                    
maxbelow=FORMAT((c2d(maxbelow)/megabyte),,2)                                    
maxabove=FORMAT((c2d(maxabove)/megabyte),,2)                                    
belowpvt=FORMAT((c2d(belowpvt)/megabyte),,2)                                    
belowsys=FORMAT((c2d(belowsys)/megabyte),,2)                                    
abovepvt=FORMAT((c2d(abovepvt)/megabyte),,2)                                    
abovesys=FORMAT((c2d(abovesys)/megabyte),,2)
/*--------------------------------------------------------------*/                       
/* Now provide a primitive graphic measure of allocation status */                                    
/*--------------------------------------------------------------*/                       
below=copies('-',FORMAT((belowpvt*50/maxbelow),,0))||'>'                        
above=copies('-',FORMAT((abovepvt*50/maxabove),,0))||'>'                        
/*-----------------------*/                       
/* Now display the panel */
/*-----------------------*/                       
CALL MESSPOP                                                                    
EXIT                                                                            
MESSPOP:                                                                        
address ISPEXEC                                                                 
'ADDPOP ROW(1) COLUMN(9)'                                                       
'DISPLAY PANEL(regpan)'                                                         
'REMPOP'                                                                        
RETURN               


THE ASSEMBLER CODE FOR REGREXX

***********************************************************************         
* REGREXX: A rexx function to provide access to the LDA and thus 
*          provide information regarding the user's memory usage.                    
* USAGE: CALL REGREXX                                                           
* NOTE:  Returned variables are as follows (all values are in bytes):
*        MAXBELOW ... maximum region possible below the line
*        MAXABOVE ... maximum region possible above the line
*        ASKEDFOR ... region requested on job card
*        BELOWPVT ... amount used below the line
*        BELOWSYS ... system usage below the line
*        ABOVEPVT ... amount used above the line
*        ABOVESYS ... system usage above the line
* LINKAGE INFORMATION: AMODE 31, RMODE ANY, program state
* POSSIBLE ABENDS: ABEND CODE 1 ... Unable to define REXX variable
*                  ABEND CODE 2 ... Unable to locate the ASCB
***********************************************************************         
         MACRO                                                                   
         SHOW  &LABEL,&ASNAME,&ERR=ABEND001                                       
         ST    R10,COMRET                                                           
         LA    6,COMSHVB                                                            
         USING SHVBLOCK,R6                                                       
         XC    COMSHVB(SHVBLEN),COMSHVB                                             
         XC    SHVNEXT,SHVNEXT                                                      
         MVI   SHVCODE,C'S'                                                        
LITLOC   LOCTR                                                                   
@_&ASNAME      DC C'&ASNAME'                                                         
@_&ASNAME._LEN EQU L'@_&ASNAME                                                  
@_&LABEL._LEN  EQU L'&LABEL                                                      
REGREXX  LOCTR                                                                   
         LA    1,@_&ASNAME                                                          
         ST    1,SHVNAMA                                                            
         LA    1,@_&ASNAME._LEN                                                     
         ST    1,SHVNAML                                                            
         LA    1,&LABEL                                                             
         ST    1,SHVVALA                                                            
         LA    1,@_&LABEL._LEN                                                      
         ST    1,SHVVALL                                                            
         LR    0,10                                                                 
         LA    1,COMS                                                               
         L     15,IRXEXCOM                                                           
         BALR  14,15                                                              
         LTR   15,15                                                               
         BNZ   &ERR                                                                
         MEND                                                                    
REGREXX  TITLE 'REXX FUNCTION TO RETRIEVE LDA INFORMATION'                       
REGREXX  AMODE 31                                                                
REGREXX  RMODE ANY                                                               
REGREXX  CSECT                                                                   
         BAKR  14,0                                                               
         LR    12,15                                                              
         USING REGREXX,12                                                        
         LR    R10,R0                  *R10 --> A(ENVIRONMENT BLOCK)            
         USING ENVBLOCK,R10                                                     
         LR    R11,R1                  *R11 --> A(PARAM LIST (EFPL))            
         USING EFPL,R11                                                         
         L     R9,ENVBLOCK_IRXEXTE     *R9 --> A(EXTERNAL EP TABLE)             
         USING IRXEXTE,R9                                                       
* Obtain some storage for REXX variable output and map with R2   
         STORAGE OBTAIN,LENGTH=AREALEN,ADDR=(2),LOC=BELOW                       
         USING WORKAREA,2                                                       
* Prepare the REXX area for use                                               
         XC    COMS(COMSLEN),COMS      * set to low values                            
         LA    15,COMID                                                           
         ST    15,COMS                                                            
         LA    15,COMDUMMY                                                        
         ST    15,COMS+4                                                          
         ST    15,COMS+8                                                          
         LA    15,COMSHVB                                                         
         ST    15,COMS+12                                                         
         LA    15,COMRET                                                          
         ST    15,COMS+16                                                         
         OI    COMS+16,X'80'                                                      
         MVC   COMID,=C'IRXEXCOM'                    
VARLOOP  DS    0H                                                                   
***********************************************************************         
*        Locate the LDA
*        ASID ==> ASCB ==> LDA
*        and map with R5
*        Note that if additional fields are required from the LDA
*        just add a SHOW macro FORMAT SHOW from,as,ERR=
*        where from is the field to return; as is the name to
*        be given to the field in REXX and ERR is the address of
*        the routine to call should the attempt to set a rexx variable
*        fail. Within this code the default for ERR is the routine
*        ABEND001 which issues an abend macro with a code of 1                                              
***********************************************************************         
         EXTRACT (5),FIELDS=(ASID)                                      
         L     5,0(,5)                                                              
         STH   5,MYASID
***********************************************************************
*        Use locascb to find the ASCB from the ASID
*        In the event of a problem abend with a code of 2
***********************************************************************
         LOCASCB ASID=MYASID                                            
         LTR   15,15                                                      
         BNZ   ABEND002
         USING ASCB,R1                                                  
         L     R5,ASCBLDA                                                  
         USING LDA,R5
         SHOW  LDALIMIT,MAXBELOW                                         
         SHOW  LDAELIM,MAXABOVE                                          
         SHOW  LDAREGRQ,ASKEDFOR                                                 
         SHOW  LDALOAL,BELOWPVT                                                  
         SHOW  LDAHIAL,BELOWSYS                                                  
         SHOW  LDAELOAL,ABOVEPVT                                                 
         SHOW  LDAEHIAL,ABOVESYS
         B     RETURN                                                               
ABEND001 DS    0H                                                               
         ABEND 1                                                                
ABEND002 DS    0H                                                               
         ABEND 2                                                                
***********************************************************************         
***      Return to caller                                                   
***      Releasing all the storage in the process                                   
***********************************************************************         
RETURN   DS 0H                                                                  
         STORAGE RELEASE,LENGTH=AREALEN,ADDR=(2)                                
         PR                                                                     
***********************************************************************         
***      Working storage etc                                            
***********************************************************************         
         TITLE  'WORKING STORAGE / DSECTS'                                      
         LTORG                                                                  
WORKAREA DSECT                                                                  
*      IRXEXCOM PARAMETER AREA                                                  
         DS    0D                                                                 
COMS     DS    5AL4                                                               
COMID    DS    CL8                                                                
COMDUMMY DS    AL4             
COMSHVB  DS    (SHVBLEN)X   
COMRET   DS    AL4                                                   
         DS    0D                                                                 
MYASID   DS    H                                                                   
AREALEN  EQU   *-WORKAREA                                                         
COMSLEN  EQU   *-COMS                                                             
         IHALDA                                                                 
         IHAASCB                                                                
         IRXEFPL                                                                
         IRXARGTB                                                               
         IRXEVALB                                                               
         IRXENVB                                                                
         IRXEXTE                                                                
         IRXSHVB                                                                
         END                                                                    


                       c Xephon 1996

