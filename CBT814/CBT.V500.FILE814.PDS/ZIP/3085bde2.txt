Pre-edited TSO broadcast messages

The normal method of creating TSO broadcast messages is both
cumbersome and tedious.  With the aid of the accompanying
CLISTs, you may use an editor such as ISPF's to create a multi-
line broadcast message quickly and conveniently.  Once the
message is created, one invocation of the BC CLIST places all
lines of the prepared text in the broadcast dataset.  The shorter
CLRBC CLIST can be used to clear the contents of the broadcast
dataset in preparation for putting out a new message.

BC CLIST

The BC CLIST is written assuming that the edited broadcast
message exists in the user's MEMO.DOCLIB as a member named
BRODCAST.  The optional keyword MEMBER can be used to specify a
different member of the user's MEMO.DOCLIB.  For example, 'BC
NEWSCAST' would put the contents of member NEWSCAST out as
the broadcast.  It is a simple modification to use a differently-
named library as the source of the broadcast text.

/* USE THIS CLIST TO PUT THE CONTENTS OF YOUR OWN                     */
/* USERID.MEMO.DOCLIB(BRODCAST) OUT TO THE BRODCAST DATASET.          */
/* TO DO THAT JUST ISSUE THE %BC COMMAND.                             */
/* IF YOU WANT TO LOAD A DIFFERENT MEMBER OUT TO BRODCAST             */
/* JUST SAY %BC (MEMNAME) WHERE MEMNAME IS THE ONE YOU WANT TO SEND.  */
/* SEE THE CLRBC CLIST FOR A WAY TO CLEAR OUT THE BRODCAST DATASET.   */
PROC 0 MEMBER(BRODCAST)
CONTROL ASIS
ISPEXEC LMINIT DATAID(DDID) DATASET('&SYSPREF..MEMO.DOCLIB') ENQ(SHRW)
ISPEXEC LMOPEN DATAID(&DDID) OPTION(INPUT)
ISPEXEC LMMFIND DATAID(&DDID) MEMBER(&MEMBER)
IF &LASTCC NE 0 THEN GOTO EXIT
SET &N = 1
DO WHILE &LASTCC = 0
   ISPEXEC LMGET DATAID(&DDID) MODE(INVAR) DATALOC(DATA) +
                 DATALEN(LRECL) MAXLEN(80)
   IF &LASTCC = 8 THEN GOTO SEND
   SET OUT&N = &STR(&DATA)
   SET &N = &N +1
END
SEND: +
   OPER
   SET &C = 1
   DO WHILE &C < &N
      SET MSG = &&OUT&C
      SEND '&MSG',SAVE
      SET &C = &C + 1
   END
END
EXIT: +
ISPEXEC LMCLOSE DATAID(&DDID)
ISPEXEC LMFREE DATAID(&DDID)

CLRBC CLIST

The CLRBC CLIST requires an argument corresponding to the
number of message lines currently in the broadcast dataset.
Specifying a number greater than the actual number of message
lines will work also, although an error message will be generated
for each numbered line not actually found in the existing message.
'CLRBC 10', for example, would generate three error messages if
there were only seven lines of broadcast message, but it would
still succeed in clearing the broadcast dataset.

/* USE THIS CLIST TO CLEAR OUT THE BRODCAST DATASET.                 */
/* YOU NEED TO KNOW (OR JUST GUESS HIGH) THE NUMBER OF MESSAGES      */
/* IN THE BRODCAST DATASET TO BE DELETED.                            */
/* FOR EXAMPLE, TO DELETE 15 BRODCAST MESSAGES, SAY %CLRBC 15.       */
/* SEE THE BC CLIST FOR AN EASY WAY TO GET A PRE-EDITED MESSAGE PUT  */
/* INTO THE BRODCAST DATASET.                                        */
PROC 1 NUM
OPER
   SET &C = 1
   DO WHILE &C < &NUM
      SEND &C,DELETE
      SET &C = &C + 1
   END
END


John Cornman
Senior Systems Programmer
Key Services Corporation (USA) © Xephon 1989

