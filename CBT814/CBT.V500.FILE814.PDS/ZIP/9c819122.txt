Using IEFUSI to control storage usage

The SMF step initiation exit IEFUSI is used at our shop to control
the amount of storage any single job  step can acquire. It is
designed to limit storage both below and above the 16-megabyte
line. As it stands, below-the-line storage is limited to the
maximum storage available less 256K, and above-the-line storage
to a maximum of 128 megabytes.

//*  IEFUSI - MVS SMF STEP INITIATION EXIT ASSEMBLY AND LINK
//ASMHCL   EXEC ASMHCL,
//         PARM.C=('OBJECT,RENT'),
//         PARM.L=('XREF,LET,RENT,REUS,REFR')
//C.SYSLIB   DD DSN=SYS1.MACLIB,DISP=SHR
//           DD DSN=SYS1.MODGEN,DISP=SHR
//C.SYSIN    DD *
         TITLE 'IEFUSI - MVS SMF STEP INITIATION EXIT'
* INITIALIZATION ROUTINE
IEFUSI   CSECT
IEFUSI   AMODE 31
IEFUSI   RMODE ANY
         STM   R14,R12,12(R13)      STORE REGISTERS
         LR    R12,R15              BASE REGISTER
         USING IEFUSI,R12           ESTABLISH BASE
SETLIMIT EQU   *
         L     R2,CVTPTR            EST ADRRESABLITY TO CVT
         USING CVTMAP,R2            FROM CONSTANT
         L     R3,CVTSMCA           SYSTEM ID
         USING SMCABASE,R3          ESTABLISH SMCA ADDRESSABILITY
         L     R3,SMCAASCB          CURRENT TASK ADDRESS SPACE
         DROP  R3                   DROP ADDRESSABILITY REGISTER
         USING ASCB,R3              ESTABLISH ASCB ADDRESSABILITY
         L     R3,ASCBLDA           LOAD ADDR OF VSM LOCAL DATA AREA
         DROP  R3                   DROP ADDRESSABILITY REGISTER
         USING LDA,R3               ESTABLISH EXT ADDRESSABILITY
         L     R3,LDAFBQAF          LOAD ADDR OF FBQE FOR ASCB
         DROP  R3                   DROP ADDRESSABILITY REGISTER
         USING FBQE,R3              ESTABLISH EXT ADDRESSABILITY
         L     R3,FBQESIZE          LOAD AVAILABLE FREE STORAGE SIZE
         L     R4,CVTTCBP           LOAD ADDR WHERE TCBS ADDR ARE
         L     R4,4(R4)             LOAD ADDR OF CURRENT TCB
         USING TCB,R4               ESTABLISH TCB ADDRESSABILITY
         L     R4,TCBJSCB           LOAD ADDR OF JSCB
         DROP  R4                   DROP ADDRESSABILITY REGISTER
         USING IEZJSCB,R4           ESTABLISH JSCB ADDRESSABILITY
         L     R4,JSCBSSIB          LOAD ADDR OF SSIB
         DROP  R4                   DROP ADDRESSABILITY REGISTER
         USING SSIB,R4              ESTABLISH SSIB ADDRESSABILITY
         CLC   SSIBJBID(3),TSUSER   TSO USER?
         BNE   CONTINUE             NO, JUST CONTINUE
         L     R3,TSOLIMIT          YES, ADJUST FOR TSO SESSION
CONTINUE L     R4,16(R1)            GET REGION SIZE INFORMATION ADDRESS
         ST    R3,8(R4)             SET REGION-LIMIT TO MAX AVAIL
         L     R5,FULL256K          LOAD 256K VALUE
         SR    R3,R5                LESS 256K FOR REGION-SIZE
         ST    R3,12(R4)            SET REGION-SIZE TO MAX MINUS 256K
         L     R3,FULL128M          LOAD 128M VALUE
         ST    R3,16(R4)            SET EXTENDED REGION-LIMIT TO 128M
         ST    R3,20(R4)            SET EXTENDED REGION-SIZE TO 128M
         L     R3,HIGHBIT           LOAD HIGH-ORDER BIT VALUE
         ST    R3,0(R4)             TELL VSM IEFUSI IS SUPPLYING INFO
RETURN   EQU   *
         LM    R0,R12,20(R13)       RESTORE ALL EXCEPT 14,15
         L     R14,12(R13)          RESTORE RETURN ADDRESS
         BR    R14                  RETURN TO CALLER
         DS    0F
TSOLIMIT DC    XL4'00100000'        TSO 4096K REGION SIZE
FULL256K DC    XL4'00040000'
FULL128M DC    XL4'04000000'        EXTENDED LIMIT 128 MEGABYTES
HIGHBIT  DC    XL4'80000000'
TSUSER   DC    CL3'TSU'
         CVT   DSECT=YES
         IEESMCA                    SYS MGT FACILITIES CTL AREA
         IHAASCB
         IHALDA
         IHAFBQE
         IKJTCB
         IEZJSCB
         IEFJSSIB
         END
/*
//*
//L.SYSLMOD  DD DSN=SYS1.LPALIB(IEFUSI),DISP=SHR
//L.SYSLIB   DD DSN=SYS1.LPALIB,DISP=SHR
//           DD DSN=SYS1.LINKLIB,DISP=SHR


Ray Smith (USA)                                                                         c Xephon 1994

