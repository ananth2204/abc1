Checking for a particular DDNAME
It is often useful to be able to determine if a certain DDNAME is allocated from
within a program.  This subroutine can be called from an Assembler program, and
passed a parameter of the DDNAME you want to enquire about.
The program then scans the TIOT looking for the name passed.  If it is found, a
return code of 0 is passed.  If the DDNAME is not found (ie it is not
allocated), then a return code of 4 will be returned.
A SAMPLE INVOCATION
         .
         .
SCAN     CALL  TIOTSCAN,(DDNAME),VL
         LTR   R15,R15
         BZ    DDOK         DDNAME IS PRESENT
         . (PROCESSING FOR WHEN DDNAME IS MISSING)
         .
DDOK     . (PROCESSING FOR WHEN DDNAME IS PRESENT)
         .
         .
DDNAME   DC    CL8'SYSPRINT'  DDNAME TO BE SEARCHED FOR
         .
THE SOURCE CODE
*---------------------------------------------------------------------*
* GENERAL PURPOSE TIOT SCAN ROUTINE. PARAMETER PASSED IS DDNAME TO BE *
* CHECKED FOR. RETURN WITH RC=0 IF FOUND, RC=4 IF NOT FOUND.          *
*---------------------------------------------------------------------*
TIOTSCAN CSECT ,
         USING *,R15
         STM   R14,R12,12(R13)    SAVE CALLERS REGISTERS
         LR    R14,R13
         BAL   R13,CHAINIT
         USING *,R13              BASE REGISTER
         DROP  R15
         DC    19F'0'             SAVE AREA
CHAINIT  ST    R13,8(,R14)        CHAIN..
         ST    R14,4(,R13)        ..SAVE AREAS
         L     R14,12(,R14)       RESTORE REG ON ENTRY
         L     R2,0(R1)           PICK UP PARAMETER
*---------------------------------------------------------------------*
* R2 --> 8 CHARACTER DDNAME TO BE SEARCHED FOR                        *
*---------------------------------------------------------------------*
         L     R4,16              CVT
         L     R4,0(R4)           TCB/ASCB WORDS
         L     R4,4(R4)           CURRENT TCB
         L     R4,12(R4)          CURRENT TIOT
         LA    R4,24(,R4)         FIRST ENTRY
         SLR   R5,R5              LENGTH REGISTER
TIOTLOOP ALR   R4,R5              SHUNT TO NEXT ENTRY
         ICM   R5,1,0(R4)         GET ENTRY LENGTH
         BZ    ENDRC4             ZERO LENGTH, DDNAME NOT FOUND
         CLC   4(8,R4),0(R2)      DDNAME FOUND ?
         BNE   TIOTLOOP           (NO )-TRY NEXT SLOT
         SLR   R15,R15            ELSE INDICATE FOUND
         B     EXIT
ENDRC4   LA    R15,4
EXIT     L     R13,4(,R13)
         L     R14,12(,R13)
         LM    R0,R12,20(R13)
         OI    15(R13),X'01'      RETURN INDICATOR
         BR    R14                RETURN TO CALLER


David Pritchard
MVS/XA Systems Programmer
Lucas Computing Services (UK) © Xephon 1988


