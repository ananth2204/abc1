ISPF/PDF edit recovery bug

There is a bug in ISPF/PDF which causes edit recovery to fail
to operate as it should.  Undetected data loss is possible.

When operating correctly, once the RECOVERY ON
command has been entered in an edit session, a time-out log-
off or any other abnormal termination of the edit session causes
an edit recovery dataset to be written with the name
userid.aaa0001.BACKUP, where aaa is the current ISPF/PDF
application code.  Upon logging back on to TSO, an attempt to
edit any dataset with an 'E' on a 3.4 screen is supposed to be
denied with a beep and an 'EDIT NOT ALLOWED' message.
Pressing PF1 or entering the HELP command at this point
should display a longer error message which reads 'EDIT
RECOVERY IS PENDING, BUT NOT ALLOWED FROM
THIS OPTION'.  At this point the user is supposed to go into
edit via option 2 to deal with the pending edit recovery
situation.

Because of the bug, however, ISPF/PDF allows the user to edit
a dataset from option 3.4 even though an edit recovery session
is pending.  If you edit the dataset you were editing when
interrupted, you will be looking at a back-level version of the
data which will not contain any of the changes made during the
edit session that was interrupted.  This carries the possibility of
considerable data loss, not to mention the time lost in re-keying
the changes.  After all, that is what edit recovery was designed
to prevent in the first place!

As of June 1, 1990, IBM did not yet have a correction for this
problem for ISPF/PDF release 3.1 or 3.2.  PTF UY48178 is
available to fix release 2.3.  But there is a work-around that
causes edit recovery to work correctly on the newer releases.

Ensure that member ISREDRT exists in your
userid.ISPF.ISPPROF.  If you already have an ISREDRT
member in your own ISPF.ISPPROF dataset, you are not
subject to the problem.  It is simply the existence of the
member which makes ISPF/PDF option 3.4 edit behave
correctly.  Member ISREDRT can be created by simply
copying the edit recovery member for any other application (eg
aaaEDRT) as ISREDRT.

Once you have at least one user with the required ISREDRT
member, you can simplify the process of correcting this
problem for the rest of your users by making the accompanying
EXEC, FIXEDIT, available to them.  The EXEC checks for
the existence of ISREDRT in the user's profile dataset.  If it
doesn't find it, it copies one in from TS21JJC's profile dataset.

To invoke the EXEC, simply type in TSO %FIXEDIT from
any command line in ISPF after placing it in a library in the
SYSPROC concatenation of the log-on procedure.  Replace the
name TS21JJC.ISPF.ISPPROF with the name of a profile
dataset or other PDS which contains the required ISREDRT
member.

/* REXX ----------------------------------------------------------*/
/* This exec is used to check for the existence of member ISREDRT */
/* in the user's userid.ISPF.ISPPROF data set.  If the member     */
/* does not exist it is copied in from TS21JJC.ISPF.ISPPROF.      */
/* The presence of the ISREDRT member causes pending edit         */
/* recovery sessions to be recognized properly by 3.4 edits       */
/* in ISPF/PDF Release 3.1 and 3.2.                               */
/*----------------------------------------------------------------*/
Address ISPEXEC
"VGET ZUSER"
profdsn = zuser".ISPF.ISPPROF(ISREDRT)"
proflib = zuser".ISPF.ISPPROF"
qprofdsn = "'"profdsn"'"
qproflib = "'"proflib"'"
Stat = SYSDSN(qprofdsn)
If Stat = "OK" Then
     Say "ISREDRT already exists in ISPF.ISPPROF. No action needed."
Else Do
   "LMINIT DATAID(DATAIN) DATASET('TS21JJC.ISPF.ISPPROF') ENQ(SHR)"
   If rc <> 0 Then
     Do
       Say "Error from LMINIT on TS21JJC.ISPF.ISPPROF"
       Select
         When rc = 8
           Then Do
                Say "Data set or file not allocated, or"
                Say "DDname not found, or"
                Say "Data set or file organization not supported."
           End
         When rc = 12
           Then Say "Invalid parameter value."
         When rc = 16
           Then Do
                Say "Truncation or translation error"
                Say "in accessing dialog variables."
           End
         When rc = 20
           Then Say "Severe error."
       End
       Exit
     End
   "LMINIT DATAID(DATAOUT) DATASET("qproflib") ENQ(SHR)"
   If rc <> 0 Then
     Do
       Say "Error from LMINIT on" profdsn
       Select
         When rc = 8
           Then Do
                Say "Data set or file not allocated, or"
                Say "DDname not found, or"
                Say "Data set or file organization not supported."
           End
         When rc = 12
           Then Say "Invalid parameter value."
         When rc = 16
           Then Do
                Say "Truncation or translation error"
                Say "in accessing dialog variables."
           End
         When rc = 20
           Then Say "Severe error."
       End
       Exit
     End
   "LMCOPY FROMID("datain") FROMMEM(ISREDRT)" ,
                    "TODATAID("dataout") TOMEM(ISREDRT)"
   If rc <> 0 Then
     Do
       Say "Error from LMCOPY. RC =" rc
       Select
         When rc = 4
           Then Do
                Say "Member not available."
           End
         When rc = 8
           Then Say "FROM or TO member not found."
         When rc = 10
           Then Say "No data set associated with the data-id."
         When rc = 12
           Then Do
                Say "Invalid data set organization, or"
                Say "Data set attribute invalid for packed data."
           End
         When rc = 16
           Then Do
                Say "Truncation error."
           End
         When rc = 20
           Then Say "Severe error."
       End
     End
   Else Say "ISREDRT successfully copied. Edit recovery is now fixed."
End
"LMFREE  DATAID("datain")"
"LMFREE  DATAID("dataout")"
Exit


John Cornman
Senior Systems Programmer
Key Services Corporation (USA)                                   c Xephon 1990

