Calling PL/I programs under TSO

It might be desirable to call a PL/I program under TSO like a TSO command, ie by
entering only the program name rather than using the CALL command where the
library name must be known.  If it is called like a TSO command then it is not
possible to pass a parameter string to the PL/I program and a message is
displayed at screen:

     IBM003 ERRONEOUS PARM OPTION HAS BEEN IGNORED

To avoid this message and to allow parameters to be passed to the PL/I main
program, the following routine, PLICMD, may be linked to the program, with
PLICMD defined as the entry point instead of PLISTART.  If this is done,
parameters will be passed to the program in the same way as if the TSO call
command were used or called by a batch job.  User parameters must start with a
slash.  Using PLICMD, this leading slash may be omitted.  The parameter MYPARM
will be passed to MYPGM in the same way in each of the following three cases:

// EXEC PGM=MYPGM,PARM='/MYPARM'
CALL 'MYLIB(MYPGM)' '/MYPARM'
MYPGM MYPARM

THE PROGRAM

* PASS TO PL/I MAIN PROGRAM, CALLED LIKE A TSO COMMAND,
* THE PARAMETER STRING SUITABLE TO PL/I:
* SLASH IN FRONT, LENGTH <= 100, COMMAND NAME AND OFFSET FIELD REMOVED.
* PASSED TO PL/I PROGRAM CALLED LIKE A TSO COMMAND:
*  <TOTAL LENGTH><OFFSET TO PARAMETER><COMMAND NAME><PARAMETER>
*  <-----2------><---------2---------#<------M-----><----N----> : BYTES
* SUITABLE TO PL/I PROGRAM:
*  <LENGTH OF PARAMETER><PARAMETER>   lST CHAR OF PARAMETER IS '/'
*  <---------2---------><----N----> : BYTES  (0<=N<=l00)
* TELL LINKAGE EDITOR:     INCLUDE PLICMD
*                          ENTRY PLICMD       INSTEAD OF PLISTART
PLICMD   CSECT                          SERIALLY REUSABLE
         USING PLICMD,R15               BASE
         TM    0(Rl),X'80'              ONLY ONE PARAMETER ?
         BO    CALL                     YES, BATCH OR TSO CALL CMD
         STM   R2,R4,28(R13)            SAVE CALLER'S REGISTERS
         L     R2,0(Rl)                 ADDR. OF LENGTH FIELD
         LH    R4,0(R2)                 TOTAL LENGTH OF CMD BUFFER
         LH    R3,2(R2)                 OFFSET TO PARAMETERS OF TSO CMD
         LA    R3,4(R3)                 + LENGTH OF CMD BUFFER PREFIX
         SLR   R4,R3                    LENGTH OF PARAMETER STRING
         LA    R2,2(R2)                 ADDR. OF OFFSET FIELD
         AH    R2,0(R2)                 + OFFSET = ADDR. OF NEW LENGTH
         CLI   2(R2),C'/'               SLASH ALREADY PRESENT ?
         BE    SSET                      YES
         BCTR  R2,0                     DECR. ADDR. OF NEW LENGTH FIELD
         LA    R4,1(R4)                 INCREMENT LENGTH
         MVI   2(R2),C'/'               SLASH IN FRONT OF PARAM. STRING
SSET     LA    R3,100                   MAX. LENGTH ALLOWED
         CLR   R4,R3                    PARAMETER STRING TOO LONG ?
         BNH   LOK                       NO
         LR    R4,R3                   SET TO ALLOWED LENGTH
LOK      STH   R4,0(R2)                NEW LENGTH FIELD
         ST    R2,0(Rl)                NEW ADDR. OF LENGTH FIELD
         OI    0(Rl),X'80'             INDICATE: ONE PARAMETER
         LM    R2,R4,28(R13)           LOAD CALLER'S REGISTERS
CALL     L     R15,=V(PLISTART)        ADDR. OF PL/I MAIN ENTRY
         BR    R15                     BRANCH TO PL/I MAIN PROGRAM
         LTORG
         END   PLICMD


Walter Wiedemann
Consultant (West Germany)     © Walter Wiedemann 1987
































































