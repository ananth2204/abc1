Detecting looping batch programs

In order to resolve a problem of programs in a loop, we have
implemented the SMF exit IEFUTL (CPU time limit exceeded).
The exit analyses the current I/O count (field ASCBIOSC in the
ASCB). This figure is compared with a field kept in the Common
Exit Parameter containing the previously-recorded I/O count. If
the two quantities are equal the program is using CPU without
performing any I/O and the exit sends out a message.

* THIS PROGRAM IS AN SMF EXIT
* INTERCEPTS JOB'S CPU TIME EXCEEDED, VERIFIES CHANGES ON I/O COUNT
* SENDS A MESSAGE IF NOT CHANGED.
* SEE MVS/ESA-SPL INSTALLATION EXITS (GC28-1836) FOR MORE INFORMATION
IEFUTL  CSECT
        STM   14,12,12(13)           SAVE REGISTERS
        LR    3,15                   SET BASE REGISTER
        USING IEFUTL,3               USING
        LR    4,0                    SAVE REASON INVOKED
        L     7,0(1)                 LOAD COMMON EXIT PARM ON R7
        LA    15,8                   SET RC=JOB CONTINUE W/EXTENSION
        C     4,=F'8'                IS REASON "JOB WAIT"?
        BE    ENDPROG                YES. GO TO RETURN
        L     6,16                   LOAD CVT ADDRESS ON R6
        L     6,0(6)                 LOAD NEXT TCB TO BE DISPATCHED
        L     4,12(,6)               LOAD ASCB FROM TCB
        USING ASCB,4                 SET ADDRESSABILITY
        L     6,ASCBIOSC             LOAD CURRENT I/O COUNT
        L     8,32(7)                LOAD OLD I/O COUNT FROM PARM
        ST    6,32(7)                SAVE CURRENT I/O COUNT ON PARM
        CR    6,8                    COMPARE OLD AND CURRENT I/O COUNT
        BNE   ENDPROG                NOT EQUAL. GO TO RETURN
*  SET ACTION FOR LOOP. FOR CANCEL ERASE '*' ON COL 1
*       LA    15,0                   SET RC=CANCEL JOB
        WTO 'TIME EXPIRED FOR THIS JOB, WITHOUT I/O'
ENDPROG EQU   *
        L     14,12(13)              LOAD OLD REGISTERS
        LM    0,12,20(13)            LOAD OLD REGISTERS
        LA    1,30                   SET TIME EXTENSION IN SECONDS
        BR    14                     RETURN TO CALLER
*  MAP OF ASCB
        IHAASCB
        END


Nestor Lacabe
Systems Programmer
Sevel Argentina SA (Argentina)                                           c Xephon 1994

