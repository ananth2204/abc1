Some useful ISPF edit macros
I have been playing with ISPF edit macros to see how useful they can be.  These
are TSO CLISTs executed with ISPF edit.  They can do virtually anything a TSO
CLIST can do, like allocating and reading datasets, executing TSO commands, as
well as manipulating the data with the file currently being edited.  Please note
that these are initial dabbles and, although they should work without any
problems, they may not have been written as elegantly as possible.
BZINIT
This is a macro that is set into the profile for source datasets with the edit
command ‘PROF IMACRO(BZINIT)’ before locking the profile with the ‘PROF LOCK’
command.  If nothing else it puts the edit into ‘CAPS ON’, even if there is
lower case data.  It also forces other defaults, which might not otherwise be
set by the system.
/*  BZINIT: EDIT INITIAL MACRO */
ISREDIT MACRO
CONTROL END(DOEN)
ISREDIT (MEM) = MEMBER /* GET MEMBER NAME */
ISREDIT (CP) = CAPS /* SAVE CURRENT "CAPS" MODE */
ISREDIT CAPS ON
ISREDIT NUM ON STD
/*?? ISREDIT NULLS ON ALL /**/
/*?? ISREDIT TABS ON /**/
IF &STR(&MEM) NE &STR() THEN ISREDIT STATS ON
IF &CP = OFF THEN DO
  SET &ZEDSMSG = &STR(LOWER-CASE EXIST)
  SET &ZEDLMSG = &STR(THERE IS LOWER-CASE TEXT WITHIN THE FILE)
  ISPEXEC SETMSG MSG(ISRZ001)
  DOEN
EXIT CODE(0)
BZREPT
Here is an example that will repeat every unexcluded line.  Simply exclude all
lines to be left alone, then on the command line enter BZREPT.  All copied lines
will be prefixed with a ‘>’ to distinguish them from the original lines.
/*  BZREPT: "REPEAT" COMMAND MACRO */
ISREDIT MACRO
ISREDIT CURSOR = 1  /* TOP OF DATA-SET */
ISREDIT (STATUS) = USER_STATE /* SAVE USER STATUS */
ISREDIT BOUNDS                /* RESET DEFAULT BOUNDS */
SET &SEEK = 0
DO WHILE &SEEK = 0
  ISREDIT SEEK P'=' NX 1 NEXT /* LOCATE FIRST "NOT-EXCLUDED" LINE */
  SET &SEEK = &LASTCC
  IF &SEEK = 0 THEN DO
    ISREDIT (LINE) = CURSOR
    ISREDIT LINE_AFTER &LINE = LINE   /* COPY LINE */
    SET &LINE = &LINE+1
    ISREDIT CURSOR = &LINE
    ISREDIT SHIFT ) &LINE 1           /* SHIFT LINE */
    ISREDIT LINE &LINE = LINE+'>'     /* PREFIX LINE */
    END
  END
ISREDIT USER_STATE = (STATUS) /* RE-SET USER STATUS */
  SET &ZEDSMSG = &STR("BZREPT" COMPLETE)
  SET &ZEDLMSG = &STR(ALL "NOT-EXCLUDED" LINES HAVE BEEN REPEATED)
  ISPEXEC SETMSG MSG(ISRZ001)
EXIT CODE(0)
BZSPARE
Here is an example that will comment out every unexcluded line.  Simply exclude
all lines to be left alone, then on the command line enter BZSPARE.  All
unexcluded lines will have the name field moved into the comment ‘*** SPARE ***
WAS “nnnnnnnn”’.
One problem that I had was that ‘&’s in the name field kept being resolved until
I used the ‘&NRSTR’ CLIST function of TSO/E.
/*  BZSPARE: "SPARE" COMMAND MACRO */
ISREDIT MACRO
/* CONTROL SYMLIST,CONLIST /**/
ISREDIT (STATUS) = USER_STATE  /* SAVE USER STATUS */
ISREDIT (MASK) = MASKLINE
SET &MASK = &SUBSTR(67:71,&MASK)
ISREDIT CURSOR = 1  /* TOP OF DATASET */
SET &SEEK = 0
ISREDIT (SCANMODE) = SCAN         /* SAVE "SCAN_MODE = OFF" */
DO WHILE &SEEK = 0
  ISREDIT SEEK P'^' NX 1 NEXT /* LOCATE FIRST "NX" LINE WITH NAME*/
  SET &SEEK = &LASTCC
  IF &SEEK = 0 THEN DO
    ISREDIT (LINE) = CURSOR
    ISREDIT SCAN = OFF          /* SET "SCAN_MODE = OFF" */
    ISREDIT (DATA) = LINE &LINE /* GET DATA ON THAT LINE */
    SET &L = 8
    SET &SPACE = &STR( )
    DO WHILE (&SUBSTR(&L:&L,&NRSTR(&DATA)) EQ &STR( ))
      SET &L = &L-1
      SET &X = &SUBSTR(&L:&L,&NRSTR(&DATA))
      END
/*  IF &L LE 0 THEN GOTO BZNONAME /* SHOULD NEVER HAPPEN! */
    SET &NAME = &SUBSTR(1:&L,&NRSTR(&DATA))
    IF &SUBSTR(1:1,&NRSTR(&NAME)) EQ &STR(*) THEN GOTO BZNONAME
    IF &SUBSTR(1:1,&NRSTR(&NAME)) EQ &STR( ) THEN GOTO BZNONAME
    IF &SUBSTR(1:2,&NRSTR(&NAME)  ) EQ &STR(.*) THEN GOTO BZNONAME
    IF &SUBSTR(10:13,&NRSTR(&DATA)) EQ &STR(EQU) +
      THEN DO
        SET &STAR = &STR(*       )
        SET &SPARE = &STR(FREE )
      END
      ELSE DO
        SET &STAR = &STR(        )
        SET &SPARE = &STR(SPARE)
      END
    ISREDIT LINE &LINE = LINE+<1,'&STAR' +
      31,'*** &SPARE *** WAS "&NRSTR(&NAME)"             ' +
      67,'&MASK'>
BZNONAME: +
    END
  END
ISREDIT SCAN = &SCANMODE    /* RESET "SCAN_MODE" */
ISREDIT USER_STATE = (STATUS) /* RE-SET USER STATUS */
  SET &ZEDSMSG = &STR("BZSPARE" COMPLETE)
  SET &ZEDLMSG = &STR(ALL "NOT-EXCLUDED" LINES HAVE BEEN "SPARE"D)
  ISPEXEC SETMSG MSG(ISRZ001)
EXIT CODE(0)


Martin Hamilton
Technical Consultant
Thomson Holidays (UK)    © Thomson Holidays 1989
















