A split/join REXX EXEC

/* ---------- Rexx -------------- */
/* This Rexx program splits a line  */
/* or joins two lines               */
/* If right of cursor is blank then */
/* it joins the next line           */
/* If right of cursor is data then  */
/* it splits the line to two        */
/* Use with a PF Key                */
/* place cursor on line & press pfk */
/* -------------------------------- */
trace o
"ISREDIT MACRO"
"ISREDIT (ROW,COL) = CURSOR"
if col=0 then do
   zedsmsg='Incorrect Cursor po'
   zedlmsg='* Error ****  Cursor is not on a data line'
   'ISPEXEC SETMSG MSG(ISRZ001)'
   exit
   end
"ISREDIT (data) = LINE" row
if substr(data,col)^='' then call split_line
 else call join_line
exit
/* --------------------- Start Of subroutines ------------ */
split_line:
 linedata=substr(data,1,col-1)
"ISREDIT LINE  " row "= (LINEDATA)"
 if wordindex(data,1)>1 then linedata=copies(' ',wordindex(data,1)-1)||substr(data,col)
  else linedata=substr(data,col)
"ISREDIT LINE_AFTER" row "= (LINEDATA)"
"ISREDIT CURSOR =" row col
return
join_line:
"ISREDIT RESET SPECIAL"
currline = data
"ISREDIT (nextline) = LINE" row+1
if nextline='' then exit
nextline=strip(nextline,'B',' ')
newdata=substr(currline,1,col-1)||nextline
if length(newdata)>length(currline) then do
   newdata=substr(newdata,1,length(currline))
   msg="'Data Truncated'"
   'ISREDIT LINE_AFTER' row-1 ' = MSGLINE' msg
   end
"ISREDIT LINE "row "= (newdata)"
"ISREDIT DELETE " row+1
"ISREDIT CURSOR =" row col
return


Moyeen Ahmed Khan
Senior Systems Designer
Manulife Financial (Canada)                                                     c Xephon 1995

