Identifying members in file concatenations
TSO/ISPF allows, through concatenation, a great deal of flexibility as to which
libraries are used.  This flexibility has, however, the disadvantage that the
system overview rapidly gets lost and one does not know from which libraries
members are being obtained.  The FINDM program can be used to obtain for a
specified file (DDNAME) a display of which library (DSNAME) contains a specified
member.
The initialisation and termination routines have been omitted from the program,
as most installations have their own macros for such tasks.
SAMPLE CLIST
PROC 2 MEMBER DDN
/* Function - display the name of the library of the associated */
/*            file which contains the specified member          */
/* Parameter-1 - membername                                     */
/*          -2 - filename (DD-name)                             */
SET &member = &SUBSTR(1:8,&member.&STR(        )) /* membername
SET &ddn = &SUBSTR(1:8,&ddn.&STR(        ))       /* filename
SET &parm = &STR(&member)&STR(&ddn)
CALL 'libraryname(findm)' '&parm'
SET &rc = &LASTCC
IF &rc NE 0 THEN WRITE RC=&rc
PROGRAM SOURCE CODE
***
* EXEC-PARAMETER:
*  01-08  MEMBER-NAME
*  09-16  DD-NAME
* RETURN-CODES:
*  0  OK
*  4  MEMBER NOT FOUND
*  8  DD-NAME NOT FOUND
* 20  PARM ERROR
***
FINDM    ...                         program initialisation
         L     R2,0(R1)              A(PARAMETER)
         USING PMDSECT,R2
         LA    R15,20
         LH    R0,PMEXLEN
         CH    R0,=Y(PMLEN)
         BNE   EOJ                   LENGTH ERROR
         LA    R8,NULLDCB
         USING IHADCB,R8
         MVC   DCBDDNAM,PMDDNAME
         OPEN  (NULLDCB,(INPUT))
         LA    R15,8              RETURN-CODE (IF OPEN ERROR)
         TM    DCBOFLGS,X'10'     DCB-OPEN?
         BZ    EOJ                OPEN ERROR
* GET TIOT-ADDRESS
         DMJOBNAM (R9)
* R9: A(TIOT)
         AH    R9,DCBTIOT         +OFFSET IN TIOT
         USING TIODSECT,R9
* FIND MEMBER
         MVC   BLDLNAME,PMMEMBER
         BLDL  NULLDCB,BLDL
         LTR   R15,R15            BLDL-ERROR?
         LA    R15,4              RETURN-CODE (IF BLDL ERROR)
         BNZ   EOJ                ERROR, MEMBER NOT FOUND
         SR    R1,R1              BYTES 0-2 LOESCHEN
         SR    R0,R0
         IC    R0,BLDLK           CONCATENTION NO.
         LTR   R0,R0
         BZ    A120               ENTRY FOUND
* NEXT ENTRY
A110     IC    R1,TIOELNGH        L(ENTRY)
         LA    R9,0(R1,R9)        A(NEXT ENTRY)
         BCT   R0,A110
A120     SR    R5,R5
         ICM   R5,B'0111',TIOEJFCB A(JFCB)
         USING JFCDSECT,R5
         LA    R1,JFCBDSNM
         TPUT  0(R1),44
         LA    R15,0              RETURN-CODE
EOJ      ...                      program termination
NULLDCB  DCB   DSORG=PO,MACRF=R
BLDL     DS    0H
         DC    H'1'               1 ENTRY
         DC    H'12'              ENTRY LENGTH
BLDLNAME DC    CL8' '             MEMBER-NAME
         DC    XL3'0'             TTR-ADDRESS
BLDLK    DC    X'0'               CONCATENTATION-NO.
         DC    XL2'0'
         LTORG
PMDSECT  DSECT
PMEXLEN  DS    HL2                LENGTH
PMMEMBER DS    CL8                MEMBER
PMDDNAME DS    CL8                DD-NAME
PMLEN    EQU   *-PMMEMBER
TIODSECT DSECT
TIOELNGH DS    AL1                ENTRY-LENGTH
         DS    XL3
TIOEDDNM DS    CL8                DD-NAME
TIOEJFCB DS    AL3                A(JFCB)
JFCDSECT DSECT
         DS    XL16               PREFIX=6
         IEFJFCBN
         DCBD  DSORG=PO,DEVD=DA
         END


A Rudd
Technical Specialist (West Germany)     © A Rudd 1988































