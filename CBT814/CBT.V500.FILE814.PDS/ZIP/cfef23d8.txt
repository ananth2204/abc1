REXX user-written functions

IRXFLOC  CSECT
SYSID    EQU   *
         DC    CL8'IRXFPACK'      Directory String
         DC    FL4'24'            header length
         DC    FL4'1'             No of rows in directory
         DC    FL4'0'             Zeros
         DC    FL4'32'            Length of directory
*------------------------------------------------------*
*   Definition of First function starts here           *
*------------------------------------------------------*
         DC    CL8'SYSID'         Function name
         DC    FL4'0'             Addr. of preloaded code
         DC    FL4'0'             RESERVED
         DC    CL8'RXSYSID'       Entry Point
         DC    CL8''              DDNAME from where to load

RXSYSID  CSECT
RXSYSID  AMODE 31
RXSYSID  RMODE ANY
*------------------------------------------------------*
*   Housekeeping                                       *
*------------------------------------------------------*
         BAKR  14,0               Save regs, ARs etc
         LAE   12,0(15,0)         Base register
         USING RXSYSID,R12
         LR    R10,R1             Save parmlist address
         SPACE
         STORAGE OBTAIN,LENGTH=WORKL
         LR    R11,R1
         SPACE
         LAE   R13,SAVEAREA       save area addr in AR/R13
         MVC   4(4,R13),=C'F1SA'  LS save area indicator
*------------------------------------------------------*
*   Get the SYSID                                      *
*------------------------------------------------------*
         L     R3,CVTPTR              Get
         L     R3,CVTSMCA-CVT(,R3)         the
         MVC   XSYSID(4),SMCASID-SMCBASE(R3)    SYSID.
*------------------------------------------------------*
*   Set up the Evaluation block to return the function *
*   values to REXX                                     *
*------------------------------------------------------*
         USING EFPL,R10
         L     R5,EFPLEVAL
         L     R5,0(,R5)
         USING EVALBLOCK,R5
         MVC   EVALBLOCK_EVLEN,=X'00000004' MOVE LEN OF DATA
         MVC   EVALBLOCK_EVDATA(4),XSYSID   DATA TO EVALBLK
         SPACE
*------------------------------------------------------*
*   Free storage and return                            *
*------------------------------------------------------*
RETURN   EQU   *
         STORAGE RELEASE,ADDR=(11),LENGTH=WORKL
         SLR   R15,R15                      RC=0
         PR                                 Return
         EJECT
WORK     DSECT
SAVEAREA DS    18F
XSYSID   DS    CL4
WORKL    EQU   *-WORK
         CVT   DSECT=YES                    CVT
         IEESMCA                            SMC
         IRXEVALB                           REXX Eval block
         IRXEFPL                           Ext. func parmlst
         YREGS                              Regs
         END

/**********************************************************/
/* REXX exec to return SMF sysid                          */
/**********************************************************/
x=sysid()           /* call sysid function without parms */
say x
exit 0


Paul Poolsaar
Royal Bank of Canada (Canada)                                      c Xephon 1995

