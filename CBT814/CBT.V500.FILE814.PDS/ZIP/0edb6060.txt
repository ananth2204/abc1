An ISPF-TSO/E TRANSMIT interface
The Interactive Data Transfer Facility commands TRANSMIT and RECEIVE, while
being very useful, are not particularly easy to use, especially for those who
use ISPF most of the time.  Unfortunately IBM have made the RECEIVE command
prompt for input rather than accepting restore parameters as operands, thus
making it very difficult to provide a friendly interface to it.  The TRANSMIT
command can, however, be front-ended with a full-screen panel, as the following
code demonstrates.
CLIST (XP)
This can be selected from an ISPF menu or as a command.
PROC 0
/*****************************************************************/
/* XP :  XMIT ASSIST CLIST - DISPLAYS PANEL FOR XMIT OPERANDS    */
/* TO INVOKE FROM SELECTION MENU CODE 'X','CMD(%XP)' FOR EXAMPLE */
/*****************************************************************/
CONTROL MAIN NOFLUSH
SET &XMEM1 = ALL
SET &XMSG  = N
SET &XDSN  = Y
ISPEXEC DISPLAY PANEL(XMITP1)
SET &RET = &LASTCC
DO WHILE &RET = 0
  /* FORMAT XMIT COMMAND */
  IF &XNIC = THEN +
     SET &XDEST = &STR(&XSYS..&XUID)
  ELSE +
     SET &XDEST = &XNIC
  IF &XMSG = Y THEN +
     IF &XDSN = Y THEN +
        SET &XMESSAGE = MSG
     ELSE +
        SET &XMESSAGE =
  ELSE +
     SET &XMESSAGE =
  IF &XDSN = Y THEN +
     DO
        IF &XODS = THEN +
           SET &XDATASET = &STR(DSN('&XPRO..&XGRP..&XTYP'))
        ELSE +
           SET &XDATASET = &STR(DSN(&XODS))
        IF &XMEM1 ^= ALL THEN +
           SET &XMEMBER = &STR(MEM(&XMEM1 &XMEM2 &XMEM3 &XMEM4 &XMEM5 +
                                   &XMEM6 &XMEM7 &XMEM8 &XMEM9 &XMEM10))
        ELSE +
           SET &XMEMBER =
     END
  ELSE +
     DO
        SET &XDATASET =
        SET &XMEMBER  =
     END
  /* ISSUE XMIT COMMAND */
  TSOEXEC XMIT &XDEST &XDATASET &XMEMBER &XMESSAGE &XOTH
  ISPEXEC CONTROL DISPLAY REFRESH
  ISPEXEC DISPLAY PANEL(XMITP1)
  SET &RET = &LASTCC
END
EXIT
PANEL (XMITP1)
Displayed by the CLIST XP.
%------------------------%TRANSMIT ASSIST PANEL%-----------------------+
%COMMAND ===>_ZCMD
%DESTINATION+ SYSTEM%===>_XSYS    +(ABCNODE, XYZNODE)
%           + USERID%===>_XUID    +(TSO userid on that system)
%    OR +   NICKNAME%===>_XNIC    +(predefined dest tag name)
+
+XMIT MESSAGE%===>_Z+  (Y or N, message data will be prompted for)
+XMIT DATASET%===>_Z+  (Y or N, enter dataset details below)
+
+DATASET+DETAILS:
+   PROJECT  %===> XPRO     +      | PDS or SEQ datasets only |
+   GROUP    %===> XGRP     +      | can be transmitted, if a |
+   TYPE     %===> XTYP     +      | PDS then also complete   |
+                                  | the member list below.   |
+   OTHER DATASET %===>_XODS                                       +
+
+MEMBER+LIST:  (enter ALL in first field for every member or SEQ files)
%===>_XMEM1   %===>_XMEM2   %===>_XMEM3   %===>_XMEM4   %===>_XMEM5   +
%===>_XMEM6   %===>_XMEM7   %===>_XMEM8   %===>_XMEM9   %===>_XMEM10  +
+
+OTHER XMIT OPTIONS%===>_XOTH                        +(eg NONOTIFY etc)
+
+Press %ENTER+ to execute XMIT command or %END+ to exit
)INIT
  .ZVARS = '(XMSG XDSN)'
  IF (&XSYS = ' ')
     &XSYS = ABCNODE
  IF (&XUID = ' ')
     &XUID = &ZUSER
  IF (&XMEM1 = ' ')
     &XMEM1 = ALL
  IF (&XOTH = ' ')
     &XOTH = NOTIFY
)PROC
  IF (&XNIC = ' ')
     VER (&XSYS,NB,LIST,ABCNODE,XYZNODE)
     VER (&XUID,NB,NAME)
  VER (&XNIC,NAME)
  IF (&XMSG = N)
     IF (&XDSN = N)
        &XMSG = ?
        &XDSN = ?
  VER (&XMSG,NB,LIST,Y,N)
  VER (&XDSN,NB,LIST,Y,N)
  IF (&XDSN = Y)
     IF (&XODS = ' ')
        VER (&XPRO,NB,NAME)
        VER (&XGRP,NB,NAME)
        VER (&XTYP,NB,NAME)
     VER (&XODS,DSNAME)
     VER (&XMEM1,NB,NAME)
     VER (&XMEM2,NAME)
     VER (&XMEM3,NAME)
     VER (&XMEM4,NAME)
     VER (&XMEM5,NAME)
     VER (&XMEM6,NAME)
     VER (&XMEM7,NAME)
     VER (&XMEM8,NAME)
     VER (&XMEM9,NAME)
     VER (&XMEM10,NAME)
  VPUT (XSYS XUID XNIC XMSG XDSN XPRO XGRP XTYP XOTH) PROFILE
)END


     © Xephon 1988







