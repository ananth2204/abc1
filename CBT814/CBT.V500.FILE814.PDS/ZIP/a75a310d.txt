Setting the MVS region size above the line

The IEFUSI exit is used to set the maximum region size above
the 16-megabyte line. The default module supplied with MVS
limits this to 32 megabytes. This version of the module (which
should be in LPALIB) allows CICS to have an initial region
size of 48 megabytes and a maximum limit of 56 megabytes.

See the MVS Customization manual for full information on this
exit.

*   MODULE : IEFUSI
*     DESC : SETS REGION SIZE LIMITS
             SETS ABOVE THE LINE MAX REGION TO 48MB AND LIMIT TO
             56 MEG FOR CICS ONLY. OTHERWISE MAX IS 32MB.
REGION   DSECT
         USING REGION,R7
REGION_FLAG_WORD   DS F
REGION_JCL_REGION  DS F
REGION_LIMIT_BELOW DS F
REGION_SIZE_BELOW  DS F
REGION_LIMIT_ABOVE DS F
REGION_SIZE_ABOVE  DS F
PROGRAM  DSECT
         USING PROGRAM,R5
PROGRAM_NAME DS CL8
FLAGWORD DSECT
         USING FLAGWORD,R8
VRFLAG   DS    F
IEFUSI   CSECT
         USING IEFUSI,R15
         B     START
         DC    C'VERSION OF IEFUSI &SYSDATE &SYSTIME'
START    DS    0H
         STM   R14,R12,12(R13)               SAVE REGS
         LR    R12,R15                       NEW BASE REG
         DROP  R15
         USING IEFUSI,R12
         LM    R3,R8,0(R1)                   ADDRESS PARMS
         OI    REGION_FLAG_WORD,X'80'        TELL VSM TO USE MY VALUES
         TM    VRFLAG,X'80'                  V=R REGION ?
         BO    ENDPROG                       YES
         L     R10,REGION_JCL_REGION         JCL REGION SIZE
         LTR   R10,R10                       ZERO (IE MAX) REQUESTED
         BZ    ENDPROG                       YES, NO ACTION
         AL    R10,F64K                      ADD 64K
         ST    R10,REGION_LIMIT_BELOW        SET LIMIT BELOW LINE
         MVC   REGION_SIZE_BELOW,REGION_JCL_REGION SIZE IS AS REQUESTED
         MVC   REGION_LIMIT_ABOVE,F32M       DEFAULT LIMIT IS 32MB
         MVC   REGION_SIZE_ABOVE,F32M        DEFAULT SIZE IS 32MB
         CLC   PROGRAM_NAME,CICS             IS IT CICS PROGRAM?
         BNE   ENDPROG                       NO, LEAVE AS IS
         MVC   REGION_LIMIT_ABOVE,F56M       LIMIT IS 56MB FOR CICS
         MVC   REGION_SIZE_ABOVE,F48M        SIZE IS 48MB
ENDPROG  DS    0H
         LM    R14,R12,12(R13)               RESTORE REGS
         XR    R15,R15                       ZERO R15
         BR    R14                           RETURN TO CALLER
F64K     DC    A(64*1024)                    64K
F32M     DC    A(32*1024*1024)               32MB
F48M     DC    A(48*1024*1024)               48MB
F56M     DC    A(56*1024*1024)               56MB
CICS     DC    CL8'DFHSIP'                   CICS PROGRAM NAME
         LTORG
         END


David E Ellis
Senior Systems Programmer (Saudi Arabia)                            c Xephon 1993

