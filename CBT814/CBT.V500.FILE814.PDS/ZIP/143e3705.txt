Dataset checking utility


VOLCHECK CSECT
         USING VOLCHECK,R15
         B     A_START
         DC    CL8'VOLCHECK'
         DC    CL8'&SYSDATE'
A_START  DS    0H
         BAKR  R14,0
         LR    R12,R15
         USING VOLCHECK,R12
         DROP  R15
         LA    R13,SAVE_AREA
         OPEN  (SYSPRINT,(OUTPUT))
         USING IHADCB,R10
         LA    R10,SYSPRINT
         TM    DCBOFLGS,DCBOFOPN                open ok ?
         BZ    EXIT
         RDJFCB VTOC                            read jfcb
         LTR   R15,R15
         BNZ   EXIT
         LA    R10,VTOC_JFCB
         MVC   JFCBDSNM-INFMJFCB(44,R10),VTOC_NAME
         OPEN  VTOC,TYPE=J
         LTR   R15,R15
         BNZ   EXIT
         MVC   VOLUME(6),JFCBVOLS-INFMJFCB(R10)
         BAL   R3,PUT_SYSPRINT
         MVC   2(L'HEADING1,R1),HEADING1
         MVC   21(6,R1),VOLUME
         BAL   R3,PUT_SYSPRINT
         MVC   2(L'HEADING2A,R1),HEADING2A
         MVC   47(L'HEADING2B,R1),HEADING2B
         LA    R11,DSCB
READ_LOOP EQU *
         READ  VTOC_ECB,SF,VTOC,(R11)           read vtoc
         CHECK VTOC_ECB
         CLI   DS1FMTID-IECSDSL1(R11),C'1'      format 1 ?
         BNE   READ_LOOP                        read next
         BAL   R3,PUT_SYSPRINT
         MVC   2(44,R1),DS1DSNAM-IECSDSL1(R11)  dataset name
         LR    R4,R1
         MVC   DSN(44),DS1DSNAM-IECSDSL1(R11)
         LOCATE CAMLSTN                         search catalog
         LTR   R15,R15
         BNZ   NOT_CATALOGED
         MVC   47(6,R4),INFO+6                  volser in catalog
         CLC   INFO+6(6),VOLUME                 our volume ?
         BE    READ_LOOP
         MVI   46(R4),C'*'                      flag it
         B     READ_LOOP
NOT_CATALOGED EQU *
         MVC   46(L'NOT_IN_CATALOG,R4),NOT_IN_CATALOG
         B     READ_LOOP
VTOC_EOF CLOSE (SYSPRINT,,VTOC)
EXIT     PR                                     return to mvs
PUT_SYSPRINT EQU *                              subroutine
         PUT   SYSPRINT
         MVI   0(R1),C' '
         MVC   1(120,R1),0(R1)
         BR    R3                               return to caller
SAVE_AREA      DC 18F'0'
DSN            DC CL44' '
INFO           DS 0D
               DS 265C
VOLUME         DC CL6' '
NOT_IN_CATALOG DC C'*dataset not in catalog'
HEADING1       DC C'Datasets on volume xxxxxx'
HEADING2A      DC C'---------- dataset name ----------'
HEADING2B      DC C'catalog entry'
DSCB           DC CL140' '
VTOC_NAME      DC 44X'04'
               DS 0F
VTOC_EXIT_LIST DC X'87'
               DC AL3(VTOC_JFCB)
               DS 0F
VTOC_JFCB      DC XL176'00'
               LTORG
CAMLSTN  CAMLST NAME,DSN,,INFO
SYSPRINT DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=PL,LRECL=121,BLKSIZE=0
VTOC     DCB   DDNAME=VTOC,DSORG=PS,MACRF=R,LRECL=96,BLKSIZE=96,       X
               RECFM=F,KEYLEN=44,EXLST=VTOC_EXIT_LIST,EODAD=VTOC_EOF
         DCBD  DSORG=PS,DEVD=DA
         DSECT
         IEFJFCBN
         DSECT
         IECSDSL1 (1)
         @REGS
         END


David Welch (New Zealand)           c Xephon 1996

