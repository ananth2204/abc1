SMS and extended reason codes


Program SOURCE CODE

DYNART   CSECT
DYNART   RMODE ANY
DYNART   AMODE 31
         SAVE  (14,12)                    save registers
         LR    12,15
         USING DYNART,12
         ST    13,SAVEAREA+4
         LA    13,SAVEAREA
*---------------------------------------------------------------------*
* Initialize RB (request block) for Dynamic allocation                *
*---------------------------------------------------------------------*
         LA    10,RB                      get address of RB storage
         USING S99RB,10
         XC    RB,RB                      init RB
         MVI   S99RBLN,20                 length = 20 (always 20)
         MVI   S99VERB,S99VRBAL           verb = dsname allocation
         MVI   S99FLG11,S99NOCNV          do not convert existing all'n
         LA    3,TXTPTRS1                 point S99TXTPP at the list
         ST    3,S99TXTPP                    of text unit pointers
         MVC   S99S99X,=A(RBX)            point to RB Extension (RBX)
*---------------------------------------------------------------------*
* Initialize the RB Extension                                         *
*---------------------------------------------------------------------*
* This enables the DYNALLOC message service which can issue error     *
* messages using WTO in addition to returning the DYNALLOC return     *
* codes.  This makes it easier to see the problem quickly instead of  *
* having to look up DYNALLOC error codes.                             *
*---------------------------------------------------------------------*
         LA    9,RBX                      get address of RBX
         USING S99RBX,9
         XC    RBX(S99ERSN+4-S99RBX),RBX  clear RBX
         MVC   S99EID,=C'S99RBX'          identifier
         MVI   S99EVER,S99RBXVR           version of RBX
         MVI   S99EOPTS,S99EIMSG+S99EWTP  issue error msgs using WTO
         MVI   S99EMGSV,S99XWARN          issue error msgs if warning
*                                            or more severe error
*---------------------------------------------------------------------*
* Call Dynamic Allocation                                             *
*---------------------------------------------------------------------*
         LA    1,RBPTR                    set up R1 for DYNALLOC
         DYNALLOC                         call DYNALLOC service
         LTR   15,15                      okay?
         BZ    RETURN                     yes...all's well
         ICM   1,B'1100',S99ERROR         put error/info in R1
         ICM   1,B'0011',S99INFO
         L     2,S99ERSN                  put extended reason in R2
         DC H'0'                          force a dump.
RETURN   EQU   *
         L     13,SAVEAREA+4
         RETURN (14,12),RC=0
*---------------------------------------------------------------------*
* Data areas                                                          *
*---------------------------------------------------------------------*
LASTBIT  EQU   X'80000000'
RBPTR    DC    A(RB+LASTBIT)            pointer to the request block
RB       DS    CL(S99RBEND-S99RB)       request block
RBX      DS    CL(S99ERSN+4-S99RBX)     request block extension
TXTPTRS1 DC    A(TU1)                   text pointers
         DC    A(TU2)
         DC    A(TU3)
         DC    A(TU4)
         DC    A(TU5)
         DC    A(TU6)
         DC    A(TU7)
         DC    A(TU8+LASTBIT)
* TEXT UNITS
TU1      DC    AL2(DALDSNAM,1,44)
TU1DSN   DC    CL44'MYHLQ.DAN.TEST1'
TU2      DC    AL2(DALSTATS,1,1)        DISP = NEW ,
         DC    X'04'
TU3      DC    AL2(DALNDISP,1,1)            CATLG,
         DC    X'02'
TU4      DC    AL2(DALCDISP,1,1)            DELETE)
         DC    X'04'
TU5      DC    AL2(DALUNIT,1,8)         UNIT=SYSDA
         DC    CL8'SYSDA'
TU6      DC    AL2(DALCYL,0)            SPACE = (CYL,
TU7      DC    AL2(DALPRIME,1,3)           5000 )
         DC    AL3(5000)
TU8      DC    AL2(DALRTDDN,1,8)       return dd
RTDD     DC    CL8' '
SAVEAREA DS    18F
         LTORG
         IEFZB4D0
         IEFZB4D2
         END


Sample Output from Program

IKJ56893I DATA SET PS0601.DAN.TEST1 NOT ALLOCATED+
IGD17273I ALLOCATION HAS FAILED FOR ALL VOLUMES SELECTED FOR DATA SET MYHLQ.DAN.TEST1
IGD17277I THERE ARE (180) CANDIDATE VOLUMES OF WHICH (55) ARE ENABLED OR QUIESCED
IEA995I SYMPTOM DUMP OUTPUT
SYSTEM COMPLETION CODE=0C1  REASON CODE=00000001
 TIME=23.22.23  SEQ=15453  CPU=0000  ASID=00A5
 PSW AT TIME OF ERROR  078D0000   90A0A100  ILC 2  INTC 01
   ACTIVE LOAD MODULE           ADDRESS=10A0A098  OFFSET=00000068
   NAME=DYNART
   DATA AT PSW  10A0A0FA - 58209020  000058D0  C14898EC
   GPR  0-3  970C0000  970C0000  00004379  10A0A14C
   GPR  4-7  000F83C4  0004FCE8  0004C000  0007C4D0
   GPR  8-11 00000000  10A0A128  10A0A114  03CD4B88
   GPR 12-15 90A0A098  10A0A1DC  80FD8D90  00000004
 END OF SYMPTOM DUMP


Dan Little (Canada)                  C Xephon 1996

