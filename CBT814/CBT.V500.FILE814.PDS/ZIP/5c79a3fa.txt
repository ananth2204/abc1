Calculating tape capacities


CHEKBLOK SOURCE CODECHEKBLOK TITLE 'COUNT BLOCKS WRITTEN TO A TAPE/CART...'
***********************************************************************
*    MODULE:   CHEKBLOK                                               *
*    DESC:     WRITE TO 'OUTPUT' DD AND COUNT HOW MANY BLOCKS ARE     *
*              WRITTEN TO IT BEFORE IT FILLS UP (TAPES/CARTS).        *
*              CHANGE THE DCB TO REFLECT THE RECFM/BLKSIZE THAT       *
*              YOU WISH TO CHECK (AS THIS IS NOT CODED IN THE         *
*              DCB BUT IS PICKED UP FROM THE JCL THERE WILL BE        *
*              NO NEED TO RE-ASSEMBLE THIS PROGRAM TO CHANGE THE      *
*              DCB CHARACTERISTICS).                                  *
***********************************************************************
*---------------------------------------------------------------------*
* HOUSEKEEPING, ETC...                                                *
*---------------------------------------------------------------------*
CHEKBLOK CSECT
         BAKR  R14,0                   SAVE CALLER DATA ON STACK
         LR    R12,R15                 GET ENTRY POINT
         USING CHEKBLOK,R12            ADDRESSABILITY TO MODULE
         RDJFCB OUTPUT                 GET JFCB FOR 'OUTPUT' DD
         OPEN  (OUTPUT,(OUTPUT))       OPEN 'OUTPUT' DD
*---------------------------------------------------------------------*
* GETMAIN A BUFFER TO WRITE FROM (GET BLKSIZE FIRST FROM DCB)...      *
*---------------------------------------------------------------------*
         XR    R2,R2                   CLEAR WORK REG
         LH    R2,OUTPUT+X'3E'         GET BLKSIZE
         GETMAIN R,LV=(2),LOC=BELOW    GETMAIN A BUFFER
         LR    R3,R1                   SAVE GETMAIN ADDRESS
         TM    OUTPUT+X'24',X'40'      VARIABLE LENGTH RECORDS?
         BNO   WRITEIT                 NO...
         MVC   0(2,R3),OUTPUT+X'52'    YES..SAVE RECSIZE IN RDW
*---------------------------------------------------------------------*
* WRITE FILE. NOTE THAT 'SWITCH' IS ONLY SET WHEN THE E-O-V EXIT IS   *
* DRIVEN...                                                           *
*---------------------------------------------------------------------*
WRITEIT  DS    0H
         PUT   OUTPUT,(3)              WRITE A BLOCK
         TM    SWITCH,X'01'            REACHED END-OF-VOLUME?
         BO    ENDIT                   YES..DISPLAY COUNT, ETC
         MVC   BLKCNT,OUTPUT+12        NO...SAVE CURRENT BLOCK COUNT
         B     WRITEIT                 KEEP GOING TO E-O-V
*---------------------------------------------------------------------*
* DISPLAY BLKSIZE USED AND BLOCKS WRITTEN...                          *
*---------------------------------------------------------------------*
ENDIT    DS    0H
         WTO '******************************************************', X
               ROUTCDE=11
         WTO '*  CHEKBLOK - DISPLAY BLOCKS WRITTEN TO CART/TAPE    *', X
               ROUTCDE=11
         L     R1,BLKCNT               GET BLOCKS WRITTEN...
         CVD   R1,DWORD                ...CONVERT TO DECIMAL
         MVC   WTOLIST1+25(8),EDPATRN1 MOVE EDIT PATTERN TO WTO
         ED    WTOLIST1+25(8),DWORD+4  EDIT IN BLKSIZE
         WTO   MF=(E,WTOLIST1)         DISPLAY MSG
         XR    R1,R1                   CLEAR WORK REG
         LH    R1,OUTPUT+X'3E'         GET BLKSIZE...
         CVD   R1,DWORD                ...CONVERT TO DECIMAL
         MVC   WTOLIST2+27(6),EDPATRN2 MOVE EDIT PATTERN TO WTO
         ED    WTOLIST2+27(6),DWORD+5  EDIT IN BLKSIZE
         WTO   MF=(E,WTOLIST2)         DISPLAY MSG
         XR    R1,R1                   CLEAR WORK REG
         LH    R1,OUTPUT+X'52'         GET LRECL...
         CVD   R1,DWORD                ...CONVERT TO DECIMAL
         MVC   WTOLIST3+27(6),EDPATRN2 MOVE EDIT PATTERN TO WTO
         ED    WTOLIST3+27(6),DWORD+5  EDIT IN LRECL
         WTO   MF=(E,WTOLIST3)         DISPLAY MSG
         MVC   WTOLIST4+27(6),JFCB+118 MOVE VOLSER FROM JFCB
         WTO   MF=(E,WTOLIST4)         DISPLAY MSG
         WTO '******************************************************', X
               ROUTCDE=11
         CLOSE OUTPUT                  CLOSE IT
         FREEMAIN R,LV=(2),A=(3)       FREE STORAGE
*---------------------------------------------------------------------*
* GET OUT OF HERE...                                                  *
*---------------------------------------------------------------------*
RETURN   DS    0H
         XR    R15,R15                 SET RC=0
         PR    ,                       RETURN
*---------------------------------------------------------------------*
* E-O-V EXIT: SET SWITCH TO BE TESTED ABOVE...                        *
*---------------------------------------------------------------------*
EOVEXIT  DS    0H
         USING *,15                    ADDRESSABILITY TO EXIT
         OI    SWITCH,X'01'            SHOW E-O-V REACHED...
         BR    R14                     RETURN FROM EXIT
         DROP  R15
*---------------------------------------------------------------------*
*        CONSTANTS                                                    *
*---------------------------------------------------------------------*
         YREGS
BLKCNT   DS    F
DWORD    DS    D
BLKSZ    DS    H
OUTAREA  DS    CL80
SWITCH   DC    X'00'
EDPATRN1 DC    XL8'4020202020202120'
EDPATRN2 DC    XL6'402020202120'
OUTPUT   DCB   DDNAME=OUTPUT,                                          X
               DSORG=PS,                                               X
               MACRF=PM,                                               X
               EXLST=EXLST             EXIT LIST ADDRESS
EXLST    DS    0F
         DC    X'07'                   SHOWS READ JFCB
         DC    AL3(JFCB)               ADDRESS OF AREA FOR JFCB
         DC    X'86'                   SHOWS E-O-V EXIT & END OF LIST
         DC    AL3(EOVEXIT)            ADDRESS OF E-O-V EXIT
JFCB     DS    CL176' '
WTOLIST1 WTO   '*     BLOCKS WRITTEN: 0000000                        *'X
               ,ROUTCDE=11,MF=L
WTOLIST2 WTO   '*     BLKSIZE:          00000                        *'X
               ,ROUTCDE=11,MF=L
WTOLIST3 WTO   '*     LRECL:            00000                        *'X
               ,ROUTCDE=11,MF=L
WTOLIST4 WTO   '*     VOLSER:          XXXXXX                        *'X
               ,ROUTCDE=11,MF=L
         END


Sample JCL

//JOB00001 JOB ,'CHEKBLOK',NOTIFY=TESTUSR,MSGCLASS=E,CLASS=A
//CHEK    EXEC PGM=CHEKBLOK
//OUTPUT   DD DSN=TEMP.CART,UNIT=CART,DISP=(NEW,DELETE,DELETE),
//         BLKSIZE=2500,RECFM=FB,LRECL=250
//SYSPRINT DD  SYSOUT=*
You will actually see the job request a second cartridge as well, but this will only be to write the final block of data.


Sample Output

  +******************************************************
  +*  CHEKBLOK - DISPLAY BLOCKS WRITTEN TO CART/TAPE    *
  +*     BLOCKS WRITTEN:   21958                        *
  +*     BLKSIZE:           9500                        *
  +*     LRECL:              250                        *
  +*     VOLSER:          301185                        *
  +******************************************************


Grant Carson
Systems Programmer (UK)                     c Xephon 1996

