Correction to the December 1995 issue


         MACRO
&LABEL   NEXTVAR &VAR,&UNIQUE=NO
         LCLC  LABCHECK
TEMPL    LOCTR
         AIF   (D'WHAT).NOWHAT
WHAT     DS    0H
WHATL    LOCTR
WHATLEN  EQU   *-WHAT
TEMPL    LOCTR
.NOWHAT  ANOP
&LABCHECK SETC '@&VAR'
         AIF   (D'&LABCHECK).NOTAGAIN
@&VAR    DC    C'&VAR'
         DC    C' '
&INLEN   SETA  K'&VAR
.NOTAGAIN ANOP
@EQ&VAR  EQU   &INLEN
LMSRGET  LOCTR
         AIF   (T'&LABEL EQ 'O').NOTMORE
&LABEL   DS    0H
.NOTMORE ANOP
         AIF   ('&UNIQUE' EQ 'NO').BYST
         LA    R6,COMSHVB              * R6 --> A(SHVBLOCK AREA)
         USING SHVBLOCK,R6
         ST    R10,COMRET              * STORE ENVBLOCK ADDRESS (R10)
         XC    COMSHVB(SHVBLEN),COMSHVB * INITIALIZE SHVBLOCK
         MVI   SHVCODE,C'S'             * SET FUNCTION CODE - 'SET'
.BYST    ANOP
*********************************************************************
*        BUILD IRXEXCOM PARAMETERS
*********************************************************************
         XC    SHVNEXT,SHVNEXT          * RESET POINTER
         LA    R5,@&VAR
         ST    R5,SHVNAMA               * STORE ADDRESS OF NAME
         LA    R5,@EQ&VAR
         ST    R5,SHVNAML               * STORE NAME LENGTH
         LA    R5,&VAR
         ST    R5,SHVVALA               * STORE VARIABLE VALUE
         AIF   ('&VAR' EQ 'WHAT').DOWHAT
         LA    R5,L'&VAR                * STORE VARIABLE LENGTH
         AGO   .DONORM
.DOWHAT  ANOP
         LA    R5,WHATLEN
.DONORM  ANOP
         ST    R5,SHVVALL               * STORE VALUE LENGTH
*********************************************************************
*        INVOKE IRXEXCOM TO SET VARIABLE
*********************************************************************
         LR    R0,R10                   * R0 --> A(ENVBLOCK)
         LA    R1,COMS                  * R1 --> A(PARM ADDR LIST)
         L     R15,IRXEXCOM             * R15 --> A(IRXEXCOM)
         BALR  R14,R15                  * CALL ROUTINE
         LTR   R15,R15                  * SET OK ?
         BZ    EOF&SYSNDX               * --> YES? - END THIS VAR
         AGO   .ABEND
.ABEND   ANOP
         LA    R1,@&VAR
         ABEND &SYSNDX
EOF&SYSNDX DS  0H
         MEND

