Getting the terminal net-id as an ISPF variable

Many installations would find it useful to be able to display the
specific net-id in a TSO/ISPF environment. Having
encountered times when I wished this information was
available, I wrote the GETNETID routine. The routine requires
authorization and can be executed only from an authorized load
library - TSO will normally not execute authorized programs.
This program calls an SVC 235 to re-authorize the program and
to unauthorize the routine. Our SVC 235 code is included but
you may already have a version that will suffice.

GETNETID should be in a LINKLIST or ISPLLIB library. The
program is called from the log-on CLIST. It VPUTs the
terminal-id into an ISPF variable, VTERMID. We modified the
ISR@PRIM menu to display the id but you could use it on any
panel you like. The command to initiate the program would be:

      ISPSTART PGM(GETNETID) NEWAPPL(ISR).

It should be coded at the end of your log-on CLIST.

The net-id can be found in the TSBTRMID field of the
Terminal Status Block (TSB). To locate the TSB, you first
address the Communication Vector Table (CVT) to get the
address of the Address Space Control Block (ASCB). The
address of the TSB is found in the ASCB. More information on
these control blocks can be found in the MVS/ESA Data Areas
manuals.

Assembling GETNETID requires SYS1.MACLIB as a
SYSLIB. The ISPLINK module from your ISPLOAD library
must be in the link step SYSLIB.


SOURCE CODE FOR GETNETID

GETNETID CSECT
GETNETID AMODE 24
GETNETID RMODE 24
**  THIS ROUTINE VPUTS TSO TERMINAL ID INTO YOUR ISPF PROFILE      **
**                      "(PROFILE POOL)"                           **
         SAVE  (14,12)
         BASR  R12,0
         USING *,R12
         ST    R13,SAVEA+4
         LA    R13,SAVEA
ADDRCVT  L     R2,CVTPTR          POINT TO CVT
         USING CVTMAP,R2          ADDRESS CVT
         L     R2,CVTTCBP         POINT TO TCBWORDS
         DROP  R2
ADDRASCB L     R3,12(R2)          POINT TO MY ASCB
         USING ASCB,R3            ADDRESS ASCB
ADDRTSB  L     R2,ASCBTSB         POINT TO MY TSB
         USING TSB,R2             ADDRESS MY TSB
         LA    R1,1               AUTHORIZE MY TSO SESSION
         SVC   235                CALL AUTHORIZATION SVC
         MODESET KEY=ZERO,MODE=PROB  GET INTO KEY 0
         MVC   TERMID,TSBTRMID       GET MY NETID (CSA PROTECTED KEY 6)
         MODESET KEY=NZERO,MODE=PROB GET BACK INTO MY KEY
         XR    R1,R1              RESET AUTHORIZATION
         SVC   235                CALL AUTHORIZATION SVC
DEFINE   CALL  ISPLINK,(VDEFINE,VTERMID,TERMID,CHAR,L8),VL
         LTR   R15,R15            CALL TO ISPEXEC SUCCESSFUL ?
         BNZ   THEEND             NO, PROBABLY WE WERE NOT UNDER ISPF
PUT      CALL  ISPLINK,(VPUT,VTERMID,PROFILE),VL
THEEND   L     R13,SAVEA+4
         RETURN (14,12),RC=0
VTERMID  DC    CL8'VTERMID'       ISPF VARIABLE
VDEFINE  DC    CL8'VDEFINE'       ISPF KEYWORD
VPUT     DC    CL8'VPUT'          ISPF KEYWORD
CHAR     DC    CL8'CHAR'          ISPF KEYWORD
PROFILE  DC    CL8'PROFILE'       ISPF KEYWORD
L8       DC    F'8'               ISPF VARIABLE LENGTH
TERMID   DS    CL8
SAVEA    DS    18F
*        REQUIRED DSECTS (FOUND IN SYS1.MACLIB)                       *
         CVT DSECT=YES,LIST=NO    CVT
         IHAASCB DSECT=YES        ASCB
         IKJTSB                   TSB
         END


SVC235 SOURCE CODE

IGC0023E CSECT
*     SVC 235                                                         *
*     THIS IS AN AUTHORIZATION SVC.                                   *
*     MUST BE REFRESHABLE AND REUSABLE.                               *
***********************************************************************
*        IF R1=1, SET APF ON.                                         *
*        IF R1=0, SET APF OFF.                                        *
         USING IGC0023E,R6        MODULE ENTRY POINT
         LA    R15,8              SET RC=8 (BAD RETURN, DEFAULT)
         USING RBBASIC,R5         ADDRESS THIS SVRB
         L     R5,RBLINK          ADDRESS PREVIOUS RB
         TM    RBSTAB,RBFTP       IS THIS A PRB ?
         BNZ   EXIT               IF NOT, NOT A VALID USE THIS SVC
         L     R9,RBCDE           CDE FOR CALLING MODULE
         USING CDENTRY,R9         ADDRESS CDE
         TM    CDATTR2,CDSYSLIB   CALLING MODULE FROM AN APF LIB ?
         BNO   EXIT               IF NOT, FORGET IT
*        WE NOW KNOW THAT CALLER CAME FROM APF AUTHORIZED LIBRARY     *
         USING TCB,R4             ADDRESS TCB
         L     R2,TCBJSCB         GET JSCB ADDRESS
         USING IEZJSCB,R2         ADDRESS JSCB
         LTR   R1,R1              TEST OPTION REG 1
         BZ    APFOFF             CALLER WANTS TO TURN APF OFF
*        CALLER WANTS TO TURN APF ON                                  *
APFON    OI    JSCBOPTS,JSCBAUTH  SET AUTH APF BIT ON
         B     RETURN             AND EXIT THIS MODULE
*        CALLER WANTS TO TURN APF OFF                                 *
APFOFF   NI    JSCBOPTS,X'FF'-JSCBAUTH  SET AUTH APF BIT OFF
*        RETURN FROM MODULE.                                          *
RETURN   LA    R15,0              SET RC=0 (FUNCTION COMPLETE)
EXIT     BR    R14                EXIT MODULE
         LTORG
*        THE FOLLOWING DSECTS CAN BE FOUND IN:                        *
*              SYS1.MACLIB                                            *
*              SYS1.AMACLIB                                           *
*              SYS1.AMODGEN                                           *
         IKJRB
         IHACDE
         IKJTCB DSECT=YES
         IEZJSCB
         END   IGC0023E


ISR@PRIM PANEL

%-----------------------  ISPF/PDF PRIMARY OPTION MENU  ------------------------
%OPTION  ===>_ZCMD                                                             +
%                                                           +USERID   - &ZUSER
%   0 +ISPF PARMS  - Specify terminal and user parameters   +TERMID   - &VTERMID
%   1 +BROWSE      - Display source data or output listings +TIME     - &ZTIME
%   2 +EDIT        - Create or change source data           +PF KEYS  - &ZKEYS
%   3 +UTILITIES   - Perform utility functions
%   5 +DIALOG TEST - ......................................

               etc

+Enter%END+command to terminate ISPF.


Jeffrey Nix
Systems Programmer (USA)                              c Xephon 1992

