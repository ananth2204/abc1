Automatic reply to IEA212I

LOADXX MEMBER

MVSCP    A5
NUCLEUS  1
NUCLST   00
SYSCAT   MVCA01113CCAT.MAST
SYSPARM  GX MV CA01100RSB1292RS11659PPB1293*

USERMOD

/*JOBPARM SYSAFF=F
//* - INS.MVSIPO.B93C.POST ------------------ MEMBER:  MVSX04   ------
//* - OBJECTIVE: MODIFICATION TO AUTOMATE DISMOUNTING OF DUPLICATE    -
//* -            VOLSER-UCBS AT IPL TIME                              -
//* - PRODUCT:   MVS/ESA                                              -
//* - FMID:      HBB4430 PLUS PTF UY99552                             -
//* - USERMOD:   @MVSX04                                              -
//* -------------------------------------------------------------------
//RECUMOD  EXEC MVSSMPEB
//SMPPTFIN  DD *
++USERMOD(@MVSX04) REWORK(94081701).
++VER(Z038) FMID(HBB4430) PRE(UY99552).
++ZAP(IEAVNP02) DISTLIB(AOSC5).
 NAME IEAVNP02
 VER  001246 411087E5
 REP  001246 47F08CE5
 REP  001D00 0007
 REP  001D02 0000
 REP  001D04 00000000
 REP  001D08 E5E5E5E5E5E5
 REP  001D0E E5E5E5E5E5E5
 REP  001D14 E7E7E7
 REP  001D17 E8E8E8
 REP  001D1A E4E4E4
 REP  001D1D 5C5C
 REP  001D1F E2E8E2D9C5E2
 REP  001D25 00
 REP  001D26 0000
 REP  001D28 58100010
 REP  001D2C 581014C4
 REP  001D30 D2028CD71019
 REP  001D36 D2058CCB8939
 REP  001D3C D2058CC58931
 REP  001D42 D2028CD18945
 REP  001D48 D2028CD4894C
 REP  001D4E 58F01068
 REP  001D52 41FF0053
 REP  001D56 D2018CDAF000
 REP  001D5C 41FF0003
 REP  001D60 50F08CC1
 REP  001D64 D5018CDA8CCB
 REP  001D6A 47708D6F
 REP  001D6E D5058CC58CDC
 REP  001D74 47808D77
 REP  001D78 58108CC1
 REP  001D7C 955C1000
 REP  001D80 47808D6F
 REP  001D84 D5038CCD1000
 REP  001D8A 47808D53
 REP  001D8E 4A108CBD
 REP  001D92 47F08D39
 REP  001D96 41101004
 REP  001D9A D5028CD11000
 REP  001DA0 47808D81
 REP  001DA4 D5028CD41000
 REP  001DAA 47808D77
 REP  001DAE 47F08D77
 REP  001DB2 411087E5
 REP  001DB6 47F08207
 REP  001DBA D20288AD8CD1
 REP  001DC0 47F08D87
 REP  001DC4 D20288AD8CD4
 REP  001DCA 47F0821F
 EXPAND IEAVNP02(1024)
/*
//SMPCNTL  DD  *
  SET BDY(GLOBAL).
  RECEIVE      S(@MVSX04) SYSMODS SOURCEID(USRMODS).
  SET BDY(MVSTZNB).
  APPLY CHECK  S(@MVSX04) REDO.
  APPLY        S(@MVSX04) REDO.
/*
//

SOURCE FOR IEAVNP02

IEAVNP02 CSECT
R0       EQU   0
R1       EQU   1
         etc
R15      EQU   15
         USING A000044,R9
         USING A001043,R8
         ORG   *+68
A000044  EQU   *
         ORG   *+4095
A001043  EQU   *
         ORG   *+515
LB0010   DC    X'411087E5' --> TRANSFER CONTROL TO USER MODIFICATIONS
         ORG   *-4
         B     USRCODE ------> INSTRUCTION TO REPLACE
         ORG   *+24
LB0030   EQU   * ------------> RETURN WITH AUTOMATIC REPLY PROVIDED
         ORG   *+1678
RRREPLY  EQU   * ------------> WORK REPLY PROGRAM
         ORG   *+132
@CONSTA  DS    CL6 ----------> CONSTANT. IN MESSAGE IEA212A
         ORG   *+2
@VOLSER  DS    CL6 ----------> VOLUME NAME IN MESSAGE IEA212A
         ORG   *+6
@ADDR1   DS    CL3 ----------> 1ST.UCB IN MESSAGE IEA212A
         ORG   *+4
@ADDR2   DS    CL3 ----------> 2ST.UCB IN MESSAGE IEA212A
         ORG   *+878 --------> POSITIONING WORKAREA GENERATED BY EXPAND
         EJECT
*        +--------------------------------------------+
*--------| WORK-AREA PROVIDED BY SENTENCE EXPAND OF   |----------------
*        | LKED.BINDER                                |
*        |                                            |
*        | USER AREAS             (START 1D00)        |
*        +--------------------------------------------+
TBDISCOL DC    H'7'                <> SIZE EACH ENTRY VOL-UCB TABLE
TBDISCO  DC    A(0)                <> ADDR VOL-UCB TABLE
STCONSTA DC    CL6'VVVVVV'         <> DUPLICATE SYSRES VOLUME
STVOLSER DC    CL6'VVVVVV'         <> CAPTURED FROM IEA212I
STADDR1  DC    CL3'XXX'            <> CAPTURED FROM IEA212I
STADDR2  DC    CL3'YYY'            <> CAPTURED FROM IEA212I
STUCBIPL DC    CL3'UUU'            <> CAPTURED FROM NVT
STVOLPRF DC    CL2'**'             <> CAPTURED FROM NVT
KSYSRES  DC    CL6'SYSRES'         <> CONSTANT
         DC    H'0'                <> POSITIONING INSTRUCT AREA
         EJECT
*        +--------------------------------------------+
*--------| DATA CAPTURER                              |----------------
*        | FROM IEA212I MESSAGE AREA AND NVT AREA     |
*        |                                            |
*        | BE CAREFUL WITH REGISTER USAGE             |
*        +--------------------------------------------+
USRCODE  EQU   *                   <> START USER CODIFICATION
         L     R1,CVTPTR           <> CVT ADDRESS
         USING CVTMAP,R1           <> CVT MAPPING
         L     R1,CVTNVT0          <> ADDR NVT AREA
         DROP  R1
         USING NVT,R1              <> NVT MAPPING
         MVC   STUCBIPL,NVTPARMM+1 <> STORE UCB-IPL PACK (3 POS)
         MVC   STVOLSER,@VOLSER    <> STORE VOLUME NAME FROM IEA212I
         MVC   STCONSTA,@CONSTA    <> STORE CONSTANT SYSRES (IF EXIST)
         MVC   STADDR1,@ADDR1      <> STORE UCB-1 FROM MESSAGE
         MVC   STADDR2,@ADDR2      <> STORE UCB-2 FROM MESSAGE
         L     R15,NVTLOAD         <> ADDR. COPY OF LOADXX IN MEMORY
         LA    R15,83(R15)         <> POSITION SYSPARM CARD IN MEMORY
         MVC   STVOLPRF,0(R15)     <> TWO-CHAR PREFIX OF VOLUMES
         LA    R15,3(R15)          <> POSITION VOL-UCB TABLE
         ST    R15,TBDISCO         <> STORE ADDR VOL-UCB TABLE
*        +--------------------------------------------+
*--------| AUTOMATIC REPLY ONLY FOR:                  |----------------
*        |                                            |
*        | VOLUMES BEGINNING WITH CONSTANT REF'D IN   |
*        | SYSPARM CARD OF MEMBER LOADXX              |
*        +--------------------------------------------+
USRCO30  EQU   *
         CLC   STVOLPRF,STVOLSER   <> MATCH TWO-WORD PREFFIX ?
         BNE   UU99                <> NO, MESSAGE IEA212I STANDARD
*        +--------------------------------------------+
*--------| IF WORD "SYSRES" APPEARS IN MESSAGE IT IS  |----------------
*        | A DUPLICATE OF SYSRES PACK                 |
*        |                                            |
*        | WE PROVIDED AUTOMATIC REPLY WITH FIRST     |
*        | UCB ADDRESS FROM MESSAGE TO DEMOUNT IT     |
*        +--------------------------------------------+
         CLC   STCONSTA,KSYSRES    <> IS SYSRES DUPLICATE ?
         BE    UU30D1              <> YES, AUTOMATE REPLY FIRST UCB
*        +--------------------------------------------+
*--------| COMPARE TEXT OF IEA212I WITH VOL UCB TABLE |----------------
*        | AND PROVIDE AUTOMATIC REPLY OR STANDARD    |
*        | PROCCESS.                                  |
*        +--------------------------------------------+
         L     R1,TBDISCO          <> ADDR VOL UCB TABLE
UU10BUC  EQU   *
         CLI   0(R1),C'*'          <> TABLE END ?
         BE    UU99                <> GO TO STANDARD PROCCES
         CLC   STVOLSER+2(4),0(R1) <> MATCH 4-POS SUFFIX OF VOLSER ?
         BE    UU10TM              <> CONTINUE PROCCESS
         AH    R1,TBDISCOL         <> INCREASE TBDISCO TO NEXT ELEMENT
         B     UU10BUC             <> LOOP AGAIN
UU10TM   EQU   *
         LA    R1,4(,R1)           <> POSITION UCB FROM TABLE
         CLC   STADDR1,0(R1)       <> MATCH FIRST UCB ?
         BE    UU30D2              <> YES, AUTO DISMOUNT SECOND UCB
         CLC   STADDR2,0(R1)       <> MATCH SECOND UCB ?
         BE    UU30D1              <> YES, AUTO DISMOUNT FIRST  UCB
* CAUTION !!!
*    AT THIS POINT, WE CAN MAKE TWO DECISIONS:
*     A) AUTOMATIC DISMOUNT OF ONE OF TWO ADDRESS. IT IS NECCESARY
*        TO EXPECT ANOTHER MESSAGE AFTER THIS INVOLVING THE SAME
*        VOLUME NAME. IF NOT, IT WILL KEEP MOUNT AN UNEXPECTED VOL.
*     B) STANDARD PROCESSING. WE DON'T MAKE ANY DECISION.
* IN THIS CASE WE TAKE THE (A) OPTION AND AUTOMATIC-DISMOUNT FIRST UCB
         B     UU30D1              <> DISMOUNT FIRST UCB
*        +--------------------------------------------+
*--------| CONTROL RETURN TO DO STANDARD PROCESS AND  |----------------
*        | ASK OPERATOR                               |
*        +--------------------------------------------+
UU99     EQU   *
         DC    X'411087E5'         <> REAL INSTRUCTION FROM ORIGINAL
         B     LB0010+4            <> RETURN TO REAL ZONE
*        +--------------------------------------------+
*--------| AUTOMATIC REPLY                            |----------------
*        | WE INHIBIT THE MESSAGE AND PROVIDE REPLY   |
*        +--------------------------------------------+
UU30D1   MVC   RRREPLY(3),STADDR1  <> DISMOUNT FIRST UCB
         B     UU30END
UU30D2   MVC   RRREPLY(3),STADDR2  <> DISMOUNT SECOND UCB
UU30END  B     LB0030              <> RETURN TO REAL ZONE AFTER WAIT
         CVT   DSECT=YES
*        IHANVT: IBM MACRO IHANVT REF'D IN DATA AREAS DOESN'T EXIST
NVT      DSECT
NVTID    DS    CL4                 <> ACRONYM NVT
         DS    CL20
NVTPARMM DS    CL8                 <> MERGE OF LOAD PARAMETERS
         DS    CL72
NVTLOAD  DS    A                   <> ADDR IMAGE CARD FROM LOADXX
         END   IEAVNP02

STANDARD IPL EXAMPLE

  SYSTEM ID G     DATE  8/18/94   94.230
  IEE042I SYSTEM LOG DATA SET INITIALIZED
  IEE041I THE SYSTEM LOG IS NOW ACTIVE
  CONTROL M,UEXIT=N IEAVN701 - INTERNALLY ISSUED K M
  IEA371I SYS1.PARMLIB ON DEVICE 0292 SELECTED FOR IPL PARAMETERS
  IEA246I LOAD   ID GX SELECTED
  IEA246I NUCLST ID 00 SELECTED
  IEA097I I/O CONFIGURATION A5 SELECTED
  IEA091I NUCLEUS 1 SELECTED
 *IEA247I USING IEASYSGX FOR RELEASE 03.8 , VERSION SP4.3.0 HBB4430
 *IEA212A DUPLICATE VOLUME 'MVRS11'  D, 243 OR 250. REPLY DEVICE ADDR
  IEE600I REPLY TO 00 IS;243
  IEA313I DEVICE 243 DISMOUNTED
 *IEA212A DUPLICATE VOLUME 'MVCA01'  D, 100 OR 253. REPLY DEVICE ADDR
  IEE600I REPLY TO 00 IS;253
  IEA313I DEVICE 253 DISMOUNTED
 *IEA212A DUPLICATE VOLUME 'MVRSB1'  D, 25B OR 28C. REPLY DEVICE ADDR
  IEE600I REPLY TO 00 IS;25B
  IEA313I DEVICE 25B DISMOUNTED
 *IEA212A DUPLICATE VOLUME 'MVPPB1'  D, 25F OR 28D. REPLY DEVICE ADDR
  IEE600I REPLY TO 00 IS;25F
  IEA313I DEVICE 25F DISMOUNTED
 *IEA212A DUPLICATE VOLUME 'MVPPB1'  D, 28D OR 28E. REPLY DEVICE ADDR
  IEE600I REPLY TO 00 IS;28D
  IEA313I DEVICE 28D DISMOUNTED
  IEA313I DEVICE 28C DISMOUNTED
 *IEA212A DUPLICATE VOLUME 'MVPPB1'  D, 28E OR 293. REPLY DEVICE ADDR
  IEE600I REPLY TO 00 IS;28E
  IEA313I DEVICE 28E DISMOUNTED
  IEA313I DEVICE 297 DISMOUNTED
 *IEA212A DUPLICATE VOLUME 'MVCA01'  D, 100 OR 2CE. REPLY DEVICE ADDR
  IEE600I REPLY TO 00 IS;2CE
  IEA313I DEVICE 2CE DISMOUNTED
 *IEA212A DUPLICATE VOLUME 'MVRS11'  D, 250 OR 643. REPLY DEVICE ADDR
  IEE600I REPLY TO 00 IS;250
  IEA313I DEVICE 250 DISMOUNTED
 *IEA212A DUPLICATE VOLUME 'MVRS11'  D, 643 OR 659. REPLY DEVICE ADDR
  IEE600I REPLY TO 00 IS;643
  IEA313I DEVICE 643 DISMOUNTED
  IEA370I MASTER CATALOG SELECTED IS CAT.MAST
  IEA940I THE FOLLOWING PAGE AND SWAP DATA SETS ARE IN USE:
          PLPA ........... - PAGE.VMVCA01.PLPA
          COMMON ......... - PAGE.VMVCA01.COMMON
          LOCAL .......... - PAGE.VMVCA01.LOCAL
          LOCAL .......... - PAGE.VMVRS11.LOCAL
          LOCAL .......... - PAGE.VMVPPB1.LOCAL
  IEA598I TIME ZONE = W.00.00.00
  IEA191I CONSOLE 920 DEFINED AS MASTER CONSOLE.

EXAMPLE OF NEW IPL

  SYSTEM ID G     DATE  8/18/94   94.230
  IEE042I SYSTEM LOG DATA SET INITIALIZED
  IEE041I THE SYSTEM LOG IS NOW ACTIVE
  CONTROL M,UEXIT=N IEAVN701 - INTERNALLY ISSUED K M
  IEA371I SYS1.PARMLIB ON DEVICE 0292 SELECTED FOR IPL PARAMETERS
  IEA246I LOAD   ID GX SELECTED
  IEA246I NUCLST ID 00 SELECTED
  IEA097I I/O CONFIGURATION A5 SELECTED
  IEA091I NUCLEUS 1 SELECTED
 *IEA247I USING IEASYSGX FOR RELEASE 03.8 , VERSION SP4.3.0 HBB4430
  IEA313I DEVICE 243 DISMOUNTED
  IEA313I DEVICE 253 DISMOUNTED
  IEA313I DEVICE 25B DISMOUNTED
  IEA313I DEVICE 25F DISMOUNTED
  IEA313I DEVICE 28D DISMOUNTED
  IEA313I DEVICE 28C DISMOUNTED
  IEA313I DEVICE 28E DISMOUNTED
  IEA313I DEVICE 297 DISMOUNTED
  IEA313I DEVICE 2CE DISMOUNTED
  IEA313I DEVICE 250 DISMOUNTED
  IEA313I DEVICE 643 DISMOUNTED
  IEA370I MASTER CATALOG SELECTED IS CAT.MAST
  IEA940I THE FOLLOWING PAGE AND SWAP DATA SETS ARE IN USE:
          PLPA ........... - PAGE.VMVCA01.PLPA
          COMMON ......... - PAGE.VMVCA01.COMMON
          LOCAL .......... - PAGE.VMVCA01.LOCAL
          LOCAL .......... - PAGE.VMVRS11.LOCAL
          LOCAL .......... - PAGE.VMVPPB1.LOCAL
  IEA598I TIME ZONE = W.00.00.00
  IEA191I CONSOLE 920 DEFINED AS MASTER CONSOLE.


Angel Luis Dominguez Martin and Miguel Angel Martinez Orga
Systems Analysts
Argentaria VSI (Spain)                                               c Xephon 1994

