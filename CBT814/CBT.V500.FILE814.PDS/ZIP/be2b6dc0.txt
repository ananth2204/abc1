TSO cancel utility

INTRODUCTION

It can sometimes be necessary to cancel all TSO users logged on.
This can be a long and tedious job for operators, especially if there
are a large number of users.

THE SOLUTION

The following utility will search down the ASCBs for TSO users,
build a cancel command, and issue the command using SVC 34.  The
utility can also be limited to cancel a group of users specified by
their TSO logon-id prefix.

The program is invoked using the following procedure.

//TSOCAN   PROC USER=
//STEPY  EXEC PGM=TSOCAN,PARM='&USER'
//STEPLIB DD DSN=TSG.TESTLOAD,DISP=SHR

SAMPLE OPERATOR COMMANDS

Cancel all TSO users

    S TSOCAN

Cancel all TSO users with a TSO prefix of 'TS'

    S TSOCAN,USER=TS

PROGRAM SOURCE CODE

TSOCAN   CSECT
SETUP    SAVE  (14,12)                   ALL
         LR    12,15                     GOOD
         USING TSOCAN,12                 HOUSEKEEPING
         ST    13,SAVEAREA+4
         LR    3,13
         LA    13,SAVEAREA
         ST    13,8(3)
CHKPARM  L     2,0(1)                    ADD OF PARM LENGTH
         LH    3,0(2)                    GET PARM LENGTH
         LTR   3,3                       ANY PARM
         BZ    ALLINFO                   NO CANCEL ALL USERS
         LA    2,2(0,2)                  POINT TO PARM ADDRESS
         BCTR  3,0                       REDUCE LENGTH FOR EX
         EX    3,EXMVC                   MOVE LOGONID PREFIX TO LOGID
         B     GO
ALLINFO  MVC   LOGID,=CL8'LIST'          MOVE LIST TO LOGID
GO       L     11,16
         USING CVT,11                    ADDRESS CVT
         L     10,CVTASVT
         USING ASVT,10                   ADDRESS ASVT
         USING ASCB,7                    ADDRESS ASCB
         L     9,ASVTMAXU                LOAD MAX NO. OF ASCB'S
         LA    8,ASVTENTY                LOAD ASCB IN USE MARKER
         B     SKIP
GETASCB  BCT   9,*+8
         B     ENDLIST
         LA    8,4(,8)                   INCREMENT ASCB IN USE MARKER
SKIP     TM    0(8),X'80'                IS ASCB IN USE
         BO    GETASCB                   NO GET NEXT ASCB
         L     7,0(8)
         ICM   6,15,ASCBTSB              LOAD ADDRESS OF TSB
         LTR   6,6                       TEST
         BZ    GETASCB                   NOT TSO ASCB GET NEXT ONE
         SR    6,6
         ICM   6,15,ASCBJBNS             LOAD POINTER TO JOB NAME
         LTR   6,6
         BZ    GETASCB
         CLC   LOGID,=CL8'LIST'          DO WE CANCEL EVERYONE
         BE    LOAD                      YES
INITCHCK EX    3,CHECK                   COMPARE LOGON TO PREFIX
         BNE  GETASCB
LOAD     MVC   USERID(8),0(6)            MOVE LOGONID IN
         SR    4,4
         LA    4,16(4)                   LOAD CMD LENGTH+4
         STH   4,CMD                     STORE IT
         MODESET KEY=ZERO,MODE=SUP       SUPERVISOR STATE KEY 0
         LA    1,CMD                     LOAD CMD ADDRESS
         SLR   0,0
         SVC   34                        ISSUE COMMAND
         MODESET KEY=NZERO,MODE=PROB     PROBLEM STATE
         B     GETASCB                   GET NEXT ASCB
ENDLIST  EQU   *
         SLR   15,15
PROGEND  L     13,4(13)
         RETURN (14,12),RC=(15)          RETURN
EXMVC    MVC   LOGID(1),0(2)             MOVE PARM INTO LOGID
CHECK    CLC   0(0,6),LOGID              COMPARE LOGONID TO PREFIX
LOGID    DC    CL8' '
         LTORG
SAVEAREA DS    18F
CMD      DC    Y(L'CMDTEXT+4),Y(0)       CMD HEADER
CMDTEXT  DS    0CL100                    COMMAND BUFFER
         DC    C'C U='
USERID   DS    CL96
         CVT   DSECT=YES,LIST=NO
         IHAASVT DSECT=YES
         IHAASCB DSECT=YES
         END   TSOCAN


Justin Connolly
Systems Programmer (Eire) © Xephon 1990
