Putting DL/I program names into SMF records

The problem with using step termination (type 4) or common address space work
(type 30) records to report DL/I activity is that the program name you get is
that of the DL/I region controller DFSRRC00.  The DL/I program name given in the
EXEC parameter field is not there.  The following SMF exit (step initiation)
takes the program name from the scheduler work area and puts it into a user
field (JMRUSEID).  You may then use the fields with the name SMFxxUIF (SMF4UIF,
SMF30UIF, etc) to get the program name.

***********************************************************************
*         IEFUSI   (SP AND XA)                                        *
* FOR ALL DL/I STEPS THE PROGRAM NAME FROM THE EXEC PARAMETER FIELD   *
* IS PUT INTO THE USER IDENTIFICATION FIELD OF COMMON EXIT PARAMETER  *
* AREA. THE PROGRAM NAME MAY THEN BE FOUND IN STEP-RELATED SMF        *
* RECORDS IN FIELDS WITH SOURCE JMRUSEID OR JCTUSEID. THE PROGRAM     *
* NAME MAY ALSO BE FOUND IN JOB-RELATED SMF RECORDS IF THE DL/I STEP  *
* IS THE LAST OR ONLY STEP IN THE JOB. CONSIDER MOVING THE            *
* MVC 20(8,R2),=CL8' ' TO AFTER THE TEST FOR DL/I STEP IF YOU WANT    *
* THE LAST DL/I-PROGRAM NAME TO BE RETAINED IN NON-DL/I STEPS         *
* (AND ALWAYS TO BE FOUND IN JOB-RELATED SMF RECORDS).                *
* THE PARAMETER FIELD IS FOUND IN SWA:                                *
*         R1 IN HSA -> LCT -> SCT -> SCTX WITH PARAMETER FIELD        *
***********************************************************************
IEFUSI   CSECT
         USING *,R15           BASE REG.
         SAVE  (14,12)         SAVE IEFSMFIE'S REGS.
         LM    R2,R4,0(R1)     PARAMETERS SET UP BY IEFSMFIE
         MVC   20(8,R2),=CL8' '  BLANK NAME GIVEN IN FORMER DL/I STEP
         CLC   =C'DFSRRC00',0(R4)    DL/I STEP?
         BNE   EXIT            NO, EXIT
         L     R5,4(R13)       POINT TO HIGHER SAVE AREA
         L     R6,24(R5)       RESTORE R1 - POINTER TO LCT
         XR    R7,R7           ZERO
         ICM   R7,7,21(R6)     SCT
         XR    R8,R8           ZERO
         ICM   R8,7,68(R7)     SCTX
         BZ    EXIT            IF NO SCTX THEN EXIT
         XR    R9,R9           ZERO
         ICM   R9,3,8(R7)      LENGTH OF PARAM. FROM SCT
         CH    R9,=H'4'        IF LENGTH < 5
         BNH   EXIT            THEN EXIT
         CLI   23(R8),C','     COMMA AFTER DLI, DBB, ULU, UPB?
         BNE   EXIT            NO, PARAM IS WRONG, EXIT
         LA    R10,12          MAX. LENGTH OF PROGRAM-NAME + DLI,
         CR    R10,R9          TOTAL PARAM.LENGTH LESS THAN 12?
         BNH   ADJUST          NO, (AND THAT IS THE WAY IT SHOULD BE)
         LR    R10,R9          YES, USE PARAM.LENGTH
ADJUST   SH    R10,=H'4'       ADJUST FOR DLI,
MOVENAME CLI   24(R8),C','     END OF PROGRAM-NAME?
         BE    EXIT            IF SO THEN EXIT
         MVC   20(1,R2),24(R8) MOVE PROGRAM-NAME
         LA    R2,1(R2)        INCR. USER ID. POINTER
         LA    R8,1(R8)        INCR. PARAM. POINTER
         BCT   R10,MOVENAME    DO UNTIL THE NAME IS MOVED
EXIT     RETURN (14,12),RC=0   RETURN TO IEFSMFIE
         END


Edvin Vik
Senior Systems Engineer
Amdahl Norway (Norway)   © Amdahl 1987




