Capturing the output from TSO commands


CAPTURE REXX EXEC

/*-REXX/CAPTURE-------------------------------------------------------*/
/* Captures TSO commands Output and write it to a sequential file.    */
/*--------------------------------------------------------------------*/
/* Syntax: CAPTURE Command < DA(OutDsn)              >                */
/*                         < DA(Prefix.CAPTURE.OUTPUT>   * DEFAULT *  */
/*--------------------------------------------------------------------*/
    Trace O
    address TSO
    parse upper arg Cmd 'DA(' OutDsn ')' .
    if OutDsn = '' then OutDsn = UserId()'.CAPTURE.OUTPUT'
/*----- Verifies if file already exists. If not, then allocate it ----*/
    Exist = sysdsn("'"OutDsn"'")
    If Exist = 'OK'
       then do
            'ALLOCATE FILE(CAPTURE) DATASET('''OutDsn''') MOD'
            end
       else do
            'ALLOCATE FILE(CAPTURE) DATASET('''OutDsn''') DSORG(PS)' ,
                      CATALOG SPACE(1,2) TRACKS RECFM(F B) LRECL(80)' ,
                      BLKSIZE(6160)'
            if Rc <> 0
               then do
                    say '*** Allocation error for the file' OutDsn ,
                        'with Return Code' Rc '***'
                    Exit 10
                    end
            end
/*--------------- Traps the command output in stem LINE. -------------*/
    x = outtrap('LINE.')
    Cmd
    RcCmd = Rc
    x = outtrap('OFF')
/*------------ Queue the LINE. Stem to the output dataset ------------*/
    do Ind = 1 to Line.0
       queue Line.Ind
    end
    'EXECIO' queued() 'DISKW CAPTURE (FINIS'
    'FREE FILE(CAPTURE)'
    zedLMsg = 'File' OutDsn 'created with the Output of the command' Cmd
    Address ISPEXEC 'SETMSG MSG(ISRZ001)'
    Address ISPEXEC 'EDIT DATASET('''OutDsn''')'
    exit RcCmd


Patrick Leroy
Systems Analyst
Banco Fonsecas & Burnay (Portugal)                       c Xephon 1996

