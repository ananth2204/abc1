Automatic log-on and sign-on to IMS

The following Physical Input Exit was needed to sign on
terminals to our IMS systems automatically. Our Physical Input
Exit (SIGNIN) will be invoked by every terminal that needs to
be automatically signed on to our IMS systems. This exit will
first check to see if the terminal is signed on: if the sign-on bit
is not on, this exit will issue the RACINIT macro to sign this
terminal on to IMS using the NODE name as the user-id. After
the sign-on request is done, the exit returns control to the
caller. The terminals defined to use this exit invoke it when a
transaction or command is generated. After the sign-on bit is
on, the exit immediately returns control to the caller.

One limitation this exit has is that if the input message is
processed by MFS, the exit is not called. This edit routine is
called only when input from a terminal: it is not called when
the message is inserted via a program to program switch.

The exit has been run successfully and without changes in both
IMS 2.2 and 3.1 systems.

The following publications detail the Physical Input Exit
routine and describe the steps necessary to define the Physical
Input Exit to your IMS system:

o      IMS/ESA Version 3 Customization Guide Data Communication
o      IMS/ESA Version 3 System Definition Reference.


How IMS communicates with the routine

The Physical Terminal Input Exit routine is specified on the
LINEGRP or TYPE macro as part of the EXIT parameter. The
exit name can be any name chosen by the user. If you are only
using a Physical Input Exit routine, you must specify (,YES)
on the EDIT parameter of the TERMINAL macro. The
following is a complete example for defining a Physical Input
Exit and having terminals access this exit.

***      EXAMPLE TERMINAL DEFINITIONS THAT USE PHYSICAL INPUT
***      EXIT CALLED SIGNIN.
      TYPE     UNITYPE=SLUTYPE2,MODEL=2,                               X
               FEAT=(PFK,NOCD,NOPEN),                                  X
               OPTIONS=(TRANRESP,COPY),                                X
               EDIT=(,SIGNIN)
****                                                              *****
      TERMINAL NAME=T0328101,EDIT=(NO,YES)
         NAME  CLTC0007
      TERMINAL NAME=T0328102,EDIT=(NO,YES)
         NAME  CLTC0008


Installation Steps

1      Before stage 1 of IMS system definition, you need to specify:

      -      TYPE EDIT=(,modulename 2) in the TYPE macro,
            where modulename 2 is for a physical input exit.

      -      TYPE EDIT=(NO,YES) in the TERMINAL macro.
            EDIT=(NO,YES) indicates that this terminal will only
            use a Physical Input Exit.

      For further details see the TYPE and TERMINAL macro
      statements description in IMS/ESA System Definition  Reference.

2      You can name the Physical Input Exit routine anything you
      wish, assemble it, and place the load module in the PDS
      defined by the USERLIB operand of the IMSGEN macro
      (see the IMSGEN macro statement description in the
      IMS/ESA System Definition Reference).

3      After coding the necessary macro changes you must run
      your IMS system definition stage 1. The stage 1 output is
      the input to system definition stage 2. You must then run
      your stage 2 jobs. For a complete description see IMS/ESA
      System Definition Reference.

*****             PROGRAM DOCUMENTATION INFORMATION               *****
*  NAME:        SIGNIN                                                *
*  PURPOSE:     AUTOMATIC SIGN-ON EXIT FOR OUR IMS SYSTEMS USING RACF *
*  INVOCATION:  TERMINALS DEFINED IN MEMBER AUTOSIGN WILL BE USING    *
*               THIS PHYSICAL INPUT EXIT.                             *
* THIS MODULE IS LINKED INTO IMSX.USERLIB AND FROM THERE IT IS        *
* LINKED INTO THE IMS SYSTEMS NUCLEUS WHENEVER A SYSGEN IS DONE.      *
* IF THIS MODULE IS MODIFIED IT MUST BE LINKED INTO IMSX.USERLIB      *
* AND THE IMS NUCLEUS MUST BE RELINKED. ONCE THIS PROCESS HAS BEEN    *
* COMPLETED THE IMS SYSTEM MUST BE RECYCLED TO PICK UP THE NEW        *
* NUCLEUS.                                                            *
* PLEASE NOTE CHANGE APPLID TO YOUR IMS APPLID                    *****
         TITLE 'SIGNIN'  - PHYSICAL INPUT EXIT FOR AUTOMATIC SIGN-ON'
SIGNIN   CSECT
         SAVE  (14,12),,SIGNIN
         LR    R12,R15
         USING SIGNIN,R12
         USING CTB,R7
         USING IECTDECB,R9
         L     R13,8(,R13)              NEXT SAVEAREA SET
         XR    R15,R15                 ZAP REGISTER 15
         ST    R1,HOLDREG1            SAVE OFF REGISTER 1
         TM    CTBFLAG6,CTB6SDON      ALREADY SIGNED ON?
         BO    RETURN                    YES, GO AROUND
         BAL   R3,SIGNON                  NO, PERFORM SIGN-ON
RETURN   DS    0H
         L     R1,HOLDREG1             RESTORE REGISTER 1
         XR    R15,R15                SET RETURN CODE TO 0
         L     R13,4(,R13)            RELOAD ADDRESS OF SAVEAREA
         LM    R2,R12,28(R13)         RELOAD REG 2 THRU 12
         L     R0,20(,R13)            RELOAD REG 0
         L     R14,12(,R13)           RELOAD REG 14 FOR BRANCH
         SR    R15,R15                SET RETURN CODE TO 0
         BR    R14                    RETURN TO IMS
SIGNON   DS    0H                      SIGN-ON ROUTINE
         MVC   USERIDNM(8),CLBNAME     MOVE NODENAME INTO USERIDNM
         MVC   LIDNAME,CLBNAME         MOVE NODENAME INTO USERIDNM
         LA    R4,USERIDNM+7
         LA    R5,8
LOOP01   CLI   0(R4),C' '              FIND LAST NON-BLANK
         BNE   LOOP01X                 EXIT LOOP IF BLANK FOUND
         BCTR  R4,0                    BACK UP 1 CHARACTER
         BCT   R5,LOOP01               VERIFY 8 CHARACTERS
LOOP01X  STC   R5,USERIDL              SET LENGTH OF USERID
         MVC   TERMIDNT,CLBNAME        MOVE USERID TO TERMID
         LA    R4,CTBRACFT             SET A(ACEE) IN CTB.
         RACINIT  USERID=USERID,TERMID=TERMIDNT,SUBPOOL=255,           X
               ENVIR=CREATE,ACEE=(R4),SMC=NO,PASSCHK=NO,               X
               APPL=APPLID
         LTR   R15,R15                 CHECK CONDITION CODE FOR RACINIT
         BNZR  R3                      RACINIT NOT SUCCESSFUL BR RETURN
         MVC   CTBUSID(8),CLBNAME      MOVE USERID
         OI    CTBFLAG6,CTB6SDON       SET SIGNON FLAG ON
*** THE FOLLOWING INSTRUCTION IS NEEDED SO THAT THE RACF/IMS       ***
*   INTERFACE ICHRGL04 WILL NOT ATTEMPT TO FREEMAIN AN ADDRESS FROM  *
*** THE ACEEAPTR. OUR ACEE DOES NOT CONTAIN AN ACEEAPTR ADDRESS    ***
         OI    CTBFLAGC,CTBCRSOD       RACF SIGN-ON DONE - XRF ALT
*** ENQ WILL BE DONE ON NON-BLANK CHARACTERS THAT IS WHY WE USE
***   8 CHARACTERS FOR OUR SHOP NCP STANDARDS.
         ENQ   (SYSIKJUA,LIDNAME,E,8,SYSTEM),RET=USE
         BR    R3                  RETURN FROM SUBROUTINE AFTER ENQ
         DS    0D
GMAREA   DC    F'0'
SYSIKJUA DC    CL8'SYSIKJUA'
LIDNAME  DC    CL8'        '
APPLID   DC    CL8'IMSP    '
HOLDREG1 DC    F'0'
TERMID   DS    0CL9
TERMIDL  DS    XL1
TERMIDNT DS    CL8
PASSWRDN DS    0CL9
PASSWRDL DC    X'08'
PASSWORD DC    CL8' '
USERID   DS    0CL8
USERIDL  DS    XL1
USERIDNM DS    CL7
         ICLI  CTBBASE=0
         ICLI  CLBBASE=0
         REQUATE SAVE=YES
         END


Bruce T Chalmers
Senior Systems Programmer
USAir Inc (USA)                                          c Xephon 1993

