TSO response time monitor

This is a simple TSO response time monitor for first period TSO sessions.
Basically, you get an average TSO response time, ready user average, and target
Multi-Programming Level (MPL).  These figures can be useful in adjusting SRM
parameters for optimal TSO response times.

MPL is the number of address spaces in a domain which can be in main storage at
any one time.  'Ready user average' is the number of address spaces in storage
ready to run; therefore, as long as the ready user average is lower than the
target MPL and you can live with your response time, you are in pretty good
shape.

In order to get an average TSO response time, an adjustment may be necessary to
the current IEAIPSxx member of SYS1.PARMLIB.  Consider the following performance
group definition:

     PGN=3,(DMN=4,OBJ=1,DP=F74,ISV=500,DUR=500,
              PPGRT=(10,20),RTO=0.1)

The RTO parameter tells SRM that there is a response time objective for this
domain, so measure it.  This is where the average response time comes from.
Care should be taken in selecting this value because the SRM could possibly
delay the first period TSO in order to match the RTO value.  Consult the manual
SPL: Init and Tuning for more insight into this option.  The domain number
should be the one specified for first period TSO.

Notice the AMODE and RMODE options in the program.  The RMCT and DMCT both
reside above the 16 megabyte line; therefore, AMODE must be set to 31.  RMODE
must be 24 because TPUT will not handle 31-bit addresses.  The following code is
currently being executed in an MVS/XA 2.1.3 environment.

RTO      CSECT
RTO      AMODE 31
RTO      RMODE 24
         USING *,RF
         B     BEGIN
         DC    CL9'RTO'
         DC    CL9'&SYSDATE'
         DC    CL6'&SYSTIME'
BEGIN    CNOP  0,4
         DROP  RF
         LR    RC,RF
         USING RTO,RC
         STM   R0,RF,RTOSAVE           SAVE CALLERS REGS
MLOGIC   EQU   *
*--------------------------------------------------------------------*
*        RMCT - SRM CONTROL TABLE                                    *
*        DMDT - DOMAIN DESCRIPTOR TABLE                              *
*--------------------------------------------------------------------*
         L     R2,X'10'                GET CVT ADDRESS
         L     R2,X'25C'(R2)           GET RMCT ADDRESS
         L     R3,X,B8'(R2)            GET ADDRESS OF LAST DMDT ENTRY
         L     R2,X'B4'(R2)            GET DMDT ADDRESS
*--------------------------------------------------------------------*
*        R2 - DMDT POINTER                                           *
*        R3 - POINTER TO LAST DMDT ENTRY                             *
*--------------------------------------------------------------------*
MLS010   EQU   *
*--------------------------------------------------------------------*
*        NOTE:  THE DOMAIN NUMBER SHOULD BE THE ONE                  *
*               FOR FIRST PERIOD TSO.                                *
*--------------------------------------------------------------------*
         CLI   0(R2),X'04'             IS THIS THE CORRECT DOMAIN
         BE    MLS020                  YES, CONTINUE
         CR    R2,R3                   IS THIS THE LAST ONE
         BE    MLS800                  YES, NO MATCH
         LA    R2,X'40'(R2)            POINT TO THE NEXT ONE
         B     MLS010                  TRY AGAIN
MLS020   EQU   *
         L     R1,X'30'(R2)            GET AVERAGE RESPONSE TIME
         CVD   R1,DWD                  CONVERT TO DECIMAL
         OI    DWD+7,X'0F'             ASSURE PRINTABLE SIGN
         MVC   RTIME,RMASK             SET EDIT MASK
         ED    RTIME,DWD+6
         LH    R1,6(R2)                GET AVERAGE READY USERS
         CVD   R1,DWD                  CONVERT TO DECIMAL
         MVC   RUSERS,UMASK            SET EDIT MASK
         ED    RUSERS,DWD+6
         LH    R1,4(R2)                GET CURRENT MPL
         CVD   R1,DWD                  CONVERT TO DECIMAL
         MVC   CMPL,CMASK              SET EDIT MASK
         ED    CMPL,DWD+6
         TPUT  RTOMSG,L'RTOMSG+5
         TPUT  RUMSG,L'RUMSG+5
         TPUT  MPLMSG,L'MPLMSG+5
         B     MLS900                  TAKE NORMAL RETURN
MLS800   EQU   *
         TPUT  ERRMSG,L'ERRMSG
         LM    R0,RF,RTOSAVE
         LA    RF,4                    SET ERROR RETURN
         BR    RE                      RETURN TO CALLER
MLS900   EQU   *
         LM    R0,RF,RTOSAVE
         XR    RF,RF                   SET NORMAL RETURN
         BR    RE                      RETURN TO CALLER
         TITLE 'SAVE AREAS AND CONSTANTS'
RTOSAVE  DS    16F                     SAVE AREA
RTOMSG   DC    C'AVERAGE TSO RESPONSE TIME - '
RTIME    DC    XL5'00'
RMASK    DC    X'40214B2020'
RUMSG    DC    C'READY USER AVERAGE - '
RUSERS   DC    XL5'00'
UMASK    DC    X'4021204B20'
MPLMSG   DC    C'TARGET MPL - '
CMPL     DC    XL5'00'
CMASK    DC    X'4040212020'
DWD      DC    D'0'
ERRMSG   DC    C'INVALID DOMAIN SPECIFIED'
         END


Steven Smith
Systems Programmer
American Family Life (USA)    © American Family Life 1987

















