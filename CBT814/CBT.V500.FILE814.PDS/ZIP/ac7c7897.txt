Determining where a module was loaded from


PROGRAM SOURCE CODE

WHEREIS  CSECT
WHEREIS  AMODE 31
WHEREIS  RMODE 24
         BAKR  14,0               Save regs, ARs, etc on linkage stack
         LAE   12,0(15,0)         Base register
         USING WHEREIS,12
         B     INIT               Branch around constants
         DC    CL9'WHEREIS '
         DC    CL9'&SYSDATE'
         DC    CL8'&SYSTIME'
         SPACE
INIT     EQU   *
         LR    R10,R1
         USING CPPL,R10
         STORAGE OBTAIN,LENGTH=WORKL  Get Work Area Storage
         LR    R11,R1             Address of storage
         USING WORK,R11           Addressability
         SPACE
         LAE   13,SAVEAREA        Set up save area addr in AR/R13
         MVC   4(4,13),=C'F1SA'   LS save indicator
         SPACE
         L     R3,CPPLCBUF        Get command buffer address
         LH    R1,2(,R3)          Offset of operands
         LA    R2,4(R1,R3)        Address of parm
         LH    R4,0(,R3)          Length of parm
         SH    R4,=H'4'
         SH    R4,2(,R3)
         MVC   XNAME(8),=CL8' '    Blank out field name
         LA    R7,XNAME            Address of name field
*---------------------------------------------------------------*
*  Scan command buffer for member name parm and move it into the*
*  BLDL parm list.                                              *
*---------------------------------------------------------------*
USERLOOP EQU   *
         CLI   0(R2),X'00'         end of parm?
         BE    GOTUSER
         CLI   0(R2),C' '          end of parm?
         BE    GOTUSER
         MVC   0(1,R7),0(R2)       Move in character
         LA    R2,1(,R2)           Increment pointer
         LA    R7,1(,R7)           Increment pointer
         BCT   4,USERLOOP          Get next char of member name
GOTUSER  EQU   *
         OC    XNAME(8),=CL8' '    Convert to upper case
*---------------------------------------------------------------*
*  Issue BLDL to find out in which library the member is in     *
*---------------------------------------------------------------*
         LA    R5,XPARMS           Address of BLDL parms
         BLDL 0,(5),NOCONNECT      Issue BLDL
         LTR   R15,R15             BLDL worked?
         BNZ   ERROR               no ... issue error msg
         MVC   XMSG1(8),XNAME      Move member name into msg
*---------------------------------------------------------------*
*  Interpret what is returned from BLDL                         *
*---------------------------------------------------------------*
PRIVATE  EQU   *
         CLI   XLIBTYPE,X'00'      Private lib?
         BNE   LINKLIB             No ... continue checking
         MVC   XMSG1+22(15),=C'PRIVATE Library'  Move in msg
         B     DOMSG               Issue msg
LINKLIB  EQU   *
         CLI   XLIBTYPE,X'01'      Linklist dataset?
         BNE   STEPLIB             No ... Continue checking
         MVC   XMSG1+22(9),=C'LINKLIB +'  Move in msg
         B     DOMSG               Issue msg
STEPLIB  EQU   *
         CLI   XLIBTYPE,X'02'       STEPLIB ?
         BNE   PARENT              No ... Check of STEPLIB of Parent
         MVC   XMSG1+22(9),=C'STEPLIB +'  Move in msg
         B     DOMSG               Issue msg
PARENT   EQU   *
         MVC   XMSG1+22(20),=C'STEPLIB of Parent n '  Move in msg
         B     MESSAGE1            Issue msg
*---------------------------------------------------------------*
*  Figure out library module is in.                             *
*---------------------------------------------------------------*
DOMSG    EQU   *
         XR    R3,R3                Clear R3
         IC    R3,XCONCAT           R3 = concat #
*
*        Make number printable                                 
*
         LA    1,0(,R3)     
         SLL   1,4                                                    
         O     1,X'0000000F'                                          
         ST    1,XWORK                                                
         UNPK  XERROR(4),XWORK(4)                                     
         LA    1,=XL16'F0F1F2F3F4F5F6F7F8F9C1C2C3C4C5C6'              
         SH    1,=H'240'                                              
         TR    XERROR(4),0(1)                                        
         MVC   XMSG1+32(2),XERROR+2 Move in concat number
         CLI   XLIBTYPE,X'02'       STEPLIB ?
         BE    MESSAGE1             If in STEPLIB don't check LL concat
         XR    R2,R2                Clear R2
         M     R2,=F'45'            Length of each dataset in linklist
         L     R4,16                Address of CVT (X'20')
         USING CVT,R4               Get addressability
         L     R5,X'4DC'(R4)
         AR    R3,R5
         MVC   XMSG2+2(44),9(R3)    Get dsn name where member is
*---------------------------------------------------------------*
*    Issue WTO tell where the module resides                    *
*---------------------------------------------------------------*
MESSAGE1 EQU   *
         MVC   XMSG1+9(12),=C'was found in'
         TPUT  XMSG1,80
         TPUT  XMSG2,80
         B     RETURN
*---------------------------------------------------------------*
*    Error Message processing                                   *
*---------------------------------------------------------------*
ERROR    EQU   *
         MVC   XMSG3(32),=C'Error During BLDL - Return Code '
         LR    R3,R15
*
*        Make number printable                                 
*
         LA    1,0(,R3)     
         SLL   1,4                                                    
         O     1,X'0000000F'                                          
         ST    1,XWORK                                                
         UNPK  XERROR(4),XWORK(4)                                     
         LA    1,=XL16'F0F1F2F3F4F5F6F7F8F9C1C2C3C4C5C6'              
         SH    1,=H'240'                                              
         TR    XERROR(4),0(1)                                        
         MVC   XMSG3+32(2),XERROR+2
         TPUT  XMSG3,80
         B     RETURN
*---------------------------------------------------------------*
*    Return to Caller                                           *
*---------------------------------------------------------------*
RETURN   EQU   *
         LR    R1,R11
         STORAGE RELEASE,ADDR=(1),LENGTH=WORKL
         SLR   R15,R15            rc = 0
         PR                       Return
         EJECT
*---------------------------------------------------------------*
*    Data areas                                                 *
*---------------------------------------------------------------*
XCONNECT DS    CL8                Reserved 8 bytes for noconnect opt.
XPARMS   DC    XL2'1'             Number of entries in list
XLENGTH  DC    XL2'76'            Length of each entry
XNAME    DS    CL8                Member to be located
XTTR     DS    XL3                Relative track number and record
XCONCAT  DS    XL1                Concat number of dataset
XLIBTYPE DS    XL1                Where system found library
XX       DC    76X'00'            User data filled in by BLDL
WORK     DSECT
XMSG1 DS    CL80
XMSG2 DS    CL80
XMSG3 DS    CL80
XWORK    DS    D                  Work field 
XERROR   DS    D                  Error field
SAVEAREA DS    18F                Savearea
WORKL    EQU   *-WORK
         YREGS
         IKJCPPL
         CVT DSECT=YES
         END


Paul Poolsaar
Systems Programmer (Canada)                    c Xephon 1996

