Freeing unused DASD space

PARTREL1 SOURCE CODE

*===PARTREL1
*   This program frees unused space of the files of a given volume
*   Must be catalogued in an apf-authorized loadlib
*   Input Parameter: volume name
*   Input File.....: temp1 (an iehlist listvtoc output)
*   Output File....: sysprint
*   See macro PARTREL in MVS/DFP System Prog Reference
*=============UCBLOOK in Application Develop Ref  Asm  Progs
PARTREL1 CSECT
         SAVE  (14,12)
         LR    R12,R15
         USING PARTREL1,R12
         LR    R2,R1               KEEP PARM POINTER IN R2
         ST    R13,SAVEA+4
         LA    R11,SAVEA
         ST    R11,8(R13)
         LR    R13,R11
*==============FIND UCB ADDRESS FOR VOLSER IN PARAMETER
         OPEN  (SYSPRINT,OUTPUT)
         L     R3,0(0,R2)          GET FIRST PARM ADDRESS
         AH    R3,=H'2'            SKIP LENGTH
         MODESET MODE=SUP          SUPERVISOR STATE
         LA    R7,UCBPOINT         AREA FOR UCBLOOK PLACE THE POINTER
         UCBLOOK VOLSER=(R3),UCBPTR=(R7),NOPIN
         LTR   R15,R15             RC OK?
         BNZ   EXIT1
         MODESET MODE=PROB         BACK TO PROBLEM STATE
         L     R7,0(0,R7)          R7 CONTAINS UCB ADDR FOR PARTREL
*==============RELEASE SPACE FOR DATASET INDICATED BY TEMP1
         OPEN  (TEMP1,INPUT)       OPEN TEMP1
         LTR   R15,R15             OPEN OK?
         BNZ   EXIT2               NO, EXIT
RECEBE   GET   TEMP1,LINE          READ LINE FROM IEHLIST OUTPUT
         CLC   EXPIRE,EXPIRE1      EXPIRE = EXPIRE1?
         BNE   RECEBE              NO, GET NEXT LINE
         PARTREL MF=(E,PARTR),DSN=DSNAME,UCB=(R7)
         LTR   R15,R15             RC 0?
         BZ    OKAY
         MVC   RETC,MSG3           NO,PUT MESSAGE IN OUTPUT LINE
OKAY     PUT   SYSPRINT,LINE       WRITE OUTPUT LINE
         B     RECEBE              GO GET NEXT
CLOSFILS CLOSE TEMP1 SYSPRINT
EXIT     DS    0H
         L     R13,SAVEA+4
         LM    R14,R12,12(R13)
         SR    R15,R15
         BR    R14
EXIT1    PUT   SYSPRINT,MSG1
         B     CLOSFILS
EXIT2    PUT   SYSPRINT,MSG2
         B     CLOSFILS
*==============WORK AREAS
SYSPRINT DCB   DSORG=PS,RECFM=F,MACRF=(PM),                            *
               LRECL=132,BLKSIZE=132,                                  *
               DDNAME=SYSPRINT
TEMP1    DCB   DSORG=PS,MACRF=(GM),EODAD=CLOSFILS,                     *
               DDNAME=TEMP1
MSG1     DC    CL132'ERROR ===>> NON-ZERO RETCODE FROM UCBLOOK'
MSG2     DC    CL132'ERROR ===>> ERROR OPENING TEMP1'
MSG3     DC    CL26' NON-ZERO RC FROM PARTREL '
EXPIRE1  DC    CL6'00.000'         EXPIRE TEXT TO BE COMPARED
LINE     DS    0CL140              INPUT LINE
         DS    C                   PRINT CONTROL CHARACTER
DSNAME   DS    CL44                DATASET NAME
         DS    CL12
EXPIRE   DS    CL6                 EXPIRE DATE (00,000)
         DS    CL51
RETC     DS    CL26
SAVEA    DS    18F
UCBPOINT DS    CL4
PARTR    PARTREL MF=L
         $REGS
         END

//JOB1 JOB MSGLEVEL=(1,1),MSGCLASS=X,NOTIFY=&SYSUID
//STEP1    EXEC PGM=IEHLIST,REGION=1M
//SYSPRINT DD DISP=(NEW,PASS),DSN=&&TEMP1,UNIT=VIO,
//         DCB=(RECFM=V,LRECL=140),SPACE=(TRK,(5,5))
//DD2      DD DISP=SHR,UNIT=3390,VOL=SER=VOL001
//SYSIN    DD *
  LISTVTOC VOL=3390=VOL001
/*
//*
//STEP2    EXEC PGM=PARTREL1,REGION=1M,PARM='VOL001'
//STEPLIB  DD DISP=SHR,DSN=your_loadlib
//TEMP1    DD DISP=(OLD,DELETE),DSN=&&TEMP1
//SYSPRINT DD SYSOUT=*
/*


Lu’s Paulo Ribeiro
Systems Programmer
Edinfor (Portugal)                                                                       c Xephon 1995

