Searching DASD for an uncatalogued dataset

INTRODUCTION

VTOCSRCH (VTOC Search) is a simple program that will search all
on-line DASD for a given dataset.  VTOCSRCH is particularly
useful in finding uncatalogued datasets.  VTOCSRCH is designed to
be invoked under TSO.  If the dataset name is not supplied as a
parameter, VTOCSRCH will prompt for it.  A sample invocation
would look something like this:

    READY
    call 'userid.load(vtocsrch)' 'userid.dataset.name'
                     ***** VTOC SEARCH FACILITY *****
    SEARCHING ALL DASD VOLUMES FOR : USERID.DATASET.NAME
    DSN FOUND ON VOLUME: TSO010 THRU PATH 29E
    ALL ONLINE DASD VTOCS HAVE BEEN SEARCHED
    READY

VTOCSRCH SOURCE CODE
         MACRO
&LABEL   TRANS &OUTPUT,&INPUT
&LABEL   MVC   HEX(5),&INPUT            INPUT THE TRANS AREA
         BAL   R10,HEXRTN               LINK THE TRANS RTN
         MVC   &OUTPUT,HEXDONE          HEX ON TO OUTPUT
         MEND
VTOCSRCH CSECT SCD
         SAVE  (14,12),,*
         LR    R12,R15                  ENTRY POINT
         USING VTOCSRCH,R12             ADDRESSABILITY
         USING UCBOB,R7                 ADDRESSABILITY FOR UCB
         ST    R13,SAVEAREA+4           ADDRESS OF PREV SAVEAREA
         LA    R13,SAVEAREA             ADDRESS OF CURR SAVEAREA
         L     R3,0(,R1)                GET ADDRESS OF PARM
         TPUT  HEADER,80
         LH    R4,0(,R3)                GET PARM LENGTH
         CH    R4,=F'0'                 ANY PARM ?
         BNE   PARMIN                   YES...
         TPUT  PROMPT,36,ASIS
         TGET  DS1DSNAM,44
         B     GO
PARMIN   BCTR  R4,0                     PARM -1
         MVC   DS1DSNAM(44),UPPER
         EX    R4,DSNMVC                MOVE DSN IN
GO       OC    DS1DSNAM,UPPER
         MVC   MSGDSN(44),DS1DSNAM
         TPUT  MSG,78
         L     R3,CVTPTR
         USING CVTMAP,R3
NEXTUCB  L     R15,CVTUCBSC
         LA    R1,PARMLIST
         BALR  R14,R15
         LTR   R15,R15
         BNZ   SRCHEND
         L     R7,UCBADDR
         TM    UCBSTAT,X'80'            ONLINE ?
         BNO   NEXTUCB                  NOP...
         MVC   VOLSER(6),UCBVOLI        MOVE VOLSER FOR OBTAIN
         OBTAIN DSCBIN1                 GET FORMAT 1 DSCB
         LTR   R15,R15                  CHECK RETURN CODE
         BNZ   NEXTUCB                  NOT FOUND...TRY NEXT
         ST    R6,WORK
         TRANS CUU(4),WORK+2
         MVI   CUU,C' '
         TPUT  INFORM,80
         B     NEXTUCB
SRCHEND  TPUT  TRAILOR,80
         L     R13,SAVEAREA+4
         LM    R14,R12,12(R13)
         BR    R14
HEXRTN   UNPK  HEXDONE(11),HEX(6)
         NC    HEXDONE(11),=X'0F0F0F0F0F0F0F0F0F0F00'
         TR    HEXDONE(10),TRTABLE
         BR    R10
TRTABLE  DC    C'0123456789ABCDEF'
HEX      DS    CL6
HEXDONE  DS    CL11
HEADER   DC    CL24' '
         DC    CL32'***** VTOC SEARCH FACILITY *****'
         DC    CL24' '
PROMPT   DC    C'ENTER FULLY QUALIFIED DSN (NO '''
         DC    X'A2'
         DC    C') - '
INBUFF   DC    CL44' '
MSG      DC    CL33'SEARCHING ALL DASD VOLUMES FOR : '
MSGDSN   DC    CL44' '
START    EQU   *
INFORM   DC    C' DSN FOUND ON VOLUME: '
VOLSER   DC    CL6' '
         DC    C' THRU PATH'
CUU      DC    CL4' '
         DC    CL(80-(*-START))' '
TRAILOR  DC    CL80'ALL ONLINE DASD VTOCS HAVE BEEN SEARCHED '
DSNMVC   MVC   DS1DSNAM(0),2(R3)    DSN IN
DSCBIN1  CAMLST   SEARCH,DS1DSNAM,VOLSER,DS1FMTID
         IECSDSL1 (1)                FORMAT 1 DSCB LAYOUT
UPPER    DC    CL44' '
WORK     DS    F
SAVEAREA DS    18F
PARMLIST DC    A(WORKAREA)
         DC    A(DEVCLASS)
         DC    X'80',AL3(UCBADDR)
         DS    0D
WORKAREA DS    100X'00'
UCBADDR  DS    F
DEVCLASS DC    AL1(UCB3DACC)
         DSECT
         CVT   DSECT=YES
         IEFUCBOB
         END

          © Xephon 1989
