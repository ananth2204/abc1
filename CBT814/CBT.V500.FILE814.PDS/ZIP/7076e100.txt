Using the RACF Panel Driver Interface (PDI)

The RACF PDI is a seldom used interface. Surprisingly, it
allows for a number of useful applications. This interface
connects a TSO user's ISPF panel to a requested RACF ISPF
panel, avoiding many of the unnecessary screens. Experienced
users can save time and inexperienced users won't have
confusing options to deal with.

The PDI uses program ICHSPF03. Before selecting the
program, three variables can be set to let ICHSPF03 determine
which panel to display. The variables (and their possible
values) are:

ICHFUNCT - ADD, CHG, DEL, ACC, DSP, or blank

ICHRESCL - A valid RACF resource class name or blank

ICHRESNM - A RACF resource name or blank.

If all variables are blank before selecting the program, the next
panel will be the RACF Services Options Menu (ICHP00).

From ISPF, a dialog is established. Depending on the
application, the PDI variables are set in the background or
foreground. The variables are put in the shared pool.
ICHSPF03 is selected, and processing continues at the next
RACF panel.

The following example uses a simple entry panel to set the
variables and a CLIST to control the dialog.


CLIST

PROC 0 DEBUG
/* GDE: RACF/MVS PANEL DRIVER INTERFACE CLIST                        */
/* DOC: THIS CLIST ALLOWS THE USER TO INTERFACE BETWEEN THIS         */
/*      CLIST (DIALOG) AND THE RACF PANELS.  SEE THE RACF MACROS     */
/*      AND INTERFACES MANUAL FOR MORE INFORMATION.                  */
IF &DEBUG = DEBUG THEN +
  CONTROL MAIN LIST CONLIST SYMLIST
ELSE +
  CONTROL MAIN NOLIST NOCONLIST NOSYMLIST
SET FUNC = &Z
START: +
ISPEXEC  DISPLAY  PANEL(RACFP01)
SET RC = &LASTCC
IF &RC ^= 0 THEN  +
  DO
  IF &RC = 8 THEN  +
    ISPEXEC  SETMSG MSG(RACF002)
  EXIT CODE(0)
  END
ISPEXEC VPUT (ICHFUNCT ICHRESCL ICHRESNM)
SET RC = &LASTCC
IF &RC ^= 0 THEN  +
  DO
  EXIT CODE(&RC)
  END
ISPEXEC SELECT PGM(ICHSPF03)
SET RC = &LASTCC
IF &RC ^= 0 THEN  +
  DO
  EXIT CODE(&RC)
  END
GOTO START
END


PANEL

)ATTR DEFAULT(%+_)
      /*  % TYPE(TEXT) INTENS(HIGH)      defaults displayed for      */
      /*  + TYPE(TEXT) INTENS(LOW)       information only            */
      /*  _ TYPE(INPUT) INTENS(HIGH) CAPS(ON) JUST(LEFT)             */
  $ TYPE(INPUT) INTENS(LOW) PAD(_)    /* input field padded with '_' */
  ! TYPE(INPUT) INTENS(LOW) JUST(LEFT)
)BODY
%---------------------  RACF/MVS PANEL DRIVER INTERFACE  -----------------------
%COMMAND ===>_ZCMD                                                             +
%
+   Enter a function code and, optionally, a resource class and name:
+
+
+                                  A(dd)    \
+                                  M(odify)   \
+             Function :$FUNC   +  D(elete)    >---  Valid codes
+                                  P(ermit)   /
+                                  L(ist)   /
+
+       Resource Class :$ICHRESCL+ A class defined in the CDT
+
+
+        Resource Name :!ICHRESNM                                    +
+
+
+ Pressing ENTER with no input displays the RACF Service Options Menu.
+ (GENERAL RESOURCES is assumed if a resource class is not entered)
+
+
+     Pf1%Help+     Pf3%End+
+
)INIT
  .HELP = RFOPRH10           /* INSERT NAME OF TUTORIAL PANEL    */
  IF (&FUNC ^= &Z)
      &FUNC = TRANS (&TFUNC A,ADD M,MODIFY D,DELETE P,PERMIT L,LIST)
  .cursor = func
)PROC
  &TFUNC = TRUNC (&FUNC ,1)
  &ICHFUNCT = TRANS (&TFUNC A,ADD M,CHG D,DEL P,ACC L,DSP)
)END

Another implementation would be to interface the ADD
DATASET 'profile name' with a TSO define alias dialog. This
would ensure that every alias added to the catalog had a
corresponding RACF dataset profile.

Steve Windhausen
Computer Technical Specialist
County of Onondaga (USA)                                    c Xephon 1992

