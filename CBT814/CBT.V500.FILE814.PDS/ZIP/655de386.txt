A dynamic allocation macro

MVS provides the Assembler programmer with the ability to
allocate a dataset dynamically, ie to allocate a dataset from
program code as opposed to coding JCL DD statements.
Unfortunately, the IBM-supplied macro, DYNALLOC, does
no more than issue SVC 99 (the dynamic allocation SVC),
leaving the programmer with the often complex and confusing
task of coding request blocks and text units, which control
functions and options.

The following macro, ALLOC, performs all the necessary
functions to allocate an existing dataset or a new SYSOUT
dataset dynamically, for example:

ALLOC DDN=SYSUT1,DSN=SYS1.MYLIB,DISP=SHR
ALLOC DDN=SYSUT1,DSNLOC=MY_DSN,DISP=SHR
ALLOC DDN=SYSUT2,SYSOUT=A

On return from the macro, Register 15 should be tested for zero
to determine success or failure.  If Register 15 is non-zero,
Register 1 contains the return code (first two bytes) and reason
code (last two bytes).  These are fully documented in MVS
SPL: System Macros and Facilities (GC28-1150) in the chapter
on dynamic allocation.

This macro has been used successfully on a MVS/XA 2.2
system, and MVS/ESA 3.1.1 and 3.1.3.

*** THIS MACRO WILL DYNAMICALLY ALLOCATE AN EXISTING DATASET OR A
* SYSOUT DATASET. THE PARAMETERS ARE:
* - DDN=    THE DD NAME EXPLICITLY IE 'DDN=SYSUT1'
* - DDNLOC- THE DDNAME IS AT A STORAGE LOCATION IE 'DDNLOC=MY_DD'
*   (SPECIFY ONLY ONE OF THE ABOVE TWO)
* - DSN=    THE DATASET NAME EXPLICITLY IE 'DSN=SYS1.MYLIB'
* - DSNLOC= THE DSNAME IS AT A STORAGE LOCATION IE 'DDNLOC=MY_DSNAME'
*   (SPECIFY ONLY ONE OF THE ABOVE TWO)
* - MEM=    THE MEMBER NAME EXPLICITLY IE 'MEM=MEMBER1'
* - MEMLOC= THE MEMBER NAME IS AT A STORAGE LOCATION IE 'MEMLOC=MY_MEM'
*   (SPECIFY ONLY ONE OF THE ABOVE TWO, IF REQUIRED.)
* - DISP=   DISPOSITION MUST BE SHR, OLD OR MOD (DEFAULT IS SHR).
* - SYSOUT= THE DDNAME SPECIFIED IS TO BE A SYSOUT DATASET.
*     EG 'SYSOUT=*' OR 'SYSOUT=A'
*     THIS PARAMETER SHOULD ONLY BE ACCOMPANIED BY DDN= OR DDNLOC=.
*     IF ANY OTHERS ARE CODED WITH SYSOUT=, THEY ARE IGNORED.
*   THE LOCATION TYPE PARAMETERS CAN ALSO BE SPECIFIED AS A REGISTER,
*   WHERE THE REGISTER POINTS TO THE STORAGE LOCATION.
*     EG ' ALLOC  DDN=DD1,DSNLOC=(4),DISP=SHR',
*   WHERE R4 POINTS TO THE DATASET NAME TO BE ALLOCATED.
*   WHEN REGISTERS ARE SPECIFIED, STORAGE LOCATION LENGTHS ARE ASSUMED
*   AS FOLLOWS:
* - DDNLOC: ASSUMED LENGTH OF 8
* - DSNLOC: ASSUMED LENGTH OF 44
* - MEMLOC: ASSUMED LENGTH OF 8
         MACRO
         ALLOC &DDN=,&DDNLOC=,                                         X
               &DSN=,&DSNLOC=,                                         X
               &MEM=,&MEMLOC=,                                         X
               &DISP=SHR,                                              X
               &SYSOUT=
&S       SETC '&SYSNDX'  SET 'S' VARIABLE TO GENERATE UNIQUE LABELS
&DDNREG  SETC 'NO'       SET 'DDNREG' VARIABLE TO INITIAL VALUE OF 'NO'
&DSNREG  SETC 'NO'       SET 'DSNREG' VARIABLE TO INITIAL VALUE OF 'NO'
&MEMREG  SETC 'NO'       SET 'MEMREG' VARIABLE TO INITIAL VALUE OF 'NO'
&BRACKET SETC '('        SET 'BRACKET VARIABLE TO '(' FOR SPEC BY REG
* VALIDATE PARAMETERS SPECIFIED                                      *
.CHECK00 AIF   ('&DDN' EQ '' AND '&DDNLOC' EQ '').ERROR00
.CHECK10 AIF   ('&DDN' NE '' AND '&DDNLOC' NE '').ERROR10
.CHECK15 AIF   ('&SYSOUT' NE '').PARMOK
.CHECK20 AIF   ('&DSN' EQ '' AND '&DSNLOC' EQ '').ERROR20
.CHECK30 AIF   ('&DSN' NE '' AND '&DSNLOC' NE '').ERROR30
.CHECK40 AIF   ('&MEM' NE '' AND '&MEMLOC' NE '').ERROR40
.CHECK50 AIF   ('&DISP' EQ 'SHR').PARMOK
         AIF   ('&DISP' EQ 'OLD').PARMOK
         AIF   ('&DISP' EQ 'MOD').PARMOK
         AGO   .ERROR50
.ERROR00 MNOTE 16,'EITHER DDN OR DDNLOC MUST BE SPECIFIED'
         MEXIT
.ERROR10 MNOTE 16,'EITHER DDN OR DDNLOC MUST BE SPECIFIED BUT NOT BOTH'
         MEXIT
.ERROR20 MNOTE 16,'EITHER DSN OR DSNLOC MUST BE SPECIFIED'
         MEXIT
.ERROR30 MNOTE 16,'EITHER DSN OR DSNLOC MUST BE SPECIFIED BUT NOT BOTH'
         MEXIT
.ERROR40 MNOTE 16,'EITHER MEM OR MEMLOC CAN BE SPECIFIED BUT NOT BOTH'
         MEXIT
.ERROR50 MNOTE 16,'DISP MUST BE SPECIFIED AS OLD,SHR OR MOD'
         MEXIT
.PARMOK  ANOP
.BRACK1  AIF   ('&DDNLOC' EQ '').BRACK2
         AIF   ('&DDNLOC'(1,1)  NE '&BRACKET').BRACK2    REG SPEC ????
&DDNREG  SETC 'YES'                THEN  SET 'DDNREG' VARIABLE TO 'YES'
.BRACK2  AIF   ('&DSNLOC' EQ '').BRACK3
         AIF   ('&DSNLOC'(1,1)  NE '&BRACKET').BRACK3    REG SPEC ????
&DSNREG  SETC 'YES'                THEN  SET 'DSNREG' VARIABLE TO 'YES'
.BRACK3  AIF   ('&MEMLOC' EQ '').START
         AIF   ('&MEMLOC'(1,1)  NE '&BRACKET').START     REG SPEC ????
&MEMREG  SETC 'YES'                THEN  SET 'MEMREG' VARIABLE TO 'YES'
.START   AIF ('&DDNLOC' EQ '').A00         DDNLOC SPECIFIED ??????????
         AIF ('&DDNREG' EQ 'YES').DDNREG   DDNLOC SPECIFIED AS REG ????
         MVC DDD&S+6(L'&DDNLOC),&DDNLOC    MOVE DDNAME IN FROM 'DDNLOC'
         MVC DDA&S+6(L'&DDNLOC),&DDNLOC    MOVE DDNAME IN FROM 'DDNLOC'
         AGO  .A00
.DDNREG  MVC DDD&S+6(8),0&DDNLOC    MOVE DDNAME IN FROM 0 OFF OF REG N
         MVC DDA&S+6(8),0&DDNLOC    MOVE DDNAME IN FROM 0 OFF OF REG N
* FREE SPECIFIED DDNAME BEFORE WE TRY AND ALLOCATE IT.                *
.A00     DS    0H
         LA  1,RBPD&S                R1 POINTS TO REQUEST BLOCK POINTER
         B   SVCD&S                  BRANCH AROUND CONSTANTS...........
DDD&S    DC X'0001',X'0001',X'0008',CL8'&DDN '        DDNAME TEXT UNITS
PTRD&S   DC A(DDD&S+X'80000000')           POINTER TO DDNAME TEXT UNITS
RBD&S    DC AL1(20,2,0,0)                                 REQUEST BLOCK
         DS F
         DC A(PTRD&S)
         DC AL4(0,0)
RBPD&S   DC A(RBD&S+X'80000000')               POINTER TO REQUEST BLOCK
SVCD&S   SVC 99                     ......AND ISSUE SVC 99 TO DEALLOC..
* NOW SET UP TEXT UNITS AND POINTERS FOR THE REQUESTED ALLOCATION     *
         DS  0H
         AIF ('&SYSOUT' NE '').A20                IS IT SYSOUT FILE???
         AIF ('&DSNLOC' EQ '').A10                DSN OR DSNLOC ??....
         AIF ('&DSNREG' EQ 'YES').DSNREG   DSNLOC SPECIFIED AS REG ????
         MVC DSN&S+6(L'&DSNLOC),&DSNLOC       MOVE DSN IN FROM 'DSNLOC'
         AGO  .A10
.DSNREG  MVC DSN&S+6(44),0&DSNLOC   MOVE DSNAME IN FROM 0 OFF OF REG N
.A10     ANOP
         AIF ('&MEMLOC' EQ '').A20          MEM OR MEMLOC SPECIFIED..??
         AIF ('&MEMREG' EQ 'YES').MEMREG   MEMLOC SPECIFIED AS REG ????
         MVC MEM&S+6(L'&MEMLOC),&MEMLOC       MOVE MEM IN FROM 'MEMLOC'
         AGO  .A20
.MEMREG  MVC MEM&S+6(8),0&MEMLOC   MOVE MEMNAME IN FROM 0 OFF OF REG N
.A20     ANOP
         LA  1,RBPA&S                R1 POINTS TO REQUEST BLOCK POINTER
         B   SVCA&S                 BRANCH AROUND CONSTANTS............
DDA&S    DC X'0001',X'0001',X'0008',CL8'&DDN '        DDNAME TEXT UNITS
         AIF ('&SYSOUT' NE '').A45                IS IT SYSOUT FILE???
DSN&S    DC X'0002',X'0001',X'002C'                   DSNAME TEXT UNITS
         DC CL44'&DSN '
         AIF ('&MEM' EQ '' AND '&MEMLOC' EQ '').A30         MEMBER ????
MEM&S    DC X'0003',X'0001',X'0008',CL8'&MEM '           MEM TEXT UNITS
.A30     AIF ('&DISP' NE 'SHR').A31
DISP&S   DC X'0004',X'0001',X'0001',X'08'          DISP=SHR  TEXT UNITS
.A31     AIF ('&DISP' NE 'OLD').A32
DISP&S   DC X'0004',X'0001',X'0001',X'01'          DISP=OLD  TEXT UNITS
.A32     AIF ('&DISP' NE 'MOD').A33
DISP&S   DC X'0004',X'0001',X'0001',X'02'          DISP=MOD  TEXT UNITS
.A33     AIF ('&MEM' EQ '' AND '&MEMLOC' EQ '').A40         MEMBER ????
PRTA&S   DC A(DDA&S,DSN&S,MEM&S,DISP&S+X'80000000')   PTR TO TEXT UNITS
         AGO .A50
.A40     ANOP
PRTA&S   DC A(DDA&S,DSN&S,DISP&S+X'80000000')     POINTER TO TEXT UNITS
         AGO .A50
* FILE IS SYSOUT NOT DSNAME.....SO ALLOCATE SYSOUT=* OR SYSOUT=N      *
.A45     AIF ('&SYSOUT' EQ '*').A46                IS IT SYSOUT=* ?????
OUT&S    DC X'0018',X'0001',X'0001',C'&SYSOUT'     SYSOUT=X  TEXT UNITS
         AGO .A47
.A46     ANOP                                      IT IS SYSOUT=N ?????
OUT&S    DC X'0018',X'0000'                        SYSOUT=*  TEXT UNITS
.A47     ANOP
PTRA&S   DC A(DDA&S,OUT&S+X'80000000')     POINTER TO SYSOUT TEXT UNITS
         OI   RBA&S+2,X'10'           CHANGE RB FLAGS TO SYSOUT (X'10')
         AGO .A50
* NOW DEFINE REQUEST BLOCK, REQUEST BLOCK POINTER AND ISSUE SVC 99    *
.A50     ANOP
RBA&S    DC AL1(20,1,0,0)                                 REQUEST BLOCK
         DS F
         DC A(PTRA&S)
         DC AL4(0,0)
RBPA&S   DC A(RBA&S+X'80000000')               POINTER TO REQUEST BLOCK
SVCA&S   SVC 99                   ......AND ISSUE SVC 99 TO ALLOCATE..
         L   1,RBA&S+4           LOAD R1 WITH REASON CODE AND INFO CODE
         MEXIT
         MEND


Steven Leek
Systems Programmer
British Gas South Eastern (UK)                     c Xephon 1991

