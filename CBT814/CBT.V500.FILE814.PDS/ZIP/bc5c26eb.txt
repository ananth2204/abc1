Sending high priority messages under TSO

TSO users are able to send messages to the operator console via the TSO SEND
command.  The message appears at the console as a low priority message.  It is
not highlighted and is removed when space is required for other messages.

The program SEND@ provides the ability, particularly for system programmers, to
send high priority messages to the console. They are highlighted and do not
disappear until deleted by the operator.

***********************************************************************
* SEND MESSAGES OF HIGH IMPORTANCE FROM TSO SCREEN TO OPERATOR CONSOLE.
* MESSAGES REMAIN ON CONSOLE AND MUST BE DELETED BY OPERATOR.
***********************************************************************
SEND@    CSECT
         SAVE  (14,6),,SEND@_&SYSDATE  SAVE REGISTERS, IDENTIFY MODULE
         LR    R2,R15                  LOAD BASE REGISTER
         USING SEND@,R2                TELL IT ASSEMBLER
         ST    R13,SAVEAREA+4          CHAIN
         LR    R3,R13                     SAVEAREAS
         LA    R13,SAVEAREA
         ST    R13,8(R3)
         SLR   R6,R6                    INDICATE: USERID NOT EXTRACTED
         TPUT  PROMPT,L'PROMPT          PROMPT USER FOR MESSAGE
TGET     TGET  MSG,L'MSG                GET MESSAGE FROM USER
         LTR   R4,R1                    LENGTH OF MESSAGE
         BZ    PROGEND                  0 = BLANK INPUT = FINISH
         LTR   R6,R6                    USERID ALREADY EXTRACTED ?
         BNZ   MOVE                      YES
         EXTRACT TIOTADR,'S',FIELDS=(TIOT) GET ADDRESS OF TIOT
         L     R6,TIOTADR              USERID AT OFFSET ZERO IN TIOT
         MVC   MCS,WMCS                MOVE 1ST PART OF WTO LIST
MOVE     LA    R5,MSG(R4)              ADDR. OF CHAR BEHIND MSG TEXT
         MVC   0(5,R5),=C' --- '       MOVE BEHIND MESSAGE TEXT
         MVC   5(7,R5),0(R6)           MOVE USERID BEHIND OF IT
         MVC   5+7(4,R5),WCODE         MOVE LAST PART OF WTO LIST
         LA    R4,4+1+5+7(R4)          COMPUTE LENGTH OF WTO LIST
         STH   R4,LEN                  LENGTH INTO WTO LIST
         LA    R1,LIST                 ADDR. OF WTO LIST
         WTO   MF=(E,(R1))             ISSUE WTO
         B     TGET                    NEXT INPUT LINE
PROGEND  L     R13,4(R13)
         RETURN (14,6),RC=0            LOAD REGISTERS AND RETURN
SAVEAREA DS    18F
TIOTADR  DS    A
LIST     DS    0F                      WTO LIST
LEN      DS    AL2                     LENGTH
MCS      DS    BL2                     WMCS
         DC    ' '                     BLANK IN FRONT OF MESSAGE
MSG      DS    CL111                   MESSAGE TEXT (MAX. LENGTH)
         DS    CL5                     ' --- '
         DS    CL7                     USERID BEHIND MESSAGE TEXT
         DS    CL4                     WCODE
PROMPT   DC    C'ENTER MESSAGE FOR OPERATOR -'
WTO      WTO   ' ',ROUTCDE=(2),DESC=(2),MF=L
WMCS     EQU   WT0+2
WCODE    EQU   WT0+5
         LTORG
         END   SEND@


Walter Wiedemann
Consultant (West Germany)     © Walter Wiedemann 1987



