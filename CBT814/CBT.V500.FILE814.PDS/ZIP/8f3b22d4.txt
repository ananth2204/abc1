ISPF library display


LIBLIST CLIST

PROC 1 DDNIMI
/**********************************************************************/
/*   DDNAME OF DESIRED LIBRARY CONCATENATION IS NEEDED                */
/* CONTROL MSG LIST CONLIST SYMLIST
/**********************************************************************/
/*   WE NEED AN ISPF TABLE TO STORE DATASET NAMES                     */
/*   NAME FOR THE TABLE IS GENERATED                                  */
/**********************************************************************/
SET &AVAIN = &STR()
SET &TARK = &SUBSTR(7:8,&STR(&SYSTIME))
SET &TABLE = &STR(DSNTBL)&TARK
/**********************************************************************/
/* CREATE THE TABLE, IF NOT SUCCESSFUL, TRY TO ERASE                  */
/**********************************************************************/
  ISPEXEC TBCREATE &TABLE KEYS(D0) NAMES(D1 D2 D3 MNINII D4) +
                          REPLACE WRITE SHARE
  SET &D0 = 0
  SET &CRC = &LASTCC
  IF &CRC ^= 0 THEN +
    DO
    IF &CRC < 12 THEN +
      DO
      ISPEXEC TBCLOSE &TABLE
      ISPEXEC TBERASE &TABLE
      SET &ERC = &LASTCC
      IF &ERC ^= 0 THEN +
        DO
        WRITE LISTAST ***** &TABLE, TBERASE FAILED, RC=&ERC
        EXIT
        END
                          /*  8 - TABLE NOT IN OUTPUT LIB          */
                          /* 12 - TABLE IN USE, ENQ FAILED         */
                          /* 16 - TABLE OUTPUT LIBRARY NOT         */
                          /*      ALLOCATED                        */
                          /* 20 - SEVERE ERROR                     */
      END
    WRITE LISTAST ***** &TABLE, TBCREATE UNSUCCESSFUL, RC=&CRC
    EXIT
    END
                          /*  4 - DUPLICATE TABLE EXISTS BUT       */
                          /*      'REPLACE' SPECIFIED              */
                          /*  8 - TABLE ALREADY EXISTS, 'REPLACE'  */
                          /*      NOT SPECIFIED                    */
                          /* 12 - TABLE IN USE, ENQ FAILED         */
                          /* 16 - WRITE MODE SPECIFIED AND TABLE   */
                          /*      INPUT LIBRARY IS NOT ALLOCATED   */
                          /* 20 - SEVERE ERROR                     */
/**********************************************************************/
/*   INITIALIZE SYSOUTTRAP PROCESSING                                 */
/**********************************************************************/
SET &SYSOUTTRAP = 1000
/**********************************************************************/
/*   ISSUE TSO COMMAND                                                */
/**********************************************************************/
LISTA ST
SET &LUKU = &SYSOUTLINE    /* NUMBER OF DATA LINES GENERATED          */
SET &SYSOUTTRAP = 0
SET &NUM = 0
SET &JUTTA = &&SYSOUTLINE1
IF &STR(&SUBSTR(1:8,&STR(&JUTTA))) = &STR(LISTA ST) THEN +
   SET &NUM = &NUM + 1
/**********************************************************************/
/*   PROCESS THE OUTPUT FROM LISTALLOC STATUS COMMAND                 */
/**********************************************************************/
DO WHILE &NUM < &LUKU
  SET &NUM = &NUM + 1
  SET &JUTTA = &&SYSOUTLINE&NUM
  IF &STR(&SUBSTR(1:8,&STR(&JUTTA))) ^= &STR(--DDNAME) THEN +
    DO
    SET &POS = 0
    SET &POS = &SYSINDEX(&STR( ),&STR(&JUTTA))
    IF &POS = 0 THEN +
      DO
/* DSNAME IN VARIABLE JUTTA                                           */
      SET &NUM = &NUM + 1
      SET &JUTTB = &&SYSOUTLINE&NUM
      SET &JUTTB = &JUTTB
      SET &JUTTD = &STR()
      SET &POS = 0
      SET &POS = &SYSINDEX(&STR( ),&STR(&JUTTB))
      IF &POS = 0 THEN +
        DO
        SET &JUTTK = &JUTTB
        END
      ELSE +
        DO
        SET &JUTTD = &SUBSTR(1:&POS-1,&STR(&JUTTB))
        SET &AVAIN = &JUTTD
        SET &JUTTK = &SUBSTR(10:&LENGTH(&STR(&JUTTB)),&STR(&JUTTB))
        END
/* DDNAME IN VARIABLE JUTTD AND DISP PARAMETER IN VARIABLE JUTTK      */
      IF &AVAIN = &DDNIMI THEN +
        DO
        SET &D0 = &D0 + 1
        SET &D1 = &JUTTA
        SET &D2 = &JUTTD
        SET &D3 = &JUTTK
        ISPEXEC TBADD &TABLE
        SET &ARC = &LASTCC
        IF &ARC ^= 0 THEN +
          DO
          WRITE LISTAST ***** &TABLE, TBADD FAILED, RC=&ARC
          GOTO TCLOSE
          END
                          /* 4  - NUMBER OF ROWS PARAMETER WAS     */
                          /*      SPECIFIED, BUT ONLY STORAGE FOR A*/
                          /*      SINGLE ROW WAS OBTAINED          */
                          /* 8  - A ROW WITH THE SAME KEY ALREADY  */
                          /*      EXISTS                           */
                          /* 12 - TABLE IS NOT OPEN                */
                          /* 16 - NUMERIC CONVERSION ERROR         */
                          /* 20 - SEVERE ERROR                     */
 /*     WRITE DSN=&JUTTA,DDNIMI=&JUTTD,DISP=&JUTTK                 */
        END
      END
    END
END
TCLOSE: +
SET &JUTTD = &DDNIMI
SET &TAVLA = &TABLE
/**********************************************************************/
/*   EXAMINE TABLE STATUS                                             */
/**********************************************************************/
ISPEXEC TBTOP &TABLE
ISPEXEC TBQUERY &TABLE ROWNUM(RIVIT)
SET &QUERC = &LASTCC
IF &QUERC ^= 0 THEN +
  DO
  WRITE LISTAST ***** TBQUERY UNSUCCESSFUL, RC=&QUERC
  END
WRITE LISTAST ***** LISTA STATUS COMMAND GENERATED &RIVIT LINES
/**********************************************************************/
/*   DISPLAY TABLE UNTIL END COMMAND IS ISSUED                        */
/**********************************************************************/
DO WHILE &DPTRC < 8
ISPEXEC TBDISPL &TABLE PANEL(LISTSPAN)
SET &DPTRC = &LASTCC
/**********************************************************************/
/*   THERE IS SOMETHING IN THE COMMAND FIELD, IT SHOULD BE            */
/*   A MEMBER NAME                                                    */
/**********************************************************************/
IF &STR(&VALI) ^= &STR(        ) THEN +
  DO
  SET &MNINII = &STR()
  SET &MODNIM = &SUBSTR(1:&LENGTH(&STR(&VALI)),&VALI)
  SET &MODNIM = &MODNIM
/*WRITE LISTAST ***** SOME COMMAND, >>>&MODNIM<<< *****
  ISPEXEC TBTOP &TABLE
  SET &TOPRC = &LASTCC
  IF &TOPRC ^= 0 THEN +
    DO
    WRITE LISTAST ***** TBTOP UNSUCCESSFUL, RC=&TOPRC
    END
  SET &LASKU = 0
  DO WHILE &RIVIT > &LASKU
  SET &LASKU = &LASKU + 1
  ISPEXEC TBSKIP &TABLE
/**********************************************************************/
/*   SYSDSN COMMAND CHECKS WHETHER THE MEMBER EXISTS IN ANY LIBRARY.  */
/*   EVERY LIBRARY IN CONCATENATION WILL BE CHECKED.                  */
/**********************************************************************/
  SET &ONKO = &STR(&SYSDSN('&D1(&MODNIM)'))
  IF &STR(&ONKO) = &STR(OK) THEN +
    DO
    SET &MNINII = &STR(&MODNIM)
    END
  ELSE +
    DO
    IF &ONKO ^= &STR(MEMBER NOT FOUND) THEN +
      WRITE LISTAST ***** SYSDSN SAYS: &ONKO *****
    SET &MNINII = &STR()
    END
/**********************************************************************/
/*   UPDATE TABLE                                                     */
/**********************************************************************/
  ISPEXEC TBMOD &TABLE
  SET &MODRC = &LASTCC
  IF &MODRC ^= 0 THEN +
    DO
    WRITE LISTAST ***** TBMOD UNSUCCESSFUL , RC=&MODRC
    END
  END
  ISPEXEC TBTOP &TABLE
  SET &TOPRC = &LASTCC
  IF &TOPRC ^= 0 THEN +
    DO
    WRITE LISTAST ***** TBTOP FAILED, RC=&TOPRC
    END
  END
/**********************************************************************/
/*   PROCESS LINE COMMANDS                                            */
/**********************************************************************/
ELSE +
  DO
  IF &TEKO = &STR(S) THEN +
    DO
/**********************************************************************/
/*   ===> ISPF DATASET LIST UTILITY, 3.4                              */
/**********************************************************************/
    SET &VALI = &STR()
    SET &ZDLDSNLV = &STR(&D1)
    SET &ZDLPVL   = &STR()
    SET &ZUPIV    = &STR(TOTAL)
    SET &ZUPCD    = &STR(YES)
    ISPEXEC VPUT (VALI ZDLDSNLV ZDLPVL ZUPIV ZUPCD)
    ISPEXEC SELECT PGM(ISRUDL) PARM(ISRUDLP)
    SET &D4 = &STR(HAETTU)
    ISPEXEC TBMOD &TABLE
    SET &MRC = &LASTCC
    IF &MRC ^= 0 THEN +
      DO
      WRITE LISTAST ***** &TABLE, TBMOD FAILED, RC=&MRC
      END
                            /*  8 - KEYED TABLES: KEYS DID NOT MATCH,*/
                            /*      NEW ROW ADDED                    */
                            /* 12 - TABLE IS NOT OPEN                */
                            /* 16 - NUMERIC CONVERSION ERROR         */
                            /* 20 - SEVERE ERROR                     */
    END
  IF &TEKO = &STR(L) THEN +
    DO
/**********************************************************************/
/*   ===> ISPF LIBRARY UTILITY, 3.1                                   */
/**********************************************************************/
    SET &VALI = &STR()
    SET &DSN  = &STR(&D1)
    ISPEXEC VPUT (VALI DSN)
    ISPEXEC SELECT PGM(ISRUDA) PARM(ISRUDA1)
    SET &D4 = &STR(LIBATTU)
    ISPEXEC  TBMOD &TABLE
    SET &LRC = &LASTCC
    IF &LRC ^= 0 THEN +
      DO
      WRITE LISTAST ***** &TABLE, TBMOD FAILED, RC=&LRC
      END
                            /*  8 - KEYED TABLES: KEYS DID NOT MATCH,*/
                            /*      NEW ROW ADDED                    */
                            /* 12 - TABLE IS NOT OPEN                */
                            /* 16 - NUMERIC CONVERSION ERROR         */
                            /* 20 - SEVERE ERROR                     */
    END
  IF &TEKO = &STR(E) THEN +
    DO
/**********************************************************************/
/*   ===> EDIT                                                        */
/**********************************************************************/
    ISPEXEC EDIT DATASET('&D1')
    SET &ERC = &LASTCC
    SET &D4 = &STR(EDIT RC=&ERC)
                            /*  4 - DATA NOT SAVED                   */
                            /* 14 - MEMBER IN USE OR DATASET IN USE  */
                            /* 16 - NO MEMBERS IN LIBRARY            */
                            /* 20 - SEVERE ERROR                     */
    ISPEXEC  TBMOD &TABLE
    SET &MRC = &LASTCC
    IF &MRC ^= 0 THEN +
      DO
      WRITE LISTAST ***** &TABLE, TBMOD FAILED, RC=&MRC
      END
                            /*  8 - KEYED TABLES: KEYS DID NOT MATCH,*/
                            /*      NEW ROW ADDED                    */
                            /* 12 - TABLE IS NOT OPEN                */
                            /* 16 - NUMERIC CONVERSION ERROR         */
                            /* 20 - SEVERE ERROR                     */
    END
  IF &TEKO = &STR(B) THEN +
    DO
/**********************************************************************/
/*   ===> BROWSE                                                      */
/**********************************************************************/
    SET &MBR = &STR()
    ISPEXEC BROWSE DATASET('&D1&MBR')
    SET &ERC = &LASTCC
    SET &D4 = &STR(BROW RC=&ERC)
                            /*  4 - DATA NOT SAVED                   */
                            /* 14 - MEMBER IN USE OR DATASET IN USE  */
                            /* 16 - NO MEMBERS IN LIBRARY            */
                            /* 20 - SEVERE ERROR                     */
    ISPEXEC  TBMOD &TABLE
    SET &MRC = &LASTCC
    IF &MRC ^= 0 THEN +
      DO
      WRITE LISTAST ***** &TABLE, TBMOD FAILED, RC=&MRC
      END
                            /*  8 - KEYED TABLES: KEYS DID NOT MATCH,*/
                            /*      NEW ROW ADDED                    */
                            /* 12 - TABLE IS NOT OPEN                */
                            /* 16 - NUMERIC CONVERSION ERROR         */
                            /* 20 - SEVERE ERROR                     */
    END
  END
END
SET &DPTRC = &LASTCC
IF &DPTRC ^= 0 THEN +
  DO
  WRITE LISTAST ***** &TABLE, TBDISPLAY ENDED, PK = &DPTRC
  END
/**********************************************************************/
/*   CLOSE TABLE                                                      */
/**********************************************************************/
ISPEXEC TBCLOSE &TABLE
SET &CLRC = &LASTCC
IF &CLRC ^= 0 THEN +
  DO
  WRITE LISTAST ***** &TABLE, TBCLOSE FAILED, RC=&CLRC
  EXIT
  END
                            /* 12 - TABLE IS NOT OPEN                */
                            /* 16 - OUTPUT LIBRARY NOT ALLOCATED     */
                            /* 20 - SEVERE ERROR                     */
ELSE +
  DO
  WRITE LISTAST ***** &TABLE, TBCLOSE SUCCESSFUL
  END
/**********************************************************************/
/*   ERASE TABLE                                                      */
/**********************************************************************/
ISPEXEC TBERASE &TABLE
SET &ERC = &LASTCC
IF &ERC ^= 0 THEN +
  DO
  WRITE LISTAST ***** &TABLE, TBERASE FAILED, RC=&ERC
  EXIT
  END
                            /*  8 - TABLE NOT IN OUTPUT LIB          */
                            /* 12 - TABLE IN USE, ENQ FAILED         */
                            /* 16 - TABLE OUTPUT LIBRARY NOT         */
                            /*      ALLOCATED                        */
                            /* 20 - SEVERE ERROR                     */


LISTPAN PANEL

)attr default(%+")
   ! type(output) intens(low) just(left) pad(' ')
   @ type(output) intens(low) just(right) pad(' ')
   ? type(input) intens(high) just(left) caps(on)
   $ type(output) intens(high) just(left) pad(' ') caps(on)
)body expand(  )
%- - - +Concatenation of%&ddnimi+libraries%- - -
+COMMAND ===>"ZCMD      +                                    +SCROLL ===>"AMT +
+
+MEMBER NAME ===>?vali      +
+
+    ddname  ===>@juttd    +   +table   ===>@tavla   +
+
+LC:    Library name:                     Disp:+
+_____  _______________                   ______
)model rows(all)
"z+    !z                              + !z     +   $z      +     !z         +
)init
  .zvars = '(teko d1 d3 mninii d4)'
  &vali = '        '
  &teko = ''''
)end


Systems Programmer (Finland)                  c Xephon 1996

