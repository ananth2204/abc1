Altering the TSO default disk unit

The following TSO command, USET, was designed to allow
added flexibility when allocating TSO datasets.  Normally, the
default unit name for dataset allocations is defined in the User
Attribute Dataset (SYS1.UADS) and saved in storage (in the
PSCB) when the user logs on to TSO.  Any non-specific dataset
allocation will be directed to a device within the esoteric group
defined with the name.  This command allows the unit name to be
changed to another name for the duration of the TSO session or
until the command is invoked a subsequent time.  Additionally,
the command can be invoked with a '?' as the operand in order to
display the current unit name.

USET     TITLE 'TSO COMMAND PROCESSOR TO RESET TSO DEFAULT DISK UNIT'
USET     CSECT ,
* FUNCTION: ALLOWS A TSO USER TO DISPLAY OR CHANGE THE DEFAULT DISK
*           ALLOCATION UNIT NAME FOR THE LIFE OF THE TSO SESSION...
* INVOKING: USET ?          <--- TO DISPLAY THE CURRENT UNITNAME
*           USET SYSDA      <--- TO CHANGE UNITNAME TO SYSDA
* MACLIBS: SYS1.MACLIB
* LINKEDIT ATTRIBUTES:  REUS, AC(1)
* RETURN CODES: 0 = UNITNAME HAS BEEN DISPLAYED OR CHANGED
*               4 = OPERAND IS INVALID FOR ONE OF FOLLOWING REASONS:
*                   (1) OPERAND WAS OMITTED
*                   (2) OPERAND LENGTH EXCEEDS 8 CHARACTERS
         USING USET,R15            TEMPORARY ADDRESSABILITY
         SAVE  (14,12),T,'USET      &SYSDATE &SYSTIME'
         LA    R12,SAVEAREA        LOAD ADDRESS OF SAVE AREA
         ST    R13,4(,R12) .       PROVIDE BACKWARD
         ST    R12,8(,R13)  ..      & FORWARD CHAINING
         LR    R13,R12             COPY NEW SAVE AREA ADDRESS
         LR    R12,R15             COPY BASE ADDRESS
         DROP  R15 .               DROP TEMPORARY ADDRESSABILITY
         USING USET,R12            PERMANENT ADDRESSABILITY FOR PROGRAM
         L     R3,8(,R1)           GET ADDRESS OF CALLER'S PSCB
         L     R1,0(,R1)           GET ADDRESS OF COMMAND BUFFER
         LH    R2,0(,R1)           GET LENGTH OF COMMAND BUFFER
         SH    R2,2(,R1)           SUBTRACT OPERAND OFFSET
         SH    R2,=H'5'            SUBTRACT DESCRIPTOR WORD LENGTH
         BM    ERROR#01             ERROR IF NO OPERAND PRESENT
         CH    R2,=H'7'            DOES OPERAND EXCEED 8 BYTES
         BH    ERROR#01             YES, NOT A VALID UNIT NAME..
         AH    R1,2(,R1)           POINT TO
         LA    R1,4(,R1)            THE OPERAND
         MVC   NEWUNIT(0),0(R1)    *** EXECUTED BY NEXT INSTR ***
         EX    R2,*-6              SAVE THE UNIT NAME
         OC    NEWUNIT,=CL8' '      AND SET IT TO UPPER-CASE
         MVC   OLDUNIT,8(R3)       COPY THE EXISTING UNITNAME
         CLI   NEWUNIT,C'?'        IS IT A UNIT NAME DISPLAY REQUEST
         BE    ERROR#00             YES, NO CHANGES TO BE MADE
         MODESET KEY=ZERO,MODE=SUP   GET INTO KEY-0 SUPERVISOR STATE *
         MVC   8(8,R3),NEWUNIT        REPLACE THE UNIT NAME IN PSCB..*
         MODESET KEY=NZERO,MODE=PROB   GET BACK INTO PROBLEM STATE...*
EOJ      LA    R15,0               SET THE FINAL
RC       EQU   EOJ+3                 RETURN CODE...
         L     R13,4(,R13)    .    POINT TO PREVIOUS SAVE AREA
         RETURN (14,12),T,RC=(15)  TERMINATE THE TASK
ERROR#00 TPUT  ERRMSG00,L'ERRMSG00 TELL CALLER WHAT UNIT NAME IS NOW
         B     EOJ                  AND TERMINATE THE COMMAND.......
ERRMSG00 DC    C'CURRENT UNIT NAME IS: XXXXXXXX'
         ORG   *-8
OLDUNIT  DC    CL8' '
ERROR#01 TPUT  ERRMSG01,L'ERRMSG01 TELL CALLER ABOUT INVALID OPERAND
         MVI   RC,4                 SET THE FINAL RETURN CODE TO FOUR
         B     EOJ                   AND TERMINATE THE COMMAND.......
ERRMSG01 DC    C'UNIT NAME MUST BE IN THE RANGE OF 1 TO 8 CHARACTERS'
         LTORG ,
SAVEAREA DC    9D'0'
NEWUNIT  DC    CL8' '
         END


Paul J Horak
SEI (USA)  © SEI 1989

