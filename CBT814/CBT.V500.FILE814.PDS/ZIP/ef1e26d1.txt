Selective update of SYS1.IMAGELIB
SYS1.IMAGELIB includes some members that are either public in nature, eg
standard character sets, or not so easy to create, eg special character sets.
The same library may include members that are unique to a group of users, eg
COPYMODs or private FCBs.
If SYS1. IMAGELIB  is not write protected, public members could be destroyed or
modified by an unauthorised user, but if it is write protected, users will not
be able to update their private members, and systems programmers would have to
run the updates to their members.  In order to protect the library, as well as
to enable updates to private members, we protected the library by a password and
selectively bypassed protection by a program.  The program executes under MVS/SP
1.3 and MVS/XA 2.2 with no resource access facility product installed.
ASSEMBLER PROGRAM DESCRIPTION
The program checks the validity of the update request and if the request is
valid bypasses protection by putting the password bit in the JSCB on and linking
to IEBIMAGE.  A valid request can include FCB, COPYMOD, INCLUDE, and OPTION
statements of IEBIMAGE, and a NAME statement follows the COPYMOD or the FCB
statement.  The name in the NAME statement must be equal to positions 2 to 4 of
the JOBNAME of the job card, because positions 2 to 4 of the JOBNAME identify
the project that owns that member.
INVOKING THE PROGRAM
The programmers invoke the program by writing the following EXEC statement:
     //STEPNAME EXEC PGM=MLMIMAGE
The DD statements and the utility control statements are identical to IEBIMAGE
DD statements and control statements.
THE ASSEMBLER SOURCE CODE
***  THIS PROGRAM ENABLES USERS TO UPDATE OR ADD MEMBERS         ***
***  TO SYS1.IMAGELIB WITHOUT SUPPLYING THE WRITE PASSWORD       ***
***  FOR THE DATASET.                                            ***
***  THE PROGRAM CALLS IEBIMAGE WHICH UPDATES THE LIBRARY.       ***
***  THE PROGRAM RESTRICTS THE PROGRAMMER TO THE FOLLOWING       ***
***  UPDATE RULES:                                               ***
***  1. ONLY INCLUDE,OPTION,COPYMOD,FCB AND NAME STATEMENTS      ***
***     CAN BE SPECIFIED.                                        ***
***  2. THE FIRST THREE CHARACTERS OF THE NAME STATEMENT MUST    ***
***     BE EQUAL TO POSITIONS 2 TO 4 IN THE JOBNAME.             ***
***  MODE OF OPERATION: MODESET WITH KEY ZERO AND SUPERVISOR     ***
***                     STATE IS ISSUED BY THE PROGRAM.          ***
***  FILES:                                                      ***
***  DDNAME SYSIN IS INPUT IN IEBIMAGE CONTROL CARDS FORMAT.     ***
***         LRECL=80   RECFM=FB                                  ***
***         BLKSIZE SHOULD BE A MULTIPLE OF 80.                  ***
***  DDNAME SYSUT2  IS OUTPUT IN IEBIMAGE CONTROL CARDS FORMAT.  ***
***         IT IS USED AS SYSIN BY IEBIMAGE.                     ***
***         LRECL=80   RECFM=FB                                  ***
***         BLKSIZE SHOULD BE A MULTIPLE OF 80.                  ***
***  DDNAME SYSPRINT IS A SYSOUT FILE USED BY IEBIMAGE.          ***
***         IF IEBIMAGE IS NOT CALLED DUE TO ERROR OR            ***
***         UNAUTHORISED CONTROL CARDS THE ERROR IS WRITTEN TO   ***
***         SYSPRINT.                                            ***
***  REGISTERS USAGE:                                            ***
***         R3 -  BASE REGISTER                                  ***
***         R4 -  POINTER TO INPUT RECORD.                       ***
***         R5 -  WORK REGISTER.                                 ***
***         R6 -  CVT, TCB.                                      ***
***         R7 -  BASE FOR JSCB.                                 ***
***         R8 -  WORK REGISTER.                                 ***
***         R9 -  WORK REGISTER.                                 ***
***         R14 - RETURN REGISTER                                ***
MLMIMAGE STARTASM                  site-dependent initiation routine
         OPEN  (SYSIN,INPUT)
         OPEN  (SYSUT2,OUTPUT)
         OPEN  (SYSPRINT,OUTPUT)
GET      GET   SYSIN,REC
         LA    R4,REC
         BAL   R5,OPERAND
         CLC   0(5,R4),=C'TABLE'     IS IT TABLE STATEMENT ?
         BE    ERROR1                IF YES, BRANCH
         CLC   0(7,R4),=C'GRAPHIC'   IS IT GRAPHIC STATEMENT?
         BE    ERROR1                IF YES, BRANCH
         CLC   0(7,R4),=C'INCLUDE'   IS IT INCLUDE STATEMENT?
         BE    BALPUT                IF YES, BRANCH
         CLC   0(6,R4),=C'OPTION'    IS IT OPTION STATEMENT?
         BE    BALPUT                IF YES, BRANCH
         TM    SW,FCB+COPYMOD        IS IT AFTER A FCB OR COPYMOD?
         BNZ   CHCKNAME              IF YES, BRANCH
         CLC   0(3,R4),=C'FCB'       IS IT FCB?
         BNE   CHCKCOPY              IF NOT, BRANCH
         OI    SW,FCB                PUT ON FCB SW
         B     BALPUT
CHCKCOPY CLC   0(7,R4),=C'COPYMOD'   IS IT COPYMOD?
         BNE   ERROR1                IF NOT, BRANCH
         OI    SW,COPYMOD            PUT ON COPYMOD SW
         B     BALPUT
CHCKNAME CLC   0(4,R4),=C'NAME'      IS IT A NAME STATEMENT?
         BE    NAMECARD              IF YES, BRANCH
BALPUT   BAL   R5,PUT
         B     GET
NAMECARD BAL   R5,OPERAND            LOCATE NEXT OPERAND
         MODESET KEY=ZERO,MODE=SUP
         L     R6,16                 CVT ADDRESS
         USING CVT,R6
         L     R7,CVTTCBP            POINTER TO TCB ADDRESS
         L     R6,0(R7)              TCB ADDRESS
         USING TCB,R6
         L     R7,TCBTIO             TIOT ADDRESS
         USING TIOT1,R7
         CLC   TIOCNJOB+1(3),0(R4)   NAME AND ACCOUNT ARE THE SAME?
         BNE   ERROR2                IF NOT, BRANCH
         L     R7,TCBJSCB            JSCB ADDRESS
         LA    R7,0(R7)              CLEARED HIGH ORDER BYTE
         USING IEZJSCB,R7
         OI    JSCBSWT1,JSCBPASS     PASSWORD BIT ON
         MODESET KEY=NZERO,MODE=PROB
         OI    SW,NAME               PUT ON NAME BIT
         XI    SW,FCB+COPYMOD
         BAL   R5,PUT
         B     GET
OPERAND  LA    R9,71
         LA    R8,REC
         AR    R9,R8
         SR    R9,R4                 R9 IS THE NO. OF BYTES FOR BCT
         CLI   0(R4),X'40'           IS IT SPACE?
         BE    BLNKLOOP              IF YES, BRANCH
NOBLANK  CLI   0(R4),X'40'           FIRST SPACE?
         BE    BLNKLOOP              IF YES, BRANCH
         LA    R4,1(R4)              NEXT BYTE
         BCT   R9,NOBLANK
         B     ERROR1
BLNKLOOP CLI   0(R4),X'40'           IS IT A SPACE?
         BNER  R5                    IF NOT, BRANCH
         LA    R4,1(R4)              NEXT BYTE
         BCT   R9,BLNKLOOP
         B     ERROR1
EOF      CLOSE SYSPRINT
         CLOSE SYSUT2
         CLOSE SYSIN
         TM    SW,NAME               IS NAME BIT ON?
         BZ    ERROR3
         LINK  EP=IEBIMAGE,PARAM=(OPTION,DDNAME),VL=1
         MODESET KEY=ZERO,MODE=SUP
         NI    JSCBSWT1,X'7F'        PASSWORD BIT OFF
         MODESET KEY=NZERO,MODE=PROB
         B     RETURN
PUT      PUT   SYSUT2,REC
         BR    R5
ERROR1   MVC   ERRMSG1+40(80),REC    MOVE INVALID CARD
         PUT   SYSPRINT,ERRMSG1
         B     RETURN
ERROR2   MODESET KEY=NZERO,MODE=PROB
         PUT   SYSPRINT,ERRMSG2
         B     RETURN
ERROR3   PUT   SYSPRINT,ERRMSG3
         B     RETURN
SYSIN    DCB   DDNAME=SYSIN,MACRF=GM,EODAD=EOF,DSORG=PS,LRECL=80
SYSUT2   DCB   DDNAME=SYSUT2,MACRF=PM,DSORG=PS,LRECL=80,BLKSIZE=80,    X
               RECFM=F
SYSPRINT DCB   DDNAME=SYSPRINT,MACRF=PM,DSORG=PS,LRECL=121,BLKSIZE=121,X
               RECFM=F
         DS    0H
OPTION   DC    X'0000'
DDNAME   DC    H'40'                 LENGTH OF DD NAME LIST
         DC    4XL8'00'              UNUSED DDNAMES
         DC    C'SYSUT2  '           SYSTEM INPUT DDNAME (SYSIN)
REC      DS    CL80
SW       DC    X'00'
FCB      EQU   X'01'                 FCB SW BIT
COPYMOD  EQU   X'02'                 COPYMOD SW BIT
NAME     EQU   X'04'                 NAME SW BIT
ERRMSG1  DC    CL121' MLMIMG01   INVALID CONTROL CARD: '
ERRMSG2  DC    CL121' MLMIMG02   NAME CARD INCONSISTENT WITH JOBNAME. '
ERRMSG3  DS    0CL121
         DC    C' MLMIMG03    NO NAME CARD IN INPUT STREAM '
         DC    CL80'.REQUEST IGNORED.'
         CVT   DSECT=YES
         IKJTCB
TIOT     DSECT
         IEFTIOT1
         IEZJSCB
         END


Avi Rosenthal
Systems Programming Manager
Malam Systems (Israel)   © Malam Systems 1989























