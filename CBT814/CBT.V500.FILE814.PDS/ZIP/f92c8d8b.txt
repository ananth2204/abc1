CLIST to find duplicate members in a PDS

This article describes a CLIST which is used to list common
member names found in two PDSs. Enter:

      TSO DUPMEM mypds1 mypds2

where mypds1 and mypds2 are the datasets for which you want
a common member name list. If there are any common member
names they are put into a temporary dataset so that you can
view/edit the list.

PROC 2 DS1 DS2
  ISPEXEC CONTROL ERRORS RETURN
  CONTROL ASIS
  IF &SUBSTR(1,&DS1) = &STR(') THEN +
    SET &DS1 = &SUBSTR(2:&EVAL(&LENGTH(&DS1)-1),&DS1)
  ELSE +
    SET &DS1 = &SYSPREF..&DS1
  IF &SYSDSN('&DS1') ^= &STR(OK)  THEN +
  DO
     WRITE Dataset "&DS1" not found/not catalogued.
     GOTO FINLAB
  END
  IF &SUBSTR(1,&DS2) = &STR(') THEN +
    SET &DS2 = &SUBSTR(2:&EVAL(&LENGTH(&DS2)-1),&DS2)
  ELSE +
    SET &DS2 = &SYSPREF..&DS2
  IF &SYSDSN('&DS2') ^= &STR(OK)   THEN +
  DO
     WRITE Dataset "&DS2" not found/not catalogued.
     GOTO FINLAB
  END
  ISPEXEC LMINIT DATAID(DID1) DATASET('&DS1') ENQ(SHR) ORG(DSORG)
  IF &DSORG ^= &STR(PO)  THEN +
  DO
    WRITE "&DS1" is not a partitioned dataset (DS1).
    ISPEXEC LMFREE DATAID(&DID1)
    EXIT
  END
  ISPEXEC LMINIT DATAID(DID2) DATASET('&DS2') ENQ(SHR) ORG(DSORG)
  IF &DSORG ^= &STR(PO)  THEN +
  DO
    WRITE "&DS2" is not a partitioned dataset (DS2).
    ISPEXEC LMFREE DATAID(&DID2)
    EXIT
  END
  ISPEXEC LMOPEN DATAID(&DID2) OPTION(INPUT)
  ISPEXEC LMOPEN DATAID(&DID1) OPTION(INPUT)
  SET &RC = &LASTCC
  SET &CN = 0
  DO WHILE &LENGTH(&STR(&DLINE)) LT 80
     SET &DLINE = &STR(&STR(&DLINE)&STR(-))
  END
  ALLOC F(RECOUT) SPACE(20,5) TRACK LRECL(80) +
        RECFM(F B) BLKSIZE(6240) REUSE
  OPENFILE RECOUT OUTPUT
  SET &RECOUT = &STR(&DLINE)
  PUTFILE RECOUT
  SET &RECOUT = &STR(Following member names are found in +
                     &DS1 and &DS2)
  PUTFILE RECOUT
  SET &RECOUT = &STR(&DLINE)
  PUTFILE RECOUT
  DO WHILE &RC = 0
    ISPEXEC LMMLIST DATAID(&DID1) OPTION(LIST) MEMBER(MEM)
    SET &RC = &LASTCC
    IF &RC = 0 THEN +
    DO
      SET &MEM  = &SUBSTR(1:&LENGTH(&MEM),&MEM)
      /* WRITE MEMBER=&MEM &RC
      ISPEXEC LMMFIND DATAID(&DID2) MEMBER(&MEM) STATS(NO)
      SET &RCC = &LASTCC
      IF &RCC = 0 THEN +
      DO
        SET &RECOUT = &STR(&MEM)
        PUTFILE RECOUT
        SET &CN = &CN + 1
      END
    END
  END
  CLOSFILE RECOUT
FINERR:   +
  CONTROL NOMSG
  ISPEXEC LMMLIST DATAID(&DID1) OPTION(FREE)
  ISPEXEC LMCLOSE DATAID(&DID1)
  ISPEXEC LMFREE  DATAID(&DID1)
  ISPEXEC LMMLIST DATAID(&DID2) OPTION(FREE)
  ISPEXEC LMCLOSE DATAID(&DID2)
  ISPEXEC LMFREE  DATAID(&DID2)
FINISH:   +
 IF &CN > 0 THEN +
 DO
    CONTROL NOMSG
    SET &DDN = &STR(RECOUT)
    ISPEXEC LMINIT DATAID(DDVAR) DDNAME(&DDN)
    SET &RC = &LASTCC
    ISPEXEC EDIT DATAID(&DDVAR)
    SET &RC = &LASTCC
    IF &RC > 4 THEN +
    DO
      SET ZEDSMSG = &NRSTR(Could not edit output)
      SET ZEDLMSG = &NRSTR(Output file not created.)
    END
    ISPEXEC LMFREE DATAID(&DDVAR)
 END
 ELSE +
 DO
    SET ZEDSMSG = &NRSTR(No members in common)
    SET ZEDLMSG = &NRSTR("&DS1" and "&DS2" have no members in common.)
 END
 FREE  FI(RECOUT)
 ISPEXEC SETMSG MSG(ISRZ000)
FINLAB:   +
EXIT


P Kumar (Australia)                              c Xephon 1992

