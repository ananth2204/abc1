Displaying the SMF system-id in ISPF

One of the confusing things about a multi-CPU environment is
knowing which system you are logged on to.  This is
compounded with split-screen TSO and multi-session monitors
like NetView Access Services and TPX. Currently, there is no
way to get this information through TSO or ISPF variables. We
wanted the four-character SMF system-id to be displayed on
the ISPF primary menu as soon as the user logged on. This
required several steps. First, a program was written to retrieve
the SYSID from the SMF control block. The program, called
SYSID, gets this data and places it into the TSO variable
&SYSID. Second, the log-on CLIST was changed. The
command that invokes ISPF was altered to the following:

      ISPSTART PANEL(ISR@PRIM) OPT(SYSID) NEWAPPL(ISR)

What this does is to invoke ISPF and immediately execute the
menu option, SYSID. This option calls the following CLIST:

      PROC 0
      CALL 'SYS1.GOLIB(SYSID)'
      SET &SYSTEM = &SYSID
      ISPEXEC VPUT (SYSTEM) PROFILE

This CLIST executes our program and puts the variable into the
ISPF profile variable pool for future reference. The library,
SYS1.GOLIB, can be any library to which you have linked the
SYSID program. Once the CLIST is finished, ISPF displays
the primary menu (ISR@PRIM in our case). Below is a copy
of this panel with the necessary lines highlighted with double
asterisks (**).


ISR@PRIM PANEL

)BODY
%----------------------   ISPF/PDF PRIMARY OPTION MENU  ----------------------
%  OPTION ===>_ZCMD
+                                                           USERID   -&ZUSER
%  0 +ISPF PARMS  - Specify terminal and user parameters    JULDATE  -&ZJDATE
%  1 +BROWSE      - Display source data or output listings  DATE     -&ZDATE
%  2 +EDIT        - Create or change source data            TIME     -&ZTIME
%  3 +UTILITIES   - Perform utility functions               TERMINAL -&ZTERM
%  4 +COMPILE     - Edit and compile GST source programs    PF KEYS  -&ZKEYS
%  5 +PDS         - Invoke the PDS subsystem
%  6 +COMMAND     - Enter TSO command or CLIST              SYSTEM =>%&SYSTEM
%  7 +DIALOG TEST - Perform dialog testing              ** here is where we **
%  8 +SDSF        - Spool display and search facility   ** put the sysid on **
%  9 +DB2I        - DB2 Interactive / SPUFI             ** the main menu    **
%  T +TUTORIAL    - Display information about ISPF/PDF
%  X +EXIT        - Terminate ISPF using log and list defaults
+
+  Enter%END+command to terminate ISPF.
+
)INIT
  VGET (SYSTEM) PROFILE   ** this gets the sysid from the pool **
)PROC
  &ZSEL = TRANS( TRUNC (&ZQ,'.')
                0,'PANEL(ISPOPTA)'
                1,'PGM(ISRBRO) PARM(ISRBRO01)'
                2,'PGM(ISREDIT) PARM(P,ISREDM01)'
                3,'PANEL(ISRUTIL)'
                4,'CMD(%GSTEDIT)'
                5,'PANEL(PDS@PRIM)'
                6,'PGM(ISRPTC)'
                7,'PGM(ISRYXDR) NOCHECK'
                8,'PANEL(ZSDSFOP2) NEWAPPL(ISF)'
                9,'PANEL(DSNEPRI) NEWAPPL(DSNE)'
                T,'PGM(ISPTUTOR) PARM(ISR00000)'
            SYSID,'CMD(%SETSYS)' ** this is the option to execute the CLIST **
              ' ',' '
                X,'EXIT'
                *,'?' )


PROGRAM TO EXTRACT THE SMF SYSTEM-ID

SYSID    CSECT
         STM   14,12,12(13)
         BALR  12,0
         USING *,12
         ST    13,SAVEAREA+4
         LA    15,SAVEAREA
         ST    15,8(,13)
         LA    13,SAVEAREA
         L     7,CVTPTR
         USING CVT,7
         L     6,CVTSMCA
         USING SMCABASE,6
         MVC   VALUE,SMCASID
         L     15,CVTPTR
         L     15,156(,15)
         L     15,TSVTVACC-TSVT(,15)
         LTR   15,15
         BNZ   CALL441
LINK441  LINK  EP=IKJCT441,                                            X
               PARAM=(ECODE,                                           X
               NAMEPTR,                                                X
               NAMELEN,                                                X
               VALUEPTR,                                               X
               VALUELEN,                                               X
               TOKEN),                                                 X
               VL=1
         B     RET441
CALL441  CALL  (15),                                                   X
               (ECODE,                                                 X
               NAMEPTR,                                                X
               NAMELEN,                                                X
               VALUEPTR,                                               X
               VALUELEN,                                               X
               TOKEN),                                                 X
               VL
RET441   LTR   15,15
         BNZ   ERRORRTN
RETURN   LA    15,0
ERRORRTN L     13,4(,13)
         L     14,12(,13)
         LM    0,12,20(13)
         BR    14
         LTORG
NAME     DC    CL5'SYSID'
NAMELEN  DC    F'5'
VALUE    DC    CL5'BONZO'
VALUELEN DC    F'5'
NAMEPTR  DC    A(NAME)
VALUEPTR DC    A(VALUE)
TOKEN    DC    F'0'
ECODE    DC    A(TSVEUPDT)
SAVEAREA DS    18F
         IKJTSVT
         CVT     DSECT=YES
         IEESMCA ,
         END


Paul Jespersen
Systems Programmer
Gulf States Toyota (USA)                                     c Xephon 1993

