MVS subsystem scan

SCANSUBS CSECT
         USING *,R15
         B     START
         DC    CL8'SCANSUBS'
         DC    CL8'&SYSDATE'
         DROP  R15
         DC    C'F1SA'
START    BAKR  R14,0
         LR    R12,R15
         USING SCANSUBS,R12                   base register
         LA    R13,SAVE_AREA
         L     R2,CVTPTR                      r2 --> cvt
         ICM   R1,15,CVTSMCA-CVT(R2)          r1 --> smca
         MVC   L0A+14(4),SMCASID-SMCABASE(R1) smfid
         L     R2,CVTJESCT-CVT(,R2)           r2 --> jesct
         L     R10,JESSSCT-JESCT(,R2)         r10 --> ssct
         OPEN  (SYSPRINT,(OUTPUT))
         LTR   R15,R15
         BNZ   EXIT
         BAL   R2,PUT_LINE
         MVC   0(L'L0A,R3),L0A                heading
         BAL   R2,PUT_LINE
         MVC   0(L'L1A,R3),L1A                heading
SSCT_LOOP EQU *
         BAL   R2,PUT_LINE
         MVC   2(4,R3),SSCTSNAM-SSCT(R10)     subsystem name
         MVC   BINARY(4),SSCTSNAM-SSCT(R10)
         BAL   R2,CONVERT_TO_HEXADECIMAL
         MVI   8(R3),C'('
         MVC   9(8,R3),ZONED
         MVI   17(R3),C')'
         MVC   BINARY(4),SSCTSSVT-SSCT(R10)
         BAL   R2,CONVERT_TO_HEXADECIMAL
         MVC   24(8,R3),ZONED
         ICM   R4,15,SSCTSSVT-SSCT(R10)       ssvt assigned ?
         BNZ   SSCT_NEXT
         MVC   34(25,R1),=C'<--- subsystem not active'
SSCT_NEXT EQU *
         ICM   R10,15,SSCTSCTA-SSCT(R10)      r10 --> next ssct
         BNZ   SSCT_LOOP
         CLOSE (SYSPRINT)
         XR    R15,R15
EXIT     PR
PUT_LINE EQU *                                subroutine
         PUT   SYSPRINT
         MVI   0(R1),C' '
         MVC   1(120,R1),0(R1)
         LR    R3,R1
         BR    R2                             return to caller
CONVERT_TO_HEXADECIMAL EQU *                  subroutine
         UNPK  ZONED(9),BINARY(5)
         NC    ZONED(8),=X'0F0F0F0F0F0F0F0F'
         TR    ZONED(8),=C'0123456789ABCDEF'
         BR    R2                             return to caller
SAVE_AREA DC   18F'0'
L0A       DC    C'  System id - xxxx'
L1A       DC    C'  Subsystem             SSVT Addr'
BINARY    DC    F'0'
          DC    X'00'
ZONED     DC    CL9' '
SYSPRINT  DCB DDNAME=SYSPRINT,MACRF=PL,DSORG=PS,LRECL=121
         CVT DSECT=YES
         IEFJSCVT
         IEFJESCT
         IEESMCA
         @REGS
         END


David Welch (New Zealand)                                                   c Xephon 1995

