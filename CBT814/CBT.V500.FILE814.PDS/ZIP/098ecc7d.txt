Cloning an MVS dataset

If you have ever wished for an easy way to create a test file or
to save the 'old' version libraries before a release update, you
will really like the CLONE command.  It is a TSO CLIST that
will run from ISPF option 3.4, any ISPF command line, or the
TSO 'READY' prompt.  The best use of this command is from
the ISPF 3.4 options panel.  The CLONE command creates a
look-alike dataset (PO, PS, or VSAM) and gives you the
option of copying the data as well.  The new dataset will be
given the same name as the original, but with '.CLONE' added
at the end.  Optionally, you can 'clone' your dataset onto a
different DASD volume by specifying the VOL subparameter.

 PROC 1 DSNAME VOL()
 IF &SUBSTR(1:1,&DSNAME) EQ ' THEN GOTO QUOTED
 SET &DSNAME = &STR('&SYSPREF..&DSNAME')
QUOTED: +
 LISTDSI &DSNAME DIRECTORY
 SET &LEN = &LENGTH(&DSNAME)
 SET &LEN = &LEN - 1
 SET &DSN = &SUBSTR(1:&LEN,&DSNAME)
 IF .&VOL NE . THEN SET &SYSVOLUME = &VOL
 IF &SYSDSORG EQ VS THEN GOTO VSAM
 IF &LENGTH(&SYSRECFM) = 1 THEN +
   DO
   SET &RECFM = &SUBSTR(1:1,&SYSRECFM)
   GOTO ALLOCDS
   END
 IF &LENGTH(&SYSRECFM) = 2 THEN +
   DO
   SET &RECFM1 = &SUBSTR(1:1,&SYSRECFM)
   SET &RECFM2 = &SUBSTR(2:2,&SYSRECFM)
   SET &RECFM = &STR(&RECFM1,&RECFM2)
   GOTO ALLOCDS
   END
 IF &LENGTH(&SYSRECFM) = 3 THEN +
   DO
   SET &RECFM1 = &SUBSTR(1:1,&SYSRECFM)
   SET &RECFM2 = &SUBSTR(2:2,&SYSRECFM)
   SET &RECFM3 = &SUBSTR(3:3,&SYSRECFM)
   SET &RECFM = &STR(&RECFM1,&RECFM2,&RECFM3)
   GOTO ALLOCDS
   END
ALLOCDS: +
 IF &SYSDSORG EQ PO THEN SET &DIR = &STR(DIR(&SYSADIRBLK))
 ALLOCATE DATASET(&DSN..CLONE') NEW +
          VOL(&SYSVOLUME) +
          UNIT(&SYSUNIT) +
          SPACE(&SYSPRIMARY,&SYSSECONDS) &SYSUNITS +
          &DIR +
          RECFM(&RECFM) +
          LRECL(&SYSLRECL) +
          BLKSIZE(&SYSBLKSIZE) +
          CATALOG
 FREE DSNAME(&DSN..CLONE')
 WRITE &DSN..CLONE' CREATED ON &SYSVOLUME
 WRITE COPY THE DATA? (Y OR N)
 READ
 IF &SUBSTR(1:1,&SYSDVAL) NE Y THEN EXIT
 ISPEXEC LMINIT DATAID(DD1ID) DATASET(&DSNAME) ORG(&SYSDSORG)
 IF .&DD1ID EQ . THEN +
   DO
   WRITE CANNOT OPEN INPUT DATASET FOR COPY - IN USE
   EXIT
   END
 ISPEXEC LMINIT DATAID(DD2ID) DATASET(&DSN..CLONE')
 IF &SYSDSORG = PO THEN +
   DO
   ISPEXEC LMCOPY FROMID(&DD1ID) FROMMEM(*) TODATAID(&DD2ID)
   WRITE COPY OF DATA COMPLETED
   EXIT
   END
 IF &SYSDSORG = PS THEN +
   DO
   ISPEXEC LMCOPY FROMID(&DD1ID) TODATAID(&DD2ID)
   WRITE COPY OF DATA COMPLETED
   EXIT
   END
 WRITE UNSUPPORTED DATASET ORGANIZATION &SYSDSORG
 EXIT 999
VSAM: +
 DEFINE CLUSTER (NAME(&DSN..CLONE') MODEL(&DSNAME) VOL(&SYSVOLUME)
 WRITE &DSN..CLONE' CLUSTER CREATED ON &SYSVOLUME
COPYVSAM: +
 WRITE COPY THE DATA? (Y OR N) NON-DB2 ONLY!
 READ
 IF &SUBSTR(1:1,&SYSDVAL) NE Y THEN EXIT
 REPRO INDATASET(&DSNAME) OUTDATASET(&DSN..CLONE')
 WRITE COPY OF DATA COMPLETED
 EXIT


Paul Jespersen
Systems Programmer
Gulf States Toyota (USA)                                © Xephon 1990

