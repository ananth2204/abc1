Changing the concatenation under TSO

The new ALTLIB command was introduced with TSO/E
Version 2 and MVS/ESA. This command is especially useful
in adding and removing CLIST and REXX libraries to or from
the normal concatenation for use in TSO under the
SYSPROC/SYSEXEC DDNAMEs. Unfortunately, the same
version of TSO under MVS/XA doesn't provide this facility.
We have developed the following CLIST to do, more or less,
the same basic function as the ALTLIB command.

It works for any DDNAME under MVS/XA 2.2 and MVS/ESA
3.1.1 and 3.1.3. We think is a useful complement to the
LIBDEF command to bring new applications under ISPF/PDF.
It uses SYSOUTLINE and SYSOUTRAP to capture the
existing libraries in a DDNAME with the LISTALC STATUS
command. It then frees the concatenation and re-allocates the
same DDNAME with or without the specified library.

PROC 0 LIB() OPE(F) DDN(SYSPROC)
CONTROL END(ZZZZ) NOMSG  NOLIST NOCONLIST NOSYMLIST
 /********************************************************************/
 /**  NAME   :  DAUDDNS                                             **/
 /**  TYPE   :  CLIST                                               **/
 /**  FUNCION:  DYNAMIC CHANGE OF A CONCATENATION OF LIBRARIES      **/
 /**            ASSIGNED TO ONE SPECIFIC DDNAME.                    **/
 /**            PRIMARILY INTENDED FOR SYSPROC. THIS IS THE DEFAULT.**/
 /**  LLAMADA:  %DAUDDNS  LIB(XXX.CMDLIB) OPE(X) DDN(SYSPROC)       **/
 /**  NOTAS:    * THE LIBRARY SPECIFIED IN -LIB- PARAMETER MUST     **/
 /**              EXIST AND BE CATALOGUED.                          **/
 /**            * THE OPTIONAL PARAMETER  -OPE-, IF SPECFIED, MUST  **/
 /**              CONTAIN ONE VALUE. POSSIBLES VALUES ARE:          **/
 /**              OPE(I)    PUT THE LIBRARY AS THE FIRST IN CONCA-  **/
 /**                        TENATION.                               **/
 /**              OPE(F)    PUT THE LIBRARY AT THE END OF CONCA-    **/
 /**                        TENATION.                               **/
 /**                        THIS IS THE DEFAULT IF NOT SPECIFIED.   **/
 /**              OPE(L)    REMOVE THE LIBRARY FROM THE CONCAT-     **/
 /**                        ENATION.                                **/
 /**            * THERE WILL NOT BE ANY MESSAGES.                   **/
 /********************************************************************/
/* ----------------------------------------------------------------- */
/*   DETECT AND STORE LIBRARIES IN DDNAME CONCATENATION           -- */
/* ----------------------------------------------------------------- */
IF &LIB = &STR() THEN GOTO ACABAR
IF &SYSDSN('&LIB') NE &STR(OK) THEN GOTO ACABAR
SET SYSOUTTRAP = 500
LISTALC STATUS
SET SYSOUTTRAP = 0
SET TOTLINE = &SYSOUTLINE
SET STDDNUM = 0
SET RETCGRAL = 0
SET CONTADOR = 1
DO UNTIL &CONTADOR > &TOTLINE OR &SWITCH = &STR(F)
   SET LINENUM = &EVAL(&CONTADOR - 1)
   SET LINEMSG = &&SYSOUTLINE&CONTADOR
   IF &SWITCH = &STR(A) +
      THEN -
       DO
         IF &SUBSTR(1:4,&LINEMSG) EQ &STR(NULL)   +
         OR &SUBSTR(1:4,&LINEMSG) EQ &STR(TERM)   +
         OR &SUBSTR(1:4,&LINEMSG) EQ &STR(JES2)   +
            THEN SET SWITCH = &STR(F)
            ELSE -
             DO
               IF &SUBSTR(1:2,&LINEMSG) NE &STR(  ) +
                  THEN -
                   DO
                     IF &STR(&LINEMSG) = &STR(&LIB) +
                        THEN -
                         DO
                           IF &OPE NE &STR(L) +
                              THEN GOTO ACABAR
                         ZZZZ
                         ELSE -
                          DO
                            SET STDDNUM = &EVAL(&STDDNUM + 1)
                            SET &STDDTEX&STDDNUM = &STR(&LINEMSG)
                          ZZZZ
                   ZZZZ
                  ELSE IF &SUBSTR(3:9,&LINEMSG) NE &STR( )   +
                          THEN -
                           DO
                             SET STDDNUM = &EVAL(&STDDNUM - 1)
                             SET SWITCH = &STR(F)
                           ZZZZ
             ZZZZ
       ZZZZ
      ELSE -
       DO
         IF &SUBSTR(1:2,&LINEMSG) EQ &STR(  ) +
        AND &SUBSTR(3:9,&LINEMSG) EQ &STR(&DDN) +
            THEN -
             DO
               SET SWITCH = &STR(A)
               IF &SUBSTR(1:2,&LINEANT) NE &STR(  ) +
                  THEN -
                   DO
                     IF &STR(&LINEANT) = &STR(&LIB) +
                        THEN -
                         DO
                           IF &OPE NE &STR(L) +
                              THEN GOTO ACABAR
                         ZZZZ
                         ELSE -
                          DO
                            SET STDDNUM = &EVAL(&STDDNUM + 1)
                            SET &STDDTEX&STDDNUM = &STR(&LINEANT)
                          ZZZZ
                   ZZZZ
             ZZZZ
            ELSE SET &LINEANT = &STR(&LINEMSG)
       ZZZZ
   SET CONTADOR = &EVAL(&CONTADOR + 1)
ZZZZ
/* ----------------------------------------------------------------- */
/*   BUILD A COMPLETE LITERAL WITH THE NEW SERIES OF LIBRARIES    -- */
/* ----------------------------------------------------------------- */
SET CONTADOR = 1
IF &OPE = &STR(I) -
   THEN SET LITERAL = &STR('&LIB')
   ELSE SET LITERAL =
DO WHILE &CONTADOR <= &STDDNUM
   SET WORKA = &&STDDTEX&CONTADOR
   SET LITERAL = &LITERAL&STR(,')&WORKA&STR(')
   SET CONTADOR = &EVAL( &CONTADOR + 1 )
ZZZZ
IF &OPE = &STR(F) -
   THEN SET LITERAL = &LITERAL&STR(,')&LIB&STR(')
/* ----------------------------------------------------------------- */
/*   DO THE NEW ALLOCATION                                        -- */
/* ----------------------------------------------------------------- */
 FREE FI(&DDN)
 ALLOC FI(&DDN) DS(&LITERAL) SHR
ACABAR: EXIT


Angel Lu’s Dominguez and Miguel Angel Mart’nez
Systems Analysts
Banco Hipotecario de Espana (Spain)                  c Xephon 1992

