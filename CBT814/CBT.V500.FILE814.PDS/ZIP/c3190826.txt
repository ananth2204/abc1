TSO dataset locate program
The following program will enable a TSO user to find any ÔlostÕ or uncatalogued
fully-qualified datasets on any on-line DASD.  It is written to scan only 3380s,
but could easily be altered to scan 3330s, 3350s, or any combination.
It is useful for locating any datasets that have not been catalogued (or have
become uncatalogued), or for finding duplicates (probably from a different
system in a multi-CPU environment).
The program will display where (and if) the dataset is catalogued and the name
of any pack it appears on.
The code is written for an MVS/XA system (MVS/SP uses a different UCB look-up
routine), but could easily be altered.
         TITLE 'DSNFIND - LOCATE ALL ONLINE VERSIONS OF A DATASET'
***********************************************************************
*** NAME     : DSNFIND                                              ***
*** PURPOSE  : DISPLAY ALL OCCURRENCES OF A DATASET ON ALL ONLINE   ***
***            DASD, CATALOGED OR NOT.                              ***
*** METHOD   : GET DATASET NAME FROM TERMINAL AND VALIDATE IT. READ ***
***            ALL UCB'S FOR ONLINE 3380'S AND SEE IF THE DATASET   ***
***            IS ON THAT PACK.                                     ***
*** MACROS   : 'LOCATE'  : CATALOG ADMINISTRATION GUIDE             ***
***            'OBTAIN'  : SYSTEM - DATA ADMINISTRATION             ***
***            'CAMLST'  : SYSTEM - DATA ADMINISTRATION             ***
***********************************************************************
DSNFIND  CSECT
         STM   R14,R12,12(R13)          SAVE CALLER'S REGISTERS
         LR    R12,R15                  GET ENTRY POINT
         LA    R11,4095(R12)
         LA    R11,1(R11)
         USING DSNFIND,R12,R11          ESTABLISH BASE CAMP(S)
         LR    R15,R13                  SAVE A (CALLERÕS SAVE AREA)
         BAL   R13,*+76                 POINT TO MY SAVEAREA
         DC    18FÕ0Õ                   MY SAVEAREA
         ST    R13,8(R15)               FORWARD CHAINING
         ST    R15,4(R13)               BACKWARD CHAINING
*---------------------------------------------------------------------*
* GET DATASET NAME, CONVERT TO UPPER CASE, CHECK FOR VALIDITY...      *
*---------------------------------------------------------------------*
START    TPUT  ENTER,ENTERLEN           DISPLAY PROMPT
         TGET  DSN,44,EDIT,WAIT         GET DATASET NAME
         LTR   R15,R15                  TGET OK?
         BZ    DSNCHK                   YES..PROCESS IT
         C     R15,=F'12'               DATASET NAME TOO LONG?
         BE    DSNERR                   YES..TELL USER THAT
         B     TGETFAIL                 NO...SOME OTHER ERROR
DSNCHK   LR    R2,R1                    SAVE TGET READ LENGTH
         LTR   R2,R2                    ANYTHING READ?
         BZ    NODSN                    NO...TELL THEM
         CLC   DSN(2),=C'? '            WANT TO KNOW HOW TO USE IT?
         BE    SPEIL                    YES..SHOW THEM
         LR    R3,R2                    GET DSN LENGTH
         BCTR  R3,0                     DECREMENT BY 1 FOR EXEC
         EX    R3,EXECTR1               EXECUTE TRANSLATE TO UPPERCASE
         CLC   DSN(5),=C'HELP '         WANT TO KNOW HOW TO USE IT?
         BE    SPEIL                    YES..SHOW THEM
         LR    R3,R2                    GET LENGTH OF SCAN
         LA    R4,DSN                   FIND START OF DATASET NAME
         LA    R5,HEAD3DSN
         XR    R8,R8                    CLEAR COUNTER
DSNLOOP1 XR    R6,R6                    CLEAR COUNTER
         LA    R7,DSN2                  WORKAREA
DSNLOOP2 CLI   0(R4),C'.'               PERIOD? (END OF QUALIFER)
         BE    CHKPERD                  YES..CHECK QUALIFIER
         CLI   0(R4),C' '               IS IT A SPACE?
         BE    DSNERR                   YES..CAN'T HAVE ONE OF THOSE
         C     R6,=F'8'                 THIS QUALIFIER ALREADY 8 LONG?
         BE    DSNERR                   YES..THEN ITÕS TOO LONG
         MVC   0(1,R5),0(R4)            MOVE CHARACTER TO OUTPUT AREA
         MVC   0(1,R7),0(R4)            MOVE CHARACTER TO WORK AREA
         LA    R4,1(R4)                 BUMP INPUT POINTER
         LA    R5,1(R5)                 BUMP OUTPUT POINTER
         LA    R6,1(R6)                 BUMP COUNTER
         LA    R7,1(R7)                 BUMP WORKAREA POINTER
         LA    R8,1(R8)                 BUMP OTHER COUNTER
         BCT   R3,DSNLOOP2              CONTINUE SCAN
         BAL   R9,TRTRTNS               TEST FINAL QUALIFIER
         B     PUTQUOTE                 AT END PUT IN QUOTE
CHKPERD  CLI   1(R4),C'.'               CONSECUTIVE PERIODS?
         BE    DSNERR                   YES..INVALID
         BAL   R9,TRTRTNS               PERFORM TRT ROUTINES
         MVC   0(1,R5),0(R4)            MOVE CHARACTER TO OUTPUT AREA
         LA    R4,1(R4)                 BUMP INPUT POINTER
         LA    R5,1(R5)                 BUMP OUTPUT POINTER
         LA    R8,1(R8)                 BUMP LENGTH OF SCAN COUNTER
         XC    DSN2,DSN2                CLEAR WORKAREA
         BCT   R3,DSNLOOP1              CONTINUE SCAN
         B     DSNERR                   CANNOT END IN '.'
TRTRTNS  TRT   DSN2(1),TRTAB2           FIRST CHAR. ALPHA/NATIONAL?
         BNZ   DSNERR                   NO...INVALID
         BCTR  R6,0                     DECREMENT FOR EXEC
         EX    R6,EXECTR2               OTHERS ALPHA/NATIONAL/NUMERIC?
         BNZ   DSNERR                   NO...INVALID
         BR    R9                       RETURN
PUTQUOTE MVI   0(R5),C''''              PUT QUOTE AT END OF DSN
*---------------------------------------------------------------------*
* FIND IF/WHERE DATASET IS CATALOGUED. IF LOCATE FINDS AN ALIAS, IT   *
* ALTERS THE DSN IN ITS PARMLIST TO THE NAME OF THE CATALOG WHERE THE *
* ALIAS IS CATALOGUED, AND SETS THE VOLID TO THAT WHERE THE CATALOG   *
* IS LOCATED...                                                       *
*---------------------------------------------------------------------*
DSNLOC   LOCATE NAME                    TRY TO FIND OUT DATASET
         LTR   R15,R15                  FOUND OK?
         BNZ   CLEARVOL                 NO...CONTINUE
         LR    R3,R2                    GET DSNAME LENGTH
         BCTR  R3,0                     DECREMENT FOR EXEC
         EX    R3,EXECCLC1              DSNAMES STILL THE SAME?
         BNE   ITSALIAS                 NO...IT'S AN ALIAS
         MVC   CATALON(6),CAMAREA+6     IT'S A DATASET - SAVE VOLID
         MVC   HEAD3VOL(6),CAMAREA+6    PUT VOLID IN HEADER
         B     CVTLOOK
ITSALIAS OI    SWITCH,ALIAS             SHOW WE HAVE AN ALIAS
         LA    R5,2(R5)                 R5 Ñ> END OF DSNAME IN HEADER
         MVC   0(L'NOT,R5),NOT          MOVE IN MSG
         MVI   HEAD3CAT,C' '
         MVC   HEAD3CAT+1(14),HEAD3CAT  CLEAR CONSTANT
         MVC   HEAD4CAT(44),DSN         MOVE IN CATALOG NAME
         LA    R6,HEAD4CAT              FIND START OF CATALOG NAME
LOOP1    CLI   0(R6),C' '               END OF CATNAME?
         BE    ENDLOOP1                 YES..
         LA    R6,1(R6)                 NO...BUMP POINTER
         B     LOOP1
ENDLOOP1 MVI   0(R6),C''''              PUT ' AT END OF CATNAME
         MVC   1(4,R6),=C' ON '         ON VOLUME...
         MVC   5(6,R6),CAMAREA+6        GET VOLUME CATALOG IS ON...
         B     CVTLOOK                  ...AND DISPLAY IT
CLEARVOL MVC   HEAD3CAT,NOTCAT          NOT CATALOGED MSG
CVTLOOK  USING CVTMAP,R4                ADDRESSABILITY TO CVT
         USING SMCABASE,R8              ADDRESSABILITY TO SMCA
         L     R4,16                    GET CVT ADDRESS
         L     R8,CVTSMCA               GET SMCA ADDRESS
         MVC   HEAD1SID(4),SMCASID      MOVE SYSID TO HEADER
         L     R4,CVTUCBSC              GETUCB SCAN ADDRESS
         ST    R4,UCBSCA                SAVE IT
         DROP  R4
         DROP  R8
HEADER   TPUT  ASTER,ASTERLEN           . . . . . . . . . . . . . . . .
         TPUT  HEAD1,HEAD1LEN           .                             .
         TPUT  ASTER,ASTERLEN           .           FORMAT            .
         TPUT  HEAD2,HEAD2LEN           .           HEADER            .
         TPUT  HEAD3,HEAD3LEN           .                             .
         TPUT  HEAD2,HEAD2LEN           . . . . . . . . . . . . . . . .
         TM    SWITCH,ALIAS             WAS IT AN ALIAS?
         BNO   UCBLOOK                  NO...DO THE REST
         TPUT  HEAD4,HEAD4LEN           YES..GIVE THE BLURB
         TPUT  HEAD2,HEAD2LEN
         TPUT  ASTER,ASTERLEN
         B     EXIT                     ...AND END
*---------------------------------------------------------------------*
* UCB LOOKUP ROUTINE...                                               *
*---------------------------------------------------------------------*
UCBLOOK  USING UCBOB,R4                 ADDRESSABILITY TO UCB
UCBLOOP  LA    R1,PARMA                 PARMLIST ADDRESS
         L     R15,UCBSCA               SCAN ROUTINE ADDRESS
         BALR  R14,R15                  CALL SCAN ROUTINE
         LTR   R15,R15                  END OF UCB'S?
         BNZ   ENDUCBS                  YES..TIDY UP
         L     R4,ADDRUCB               NO..GET UCB ADDRESS
         TM    UCBTBYT3,DASDTYPE        IS IT DASD?
         BNO   NEXTUCB                  NO...GET NEXT ONE
         TM    UCBSTAT,ONLINE           IS IT ONLINE?
         BNO   NEXTUCB                  NO...GET NEXT ONE
         CLC   UCBVOL1(6),=6X'00'       REAL DASD?
         BE    NEXTUCB                  NO (DUMMY) - GET NEXT
         CLI   UCBTBYT4,X'0E'           3380?
         BNE   NEXTUCB                  NO...GET NEXT
*---------------------------------------------------------------------*
* WE NOW HAVE AN ONLINE 3380 UCB. USE THE 'OBTAIN' MACRO TO SEE IF    *
* THE DATASET EXISTS ON IT...                                         *
*---------------------------------------------------------------------*
ISITHERE MVC   VOLNUM,UCBVOLI           GET THIS VOLID
         MVC   DSNSRCH,DSN              GET DSNAME
         OBTAIN SEARCH                  SEE IF DATASET IS ON THIS PACK
         LTR   R15,R15                  IS IT?
         BNZ   NEXTUCB                  NO...GET NEXT UCB
         AP    COUNTER,=P'1'            ADD TO NO. FOUND
         UNPK  DWORD(8),UCBCHAN(3)      UNPACK DEVICE ADDR + EXTRA BYTE
         TR    DWORD+4(3),TRTAB4        CONVERT TO PRINTABLE HEX
         MVC   HEAD5CUU(3),DWORD+4      MOVE TO HEADER
         MVC   HEAD5VOL(6),UCBVOLI      SHOW VOLID FOR DISPLAY
         CLC   HEAD5VOL(6),CATALON      IS IT CATALOGED ON HERE?
         BNE   SHOWNOT                  NO...SHOW THAT
         MVC   HEAD5CAT(5),CATAL        YES..SHOW THAT
         B     SHOWHD5
SHOWNOT  MVC   HEAD5CAT(5),NOTCATAL
SHOWHD5  TPUT  HEAD5,HEAD5LEN
NEXTUCB  B     UCBLOOP
ENDUCBS  CP    COUNTER,=P'0'            ANY DATASETS FOUND?
         BNE   DSFND                    YES..BRANCH
NODSFND  TPUT  HEAD6,HEAD6LEN           SHOW NONE FOUND
DSFND    TPUT  HEAD2,HEAD2LEN           CLOSE OFF BOX
         TPUT  ASTER,ASTERLEN
         B     EXIT
*---------------------------------------------------------------------*
* ERROR MESSAGES, ETC...                                              *
*---------------------------------------------------------------------*
NODSN    TPUT  ERR1,ERR1LEN             NO DATASET NAME ENTERED
         B     EXIT
DSNERR   TPUT  ERR2,ERR2LEN             INVALID DATASET NAME
         B     EXIT
TGETFAIL TPUT  ERR3,ERR3LEN             ERROR DOING TGET
         B     EXIT
CAMERR   TPUT  ERR4,ERR4LEN             ERROR IN CAMLST MACRO
         B     EXIT
SPEIL    TPUT  SPEIL1,SPEIL1LN          EXPLAIN HOW TO USE IT
         TPUT  SPEIL2,SPEIL2LN
         TPUT  SPEIL3,SPEIL3LN
         TPUT  SPEIL4,SPEIL4LN
         TPUT  SPEIL5,SPEIL5LN
         TPUT  SPEIL6,SPEIL6LN
         TPUT  SPEIL7,SPEIL7LN
         TPUT  SPEIL8,SPEIL8LN
         TPUT  SPEIL9,SPEIL9LN
         TPUT  SPEILA,SPEILALN
         B     EXIT
*---------------------------------------------------------------------*
* RETURN...                                                           *
*---------------------------------------------------------------------*
EXIT     XR    R15,R15                  CLEAR RETURN CODE
         L     R13,4(R13)               RESTORE CALLER'S SAVEAREA ADDR.
         LM    R14,R12,12(R13)          RESTORE CALLER'S REGISTERS
         BR    R14                      GET OUT OF HERE
         LTORG
DASDTYPE EQU   X'20'
ONLINE   EQU   X'80'
GENERIC  EQU   X'80'
ALIAS    EQU   X'40'
***********************************************************************
******** TRANSLATE TABLES... ******************************************
***********************************************************************
* CONVERT LOWER CASE TO UPPER CASE...
*                 0 1 2 3 4 5 6 7 8 9 A B C D E F
TRTAB1   DC    X'000102030405060708090A0B0C0D0E0F'    00-0F
         DC    X'101112131415161718191A1B1C1D1E1F'    10-1F
         DC    X'202122232425262728292A2B2C2D2E2F'    20-2F
         DC    X'303132333435363738393A3B3C3D3E3F'    30-3F
         DC    X'404142434445464748494A4B4C4D4E4F'    40-4F
         DC    X'505152535455565758595A5B5C5D5E5F'    50-5F
         DC    X'606162636465666768696A6B6C6D6E6F'    60-6F
         DC    X'707172737475767778797A7B7C7D7E7F'    70-7F
         DC    X'80C1C2C3C4C5C6C7C8C98A8B8C8D8E8F'    80-8F
         DC    X'90D1D2D3D4D5D6D7D8D99A9B9C9D9E9F'    90-9F
         DC    X'A0A1E2E3E4E5E6E7E8E9AAABACADAEAF'    A0-AF
         DC    X'B0B1B2B3B4B5B6B7B8B9BABBBCBDBEBF'    B0-BF
         DC    X'C0C1C2C3C4C5C6C7C8C9CACBCCCDCECF'    C0-CF
         DC    X'D0D1D2D3D4D5D6D7D8D9DADBDCDDDEDF'    D0-DF
         DC    X'E0E1E2E3E4E5E6E7E8E9EAEBECEDEEEF'    E0-EF
         DC    X'F0F1F2F3F4F5F6F7F8F9FAFBFCFDFEFF'    F0-FF
* TEST FIRST CHARACTER OF QUALIFER FOR ALPHA/NATIONAL CHARACTERS...
                  0 1 2 3 4 5 6 7 8 9 A B C D E F
TRTAB2   DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF'    00-0F
         DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF'    10-1F
         DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF'    20-2F
         DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF'    30-3F
         DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF'    40-4F
         DC    X'FFFFFFFFFFFFFFFFFFFFFF00FFFFFFFF'    50-5F
         DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF'    60-6F
         DC    X'FFFFFFFFFFFFFFFFFFFFFF0000FFFFFF'    70-7F
         DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF'    80-8F
         DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF'    90-9F
         DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF'    A0-AF
         DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF'    B0-BF
         DC    X'FF000000000000000000FFFFFFFFFFFF'    C0-CF
         DC    X'FF000000000000000000FFFFFFFFFFFF'    D0-DF
         DC    X'FFFF0000000000000000FFFFFFFFFFFF'    E0-EF
         DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF'    F0-FF
* TEST QUALIFER FOR ALPHA/NATIONAL/NUMERIC CHARACTERS...
                  0 1 2 3 4 5 6 7 8 9 A B C D E F
TRTAB3   DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF'    00-0F
         DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF'    10-1F
         DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF'    30-3F
         DC    X'FFFFFFFFFFFFFFFFFFFFFF00FFFFFFFF'    40-4F
         DC    X'FFFFFFFFFFFFFFFFFFFFFF00FFFFFFFF'    50-5F
         DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF'    60-6F
         DC    X'FFFFFFFFFFFFFFFFFFFFFF0000FFFFFF'    70-7F
         DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF'    80-8F
         DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF'    90-9F
         DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF'    A0-AF
         DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF'    B0-BF
         DC    X'FF000000000000000000FFFFFFFFFFFF'    C0-CF
         DC    X'FF000000000000000000FFFFFFFFFFFF'    D0-DF
         DC    X'FFFF0000000000000000FFFFFFFFFFFF'    E0-EF
         DC    X'00000000000000000000FFFFFFFFFFFF'    F0-FF
* CONVERT CUU TO PRINTABLE HEX...
TRTAB4   EQU   *-240
         DC    C'0123456789ABCDEF'
***********************************************************************
******** HEADERS/MESSAGES, ETC... *************************************
***********************************************************************
ENTER    DC    C'ENTER DATASET NAME:'
ENTERLEN EQU   *-ENTER
ASTER    DC    78C'*'
ASTERLEN EQU   *-ASTER
HEAD1    DS    0CL78
         DC    CL28'* SYSTEM ID=XXXX'
         DC    CL20'Ñ DATASET SCAN Ñ'
         DC    CL29' '
         DC    C'*'
HEAD1SID EQU   HEAD1+12,4
HEAD1LEN EQU   *-HEAD1
HEAD2    DS    0CL78
         DC    C'*'
         DC    CL76' '
         DC    C'*'
HEAD2LEN EQU   *-HEAD2
HEAD3    DS    0CL78
         DC    CL7'* DSN='''
HEAD3DSN DC    CL44' '
HEAD3CAT DC    CL14' CATALOGED ON '
HEAD3VOL DC    CL12' '
         DC    C'*'
HEAD3LEN EQU   *-HEAD3
HEAD4    DS    0CL78
         DC    CL21'* IT IS AN ALIAS IN '''
HEAD4CAT DC    CL56' '
         DC    C'*'
HEAD4LEN EQU   *-HEAD4
HEAD5    DS    0CL78
         DC    CL12'*  FOUND ON '
HEAD5CUU DC    CL6'XXX - '
HEAD5VOL DC    CL6'XXXXXX'
HEAD5CAT DC    CL5' (U) '
         DC    CL48' '
         DC    C'*'
HEAD5LEN EQU   *-HEAD5
HEAD6    DS    0CL78
         DC    CL77'* NOT FOUND ON ANY ONLINE 3380...'
         DC    C'*'
HEAD6LEN EQU   *-HEAD6
HEAD7    DS    0CL78
         DC    C'*  '
HEAD7DSN DS    CL44
         DC    CL11'  FOUND ON '
HEAD7VOL DS    CL6
         DC    CL13' '
         DC    C'*'
HEAD7LEN EQU   *-HEAD7
ERR1     DC    C'NO DATASET NAME ENTERED...'
ERR1LEN  EQU   *-ERR1
ERR2     DC    C'INVALID DATASET NAME...'
ERR2LEN  EQU   *-ERR2
ERR3     DC    C'DSNFIND- ERROR IN "TGET" MACRO...'
ERR3LEN  EQU   *-ERR3
ERR4     DC    C'DSNFIND- ERROR IN "CAMLST" MACRO...'
ERR4LEN  EQU   *-ERR4
SPEIL1   DC    C'                       "DSNFIND"'
SPEIL1LN EQU   *-SPEIL1
SPEIL2   DC    C'                        ÑÑÑ'
SPEIL2LN EQU   *-SPEIL2
SPEIL3   DC    C' '
SPEIL3LN EQU   *-SPEIL3
SPEIL4   DC    C'  THE "DSNFIND" PROGRAM  WILL LOCATE  ALL OCCURRENCES'
SPEIL4LN EQU   *-SPEIL4
SPEIL5   DC    C'  OF A GIVEN DATASET ON ANY ONLINE 3380 DASD, WHETHER'
SPEIL5LN EQU   *-SPEIL5
SPEIL6   DC    C'  CATALOGUED OR NOT. IT WILL DISPLAY IF AND WHERE THE'
SPEIL6LN EQU   *-SPEIL6
SPEIL7   DC    C'  DATASET IS  CATALOGUED, AND  WHERE IT  EXISTS IN AN'
SPEIL7LN EQU   *-SPEIL7
SPEIL8   DC    C'  UN-CATALOGUED  STATE. IF THE DATASET  NAME GIVEN IS'
SPEIL8LN EQU   *-SPEIL8
SPEIL9   DC    C'  IN FACT AN ALIAS, IT WILL  DISPLAY THE CATALOG NAME'
SPEIL9LN EQU   *-SPEIL9
SPEILA   DC    C'  CONTAINING  THE ALIAS  AND  WHICH VOLUME  IT IS ON.'
SPEILALN EQU   *-SPEILA
***********************************************************************
******** WORK AREAS... ************************************************
***********************************************************************
NOT      DC    CL16'DOES NOT EXIST -'
NOTCAT   DC    CL15' NOT CATALOGED '
NOTCATAL DC    CL5' (U) '
CATAL    DC    CL5' (C) '
DSN2     DS    CL8
DSN3     DS    CL44
SWITCH   DC    X'00'
COUNTER  DC    PL2'0'
EXECCLC1 CLC   DSN(0),HEAD3DSN          COMPARE DATASETNAMES
EXECTR1  TR    DSN(0),TRTAB1            TRANSLATE LOWCASE Ñ> UPCASE
EXECTR2  TRT   DSN2(0),TRTAB3           TEST ALPHA/NATIONAL/NUMERIC
CATALON  DC    CL6' '
DWORD    DS    D
***********************************************************************
******** CAMLST PARAMETER LISTS... ************************************
***********************************************************************
NAME     CAMLST NAME,DSN,,CAMAREA
DSN      DC    CL44' ',C' '
CAMAREA  DS    0D
         DS    CL265
SEARCH   CAMLST SEARCH,DSNSRCH,VOLNUM,WORK
DSNSRCH  DS    CL44
VOLNUM   DS    CL6
WORK     DS    CL140
***********************************************************************
******** UCB SCAN ROUTINE PARAMETERS... *******************************
***********************************************************************
PARMA    DC    A(WORKAREA)              ADDR OF 100 BYTE WORK AREA
PARMB    DC    A(DEVCLASS)              ADDR OF DEVCLASS BYTE
PARMC    DC    X'80',AL3(ADDRUCB)       ADDR OF WORK TO HOLD UCB ADDR.
UCBSCA   DS    A                        ADDR OF UCB SCAN RTN. (EX CVT)
ADDRUCB  DS    A                        ADDR OF NEXT UCB
WORKAREA DS    0D
         DC    XL100'00'
DEVCLASS DC    X'00'
***********************************************************************
******** SMCA DSECT... ************************************************
***********************************************************************
         IEESMCA ,                      SMCA DSECT
***********************************************************************
******** UCB DSECT... *************************************************
***********************************************************************
         IEFUCBOB ,                     UCB DSECT
***********************************************************************
******** CVT DSECT... *************************************************
***********************************************************************
         CVT   DSECT=YES                CVT DSECT
         END


G Carson
Consultant
Independent Software Support Ltd (UK)   © Xephon 1988











































