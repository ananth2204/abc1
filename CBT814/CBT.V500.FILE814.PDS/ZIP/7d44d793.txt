Converting the catalog device type

Recently, when we started to install a string of 3490Es, we found
difficulties in allocating tape datasets in read mode that had been
created using 3480 or 3480X units. To redirect all input tape
datasets requires overriding the catalog device type (ie the
UCBTYPE). This can be done using the special esoteric name
SYS3480R to direct read-only allocations to 3490Es, or you
could use the program shown below to change catalog entries for
the tape dataset.

The program is used to convert catalog entries for tape datasets
from a UCBTYPE of 3480 to 3490. It generates DELETE
NOSCRATCH and DEFINE NONVSAM IDCAMS control
statements from a listing of the user catalog. One parameter must
be specified on the SYSIN card, which is used to select the type of
conversion. The program is written for the following catalog
entries:

o      From 3480 to 3480X - SYSIN card: 7800808078048080

o      From 3480 to 3490 - SYSIN card: 7800808078048081

o      From 3480X to 3490 - SYSIN card: 7804808078048081.

The program has been tested under MVS/ESA 3.1.3.

SYAPBDAN SOURCE CODE

 IDENTIFICATION DIVISION.
 PROGRAM-ID.    SYAPBDAN.
 ENVIRONMENT DIVISION.
 CONFIGURATION SECTION.
 SPECIAL-NAMES.
     DECIMAL-POINT IS COMMA.
 INPUT-OUTPUT SECTION.
 FILE-CONTROL.
     SELECT   IN01    ASSIGN TO UT-S-1N01.
     SELECT   OUT01   ASSIGN TO UT-S-OUT01.
 DATA DIVISION.
 FILE SECTION.
 FD  IN01
     LABEL RECORD STANDARD
     BLOCK CONTAINS 0 RECORDS
     RECORDING MODE V.
 01  REC-IN01                              PIC X(121).
 FD  OUT01
     LABEL RECORD STANDARD
     BLOCK CONTAINS 0 RECORDS
     RECORDING MODE F.
 01  REC-OUT01                             PIC X(80).
 WORKING-STORAGE SECTION.
* ------------------------------------ *
* SYSIN - UCBTYPE.                     *
* ------------------------------------ *
 01  W-PARAM.
   03  W-PAR-OLD                           PIC X(8).
   03  W-PAR-NEW                           PIC X(8).
* --------------------------------- *
 01  FIL-TAB.
   03  FILLER                              PIC X(20) VALUE
                         "78008080780480803480".
   03  FILLER                              PIC X(20) VALUE
                         "78008080780480813490".
   03  FILLER                              PIC X(20) VALUE
                         "78048080780480813490".
   03  FILLER                              PIC X(20) VALUE SPACE.
   03  FILLER                              PIC X(20) VALUE SPACE.
   03  FILLER                              PIC X(20) VALUE SPACE.
   03  FILLER                              PIC X(20) VALUE SPACE.
   03  FILLER                              PIC X(20) VALUE SPACE.
   03  FILLER                              PIC X(20) VALUE SPACE.
   03  FILLER                              PIC X(20) VALUE SPACE.
 01  W-TABELLA REDEFINES FIL-TAB.
   03  FILLER      OCCURS 10.
     05 FIL-PARAM.
       07 EL-TAB-OLD                       PIC X(8).
       07 EL-TAB-NEW                       PIC X(8).
     05 EL-TAB-DEVT                        PIC X(4).
* ---------------------------- *
 01  W-IN01-1.
   03  FILLER                              PIC X.
   03  W-TIPO-1.
     05  W-TIPO-FILE                       PIC X(7).
     05  FILLER                            PIC X(9).
     05  W-NOME-FILE                       PIC X(44).
     05  FILLER                            PIC X(60).
   03  W-TIPO-2 REDEFINES W-TIPO-1.
     05  FILLER                            PIC X(7).
     05  W-TIPO-VOL                        PIC X(6).
     05  FILLER                            PIC X(12).
     05  W-VOLUME                          PIC X(6).
     05  FILLER                            PIC X(5).
     05  W-TIPO-DEV                        PIC X(7).
     05  FILLER                            PIC X(8).
     05  W-DEVICE-TYPE                     PIC X(8).
     05  FILLER                            PIC X(6).
     05  W-TIPO-FSEQ                       PIC X(5).
     05  FILLER                            PIC X(15).
     05  W-SEQUENCE                        PIC X(4).
     05  FILLER                            PIC X(31).
   03  W-TIPO-3 REDEFINES W-TIPO-1.
     05  FILLER                            PIC X(5).
     05  W-TAPPO                           PIC X(12).
     05  FILLER                            PIC X(62).
* ----------------------------- *
 01  W-OUT01-01.
   03  FILLER                              PIC X VALUE SPACE.
   03  FILLER                              PIC X(7) VALUE
       "DELETE ".
   03  W-NOME-01                           PIC X(44) VALUE SPACE.
   03  FILLER                              PIC X(16) VALUE
       " NOSCRATCH PURGE".
 01  W-OUT01-02.
   03  FILLER                              PIC X VALUE SPACE.
   03  FILLER                              PIC X(15) VALUE
       "DEF NVSAM(NAME(".
   03  W-NOME-02                           PIC X(44) VALUE SPACE.
   03  FILLER                              PIC X(3) VALUE
       ") -".
 01  W-OUT01-03.
   03  FILLER                              PIC X VALUE SPACE.
   03  FILLER                              PIC X(6) VALUE
       " DEVT(".
   03  W-DEVTYPE                           PIC X(4) VALUE SPACE.
   03  FILLER                              PIC X(3) VALUE
       ") -".
 01  W-OUT01-04
   03  FILLER                              PIC X VALUE SPACE.
   03  FILLER                              PIC X(9) VALUE
       " FSEQN( -".
 01  W-OUT01-05.
   03  FILLER                              PIC X VALUE SPACE.
   03  FILLER   OCCURS 11.
     05  W-FIL-05                          PIC X.
     05  W-FSEQ                            PIC X(4).
 01  W-OUT01-06.
   03  FILLER                              PIC X VALUE SPACE.
   03  FILLER                              PIC X(3) VALUE
       ") -".
 01  W-OUT01-07.
   03  FILLER                              PIC X VALUE SPACE.
   03  FILLER                              PIC X(7) VALUE
       " VOL( -".
 01  W-OUT01-08.
   03  FILLER                              PIC X VALUE SPACE.
   03  FILLER  OCCURS 11.
     05  W-FIL-08                          PIC X.
     05  W-VOL                             PIC X(6).
 01  W-OUT01-09.
   03  FILLER                              PIC X VALUE SPACE.
   03  FILLER                              PIC X(2) VALUE "))".
 01  CHAR                                  PIC X VALUE "Y".
 01  CTR                                   PIC 9(4) VALUE ZERO.
 01  IDX                                   PIC 9(4) VALUE ZERO.
 01  POP                                   PIC 9(4) VALUE ZERO.
 01  TOT-TROVATI                           PIC 9(6) VALUE ZERO.
 01  TOT-TRATTATI                          PIC 9(6) VALUE ZERO.
 01  TAB-COMODO.
   03  EL-COMODO  OCCURS 1000              PIC X(6).
 PROCEDURE DIVISION.
     ACCEPT  W-PARAM FROM SYSIN
     IF      W-PARAM = SPACE
         DISPLAY " ERROR: INVALID UCBTYPE " W-PARAM
         STOP RUN
     END-IF
     PERFORM WITH TEST AFTER
       VARYING CTR FROM 1 BY 1
         UNTIL CTR > 9
            IF FIL-PARAM(CTR) = SPACE
               MOVE 20 TO CTR
            END-IF
            IF W-PARAM = FIL-PARAM(CTR)
               MOVE EL-TAB-DEVT(CTR) TO W-DEVTYPE
               MOVE 20 TO CTR
            END-IF
     END-PERFORM
     IF W-DEVTYPE = SPACE
        DISPLAY " ERROR: WRONG UCBTYPE " W-PARAM
        STOP RUN
     END-IF
     PERFORM AZZERAMENTI
     OPEN    INPUT IN01.
     READ    IN01 INTO W-IN01-1
          AT END
             DISPLAY " ERROR: EMPTY FILE IN01 "
             MOVE "N" TO CHAR
     END-READ
     OPEN    OUTPUT OUT01.
     PERFORM WITH TEST AFTER
       UNTIL CHAR NOT = "Y"
          IF W-TIPO-FILE = "NONVSAM"
             ADD     1 TO TOT-TROVATI
             MOVE    W-NOME-FILE TO W-NOME-01
             PERFORM VERIFICA
          END-IF
          READ  IN01 INTO W-IN01-1
              AT END
                 PERFORM SCRITTURA-FINE
                 MOVE    "N" TO CHAR
          END-READ
     END-PERFORM
     DISPLAY "* ----------------------------------- *"
     DISPLAY "*                                     *"
     DISPLAY "* TOTAL     DATASET   ..... " TOT-TROVATI  " *"
     DISPLAY "* CONVERTED DATASET   ..... " TOT-TRATTATI " *"
     DISPLAY "*                                     *"
     DISPLAY "* ----------------------------------- *"
     CLOSE IN01 OUT01.
     STOP RUN.
* -------------------------------------------------------------- *
 AZZERAMENTI SECTION.
     PERFORM WITH TEST AFTER
       VARYING CTR FROM 1 BY 1
         UNTIL CTR > 10
            MOVE SPACE TO W-FIL-05(CTR)
            MOVE SPACE TO W-FSEQ(CTR)
            MOVE SPACE TO W-FIL-08(CTR)
            MOVE SPACE TO W-VOL(CTR)
     END PERFORM
     MOVE SPACE TO W-NOME-01 W-NOME-02 TAB-COMODO.
* ------------------------------------------------------------ *
 VERIFICA SECTION.
     PERFORM WITH TEST AFTER
       UNTIL CHAR NOT = "Y"
          READ   IN01 INTO W-IN01-1
              AT END
                 PERFORM SCRITTURA-FINE
                 MOVE "N" TO CHAR
              NOT END
                 IF  W-TIPO-DEV = "DEVTYPE"
                     IF W-DEVICE-TYPE = W-PAR-OLD
                          PERFORM MODIFICA
                     END-IF
                   ELSE
                      IF W-TAPPO = "ASSOCIATIONS"
                         MOVE "K" TO CHAR
                         IF POP > ZERO
                            PERFORM SCRITTURA-FINE
                            MOVE ZERO TO POP
                            PERFORM AZZERAMENTI
                         END-IF
                      END-IF
                 END-IF
          END-READ
     END-PERFORM
     IF CHAR = "K"
        MOVE "Y" TO CHAR
     END-IF.
* ------------------------------------------------------------- *
 MODIFICA SECTION.
     IF W-NOME-02 NOT EQUAL W-NOME-01
        MOVE    W-NOME-01 TO W-NOME-02
        PERFORM SCRITTURA-INIZIO
        ADD     1 TO TOT-TRATTATI
     END-IF
     ADD 1 TO IDX POP
     IF IDX = 11
        MOVE    "-" TO W-FSEQ(IDX)
        PERFORM SCRITTURA-FSEQ
        MOVE    1 TO IDX
     END-IF
     MOVE    W-VOLUME TO EL-COMODO(POP)
     INSPECT W-SEQUENCE REPLACING ALL "-" BY " "
     MOVE    W-SEQUENCE TO W-FSEQ(IDX).
* ------------------------------------------------------------- *
 SCRITTURA-INIZIO SECTION.
     WRITE REC-OUT01 FROM W-OUT01-01
     WRITE REC-OUT01 FROM W-OUT01-02
     WRITE REC-OUT01 FROM W-OUT01-03
     WRITE REC-OUT01 FROM W-OUT01-04.
* -------------------------------------------------------------.*
 SCRITTURA-FSEQ SECTION.
     WRITE REC-OUT01 FROM W-OUT01-05
     MOVE  SPACE TO W-OUT01-05.
* ------------------------------------------------------------- *
 SCRITTURA-FINE SECTION.
     IF    W-FSEQ(1) NOT EQUAL SPACE
           ADD 1 TO IDX
           MOVE "-" TO W-FSEQ(IDX)
           WRITE REC-OUT01 FROM W-OUT01-05
           WRITE REC-OUT01 FROM W-OUT01-06
           WRITE REC-OUT01 FROM W-OUT01-07
     END-IF
     MOVE  ZERO TO IDX
     PERFORM WITH TEST BEFORE
       VARYING CTR FROM 1 BY 1
         UNTIL CTR > POP
            ADD 1 TO IDX
            IF IDX = 11
               MOVE "-" TO W-VOL(IDX)
               WRITE REC-OUT01 FROM W-OUT01-08
               MOVE SPACE TO W-OUT01-08
               MOVE 1 TO IDX
            END-IF
            IF EL-COMODO(CTR) NOT EQUAL SPACE
               MOVE EL-COMODO(CTR) TO W-VOL(IDX)
            END-IF
     END-PERFORM
     IF W-VOL(1) NOT EQUAL SPACE
        ADD 1 TO IDX
        MOVE "-" TO W-VOL(IDX)
        WRITE REC-OUT01 FROM W-OUT01-08
        WRITE REC-OUT01 FROM W-OUT01-09
     END-IF
     MOVE ZERO TO IDX.
SAMPLE EXECUTION JCL
//XXXXXXXX JOB XXXXXX,XXXXXX,CLASS=X,MSGCLASS=X,REGION=5M
//**------------------------------------------------------------------**
//STEP00   EXEC PGM=IDCAMS
//SYSPRINT DD   DSN=&&TEMP1,UNIT=VIO,SPACE=(TRK,75),DISP(,PASS),
// DCB=(LRECL=125,BLKSIZE=6050,RECFM=VB)
//SYSIN    DD   *
 LISTC LVL(ZZZZZZZZZZZZZZZ) ALL
/*
//**------------------------------------------------------------------**
//STEP01   EXEC PGM=SYAPBDAN
//STEPLIB  DD   DSN=XXX.X.XX,DISP-SHR                  <--- YOUR STEPLIB
//IN01     DD   DSN=&&TEMP1,DISP=(OLD,PASS)
//OUT01    DD   DSN=XXXXXXXX,DISP=OLD,                 <--- OUTPUT D/S
//   DCB=(LRECL=80,RECFM=6160,BLKSIZE=FB)
//SYSOUT   DD   SYSOUT=*
//SYSABEND DD   SYSOUT=*
//SYSABOUT DD   SYSOUT=*
//SYSIN    DD   *
7804808178048080
/*
//


                                                                                                           c  Xephon 1994

