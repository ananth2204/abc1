Disallowing multiple sign-ons in IMS

DFSCSGN0 is a user sign-on verification exit which prevents
users from signing on multiple times with the same user-id.
DFSCSGN0 does a GETMAIN and creates an in-core table,
which contains all the user-ids and terminal-ids of each signed-
on user. If a user attempts to sign on a second time with a user-
id contained in our table, the sign-on attempted is denied and a
message is sent to the terminal he or she is attempting to sign
on to. DFSCSGN0 uses DFSCMTU0, which informs the user
that the sign-on request has been denied and tells him or her to
depress the PA1 key. After depressing the PA1 key, our
DFSCSGN0-generated message is displayed which informs the
user that his or her user-id is in use and on what terminal.

The IMS DFSCSGN0 user sign-on verification exit was created
for our IMS systems with RACF 1.8 or 1.9 as our security
system. Our DFSCSGN0 is designed to build an internal table
that will contain the user-ids and terminal-ids of everyone
signed on to our IMS systems. If a user attempts to sign on a
second time with the same user-id the sign-on request is denied
and he or she receives the following message:

      DFSU513 SIGNON REQUEST DENIED. DEPRESS PA1 KEY FOR MORE INFO

after depressing the PA1 key the next message is:

      SGN001 YOUR USERID xxxxxxxx IS ALREADY IN USE ON TERMINAL yyyyyyyy

Once the user signs off the IMS system the table entry is
removed and he or she will be allowed to sign on again.

A message table was created to be used in conjunction with our
user sign-on verification exit DFSCSGN0. If our sign-on exit
(DFSCSGN0) rejects a user sign-on request because he or she
has attempted to sign on multiple times with the same user-id,
we load a value of 513 into register 15 and change the value
513 to negative 513; this causes our IMS system to look for the
positive value of this message code (513) as a key in our
message table (DFSCMTU0) rather than in the IMS system
message table.

The message DFSU513 message is then issued as follows:

      DFSU513 SIGNON REQUEST DENIED. DEPRESS PA1 KEY FOR MORE INFO

Once the user depresses the PA1 key, he or she will receive the
message which is coded in our sign-on user verification exit
DFSCSGN0.

This user message table can be used by any of the following
user edit and exit routines:

o      Physical terminal input edit routines
o      Transaction code input edit routine
o      Message switching input edit routine
o      Queue space notification exit routine
o      Sign on user exit routine.

This exit has been successfully run in both an IMS 2.2 and 3.1
system. No changes to this exit are necessary to run in either an
IMS 2.2 or 3.1 system.

Related publications detailing the DFSCSGN0 /SIGNON exit
routine and the steps necessary to define DFSCSGN0 to your
IMS system are:

o      IMS/ESA Version 3 Customization Guide Data Communication

o      IMS/ESA Version 3 System Definition Reference.


Installation Steps

The following steps are necessary to install the
DFSCSGN0/SIGN ON exit routine into an IMS system.

1      Before stage 1 of IMS system definition, you need to specify:

      SECURITY TYPE=(...SIGNEXIT...)

      in the SECURITY macro. For details about this, see the
      SECURITY macro statement description in IMS/ESA
      System Definition  Reference.

2      You must name the /SIGN ON exit routine, DFSCSGN0,
      assemble it, and place it in the PDS defined by the
      USERLIB operand of the IMSGEN macro. For details
      about this, see the IMSGEN macro statement description in
      the IMS/ESA System Definition Reference.

3      After coding the necessary macro changes you must run
      your IMS system definition stage 1. The stage 1 output is
      the input to system definition stage 2. You must then run
      your stage 2 jobs. For a complete description see IMS/ESA
      System Definition Reference.


DFSCSGN0 SOURCE CODE

CSGN     TITLE 'CSGN COMM /SIGN EXIT USAIR'
***********************************************************************
* MODULE NAME: DFSCSGN0                                               *
* DESCRIPTIVE NAME: COMM SIGN EXIT FOR USAIR                          *
* COPYRIGHT: REFER TO MODULE DFSAADC0                          @BMY0200
* STATUS: VERSION 1.1.5                                               *
* FUNCTION:                                                           *
*    THIS DFSCSGN0 WILL ADD THE USERID SIGNIN ON TO IMS INTO AN INCORE*
*    TABLE THAT IS BUILT VIA REGISTER 5. IF THE USER SIGNS OFF FROM   *
*    IMS THE ENTRY IS REMOVED FROM THE TABLE. IF THE USER ATTEMPTS    *
*    TO SIGN ON AND THEIR USERID IS IN THE TABLE THEY WILL RECEIVE    *
*    THE FOLLOWING MESSAGES.                                          *
* 'DFSU513 SIGNON REQUEST DENIED. DEPRESS PA1 KEY FOR MORE INFO.'     *
*               UPON ENTERING THE PA1 KEY THIS MESSAGE WILL APPEAR    *
* 'SGN001 YOUR USERID XXXXXXX IS ALREADY IN USE ON TERMINAL YYYYYYYY' *
******************* PLEASE NOTE ***************************************
*    IF THE RACF OPTION IS SELECTED IN IMS/VS SYSTEM DEFINITION,      *
*    THEN THIS EXIT WILL BE INVOKED AFTER THE RACF SIGNON             *
*    VERIFICATION, AND ONLY IF THE RACF CHECK IS SUCCESSFUL.          *
*    IF THE RACF CHECK FAILS, THEN DFSCSGN0 WILL NOT BE CALLED.       *
***********************************************************************
*    RESTRICTIONS:       PLEASE NOTE ---- VERY IMPORTANT              *
*    THIS USER EXIT ROUTINE HAS BEEN CODED FOR USERIDS THAT ARE       *
*    SEVEN CHARACTERS. IF YOUR USERIDS ARE LESS THAN SEVEN CHARACTERS *
*    YOU MUST PROVIDE THE NECESSARY SPACES. IF THIS IS NOT DONE YOUR  *
*    TABLE ENTRY COULD BE BUILT INCORRECTLY.                          *
***********************************************************************
*                        PLEASE NOTE ---- VERY IMPORTANT              *
*    IF THE USERID IS FOUND IN THE INCORE TABLE R15 IS LOADED WITH A  *
*    VALUE OF 513 AND THEN CHANGED TO A NEGATIVE 513 WHICH THEN SENDS *
*    OUR DFSU513 MESSAGE TO THE CURRENT CTB INFORMING THAT THE SIGN-ON*
*    HAS BEEN REJECTED AND MORE INFORMATION FOLLOWS AFTER ENTERING THE*
*    PA1 KEY.                                                         *
***********************************************************************
*                        PLEASE NOTE ---- VERY IMPORTANT              *
*    IF IT BECOMES NECESSARY TO LOOK AT OUR INTERNAL TABLE USED TO    *
*    STORE THE USERIDS AND NODE NAMES THE FOLLOWING PROCESS SHOULD BE *
*    FOLLOWED.                                                        *
*    LOGON TO OMEGAMON/IMS FOR THE RESPECTIVE SYSTEM                  *
*    DUMP THE 'SCD' CONTROL BLOCK AND GO TO                           *
*    FIELD NAME (SCDCSGN0)                                            *
*    THAT  WILL BE THE ADDRESS OF THE DFSCSGN0 EXIT                   *
*    GO TO THAT ADDRESS                                               *
*    ONCE AT THE DFSCSGN0 EXIT GO TO THE OFFSET FOR THE FIELD ANCHOR  *
*    CURRENTLY IT IS '1F4'                                            *
*    THAT WILL BE THE ADDRESS OF OUR INTERNAL TABLE                   *
*    IF THERE ARE MULTIPLE TABLES THE FIRST ENTRY WILL BE THE         *
*    ADDRESS OF THE SECOND TABLE, IF THE AREA HAS X'0000'             *
*    WE HAVE ONLY CREATED ONE TABLE.                                  *
***********************************************************************
* REGISTERS:                                                          *
*        R0    SIGN FUNCTION... ON OR OFF                             *
*              R0 = 0 /SIGN ON                                        *
*              R0 = 1 /SIGN OFF                                       *
*          R1 = A POINTER TO A VARIABLE LENGTH STRING CALLED          *
*               THE USER VERIFICATION STRING (UVS). THE FORMAT        *
*                   OF THE UVS IS:                                    *
*                A.  4 BYTES FOR LLZZ.                                *
*                B.  TEXT (STARTING WITH FIRST CHARACTER OF           *
*                    USERID)                                          *
*                NOTE1: IMS/VS DOES NOT EDIT THIS UVS PRIOR TO        *
*                       INVOKING THIS USER EXIT ROUTINE AFTER         *
*                       THE /SIGN ON COMMAND IS ISSUED OTHER          *
*                       THAN THE REMOVAL OF BLANKS BEFORE THE         *
*                       USERID.                                       *
*                NOTE2: THIS USER EXIT MAY CHANGE THE USERID          *
*                       TO ANY OTHER USERID, AS LONG AS THE           *
*                       LENGTH OF THE UVS REMAINS UNCHANGED.          *
*        IF R0 = 2, THEN                                              *
*              R1 = THE ADDRESS OF THE USERID.                        *
*                  NOTE1: THE FUNCTION CODE OF 2 WILL ONLY BE         *
*                         SPECIFIED BY THE ALTERNATE SYSTEM IN        *
*                         AN XRF ENVIRONMENT.                         *
*                  NOTE2: THIS USER EXIT MAY CHANGE THE USERID        *
*                         TO ANY OTHER USERID.                        *
*        IF R0 = 1 OR 3, THEN                                         *
*              R1 WILL BE INSIGNIFICANT.                              *
*        R7 = SOURCE CTB POINTER                                      *
*       NOTE1: IF RACF IS NOT USED WITH IMS/VS, THEN CTBRACFT         *
*              MAY BE USED WITH THIS ROUTINE. THIS WILL ENABLE        *
*              DFSCSGN0 TO SAVE A POINTER ASSOCIATED WITH THE         *
*              USERID BETWEEN /SIGN ON AND /SIGN OFF TO               *
*              ELIMINATE SEARCHING FOR THE USER PROFILE               *
*              EACH TIME DFSCTRN0 IS INVOKED FOR TRANSACTION          *
*              AUTHORIZATION.                                         *
*        R11 = ADDRESS OF SCD                                         *
*        R13-R15 = STANDARD OS CALLING SEQUENCE                       *
*          NOTE1: THE STANDARD REGISTER SAVE AREA FIRST THREE         *
*                 WORDS MUST NOT BE MODIFIED.                         *
* OUTPUT:                                                             *
*        UPON RETURN THE FOLLOWING RETURN CODES WILL BE USED IN R15:  *
*   RC = 0 ACCEPT THE SIGNON                                          *
*   RC =  NEGATIVE, SEND USER DEFINED MESSAGE NUMBER AND REJECT       *
*              THE SIGNON. THE NUMBER WILL BE COMPLEMENTED INTO A     *
*              MESSAGE NUMBER. THIS NUMBER MUST BE LESS THAN -24      *
*              OR A DFS2467 WILL BE SENT INSTEAD.                     *
*   RC=  POSITIVE, REJECT THE SIGNON. THIS NUMBER WILL APPEAR         *
*              AS A SUBCODE ON THE DFS2467 MESSAGE.                   *
***********************************************************************
     DFSCMTI FUNC=DSECT
DFSCSGN0 CSECT
         CHANGEID NAME=DFSCSGN0&SYSDATE&SYSTIME,                       X
               BASE=12,LINKAGE=IMS
         CHANGEID IDEND=YES
         USING SCD,R11             SCD ADDRESSABILITY
         USING CTB,R7              CTB ADDRESSABILITY
         LR    R4,R1               SAVE POINTER TO USERID
         CL    R0,ONE              TEST FOR SIGNOFF FUNCTION
         BH    RET0                SKIP XRF CONDITIONS
         BE    SIGNOFF             BRANCH IF SIGNOFF
*        FUNCTION IS SIGNON                                           *
         ICM   R8,15,ANCHOR        GET POINTER TO USER TABLE
         BNZ   HAVTBL              ASSUME POINTER IS VALID
         LA    R8,ANCHOR           POINT TO ANCHOR
*        ACQUIRE AND INITIALIZE TABLE SPACE                           *
ADDTBL   GETMAIN RC,LV=24576,BNDRY=PAGE,LOC=(ANY,ANY)
         LTR   R15,R15             GETMAIN SUCCESSFUL ??
         BNZ   RET0                NO - SUSPEND FURTHER VERIFICATIONS
         LR    R2,R1               GET ADDRESS IN R2
         L     R3,TABSIZE          SET UP BYTE COUNT
         SLR   R5,R5               "
         MVCL  R2,R4               INITIALIZE AREA
         ST    R1,0(,R8)           SAVE POINTER FOR FUTURE REFERENCE
*        MVC   4(4,R8),=C'UNNT'    CREATE A EYECATCHER
         LR    R8,R1               GET POINTER IN R8
         B     NEWTBL              NO NEED TO SCAN NEW TABLE
*        SEARCH EXISTING TABLE FOR USERID                             *
HAVTBL   LA    R14,24576/16-1      SET UP FOR SEARCH
         LA    R5,16(,R8)          POINT TO FIRST USER
SRCH1    CLC   0(8,R5),4(R4)       COMPARE USER TO TABLE
         BNE   CSRCH               NOT FOUND BRANCH & CONTINUE SEARCH
         L     R10,CTBCLB          LOAD CTB ADDRESS INTO R10
         USING IECTDECB,R10        CLB ADDRESSABILITY
         CLC   8(8,R5),CLBNAME     COMPARE NODE NAME TO TABLE ENTRY
         BNE   ERRS010             DIFFERENT NODE REFUSE SIGNON
         B     RET0                SAME NODE ALLOW SIGNON
CSRCH    LA    R5,16(,R5)          POINT TO NEXT ENTRY
         BCT   R14,SRCH1           SEARCH ENTIRE TABLE
         ICM   R8,15,0(R8)
         BNZ   HAVTBL              BRANCH ON NOT ZEROS TO HAVTBL
         L     R8,ANCHOR           LOAD VALUE OF ANCHOR INTO R8
*        USER NOT FOUND - FIND EMPTY SLOT AND ADD HIM                 *
NEWTBL   LA    R14,24576/16-1      RESET MAX USERS
EMPTLP1  CL    R14,4(,R8)          ANY ROOM LEFT ??
         BH    FINDEMPT
FEMPNEXT ICM   R6,15,0(R8)
         BZ    ADDTBL
         LR    R8,R6
         B     EMPTLP1
FINDEMPT DS    0H
         LA    R5,16(,R8)          POINT TO FIRST SLOT
SRCH2    OC    0(8,R5),0(R5)       EMPTY ENTRY ??
         BZ    ADDUSER             YES - ADD THE USER
         LA    R5,16(,R5)          POINT TO NEXT SLOT
         BCT   R14,SRCH2           SEARCH THEM ALL
         B     FEMPNEXT            SEE IF A ANOTHER TABLE EXISTS
ADDUSER  MVC   0(8,R5),4(R4)       PLUG IN THE USER
         L     R14,CTBCLB          LOAD CTB ADDRESS INTO R14
         USING IECTDECB,R14        CLB ADDRESSABILITY
         MVC   8(8,R5),CLBNAME     PLUG IN THE NODE NAME USER IS ON
         DROP  R14
         LA    R14,1               INCREMENT USER COUNT
         AL    R14,4(,R8)
         ST    R14,4(,R8)
         B     RET0                GO TO COMMON RETURN         @BM00028
*        FUNCTION IS SIGNOFF                                          *
SIGNOFF  LA    R8,ANCHOR           POINT TO ANCHOR
NEXTBL2  ICM   R8,15,0(R8)         GET POINTER TO TABLE
         BZ    RET0                SOMETHING IS AMUCK
         LA    R14,24576/16-1      SET UP FOR SEARCH
         LA    R5,16(,R8)          POINT TO FIRST USER
SRCH3    CLC   0(8,R5),CTBUSID     COMPARE USER TO TABLE
         BE    REMOVE              FOUND HIM - REMOVE ENTRY
         LA    R5,16(,R5)          POINT TO NEXT ENTRY
         BCT   R14,SRCH3           SEARCH ENTIRE TABLE
         B     NEXTBL2             SEARCH SUBSEQUENT TABLES
REMOVE   XC    0(16,R5),0(R5)      CLEAR ENTRY
         L     R5,4(,R8)           PICK UP USER COUNT
         BCTR  R5,0                SUBTRACT 1
         ST    R5,4(,R8)           REPLACE UPDATED USER COUNT
         B     RET0                TAKE NORMAL EXIT
ERRS010  EQU   *
         L     R2,CTBCLB           POINT TO ECB AT CLB
         LA    R9,CMTIAWE          POINT TO AWE
         USING DFSAWE,R9           AWE ADDRESSABILITY
         LA    R3,AWEHTEXT         POINT TO MESSAGE AREA
         MVC   0(ML0015,R3),SGNL1  MOVE MSG TO LOG WORK AREA
         MVC   TAGID-SGNL1(L'TAGID,R3),0(R5)
         MVC   TRMID-SGNL1(L'TRMID,R3),8(R5)
         XR    R6,R6               CLEAR R6
         L     R6,CTBCNT           GET CNT ADDRESS
*        WORKING STORAGE
         DFSCMTI  FUNC=MESSAGE,                                        X
               LTERM=(CNT,(R6)),                                       X
               PARMS=PREEDIT,                                          X
               AWE=(R9),                                               X
               GET=(GETBCB,MSGP),                                      X
               ECB=(R2),                                               X
               SCD=(R11),                                              X
               WTO=NO,                                                 X
               BRANCH=YES
ERRS020  EQU   *
         L     R2,CTBCLB           POINT TO ECB AT CLB
         LA    R9,CMTIAWE          POINT TO AWE
         USING DFSAWE,R9           AWE ADDRESSABILITY
         LA    R3,AWEHTEXT         POINT TO MESSAGE AREA
         MVC   0(ML0015,R3),SGNL1  MOVE MSG TO LOG WORK AREA
         MVC   TAGID-SGNL1(L'TAGID,R3),0(R5)
         MVC   TRMID-SGNL1(L'TRMID,R3),8(R5)
         XR    R6,R6               CLEAR R6
         L     R6,SCDPMTRM         GET CNT ADDRESS
*        WORKING STORAGE
         DFSCMTI  FUNC=MESSAGE,                                        X
               LTERM=(CNT,(R6)),                                       X
               PARMS=PREEDIT,                                          X
               AWE=(R9),                                               X
               GET=(GETBCB,MSGP),                                      X
               ECB=(R2),                                               X
               SCD=(R11),                                              X
               WTO=NO,                                                 X
               BRANCH=YES
         LA    R15,513             REJECT SIGNON
         LNR   R15,R15             CHANGE 513 TO NEGATIVE 513
         B     EXIT                EXIT
RET0     SLR   R15,R15             SET RETURN CODE
EXIT     L     R13,4(,R13)         UNCHAIN SAVEAREAS
         L     R14,12(,R13)        RESTORE RETURN ADDRESS
         LM    R0,R12,20(R13)      RESTORE REGS R0-R12
         BR    R14                 RETURN TO CALLER
*        DC    C'UNNT ANCHOR'
ANCHOR   DC    F'0'                POINTER TO USER TABLE(S)
ONE      DC    F'1'                COMPARE VALUE
TABSIZE  DC    F'24576'            TABLE SIZE (6 PAGES)
CMTIAWE  DC    256X'00'
SGN001   DFSCMTI FUNC=MSG,                                             X
               WTO=NO,                                                 X
               TEXT=((SGNL1,LLZZ),                                     X
               (CL7'SGN001 '),                                         X
               (CL12'YOUR USERID '),                                   X
               (TAGID,CL8' '),                                         X
               (CL31' IS ALREADY IN USE ON TERMINAL '),                X
               (TRMID,CL8' '))
SGNEND   EQU  *
SGNLN1   EQU  SGNEND-SGN001
   ISCD  SCDBASE=0
   ICLI  CLBBASE=0
   IDLI  PSTBASE=0,PDRBASE=0,DDRBASE=0
CLB      EQU   IECTDECB
   ICLI  CTBBASE=0,CNTBASE=0
   DFSDAWE   FUNC=DSECT,TYPE=BCB
   DFSBCB    FUNC=DSECT
   DBFFPCP
   DBFEPST
   REQUATE
   END


Installing THE User Message Table

1      Before stage 1 of IMS system definition, you need to
      specify OPTIONS=(...USERMSG,...) in the COMM
      macro. For details about this, see the COMM macro
      statement description in IMS/ESA System Definition
      Reference.

2      You must name the message table module DFSCMTU0,
      assemble it, and place it in the PDS defined by the
      USERLIB operand of the IMSGEN macro. For details
      about this, see the IMSGEN macro statement description in
      the IMS/ESA System definition Reference.

3      After coding the necessary MACRO changes you must run
      your IMS system definition stage 1. The stage 1 output is
      the input to system definition stage 2. You must then run
      your stage 2 jobs. For a complete description see IMS/ESA
      System Definition Reference.

DFSCMTU0 CSECT
***********************************************************************
* MODULE NAME: DFSCMTU0                                               *
* DESCRIPTIVE NAME: IMS USER COMMUNICATION MESSAGE TABLE              *
* THIS MODULE WILL BE INVOKED FROM:                                   *
* (1) DFSCSGN0 WHEN A USER HAS ATTEMPTED TO SIGN ON TO OUR IMS SYSTEM *
*     A SECOND TIME WITH THE SAME USERID.                             *
***********************************************************************
         BALR  15,14
M013     DC    H'513'
         DC    AL2(M999-M013)
         DC    C'SIGNON REQUEST DENIED. DEPRESS PA1 KEY FOR MORE INFO'
M999     DC    X'7FFF'
         END


Bruce T Chalmers
Senior Systems Programmer
USAIR Inc (USA)                                          c Xephon 1993

