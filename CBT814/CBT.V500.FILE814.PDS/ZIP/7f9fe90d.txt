Primary delete command

      d 22

      d *

      d update

/* ---------- Rexx ---------------- */
/* This Rexx program deletes lines  */
/* -------------------------------- */
trace o
all='*'
all=translate(all)                       /* In case '*' is replaced by ALL*/
'ISREDIT MACRO (arg) NOPROCESS'
'ISREDIT PROCESS RANGE /'
if rc=0 then do
  'ISREDIT (First) = LINENUM .ZFRANGE'
  'ISREDIT (Last)  = LINENUM .ZLRANGE'
   end
 else do
  select
   when arg='' then quit='y'
   when translate(arg)=all then do
     'ISREDIT (first,bot) = DISPLAY_LINES'
     'ISREDIT (Last)  = LINENUM .ZLAST'
      end
  otherwise
     'ISREDIT (first,bot) = DISPLAY_LINES'
     if datatype(arg,'W') then do
        last=first+arg-1
        end
     else do
     'ISREDIT FIND ' arg
     if rc^=0 then do
        zedsmsg='*No Action'
        zedlmsg='* Error **** string not found'
        'ISPEXEC SETMSG MSG(ISRZ001)'
        exit
        end
     'ISREDIT (last) = LINENUM .ZCSR'
     if last > 0 then last=last-1
     end
   end
end
if quit='y' then do
   zedsmsg='*No Action'
   zedlmsg='* Error **** No string or ranges specified'
   'ISPEXEC SETMSG MSG(ISRZ001)'
   exit
   end
if last=first then 'ISREDIT DELETE ' first
  else 'ISREDIT DELETE ' first last


Moyeen Ahmed Khan
Senior Systems Designer
Manulife Financial (Canada)                                               c Xephon 1995

