Finding the SYSRES and LINKLIST with REXX

SYSRES REXX EXEC

/* REXX */
/* This rexx uses the CVT field CVTSYSAD to access the UCB for */
/* the SYSRES volume. It then finds the volume serial number of*/
/* the SYSRES from the UCB and returns it to the caller.      */
CVTSYSAD=D2X(C2D(STORAGE(10,4))+48) /* point to cvtsysad */
volser_address=D2X(C2D(STORAGE(CVTSYSAD,4))+28)
sysres=STORAGE(volser_address,6)
SAY sysres

LINKLIST REXX EXEC

/* REXX */
/* this REXX uses the CVT field CVTLLTA to locate the incore table of */
/* link list datasets. The datasets are then returned to the caller   */
CVTLLTA = D2X(C2D(STORAGE(10,4))+1244)
CVTLLTA = D2X(C2D(STORAGE(D2X(C2D(STORAGE(10,4))+1244),4))+4)
dsn_cnt=C2D(STORAGE(CVTLLTA,4))
link_dsn=D2X(X2D(CVTLLTA)+4)      /* POINT TO START OF DSNS */
/*  */
/* LOOP THROUGH THE LINK LIST DATASETS                      */
/* Format of the LLT is 4 bytes of acronym                  */
/* then 4 bytes containing the binary count of number of    */
/* linklist datasets.                                       */
/* then each dataset entry consists of 45 bytes where byte 1*/
/* is the length of the dataset name, and the next 44 bytes */
/* is the name itself                                       */
/*  */
DO cnt=1 TO dsn_cnt
dsn_length=C2D(STORAGE(link_dsn,1))
link_dsn=D2X(X2D(link_dsn)+1)      /* address dataset name */
dsn=STORAGE(link_dsn,dsn_length)
say dsn
link_dsn=D2X(X2D(link_dsn)+44) /* move to next entry */
END


Systems Programmer (UK)                                                          c Xephon 1995

