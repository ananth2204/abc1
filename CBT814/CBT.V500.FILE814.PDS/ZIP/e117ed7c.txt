Pending edit recovery information

The following CLIST, DISEDRC, displays information about
the pending edit recovery.  When you are using edit with the
'recovery on' option, a back-up dataset is created and control
information is stored in the profile dataset.  This CLIST uses
the information stored in the profile dataset and displays the
control information and the application-id corresponding to the
edit recovery.

There are two formats of the display and you can toggle
between them by entering any non-blank character on the
command line.  The CLIST uses two table display panels
called DISEDRP1 and DISEDRP2.


CLIST DISEDRC

PROC 0
/*********************************************************************/
/* The following file is DISEDRC                                     */
/* Include this in a PDS which is in SYSPROC concatenation           */
/*********************************************************************/
  ISPEXEC CONTROL ERRORS RETURN
  CONTROL ASIS
  SET &DDN = &STR(ISPPROF)                    /* ispf profile ddname */
  SET &PAN = &STR(DISEDRP1)                   /* display panel       */
  SET &TB  = &STR(TEMPTAB)                    /* temporary table     */
  ISPEXEC LMINIT DATAID(DID) DDNAME(&DDN) ENQ(SHR)
  SET &RC = &LASTCC
  IF &RC > 0 THEN +
  DO
    SET ZEDSMSG = &STR(LMINIT failed RC=&RC)
    SET ZEDLMSG = &STR(LMINIT failed for ddname "&DDN" with RC=&RC..)
    ISPEXEC SETMSG MSG(ISRZ001)
    EXIT
  END
  ISPEXEC LMOPEN DATAID(&DID) OPTION(INPUT)
  SET &RC = &LASTCC
  IF &RC > 0 THEN +
  DO
    SET ZEDSMSG = &STR(LMOPEN failed RC=&RC)
    SET ZEDLMSG = &STR(LMOPEN failed for ddname "&DDN" with RC=&RC..)
    ISPEXEC SETMSG MSG(ISRZ001)
    EXIT
  END
  SET &ADDCNT = 0
  ISPEXEC TBCREATE &TB NOWRITE +
          NAMES(ZEDSTAT ZEDDISP ZEDBDSN ZEDTDSN ZEDTMEM ZEDTVOL APPL)
  SET RC = &LASTCC
  DO WHILE &RC = 0
    ISPEXEC LMMLIST DATAID(&DID) OPTION(LIST) MEMBER(MEM)
    SET &RC = &LASTCC
    IF &RC = 0 THEN +
    DO
      SET &MEM  = &SUBSTR(1:&LENGTH(&MEM),&MEM)
      SET &A    = &SYSINDEX(EDRT,&STR(&MEM))
      IF &A ^= 0 THEN +
      DO
        IF &A >= 2 THEN +
           SET &APPL = &SUBSTR(1:&A-1,&MEM)
        ELSE +
           SET &APPL = &STR(****)
        ISPEXEC TBOPEN &MEM LIBRARY(&DDN) NOWRITE SHARE
        ISPEXEC TBSKIP &MEM
        SET &SKIPRC = &LASTCC
        DO WHILE &SKIPRC = 0
          IF &ZEDSTAT = &STR(1) THEN +
          DO
             ISPEXEC TBADD &TB
             SET &ADDCNT = &ADDCNT + 1
          END
          ISPEXEC TBSKIP &MEM
          SET &SKIPRC = &LASTCC
        END
        ISPEXEC TBEND &MEM
      END                      /* A ^= 0   */
    END
  END                          /* do while */
  IF &ADDCNT <= 0 THEN +
  DO
    SET ZEDSMSG = &STR(No pending recovery)
    SET ZEDLMSG = &STR(No pending edit recovery)
    SET &MSG = &STR(ISRZ000)
  END
  ELSE +
    SET &MSG = &STR()
REDISP: +
  ISPEXEC TBTOP &TB
  ISPEXEC TBDISPL &TB PANEL(&PAN) MSG(&MSG)
  SET &DISPRC = &LASTCC
  IF &DISPRC = 0 THEN +
  DO
/*********************************************************************/
/* Enter a non blank char on the command field to toggle between     */
/* two display formats.(Short and long formats).                     */
/*********************************************************************/
    IF &PAN = &STR(DISEDRP1) THEN SET &PAN = &STR(DISEDRP2)
    ELSE SET &PAN = &STR(DISEDRP1)
    GOTO REDISP
  END
  ISPEXEC TBEND &TB
  CONTROL NOMSG
  ISPEXEC LMMLIST DATAID(&DID) OPTION(FREE)
  ISPEXEC LMCLOSE DATAID(&DID)
  ISPEXEC LMFREE  DATAID(&DID)
EXIT


PANEL DISEDRP1

)ATTR
 @ TYPE(OUTPUT) INTENS(LOW)
 $ TYPE(OUTPUT) INTENS(HIGH)
)BODY EXPAND(]])
%-]-]- Display all pending edit recovery -]-]-
%COMMAND INPUT ===>_ZCMD                                      %SCROLL ===>_AMT +
+
+-------------------------------------------------------------------------------
+Applid Bkup DSN               Target DSN                       Mem       Vol
+-------------------------------------------------------------------------------
)MODEL
$Z     @Z                     @Z                               @Z        @Z
)INIT
  .ZVARS = '(APPL ZEDBDSN ZEDTDSN ZEDTMEM ZEDTVOL)'
  &AMT = CUR
)END


PANEL DISEDRP2

)ATTR
 @ TYPE(OUTPUT) INTENS(LOW)
 $ TYPE(OUTPUT) INTENS(HIGH)
)BODY EXPAND(]])
%-]-]- Display all pending edit recovery -]-]-
%COMMAND INPUT ===>_ZCMD                                      %SCROLL ===>_AMT +
+
+-------------------------------------------------------------------------------
+Applid Bkup/Target DSN                                         Mem       Vol
+-------------------------------------------------------------------------------
)MODEL
$Z     @Z
       @Z                                                      @Z        @Z
+-------------------------------------------------------------------------------
)INIT
  .ZVARS = '(APPL ZEDBDSN ZEDTDSN ZEDTMEM ZEDTVOL)'
  &AMT = CUR
)END


G Lalitha (Australia)                                          c Xephon 1992

