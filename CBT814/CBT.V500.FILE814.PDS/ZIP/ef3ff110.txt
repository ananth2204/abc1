Cleaning up the VTOC

PROBLEM

In our environment, where all files are accessed via the
catalogue, we have observed files that were not catalogued and
which had been allocated disk space yet were not being
utilized.  Some of these were uncatalogued for later deletion
and were eventually forgotten.  Others, owing to problems in
the utilization of utilities, were recorded without a pointer in
the catalogue.  This could be caused, for example, by omitting
the catalogue option from a restore job.


SOLUTION

A short CLIST was written in order to list the contents of the
VTOC of a specific disk, to verify that each of the files is in
fact catalogued, and to create a report showing those that are
not catalogued.  Results thus obtained enabled us to see files
that had been forgotten and a great deal of disk space was made
available.


PROC 0 VOL()
/*---------------------------------------------------------*/
CONTROL NOMSG
IF .&VOL EQ . THEN   +
      DO
      WRITE 'THE CORRECT SYNTAX IS ====>  CLEANVTC VOL(VOLSER)'
      WRITE 'WHERE VOLSER = VOLUME THAT WILL SEARCHED'
      EXIT
      END
  ELSE  SET VOLSER = &VOL
/*---------------------------------------------------------*/
DELETE 'OUTPUT.LIST.VTOC'
DELETE 'OUTPUT.NOT.CATAL'
FREE FI(SYSPRINT.SYSIN)
/*---------------------------------------------------------*/
/* CHANGE VOLUME BELOW TO SUIT YOUR INSTALLATION.          */
/*---------------------------------------------------------*/
ALLOC FILE(SYSPRINT) NEW REU DSNAME('OUTPUT.LIST.VTOC')  +
                    SPACE(1,1) CYL DSORG(PS)             +
                    RECFM(F B A) LRECL(121) BLKSIZE(121) +
                    UNIT(3380) VOLUME(XXXXXX) CATALOG
/*---------------------------------------------------------*/
/* THE DSNAME BELOW SHALL BE ALLOCATED IN ALL VOLUMES      */
/*---------------------------------------------------------*/
ALLOC FILE(DDX) DS(CLEAN.&VOLSER.) UNIT(3380)            +
                    VOLUME(&VOLSER) SHR
ALLOC FILE(SYSIN) SPACE(1,1) TRACK LRECL(80) RECFM(F)    +
  BLKSIZE(80) REUSE
/*---------------------------------------------------------*/
OPENFILE SYSIN OUTPUT
SET SYSIN = &STR(  LISTVTOC VOL=3380=&VOLSER)
PUTFILE SYSIN
CLOSFILE SYSIN
/*---------------------------------------------------------*/
/* CHANGE SYS1.LINKLIB BELOW TO SUIT YOUR INSTALLATION     */
/*---------------------------------------------------------*/
TSOEXEC CALL 'SYS1.LINKLIB(IEHLIST)'
FREE FI(SYSPRINT.SYSIN.DDX)
/*---------------------------------------------------------*/
ALLOC FILE(VTOC) DSNAME('OUTPUT.LIST.VTOC') SHR
ALLOC FILE(DMMY) SYSOUT(S)
/*---------------------------------------------------------*/
OPEN: +
  OPENFILE VTOC INPUT
  OPENFILE DMMY OUTPUT
GETFILE: +
  ERROR GOTO NOMORE
  GETFILE VTOC
  SET DSN = &SUBSTR(2:44,&VTOC)
  SET ISDSN = &SUBSTR(51:51,&VTOC)
  IF &ISDSN ^= . THEN GOTO GETFILE
  LISTDSI &DSN
  IF &LASTCC > 4 THEN  +
     DO
     SET DMMY = &DSN
     PUTFILE DMMY
     END
  GOTO GETFILE
/----------------------------------------------------------*/
NOMORE: +
  ERROR OFF
  CLOSFILE VTOC
  CLOSFILE DMMY
FREE FILE(VTOC)
FREE FILE(DMMY)
EXIT
/*---------------------------------------------------------*/


Wilson Correia Gil
Support Analyst (Brazil)                                      c Xephon 1991

