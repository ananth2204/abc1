REXX to compare AMBLIST LISTIDRs

Here's a TSO ISPF REXX which runs the IBM service aid
program AMBLIST to request LISTIDR reports against two
load modules then compares the reports using the ISPF
SuperCompare program.  The resulting report showing the
differences can be used to see which CSECTs were added,
changed, or dropped during the transition of programs from test
to production.  The output is narrowed in the SuperCompare
step to accommodate users of 80-column TSO terminals.

The COMPIDR REXX can be invoked as a TSO command
within ISPF.  For example, to compare
DPOPROD.DB2.LOADLIB (BUOGIVL) against
SSVSAG.MASTER.LOAD(BUOGIVL), invoke:

      COMPIDR 'DPOPROD.DB2.LOADLIB(BUOGIVL)' 'SSVSAG.MASTER.LOAD(BUOGIVL)'

or

      COMPIDR  DPOPROD.DB2.LOADLIB BUOGIVL    SSVSAG.MASTER.LOAD BUOGIVL

This code will work in any environment of MVS TSO/E
Version 2 and ISPF Version 2.3 or later.


COMPIDR REXX

/* REXX TO COMPARE AMBLIST LISTIDR REPORTS OF TWO LOAD MODULES */
ARG PREFO '(' MEMBRO ')' SUFFO PREFN '(' MEMBRN ')' SUFFN
PARSE ARG ARG1 ARG2 ARG3 ARG4 ARG5
DSNAMEO = ARG1
DSNAMEN = ARG2
 IF ARG4 = '' & MEMBRN = '' THEN
    RETURN('PLEASE SUPPLY 2 DSNS & MEMBERS')
 IF ARG5 \= '' THEN RETURN("TOO MANY ARGUMENTS")
 IF ARG4 \= '' THEN
    DO
       DSNAMEO = "'"||ARG1||"("||ARG2||")'"
       DSNAMEN = "'"||ARG3||"("||ARG4||")'"
       MEMBRO  = ARG2
       MEMBRN  = ARG4
       PREFO   = "'"||ARG1
       PREFN   = "'"||ARG3
       SUFFO   = "'"
       SUFFN   = "'"
    END
 CALL TESTDSN DSNAMEO
 CALL TESTDSN DSNAMEN
 CALL LISTIDR 'OLD' PREFO MEMBRO SUFFO
 CALL LISTIDR 'NEW' PREFN MEMBRN SUFFN
 CALL SUPERCOMPARE AMBLIST.OLD.LIST AMBLIST.NEW.LIST
 ADDRESS TSO 'DELETE AMBLIST.NEW.LIST AMBLIST.OLD.LIST'
 EXIT


TESTDSN PROCEDURE

 ARG DSN
 MSG = MSG()
 M = MSG(OFF)
 COND = SYSDSN(DSN)
 IF COND \= 'OK' THEN,
    DO
       SAY DSN COND
       M=MSG(MSG)
       EXIT
    END
 RETURN


LISTIDR PROCEDURE

 ARG VERSION PREF MEMBR SUFF
 ADDRESS TSO 'ALLOC F(SYSIN) UNIT(VIO) RECFM(F B) LRECL(80) BLKSIZE(80)'
 LISTIDR = ' LISTIDR MEMBER='||MEMBR
 QUEUE LISTIDR
 QUEUE ''
 "EXECIO * DISKW SYSIN (FINIS"
 ADDRESS TSO 'FREE F(SYSPRINT SYSLIB)'
 ADDRESS TSO 'DELETE AMBLIST.'||VERSION||'.LIST'
 ADDRESS TSO 'ALLOC DA(AMBLIST.'||VERSION||'.LIST)
              DD(SYSPRINT) NEW RECFM(F B A)
              LRECL(121) BLKSIZE(3630) SPACE(5 5) TRACKS'
 ADDRESS TSO 'ALLOC F(SYSLIB) DA('||PREF||SUFF||')'
 ADDRESS TSO 'ALLOC DUMMY DD(SYSUDUMP)'
 ADDRESS ISPEXEC 'SELECT PGM(AMBLIST)'
 ADDRESS TSO 'FREE F(SYSPRINT SYSLIB)'
 RETURN


SUPERCOMPARE PROCEDURE

 ARG DSNAME1 DSNAME2
 ADDRESS TSO 'DELETE AMBCOMPR.LIST'
 ADDRESS TSO 'ALLOC DA(AMBCOMPR.LIST)
              DD(OUTDD) NEW RECFM(F B A)
              LRECL(133) BLKSIZE(13300) SPACE(5 5) TRACKS'
 ADDRESS TSO 'ALLOC F(OLDDD) DA('||DSNAME1||') SHR REUSE'
 ADDRESS TSO 'ALLOC F(NEWDD) DA('||DSNAME2||') SHR REUSE'
 /* PROCESS STATEMENTS FOR COMPARISON OF AMBLIST LISTIDR OUTPUTS */
 ADDRESS TSO 'ALLOC F(SYSIN) UNIT(VIO) RECFM(F B) LRECL(80) BLKSIZE(80)'
 QUEUE  "CMPCOLM 2:131                          "  /* IGNORE CC       */
 QUEUE  "DPLINE ' PAGE '                        "  /* IGNORE PAGE#S   */
 QUEUE  "DPLINE 'TRANSLATOR '                   "  /* IGNORE HEADINGS */
 QUEUE  "OCHGT 'THIS LOAD MODULE WAS ',' ',3:90 "  /* NARROW OUTPUT   */
 QUEUE  "NCHGT 'THIS LOAD MODULE WAS ',' ',3:90 "  /* NARROW OUTPUT   */
 QUEUE  "OCHGT '               ',' ',3:100      "  /* NARROW OUTPUT   */
 QUEUE  "NCHGT '               ',' ',3:100      "  /* NARROW OUTPUT   */
 QUEUE  "OCHGT '       ',' ',3:100              "  /* NARROW OUTPUT   */
 QUEUE  "NCHGT '       ',' ',3:100              "  /* NARROW OUTPUT   */
 QUEUE  ""
 "EXECIO * DISKW SYSIN (FINIS"
 ADDRESS ISPEXEC 'SELECT PGM(ISRSUPC)
                  PARM(DELTAL,WORDCMP,NOPRTCC)'
 ADDRESS TSO 'FREE F(SYSIN OUTDD OLDDD NEWDD)'
 ADDRESS ISPEXEC 'BROWSE DATASET(AMBCOMPR.LIST)'
 ADDRESS TSO 'DELETE AMBCOMPR.LIST'
 RETURN


Abe Getzler
Senior Technical Analyst
Brooklyn Union Gas Co (USA)                        c Brooklyn Union Gas 1991

