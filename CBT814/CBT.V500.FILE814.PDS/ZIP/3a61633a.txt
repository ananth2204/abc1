A TSO/ISPF editor split-screen data copier

One of the features of SPF/PC that is not available on the
mainframe version of ISPF is the ability to copy blocks of data
from a split-screen session during edit.  You can do it manually
by creating a new member in the library you are editing, with
the data to be copied, or you can use the following ISPF edit
macros.

¥     XOUT - this macro is used to stage the data to be copied.
      First you need to indicate the lines to copy with the
      standard ISPF edit line commands 'C', 'CC', or 'Cnn'.  This
      process is usually done after you split your session to edit
      another dataset, but it can be done whenever you are in
      edit.  Once the data is staged, you are ready to copy.

¥     XIN - this macro gets the data that was staged by XOUT
      and includes it in the dataset you are editing where you put
      the ISPF edit line command 'A'.

The XOUT/XIN copy process will work for partitioned and
sequential datasets, or any combination and there is no limit as
to how much data can be copied.

XOUT

ISREDIT MACRO NOPROCESS
ISREDIT PROCESS RANGE C
IF &LASTCC NE 0 THEN GOTO ERROR
ISREDIT (FIRST) = LINENUM .ZFRANGE
ISREDIT (LAST) = LINENUM .ZLRANGE
CONTROL NOMSG
DELETE XINXOUT
ATTRIB MODEL RECFM(F B) LRECL(80) BLKSIZE(80)
ALLOC FILE(XFER) DSNAME('&SYSUID..XINXOUT') USING(MODEL) +
  NEW CATALOG UNIT(DISK) SPACE(1,1) CYLINDERS
FREE ATTRLIST(MODEL)
OPENFILE XFER OUTPUT
DO WHILE &FIRST LE &LAST
  ISREDIT (XFER) = LINE &FIRST
  PUTFILE XFER
  SET &FIRST = &FIRST + 1
  END
CLOSFILE XFER
FREE FILE(XFER)
SET ZEDSMSG = &STR(READY FOR COPY)
SET ZEDLMSG = &STR(YOUR DATA HAS BEEN STAGED AND IS READY FOR +
    COPYING INTO THE TARGET DATASET)
ISPEXEC SETMSG MSG(ISRZ001)
EXIT CODE(0)
ERROR: +
SET ZEDSMSG = &STR(ENTER "C" LINE CMD)
SET ZEDLMSG = &STR(XFER REQUIRES 1 OR MORE LINES MARKED WITH +
    "C" CMDS TO PROCESS THE COPY)
ISPEXEC SETMSG MSG(ISRZ001)
EXIT CODE(12)

XIN

ISREDIT MACRO NOPROCESS
ISPEXEC CONTROL ERRORS RETURN
IF &SYSDSN(XINXOUT) NE OK THEN GOTO NOXOUT
ISREDIT PROCESS RANGE A
IF &LASTCC NE 0 THEN GOTO ERROR
ISREDIT (FIRST) = LINENUM .ZFRANGE
ISREDIT (LAST) = LINENUM .ZLRANGE
XIN: +
ALLOC FILE(XFER) DSNAME('&SYSUID..XINXOUT')
OPENFILE XFER INPUT
GETFILE: +
ERROR GOTO NOMORE
GETFILE XFER
ERROR OFF
ISREDIT LINE_AFTER &FIRST = "&XFER"
IF &LASTCC > 4 THEN +
   ISREDIT LINE_AFTER &FIRST = '&XFER'
   IF &LASTCC > 4 THEN EXIT CODE(16)
SET &FIRST = &FIRST + 1
GOTO GETFILE
NOMORE: +
ERROR OFF
CLOSFILE XFER
FREE FILE(XFER) DELETE
SET ZEDSMSG = &STR(COPY COMPLETED)
SET ZEDLMSG = &STR(YOUR DATA HAS BEEN SUCCESSFULLY COPIED)
ISPEXEC SETMSG MSG(ISRZ001)
EXIT CODE(0)
ERROR: +
SET ZEDSMSG = &STR(ENTER "A" LINE CMD)
SET ZEDLMSG = &STR(XFER REQUIRES TARGET LINE TO BE MARKED WITH +
    AN "A" CMD TO PROCESS THE COPY)
ISPEXEC SETMSG MSG(ISRZ001)
EXIT CODE(12)
NOXOUT: +
SET ZEDSMSG = &STR(XIN BEFORE XOUT)
SET ZEDLMSG = &STR(XFER REQUIRES YOU RUN XOUT TO STAGE THE DATA +
    TO BE COPIED BEFORE RUNNING XIN)
ISPEXEC SETMSG MSG(ISRZ001)


Paul Jespersen
Systems Programmer
Gulf States Toyota (USA)               © Xephon 1990

