A JCL scanner


THE JSCAN REXX EXEC

/************************ REXX JCL SCANNER /***************************/
/* EDIT MACRO SET UP AND INITIALIZATION */
ADDRESS ISREDIT "MACRO (NAME)"
TRACE OFF
X = MSG(OFF)
ADDRESS ISREDIT "(ZMEM) = MEMBER"
ADDRESS ISREDIT "(ZLIB) = DATASET"
ADDRESS ISREDIT "DOWN MAX"
ADDRESS ISREDIT "(TOP,BOT) = DISPLAY_LINES"
FLG = 0
/* START OF THE BIG LOOP */
DO CNT = 1 TO BOT
 ADDRESS ISREDIT "(CURLINE) = LINE &CNT"
 IND = 0
 N1 = INDEX(CURLINE,'DSN=')        /* DETERMINE DATASET NAME */
 IF N1 > 0 THEN DO
 N2 = INDEX(CURLINE,',',14)        /* DATASET NAME END */
 IF N2 < N1 THEN N2 = INDEX(CURLINE,' ',14)
 CURDSN = SUBSTR(CURLINE,N1+4,N2-N1-4)     /* COMPLETE DATASET NAME */
 N3 = INDEX(CURLINE,'(')           /* CHECK FOR GDG DATASET */
 IF N3 > 0 THEN DO
  VAL1 = SUBSTR(CURLINE,N3+1,1)
  IF VAL1 = 0 | VAL1 = '-' | VAL1 = '+' THEN,
  CURDSN = SUBSTR(CURLINE,N1+4,N3-N1-4)
 END
 BYP = SUBSTR(CURDSN,1,1)
 BYP1 = SUBSTR(CURDSN,1,3)
 IF BYP = '*' | BYP1 = 'TAP' | BYP = '&' THEN NOP
 ELSE DO                           /* CHECK FOR EXCEPTIONS */
  DSNOK= SYSDSN(CURDSN)
  IF DSNOK = OK THEN NOP
  ELSE DO                          /* WRITE RESULT ON CHECK */
   ADDRESS ISREDIT "LINE_AFTER &CNT = MSGLINE '&DSNOK'"
   FLG = 1
  END
 END
 IF BYP1 = 'TAP' THEN DO           /* TAPE DATASETS */
  TD = LISTDSI(CURDSN)
  IF SYSREASON = 5 THEN DO         /* WRITE RESULT */
   DSNMSG = 'NOT CATALOGED'
   ADDRESS ISREDIT "LINE_AFTER &CNT = MSGLINE '&DSNMSG'"
   FLG= 1
  END
  ELSE NOP
 END
END                                /* END OF DATASET SECTION */
N4 = INDEX(CURLINE,'UNIT=')        /* DETERMINE UNIT NAME */
IF N4 > 0 THEN DO
 CUNIT = SUBSTR(CURLINE,N4+5,5)    /* GET THE COMPLETE UNIT NAME */
 /* CHECK IF UNIT EQUAL SYSDA, TAPE, CASS, OR 3380 */
 IF CUNIT = SYSDA | CUNIT = TAPE | CUNIT = CASS | CUNIT = 3380,
 | CUNIT = 'TAPE,' | CUNIT = 'CASS,' | CUNIT ='3380,', | CUNIT = 3390,
 | CUNIT = '3390,' | CUNIT= '(3390', | CUNIT= '(TAPE'  | CUNIT= '(CASS'|
 CUNIT = '(3380' THEN IND = 1
 IF IND = 0 THEN DO          /* WRITE RESULT IF UNIT NOT ANY OF ABOVE */
  UNITMSG = 'INVALID UNIT NAME SPECIFIED'
  ADDRESS ISREDIT "LINE_AFTER &CNT = MSGLINE '&UNITMSG'"
  FLG = 1
 END
END                                /* END OF UNIT CHECKING */
N5 = INDEX(CURLINE,'RECFM=')       /* RECFM IN CURLINE */
IF N5 > 0 THEN RVAL = SUBSTR(CURLINE,N5+6,2)
N6 = INDEX(CURLINE,'LRECL=')       /* LRECL IN CURLINE */
IF N6 > 0 THEN DO
 N7 = INDEX(CURLINE,',',N6)        /* END OF LRECL */
 IF N7 = 0 THEN N7 = INDEX(CURLINE,')',N6)
 LRVAL = SUBSTR(CURLINE,N6+6,N7-N6-6) /* VALUE OF LRECL */
END
 N8 = INDEX(CURLINE,'BLKSIZE=')   /* BLKSIZE IN CURLINE */
 IF N8 > 0 THEN DO
  N9 = INDEX(CURLINE,')',N8)      /* END OF BLKSIZE */
  IF N9 = 0 | N9 > N8 + 13 THEN N9 = INDEX(CURLINE,',',N8)
  IF N9 = 0 THEN N9 = INDEX(CURLINE,' ',N8)
  BVAL = SUBSTR(CURLINE,N8+8,N9-N8-8)     /* VALUE OF BLKSIZE */
  IF LRVAL = 0 THEN NOP
  ELSE
  IF N8 > 0 & N6 > 0 THEN DO
  /** CHECK ONLY IF BOTH ARE TRUE **/
   BD = BVAL//LRVAL
   IF RVAL = FB & BD = 0 THEN NOP
   IF RVAL = FB & BD ^= 0 THEN DO /* LRECL MULTIPLE OF BLKSIZE */
    RECMSG = 'LRECL IS NOT A MULTIPLE OF BLKSIZE'
    ADDRESS ISREDIT "LINE_AFTER &CNT = MSGLINE '&RECMSG'"
    FLG = 1
   END
   IF RVAL = VB & BD = 4 THEN NOP
   IF RVAL = VB & BD ^= 4 THEN DO /* LRECL MULTIPLE OF BLKSIZE */
    RECMSG = 'LRECL IS NOT A MULTIPLE OF BLKSIZE'
    ADDRESS ISREDIT "LINE_AFTER &CNT = MSGLINE '&RECMSG'"
    FLG = 1
   END
  END
 END                             /* END OF THE BIG LOOP */
END
/* END OF JCL SCANNER */
ENDIT:
IF FLG = 1 THEN ADDRESS ISPEXEC "SETMSG MSG(JCL001)"
ELSE ADDRESS ISPEXEC "SETMSG MSG(JCL002)"
ADDRESS ISREDIT "UP MAX"
EXIT


JCL00 (messages used in scanner)

JCL001 'ERRORS FOUND' .ALARM=NO
'PLEASE CHECK YOUR JCL - SOME ERRORS WERE FOUND'

JCL002 'NO ERRORS FOUND' .ALARM=NO
'YOUR JCL IS FINE - NO PROBLEMS FOUND'


N W Potgieter
Senior Systems Engineer
SPL (South Africa)               c SPL 1996

