JES exit 2 and the StorageTek 4400 ACS


INTRODUCTION AND BRIEF DESCRIPTION

The StorageTek 4400 Automated Cartridge System (ACS)
gives us the chance to implement some cartridge allocation
facilities through JES exit 2 on the Host Software Component
(HSC): HSC is the software component in the host used to
accomplish automated mounting and dismounting of resident
cartridges for the library attached to the host. An ACS is
composed of between one and sixteen Library Storage Modules
(LSMs): an LSM is where cartridges are stored and are in
position to be caught by a robot hand. An LSM holds
approximately 5700 cartridges.

JES exit 2 is the JES2 allocation user exit and is used for:

1      Directing cartridge allocations to the LSM which has the
      greater number of scratch volumes in a multi-LSM environment.

2      Selecting cartridge allocation into or out of an LSM by job
      name, dataset name, etc. In this case, JES exit 2 is very
      powerful because it is not necessary to make any changes
      to the UNIT parameter of any production job DD
      statements to route cartridge allocations into the LSM or to
      go to the manual cartridges drives.

A JES exit 2 copy is loaded into the CSA area in virtual storage
by HSC when it is started up, and this copy is not refreshed
while HSC is running, so if you want to make any JES exit 2
modifications you must stop and restart the HSC component.

In some circumstances the production department will need to
refresh the modifications made in JES exit 2 but if HSC is very
busy attending to restarting mount and demount requests it will
not be possible. In order to avoid this possible situation you
can design a very easy JES exit 2 routine with two principal
functions:

1      Issue a call to another module where the actual JES exit 2
      algorithm is to be found.

2      When JES exit 2 regains control, it must analyse the
      returned results and use them to return to HSC, loading the
      necessary code to route the allocation request in GPR 15.

At our installation the module is named SLSUX02 and is
shown below. The actual JES exit 2 algorithm is contained in
an independent module  named SLSUX2 and is also shown
below.


IMPLEMENTING THE STRATEGY

I have implemented this strategy using the LOAD and
DELETE macros from JES exit 2. The LOAD macro brings a
new copy of SLSUX2 to an address space private area if there
is not already one in virtual storage, although I have satisfied
myself that the LOAD macro always gets a new copy of
SLSUX2 by using the DELETE macro immediately after
control processing is returned to the caller SLSUX02 (virtual
storage assigned to a loaded module is released by the
DELETE macro).


SOME CONSIDERATIONS

1      This method gives high flexibility in making JES exit 2
      modifications effective but forces MVS to bring a new
      copy of SLSUX2 into virtual storage for each LOAD
      macro invoked from SLSUX02, so if you do not need to
      make any frequent changes in JES exit 2 or your HSC can
      be restarted at any time without any restrictions, you
      should not use this technique. Integrating all code in only
      one compacted module in CSA yields better performance.

2      If the partitioned dataset where SLSUX2 resides is under
      LLA control, you must remember to send 'F
      LLA,REFRESH' to the LLA MVS component in order to
      make ready the new module version with its new
      modifications to the LOAD macro.

3      The HSC passes a parameter list to JES exit 2. This list
      contains the following data: job name, DSNAME,
      DDNAME, step name, etc. These fields are accessible
      from JES exit 2 by the names UX02JOBN, UX02DSN,
      UX02DDN, UX02STPN, etc, respectively. This
      information is given in Chapter 9: User Exits in the manual
      StorageTek System Programmer's Guide. Before
      SLSUX02 gives control to SLSUX2, the address of
      UX02JOBN is loaded into GPR 3, and the address of
      UX02DSN is loaded into GPR 4 because my SLSUX2
      algorithm only uses this information to route cartridge
      allocations to an LSM or to manual cartridge drives.

4      SLSUX2 should be assembled and link-edited with the
      following attributes: AMODE 31, RMODE ANY, AC 0,
      NORENT, and NOREUS.


DOCUMENTATION

o      StorageTek System Programmer's Guide.

o      IBM MVS/ESA Application Development Guide:
      Assembler Language Programs.

o      IBM MVS/ESA Application Development Reference:
      Services for Authorized Assembler Language Programs.

SLSUX02 MODULE

         .
      SLSUX02 previous code
         .
         LA    R3,UX02JOBN           . R3 POINTS TO JOB NAME FIELD
         LA    R4,UX02DSN            . R4 POINTS TO DSNAME  FIELD
         LOAD  ,SF=(E,LOADL)         . EXECUTE LOAD LIST EXPANSION
         LR    R15,R0                .
         BASSM R9,R15                . GIVE CONTROL TO SLSUX2 MOD.
         DELETE EP=SLSUX2            . FREE SLSUX2 VIRTUAL STORAGE
         CL    R7,=F'4'              . ANALYSE SLSUX2 RESULTS
         BE    USELIB                .
         B     NOUSELIB              .
USELIB   EQU   *                     .
         LA    R15,UX02LIB           . INDICATE USE LIB. DRIVES
         B     RETURN                .
NOUSELIB EQU   *                     .
         LA    R15,UX02NLIB          . INDICATE NO USE LIB. DRIVES
         B     RETURN                .
RETURN   DS    0H
         LM    R0,R12,20(R13)        . RESTORE GPR REGISTERS
         L     R14,12(,R13)          . RESTORE RETURN ADDRESS
         BR    R14                   . RETURN TO THE CALLER
LOADL    LOAD  EP=SLSUX2,SF=L        . LOAD MACRO LIST FORMAT
         LTORG                       . LITERAL ORIGIN
         END


SLSUX2 module

***- R15 HAS MODULE ENTRY POINT
* .- R9 POINTS TO RETURN ADD. IN SLSUX02
* .- R8 IS BASE ADDRESSABILITY
* .- R7 HAS CODE FOR USELIB O NO USELIB
* .- R4 POINTS TO UX02DSN
* .- R3 POINTS TO UX02JOBN
* .- R14, R13 R12, AND R10 ARE USED BY SLSUX02
* .- FREE REGISTERS ARE: R0, R1, R2, R5 AND R6
***- ATTRIBUTES: NORENT, NOREUS, RMODE=ANY, AMODE=31, AC=0
SLSUX2   CSECT                       .
SLSUX2   AMODE 31                    .
SLSUX2   RMODE ANY                   .
         USING SLSUX2,R8             .
         LR    R8,R15                .
******** CONTROL BY JOB NAME
         CLC   =C'COEF81DD',0(R3)    .
         BE    USELIB                .
******** CONTROL BY DSNAME
         CLC   =C'SLOG.',0(R4)       .
         BE    USELIB                .
         CLC   =C'AUXFA.',0(R4)      .
         BE    USELIB                .
         CLC   =C'TRAFA.',0(R4)      .
         BE    USELIB                .
         CLC   =C'FIXFA.',0(R4)      .
         BE    USELIB                .
         B     NOUSELIB              .
USELIB   EQU   *                     .
         LA    R7,4                  . INDICATE USE LIB. DRIVES
         B     RETURN                .
NOUSELIB EQU   *                     .
         LA    R7,8                  . INDICATE NO USE LIB. DRIVES
         B     RETURN                .
RETURN   DS    0H                    .
         BSM   0,R9                  . RETURN TO SLSUX02
         LTORG                       . LITERAL ORIGIN
         END


Juan Manuel Cuenca Jimenez
Systems Analyst
Sedava AIE (Spain)                              c Xephon 1992

