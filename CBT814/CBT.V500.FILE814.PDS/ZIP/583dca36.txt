Obtaining MVS subsystem console information

In dealing with automation, I have recently come upon the
problem of determining the owners of MVS subsystem
consoles.  At our installation, we have NetView for network
automation and OPS/MVS for console automation, both of
which use subsystem consoles as part of their operation.
Recently we ran into a problem where a NetView user could
not issue an MVS command because there were no more
subsystem consoles available to be allocated.  I went about
trying to find who was using these consoles, but found that
there is no standard way in MVS to display their usage.  The
only likely way, via the MVS DISPLAY CONSOLES
command, will only show real devices which are defined as
consoles.  Rather than just adding more subsystem consoles to
alleviate the problem, I decided to look at the console control
blocks to find any related subsystem console information.

It turns out that subsystem console control blocks are chained
along with the standard console control blocks.  By following
this chain, one can find all the defined subsystem consoles, if
they are allocated, who they are allocated to, and what
attributes are assigned to them.  Using this information, we
wrote a program to display subsystem console usage and found
that NetView had used most of them.  We then looked into the
INFO/MVS database to find information about NetView's use
of subsystem consoles.  We found that NetView has a
RELCONID command which can be used to release an
acquired console.  Since most of the procedures executed under
NetView are CLISTs, we simply added the RELCONID
command to the end of each CLIST.  This appears to have
alleviated our problem.

As a result of this small effort, we did not have to increase the
number of defined subsystem consoles.  I am sure that with
some more modifications, this program can serve other uses for
real as well as subsystem consoles.

SUBCONS  CSECT                         ESTABLISH CSECT
         SAVE  (14,12),,SUBCONS-&SYSDATE
         LR    R12,R15                 LOAD R12 W/EPA ADDRESS
         USING SUBCONS,R12             ESTABLISH ADDRESSABLITY
         GETMAIN RU,LV=WORKLEN         GETMAIN WORKAREA
         ST    R13,4(R1)               ST CALLERS S/A ADDR IN MY S/A
         ST    R1,8(R13)               ST MY S/A ADDR IN CALLERS S/A
         LR    R13,R1                  LOAD ADDR OF MY S/A IN R13
         USING WORKAREA,R13            ESTABLISH ADDRESSABILITY
         L     R1,CVTPTR               LOAD A(CVT)
         USING CVT,R1                  ESTABLISH ADDRESSABILITY
         L     R1,CVTCUCB              LOAD A(UCM BASE)
         USING UCM,R1                  ESTABLISH ADDRESSABILITY
         LM    R2,R4,UCMVEA            LOAD A(FIRST UCM ENTRY),
*                    LENGTH OF EACH UCM ENTRY, A(LAST UCM ENTRY)
         USING UCMLIST,R2              ESTABLISH ADDRESSABILITY
         TPUT  HEADER1,L'HEADER1       PUT HEADER
         TPUT  HEADER2,L'HEADER2       PUT HEADER
         TPUT  HEADER3,L'HEADER3       PUT HEADER
UCMLOOP  MVI   DETAIL,C' '             CLEAR LINE
         MVC   DETAIL+1(L'DETAIL-1),DETAIL CLEAR LINE
         XC    FLAG,FLAG               CLEAR FLAG
         L     R15,UCMUCB              ELSE GET A(UCB)
         USING UCBOB,R15               ESTABLISH ADDRESSABILITY
         MVC   DETAIL+9(3),UCBNAME     MOVE UCB NAME
         TM    UCMDISP1,UCMDISPB       IS THIS A HARDCOPY CONSOLE
         BZ    CHKSUBSY                NO, CHECK IF SUBSYSTEM CONSOLE
         MVC   DETAIL+12(3),=C'/HC'    ELSE SHOW HARDCOPY
         CLI   UCMEDEVX,UCM3215        IS THIS A SUBSYSTEM CONSOLE
         BNE   NOSUBSYS                NO, SKIP
         OI    FLAG,CONSFND            SHOW SUBSYSTEM CONSOLE FOUND
NOSUBSYS MVC   HEXIN(2),UCMAUTH        MOVE IN CONSOLE ID TO TRANSLATE
         UNPK  HEXOUT(9),HEXIN(5)      UNPACK BYTES
         TR    HEXOUT(4),HEXTAB-240    TRANSLATE TO PRINTABLE HEX
         MVC   DETAIL+33(4),HEXOUT     MOVE OUT TRANSLATED BYTES
         MVC   DETAIL+41(3),=C'NO '    ASSUME DEVICE NOT ACTIVE
         TM    UCMATR,UCMUF            IS DEVICE ACTIVE
         BZ    NOTACTIV                NO, SKIP IT
         MVC   DETAIL+41(3),=C'YES'    ELSE SAY YES
NOTACTIV L     R5,UCMFEXTP             LOAD A(UCME FIXED EXTENSION)
         USING UCMEFEXT,R5             ESTABLISH ADDRESSABILITY
         SR    R15,R15                 CLEAR REGISTER
         LH    R15,UCMEFCNN            LOAD CONSOLE ID
         CVD   R15,DBLWORD             CONVERT TO DECIMAL
         OI    DBLWORD+7,X'0F'         FIX THE SIGN
         UNPK  DETAIL+1(4),DBLWORD+5(3) UNPACK TO DETAIL LINE
         L     R5,UCMEFPEX             LOAD A(UCME PAGEABLE EXTENSION)
         USING UCMEPEXT,R5             ESTABLISH ADDRESSABILITY
         TM    FLAG,CONSFND            IS THIS A SUBSYSTEM CONSOLE
         BZ    GETCONID                NO, THEN SKIP
         MVC   DETAIL+9(4),UCMEPNME    MOVE SUBSYSTEM NAME
         CLC   ZEROES,UCMEPNME         IS SUBSYTEM NAME = 0
         BNE   GETCONID                NO, GO GET CONSOLE ID
         MVC   DETAIL+7(8),=C'*SUBSYS*' MOVE SUBSYS AS TYPE
GETCONID MVC   HEXIN(2),UCMEPAID       MOVE IN CONSOLE ID TO TRANSLATE
         UNPK  HEXOUT(9),HEXIN(5)      UNPACK BYTES
         TR    HEXOUT(4),HEXTAB-240    TRANSLATE TO PRINTABLE HEX
         MVC   DETAIL+27(4),HEXOUT     MOVE OUT TRANSLATED BYTES
         SR    R15,R15                 CLEAR REGISTER
         LH    R15,UCMEPAID            LOAD ASID OF CONSOLE OWNER
         SLL   R15,2                   MULT. BY 4 FOR INDEXING
ASVTLKUP L     R1,CVTPTR               LOAD A(CVT)
         USING CVT,R1                  ESTABLISH ADDRESSABILITY
         L     R1,CVTASVT              LOAD A(ASVT)
         USING ASVT,R1                 ESTABLISH ADDRESSABILITY
         LA    R1,ASVTENTY-4           LOAD A(1ST ASVT ENTRY)
         LA    R1,0(R15,R1)            LOAD A(REQUESTED ASVT ENTRY)
         TM    0(R1),ASVTAVAL          IS ENTRY AVAILABLE
         BZ    GETASCB                 NO, GET ASCB
         B     ARNDJOB                 ELSE SKIP JOB NAME
GETASCB  L     R1,0(,R1)               LOAD A(ASCB)
         USING ASCB,R1                 ESTABLISH ADDRESSABILITY
         L     R1,ASCBCSCB             LOAD A(CSCB)
         USING CHAIN,R1                ESTABLISH ADDRESSABILITY
         MVC   DETAIL+17(8),CHCLS      MOVE JOB NAME
ARNDJOB  TPUT  DETAIL,L'DETAIL         PUT DETAIL LINE
UCMBUMP  AR    R2,R3                   LOAD A(NEXT UCME)
         CR    R2,R4                   ARE WE PAST THE END OF UCMES
         BNH   UCMLOOP                 NO, GO CHECK AGAIN
         TM    FLAG,CONSFND            WERE ANY SUBSYSTEM CONSOLE FOUND
         BO    RETURN                  YES, SKIP MESSAGE
         TPUT  NOCONS,L'NOCONS         ELSE SAY NONE FOUND
RETURN   LR    R1,R13                  LOAD R1 W/A(SAVEAREA)
         L     R13,4(R13)              LOAD R13 W/ADDR OF CALLER'S S/A
         FREEMAIN RU,LV=WORKLEN,A=(1)  FREEMAIN WORKAREA
         RETURN (14,12),RC=0           RETURN TO OS WITH RETCODE=0
HEADER1  DC    CL60' CONS    ADDR/     JOB           CMD   DEVICE'
HEADER2  DC    CL60'  ID   SUBSYSTEM   NAME    ASID  AUTH  ACTIVE'
HEADER3  DC    CL60' ****  ********  ********  ****  ****  ******'
NOCONS   DC    CL60'** NO SUBSYSTEM CONSOLES WERE FOUND'
HEXTAB   DC    C'0123456789ABCDEF'     TRANSLATE TABLE
ZEROES   DC    F'0'                    ZERO CONSTANT
WORKAREA DSECT                         WORK AREA
SAVEAREA DC    18F'0'                  SHOULD BE FIRST IN WORKAREA
DBLWORD  DS    D                       WORK AREA FOR UNPACKING
DETAIL   DC    CL60' ****  ********  ********  ****  ****    *** '
FLAG     DC    X'00'                   FLAG FIELD
CONSFND  EQU   X'80'                   SUBSYSTEM CONSOLE FOUND
HEXIN    DS    4X                      INPUT FIELD FOR CONVERSION
         DS    CL1                     REQUIRED AFTER HEXIN FOR BITFLIP
HEXOUT   DS    8X                      OUTPUT FIELD FOR CONVERSION
         DS    CL1                    REQUIRED AFTER HEXOUT FOR BITFLIP
WORKLEN  EQU   *-WORKAREA              WORK AREA LENGTH
         IEECUCM FORMAT=NEW
         CVT   LIST=NO,DSECT=YES
         IHAASVT LIST=NO
         IHAASCB LIST=NO
         IEFUCBOB LIST=NO
CSCB     DSECT
         IEECHAIN
         END


                                          c Xephon 1991

