Reading other usersÕ TSO mail
The most common TSO mail items are $HASP165 TSO NOTIFY messages that indicate
the success or failure of batch jobs.  The NOTIFY subparameter of the JOB card
only allows for one user-id to be specified.  As a result, only one user has
access to the NOTIFY message returned by a job.  This is fine if a single person
is responsible for tracking a collection of important jobs.  However, should
that one person be away, the only way fellow workers can continue to track the
jobs is by changing the JCL NOTIFY statements or by knowing the missing personÕs
password.  Neither method is very desirable.
The LISTBCX (LISTBC extended) TSO command allows a TSO user to read and
optionally delete the TSO messages waiting to be read by another user.  This
allows other users access to the TSO NOTIFY messages sent to a single user.  If
the messages are only read (deleted not requested) the messages remain available
to other users until the owning user logs on and receives the messages.  If the
messages are deleted, the owning user is sent a TSO message indicating who
deleted his/her previous mail items.
LISTBCX is a TSO command processor, and performs broadcast dataset services in
the same way as the IBM-supplied LISTBC command.  The LISTBCX has the following
syntax:
     LISTBCX U(user1,user2,...user5) READONLY/PURGE
Up to five user-ids may be specified and if the mail items are to be deleted the
PURGE keyword is required.  If PURGE is not specified, the default is READONLY.
LISTBCX, as provided, controls access to usersÕ mail via ACF2 but this can be
removed or replaced without much effort.
         MACRO
         $SETR
         LCLA      &A
.LOOP    ANOP
R&A      EQU       &A
&A       SETA      &A+1
         AIF       (&A LT 16).LOOP
         MEND
LISTBCX  CSECT     SCD
***********************************************************************
* LISTBC EXTENDED FUNCTIONS
*  - ALLOWS ACCESS TO OTHER USERS' TSO MAIL ITEMS
* SYNTAX:
*          LISTBCX U(USER1,...USER5) READONLY/PURGE
* NOTE - THIS TSO COMMAND PROCESSOR ISSUES MODESET.  MODESET IS ONLY
*        REQUIRED WHEN SENDING A MESSAGE TO THE OWNER OF PURGED MAIL.
*        IF THIS FEATURE IS NOT REQUIRED, THEN THE MODESET CAN BE
*        REMOVED BY DELETING LINES WITH 'MODE' IN COLS 68
*        TO KEEP THIS FEATURE REMEMBER AC=1 AND APF REQUIREMENTS AND
*        TO MAKE THE NECESSARY CHANGES (TSO AUTH TABLE) IF THIS CP
*        IS TO RUN UNDER ISPF
* NOTE - ACCESS TO OTHER USERS' MAIL IS CONTROLLED BY ACF2 ACCESS TO
*        THE DUMMY DATASET NAME 'USERID.LISTBCX.BRODCAST.MESSAGES'
*         -  TO ALLOW MAIL TO BE READ, ALLOW READ ACCESS TO DUMMY DSN
*         -  TO ALLOW MAIL TO BE DELETED, ALLOW WRITE ACCESS
*        TO REMOVE ACF2 CODE SEE LINES WITH 'ACF2' IN COLS 68
***********************************************************************
         $SETR                         REGISTER EQUATES
         USING LISTBCX,R12
         STM   R14,R12,12(R13)
         LR    R12,R15                 SET INITIAL BASE
         ST    R13,SAVE+4
         LA    R13,SAVE
         USING CPPL,R1                  SET USING
         LA    R2,PPLREAL               PPL STORAGE AREA
         USING PPL,R2                   SET USING
         MVC   PPLUPT,CPPLUPT           PLUG UPT
         MVC   PPLECT,CPPLECT           PLUG ECT
         MVC   PPLECB,=A(ECB)           PLUG ECB
         MVC   PPLPCL,=A(PCL)           PLUG PCL
         MVC   PPLANS,=A(ANS)           PLUG ANS
         MVC   PPLCBUF,CPPLCBUF         PLUG CBUF
         XC    PPLUWA,PPLUWA            CLEAR UWA
         DROP  R1                       DROP CPPL BASE
         CALLTSSR EP=IKJPARS,MF=(E,PPLREAL) CALL PARSE
         DROP  R2                       DROP PPL BASE
         LTR   R15,R15                  GO O.K. ?
         BZ    PARSEOK                  YES...CONTINUE
         TPUT  NOPARSE,L'NOPARSE        NO...ISSUE MESSAGE
         B     RETURN                   RETURN
NOPARSE  DC    C'ERROR IN IKJPARS ROUTINE'
PARSEOK  L     R7,ANS                   GET PDL POINTER
         USING IKJPARMD,R7              SET USING
         CLC   PDL2,=AL2(2)             PURGE REQUESTED ?
         BNE   USERCHK                  NO...CONTINUE
         OI    FLAG,DELETE              YES...INDICATE DELETE REQUESTED
         L     R3,CVTPTR                CVT ADDRESS
         USING CVTMAP,R3                ADDRESS CVT
         L     R4,CVTTCBP               ADDR OF 4 FULLWORDS
         L     R4,12(,R4)               LOAD CURRENT ASCB ADDR
         USING ASCB,R4                  ASCB ADDRESSABILITY
         L     R5,ASCBJBNS              ADDRESS JOBNAME
         MVC   JOBNAME(8),0(R5)         MOVE JOBNAME IN
         DROP  R3,R4
USERCHK  CLC   PDLLIST(4),=F'0'         ANY USERIDS SPECIFIED ?
         BNE   GETHI                    YES...THEN CONTINUE
         TPUT  MISSUSER,L'MISSUSER      NO...ISSUE MESSAGE
         B     RETURN                   AND RETURN
MISSUSER DC    C'USERID MUST BE SUPPLIED'
GETHI    LA    R1,PDLLIST               ADDRESS USERID AREA
         LA    R4,USERTAB               ADDRESS USER TABLE
GETHILP  L     R2,0(,R1)                ADDRESS USERID
         SR    R3,R3                    CLEAR REGISTER
         IC    R3,5(,R1)                GET USERID LENGTH
         MVC   0(7,R4),=CL7' '          CLEAR USER ID AREA
         STC   R3,7(,R4)                SAVE LENGTH
         BCTR  R3,0                     REDUCE FOR EXECUTE
         EX    R3,MVCUSER               AND MOVE USER ID IN
         LA    R4,8(,R4)                ADDRESS NEXT AREA
         C     R4,=A(USERTAB+40)        AT MAX ?
         BH    MAXUSER                  YES...GET OUT
         ST    R4,USERTABE              STORE TABLE END
GETHINXT CLI   8(R1),X'FF'              ANY MORE USERIDS ?
         BE    ALLOCIT                  NO...THATS IT
         L     R1,8(,R1)                ADDRESS NEXT USERID
         B     GETHILP                  AND CONTINUE
MVCUSER  MVC   0(0,R4),0(R2)            MOVE IN USERID
MAXUSERS DC    C'5 USERID MAXIMUM EXCEEDED'
MAXUSER  TPUT  MAXUSERS,L'MAXUSERS      ISSUE MESSAGE
*  ALLOCATE DATASET
ALLOCIT  MVI   S99VERB,ALLOC           ALLOCATE DATASET
         MVC   S99FLAG1(4),ZERO        CLEAR STATUS
         MVC   S99INFO(2),ZERO         CLEAR EXTRA INFO
         LA    R1,S99RBPTR             SET R1 TO RB PTR
         SVC   99                      ISSUE ALLOC
         LTR   R15,R15                 ALLOCATED ?
         BZ    ENQIT                   YES...ENQ
         TPUT  ALLFAIL,L'ALLFAIL       NO...ISSUE MESSAGE
         B     RETURN                  AND RETURN
ALLFAIL  DC    C'ALLOCATION FAILED FOR BRODCAST DATASET'
ENQIT    ENQ   (MAJOR,ENQZERO,S,3,SYSTEM),RET=USE   MINOR = RBA OF ZERO
         LTR   R15,R15                 GOT IT ?
         BZ    OPENIT                   YES...GO OPEN IT
         TPUT  ENQFAIL,L'ENQFAIL        NO...ISSUE MESSAGE
         B     UNALLOC                  AND GO DEALLOCATE
ENQFAIL  DC    C'BRODCAST UNAVAILABLE AT THIS TIME (ENQ)'
OPENIT   OPEN  (BRODCAST,(UPDATE))      OPEN FILE
         LA    R2,BRODCAST              ADDRESS DCB
         USING IHADCB,R2
         TM    DCBOFLGS,X'10'           OPEN O.K. ?
         BNZ   GETREC1                  YES...CONTINUE
         TPUT  OPNFAIL,L'OPNFAIL        NO...ISSUE MESSAGE
         B     UNALLOC                  AND GO DEALLOCATE
OPNFAIL  DC    C'OPEN FAILED FOR BRODCAST DATASET'
         DROP  R2
* READ RECORD ONE / CONFIRM BRODCAST INITIALIZED / GET RBA OF DIR     *
* READ RECORD 1 (RBA 0)
GETREC1  MVC   RBA,=F'0'               SET RBA VALUE
         BAL   R9,READ                 READ RECORD
         LA    R2,RECORD               ADDRESS RECORD 1
         USING RECORD1,R2
         CLC   R1DSN,=C' SYS1.BRODCAST DATA SET ' INITIALIZED ?
         BNE   NOTINIT                 NO...ERROR
         CLC   R1LVL,=C'LEVEL 2'       INITIALIZED ?
         BE    INITOK                  YES...CONTINUE
NOTINIT  TPUT  NOINIT,L'NOINIT          NO...ISSUE MESSAGE
         B     CLOSEIT                  AND GO CLOSE
NOINIT   DC    C'BRODCAST DATASET IS NOT INITIALIZED'
INITOK   MVC   DIRRBA,R1MAIL           GET RBA OF MAIL DIRECTORY
         MVC   FREERBA,R1FRSRCH        SAVE FREE SEARCH RBA
         DROP  R2
RECORD1  DSECT
         DS    C
R1NOTICE DS    CL3                     RBA OF NOTICES
         DS    C
R1MAIL   DS    CL3                     RBA OF MAIL DIRECTORY
R1TOTREC DS    F                       TOTAL RECORDS
R1MAXMSG DS    H                       MAX. NUMBER OF MESSAGES
R1DSN    DS    CL24                    DATASET NAME
R1LVL    DS    CL7                     LEVEL
         DS    C
R1FRSRCH DS    CL3                     RBA OF FREE SEARCH RECORD
LISTBCX CSECT
* SETUP ACF2 ENVIRONMENT                                           ACF2
         XC    ACFSPARM(ACFSPRML),ACFSPARM CLEAR PARM LIST         ACF2
         MVI   ACFSPREQ,ACFSPRDS       REQ DSN VALIDATION ONLY     ACF2
         MVI   ACFSPID1,ACFSPIUR       USER CALL                   ACF2
         MVI   ACFSPAC2,0                                          ACF2
         LA    R5,DSNAREA              GET ADDRESS OF DSNHEAD      ACF2
         ST    R5,ACFSPDSN             GIVE ACF ADDRESS OF DSN     ACF2
         ACFGACVT R10,NONE=NOACF2      -> ACF'S CVT                ACF2
         USING ACCVT,R10               ADDRESS IT                  ACF2
* READ THE MAIL DIRECTORY RECORD THAT CONTAINS THE USERID
SEARCH   MVC   ENQRBA,DIRRBA           SET RBA FOR ENQ
         ENQ   (MAJOR,ENQRBA,E,3,SYSTEM) ENQ MINOR = DIRECTORY RBA
         MVC   RBA,ENQRBA              GET RBA OF MAIL DIRECTORY REC
         BAL   R9,READ                 BRING IN RECORD
         MVC   DIRRBA,RECORD+126       GET NEXT RBA
         MVC   DIRREC,RECORD           SAVE A COPY OF THE DIRECTORY REC
         LA    R2,DIRREC               ADDRESS RECORD
USERSRCH CLC   0(7,R2),=D'0'           USER ID EXIST ?
         BE    FINDNXT                 NO...CHECK NEXT
         LA    R4,USERTAB              ADDRESS TABLE
USIDSRCH CLC   0(7,R4),0(R2)           USER ID FOUND ?
         BNE   SRCHNEXT                NO...CONTINUE SEARCH
         STM   R0,R15,SAVEALL
         B     GOTDIR                  GO CHECK FOR MAIL
CONTINUE LM    R0,R15,SAVEALL
SRCHNEXT LA    R4,8(,R4)               ADDRESS NEXT ENTRY
         C     R4,USERTABE             ALL CHECKED ?
         BL    USIDSRCH                NO...CONTINUE
         B     FINDNXT
FINDNXT  CLI   21(R2),X'7F'            AT THE LAST ENTRY ?
         BE    NEXTDIR                 YES...THEN GET NEXT DIRECTORY
         LA    R2,13(,R2)              NO...ADDRESS NEXT ENTRY
         B     USERSRCH                AND TRY AGAIN
SAVEALL  DS    16F
NEXTDIR  DEQ   (MAJOR,ENQRBA,3,SYSTEM),RET=HAVE DEQ MINOR = DIR RBA
         CLC   USERTAB(40),=40X'00'    ALL USERIDS PROCESSED ?
         BE    CLOSEIT                 YES...THEN LEAVE
         CLC   DIRRBA,=F'0'            MORE DIRECTORY BLOCKS EXIST
         BNE   SEARCH                  YES...GO GET THEM
         LA    R4,USERTAB              ADDRESS TABLE
LEFTCHK  CLC   0(8,R4),=D'0'           USER ID LEFT ?
         BE    NOTLEFT                 YES...SKIP MESSAGE
         MVC   NOUSERM+25(7),0(R4)     USERID TO MESSAGE
         TPUT  NOUSERM,L'NOUSERM
NOTLEFT  LA    R4,8(,R4)               ADDRESS NEXT ENTRY
         C     R4,USERTABE             ALL CHECKED ?
         BL    LEFTCHK                 NO...CONTINUE
         B     CLOSEIT
NOUSERM  DC    C'USER ID DOES NOT EXIST -        '
GOTDIR   MVC   USERID,0(R4)            COPY USERID
* CHECK THAT CALLER HAS AUTHORIZATION TO READ THIS USER'S MAIL
         SR    R3,R3                   CLEAR
         IC    R3,7(,R4)               GET LENGTH
         MVC   0(8,R4),=D'0'           ZERO USERID IN TABLE
         MVC   DSNAREA(44),=CL44' '    CLEAR DATASET NAME AREA
         MVC   DSNAREA(7),USERID       SET USERID FOR ACCESS CHECK
         LA    R1,DSNAREA              ADDRESS DSN AREA
         AR    R1,R3                   PLUS USERID LENGTH-1
         MVC   0(26,R1),=CL26'.LISTBCX.BRODCAST.MESSAGES'
         MVI   ACFSPAC1,ACFSPAIN       READ ACCESS                 ACF2
         ACFSVC ACFSPARM,TYPE=S,NONE=NOACF2,CVT=HAVE               ACF2
*        SR    R15,R15                 ALLOW ACCESS (NO ACF2)    NOACF2
         LTR   R15,R15                 IS USER AUTHORIZED ?
         BZ    DELCHECK                YES...CONTINUE
         MVC   NOAUTH+45(7),USERID     USERID TO MESSAGE
         TPUT  NOAUTH,L'NOAUTH         ISSUE MESSAGE
         B     CONTINUE                GO GET NEXT USER ID
NOAUTH   DC    C'AUTHORIZATION IS REQUIRED TO ACCESS MAIL FOR         '
NOACF2   TPUT  NOACF2M,L'NOACF2M
         B     CLOSEIT
NOACF2M  DC    C'ACF2 IS NOT AVAILABLE, ACCESS DENIED'
DELCHECK OI    FLAG,NOWRITE            DISABLE WRITE
         TM    FLAG,DELETE             WAS DELETE REQUESTED ?
         BNO   MAILCHK                 NO...GO GET MAIL
* CHECK THAT CALLER HAS AUTHORIZATION TO DELETE THIS USER'S MAIL
         MVI   ACFSPAC1,ACFSPAOU       WRITE ACCESS                ACF2
         ACFSVC ACFSPARM,TYPE=S,NONE=NOACF2,CVT=HAVE               ACF2
*        SR    R15,R15                 ALLOW ACCESS (NO ACF2)    NOACF2
         LTR   R15,R15                 IS USER AUTHORIZED ?
         BNZ   NODEL                   NO...ISSUE MESSAGE
         NI    FLAG,255-NOWRITE        ENABLE WRITE
         B     MAILCHK                 AND GO GET MAIL
NODEL    MVC   NOAUTHD+44(7),USERID    USERID TO MESSAGE
         TPUT  NOAUTHD,L'NOAUTHD       ISSUE MESSAGE
         B     MAILCHK                 GO GET MAIL
NOAUTHD  DC    C'AUTHORIZATION IS REQUIRED TO PURGE MAIL FOR         '
MAILCHK  CLC   7(6,R2),=D'0'           ANY MAIL ?
         BNE   DIRCLEAR                YES...GO GET CHAIN
         MVC   NOMAIL+19(7),USERID     USERID TO MESSAGE
         TPUT  NOMAIL,L'NOMAIL         INDICATE NO MAIL
         B     CONTINUE                AND RETURN
NOMAIL   DC    C'NO MAIL EXISTS FOR         '
* MAIL EXIST / GET RBA OF MAIL CHAIN / CLEAR DIRECTORY RBA
DIRCLEAR MVC   MAILMSG+9(7),USERID     USERID TO MESSAGE
         TPUT  MAILMSG,L'MAILMSG
         LA    R2,7(,R2)               ADDRESS RBA POINTERS
         ST    R2,SAVEMRBA             SAVE ADDRESS OF POINTERS
         MVC   MAILRBA(6),0(R2)        SAVE MAIL FIRST/LAST RBA
         MVC   0(6,R2),=D'0'           CLEAR MAIL RBA IN DIRECTORY
         MVC   RECORD,DIRREC           MOVE DIR RECORD OUT FOR WRITE
         BAL   R9,WRITE                WRITE BACK RECORD
         B     GETMAIL
MAILMSG  DC    C'MAIL FOR         '
* BRING IN MAIL
GETMAIL  MVC   RBA,MAILRBA             GET RBA OF MAIL CHAIN
         BAL   R9,READ                 BRING IN RECORD
         SR    R9,R9                   CLEAR REGISTER
         IC    R9,RECORD               GET RECORD LENGTH
         A     R9,=F'1'                ADD ONE FOR INDENT
         MVI   RECORD,C' '             CLEAR LENGTH BYTE
         TPUT  RECORD,(R9)             ISSUE MESSAGE
         CLC   RBA,LOWRBA              NEW LOW RBA FOUND ?
         BH    DELETEIT                NO...JUST DELETE THE MAIL
         MVC   LOWRBA,RBA              YES...SET NEW LOW RBA
* DELETE MAIL RECORD
DELETEIT MVC   MAILRBA,RECORD+126      GET RBA OF NEXT MAIL ITEM
         MVI   KEY,X'FF'               SET DELETE KEY
         MVC   RECORD(1),FEEDBACK+7    SET RELATIVE TRACK POSITION
         BAL   R9,WRITE                WRITE BACK RECORD (DELETE KEY)
         CLC   MAILRBA,=F'0'           ANY MORE RECORDS ?
         BNE   GETMAIL                 YES...CONTINUE
* UPDATE LOW RBA CHAIN POINTER
UPDTLOW  MVC   RBA,FREERBA             GET RBA OF FREE SEARCH RECORD
         BAL   R9,READ                 READ THE RECORD
         CLC   RECORD+1(3),LOWRBA      HAS A NEW LOW BEEN FOUND ?
         BNH   PURGEMSG                NO...JUST CONTINUE
         MVC   RECORD+1(3),LOWRBA      YES...SET NEW LOW
         BAL   R9,WRITE                AND WRITE BACK THE RECORD
PURGEMSG TM    FLAG,NOWRITE            WAS WRITE DISABLED
         BO    CONTINUE                YES...JUST CONTINUE
         CLC   USERID,JOBNAME          PURGED OWN MESSAGES ?
         BE    CONTINUE                YES...JUST CONTINUE
         MVC   USERID1,JOBNAME         MOVE OUT THIS TASKS JOBNAME
         LA    R1,USERID2(R3)          ADDRESS ENDING BYTE OF SEND
         MVI   0(R1),C')'              PUT IN TRAILING )
         BCTR  R3,0                    REDUCE USERID LENGTH
         EX    R3,USERCMD              MOVE USERID TO SEND COMMAND
         MODESET MODE=SUP,KEY=ZERO     INTO THE TELEPHONE BOOTH    MODE
         LA    R0,0                    ZERO REG 0                  MODE
         LA    R1,COMMAND              ADDRESS OF COMMAND          MODE
         SVC   34                      ISSUE REFRESH               MODE
         MODESET MODE=PROB,KEY=NZERO   BACK TO CLARK KENT          MODE
         B     CONTINUE                CONTINUE
USERCMD  MVC   USERID2(0),USERID       MOVE USERID TO SEND COMMAND
BDAMERR  DC    C'BDAM I/O ERROR - BRODCAST INACCESSIBLE'
NORECORD TPUT  BDAMERR,L'BDAMERR
CLOSEIT  CLOSE (BRODCAST)               CLOSE FILE
DEQZERO  DEQ   (MAJOR,ENQZERO,3,SYSTEM),RET=HAVE  MINOR = RBA OF ZERO
*  DEALLOCATE DATASET
UNALLOC  MVI   S99VERB,DEALLOC         DEALLOCATE DATASET
         MVC   S99FLAG1(4),ZERO        CLEAR STATUS
         MVC   S99INFO(2),ZERO         CLEAR EXTRA INFO
         OI    S99TUPL,X'80'           ONLY DDNAME REQUIRED
         LA    R1,S99RBPTR             SET R1 TO RB PTR
         SVC   99                      ISSUE ALLOC
RETURN   SR    R15,R15
         L     R13,SAVE+4
         L     R14,12(R13)
         LM    R0,R12,20(R13)
         BR    R14
READ     XC    FEEDBACK,FEEDBACK  CLEAR FEED BACK AREA
         MVC   FEEDBACK(3),RBA    SET RBA
         READ  READDECB,DIF,BRODCAST,RECORD,'S',KEY,FEEDBACK
         CHECK READDECB           WAIT FOR IO
         BR    R9
FEEDBACK DS    CL8
WRITE    TM    FLAG,NOWRITE       WRITE DISABLED ?
         BOR   R9                 YES...GET OUT
         WRITE WRITDECB,DI,BRODCAST,RECORD,'S',KEY,RBA
         CHECK WRITDECB           WAIT FOR IO
         BR    R9
SAVE     DS    18F
SAVER1   DS    F
USERID   DS    CL7
JOBNAME  DS    CL8
FLAG     DC    X'01'              WRITE DISABLED
DELETE   EQU   X'80'              DELETE REQUESTED
NOWRITE  EQU   X'01'              WRITE DISABLED
MAJOR    DC    CL8'SYSIKJBC'
ENQZERO  DS    X'000000'
MAILRBA  DS    X'000000'
MAILRBAL DS    X'000000'
ENQRBA   DS    X'000000'
FREERBA  DS    X'000000'
LOWRBA   DS    X'000000'
DIRRBA   DS    X'000000'
RBA      DS    X'000000'
KEY      DS    X'00'
RECORD   DS    CL129
DIRREC   DS    CL129
SAVEMRBA DS    F
         LTORG
USERTAB  DC    5D'0'                 5 USERID MAX
USERTABE DS    F
BRODCAST DCB   DSORG=DA,                                               X
               MACRF=RIC,                                              X
               RECFM=F,                                                X
               DDNAME=SYSLBC,                                          X
               OPTCD=R,                                                X
               BUFNO=255,                                              X
               SYNAD=NORECORD
*        S V C 9 9   A R E A                                          *
ZERO     DC    F'0'
S99RBPTR DC    X'80',AL3(S99RB)        ADDRESS OF PARM LIST
S99RB    DC    X'14'
S99VERB  DC    X'01'
ALLOC    EQU   X'01'
DEALLOC  EQU   X'02'
S99FLAG1 DC    X'0000'
S99ERROR DC    X'0000'
S99INFO  DC    X'0000'
S99TXTP  DC    AL4(S99TUPL)
         DC    F'0'
S99FLAG2 DC    F'0'
TEXTPTRS EQU   *
S99TUPL  DC    AL4(TU0001)
TU2      DC    AL4(TU0002)
TU3      DC    AL4(TU0003)
TU4      DC    AL4(TU0004)
TU5      DC    X'80',AL3(TU0005)       SET HIGH BIT ON LAST PTR
* //TU0001  DD DSN=TU0002,DISP=(TU0003,TU0004,TU0005)
TU0001   DC    X'0001',X'0001',X'0008'
DDNAME   DC    CL8'SYSLBC'                    //SYSLBC DD
TU0002   DC    X'0002',X'0001',X'002C'
DSN      DC    CL44'SYS1.BRODCAST'            DSN=SYS1.BRODCAST,
TU0003   DC    X'0004',X'0001',X'0001',X'08'  DISP=(SHR,
TU0004   DC    X'0005',X'0001',X'0001',X'08'            KEEP,
TU0005   DC    X'0006',X'0001',X'0001',X'08'                 KEEP)
DSNAREA  DS    CL44                    AREA TO CREATE DSN
ACF2AREA ACDSV DSECT=NO                                            ACF2
         DS    0F
COMMAND  DC AL2(L'COMMAND+L'CMD2+L'CMD3+L'CMD4)
CMD2     DC H'0'
CMD3     DC C'SE ''YOUR PREVIOUS MESSAGES WERE PURGED BY XXXXXXXX'''
CMD4     DC C',LOGON,USER=(        '
USERID1  EQU   CMD3+42,8
USERID2  EQU   CMD4+13,7
         DC    0F'0'                    ALIGN
PPLREAL  DC    (PPLLNGTH)X'00'          PPL STORAGE AREA
ECB      DC    F'0'                     COMMAND PROCESSOR ECB
ANS      DC    A(0)                     ANSWER WORD FOR PDL POINTER
PCL      IKJPARM
PDL1     IKJKEYWD
         IKJNAME   'USER',SUBFLD=PDLUSRID
PDL2     IKJKEYWD  DEFAULT='READONLY'
         IKJNAME   'READONLY'
         IKJNAME   'PURGE'
PDLUSRID IKJSUBF
PDLLIST  IKJIDENT  'USERID',LIST,UPPERCASE,                            X
               MAXLNTH=7,FIRST=ALPHA,OTHER=ALPHANUM,                   X
               PROMPT='USERID(S) FOR WHOM MESSAGES ARE TO BE READ',    X
               HELP=('USERID(S) OF MESSAGE DESTINATION')
         IKJENDP
         IKJCPPL
         DCBD  DSORG=DA
         ACCVT DSECT=YES
         IKJPPL                        PPL DSECT
PPLLNGTH EQU   *-PPL                   PPL LENGTH
         CVT   DSECT=YES,LIST=NO
         IHAASCB
         END


     © Xephon 1988































