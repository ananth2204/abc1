Assembler routine to display open catalogs

We have recently implemented a catalog recovery procedure at
our site and as part of that process one of the tasks that was
required was that of identifying which catalogs were open at
certain points in time. I initially thought that the MVS MODIFY
CATALOG command would help us in this area; however, it
does not allow a display of all open catalogs to be produced.

To achieve the objective of producing a display of all open
catalogs, I wrote the following small Assembler program. This
program accesses the Catalog Auxiliary Work Area (CAXWA)
control block and the Access Method Control Block Structure
(AMCBS) to determine which catalogs are open. The CAXWA is
built whenever a master or user catalog is opened or created. For
MVS systems there is always one catalog open - the master
catalog. This is opened when the system is started and remains
open until it is stopped. The program presented will run as a batch
job or under TSO and produces a list of open catalogs and
identifies them as USER or MASTER catalogs. A partial example
of the output produced is shown in Figure 1.

                 J E S 2  J O B  L O G  --  S Y S T E M S P 6 0  --  N O D

  14.53.45 JOB 7867  TSS701I STS01    LAST-USED 16 APR 94 14:52 SYSTEM=SP60 FACILI
  14.53.45 JOB 7867  TSS702I COUNT=25699  MODE=IMPL  LOCKTIME=NONE NAME=BRADLEY.J
  14.53.45 JOB 7867  $HASP373 STS01B   STARTED - INIT  6 - CLASS A - SYS SP60
  14.53.45 JOB 7867  IEF403I STS01B - STARTED - TIME=14.53.45
  14.53.46 JOB 7867  +CATALOG TYPE = USER   NAME = CATALOG.SCADAM
  14.53.46 JOB 7867  +CATALOG TYPE = USER   NAME = CATALOG.ATEST
  14.53.46 JOB 7867  +CATALOG TYPE = USER   NAME = CATALOG.APROD
  14.53.46 JOB 7867  +CATALOG TYPE = USER   NAME = CATALOG.SYSCICS
  14.53.46 JOB 7867  +CATALOG TYPE = USER   NAME = CATALOG.QPROD
  14.53.46 JOB 7867  +CATALOG TYPE = USER   NAME = CATALOG.STEST
  14.53.46 JOB 7867  +CATALOG TYPE = USER   NAME = CATALOG.SPROD
  14.53.46 JOB 7867  +CATALOG TYPE = USER   NAME = CATALOG.SCICS
  14.53.46 JOB 7867  +CATALOG TYPE = USER   NAME = CATALOG.SNCP
  14.53.46 JOB 7867  +CATALOG TYPE = USER   NAME = CATALOG.STSG
  14.53.46 JOB 7867  +CATALOG TYPE = USER   NAME = CATALOG.SNCP89C
  14.53.46 JOB 7867  +CATALOG TYPE = USER   NAME = CATALOG.SIM
  14.53.46 JOB 7867  +CATALOG TYPE = USER   NAME = CATALOG.FDRABR
  14.53.46 JOB 7867  +CATALOG TYPE = MASTER NAME = CATALOG.DMVS89D
  14.53.46 JOB 7867  -                                                 --TIMINGS (M
  14.53.46 JOB 7867  -JOBNAME  STEPNAME PROCSTEP    RC   EXCP   CONN     TCB    SRB
  14.53.46 JOB 7867  -STS01B   STEP1                00      6     28     .00    .00
  14.53.47 JOB 7867  IEF404I STS01B - ENDED - TIME=14.53.47
  14.53.47 JOB 7867  -STS01B   ENDED.  NAME-JOHN BRADLEY         TOTAL TCB CPU TIM
  14.53.47 JOB 7867  $HASP395 STS01B   ENDED

  ------ JES2 JOB STATISTICS ------
      16 APR 94 JOB EXECUTION DATE
             14 CARDS READ
             79 SYSOUT PRINT RECORDS
              0 SYSOUT PUNCH RECORDS
              5 SYSOUT SPOOL KBYTES
           0.03 MINUTES EXECUTION TIME

   Figure 1: Sample display


SOURCE CODE FOR BCATOPEN

         TITLE     'DISPLAY OPEN CATALOG PROGRAM'
*        **************************************************************
*        * PROGRAM NAME: BCATOPEN.                                    *
*        * PURPOSE:      TO DISPLAY STATUS OF CATALOGS IN SYSTEM.     *
*        **************************************************************
BCATOPEN CSECT
         SAVE      (14,12)             /* SAVE REGISTERS.            */
         LR        R12,R15             /* LOAD ENTRY POINT.          */
         USING     BCATOPEN,R12        /* ESTABLISH A BASE.          */
         ST        R13,SAVEAREA+4      /* STORE SAVEAREA ADDRESS.    */
         LA        R2,SAVEAREA         /* CHAIN SAVEAREA.            */
         ST        R15,8(R13)          /* CHAIN SAVEAREA.            */
         LR        R13,R2              /* LOAD NEW SAVEAREA          */
START    EQU       *
         LA        R2,16               /* ADDRESS OF CVT.            */
         L         R2,0(R2)            /* POINT TO CVT.              */
         LA        R3,256              /* OFFSET TO AMCBS PTR.       */
         L         R2,0(R2,R3)         /* POINT TO AMCBS.            */
         ENQ       (MAJNAME,MINNAME,S,4,SYSTEM) /* ENQ THE CAXWA.    */
         LTR       R15,R15             /* CHECK REGISTER 15.         */
         BNZ       ABEND               /* FAILED, THEN END.          */
         L         R2,20(R2)           /* POINT TO CAXWA CHAIN.      */
         LTR       R2,R2               /* IS IT 0?                   */
         BZ        NOCATS              /* YES, THEN GO ISSUE MESSAGE.*/
CAXWA    EQU       *
         TM        8(R2),X'04'         /* MASTER OR USER CATALOG?    */
         BNO       USER                /* PUT USER IN MESSAGE.       */
         MVC       WTO+23(6),=C'MASTER' /* PUT MASTER IN MESSAGE.    */
         B         CATNAME             /* GO ISSUE IT.               */
USER     EQU       *
         MVC       WTO+23(6),=C'USER  ' /* PUT USER IN MESSAGE.      */
CATNAME  EQU       *
         MVC       WTO+37(44),52(R2)   /* GET CATALOG NAME.          */
WTO      WTO       'CATALOG TYPE =        NAME =                       X
                                      '
         L         R2,4(R2)            /* POINT TO NEXT ENTRY.       */
         LTR       R2,R2               /* DOES IT EXIST?             */
         BNZ       CAXWA               /* YES THEN LOOP ROUND.       */
         B         GETOUT              /* NO, THEN GET OUT.          */
NOCATS   EQU       *
*        IMPOSSIBLE TO COME HERE UNDER MVS AS MASTER CATALOG IS ALWAYS
*        OPEN.
         WTO       'NO CATALOGS OPEN ON THIS SYSTEM.'
GETOUT   EQU       *
         DEQ       (MAJNAME,MINNAME,4,SYSTEM) /* ISSUE DEQ MACRO.    */
         L         R13,SAVEAREA+4      /* RELOAD SAVEAREA.           */
         LM        R14,R12,12(R13)     /* RELOAD REGS.               */
         SR        R15,R15             /* ZEROIZE REG15.             */
         BR        R14                 /* RETURN TO CALLER.          */
ABEND    EQU       *
         ABEND     0001,DUMP           /* ABEND IF ERROR.            */
SAVEAREA DS        18F
MAJNAME  DC        CL8'SYAZCAXW'
MINNAME  DC        CL4'CAXW'
         END

JCL TO EXECUTE BCATOPEN IN BATCH

//STS01B      JOB   (SDTS),'JOHN BRADLEY',CLASS=A,MSGCLASS=Q,
//            MSGLEVEL=(1,1),NOTIFY=STS01
//*      ***************************************************************
//*      * JCL TO RUN PROGRAM TO LIST OPEN CATALOGS.                   *
//*      ***************************************************************
//STEP1       EXEC  PGM=BCATOPEN
//STEPLIB     DD    DSN=STS01.BD.LOAD,DISP=SHR
//SYSPRINT    DD    SYSOUT=*
//SYSUDUMP    DD    SYSOUT=*

JCL TO ASSEMBLE AND LINK EDIT BCATOPEN

//STS01V      JOB   (SDTS),'J.BRADLEY',CLASS=A,MSGCLASS=Q,
//            MSGLEVEL=(1,1),NOTIFY=STS01
//*
//ASM1        EXEC  PGM=IEV90,PARM='OBJ,NODECK,XREF(SHORT)'
//SYSLIB      DD    DSN=SYS1.MACLIB,DISP=SHR
//            DD    DSN=SYS1.AMODGEN,DISP=SHR
//            DD    DSN=STS01.BD.MACLIB,DISP=SHR
//            DD    DSN=SYS1.HASPSRC,DISP=SHR
//            DD    DSN=SYS1.ATSOMAC,DISP=SHR
//SYSUT1      DD    UNIT=SYSDA,SPACE=(1700,(600,100))
//SYSUT2      DD    UNIT=SYSDA,SPACE=(1700,(300,50))
//SYSUT3      DD    UNIT=SYSDA,SPACE=(1700,(300,50))
//SYSPRINT    DD    SYSOUT=*
//SYSLIN      DD    UNIT=SYSDA,SPACE=(80,(200,50)),DISP=(,PASS)
//SYSIN       DD    DSN=STS01.BD.SOURCE(BCATOPEN),DISP=SHR
//*
//LKED1       EXEC  PGM=IEWL,PARM=('MAP,LET,LIST,NCAL'),
//            COND=(0,NE,ASM1)
//SYSUT1      DD    UNIT=SYSDA,SPACE=(1024,(50,20))
//SYSPRINT    DD    SYSOUT=*
//SYSLMOD     DD    DSN=STS01.BD.LOAD,DISP=SHR
//SYSLIB      DD    DSN=*.ASM1.SYSLIN,DISP=(OLD,DELETE)
//SYSLIN      DD    *
 INCLUDE SYSLIB
 NAME    BCATOPEN(R)
/*


John Bradley
Senior Technical Support Engineer (UK)                            c Xephon 1994

