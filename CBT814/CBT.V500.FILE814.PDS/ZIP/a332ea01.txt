Edit macro print facility

An edit macro has been written at our company to print the
saved or unsaved, complete or partial, contents of a sequential
dataset or member of a partitioned dataset on a selected printer.
When the dataset is selected to print on a local printer, a
background job is submitted to print the required text. If the
remote printer is selected, a PRINTOFF command is executed
in foreground. We have a Delphax local printer, which allows
us to print in landscape or portrait, single-sided or double-
sided. Depending upon the selection for the type of printout on
the local printer, a control code is added on top of the text
when a batch job is submitted. When this macro is executed
from the command line of a member or a sequential dataset, a
panel is displayed to select the type of printout, record format
(FB or FBA), the number of copies, and the remote destination
of the printer if printing on a local printer is not required. If the
selected lines are required for printing, perform any one of the
following before entering the PRINTIT command on the
command line:

1      Enter CC for the starting and ending lines to print, on the
      edit line command.

2      Enter Cnn on the edit line command to print, where nn
      represents the total number of lines to print starting from
      the first line where the edit line command is entered.

When the ENTER key is pressed on the panel, a temporary
dataset is allocated to contain the selected lines or complete
dataset or member to print.  If the RECFM of the input dataset
or member is not FBA, the whole text is shifted by one
character.  After copying the requested print to a temporary
dataset, either a batch job is submitted to print the text on a
local printer or the foreground command PRINTOFF is
executed for the remote printer selected.

The PRINTIT macro consists of a CLIST, panel, skeleton, and
messages.  If a Delphax printer is not installed at your site, a
minor modification to the CLIST and skeleton will be required.


CLIST PRINTIT

ISREDIT MACRO NOPROCESS
/*
/*   PRINT SAVED OR UNSAVED DATASET/MEMBER ON A SELECTED PRINTER
/*
/*
ISREDIT (LAST) = LINENUM .ZLAST
ISREDIT (WIDTH) = DATA_WIDTH
ISREDIT (L1,C1) = CURSOR
ISREDIT PROCESS RANGE C
ISREDIT (FIRST1) = LINENUM .ZFRANGE
ISREDIT (LAST1)  = LINENUM .ZLRANGE
/*
SET &CMSGID = &STR( )
DSPLAY: +
ISPEXEC DISPLAY PANEL(PRINTX1) MSG(&CMSGID)
IF &LASTCC = 8 THEN GOTO FINISH
/*
SET &CMSGID = &STR( )
/*
/* BUILD TEMPORARY DATASET NAME TO STORE ALLOC COMMAND BEFORE
/* EXECUTION
/*
SET &PDATE = &SUBSTR(7:8,&SYSDATE)&SUBSTR(1:2,&SYSDATE)
SET &PDATE = &PDATE&SUBSTR(4:5,&SYSDATE)
SET &PTIME = &SUBSTR(1:2,&SYSTIME)&SUBSTR(4:5,&SYSTIME)
SET &PTIME = &PTIME&SUBSTR(7:8,&SYSTIME)
SET &TEMPP=&SYSUID&STR(.PRINTIT.D)&PDATE&STR(.T)&PTIME
/*
/* ALLOCATE TEMPORARY DATASET NAME
/*
SET &DELETE = &STR(DELETE)
IF &PDEST = THEN SET &DELETE =
IF &WIDTH = 72 THEN SET &WIDTH = 81
IF &WIDTH = 80 THEN SET &WIDTH = 81
IF &WIDTH = 132 THEN SET &WIDTH = 133
IF &WIDTH >81 THEN DO
   IF &WIDTH ^= 133 THEN DO
       SET &CMSGID = &STR(PRNT001F)
       GOTO DSPLAY
       END
    END
/*
IF &WIDTH > 81 AND &PTYPE < 3 THEN DO
   SET &CMSGID = &STR(PRNT001A)
   GOTO DSPLAY
   END
SET &BLKSZ = &WIDTH*40
/*
ALLOC DA('&TEMPP') LRECL(&WIDTH) BLKSIZE(&BLKSZ) SP(10) TR NEW -
    RECFM(F B A) REUSE FI(ASHP) UNIT(VIO) &DELETE
/*
OPENFILE ASHP OUTPUT              /* OPEN TEMP. FILE
/*
SET &LINEL = &FIRST1
NEXTP: +
IF &LINEL > &LAST1 THEN GOTO QUIT
ISREDIT (ASHP) = LINE &LINEL
/*  SET ASHP = &STR(&TXLINE)
IF &PRECFM = 2 THEN SET ASHP = &STR( )&STR(&ASHP)
PUTFILE ASHP
SET &LINEL = &LINEL + 1
GOTO NEXTP
QUIT: +
CLOSFILE ASHP
/*
/* PRINT THE DATASET NOW
/*
IF &PDEST = THEN DO
    IF &PCPIES = THEN SET &PCPIES = 1
    ISPEXEC VGET (ZTEMPF) SHARED
    SET TEMP = &ZTEMPF
    ISPEXEC FTOPEN TEMP
    ISPEXEC FTINCL PRINTX2
    ISPEXEC FTCLOSE
    SUB '&TEMP'
    END
ELSE DO
    DO WHILE &PCPIES ^= 0
       PRINTOFF '&TEMPP' DEST(&PDEST)
       SET &PCPIES = &PCPIES - 1
       END
END
/*
FREE F(ASHP)
FINISH: +
END


PANEL PRINTX1

)ATTR
  ^ TYPE(INPUT) INTENS(NON)
)BODY
%------------------------- PRINT UTILITY ---------------------------------------
%COMMAND ===>_ZCMD
%
+
+      Type of Printout%=========>_Z+  (1=Portrait single sided
+                                       2=Portrait double sided
+                                       3=Landscape single sided
+                                       4=Landscape double sided)
+
+      Recfm of input Dataset%===>_Z+ (1=FBA, 2=FB)
+
+ Number of copies to Print%=====>_PCPIES+
+
+ Remote Printer Destination%====>_PDEST   +
+
+ Note 1: - If the record length of input dataset is greater than 80 then only
+           Landscape single sided or double sided printout can be selected.
+ Note 2: - If the remote destination is specified then type of printout will
+           be ignored.
+
)INIT
 .ZVARS = '(PTYPE,PRECFM)'
 &PCPIES = 1
)PROC
 VER (&PRECFM,NB,LIST,1,2,MSG=PRNT001E)
 VER (&PCPIES,NB,NUM,MSG=PRNT001C)
 IF (&PDEST = ' ')
   VER (&PTYPE,NB,LIST,1,2,3,4,MSG=PRNT001B)
)END


SKELETON PRINTX2

The control codes for landscape and portrait, single-sided and
double- sided printing are on a different dataset with
ADQ.SYSF.* concatenated at the start of the temporary dataset
to print. The control code for the double-sided landscape is
concatenated at the end of the temporary dataset to leave the
local printer in that mode after the job is printed. The control
codes are not specified here because they are peculiar to the
Delphax printer.

//&SYSUID.P  JOB (ADP,MVS,000107),'&SYSUID..PRINTIT',
//   CLASS=A,MSGCLASS=Z,MSGLEVEL=(1,1),
//   NOTIFY=&SYSUID
//STEP010  EXEC PGM=IEBGENER,REGION=2048K
)SEL &PTYPE > 2
//OUT1     OUTPUT CLASS=A,COPIES=&PCPIES
)ENDSEL
)SEL &PTYPE < 3
//OUT1     OUTPUT CLASS=A,COPIES=&PCPIES,LINECT=72
)ENDSEL
)SEL &PTYPE = 1
//SYSUT1   DD  DSN=ADQ.SYSF.DELPHAX.FBA80(SIMPOR),DISP=SHR
)ENDSEL
)SEL &PTYPE = 2
//SYSUT1   DD  DSN=ADQ.SYSF.DELPHAX.FBA80(DUPPOR),DISP=SHR
)ENDSEL
)SEL &PTYPE = 3 && &WIDTH < 133
//SYSUT1   DD  DSN=ADQ.SYSF.DELPHAX.FBA80(SIMLAN),DISP=SHR
)ENDSEL
)SEL &PTYPE = 4 && &WIDTH < 133
//SYSUT1   DD  DSN=ADQ.SYSF.DELPHAX.FBA80(DUPLAN),DISP=SHR
)ENDSEL
)SEL &PTYPE = 3 && &WIDTH = 133
//SYSUT1   DD  DSN=ADQ.SYSF.DELPHAX.FBA133(SIMLAN),DISP=SHR
)ENDSEL
)SEL &PTYPE = 4 && &WIDTH = 133
//SYSUT1   DD  DSN=ADQ.SYSF.DELPHAX.FBA133(DUPLAN),DISP=SHR
)ENDSEL
//*
//         DD   DSN=&TEMPP,
//      DCB=(RECFM=FBA,LRECL=&WIDTH,BLKSIZE=&BLKSZ),
//      DISP=(OLD,DELETE)
)SEL &WIDTH < 133
//         DD  DSN=ADQ.SYSF.DELPHAX.FBA80(DUPLAN),DISP=SHR
)ENDSEL
)SEL &WIDTH = 133
//         DD  DSN=ADQ.SYSF.DELPHAX.FBA133(DUPLAN),DISP=SHR
)ENDSEL
//*
//SYSUT2   DD  SYSOUT=(,),
//      DCB=(RECFM=FBA,LRECL=&WIDTH,BLKSIZE=&BLKSZ),
//             OUTPUT=(*.OUT1)
//SYSPRINT DD  SYSOUT=*
//SYSIN    DD  DUMMY
//


MESSAGES PRINT00

PRNT001A 'INVALID TYPE OF PRINTOUT'                     .ALARM=YES
'TYPE OF PRINTOUT MUST BE 3 OR 4 IF THE RECORD LENGTH IS > 80'
PRNT001B 'INVALID TYPE OF PRINTOUT'                     .ALARM=YES
'TYPE OF PRINTOUT MUST BE 1 TO 4'
PRNT001C 'MUST BE NUMERIC'                              .ALARM=YES
'NUMBER OF COPIES TO PRINT MUST BE NUMERIC'
PRNT001E 'INVALID RECORD FORMAT'                        .ALARM=YES
'RECFM MUST BE 1 OR 2 IE FB OR FBA'
PRNT001F 'INVALID RECORD LENGTH'                        .ALARM=YES
'IF RECORD LENGTH IS GREATER THAN 81 THEN IT MUST BE 133'


Ashwani Puri
Systems Programmer
Argos plc (UK)                                                 c Xephon 1992

