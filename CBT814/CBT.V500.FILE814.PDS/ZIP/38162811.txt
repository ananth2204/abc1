Handling operator console messages

Some MVS console messages roll off the screen even though they may be important.
The WTO user exit that follows will highlight such messages and keep them on the
screen until manually deleted by an operator.  The exit achieves this by
changing the descriptor code of the messages to a 2 (immediate action).

The messages that you choose to keep on the screen must have unique message
identifiers, they should be infrequent, and they should be important enough to
require manual action from an operator.

Here are some examples:

1    IEF450I jobname ABENDED.  For production jobs, you may want to take
suitable action.

2    IST102I VTAM INITIALISATION COMPLETE.  You may want to start TSO and other
VTAM applications.

To activate the exit, you need to:

1    Assemble and link-edit it into LPALIB (with the link-edit attributes RENT,
REFR, and AC=l).

2    Update the MPFLSTxx member in SYSl.PARMLIB (or create one), as follows:

     IEF450I,SUP(NO),RETAIN(NO),USEREXIT(NOROLL)

It may be desirable to activate the exit only at nights, to prevent the console
from being flooded with innocuous abend messages, like S522s from TSO users.  In
this case, you need to do the following:

1    Create a night-time MPFLSTxx.

2    Invoke the exit from only this night-time MPFLST.

3    Activate this MPFLST at nights automatically by MVS auto-commands.

4    Activate the day-time MPFLST during days, again by MVS auto-commands.

Here is the exit:

*--------------------------------------------------------------------*
*                        WTO USER EXIT                               *
*      TO PREVENT MVS CONSOLE MESSAGES FROM ROLLING OFF THE SCREEN   *
*--------------------------------------------------------------------*
NOROLL   CSECT
NOROLL   AMODE 31                    31-BIT ADDRESSING MODE
NOROLL   RMODE ANY                   31-BIT RESIDENCE
CTXTPTR  EQU   5                     R5 POINTS TTXT
         STM   R14,R12,12(R13)       SAVE CALLERS REGS
         BALR  R12,R0                ESTABLISH MODULE BASE
         USING *,R12                 REGISTER
         L     CTXTPTR,0(R1)         ESTABLISH ADDRESSABILITY
         USING CTXT,CTXTPTR          TO THE CTXT
         OI    CTXTRFB1,CTXTRCDC     REQUEST TO CHANGE DESC CODES
         L     R2,CTXTDCP            ESTABLISH ADDRESSABILITY
         USING CTXTDESC,R2           TO THE DESCRIPTOR CODES
         XC    CTXTDESC(2),CTXTDESC  ZERO THE DESC CODES
         OI    CTXTDC1,CTXTDC02      TURN ON DESC CODE 2 FLAG
         LM    R14,R12,12(R13)       THAT'S ALL, PREPARE TO...
         BR    R14                   EXIT
         IEZVX100
         END   NOROLL


Dinesh Dattani
Technical Analyst
CIL Inc (Canada)    © CIL Inc 1987































































