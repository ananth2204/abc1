ISPF dataset list utility CLISTs

The dataset list utility offers many primary and line commands
to process a dataset.  While it is quite comprehensive, some
functions that a user may want to use against a dataset are not
currently supported, for example, listing catalog entries
including tape entries, deleting GDG base entries, transmitting
multiple datasets, or transmitting a member of a dataset to
multiple locations.  Documented below are four CLISTs that
address these requirements.

When executing these CLISTs, remember that you can
overtype the dataset name without 'damaging' it.  The slash (/)
must be used where indicated because the CLIST requires the
quoted dataset name.  Both of these items are explained in the
dataset list utility tutorial.

LDS - List a dataset Catalog Entry

This CLIST will issue an IDCAMS LISTC ALL command
against the dataset when only LDS is issued.  Tape and VSAM
dataset entries can be displayed.  If PARM is used, the slash (/)
is required and the LISTC parameters must be separated by
blanks and enclosed in quotes.  If PARM(P) is issued, you will
be prompted for the LISTC parameters.

PROC 1 DS PARM()
/*  EXECUTE A LISTC COMMAND AGAINST THE DATA SET.  */
/*  COMMAND FORMAT:  */
/*   LDS / PARM('PARM PARM...'|P)  */
/*  THE 'ALL' PARAMETER IS USED UNLESS PARM('PARM PARM...') IS  */
/*  ISSUED SPECIFYING IDCAMS LISTC PARAMETERS.  IF PARM(P) IS  */
/*  ISSUED, THE USER WILL BE PROMPTED FOR PARAMETERS.  */
CONTROL NOMSG END(STOP) NOFLUSH PROMPT
/*  FOR READABILITY, INSERT A COMMAND TO CLEAR THE SCREEN.  */
ERROR DO
 EXIT
STOP
SET &ZDLMSG EQ &STR( )
ISPEXEC VPUT (ZDLMSG)
IF &PARM EQ P THEN DO
 WRITE
 WRITE SPECIFY LISTC PARAMETERS SEPARATED BY BLANKS, ENCLOSED +
   WITHIN QUOTES
 WRITENR FOR &DS, SPECIFY 'PARM PARM...'
 READ &PARM
STOP
IF &PARM NE  THEN LISTC ENT(&DS) &PARM
ELSE LISTC ENT(&DS) ALL
SET &ZDLMSG = &STR(LISTED)
ISPEXEC VPUT (ZDLMSG)
EXIT CODE (0)

RGDG - Delete a GDG Base Entry

This CLIST will issue an IDCAMS DELETE command against
the dataset when only RGDG is issued.  No additional
parameters are used, therefore default processing is done.  You
can use this command against any dataset, but a delete
verification screen will not be displayed.  If PARM is used, the
slash (/) is required and the DELETE parameters must be
separated by blanks and enclosed in quotes.  If PARM(P) is
issued, you will be prompted for the DELETE parameters.

PROC 1 DS PARM()
/*  EXECUTE THE DELETE COMMAND AGAINST THE GDG BASE ENTRY.  */
/*  COMMAND FORMAT:  */
/*   RGDG / PARM('PARM PARM...'|P)  */
/*  IDCAMS DEFAULT PARAMETERS ARE USED UNLESS PARM('PARM PARM...')  */
/*  IS ISSUED SPECIFYING IDCAMS DELETE PARAMETERS.  IF PARM(P) IS  */
/*  ISSUED, THE USER WILL BE PROMPTED FOR PARAMETERS.  */
CONTROL MSG END(STOP) NOFLUSH PROMPT
/*  FOR READABILITY, INSERT A COMMAND TO CLEAR THE SCREEN.  */
ERROR DO
 EXIT
STOP
SET &ZDLMSG EQ &STR( )
ISPEXEC VPUT (ZDLMSG)
IF &PARM EQ P THEN DO
 WRITE
 WRITE SPECIFY DELETE GDG PARAMETERS SEPARATED BY BLANKS, ENCLOSED +
   WITHIN QUOTES
 WRITENR FOR &DS, SPECIFY 'PARM PARM...'
 READ &PARM
STOP
IF &PARM NE  THEN DEL &DS &PARM
ELSE DEL &DS
SET &ZDLMSG EQ &STR(GDG BASE DELETED)
ISPEXEC VPUT (ZDLMSG)
EXIT CODE (0)

XDS - Transmit a dataset, a PDS, or members of a PDS

This CLIST will issue a TRANSMIT command against the
dataset.  If the dataset is a PDS, all members will be
transmitted unless MEM is used.  DEST is required and you
will be prompted if it is omitted.  If MEM is used, the member
names must be separated by commas and enclosed in quotes.
If MEM(P) is issued, you will be prompted for the member
names.  If DEST and/or MEM is used, the slash (/) is required.

PROC 1 DS DEST() MEM()
/*  EXECUTE THE TRANSMIT COMMAND AGAINST THE DATA SET.  */
/*  COMMAND FORMAT:  */
/*   XDS / DEST(NODE.USERID) MEM('MEMBER,MEMBER...'|P)  */
/*  THE DEST PARAMETER MUST BE SPECIFIED OR A PROMPT WILL BE ISSUED.  */
/*  IF THE DATA SET IS A PDS, ALL MEMBERS ARE SENT UNLESS  */
/*  MEM('MEMBER,MEMBER...') IS ISSUED SPECIFYING MEMBER NAMES.  */
/*  IF MEM(P) IS ISSUED, THE USER WILL BE PROMPTED FOR A MEMBER LIST. */
CONTROL NOMSG END(STOP) NOFLUSH PROMPT
/*  FOR READABILITY, INSERT A COMMAND TO CLEAR THE SCREEN.  */
ERROR DO
 EXIT
STOP
SET &ZDLMSG EQ &STR( )
ISPEXEC VPUT (ZDLMSG)
IF &DEST EQ  THEN DO
 WRITE
 WRITENR TO TRANSMIT &DS, SPECIFY NODE.USERID
 READ &DEST
STOP
IF &MEM EQ P THEN DO
 WRITE
 WRITE SPECIFY MEMBER NAMES SEPARATED BY COMMAS, ENCLOSED +
   WITHIN QUOTES
 WRITENR FOR &DS, SPECIFY 'MEMBER,MEMBER...'
 READ &MEM
STOP
IF &MEM NE  THEN TRANSMIT &DEST DA(&DS) MEM(&MEM)
ELSE TRANSMIT &DEST DA(&DS)
SET &ZDLMSG EQ &STR(TRANSMITTED)
ISPEXEC VPUT (ZDLMSG)
EXIT CODE (0)

XDSM - Transmit a member of a PDS

This CLIST will issue a TRANSMIT command against the
dataset and member.  It can only by used if the DISPLAY
MEMBER LIST (M) line command was issued against the
dataset.  It is executed from the member selection list line
command area.  Because of the restrictions on line command
length, you will be prompted for the receiving node and user-
id.  This CLIST will allow multiple transmissions of the
member until you hit ENTER instead of specifying a receiving
node and user-id.

PROC 1 DS DEST()
/*  EXECUTE THE TRANSMIT COMMAND AGAINST A MEMBER OF A PDS.  */
/*  COMMAND FORMAT:  */
/*   XDSM  */
/*  THIS COMMAND CAN ONLY BE USED IF THE DISPLAY MEMBER LIST (M)  */
/*  LINE COMMAND WAS ISSUED.  THIS CLIST IS EXECUTED FROM THE MEMBER  */
/*  SELECTION LIST LINE COMMAND AREA.  */
/*  BECAUSE OF THE RESTRICTIONS ON LINE COMMAND LENGTH,  THE USER  */
/*  WILL BE PROMPTED FOR RECEIVING NODE AND USERID.  */
/*  THIS CLIST WILL ALLOW MULTIPLE TRANSMISSIONS OF THE MEMBER UNTIL  */
/*  THE USER HITS 'ENTER' INSTEAD OF SPECIFYING A RECEIVING NODE AND  */
/*  USERID.  */
CONTROL NOMSG END(STOP) NOFLUSH PROMPT
/*  FOR READABILITY, INSERT A COMMAND TO CLEAR THE SCREEN.  */
ERROR DO
 EXIT CODE(8)
STOP
LOOP: -
WRITE
WRITE IF TRANSMISSION IS COMPLETE, PRESS ENTER.
WRITENR TO TRANSMIT &DS, SPECIFY NODE.USERID
READ &DEST
IF &DEST =  THEN EXIT CODE (0)
TRANSMIT &DEST DA(&DS)
GOTO LOOP


Richard A Morales
Systems Programmer
US West Communications Inc (USA)           © US West Communications 1990

