Adding the date to HASP165 notify messages

INTRODUCTION

The JES2 HASP165 notify message is sent to TSO users as a
result of the NOTIFY parameter on the job card.  The message
includes the time, but not the date.  When a TSO-id has not been
used in several days (after a weekend, for example), the date is
very useful in determining when a job ended or abended.
JESEXT10 is a very short and simple JES2 exit 10, which will
add the date to the HASP165 notify message.  The effects of the
exit are shown below.

Standard HASP165 message

14:32:24 JOB 3452 $HASP165 PAYROLL1 ENDED AT NODE1 - ABENDED

HASP165 message after JESEXT10 installed

89126 14:32 J3452 $HASP165 PAYROLL1 ENDED AT NODE1 - ABENDED

JESEXT10

JESEXT10 TITLE 'ADD DATE TO HASP165 NOTIFY MESSAGES'
         COPY  $HASPGBL           COPY HASPGBL PARAMETERS
* THIS EXIT HAS ONLY BEEN TESTED UNDER JES2 SP2.2.0
* BUT SHOULD WORK WITH ALL JES2 RELEASES THAT SUPPORT EXIT 10
JESEXT10 $MODULE NOTICE=SP220,                                         X
               RPL,                                                    X
               $BUFFER,                                                X
               $CAT,                                                   X
               $CMB,                                                   X
               $HASPEQU,                                               X
               $HCT,                                                   X
               $JCT,                                                   X
               $JQE,                                                   X
               $MIT,                                                   X
               SSOB,                                                   X
               $PCE
EXIT10   $ENTRY BASE=R12,CSECT=YES SCD
         USING  PCE,R13
         $SAVE
         LR    R12,R15            SETUP ADDRESSABILITY
         LR    R4,R1              SAVE PARM
         USING CMB,R4
         CLC   CMBMID(8),=C'$HASP165' IS IT A NOTIFY MESSAGE
         BNE   RETURN0            NO...JUST RETURN
         MVC   CMBTIME+12(1),CMBTIME+9 JOB TO J
         MVI   CMBTIME+11,C' '    MOVE IN BLANK
         MVC   CMBTIME+6(5),CMBTIME SHIFT TIME
         MVI   CMBTIME+5,C' '     MOVE IN BLANK
         TIME  DEC                GET TIME AND DATE
         ST    R1,PCEUSER0 STORE  DATE FOR UNPACK
         UNPK  CMBTIME(5),PCEUSER0+1(3) DATE TO RECORD
RETURN0  SR    R15,R15            SET CONDITION CODE
RETURN   $STORE R15               SAVE CODE
         $RETURN RC=(R15)         AND RETURN
         LTORG ,
EXIT10   CSECT
         $MODEND ,
         END


    © Xephon 1989

