How many TSO users are logged on?
In a support function it is sometimes helpful to know the number of TSO users
from a particular group prefix as well as the total number of TSO users logged
on.  The following program will provide the total number of users logged on plus
several options:
¥    If ÔallÕ is supplied as the TSO user-id group, the actual TSO-id for every
currently logged-on user will be reported.
¥    Any other character string (limit of seven characters) will be interpreted
as the group prefix and ids that begin with the specified string only will be
reported.
¥    If no character string is supplied, only the total number of users will be
reported.
Note: this program was written to run in an MVS/XA environment - some of the
control blocks reside above the 16Mb line.
SAMPLE INVOCATIONS
     CALL 'YOUR.LOADLIB(TSOCNT01)' 'group'
where group is the TSO prefix you wish to have reported
     CALL 'YOUR.LOADLIB(TSOCNT01)' 'ALL'
     CALL 'YOUR.LOADLIB(TSOCNT01)'
THE ASSEMBLER SOURCE CODE
*** THIS PROGRAM LOCATES JOB NAME FOR ALL TSO USERS.  EXECUTION  ***
*   PARM (MAXIMUM LENGTH OF 7) CONTROLS THE TSO "GROUP" COUNT      *
*   ENQUIRY - ONLY THOSE ID'S MATCHING THE GROUP ARE PRINTED UNLESS*
*   THE PARM IS ALL IN WHICH CASE ALL TSO IDS ARE PRINTED.  BLANK  *
*** PARM CAUSES ONLY TOTAL COUNT TO BE PRINTED.                  ***
TSOCNT01 CSECT
         STM   R14,R12,12(R13)          SAVE REGISTERS
         LR    R12,R15                  LOAD BASE REGISTER
         LR    R7,R1                    PUT PARM ADDR IN SAFE REG
         USING TSOCNT01,R12
         ST    R13,SAVEA+4              CHAIN SAVE AREA
         LR    R9,R13
         LA    R13,SAVEA
         ST    R13,8(R9)
ML00200  L     R4,0(R7)                 GET ADDR OF PARM LENGTH
         LH    R3,0(R4)                 GET PARM LENGTH
         LTR   R3,R3                    ANY EXECUTION PARM?
         BZ    ML00300                  NO
         BCTR  R3,R0                    DECREMENT LENGTH FOR EX USE
         CH    R3,HSIX                  WAS PARM > 7
         BNH   ML00210                  NO
         LH    R3,HSIX                  YES, USE 7
         TPUT  ERRMSG1,L'ERRMSG1
ML00210  OI    SWPARM,HIVALUE           YES, SET SWITCH
         LA    R4,2(,R4)                SKIP OVER PARM LENGTH
         ST    R4,R4SAV                 SAVE PARM ADDR
ML00300  L     R4,CVTPTR
         USING CVT,R4
         L     R5,CVTASVT               GET ASVT ADDR
         USING ASVT,R5
         LA    R9,ASVTENTY
         L     R6,ASVTENTY              GET ASID ENTRY ADDR
         ST    R6,R6SAV
         USING ASCB,R6
         L     R8,ASCBOUCB              GET ADDR OF OUCB
         L     R4,R4SAV                 RELOAD PARM ADDR
AS00010  TM    R6SAV,ASVTAVAL           THIS ASID AVAILABLE?
         BO    AS00020                  YES, SKIP IT THEN
         L     R7,ASCBJBNI              NO, IN USE SO CONTINUE
         LTR   R7,R7                    INITIATED TASK?
         BNZ   AS00030                  YES, IGNORE
AS00012  L     R7,ASCBJBNS
         LTR   R7,R7                    NAME FIELD OK?
         BZ    AS00030                  NO
         CLC   0(4,R7),LITINIT          INITIATOR?
         BE    AS00030                  YES, IGNORE
         L     R15,P31ADDR              RESET TO 31 BIT MODE AND
         BASSM R14,R15                  CHECK OUCB FIELDS
         B     AS00100
AS00020  CLC   R6SAV+1(3),FZERO         END OF ASVT?
         BE    ML00999                  YES, BYE
AS00030  LA    R9,4(,R9)                PROCESS NEXT ASCB
         L     R6,0(,R9)
         ST    R6,R6SAV
         L     R8,ASCBOUCB              GET ADDR OF OUCB
         B     AS00010
AS00100  CLI   SWTSO,HIVALUE            ASCB A TSO USER?
         BNE   AS00030                  NO, PROCESS NEXT ASCB
AS00110  CLI   SWPARM,HIVALUE           PARM FOUND?
         BNE   AS00120                  NO, SKIP TSO ID PRINT
         CLC   0(3,R4),LITALL           ALL TSO ID WANTED?
         BE    AS00115                  YES, PRINT TSO ID
         EX    R3,CHKID                 TSO ID MATCH PARM?
         BNE   AS00130                  NO, PROCESS NEXT ASCB
AS00115  TPUT  0(R7),EQ7                PRINT OUT NAME FIELD
         CLC   0(3,R4),LITALL           ALL TSO ID WANTED?
         BE    AS00130                  YES, SKIP COUNTER FOR GROUP
AS00120  AP    CTRGRP,PONE              INCREMENT GROUP COUNTER
AS00130  MVI   SWTSO,LOVALUE            RESET TSO SWITCH
         B     AS00030
CHKID    CLC   0(0,R4),0(R7)            NAME FIELD = PARM?
MVCPARM  MVC   OUTDATA+17(0),0(R4)      MOVE PARM TO OUTPUT
*  RETURN TO SYSTEM
ML00999  CLI   SWPARM,HIVALUE
         BNE   ML09000
         CLC   0(3,R4),LITALL           ALL TSO ID WANTED?
         BE    ML09000
         MVC   OUTDATA+1(17),MSGCNT1    PRINT MSG FOR GROUP COUNT
         MVC   OUTDATA+26(7),EDMASK1    GROUP COUNT
         ED    OUTDATA+26(7),CTRGRP
         EX    R3,MVCPARM               MOVE GROUP TO PRINT
         TPUT  OUTLINE,L'OUTLINE
ML09000  CLEAR OUTLINE
         MVC   OUTDATA+1(16),MSGCNT2    PRINT TOTAL TSO COUNT MSG
         MVC   OUTDATA+17(7),EDMASK1
         ED    OUTDATA+17(7),CTRTOT
         TPUT  OUTLINE,L'OUTLINE
         L     R13,4(R13)               RESTORE REGISTERS
         LM    R14,R12,12(R13)
         XR    R15,R15                  LOAD ZERO RETURN CODE
         BR    R14                      RETURN
* WORK CONSTANTS
SWPARM   DC    XL1'00'
LITALL   DC    CL3'ALL'
LITINIT  DC    CL4'INIT'
FZERO    DC    F'0'
P31ADDR  DC    A(X'80000000'+PROC31)
SAVEA    DS    18F
R4SAV    DS    F
R6SAV    DS    F
SWTSO    DS    CL1
ERRPRNT  DS    CL1
CTRGRP   DC    PL3'0'                   GROUP ID COUNTER
CTRTOT   DC    PL3'0'                   TOTAL TSO ID COUNTER
PONE     DC    PL1'1'
EDMASK1  DC    XL7'4020206B202120'      EDIT MASK PRINT COUNTER
HSIX     DC    H'6'
* MESSAGES
ERRMSG1  DC    CL36'GROUP TOO LONG, TRUNCATED AT 7 BYTES'
MSGCNT1  DC    CL16'TOTAL COUNT FOR '
MSGCNT2  DC    CL16'TOTAL TSO USERS '
* REPORT LINE DEFINITIONS
OUTLINE  DS    CL40                     OUTPUT REPORT LINE
         ORG   OUTLINE
         DC    CL1' '
OUTDATA  DS    CL39                     GENERAL DATA AREA
         ORG
EQ7      EQU   X'07'
BLNK     EQU   C' '
HIVALUE  EQU   X'FF'
LOVALUE  EQU   X'00'
         CVT   DSECT=YES,LIST=NO
         IHAASVT DSECT=YES,LIST=NO
         IHAASCB DSECT=YES,LIST=NO
         IHADCB DSORG=PS,DEVD=DA
* 31 BIT MODE SUBROUTINE FOR OUCB CONTROL BLOCK                    *
* IS THIS A LOGGED ON USER?                                        *
PROC31   CSECT
PROC31   AMODE 31
PROC31   RMODE ANY
         LR    R11,R15
         USING PROC31,R11
         USING OUCB,R8
         TM    OUCBYFL,OUCBLOG          LOGGED ON USER?
         BNO   PR31010                  NO, EXIT
         MVI   SWTSO,HIVALUE            YES, SET SWITCH
         AP    CTRTOT,PONE              INCREMENT COUNTER
PR31010  BSM   R0,R14                   RETURN
         IRAOUCB
         END


Harold Dunlap
Senior Technical Analyst
Hudson's Bay Company (Canada) © Harold Dunlap 1988

































