Tracking DASD growth

SAMPLE EXECUTION JCL

//*   Parm options are as follows;
//*             VL =  Volumes to track, defaults to all, * for mask
//*             SG =  Storage group to track, defaults to all
//*             GA =  KB growth to report, smaller values on the
//*                   database will be ignored in report.  Numeric
//*                   five digit value with leading zeros.  Used only
//*                   in conjunction with the DONLY parm.
//*          DONLY =  Detail report only, don't refresh database
//*          TONLY =  Trend report only, don't refresh database
//*          NONLY =  Report only new datasets, used with DONLY
//*             DT =  Date from which DONLY or TONLY reports should
//*                   begin, format is YYMMDD
//*   Default database refresh processing requires no parameters.
//STEP1     EXEC PGM=DASDGROW,PARM='DONLY,DT=940304,GA=27000'
//OLDFILE   DD   DSN=dasd.tracking.dataset(0),DISP=SHR
//NEWFILE   DD   DSN=dasd.tracking.dataset(+1),DISP=(,CATLG,DELETE),
//          SPACE=(CYL,(10,1))
//DETAIL    DD   SYSOUT=*
//TRENDS    DD   SYSOUT=*
//WORKFILE  DD   DSN=&&WORKFILE,DISP=(,DELETE),
//          LRECL=54,RECFM=FB,DSORG=PS,SPACE=(CYL,(5,1))
//DCOLLECT  DD   DSN=&&DCOLLECT,DISP=(,DELETE),
//          LRECL=264,RECFM=VB,DSORG=PS,SPACE=(CYL,(10,5))
//SYSIN     DD   UNIT=VIO,SPACE=(CYL,(1,1))
//SYSPRINT  DD   SYSOUT=*
//SYSOUT    DD   SYSOUT=*
//SORTIN    DD   UNIT=VIO,SPACE=(CYL,(5,1))
//SORTOUT   DD   UNIT=VIO,SPACE=(CYL,(5,1))

DASDGROW SOURCE CODE

DASDGROW  CSECT
          STM   R14,R12,12(R13)        SAVE REGS
          LR    R10,R15                SAVE ENTRY ADDRESS
          LA    R12,2048               SET UP
          LA    R12,2048(R10,R12)        SECOND BASE REG
          USING DASDGROW,R10,R12       ESTABLISH ADDRESSABILITY
          LA    R4,REGSAVE             LOAD REGSAVE ADDRESS
          ST    R4,8(R13)              STORE OUR REGSAVE ADDRESS SAVE
          ST    R13,4(R4)              STORE CALLING REGSAVE ADDRESS
          LR    R13,R4                 LOAD OUR REGSAVE
          L     R4,0(R1)               GET PARM LENGTH ADDRESS
          LH    R5,0(R4)               LENGTH OF PARM DATA
          LTR   R5,R5                  IS IT ZERO?
          BZ    DASDG024               YES, NO PARM
          LA    R5,2(R5,R4)            END OF PARM DATA
          LA    R4,1(,R4)              POSITION FOR COMPARE
DASDG000  LA    R4,1(,R4)              POSITION FOR COMPARE
          CLC   0(3,R4),=C'SG='        STORAGE GROUP?
          BE    DASDG006               YES, SAVE IT
          CLC   0(3,R4),=C'DT='        DATE?
          BE    DASDG014               YES, SAVE IT
          CLC   0(3,R4),=C'GA='        GROWTH AMOUNT?
          BE    DASDG012               YES, SAVE IT
          CLC   0(3,R4),=C'VL='        VOLUME?
          BE    DASDG008               YES, SAVE IT
          CLC   0(5,R4),=C'DONLY'      DETAIL ONLY?
          BE    DASDG016               YES, SAVE IT
          CLC   0(5,R4),=C'TONLY'      TRENDS ONLY?
          BE    DASDG020               YES, GO DO IT
          CLC   0(5,R4),=C'NONLY'      REPORT NEW ONLY?
          BE    DASDG018               YES, GO DO IT
DASDG002  BCTR  R4,0                   SUBTRACT 1 FROM POSITION
          MVC   WTOBPRM1+32(6),0(R4)   MOVE PARM INTO MESSAGE
          WTO   MF=(E,WTOBPRM1)        WRITE BAD PARM MESSAGE
          ABEND 996                    ABEND AND QUIT
DASDG004  WTO   MF=(E,WTOBPRM2)        WRITE GA WITH DONLY MESSAGE
          ABEND 997                    ABEND AND QUIT
DASDG006  LA    R6,DCOLSTO2            PREPARE FOR STORGRP MOVE
          MVI   PRIMEFLG,C'1'          INDICATE THERE WAS A STORGRP
          MVC   DCOLSTO1,=C' STOG('    PRIME DCOLLECT
          MVI   DCOLSTO3,C')'
          LA    R4,3(R4)               POINT TO STORGRP CHARACTER
          B     DASDG010               GO MOVE IT
DASDG008  LA    R6,DCOLVOL2            PREPARE FOR VOLUME MOVE
          MVI   PRIMEFLG,C'1'          INDICATE THERE WAS A VOLUME
          MVC   DCOLVOL1,=C' VOL('     PRIME DCOLLECT
          MVI   DCOLVOL3,C')'
          LA    R4,3(R4)               POINT TO VOLUME CHARACTER
DASDG010  MVC   0(1,R6),0(R4)          MOVE CHARACTER
          LA    R4,1(R4)               NEXT CHARACTER
          CR    R4,R5                  AT END?
          BE    DASDG024               IF YES, GO PROCESS
          CLI   0(R4),C','             IS IT A COMMA?
          BE    DASDG000               IF YES, GET NEXT PARM
          LA    R6,1(R6)               NEXT DCOLLECT POSITION
          B     DASDG010               GO MOVE IT
DASDG012  LA    R4,3(R4)               POINT TO GROWTH AMOUNT
          TRT   0(5,R4),TRTNUM         IS IT NUMERIC?
          BC    7,DASDG002             IF NO, BAD PARM
          MVC   PARMGAMT(5),0(R4)      MOVE NUMBER TO SAVED GA
          MVI   GRAMTFLG,C'1'          INDICATE GROWTH AMOUNT PRESENT
          LA    R4,5(R4)               JUMP PAST GROWTH AMOUNT
          B     DASDG022               GO CHECK FOR PARM END
DASDG014  LA    R4,3(R4)               POINT TO DATE
          TRT   0(6,R4),TRTNUM         IS IT NUMERIC?
          BC    7,DASDG002             IF NO, BAD PARM
          MVC   PARMDATE(6),0(R4)      PUT DATE INTO SAVE AREA
          MVC   H1YR,PDYR              PUT YEAR IN HEADING
          MVC   H1MO,PDMO              PUT MONTH IN HEADING
          MVC   H1DY,PDDY              PUT DAY IN HEADING
          LA    R4,6(R4)               JUMP PAST DATE VALUE
          B     DASDG022               GO CHECK FOR PARM END
DASDG016  LA    R4,5(R4)               JUMP PAST DONLY PARM
          MVI   DONLYFLG,C'1'          INDICATE DETAIL ONLY REQUEST
          B     DASDG022               GO CHECK FOR PARM END
DASDG018  LA    R4,5(R4)               JUMP PAST NONLY PARM
          MVI   NONLYFLG,C'1'          INDICATE NEW ONLY INFO REQUEST
          MVC   H1NOTE,=C'(NEW ONLY)'  SET UP PROPER HEADING
          B     DASDG022               GO CHECK FOR PARM END
DASDG020  LA    R4,5(R4)               JUMP PAST TONLY PARM
          MVI   TONLYFLG,C'1'          INDICATE TRENDS ONLY REQUEST
DASDG022  CR    R4,R5                  AT END?
          BE    DASDG024               IF YES, GO PROCESS
          B     DASDG000               GO CHECK NEXT PARM
DASDG024  OPEN  (OLDFILE,,NEWFILE,(OUTPUT))
          OPEN  (DETAIL,(OUTPUT),TRENDS,(OUTPUT))
          CLI   TONLYFLG,C'1'          WAS THIS A TREND ONLY REQUEST?
          BE    DASDG122               IF YES, GO PROCESS
          PACK  GAMT,PARMGAMT          PACK THE GROWTH AMOUNT PARM
          MVC   H1AMT,EDTPTRN1         SET UP HEADING GA EDIT
          ED    H1AMT,GAMT             EDIT GA INTO HEADING
          CLI   DONLYFLG,C'1'          WAS THIS A DETAIL ONLY REQUEST?
          BE    DASDG111               IF YES, GO PROCESS
          CLI   GRAMTFLG,C'1'          WAS GA CODED WITHOUT DONLY?
          BE    DASDG004               IF YES, DON'T ALLOW IT
          CLI   H1YR,C' '              WAS THERE A PARM DATE?
          BNE   DASDG026               IF YES, DATE WILL BE IN HEADING
          MVC   H1YR(8),=C'LAST RUN'   INDICATE SINCE LAST RUN PROCESS
DASDG026  CLI   PRIMEFLG,C'1'          WAS THERE A STORGRP OR VOLUME?
          BE    DASDG028               IF YES, CONTINUE
          MVC   DCOLVOL1,=C' VOL('     IF NO, NEED TO PRIME DCOLLECT
          MVC   DCOLVOL2,=C'*     '       FOR ALL
          MVI   DCOLVOL3,C')'                VOLUMES
DASDG028  L     R1,16                  GET CVT ADDRESS
          MVC   WKFLD0+8(4),57(R1)     MOVE CVT DATE TO WKFLD0 AREA
          ZAP   WKFLD0(8),=P'0'        ZERO WKFLD0 AREA
          MVO   WKFLD0+6(2),WKFLD0+8(1)  MOVE YEAR SEGMENT
          UNPK  SAVEDATE+4(2),WKFLD0+6(2) UNPACK INTO OUTPUT AREA
          LA    R15,MNTHTBL           LOAD MONTH TABLE ADDRESS
          CVB   R1,WKFLD0             CONVERT TO BINARY
          SLL   R1,30                 SHIFT ALL BUT LAST TWO
          LTR   R1,R1                 TEST RESIDUAL
          BNZ   DASDG030              NOT 0, SO NOT LEAP YEAR
          LA    R15,MNTHTBLL          LOAD LEAP YEAR MONTH TABLE
DASDG030  LR    R14,R15               SAVE TABLE WE'RE USING
          ZAP   WKFLD0(8),WKFLD0+9(2) MOVE DAY SEGMENT
          CVB   R0,WKFLD0             CONVERT TO BINARY
          SR    R1,R1                 CLEAR REG 1
DASDG032  IC    R1,0(R15)             LOAD VALUE FROM TABLE
          LA    R15,1(R15)            INDEX TABLE POINTER
          SR    R0,R1                 SUBTRACT FROM DAY VALUE
          BP    DASDG032              STILL POSITIVE, DO AGAIN
          AR    R0,R1                 ADD DAY VALUE BACK IN
          CVD   R0,WKFLD0             CONVERT DAY TO DECIMAL
          UNPK  SAVEDATE+2(2),WKFLD0+6(2) UNPACK DAY INTO OUTPUT
          SR    R15,R14               GET VALUE OF CURRENT INDEX
          CVD   R15,WKFLD0            CONVERT TO DECIMAL
          UNPK  SAVEDATE(2),WKFLD0+6(2) UNPACK MONTH INTO OUTPUT
          PACK  COMPYRP(2),SAVEDATE+4(2) PACK YEAR FOR COMPARE
          PACK  COMPMOP(2),SAVEDATE(2) PACK MONTH FOR COMPARE
          CP    COMPMOP,=P'01'        COMPARE MONTH TO 1
          BE    DASDG034              IF JANUARY, PREVIOUS IS 12
          SP    COMPMOP,=P'01'        SUBTRACT 1 TO GET PREVIOUS
          B     DASDG036              CONTINUE ON
DASDG034  SP    COMPYRP,=P'01'        PREVIOUS MONTH IS DECEMBER
          ZAP   COMPMOP,=P'12'          OF PREVIOUS YEAR
DASDG036  UNPK  COMPYR(2),COMPYRP     UNPACK THE COMPARE YEAR
          UNPK  COMPMO(2),COMPMOP     UNPACK THE COMPARE MONTH
          OI    COMPDATE+1,X'F0'      ENSURE YEAR NUMERIC
          OI    COMPDATE+3,X'F0'      ENSURE MONTH NUMERIC
          OI    SAVEDATE+1,X'F0'      ENSURE MONTH NUMERIC
          OI    SAVEDATE+3,X'F0'      ENSURE DAY NUMERIC
          OI    SAVEDATE+5,X'F0'      ENSURE YEAR NUMERIC
          MVC   SAVEYR(2),SAVEDATE+4  SAVE YEAR FOR SHIFT
          MVC   SAVEDATE+4(2),SAVEDATE+2 SHIFT DAY TO LAST POSITION
          MVC   COMPDY(2),SAVEDATE+2  MAKE COMPARE DAY SAME AS CURRENT
          MVC   SAVEDATE+2(2),SAVEDATE SHIFT MONTH TO SECOND POSITION
          MVC   SAVEDATE(2),SAVEYR    PUT YEAR IN FIRST POSITION
          MVI   SAVEDSN,C' '          BLANK OUT FIRST CHARACTER OF DSN
          OPEN  (SYSIN,(OUTPUT))
          PUT   SYSIN,DCOLCARD        PUT DCOLLECT OUT TO IDCAMS
          CLOSE SYSIN                 CLOSE SYSIN SO IDCAMS CAN READ
          LA    R1,0                  PREPARE FOR IDCAMS LINK
          LINK  EP=IDCAMS             CALL IDCAMS TO DO DCOLLECT
          LTR   R15,R15               ZERO RC FROM IDCAMS DCOLLECT?
          BZ    DASDG038              IF YES, CONTINUE
          WTO   '*  NON ZERO DATA DCOLLECT RC, REVIEW SYSPRINT *'
          ABEND 998                   BAD DCOLLECT, ABEND AND QUIT
DASDG038  OPEN  (DCOLLECT,,SORTIN,(OUTPUT))
          SR    R4,R4                 ZERO REG 4
DASDG040  GET   DCOLLECT              READ DATA FROM DCOLLECT
          LA    R9,4(R1)              POINT TO RECORD
          USING IDCDOUT,R9            ESTABLISH DCOLLECT ADDRESSABILITY
          CLI   DCURCTYP,C'D'         DATASET RECORD?
          BNE   DASDG040              IF NO, DON'T WANT IT
          TM    DCDFLAG1,DCDTEMP      IS IT TEMPORARY?
          BO    DASDG040              IF YES, DON'T WANT IT
          CLC   DCDVOLSR,DCDDSSER     MULTI-VOLUME, NOT FIRST VOLUME?
          BNE   DASDG040              IF YES, DON'T WANT IT
          CLC   DCDERROR,=X'00'       ANY ERRORS?
          BE    DASDG042              IF NO, OK TO PROCESS
          WTO   ' DCOLLECT ERROR FOR DATASET:' INDICATE ERRORS EXIST
          MVC   WTOLIST+6(44),DCDDSNAM MOVE IN DATASET NAME
          WTO   MF=(E,WTOLIST)        WRITE DATASET IN ERROR
          B     DASDG040              GO PROCESS NEXT RECORD
DASDG042  MVC   DRDSN,DCDDSNAM        SAVE DSNAME IN DATA RECORD
          MVC   DRCUSIZE,DCDALLSP     SAVE ALLOCATED SPACE
          A     R4,DCDALLSP           ADD ALLOCATED SPACE TO TOTAL
          MVI   DRFLAG,C'1'           ASSUME THIS IS A GDG
          AP    DSNCOUNT,=P'1'        ADD 1 TO DSN COUNT
          TM    DCDFLAG1,DCDGDS       IS THIS A GDG?
          BO    DASDG044              IF YES, FLAG ALREADY SET
          MVI   DRFLAG,C'0'           THIS IS NOT A GDG
DASDG044  PUT   SORTIN,DATARCRD       PUT DATA RECORD OUT TO SORT
          B     DASDG040              GO PROCESS NEXT DCOLLECT RECORD
DASDG046  ST    R4,SAVEZAMT           STORE TOTAL ALLOCATED SPACE
          MVC   ZZZZAMT,SAVEZAMT      PUT TOTAL ALLOC IN DATA RECORD
          MVC   ZZZZCNT,DSNCOUNT      PUT TOTAL COUNT IN DATA RECORD
          MVC   ZZZZDATE,SAVEDATE     PUT CURRENT DATE IN DATA RECORD
          PUT   SORTIN,ZZZZREC        PUT TOTALS RECORD OUT TO SORT
          CLOSE (DCOLLECT,,SORTIN)    CLOSE SORTIN SO SORT CAN READ
          LA    R1,POINTER            PREPARE FOR SORT
          LINK  EP=SORT               CALL SORT
          LTR   R15,R15               WAS SORT RC ZERO?
          BZ    DASDG048              IF YES, CONTINUE ON
          WTO   '* NON ZERO RC FROM SORT, REVIEW SYSOUT *'
          ABEND 999                   ABEND AND QUIT
DASDG048  OPEN  (SORTOUT,,WORKFILE,(OUTPUT))
DASDG050  GET   SORTOUT               READ SORTED RECORD
          LR    R2,R1                 SAVE REG 1
          CLC   0(6,R2),=C'ZZZZZZ'    WAS THIS A TOTALS RECORD?
          BE    DASDG053              IF YES, PROCESS AS SUCH
          CLI   48(R2),C'1'           IS THIS A GDG?
          BE    DASDG054              IF YES, PROCESS AS SUCH
          CLI   GDGFLAG,C'0'          WAS A GDG BEING PROCESS?
          BE    DASDG052              IF NOT, CONTINUE
          BAL   R6,DASDG065           GO WRITE HELD INFO TO DATA RECORD
          MVI   GDGFLAG,C'0'          RESET PROCESSING GDG FLAG
DASDG052  MVC   DRCUSIZE(4),44(R2)    PUT DSN SIZE IN DATA RECORD
          MVC   DRAGSIZE,DRCUSIZE     MAKE AFTER GROWTH = CURRENT
          MVC   DRPGSIZE,=X'00000000' INITIALIZE PRE-GROWTH TO 0
          MVC   DRFNDDTE,SAVEDATE     PUT CURRENT DATE IN DATA RECORD
          MVC   DRDSN(44),0(R2)       PUT DSNAME IN DATA RECORD
          MVI   SAVEDSN,C' '          CLEAR FIRST CHARACTER OF SAVE DSN
          PUT   WORKFILE,DATARCRD     PUT RECORD OUT TO SORT
          B     DASDG050              GO PROCESS NEXT SORTED RECORD
DASDG053  MVC   DATARCRD(72),0(R2)    MOVE TOTALS RECORD TO DATA RECORD
          PUT   WORKFILE,DATARCRD     PUT TOTALS RECORD OUT TO WORK
          B     DASDG050              GO PROCESS NEXT SORTED RECORD
DASDG054  MVI   GDGFLAG,C'1'          INDICATE A GDG IS BEING PROCESSED
          MVI   DRDSN,C' '            CLEAR DATA RECORD DSN
          MVC   DRDSN+1(L'DRDSN-1),DRDSN
          LA    R5,44                 MAX DSNAME LENGTH IS 44
          SR    R7,R7                 CLEAR REG 7
          LR    R8,R2                 POINT TO BEGINNING OF DSNAME
DASDG056  CLI   0(R8),C' '            IS THIS A BLANK?
          BE    DASDG060              IF YES, END OF DSNAME
          LA    R7,1(,R7)             BUMP COUNT
          CR    R7,R5                 AT MAX LENGTH?
          BE    DASDG060              IF YES, GO PROCESS
          LA    R8,1(,R8)             BUMP TO NEXT CHARACTER
          B     DASDG056              GO CHECK THIS CHARACTER
DASDG058  MVC   DRDSN(0),0(R2)        MOVE DSN WITHOUT G0000V00
DASDG060  S     R8,=F'7'              SUBTRACT 7 FOR * MOVE
          MVI   0(R8),C'*'            MOVE A * IN PLACE OF G0000V00
          S     R7,=F'7'              SUBTRACT 7 FOR BASE MOVE
          EX    R7,DASDG058           MOVE BASE SEGMENT OF DSNAME
          CLC   DRDSN,SAVEDSN         IS THIS SAME BASE?
          BNE   DASDG062              IF NOT, THIS IS A NEW GDG
          A     R3,44(R2)             ADD SIZE TO BASE SIZE
          B     DASDG050              GO PROCESS NEXT RECORD
DASDG062  CLI   SAVEDSN,C' '          IS SAVE DSN BLANK?
          BE    DASDG064              IF NOT, A GDG WAS BEING PROCESSED
          MVC   HOLDDSN,DRDSN         HOLD CURRENT DSNAME
          BAL   R6,DASDG065           GO WRITE HELD INFO TO DATA RECORD
          MVC   DRDSN,HOLDDSN         RESTORE CURRENT DSNAME
DASDG064  MVC   SAVEDSN,DRDSN         SAVE CURRENT DSNAME
          SR    R3,R3                 ZERO REG 3
          A     R3,44(R2)             ADD GDG SIZE TO BASE
          B     DASDG050              GO PROCESS NEXT RECORD
DASDG065  MVC   DRDSN,SAVEDSN         MOVE PREVIOUS DSNAME TO CURRENT
          ST    R3,SAVESIZE           STORE PREVIOUS DSN SIZE
          MVC   DRCUSIZE,SAVESIZE     MOVE SIZE TO DATA RECORD
          MVC   DRAGSIZE,DRCUSIZE     MAKE AFTER GROWTH = CURRENT
          MVC   DRPGSIZE,=X'00000000' INITIALIZE PRE-GROWTH TO 0
          MVC   DRFNDDTE,SAVEDATE     PUT CURRENT DATE INTO DATA RECORD
          PUT   WORKFILE,DATARCRD     PUT DATA RECORD OUT TO WORK
          BR    R6                    GO BACK TO WHERE WE CAME FROM
DASDG066  CLOSE (SORTOUT,,WORKFILE)   DONE PROCESSING SORTED DATA
          OPEN  WORKFILE              PREPARE TO REPROCESS WORK FILE
          BAL   R8,DASDG108           GO PRINT HEADING
DASDG068  GET   OLDFILE               GET RECORD FROM OLD DATA FILE
          LR    R2,R1                 SAVE REG 1
DASDG070  GET   WORKFILE              GET RECORD FROM CURRENT DATA FILE
          LR    R7,R1                 SAVE REG 1
          MVI   FIRSTFLG,C'1'         INDICATE NO LONGER FIRST TIME
DASDG072  CLC   0(44,R7),0(R2)        ARE THE OLD AND CURRENT DSNAME =
          BE    DASDG082              IF YES, GO PROCESS
          BL    DASDG080              IF CURRENT IS LESS, GO PROCESS
          MVC   DATARCRD(72),0(R2)    PUT OLD DATA INTO DATA RECORD
          CLC   DRDSN(6),=C'ZZZZZZ'   IS THIS A TOTALS RECORD?
          BNE   DASDG074              IF NOT, CONTINUE
          BAL   R5,DASDG088           GO PROCESS A TOTALS RECORD
          B     DASDG076              CONTINUE
DASDG074  CLC   DRFNDDTE,COMPDATE     COMPARE DATE TO PARM DATE
          BL    DASDG078              IF LESS, DON'T KEEP ON FILE
DASDG076  PUT   NEWFILE,DATARCRD      PUT DATA RECORD OUT TO FILE
DASDG078  GET   OLDFILE               GET NEXT RECORD FROM OLD FILE
          LR    R2,R1                 SAVE REG 1
          B     DASDG072              GO COMPARE TO CURRENT
DASDG080  MVC   DLPGSIZE(18),BLANKS   PRE-GROWTH NOT REPORTED ON NEW
          MVC   DLGRDATE(6),BLANKS    GROWTH DATE NOT REPORTED ON NEW
          MVC   DLGRAMT(18),BLANKS    GROWTH AMT NOT REPORTED ON NEW
          BAL   R6,DASDG092           GO PROCESS FIRST TIME DSN
          B     DASDG070              GET NEXT INFO FROM CURRENT WORK
DASDG082  MVC   DATARCRD(72),0(R2)    SAVE OLD INFO TO DATA RECORD
          CLC   DRDSN(6),=C'ZZZZZZ'   IS THIS A TOTALS RECORD?
          BNE   DASDG084              IF NOT, DON'T PROCESS AS ONE
          BAL   R5,DASDG088           GO PROCESS TOTALS INFO
          B     DASDG086              GO PUT TOTALS INFO OUT TO FILE
DASDG084  CLI   TONLYFLG,C'1'         WERE TRENDS ONLY REQUESTED?
          BE    DASDG086              IF YES, DON'T REPORT DETAIL
          L     R4,44(R7)             ADDRESS OF CURRENT SIZE
          CVD   R4,WKFLD4             CONVERT CURRENT SIZE TO DECIMAL
          MVC   DRCUSIZE,44(R7)       MOVE CURRENT SIZE TO DATA RECORD
          MVC   DLCUSIZE,EDTPTRN3     PREPARE TO EDIT CURRENT SIZE
          ED    DLCUSIZE,WKFLD4       EDIT CURRENT SIZE INTO REPORT
          L     R4,54(R2)             ADDRESS OF AFTER GROWTH SIZE
          CVD   R4,WKFLD1             CONVERT OLD SIZE TO DECIMAL
          SP    WKFLD5,WKFLD3         SUBTRACT OLD SIZE FROM CURRENT
          CP    WKFLD5,GAMT           COMPARE DIFFERENCE TO GROWTH AMT
          BL    DASDG086              IF LESS, DON'T REPORT IT
          MVC   DRAGSIZE,44(R7)       MOVE CURRENT SIZE TO AFTER GROWTH
          MVC   DRPGSIZE,54(R2)       SAVE OLD AFTER GROWTH INTO PRE
          MVC   DLPGSIZE,EDTPTRN3     PREPARE TO EDIT PRE-GROWTH SIZE
          ED    DLPGSIZE,WKFLD1       EDIT PRE-GROWTH INTO REPORT
          MVC   DLGRAMT,EDTPTRN3      PREPARE TO EDIT GROWTH AMOUNT
          ED    DLGRAMT,WKFLD4        EDIT GROWTH AMOUNT INTO REPORT
          MVC   DRGRAMT,WKFLD5        MOVE GROWTH AMOUNT TO DATA RECORD
          MVC   DRGRDATE,SAVEDATE     MOVE GROWTH DATE TO DATA RECORD
          MVC   DLGRDATE,DRGRDATE     MOVE GROWTH DATE TO DETAIL LINE
          CLI   NONLYFLG,C'1'         ARE WE REPORTING NEW ONLY?
          BE    DASDG086              IF YES, DON'T REPORT THIS GROWTH
          BAL   R6,DASDG095           GO PROCESS DETAIL REPORT RECORD
DASDG086  PUT   NEWFILE,DATARCRD      PUT DATA RECORD TO NEW FILE
          B     DASDG068              GO PROCESS NEXT OLD/CURRENT
DASDG088  BAL   R8,DASDG118           GO PRINT TREND HEADINGS
          CLC   DRDSN+6(6),COMPDATE   IS THIS RECORD TOO OLD TO REPORT?
          BL    DASDG090              IF YES, SKIP PROCESSING
          MVC   TRENDDTE(6),DRDSN+6   MOVE DATE TO TREND REPORT
          MVC   WKFLD2(4),DRAGSIZE    MOVE TOTAL SPACE TO WORK FIELD
          L     R8,WKFLD2             LOAD TOTAL SPACE
          CVD   R8,WKFLD1             CONVERT TOTAL SPACE TO DECIMAL
          MVC   TRENDAMT,EDTPTRN4     PREPARE TO EDIT TOTAL SPACE
          ED    TRENDAMT,WKFLD1       EDIT TOTAL SPACE IN TREND REPORT
          MVC   TRENDCNT,EDTPTRN2     PREPARE TO EDIT DSN COUNT
          MVC   WKFLD2(4),DRCUSIZE    MOVE DSN COUNT TO WORK FIELD
          ED    TRENDCNT,WKFLD2       EDIT DSN COUNT INTO TREND REPORT
          PUT   TRENDS,TRENDLNE       PUT LINE OUT TO TRENDS REPORT
          AP    TLINECNT,=P'1'        ADD 1 TO LINE COUNT
DASDG090  BR    R5                    RETURN TO WHERE WE CAME FROM
DASDG092  MVC   DATARCRD(72),0(R7)    SAVE FIRST TIME DSN INFO
          PUT   NEWFILE,DATARCRD      PUT INFO OUT TO NEW FILE
          CLC   DRDSN(6),=C'ZZZZZZ'   WAS THIS THE TOTALS RECORD?
          BNE   DASDG094              IF NOT, SKIP THAT PROCESS
          BAL   R5,DASDG088           GO PROCESS TOTALS RECORD
          BR    R6                    RETURN TO WHERE WE CAME FROM
DASDG094  L     R4,DRCUSIZE           LOAD CURRENT SIZE
          CVD   R4,WKFLD1             CONVERT SIZE TO DECIMAL
          CP    WKFLD3,GAMT           COMPARE SIZE TO GROWTH AMOUNT
          BL    DASDG098              IF LESS, DON'T REPORT IT
          MVC   DLCUSIZE,EDTPTRN3     PREPARE TO EDIT CURRENT SIZE
          ED    DLCUSIZE,WKFLD1       EDIT SIZE INTO DETAIL LINE
DASDG095  MVC   DLFNDDTE,DRFNDDTE     MOVE DATE FOUND TO DETAIL LINE
DASDG096  MVC   DLDSN,DRDSN           MOVE DSNAME TO DETAIL LINE
          BAL   R8,DASDG108           GO CHECK HEADING REQUIREMENT
          PUT   DETAIL,DETLINE        PUT DETAIL LINE OUT TO REPORT
          AP    DLINECNT,=P'1'        ADD 1 TO LINE COUNT
DASDG098  BR    R6                    RETURN TO WHERE WE CAME FROM
DASDG100  MVI   WRKFFLAG,C'1'         INDICATE WORK FILE IS AT END
          CLI   OLDFFLAG,C'1'         IS OLD FILE AT END?
          BE    DASDG126              IF YES, NOTHING LEFT TO PROCESS
          MVC   DATARCRD(72),0(R2)    MOVE OLD FILE INFO TO DATA RECORD
          PUT   NEWFILE,DATARCRD      PUT INFO OUT TO NEW FILE
DASDG102  GET   OLDFILE               READ NEXT OLD FILE RECORD
          MVC   DATARCRD(72),0(R1)    MOVE OLD FILE INFO TO DATA RECORD
          PUT   NEWFILE,DATARCRD      PUT INFO OUT TO NEW FILE
          B     DASDG102              PROCESS OLD FILE TILL EOF
DASDG104  CLI   TONLYFLG,C'1'         WAS THIS A TRENDS REQUEST?
          BE    DASDG128              IF YES, WE'RE DONE
          MVI   OLDFFLAG,C'1'         INDICATE END OF OLD FILE
          CLI   WRKFFLAG,C'1'         IS WORK FILE AT END?
          BE    DASDG126              IF YES, NOTHING LEFT TO PROCESS
          CLC   0(44,R7),0(R2)        DO OLD/CURRENT DSN MATCH?
          BE    DASDG106              IF YES, IT WAS PROCESSED
          CLI   FIRSTFLG,C'0'         IS THIS FIRST TIME THROUGH?
          BE    DASDG106              IF YES, DON'T WRITE ANYTHING YET
          BAL   R6,DASDG092           GO PROCESS FIRST TIME DSN INFO
DASDG106  GET   WORKFILE              GET NEXT WORK FILE RECORD
          LR    R7,R1                 SAVE REG 1
          BAL   R6,DASDG092           GO PROCESS FIRST TIME DSN INFO
          B     DASDG106              PROCESS WORK FILE TILL EOF
DASDG108  CP    LNELIMIT,DLINECNT     ARE WE READY YET FOR HEADINGS?
          BNE   DASDG110              IF NO, SKIP PRINTING HEADINGS
          PUT   DETAIL,HEADING1       PRINT HEADING1
          PUT   DETAIL,BLANKS         PRINT BLANK LINE
          PUT   DETAIL,HEADING2       PRINT HEADING2
          PUT   DETAIL,HEADING3       PRINT HEADING3
          ZAP   DLINECNT,=P'5'        STARTING AT LINE 5
DASDG110  BR    R8                    RETURN TO WHERE WE CAME FROM
DASDG111  MVI   WRKFFLAG,C'1'         THERE IS NO WORK FILE WITH DONLY
          BAL   R8,DASDG108           GO PRINT DETAIL HEADINGS
DASDG112  GET   OLDFILE               GET A RECORD FROM OLD FILE
          LR    R7,R1                 SAVE REG 1
          MVC   DATARCRD(72),0(R7)    MOVE INFO TO DATA RECORD
          PUT   NEWFILE,DATARCRD      PUT DATA RECORD TO NEW FILE
          CLC   0(6,R7),=C'ZZZZZZ'    IS THIS A TOTALS RECORD?
          BE    DASDG112              IF YES, DON'T PROCESS IT
          MVI   GRWCKFLG,C'0'         RESET GROWTH AMT CHECK FLAG
          CLC   PARMDATE,DRFNDDTE     COMPARE DATE FOUND TO PARM DATE
          BNH   DASDG113              IF DATE FOUND IS >=, PROCESS
          CLI   NONLYFLG,C'1'         IS THIS A NEW ONLY REQUEST?
          BE    DASDG112              IF YES, DON'T CHECK GROWTH DATE
          MVI   GRWCKFLG,C'1'         WE NEED GROWTH AMT CHECKED
          CLC   PARMDATE,DRGRDATE     COMPARE GROWTH DATE WITH PARM
          BNH   DASDG113              IF GROWTH IS >=, PROCESS
          B     DASDG112              MOVE ON TO NEXT RECORD
DASDG113  L     R8,DRCUSIZE           LOAD CURRENT SIZE
          CVD   R8,WKFLD1             CONVERT CURRENT SIZE TO DECIMAL
          CP    WKFLD3,GAMT           COMPARE CURRENT TO GROWTH AMT
          BL    DASDG114              IF LESS, DON'T PROCESS
          B     DASDG115              GROWTH IS ENOUGH TO PROCESS
DASDG114  CLI   NONLYFLG,C'1'         WAS THIS A NEW ONLY REQUEST?
          BE    DASDG112              IF YES, DON'T PROCESS
          MVI   GRWCKFLG,C'1'         RECORD NEEDS GROWTH AMT CHECK
DASDG115  MVC   DLCUSIZE,EDTPTRN3     PREPARE TO EDIT CURRENT SIZE
          ED    DLCUSIZE,WKFLD1       EDIT CURRENT SIZE INTO DETAIL
          MVC   DLFNDDTE,DRFNDDTE     MOVE DATE FOUND INTO DETAIL
          MVC   WKFLD2(4),DRPGSIZE    MOVE PRE-GROWTH SIZE TO WORK
          L     R8,WKFLD2             LOAD PRE-GROWTH SIZE
          CVD   R8,WKFLD1             CONVERT PRE-GROWTH TO DECIMAL
          MVC   DLPGSIZE,EDTPTRN3     PREPARE TO EDIT PRE-GROWTH SIZE
          ED    DLPGSIZE,WKFLD1       EDIT PRE-GROWTH INTO DETAIL
          MVC   DLGRDATE(6),BLANKS    ASSUME NO GROWTH DATE
          MVC   DLGRAMT(18),BLANKS    ASSUME NO GROWTH AMOUNT
          ZAP   WKFLD4,=P'0'          ZERO WORK FIELD
          MVC   WKFLD5(4),DRGRAMT     MOVE GROWTH AMOUNT TO WORK FIELD
          CLI   NONLYFLG,C'1'         IS THIS A NEW ONLY REQUEST?
          BE    DASDG116              IF YES, SKIP GROWTH AMT CHECK
          CLI   GRWCKFLG,C'0'         WAS GROWTH AMT CHECK SET?
          BE    DASDG116              IF NO, GO AHEAD AND PROCESS
          CP    WKFLD5,GAMT           COMPARE GROWTH AMT TO PARM AMT
          BL    DASDG112              IF LESS, DON'T PROCESS
DASDG116  MVC   DLGRAMT,EDTPTRN3      PREPARE TO EDIT GROWTH AMT
          ED    DLGRAMT,WKFLD4        EDIT GROWTH AMT INTO DETAIL
          MVC   DLGRDATE,DRGRDATE     MOVE GROWTH DATE TO DETAIL
DASDG117  BAL   R6,DASDG096           GO PRINT DETAIL LINE
          B     DASDG112              GO PROCESS NEXT RECORD
DASDG118  CP    LNELIMIT,TLINECNT     TREND HEADINGS REQUIRED?
          BNE   DASDG120              IF NOT, SKIP PRINTING THEM
          PUT   TRENDS,HEADING4       PRINT HEADING4
          PUT   TRENDS,BLANKS         PRINT BLANK LINE
          PUT   TRENDS,HEADING5       PRINT HEADING5
          PUT   TRENDS,HEADING6       PRINT HEADING6
          ZAP   TLINECNT,=P'5'        STARTING AT LINE 5
DASDG120  BR    R8                    RETURN TO WHERE WE CAME FROM
DASDG122  BAL   R8,DASDG118           GO CHECK FOR TREND HEADINGS
DASDG124  GET   OLDFILE               GET RECORD FROM OLD FILE
          LR    R7,R1                 SAVE REG 1
          MVC   DATARCRD(72),0(R7)    MOVE INFO TO DATA RECORD
          PUT   NEWFILE,DATARCRD      PUT DATA RECORD TO NEW FILE
          CLC   0(6,R7),=C'ZZZZZZ'    IS THIS A TOTALS RECORD?
          BNE   DASDG124              IF NO, GO GET NEXT ONE
          CLC   6(6,R7),PARMDATE      COMPARE DATE WITH PARM DATE
          BL    DASDG124              IF LESS, GO GET NEXT ONE
          MVC   TRENDDTE(6),6(R7)     MOVE DATE TO TREND LINE
          L     R8,54(R7)             LOAD TOTAL SPACE AMOUNT
          CVD   R8,WKFLD1             CONVERT TOTAL SPACE TO DECIMAL
          MVC   TRENDAMT,EDTPTRN4     PREPARE TO EDIT TOTAL SPACE
          ED    TRENDAMT,WKFLD1       EDIT TOTAL SPACE INTO TREND LINE
          MVC   TRENDCNT,EDTPTRN2     PREPARE TO EDIT DSN COUNT
          ED    TRENDCNT,44(R7)       EDIT DSN COUNT INTO TREND LINE
          BAL   R8,DASDG118           GO CHECK HEADING REQUIREMENTS
          PUT   TRENDS,TRENDLNE       PUT LINE OUT TO TRENDS REPORT
          AP    TLINECNT,=P'1'        ADD 1 TO LINE COUNT
          B     DASDG124              GO PROCESS NEXT RECORD
DASDG126  CLI   DONLYFLG,C'1'         WAS THIS A DETAIL ONLY REQUEST?
          BE    DASDG128              IF YES, NO TOTAL LINE TO PRINT
          MVC   TOTCOUNT,EDTPTRN2     PREPARE TO EDIT TOTAL DSN COUNT
          ED    TOTCOUNT,ZZZZCNT      EDIT TOTAL DSN COUNT IN DETAIL
          L     R4,SAVEZAMT           LOAD TOTAL ALLOCATED SPACE
          CVD   R4,WKFLD1             CONVERT TOTAL SPACE TO DECIMAL
          MVC   TOTSIZE,EDTPTRN4      PREPARE TO EDIT TOTAL SPACE
          ED    TOTSIZE,WKFLD1        EDIT TOTAL SPACE INTO DETAIL
          PUT   DETAIL,BLANKS         PRINT BLANK LINE
          PUT   DETAIL,TOTLINE        PRINT TOTAL LINE IN DETAIL RPT
          CLOSE WORKFILE              CLOSE WORK FILE
DASDG128  CLOSE (OLDFILE,,DETAIL,,NEWFILE,,TRENDS) CLOSE REST OF FILES
          L     R13,REGSAVE+4         RELOAD SAVE ADDRESS
          ST    R15,16(R13)           STORE RC
          LM    R14,R12,12(R13)       RELOAD REGS
          BR    R14                   PROCESSING COMPLETE
          LTORG
REGSAVE   DS    18F
EDTPTRN1  DC    X'4020206B202020'
EDTPTRN2  DC    X'40206B2020206B202020'
EDTPTRN3  DC    X'402020202020202020206B2020206B202020'
EDTPTRN4  DC    X'402020206B2020206B2020206B2020206B202020'
MNTHTBL   DC    AL1(31,28,31,30,31,30,31,31,30,31,30,31) MONTH TABLE
MNTHTBLL  DC    AL1(31,29,31,30,31,30,31,31,30,31,30,31) LEAP YEAR
FIRSTFLG  DC    C'0'                  FIRST TIME THROUGH FLAG
TONLYFLG  DC    C'0'                  TRENDS ONLY FLAG
DONLYFLG  DC    C'0'                  DETAIL ONLY FLAG
NONLYFLG  DC    C'0'                  REPORT NEW ONLY FLAG
GRAMTFLG  DC    C'0'                  GROWTH AMT PRESENT FLAG
PRIMEFLG  DC    C'0'                  PRIME DCOLLECT FLAG
GRWCKFLG  DC    C'0'                  GROWTH AMT CHECK FLAG
OLDFFLAG  DC    C'0'                  OLD FILE EOF FLAG
WRKFFLAG  DC    C'0'                  WORK FILE EOF FLAG
GDGFLAG   DC    C'0'                  PROCESSING GDG FLAG
SAVEYR    DS    CL2                   INTERIM SAVE FOR YEAR
DSNCOUNT  DC    PL4'0'                TOTAL DSN COUNT
LNELIMIT  DC    PL2'64'               LINES PER PAGE LIMIT
DLINECNT  DC    PL2'64'               DETAIL REPORT LINE COUNT
TLINECNT  DC    PL2'64'               TREND REPORT LINE COUNT
GAMT      DS    PL3                   GROWTH AMOUNT FROM INPUT PARM
PARMGAMT  DC    C'02500'              DEFAULT GROWTH AMOUNT
PARMDATE  DS    0CL6                  DATE FROM INPUT PARM
PDYR      DC    CL2' '                PARM DATE YEAR
PDMO      DC    CL2' '                PARM DATE MONTH
PDDY      DC    CL2' '                PARM DATE DAY
          DS    0D                    DOUBLE WORD ALIGN
WKFLD0    DS    3F                    VARIOUS WORK FIELDS
WKFLD1    DS    0D
WKFLD2    DS    F
WKFLD3    DS    F
WKFLD4    DS    0D
          DS    F
WKFLD5    DS    F
TRTNUM    DC    256XL1'FF'            TRT TABLE TO CHECK NUMERICS
          ORG   TRTNUM+C'0'
          DC    XL10'00'
          ORG
COMPYRP   DS    PL2                   COMPARISON YEAR PACKED
COMPMOP   DS    PL2                   COMPARISON MONTH PACKED
COMPDATE  DS    0CL6                  COMPARISON DATE FOR PURGING
COMPYR    DS    CL2                   COMPARISON YEAR
COMPMO    DS    CL2                   COMPARISON MONTH
COMPDY    DS    CL2                   COMPARISON DAY
SAVEDATE  DS    CL6                   WORK DATE SAVE
SAVESIZE  DS    F                     WORK SIZE SAVE
SAVEZAMT  DS    F                     TOTAL ALLOCATED SPACE SAVE
HOLDDSN   DS    CL44                  INTERIM DSN SAVE
SAVEDSN   DS    CL44                  WORK DSN SAVE
BLANKS    DC    CL133' '              BLANKS FOR PRINTING
HEADING1  DC    CL133' '              DETAIL HEADING 1
          ORG   HEADING1
          DC    CL44'1'
          DC    C'DATASET GROWTH SINCE '
H1YR      DS    CL2
          DC    C'/'
H1MO      DS    CL2
          DC    C'/'
H1DY      DS    CL2
          DC    C' USING'
H1AMT     DS    CL7
          DC    C'KB'
          DS    CL5
H1NOTE    DS    CL10
          ORG
HEADING2  DC    CL133' '              DETAIL HEADING 2
          ORG   HEADING2
          DS    CL9
          DC    C'DATASET NAME'
          DS    CL23
          DC    C'DATE FOUND'
          DS    CL12
          DC    C'CURRENT SIZE'
          DS    CL6
          DC    C'PRE-GROWTH SIZE'
          DS    CL3
          DC    C'GROWTH DATE'
          DS    CL7
          DC    C'GROWTH AMOUNT'
          ORG
HEADING3  DC    CL133' '              DETAIL HEADING 3
          ORG   HEADING3
          DS    CL9
          DC    C'------------'
          DS    CL23
          DC    C'----------'
          DS    CL12
          DC    C'------------'
          DS    CL6
          DC    C'---------------'
          DS    CL3
          DC    C'-----------'
          DS    CL7
          DC    C'-------------'
          ORG
HEADING4  DC    CL133' '              TREND HEADING 1
          ORG   HEADING4
          DC    CL35'1'
          DC    C'DASD GROWTH TREND'
          ORG
HEADING5  DC    CL133' '              TREND HEADING 2
          ORG   HEADING5
          DS    CL15
          DC    C' DATE'
          DS    CL15
          DC    C'TOTAL # DSNS'
          DS    CL9
          DC    C'TOTAL SPACE ALLOCATED'
          ORG
HEADING6  DC    CL133' '              TREND HEADING 3
          ORG   HEADING6
          DS    CL15
          DC    C' ----'
          DS    CL15
          DC    C'------------'
          DS    CL9
          DC    C'---------------------'
          ORG
TOTLINE   DC    CL133' '              DETAIL REPORT TOTALS LINE
          ORG   TOTLINE
          DS    CL1
          DC    C'TOTAL # PERMANENT DATASETS->'
TOTCOUNT  DS    CL10
          DS    CL10
          DC    C'TOTAL AMOUNT ALLOCATED SPACE->'
TOTSIZE   DS    CL20
          ORG
DETLINE   DC    CL133' '              DETAIL REPORT LINE
          ORG   DETLINE
          DS    CL1
DLDSN     DS    CL44
          DS    CL3
DLFNDDTE  DS    CL6
          DS    CL6
DLCUSIZE  DS    CL18
          DS    CL3
DLPGSIZE  DS    CL18
          DS    CL8
DLGRDATE  DS    CL6
          DS    CL2
DLGRAMT   DS    CL18
          ORG
TRENDLNE  DC    CL133' '              TREND REPORT LINE
          ORG   TRENDLNE
          DS    CL15
TRENDDTE  DS    CL6
          DS    CL14
TRENDCNT  DS    CL10
          DS    CL9
TRENDAMT  DS    CL20
          ORG
          DS    0F
DATARCRD  DC    CL72' '               DATA RECORD HOLD
          ORG   DATARCRD
DRDSN     DS    CL44                  DATASET NAME
DRCUSIZE  DS    CL4                   CURRENT SIZE
DRFLAG    DS    CL1                   INTERIM GDG FLAG
          ORG   DRFLAG
DRFNDDTE  DS    CL6                   FOUND DATE
DRAGSIZE  DS    CL4                   AFTER GROWTH SIZE
DRGRDATE  DS    CL6                   LAST GROWTH DATE
DRGRAMT   DC    PL4'0'                LAST GROWTH AMOUNT
DRPGSIZE  DS    CL4                   PRE-GROWTH SIZE
          ORG
          DS    0F
ZZZZREC   DC    CL72' '               TOTALS RECORD
          ORG   ZZZZREC
          DC    C'ZZZZZZ'
ZZZZDATE  DS    CL6
          DS    CL32
ZZZZCNT   DS    PL4
          DS    CL6
ZZZZAMT   DS    CL4
          ORG
          CNOP  2,4
BYTECNT   DC    Y(24)                 SORT PARAMETERS
          DC    A(SORTBEG)
          DC    A(SORTEND)
          DC    A(RECBEG)
          DC    A(RECEND)
          DC    2F'0'
POINTER   DC    X'80'
          DC    AL3(BYTECNT)
SORTBEG   DC    C' SORT FIELDS=('
SORTFLD   DC    C'1,44,CH,A)'
SORTEND   DC    C' '
RECBEG    DC    C' RECORD TYPE=F,LENGTH=72'
RECEND    DC    C' '
WTOBPRM1  WTO   '* BAD PARM DATA STARTING AT XXXXXX *',MF=L
WTOBPRM2  WTO   '* GA= CAN BE CODED ONLY WITH DONLY *',MF=L
WTOLIST   WTO   '  XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX',MF=L
DCOLCARD  DC    CL80' '               IDCAMS DATA DCOLLECT REQUEST
          ORG   DCOLCARD
          DC    C' DCOLLECT'
          DC    C' OFILE(DCOLLECT)'
DCOLVOL1  DC    CL5' '
DCOLVOL2  DC    CL6' '
DCOLVOL3  DC    C' '
DCOLSTO1  DC    CL6' '
DCOLSTO2  DC    CL8' '
DCOLSTO3  DC    C' '
          DC    C' NOVOLUMEINFO'
          ORG
OLDFILE   DCB   DDNAME=OLDFILE,DSORG=PS,MACRF=GL,EODAD=DASDG104
NEWFILE   DCB   DDNAME=NEWFILE,DSORG=PS,MACRF=PM,                      X
               LRECL=72,RECFM=FB
DETAIL    DCB   DDNAME=DETAIL,DSORG=PS,MACRF=PM,                       X
               LRECL=133,RECFM=FBA
TRENDS    DCB   DDNAME=TRENDS,DSORG=PS,MACRF=PM,                       X
               LRECL=133,RECFM=FBA
DCOLLECT  DCB   DDNAME=DCOLLECT,DSORG=PS,MACRF=GL,EODAD=DASDG046
SYSIN     DCB   DDNAME=SYSIN,DSORG=PS,MACRF=PM,                        X
               LRECL=80,BLKSIZE=80,RECFM=F
SORTOUT   DCB   DDNAME=SORTOUT,DSORG=PS,MACRF=GL,EODAD=DASDG066
SORTIN    DCB   DDNAME=SORTIN,DSORG=PS,MACRF=PM,RECFM=FB,LRECL=72
WORKFILE  DCB   DDNAME=WORKFILE,DSORG=PS,MACRF=(PM,GL),RECFM=FB,       X
               LRECL=72,EODAD=DASDG100
          IDCDOUT
          END


Ernie Ishman
Manager Systems Support
Geisinger System Services (USA)                                       c Xephon 1994

