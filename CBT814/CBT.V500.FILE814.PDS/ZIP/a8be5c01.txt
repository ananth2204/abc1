Quickly printing the current edit in ISPF/PDF
The following edit macro, when issued as a primary edit command, will print the
current (in-storage) edited dataset.  This is useful when you need a hardcopy of
a program that you are writing, for example, and avoids the need to leave edit
to perform this function.  It can also be used to print temporarily modified
data that you do not intend to save back to disk (which otherwise is necessary
before any printing).
/******************************************************/
/* EDIT MACRO : PRT (OPTIONAL OPERAND OF A JES2 DEST) */
/******************************************************/
ISREDIT MACRO (DEST)
  ISREDIT (LR) = LRECL
  IF &DEST = THEN +
     ALLOC F(PRXX) SYSOUT(A) REUSE RECFM(F) LRECL(&LR)
  ELSE +
     ALLOC F(PRXX) SYSOUT(A) REUSE RECFM(F) LRECL(&LR) DEST(&DEST)
  SET &COUNT = 1
  ISPEXEC CONTROL ERRORS RETURN
  OPENFILE PRXX OUTPUT
/* loop through edit file            */
  DO WHILE (&COUNT > 0)
    ISREDIT (PRXX) = LINE &COUNT
    IF &LASTCC = 0 THEN DO
       PUTFILE PRXX
       SET &COUNT = &COUNT + 1
       END
    ELSE +
       SET &COUNT = 0
  END
  CLOSFILE PRXX
  FREE F(PRXX)
  ISPEXEC CONTROL ERRORS CANCEL
/* issue message                     */
  SET &ZEDSMSG = PRINTED &DEST
  SET &ZEDLMSG = THIS EDIT DATA HAS BEEN PRINTED &DEST
  ISPEXEC SETMSG MSG(ISRZ000)
EXIT CODE(0)


     © Xephon 1989


























