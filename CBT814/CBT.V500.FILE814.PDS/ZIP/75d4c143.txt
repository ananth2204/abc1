Suppressing passwords under SDSF
Our installation still uses the standard password facility which issues the
message IEC301A S JOB jobname DSNAME dataset.name when a batch job requires a
password to access a dataset.
When the WTOR message is replied to, the system knows that it is a security
message (because it has ROUTCDE=9) and suppresses the entered reply.  If anyone
uses SDSF to enter the reply, SDSF issues a message SDSF COMMAND EXECUTED and
shows the entered password.  This exit will intercept that message and replace
the supplied password with the word ÔSUPPRESSEDÕ.
Put the load module called SDSFWTOX into a LINKLIST library (donÕt forget to
perform a REFRESH LLA if you are running under MVS/XA), and add the following
MPFLST card to your current MPFLSTxx member in SYS1.PARMLIB:
     SDSF*,USEREXIT(SDSFWTOX)
After issuing a SET MPF=xx command, passwords will no longer be displayed when
coming from SDSF.
SDSFWTOX CSECT ,
SDSFWTOX AMODE ANY
SDSFWTOX RMODE ANY
         STM   R14,R12,12(R13)     SAVE ENTRY REGISTERS
         LR    R11,R15             ENTRY REGISTER IS BASE
         USING SDSFWTOX,R11
         L     R8,0(,R1)           PARAMETER LIST
         USING CTXT,R8
         CLC   =C'CTXT',CTXTACRN   CORRECT BLOCK?
         BNE   *+1                   NO, THEN ERROR
         L     R9,CTXTTXPJ         GET MAJOR LINE TEXT POINTER
         CLC   MWTOCONS,CTXTTMSG-CTXTATTR(R9)
         NOP   *+1                  NO, THEN GIVE ME A DUMP
         BNE   RETNZERO             NO, THEN EXIT NOW
         CLC   =Y(MWTODTAL),CTXTTLMX-CTXTATTR(R9) ENOUGH ROOM?
         NOP   *+1                  NO, THEN GIVE ME A DUMP
         BH    RETNZERO             NO, THEN EXIT NOW
         LA    R2,(MWTODATA-MWTO)+(CTXTTMSG-CTXTATTR)(,R9)
         CLI   0(R2),C'0'          "REPLY"?
         BNL   ISREPNUM              YES, THEN PROCESS
         CLI   0(R2),C'R'          "REPLY"?
         BNE   RETNZERO              NO, THEN EXIT NOW
         CLI   1(R2),C' '          "R XX"?
         BE    ISREPCHR              YES, THEN PROCESS
         CLC   MWTORPLY,0(R2)      "REPLY XX"?
         BNE   RETNZERO              NO, THEN EXIT NOW
         LA    R2,6-2(,R2)         POINT AFTER 'REPLY'
ISREPCHR EQU   *
         LA    R2,2(,R2)           POINT AFTER 'R '
ISREPNUM EQU   *
*** R2->NUMBER WITH POSSIBLE LEADING SPACES
LEADSPLP EQU   *
         TM    0(R2),X'BF'         LEADING SPACE?
         BNZ   LEADSPEN              NO, THEN CARRY ON
         LA    R2,1(,R2)           STEP OVER LEADING SPACE
         B     LEADSPLP              AND LOOP BACK
LEADSPEN EQU   *
         LA    R3,2                MAX LENGTH OF NUMBER
         SLR   R4,R4               CLEAR ACCUMULATOR
NUMENDLP EQU   *
         CLI   0(R2),C'0'          END OF NUMBER?
         BL    NUMENDEN              YES, THEN CARRY ON
         SLL   R4,8                SHUNT NUMBER UP
         IC    R4,0(,R2)             AND INSERT NEW NUMBER
         LA    R2,1(,R2)           STEP OVER DIGIT
         BCT   R3,NUMENDLP           AND LOOP BACK
NUMENDEN EQU   *
         LTR   R4,R4               ANY NUMBER FOUND?
         BZ    RPLYRETN              NO, THEN EXIT NOW
         O     R4,=C'0000'         ADD LEADING ZEROES
         L     R6,CVTPTR           "CVT" ADDR
         L     R6,CVTCUCB-CVT(,R6) "UCM"
         USING UCM,R6              ADDRESS "UCM"
         LA    R7,UCMRPYQ-(ORELKP-OREF) REPLY QUEUE,----"ORE"
         USING OREF,R7             ADDRESS "ORE"
NEXTORE  EQU   *
         L     R7,ORELKP           NEXT "ORE"
         LA    R7,0(,R7)
         LTR   R7,R7               CHECK THERE IS AN "ORE"
         BZ    RPLYRETN              THERE ISN'T, JUST DO "MOD
         CLM   R4,B'0011',OREID    CHECK MATCHING DIGITS
         BNE   NEXTORE               NO, LOOP BACK FOR NEXT
         NOP   *+1                 *** TEST ***
         TM    ORERTB,ORERT009     IS SECURITY BIT ON
         BZ    NTSECMSG            SKIP SUPPRESSION IF NOT
         MVC   0(L'MWTOSUPP,R2),MWTOSUPP OVER-WRITE PASSWORD
         OI    CTXTRFB1,CTXTRCMT   INDICATE TEXT CHANGED
NTSECMSG EQU   *
RPLYRETN EQU   *
         NOP   *+1                 *** TEST ***
RETNZERO EQU   *
        RETURN (14,12) ?,RC=0 (15)
MWTO     DC    0D'0'
MWTOCONS DC    C'SDSF COMMAND EXECUTED',C'  '''
MWTODATA DC    CL42'REPLY 99,SUPPRESSED',C''''
MWTORPLY EQU   MWTODATA+0,6,C'C'
MWTOSUPP EQU   MWTODATA+8,11,C'C'
MWTODTAL EQU   *-MWTO
         LTORG ,
DSECT    DSECT ,
        IEZVX100 ,
         ORG   ,
SDSFWTOX CSECT ,
        CVT    DSECT=YES,LIST=NO
SDSFWTOX CSECT ,
        IEECUCM FORMAT=NEW         "UCM" MAP
        IHAORE ,                   "ORE" MAP
SDSFWTOX CSECT ,
         END   SDSFWTOX


Martin Hamilton
Technical Consultant
Thomson Holidays (UK)    © Thomson Holidays 1988























