Changing a RACF user-id

We frequently need to access certain datasets in order to
provide support for problems occurring on our system. As our
user-id doesn't have the RACF OPERATIONS attribute and we
aren't usually in the dataset profile's access list, we needed to
be able to change the user-id to one that has the OPERATIONS
attribute and is also audited for control.

To achieve this, we defined a new class in the RACF Class
Descriptor Table (ICHRRCDE), called @SOFTSEC, and
defined an entry with this class in the RACF Router Table
(ICHRFR01). (An IPL of the operating system is required to
activate these tables.) The RACF SETROPTS command  is
used to activate the class and it must be entered by the RACF
Security Administrator. We defined a resource called
@SOFTUSR in the class @SOFTSEC with UNIVERSAL
ACCESS NONE,  and put in its  access list the user-ids or
group allowed to perform this change. USER (SOFTUSR) was
defined with the OPERATIONS attribute. We don't need to
know its password.

The following steps are required to implement the change:

1      Define the following entry in the Class Descriptor Table
      (ICHRRCDE):

@SOFTSEC  ICHERCDE CLASS=@SOFTSEC,
X
               ID=130,                                                 X
               MAXLNTH=8,                                              X
               FIRST=ANY,                                              X
               OTHER=ANY,                                              X
               POSIT=27,                                               X
               OPER=NO,                                                X
               DFTUACC=NONE

Note: the values for ID and POSIT may have to be changed to
suit your own installation's standards.

2      Define the following entry in the RACF Router Table
      (ICHRFR01):
      SOFTSEC  ICHRFRTB CLASS=@SOFTSEC,ACTION=RACF

3      Activate the class with the command:
      SETROPTS CLASSACT(@SOFTSEC)

4      Define the resource in the class:
      RDEFINE  @SOFTSEC (@SOFTUSR) UACC(NONE)

5      Assign authority for users:
      PERMIT  @SOFTUSR CLASS(@SOFTSEC) ACCESS ID(user1,user2,...)

We also call a user SVC that performs authorization, ie it
authorizes the user to perform the RACINIT and then turns the
authorization off. The SVC is:

IGC0024I CSECT
         USING  *,6
         USING  TCB,4
         USING  IEZJSCB,2
         L      2,TCBJSCB
         LTR    0,0
         BZ     AUTHOFF
         OI     JSCBOPTS,JSCBAUTH           TURN IT AUTHORIZED
         BR     14
AUTHOFF  NI     JSCBOPTS,255-JSCBAUTH       TURN OFF AUTH.
         BR     14                          (RACINIT WAS OK)
         LTORG
         IKJTCB
         IEZJSCB
         END    IGC0024I

Note: this SVC must be authorized.


RACINIT TO CHANGE USER-ID

CHANGUSR CSECT
         SAVE (14,12),,CHANGUSR.&SYSTIME..&SYSDATE
         LR    R12,R15
         USING CHANGUSR,R12
         ST    R13,SAVEAREA+4         * SAVE CALLER'S SAVEAREA ADDR
         LR    R10,R13                * CALLER'S SAVEAREA
         LA    R13,SAVEAREA           * MY SAVEAREA
         ST    R13,8(R10)             * MY SAVEAREA ADDR IN CALLERS
* *             FIND USER
         LR    R9,R1                  * SAVE PARAMETER
         L     R1,PSAAOLD-PSA         * LAST ASCB DISPACHED
         L     R1,ASCBASXB-ASCB(R1)   * EXT.BLOCK
         L     R1,ASXBSENV-ASXB(R1)   * LOAD RACF CNTL.BLK
         XR    R2,R2                  * CLEAN
         ICM   R2,B'0001',ACEEUSRL-ACEE(R1)   * LENGTH
         LA    R1,ACEEUSRI-ACEE(R1)   * POINT TO USER
         EX    R2,MOVERAC             * MOVE USER
         L     R1,16(R0)              * CVT ADDR
         L     R1,00(R1)              * TCB ADDR
         L     R1,04(R1)              * CURRENT TCB
         L     R1,12(R1)              * TIOT ADDR
         CLC   RACUSER(07),00(R1)     * TSO USERID = RACUSERID ?
         BE    CHECK                  * YES, CHECK RACF CLASS
         MVI   USERID,X'07'           * RESTORE USERID LENGTH
         MVC   NEWUSER(07),00(R1)     * CHANGE TO TSO USERID
         B     AUTHORIZ               * AUTHORIZE CHANGE
CHECK    EQU   *
         LR    R1,R9                  * RESTORE PARAMETER
         L     R1,00(R1)              * LOAD PARM ADDR
         XR    R2,R2                  * CLEAN
         LH    R2,00(R1)              * LOAD PARM LENGTH
         CH    R2,=H'07'              * COMPARE TO HIGHEST SIZE
         BH    PARMERRO               * ERROR MESSAGE IF  > 7
         LTR   R2,R2                  * PARM = 0 ?
         BE    PARMERRO               * YES, ERROR MESSAGE
         LR    R3,R2                  * SAVE LENGTH
         BCTR  R2,R0                  * SUBTRACT FOR EXECUTE MOVE
         EX    R2,MOVECHK             * MOVE  TO  NEWUSER
         RACHECK ENTITY=USERID,CLASS='@SOFTSEC'   CHECK RACF
         LTR   R15,R15                * RACHECK OK ?
         BNZ   NOTAUTH                * NO, IT'S NOT AUTHORIZED
         STCM  R3,B'0001',USERID      * SAVE LENGTH
* *               CHANGE USERID ROUTINE
AUTHORIZ EQU   *
         LA    R0,1                   * NOT 0 TO TURN AUTH ON
         SVC   249                    * AUTHORIZE FOR RACFINIT
         RACINIT ENVIR=DELETE,RELEASE=1.8
*                                     * CREATE NEW USERID
         RACINIT USERID=USERID,ENVIR=CREATE,PASSCHK=NO
         SR    R0,R0                  * R0 = 0  TO TURN AUTH OFF
         SVC   249                    * AUTH OFF AFTER RACINIT OK
         B     RETURN                 * NOW THE CHANGE
*                                     * CAN BE APPLIED....
* *               AUTHORIZATION FAILED ROUTINE
NOTAUTH  EQU   *
         WTO   '*** YOU AREN''T AUTHORIZED FOR CHANGE TO NEW USER ****'X
               ,ROUTCDE=11
         LA    R15,08                 * INDICATED NON ZERO RETURN CODE
         B     RETURN                 * AND GO TO RETURN
PARMERRO EQU   *
         WTO   '**** INVALID PARM LENGTH ****',ROUTCDE=11
         LA    R15,04                 * SET RETURN CODE FOR PARMERRO
         B     RETURN                 * AND GO TO RETURN
* *               RETURN TO CALLER
RETURN   EQU     *
         L     R15,RETCODE          * LOAD RETURN CODE
         L     R13,4(R13)           * CALLER'S SAVE AREA
         LM    R14,R12,12(R13)
         BR    R14
* *               LITERAL POOL
         LTORG
* *               MISC. CONSTANTS
SAVEAREA DS    18F
RETCODE  DC    F'0'
MOVERAC  MVC   RACUSER(00),00(R1)     * MOVE RACUSER
MOVECHK  MVC   NEWUSER(00),02(R1)
USERID   DC   C'@'                    * @ FOR PROFILE
NEWUSER  DC   CL7'       '
         DC   CL40' '
RACUSER  DC   XL7'00'
         IHAASCB
         IHAASXB
         IHAACEE
         IHAPSA
         END


Rosane N Baronas
Origin C & P (Brazil)                                    c Xephon 1993

