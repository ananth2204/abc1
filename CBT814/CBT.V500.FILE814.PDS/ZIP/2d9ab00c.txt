Adding a new SSCVT entry without an IPL

A Subsystem Communications Vector Table (SSCVT) is the 
control block that is created at IPL time for each entry in the 
IEFSSNxx member of SYS1.PARMLIB.  Products that run as a 
subsystem require an SSCVT.  Some products build an SSCVT as 
part of their initialisation, but others require the SSCVT to be pre-
built at IPL time via IEFSSNxx.  This latter case poses a problem, 
particularly when a 'must implement' product arrives on Monday 
but the next IPL is scheduled for Saturday night.

The program, NEWSSCVT, will add a new SSCVT to the 
SSCVT chain for any given subsystem name without an IPL.  The 
program must be linked into an authorised library and with an AC 
code of 1.  The four-character subsystem name is passed as a 
PARM value.  A return code of zero indicates the SSCVT has 
been added successfully, a return code of four indicates an invalid 
PARM value, and a return code of eight indicates that the 
specified subsystem entry already exists.

EXAMPLE OF USE

// JOB
//NEWSSCVT EXEC PGM=NEWSSCT,PARM='JESA'

The above job would result in the same SSCVT as putting 'JESA' 
in member IEFSSNxx and IPLing.

SOURCE CODE FOR NEWSSCVT

NEWSSCVT CSECT SCD
         $SETR
         USING NEWSSCVT,R12
         STM   R14,R12,12(R13)
         LR    R12,R15                  SET INITIAL BASE
         ST    R13,SAVE+4
         LA    R13,SAVE
         L     R3,0(,R1)                GET ADDRESS OF PARM LENGTH
         LH    R4,0(,R3)                LOAD PARM LENGTH
         C     R4,=F'4'
         BNE   RETURN4
         LA    R3,2(,R3)                GET ADDRESS OF PARM
         MVC   SSYSNAME(4),0(R3)
         OC    SSYSNAME(4),=CL4' '      FORCE TO UPPER CASE
***  FIND SSCVT  ***
         L     R2,CVTPTR                CVT ADDRESS         
         USING CVTMAP,R2                ADDRESS CVT
         L     R7,CVTJESCT              JESCT ADDRESS
         USING JESCT,R7                 ADDRESS JESCT
         L     R3,JESSSCT               SSCVT ADDRESS
         USING SSCT,R3                  ADDRESS SSCVT
FINDSSCT CLC   SSCTSNAM,SSYSNAME        SSCVT ALREADY EXIST ?
         BE    RETURN8                  YES...THEN JUST RETURN
         CLC   SSCTSCTA,=F'0'           NEXT SSCVT EXIST ?
         BE    NEWSSCVT                 NO...THEN NOT FOUND...GO BUILD
         L     R3,SSCTSCTA              ADDRESS NEXT SSCVT
         B     FINDSSCT                 CONTINUE SEARCH
         DROP  R3
NEWSSCVT MODESET MODE=SUP
         MODESET EXTKEY=ZERO,SAVEKEY=(2),WORKREG=(2) KEY ZERO
         GETMAIN RC,LV=SSCTSIZE,SP=241,RELATED='G-SSCT' GET SSCT
         LTR   R15,R15                  GO OK ?
         BNZ   GETERR                   NO...RETURN
         USING SSCT,R1                  ADDRESS NEW SSCVT
         XC    SSCTID(SSCTSIZE),SSCTID  ZERO STORAGE
         MVC   SSCTID,=C'SSCT'          PUT ID IN
         MVC   SSCTSNAM,SSYSNAME        SUBSYSTEM NEWSSCVT
         DROP  R1
* RE-SEARCH IN CASE THINGS HAVE CHANGED
         L     R3,JESSSCT               SSCVT ADDRESS
         USING SSCT,R3                  ADDRESS SSCVT
SRCHSSCT CLC   SSCTSCTA,=F'0'           NEXT SSCVT EXIST?
         BE    PUTSSCVT                 NO...THEN NOT FOUND...INSERT
         L     R3,SSCTSCTA              ADDRESS NEXT SSCVT
         B     SRCHSSCT                 CONTINUE SEARCH
PUTSSCVT ST    R1,SSCTSCTA              NEW SSCVT NOW IN PLACE
         MODESET KEYADDR=(2)
         MODESET MODE=PROB
         B     RETURN0
RETURN8  L     R15,=F'8'
         B     RETURN
RETURN4  L     R15,=F'4'
         B     RETURN
RETURN0  SR    R15,R15
RETURN   L     R13,SAVE+4
         LM    R0,R12,20(R13)
         L     R14,12(R13)
         BR    R14
GETERR   ABEND 111
SAVE     DS    18F
SSYSNAME DC    CL4'    '
         LTORG
         CVT   DSECT=YES
         IEFJESCT
         IEFJSCVT
         END


	© Xephon 1989

