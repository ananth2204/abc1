Restricting TSO commands
A simple way to restrict the use of certain TSO commands to users with the
OPERATOR (or ACCT) attribute in the UADS is to front-end the command with a
program that checks the PSCB fields.  The command is then re-linked with the
entry point of this program.  Be sure to assign any aliases correctly and set
the correct link-edit attributes.
PREOP    CSECT
***  FRONT END FOR COMMAND PROCESSORS TO RESTRICT USAGE TO OPER
***  TYPE USERS, SET UP FOR PTCMD BUT CAN BE LINKED INTO ANY CP.
         STM   R14,R12,12(R13)   SAVE REGISTERS
         BALR  R12,0             ADDRESSABILITY
         USING START,R12
START    LR    R6,R1             R6 -> CPPL
         L     R0,SIZDAT1        R0 = SIZE OF DYNAMIC AREA
         GETMAIN R,LV=(0)        GETMAIN DYNAMIC AREA
         LR    R11,R1            R11 -> DYNAMIC AREA
         USING DATD,R11          ADDRESS DYNAMIC AREA
         ST    R13,SAVEAREA+4    SAVE CALLERS SAVEAREA ADDRESS
         ST    R11,8(R13)        SAVE OUR SAVEAREA ADDRESS
         LR    R13,R11           R13 -> SAVEAREA
         LM    R2,R5,0(R6)       LOAD CPPL
         USING PSCB,R4           ADDRESS PSCB
***  ADD OTHER TESTS HERE IF DESIRED ***
         TM    PSCBATR1,X'20'    TEST PSCB ATTRIBUTES
         BO    ALLOW             HE IS OPER PRIVILEGED
         B     NOTALLOW          NOT ALLOWED THIS COMMAND
ALLOW    LR    R1,R6
         L     R15,=V(PTCMD)     CALL ORIGINAL COMMAND ENTRY POINT
         BALR  R14,R15           PASSING ORIGINAL CPPL ADDRESS
         B     OUT
NOTALLOW TPUT  MSG,L'MSG
OUT      L     R13,SAVEAREA+4    RESTORE R13
         L     R0,SIZDAT1        R0 = DYNAMIC AREA SIZE
         LR    R1,R11            R1 -> DYNAMIC AREA ADDRESS
         FREEMAIN R,LV=(0),A=(1) FREE DYNAMIC AREA
         LR    R15,R10           RETURN CODE TO R15
         L     R14,12(R13)       RESTORE R14
         LM    R0,R12,20(R13)    RESTORE R0 TO R12
         BR    R14               RETURN
         LTORG
DATA     DS    0F
MSG      DC    C'COMMAND PTCMD NOT FOUND'
SIZDAT1  DC    AL1(0)            DYNAMIC AREA SIZE
         DC    AL3(ENDDATD-DATD)
         DS    0F
DATD     DSECT
SAVEAREA DS    18F        SAVE AREA
ENDDATD  EQU   *
         IKJPSCB
         END


Peter Toogood
Systems Programmer (UK)  © Xephon 1988












