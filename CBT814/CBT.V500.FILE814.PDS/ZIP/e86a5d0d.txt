An SDSF user exit for use with RACF

The System Display and Search Facility (SDSF) is an extremely 
useful product to display spool files but the options available to 
define who may access which spool files are relatively simplistic.  
This example of an SDSF user exit provides an extension of the 
access definitions by testing a RACF rule if SDSF would 
otherwise deny access.  These rules can then be used to define 
unusual or complex access requirements such as the sharing of 
output within a project group and so on.  The RACF rule tested is 
resource type SYSOUT resource name JOBNAME, where this is 
the job name actually selected by the SDSF user.

Standard RACF profiles can then be defined to control SDSF 
access and this therefore allows rules to contain masks for job 
prefixes and other features that make maintenance of access 
definitions simple.  A major benefit of using RACF in this way is 
that any rule change is immediately available with no need to 
cause a system outage.  The standard SDSF parameter definitions 
can be left as they are or reduced in scope to bring more control 
under RACF, according to installation preference.

The exit tests for READ access permission if display of a job's 
output is requested and for UPDATE if any operator commands 
are issued against the job such as purge, requeue, cancel etc.

More advanced tests can be coded by inspecting the information 
passed in the SDSF user exit parameter block.  For example, jobs 
with certain characters in the programmer name field could be 
tested against a special resource name instead of the actual job 
name.  Such pseudo-job names should be chosen to avoid possible 
conflict with real job names.

         TITLE 'SDSF USER EXIT WITH RACF RESOURCE CHECKS'
         COPY  HASPGEN
         COPY  ISFEQU
***********************************************************************
*  SDSF USER EXIT WITH VALIDATION USING RACF RESOURCE CHECKING.       *
*  ANY ACCESS ATTEMPT DENIED BY SDSF PARMS IS TESTED USING A RESOURCE *
*  TYPE OF SYSOUT AND RESOURCE NAME EQUAL TO THE JOBNAME THAT HAD     *
*  THE SDSF REQUEST ISSUED AGAINST IT. READ ACCESS IS TESTED FOR      *
*  DISPLAY REQUESTS AND UPDATE FOR PURGE, REQUEUE AND OTHER COMMANDS. *
*  IF RACF PERMITS ACCESS THEN REQUEST IS PERMITTED OTHERWISE IT      *
*  IS DENIED. THIS EXTENDS SDSF PARM RESOURCE DEFINITION CAPABILITIES.*
*  THIS EXIT IS CODED REENTRANTLY, AND MUST BE INSTALLED AS A SMP     *
*  USERMOD. ALLOWS AMODE 24 OR 31. RACF CLASS SYSOUT MUST BE ADDED    *
*  TO THE RACF CLASS TABLE AND TO THE RACF ROUTER TABLE, AND THE      *
*  CLASS MUST BE MADE ACTIVE WITH THE SETROPTS COMMAND.               *
***********************************************************************
ISFUSER  CSECT
         ISFSAVE
         LR    R12,R15              BASE REGISTER
         USING ISFUSER,R12          ADDR
         LR    R11,R1               USER EXIT PARM
         USING USERPARM,R11         ADDR IT
         LR    R10,R0               CALL TYPE
         B     CALLTYPE(R10)        GO TO PROPER CODE
CALLTYPE B     INITCALL             INITIALISATION
         B     COMMAND              COMMAND CHECK
         B     DISPLAY              DISPLAY SYSOUT CHECK
         B     TERMCALL             EXIT PROCESSING
*****    INITIALISATION CALL
INITCALL EQU   *                    INITIALIZATION CALL
         L     R0,SIZDATD           SIZE OF GETMAIN
         GETMAIN R,LV=(0)           GET STORAGE
         ST    R1,UPRUWORD          STORE ADDR IN USER WORD
         B     RETURN
*****    OPERATOR COMMAND CHECK SECTION
COMMAND  EQU   *
         CLC   UPRARETC,=F'0'          IS ACCESS ALLOWED BY SDSF ?
         BE    RETURN                  YES, EXIT IMMEDIATELY
* SEE IF THE OPERATOR COMMAND WAS RELATED TO A SPECIFIC JOB
         CLC   UPRJNAME,ZEROS          IS JOBNAME AVAILABLE ?
         BE    DISALLOW                NO, THEN IT IS /CMD
         CLC   UPRJNAME,BLANKS         IS JOBNAME AVAILABLE ?
         BE    DISALLOW                NO, THEN IT IS /CMD
* PREPARE FOR RACF TEST CODE
         LR    R4,R13                  OLD SAVE AREA
         L     R8,UPRUWORD             ADDR OF WORK STORAGE
         USING DATD,R8                 ADDR IT
         LR    R13,R8                  NEW SAVE AREA
         MODESET EXTKEY=ZERO,SAVEKEY=(2) GET KEY ZERO
* EXAMPLE TEST IS USING JOBNAME AS THE RACF RESOURCE (UPDATE ACCESS)
         MVC   RESOURCE,UPRJNAME       SET JOBNAME AS RESOURCE
         MVC   RACR(RACHTMPL),RACHTMP  COPY TEMPLATE
         LA    R1,RACR                 ADDR RACROUTE
         LA    R5,RESOURCE             ADDR JOBNAME
         RACROUTE REQUEST=AUTH,WORKA=RACFWORK,CLASS=SYSOUT,           X
               ENTITY=((5)),MF=(E,(1)),ATTR=UPDATE,LOG=ASIS
         LR    R6,R15                  SAVE RC
         MODESET KEYADDR=(2)           RESTORE PREVIOUS KEY
         LR    R13,R4                  OLD SAVE AREA RESTORED
         LTR   R6,R6                   TEST RACF RESULT
         BZ    ALLOW                   PERMIT IT
         B     DISALLOW                DISALLOW IT
         DROP  R8
*****    DISPLAY SYSOUT CHECK SECTION
DISPLAY  EQU   *
         CLC   UPRARETC,=F'0'          IS ACCESS ALLOWED BY SDSF ?
         BE    RETURN                  YES, EXIT IMMEDIATELY
* PREPARE FOR RACF TEST CODE
         LR    R4,R13                  OLD SAVE AREA
         L     R8,UPRUWORD             ADDR OF WORK STORAGE
         USING DATD,R8                 ADDR IT 
         LR    R13,R8                  NEW SAVE AREA
         MODESET EXTKEY=ZERO,SAVEKEY=(2) GET KEY ZERO
* EXAMPLE TEST IS USING JOBNAME AS THE RACF RESOURCE (READ ACCESS)
         MVC   RESOURCE,UPRJNAME       SET JOBNAME AS RESOURCE
         MVC   RACR(RACHTMPL),RACHTMP  COPY TEMPLATE
         LA    R1,RACR                 ADDR RACROUTE
         LA    R5,RESOURCE             ADDR JOBNAME
         RACROUTE REQUEST=AUTH,WORKA=RACFWORK,CLASS=SYSOUT,           X
               ENTITY=((5)),MF=(E,(1)),ATTR=READ,LOG=ASIS
         LR    R6,R15                  SAVE RC
         MODESET KEYADDR=(2)           RESTORE PREVIOUS KEY
         LR    R13,R4                  OLD SAVE AREA RESTORED
         LTR   R6,R6                   TEST RACF RESULT
         BZ    ALLOW                   PERMIT IT
         B     DISALLOW                DISALLOW IT
         DROP  R8
*****    TERMINATION CALL SECTION
TERMCALL EQU  *
         L    R0,SIZDATD           SIZE OF GETMAIN
         L    R1,UPRUWORD          LOAD ADDR FROM USER WORD
         FREEMAIN R,LV=(0),A=(1)   FREE STORAGE
         B    RETURN
*****    VARIOUS EXITS FROM EXIT 
ALLOW    EQU    *
         MVC    UPRARETC,=F'0'
         B      RETURN
DISALLOW EQU    *
         MVC    UPRARETC,=F'4'
         B      FAIL
RETURN   RETURN (14,12),T,RC=0
FAIL     RETURN (14,12),T,RC=4
*   CONSTANTS (NON-MODIFIABLE FOR REENTRANCY)
         LTORG
DATA     DS    0F
SIZDATD  DC    AL1(0),AL3(ENDDATD-DATD)     LEN FOR GETMAIN
SYSOUT   DC    XL1'06',CL8'SYSOUT'     RACF RESOURCE CLASS NAME
BLANKS   DC    CL8' '                  BLANKS
ZEROS    DC    XL8'0000000000000000'   ZEROS
RACHTMP  RACROUTE REQUEST=AUTH,MF=L    RACF TEMPLATE
RACHTMPL EQU   *-RACHTMP               LEN
*****    DSECTS
         IEZBITS                   BITS EQUATES
USERPARM DSECT                     USER EXIT PARM
         ISFUPRM 
DATD     DSECT                     MODIFIABLE GETMAINED STORAGE
SAVEAREA DS    18F                 SAVE AREA (NEW)
RACR     RACROUTE REQUEST=AUTH,MF=L  LAYOUT FOR RACROUTE
RESOURCE DS    CL8                 RACF RESOURCE NAME
RACFWORK DS    CL512               RACF WORK AREA
ENDDATD  EQU   * 
         END


	© Reserved 1989

