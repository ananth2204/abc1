Verifying whether a job is currently active

A program to verify whether another program is currently running can be very
handy.  Perhaps you wish to use some resource that this other job provides, or
maybe you can run only when this other job is not running.

This particular program scans the MVS Address Space Vector Table (ASVT) to see
if the job you want is active.  You specify a job name (which can also be a
started task or TSO user) on the PARM parameter of the EXEC card.  Output takes
two forms: an informational message and a return code.  The message is sent to a
TSO user via TPUT and to a batch job's JES log via WTO.  The return code is set
as follows:

¥              0    -    Job active
¥              4    -    Not active
¥              8    -    Bad parameter.


SAMPLE JCL

//SAMPLE1 JOB 0,'TEST',CLASS=A,MSGCLASS=A
//*=============================================
//* RUN ONLY IF PRODIDMS IS UP.
//*=============================================
//STEP1 EXEC PGM=ACT,PARM='PRODIDMS'
//STEP2  EXEC PGM=XYZ,COND=(0,NE,STEP1)

//SAMPLE2 JOB 0,'TEST',CLASS=A,MSGCLASS=A
//*=============================================
//* RUN ONLY IF PRODCICS IS DOWN.
//*=============================================
//STEP1 EXEC PGM=ACT,PARM='PRODCICS'
//STEP2  EXEC PGM=XYZ,COND=(4,NE,STEP1)

SAMPLE TSO CLISTS

/* IS PRODIDMS UP? */
CALL 'SYS1.UTIL.LOADLIB(ACT)'  'PRODIDMS'
IF &LASTCC = 0 THEN   ... DO IT ...

/* IS PRODC!CS DOWN? */
CALL 'SYS1.UTIL.LOADLIB(ACT)'  'PRODIDMS'
IF &LASTCC = 4 THEN   ... DO IT ...

PROGRAM ACT

* VERSION 1:     VERIFY IF ANOTHER JOB, STC, OR TSO IS RUNNING.
* STATUS:        PRODUCTION
* PROCESS:       FOLLOW ASVT CHAIN AND LIST JOB AND STEP.
* INPUT:         PARM CARD JOB NAME.
*                CONTROL BLOCKS SEE 'FEATURES'
* OUTPUT:        MESSAGES - 1) UNDER TSO - VIA TPUT TO SCREEN
*                           2) BATCH     - VIA WTO TO LOG.
*                RETURN CODE 0 - ACTIVE
*                RETURN CODE 4 - NOT ACTIVE
*                RETURN CODE 8 - BAD PARM CARD
*                PSA-->CVT-->ASVT-->ASCB
* DOCUMENTATION
*                DEBUGGING LIBRARY: DEBUGGING HANDBOOK VOLUME 2
*                    CONTROL BLOCKS
*                LY28-1091 SYSTEM LOGIC LIBRARY VOL 9    P SCTL-23
*                    ADDRESS SPACE QUEUES
         INTRO BASEREG=R9,AUTHOR=DONDUNN,PROGNAM=ACT,                 X
               VERSION=1
         L     R1,0(R1)           GET PARMS
         XR    R2,R2                   CLEAR
         LH    R2,0(R1)                LOAD LENGTH
         CH    R2,=H'0'
         BE    BADPARM                 TOO SHORT, END
         CH    R2,=H'8'
         BHE   BADPARM
         BCTR  R2,0                      LESS 1 FOR EX
         EX    R2,MOVE                 OK, MOVE
         B     AFTPARMS
MOVE     MVC   JOBNAME(0),2(R1)
AFTPARMS DS    0H
         EXTRACT ADDRSPC,'S',FIELDS=(TSO)     ARE WE A TSO SESSION?
         L     R12,ADDRSPC
         L     R12,0(R12)
         LA    R3,0
         USING PSA,R3                  PSA---->CVT
         L     R4,FLCCVT
         USING CVTMAP,R4               CVT---->ASVT
         L     R7,CVTASVT
         USING ASVT,R7                 ASVT--->ASCB
         XR    R8,R8                   ZERO
NEXTASID DS    0H
         L     R5,ASVTENTY(R8)         AN ASID
         USING ASCB,R5
*        DEBUG IN=(R5)                 SHOW ME REGISTER CONTENTS
         CLM   R5,B'1000',=X'80'       IS HIGH BIT ON?
         BNE   INUSE                       NO, VALID
         CLM   R5,B'0111',=XL3'0'      END OF TABLE?
         BE    ENDASVT                     ALL OVER
RENTR    DS    0H
         LA    R8,4(R8)                INCREMENT ASVTENTY ENTRY
         B     NEXTASID
INUSE    DS    0H
         L     R6,ASCBJBNI             JOBNAME IF INITIATOR
         LTR   R6,R6
         BNZ   TESTIT
         L     R6,ASCBJBNS             JOBNAME IF START TASK, ETC
TESTIT   CLC   JOBNAME,0(R6)           IS THIS THE JOB?
         BNE   INCRENTR                  NO, LEAVE
         MVC   MESSAGE,ACTIVE
         LTR   R12,R12          ARE WE A TSO SESSION?
         BM    MESSTS01
         MVC   WTOMSG+8(80),JOBMSG
         BAL   R10,WTOMSG
         B     RETCODE0
MESSTSOl TPUT  JOBMSG,80,EDIT,,HOLD
         B     RETCODE0
ENDASVT  DS    0H
         LTR   R12,R12          ARE WE-A TSO-SESSION?
         BM    MESSTS02
         MVC   WTOMSG+8(80),JOBMSG
         BAL   R10,WTOMSG
         B     RETCODE4
MESSTS02 TPUT  JOBMSG,80,EDIT,,HOLD
         B     RETCODE4
BADPARM  DS    0H                     BAD PARM CARD
         LTR   R12,R12          ARE WE A TSO SESSION?
         BM    MESSTS03
         MVC   WTOMSG+8(80),BADMSG        NO
         BAL   R10,WTOMSG
         B     RETCODE8
MESSTS03 TPUT  BADMSG,80,EDIT,,HOLD      YES
RETCODE8 DS    0H
         LA    R15,8
         B     THEEND
RETCODE4 DS    0H
         LA    R15,4
         B     THEEND
RETCODE0 DS    0H
         LA    R15,0
THEEND   EXIT
JOBMSG   DC    CL80' '
         ORG   JOBMSG
JOBNAME  DC    CL8' '
MESSAGE  DC    C' IS NOT ACTIVE'
             ORG
ACTIVE   DC    C' IS ACTIVE'
WTOMSG   WTO   '                                                      +
                                        '
         BR    R10                    RETURN
BADMSG   DC    CL80'JOBNAME ON PARAMETER CARD MISSING OR TOO LONG'
ADDRSPC  DS    F                    ARE WE TSO??
TS0      EQU   X'80'                TSO ADDRESS SPACE
         LTORG
         IHAPSA
         CVT DSECT=YES
         IHAASVT
         IHAASCB
         IKJTCB


Donald Dunn
Senior Software Specialist
Sacramento Municipal Utility District (USA)  © Xephon 1987







































