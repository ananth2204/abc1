A fast path browse/edit facility

Most of us in the ISPF/PDF world spend our time browsing
and editing a variety of datasets.  The PDF utility option 3.4
can provide some productivity gains if you happen to be
working with datasets with the same high-level qualifiers.
However, often option 3.4 cannot provide you with the set of
datasets that you wish to browse or edit in sequence.  And how
many times have you used 3.4 just because you couldn't
remember a dataset  name?

Used as an option on your primary ISPF menu, or as an entry
in your ISPF Command Table, the following dialogue can be
used to save hours of valuable time in your shop.  It will
increase productivity for everyone from the beginner to the
most experienced ISPF/PDF user.  The dialogue consists of a
CLIST and two panels.  It allows the user to enter up to 32
frequently-accessed datasets that can be edited or browsed
simply by entering the appropriate selection number.  A help
panel is also provided.  Once entered, the list of datasets is
stored in your ISPF profile and recalled each time you execute
the dialogue.

To install the dialogue, include the FASTPATH CLIST code in
a dataset in your SYSPROC concatenation and the two panels
in an ISPPLIB-concatenated dataset.  I strongly recommend
adding FASTPATH to your ISPF Command Table so that you
can pop in and out of edit or browse from any other ISPF/PDF
menu.  We have FASTPATH entered with a command name of
FP to make it even easier to execute.  Complete documentation
for ISPF panel definitions, dialogues, and Command Tables
can be found in the ISPF Dialogue Management Guide.

FASTPATH CLIST

/*********************************************************************/
/* -function - The FASTPATH CLIST allows the user to browse or edit   */
/*             commonly used datasets (users choose datasets)         */
/*             Associated panels - FASTPATH and FASTHELP              */
/*********************************************************************/
PROC 0
CONTROL NOMSG NOFLUSH
REF: -
   FREE F(TEMP)
   ISPEXEC DISPLAY PANEL(FASTPATH)      /*DISPLAY PANEL           */
   IF LASTCC = 8 THEN GOTO FINAL
   SET FPDS = &&FPDS
   IF &FPDS&NUM = &Z THEN DO
       WRITE NO DATASET SPECIFIED AT ENTRY &NUM.....TRY AGAIN!
       GOTO REF
       END
   IF &E = B THEN GOTO BROWSEIT  /* IF B FOR BROWSE...GOTO BROWSE */
   IF &SUBSTR(1:1,&FPDS&NUM) = &STR(') THEN GOTO REDIT1
   /* CHECK IF DATASET OR MEMBER EXIST */
   IF &SYSDSN('&SYSUID..&FPDS&NUM') = &STR(DATASET NOT FOUND) THEN DO
      WRITE dataset '&SYSUID..&FPDS&NUM' DOES NOT EXIST....CORRECT +
      AND RETRY
      GOTO REF
      END
   IF &SYSDSN('&SYSUID..&FPDS&NUM') = &STR(MEMBER NOT FOUND) THEN DO
      WRITE MEMBER '&SYSUID..&FPDS&NUM' DOES NOT EXIST....CORRECT +
      AND RETRY
      GOTO REF
      END
   ISPEXEC EDIT DATASET ('&SYSUID..&FPDS&NUM')
   GOTO REF
REDIT1: -
   IF &SYSDSN(&FPDS&NUM) = &STR(DATASET NOT FOUND) THEN DO
   /* CHECK IF DATASET OR MEMBER EXIST */
      WRITE dataset &FPDS&NUM DOES NOT EXIST....CORRECT +
      AND RETRY
      GOTO REF
      END
   IF &SYSDSN(&FPDS&NUM) = &STR(MEMBER NOT FOUND) THEN DO
      WRITE MEMBER &FPDS&NUM DOES NOT EXIST....CORRECT +
      AND RETRY
      GOTO REF
      END
   ISPEXEC EDIT DATASET (&FPDS&NUM)
   GOTO REF
BROWSEIT: -
   IF &SUBSTR(1:1,&FPDS&NUM) = (') THEN GOTO RBRS1
   IF &SYSDSN('&SYSUID..&FPDS&NUM') = &STR(DATASET NOT FOUND) THEN DO
      WRITE dataset '&SYSUID..&FPDS&NUM' DOES NOT EXIST....CORRECT +
      AND RETRY
      GOTO REF
      END
   IF &SYSDSN('&SYSUID..&FPDS&NUM') = &STR(MEMBER NOT FOUND) THEN DO
      WRITE MEMBER '&SYSUID..&FPDS&NUM' DOES NOT EXIST....CORRECT +
      AND RETRY
      GOTO REF
      END
   ISPEXEC BROWSE DATASET ('&SYSUID..&FPDS&NUM')
   GOTO REF
RBRS1: -
   IF &SYSDSN(&FPDS&NUM) = &STR(DATASET NOT FOUND) THEN DO
   /* CHECK IF DATASET OR MEMBER EXIST */
      WRITE dataset &FPDS&NUM DOES NOT EXIST....CORRECT +
      AND RETRY
      GOTO REF
      END
   IF &SYSDSN(&FPDS&NUM) = &STR(MEMBER NOT FOUND) THEN DO
      WRITE MEMBER &FPDS&NUM DOES NOT EXIST....CORRECT +
      AND RETRY
      GOTO REF
      END
   ISPEXEC BROWSE DATASET (&FPDS&NUM)
   GOTO REF
FINAL:-
   END

FASTPATH MAIN PANEL

)ATTR
   % TYPE(TEXT)  INTENS(HIGH)
   + TYPE(TEXT)  INTENS(LOW) SKIP(ON)
   _ TYPE(INPUT)  INTENS(LOW)
   $ TYPE(INPUT)  INTENS(HIGH)

)BODY
%--------------------FASTPATH EDIT/BROWSE FACILITY-----(PF1/13  HELP)---%&ZTIME
%COMMAND====>_ZCMD
+
+  Edit or Browse %==>$E+(-EÓorÓBÓ)
+  Dataset Number %==>$NUM
+
%              DATASET NAME                           DATASET NAME
%    ----------------------------------     ----------------------------------+
 1%=>_FPDS1                           +17%=>_FPDS17                           +
 2%=>_FPDS2                           +18%=>_FPDS18                           +
 3%=>_FPDS3                           +19%=>_FPDS19                           +
 4%=>_FPDS4                           +20%=>_FPDS20                           +
 5%=>_FPDS5                           +21%=>_FPDS21                           +
 6%=>_FPDS6                           +22%=>_FPDS22                           +
 7%=>_FPDS7                           +23%=>_FPDS23                           +
 8%=>_FPDS8                           +24%=>_FPDS24                           +
 9%=>_FPDS9                           +25%=>_FPDS25                           +
10%=>_FPDS10                          +26%=>_FPDS26                           +
11%=>_FPDS11                          +27%=>_FPDS27                           +
12%=>_FPDS12                          +28%=>_FPDS28                           +
13%=>_FPDS13                          +29%=>_FPDS29                           +
14%=>_FPDS14                          +30%=>_FPDS30                           +
15%=>_FPDS15                          +31%=>_FPDS31                           +
16%=>_FPDS16                          +32%=>_FPDS32                           +
+
)INIT
 &NUM = &Z
 .CURSOR = NUM
 .HELP = FASTHELP
)PROC
  VER(&E,NB)
  VER(&E,LIST,E,B)
  VER(&NUM,NB)
  VER(&NUM,LIST,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,
                19,20,21,22,23,24,25,26,27,28,29,30,31,32)
  &CMD  = TRUNC (&ZCMD, '.')
  VPUT (FPDS1,FPDS2,FPDS3,FPDS4,FPDS5,FPDS6,
        FPDS7,FPDS8,FPDS9,FPDS10,FPDS11,FPDS12,
        FPDS13,FPDS14,FPDS15,FPDS16,FPDS17,FPDS18,
        FPDS19,FPDS20,FPDS21,FPDS22,FPDS23,FPDS24,
        FPDS25,FPDS26,FPDS27,FPDS28,FPDS29,FPDS30,
        FPDS31,FPDS32,E) PROFILE
)END

FASTPATH HELP PANEL

)ATTR
   % type(text)  intens(high)
   + type(text)  intens(low)
   ¢ type(text)  intens(low) color(yellow)
   ! type(text)  intens(low) color(turq)

)BODY
%--------------------FASTPATH EDIT/BROWSE FACILITY----------------------%&ZTIME
%COMMAND====>_ZCMD
+
+  Edit or Browse %==>E+(-EÓorÓBÓ)
+  Dataset Number %==>2
+
%              DATASET NAME                           DATASET NAME
%    ----------------------------------     ----------------------------------+
 1%=>clist                            +17%=>'sys2.ispplib'                    +
 2%=>'sys1.proclib'                   +18%=>'sys1.parmlib'                    +
+
+  The Fastpath panel is rather straight forward. It allows you to define
+ a number of datasets that you can quickly%EDIT+or%BROWSE.
+
+  As shown in the example above, defining a dataset is typical of other
+ ISPF panels.  Using single quotes to define a specific dataset or no
+ quotes, letting your tso user id default as the high level qualifier.
+ Your datasets are saved between sessions.
+
+ Simple select%"E"+for Edit or%"B"+for Browse, then select the number
+ corresponding with the dataset you wish to select.  Your Edit or Browse
+ selection is also saved between sessions.
)INIT
)PROC
)END


Brent Davidson
Technical Support Analyst
Economical Mutual Insurance (Canada)                           © Xephon 1990

