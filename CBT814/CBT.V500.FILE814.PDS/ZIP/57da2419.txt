Converting SWA address tokens

Since the availability of MVS/XA 2.2 with its corresponding
JES component, it has been an option to allow the Scheduler
Work Area (SWA) control blocks to reside above the 16-
megabyte line.  Several years have passed since then and all of
the products that I know about are now capable of handling the
new addressing involved.  At our shop we have moved the
SWA control blocks above the line for everything.  Every byte
counts in on-line systems and batch jobs that run, primarily,
below the line.

What we have found is that all of our bought-in software works
great.  Where we had problems is in the home-grown programs
and public domain software.  Two worth mentioning are the
PDS product  from the CBT tape and the program XALLOC
found in the August 1990 issue of MVS Update.  Both
programs use the 3-byte TTR field of the TIOT to locate the
JFCB.

With the JFCB above the line you can no longer add 16 bytes
to the TTR and come up with a valid address.  For the various
control blocks that reside above the line, the TTR field contains
an SWA 'token' instead of an address.  Fortunately there is a
way to convert the token to a real address.

The subroutine below will, if passed a SWA token, return the
31-bit address of a SWA control block.  This can then be used
to address that control block.  It doesn't matter that the control
block may actually reside below the line - extended addresses
can just as easily address data below the line as above it.

In both of the programs mentioned above, I simply  added a
call to this subroutine at the point where they were attempting
to obtain addressing to the JFCB.  I then, temporarily, changed
the addressing mode to 31-bit addressing.  I was then able to
retrieve the desired information from the JFCB.  Once
addressability to the JFCB was no longer needed, I returned the
addressing mode to 24-bit.

* NAME      : SWA2ADDR                                                *
* FUNCTION  : THIS SUBROUTINE CONVERTS SWA TOKENS TO REAL ADDRESSES.  *
* FEATURES  : THIS PROGRAM WILL WORK ON ALL MVS/XA SYSTEMS, RELEASE   *
*             2.2 OR LATER. IT DOES NOT MATTER WHETHER OR NOT THE SWA *
*             CONTROL BLOCKS RESIDE ABOVE OR BELOW THE 16 MEG LINE.   *
*             IN EITHER CASE IT WILL STILL RETURN A VALID ADDRESS.    *
* CALL FMT  : CALL SWA2ADDR(TOKEN,ADDRESS)                            *
* PARAMETERS: THE TWO PARAMETERS PASSED TO THIS SUBROUTINE ARE DEFINED*
*             AS FOLLOWS.                                             *
* TOKEN      DS   XL3      THE TOKEN FOR A SWA CONTROL BLOCK SUPPLIED *
*                          BY THE CALLER.                             *
* ADDRESS    DS   A        A 31 BIT ADDRESS TO BE RETURNED TO THE     *
*                          CALLER.                                    *
* REG-15 WILL CONTAIN THE RETURN CODE FROM THE SWAREQ MACRO.          *
SWA2ADDR CSECT                         MY NAME IS ...
SWA2ADDR AMODE 31                      I CAN LIVE IN HEAVEN..
SWA2ADDR RMODE ANY                     ..OR THAT OTHER PLACE
         BAKR  R14,0                   ESA STYLE SAVE
         BASR  R12,0                   ADDRESS THIS CSECT
         USING *,R12                   AH .. ADDRESSABILITY
         LM    R3,R4,0(R1)             R3->TOKEN, R4->ADDRESS
         USING SWAEPA,R10              MAP SWA EPA
         LA    R10,SWA_EPA             ADDRESS THE ENTRY PARM
         XC    SWAEPA,SWAEPA           CLEAR THE AREA
         MVC   SWVA(3),0(R3)           PLACE SWA TOKEN IN PLIST
         SWAREQ FCODE=RL,              READ/LOCATE REQUEST             -
               EPA=SWEPAPTR,           ENTRY PAREMETER LIST            -
               MF=(E,SWAPARMS),        LIST FORM ENTRY                 -
               UNAUTH=YES              PROGRAM EXECUTES UNAUTHORIZED
         L     R1,SWBLKPTR             GET ADDRESS OF SCHED CB
         ST    R1,0(R4)                STORE SWA CONTROL BLOCK ADDRESS
         DROP  R10                     DROP ADDRESSABILITY
         PR                            EXIT
         LTORG
SWEPAPTR DC    A(SWA_EPA)              ADDRESS OF ENTRY PARM LIST
SWA_EPA  DS    XL28                    PARM LIST MAPED BY IEFZB505
SWAPARMS SWAREQ MF=L,                  LIST FORM OF THE REQUEST        -
               UNAUTH=YES
         IEFZB505                      MAP THE PARM LIST
         IEFJESCT                      JES CONTROL TABLE
         CVT   DSECT=YES               GOOD OLD CVT
         END


Paul Lemmons
Systems Programmer
Tucson Medical Center (USA)                        c Xephon 1991

