Reversing the order of a GDG

PS010     EXEC PGM=IKJEFT01,PARM='%XSGENREV SYSUT1 IEBGENER SHR'

/* REXX                                                              */
/*********************************************************************/
/* NAME       - XSGENREV                                             */
/*********************************************************************/
/* trace ?R                                                          */
/*   REXX   */
arg ddname modul disp
if ddname = '' then do
   say 'No DDNAME specified.'
   exit 1008
end
if disp = '' then do
   say 'No DISP parm specified, DISP set to default value (OLD)'
   disp = 'OLD'
end
if modul = '' then do
   say 'No Loadmodule specified'
   exit 1008
end
rcode = LISTDSI(ddname 'FILE')
if rcode <> 0 then do
   say 'Error in RC from LISTDSI rc='rcode
   exit 1008
end
dsname = SYSDSNAME
dsname2 = translate(dsname,' ','.')
gdglvl = subword(dsname2,words(dsname2),1)
dsnogdg = subword(dsname2,1,words(dsname2)-1)
dsnogdg = translate(dsnogdg,'.',' ')
if substr(gdglvl,1,1) <> 'G' |,
   substr(gdglvl,6,1) <> 'V' then do
   say 'DDNAME 'ddname' is NOT allocated to a GENERATION Dataset'
   EXIT 1016
end
x = OUTTRAP("LDS.",'*',"CONCAT")
"LISTCAT ENT('"DSNOGDG"')"
if rc <> 0 then do
   say 'Error in RC from LISTCAT rc='rc
end
count = 0
do I = 1 to lds.0
  if subword(lds.i,1,2) = 'NONVSAM ----' then do
     count = count + 1
     ut.count = subword(lds.i,3,1)
  end
end
ut.0 = count
x = OUTTRAP("LDS.",0)
dataset = ''
do I = 1 to ut.0
   dataset = dataset||"'"ut.i"'"||' '
end
dataset = strip(dataset)
"free f("ddname")"
if rc <> 0 then do
   say 'Error in RC from FREE command rc='rc
   exit 1008
end
"ALLOC F("ddname") DA("dataset") "disp
if rc <> 0 then do
   say 'Error in RC from ALLOC command rc='rc
   exit 1008
end
modul
drc = rc
return drc


Torkel JŸrgensen
MVS Systems Programmer
Skandia Data Operations (Sweden)                  c Torkel JŸrgensen 1995

