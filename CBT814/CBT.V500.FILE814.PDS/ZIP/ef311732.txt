Monitoring CSA/SQA usage with REXX


EXAMPLE DISPLAY

                COMMON STORAGE SITUATION AT 11:50:00

            CSA DEFINED ===============> 3.13M
            SQA DEFINED ===============> 1.30M
            ECSA DEFINED ==============> 45.98M
            ESQA DEFINED ==============> 19.38M
            CSA CONVERTED TO SQA ======> 0.11M
            ECSA CONVERTED TO SQA =====> 0.00M
            COMMON STORAGE BELOW LINE => 1.08M
            Total CSA got =============> 1.79M
            Total SQA got =============> 1.34M
            Total ECSA got ============> 27.91M
            Total ESQA got ============> 16.44M


CSAMON SOURCE CODE

/* REXX */
/* */
/* First define a MB as 1024K */
/* */
megabyte=1024*1024
/* */
/* locate the GDA which contains all the CSA and SQA data */
/* */
CVTGDA=D2X(C2D(STORAGE(10,4))+560)                   /* point to cvtgda */
/* */
/* first get all the IPL defined details */
/* */
csa=C2D(STORAGE(D2X(C2D(STORAGE(CVTGDA,4))+112),4))  /* csa size */
sqa=C2D(STORAGE(D2X(C2D(STORAGE(CVTGDA,4))+148),4))  /* sqa size */
ecsa=C2D(STORAGE(D2X(C2D(STORAGE(CVTGDA,4))+128),4)) /* ecsa size */
esqa=C2D(STORAGE(D2X(C2D(STORAGE(CVTGDA,4))+156),4)) /* esqa size */
/* */
/* now get the amount of CSA converted to SQA and the amount of ECSA */
/* */
csa2sqa=C2D(STORAGE(D2X(C2D(STORAGE(CVTGDA,4))+448),4))  /* csa conv'd*/
ecsa2sqa=C2D(STORAGE(D2X(C2D(STORAGE(CVTGDA,4))+452),4)) /* ecsa 2 */
/* */
/* now find how much CSA remains */
/* */
anyleft=C2D(STORAGE(D2X(C2D(STORAGE(CVTGDA,4))+132),4))  /* csa sqa 2go*/
/* */
/* then obtain how much of csa and sqa (above or below) that has been got */
/* */
csatot=C2D(STORAGE(D2X(C2D(STORAGE(CVTGDA,4))+432),4))   /* csa got*/
sqatot=C2D(STORAGE(D2X(C2D(STORAGE(CVTGDA,4))+440),4))   /* sqa got*/
ecsatot=C2D(STORAGE(D2X(C2D(STORAGE(CVTGDA,4))+436),4))  /* ecsa got*/
esqatot=C2D(STORAGE(D2X(C2D(STORAGE(CVTGDA,4))+444),4))  /* esqa got*/
/* */
/* now the ISPF bit. Convert all obtained values to Mb */
/* and write to a file for display                     */
/* */
queue '     COMMON STORAGE SITUATION AT' TIME()
queue ' '
queue 'CSA DEFINED ===============>' FORMAT((csa/megabyte),,2)||'M'
queue 'SQA DEFINED ===============>' FORMAT((sqa/megabyte),,2)||'M'
queue 'ECSA DEFINED ==============>' FORMAT((ecsa/megabyte),,2)||'M'
queue 'ESQA DEFINED ==============>' FORMAT((esqa/megabyte),,2)||'M'
queue 'CSA CONVERTED TO SQA ======>' FORMAT((csa2sqa/megabyte),,2)||'M'
queue 'ECSA CONVERTED TO SQA =====>' FORMAT((ecsa2sqa/megabyte),,2)||'M'
queue 'COMMON STORAGE BELOW LINE =>' FORMAT((anyleft/megabyte),,2)||'M'
queue 'Total CSA got =============>' FORMAT((csatot/megabyte),,2)||'M'
queue 'Total SQA got =============>' FORMAT((sqatot/megabyte),,2)||'M'
queue 'Total ECSA got ============>' FORMAT((ecsatot/megabyte),,2)||'M'
queue 'Total ESQA got ============>' FORMAT((esqatot/megabyte),,2)||'M'
queue ''
CALL MSG(OFF)
'FREE FI(SPONGE)'
"ALLOC FI(SPONGE) CATALOG",
   "DSORG(PS) REC(F B) LR(44) BLK(440)",
   "SPACE(1,2) TRACKS"
'EXECIO * DISKW SPONGE (FINIS'
X=LISTDSI(SPONGE 'FILE')
/* */
/* Please note if you are on an earlier ISPF than 4.1, you will need */
/* to replace the VIEW statement with either BROWSE or EDIT */
/* */
'ISPEXEC VIEW DATASET('sysdsname') VOLUME('sysvolume')'


Systems Programmer (UK)                      c Xephon 1996

