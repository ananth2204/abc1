Getting DASD volumes into a file

Several of IBM's utilities require control cards and/or DD statements for all of
the DASD that will be used (eg  IEHMOVE, IEHLIST, etc).  At this point you must
go and look up the volume labels of all the volumes you wish to use and create
the input necessary.  An easier method to do this is to use a program to scan
the UCB chains and to build a file with all the volume labels in it.  This can
later be used as input to other utilities such as ISPF skeleton files to build
the required cards.  Any volumes not required can then be removed.

This program is very simple to use.  It requires one file with a record length
of 80 and any multiple of 80 for the block size.

The UCB scan service is documented in the System Programming Library: Supervisor
manual.

UCBDASD TITLE   'UCB SCANNER'
*=====================================================================
* VERSION 1: LIST ALL ONLINE DASD TO A FILE
* STATUS: PRODUCTION
* PROCESS
*    SCAN UCB'S FOR ALL DASD VOLSERS
*    PUT TO A FILE
* INPUT
*   UCB'S
* OUTPUT
*    FILE OF VOLSERS
* FEATURES:
*   IOSYSUCB
* DOCUMENTATION
*   GC28-1046 SPL: SUPERVISOR        UCB SCAN
*=====================================================================
          INTRO BASEREG=R10,PROGNAM=UCB,VERSION=1,AUTHOR=DONDUNN
                         Initiation macro   [See Issue 11, Pages 8-12]
          OPEN  (OUTPUT,(OUTPUT))
          L     R3,CVTPTR                  GET CVT ADDRESS WHICH HAS
          USING CVTMAP,R3                    IOSYSUCB ADDRESS
*=====================================================================
* UCB SCAN LOOP
*=====================================================================
UCBLOOP   DS    0H
          L     R15,CVTUCBSC                     HERE
          MVI   UCBDEVCL,UCB3DACC          DIRECT ACCESS ONLY
          USING UCBOB,R2                   ADDRESS INTO UCB
          LA    R1,UCBSCANL                LOAD PARM LIST
          L     R15,CVTUCBSC                     HERE
          BALR  R14,R15                    UCB SCAN ROUTINE
          LTR   R15,R15                    LAST ONE?
          BNZ   ENDOFUCB                       YES
          L     R2,UCBADDR                 ADDRESS UCB ENTRY
          CLI   UCBVOLI,X'00'              NO ENTRY???
          BE    UCBLOOP                         NONE FOUND
          MVC   VOLSER,UCBVOLI             MOVE INTO TABLE
          PUT   OUTPUT, OUTREC
          B     UCBLOOP                    NEXT ENTRY
*=====================================================================
* END OF UCB'S
*=====================================================================
ENDOFUCB  DS    0H                         ALL UCB'S FOUND
         CLOSE (OUTPUT)
         XR    R15,R15                     RETURN CODE ZERO
         EXIT                              TERMINATION ROUTINE
*=====================================================================
UCBSCANL DC  A(UCBWORK)
         DC  A(UCBDEVCL)
         DC  X'10'                       END OF LIST
         DC  AL3(UCBADDR)
*
UCBWORK  DC  XL100'00'                   UCB SCAN OUTPUT
UCBDEVCL DS  CL1                         DEVICE TYPES TO LOOK FOR
UCBADDR  DS  F                           UCB ADDRESS
*=====================================================================
OUTPUT   DCB   DDNAME=SYSUT1,DSORG=PS,MACRF=PM
OUTREC   DC  CL80' '
         ORG OUTREC
VOLSER   DS  CL6
         ORG
*=====================================================================
         LTORG
         DSECT
         IEFUCBOB
         CVT     DSECT=YES
         END


Donald Dunn
Senior Software Specialist
Sacramento Municipal Utility District (USA)  © Xephon 1987












































