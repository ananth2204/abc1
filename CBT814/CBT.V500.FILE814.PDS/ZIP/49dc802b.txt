An SVC to obtain virtual device status

The following SVC has been implemented at our installation and has proved
invaluable in many circumstances.  Its function is to return status information,
using a VM diagnose code, of a virtual device attached to MVS, whereupon the
calling program can decide about actions to take on the device in question.

At our particular installation, one of the services provided by this SVC is to
relay information back to a CICS time-out transaction running under MVS, to
determine if the virtual 3270 was attached.  If so, the terminal is potentially
eligible for the sign-off transaction.  Should the time-out transaction be
initiated at a detached screen, the screen in question is put 'out of service'
to CICS.

This useful SVC should be link-edited into SYS1.SVCLIB.


THE PROGRAM SOURCE CODE

*/* NAME: SVC229 PROGRAM ID: IGC0022I
*/* REFERENCES: OS/MVS PLANNING AND USER GUIDE
*/*             VM SYSTEM PROGRAMMERS GUIDE
*/* FUNCTION: THIS SVC OS USED TO OBTAIN STATUS INFORMATION ON A
*/*           VIRTUAL DEVICE.  DATA IS PASSED TO AND FROM THE SVC
*/*           VIA A PARAMETER LIST WHOSE ADDRESS IS SET IN REG 1
*/*           BY THE CALLER.
*/* REGISTER USAGE: R1:- PARAMETER LIST ADDRESS
*/*                 R7:- VIRTUAL DEVICE ADDRESS (HEX)
*/*                 R8:- ADDRESS FOR RETURN CODE
*/*                      0 - ATTACHED
*/*                      1 - UNDEFINED
*/*                      2 - EXISTS BUT NOT ATTACHED
*/*                      3 - DEVICE DOES NOT EXIST
*/*                 R9:- DEVICE INFORMATION
*/*                R10:- DEVICE INFORMATION
*/*                R14:- RETURN ADDRESS OF CALLER
         DS    0D
SVC229   CSECT
         BALR  6,0                      REG 6 BECOMES BASE ADDRESS
         USING *,6
         LM    7,10,0(1)                LOAD PARAMETER LIST
         DC    X'83',X'79',XL2'0024'    ISSUE DIAGNOSE CODE
         STM   7,10,0(1)                STORE RESULTS IN PARM LIST
         BH    LAB2                     TEST CONDITION CODE
         BL    LAB1
         BE    LAB0
         LA    13,3                     DEVICE DOES NOT EXIST
         B     LABRET
LAB1     LA    13,1                     UNDEFINED
         B     LABRET
LAB2     LA    13,2                     EXISTS BUT NOT ATTACHED
         B     LABRET
LAB0     LA    13,0                     ATTACHED
LABRET   ST    13,4(1)                  STORE RETURN CODE
         BR    14                       RETURN TO CALLER
         END


AN EXAMPLE OF THE SVC'S USE

         .
         .
         STM   14,12,SAVEAREA           SAVE PROGRAMS REGISTERS
         L     1,DEVADHEX               LOAD IN ADDRESS OF DEVICE
         ST    1,DEVADDR                AND STORE IN PARAMETER LIST
         LA    1,PARMS                  REG1 = ADDR OF PARAMETER LIST
         LA    14,SVCRET                REG14 = RETURN ADDRESS
         SVC   229
SVCRET   EQU   *
         LM    14,12,SAVEAREA           RELOAD PROGRAMS REGISTERS
         CLI   RETCODE+3,...            TEST RETURN CODE
         .
         .
*/*      PARAMETER LIST
*/*
PARMS    DS    0F
DEVADDR  DS    F                        PASSED
RETCODE  DS    F                        RETURNED
DEVINFO1 DS    F                        RETURNED
DEVINFO2 DS    F                        RETURNED
*/*
*/*      OTHER VARIABLES
*/*
DEVADHEX DC    X'000000A6'       or whatever the device is called
SAVEAREA DS    18F                      PROGRAMS SAVE AREA
         .
         .


Paul Sapsford
Systems Programmer (UK)  © Paul Sapsford 1987









































