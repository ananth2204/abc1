A subroutine to produce a SNAP dump

The following program (called as a subroutine from a main
program) will establish a SPIE routine to be given control should
your main program abend with a program exception.  The SPIE
routine will dynamically allocate a SYSOUT dataset (DDNAME
DDSNAP) and produce a SNAP dump.

I00SPIE  CSECT
         USING I00SPIE,R15
         B     START
         DC    C'I00SPIE '
         DC    C'&SYSDATE'
         DROP  R15
START    DS    0H
         STM   R14,R12,12(R13)        save registers
         BALR  R12,0                  load base register
         USING *,R12                  assign base register
         ST    R13,SAVEAREA+4         backward pointer
         LA    R14,SAVEAREA           address of my savearea
         ST    R14,8(R13)             forward pointer
         LR    R13,R14                R13 --> my savearea
         SPIE  SPIERET,((1,11))       enable routine
         XR    R15,R15                0 return code
         L     R13,4(R13)             R13 --> callers savearea
         L     R14,12(R13)            return address
         LM    R0,R12,20(R13)         restore caller's registers
         BR    R14                    return to caller
SAVEAREA DC    18F'0'
         DROP  R12
         DC    C'* * * SPIE ROUTINE * * *'
         DS    0H
SPIERET  EQU   *
         USING SPIERET,R15
         STM   R3,R13,REGS+12         set up regs 0-15
         MVC   REGS(12),20(R1)        .. as they were at
         MVC   REGS+56(8),12(R1)      .. time of abend
         MVC   PSW(8),4(R1)           psw at time of abend
         LR    R12,R15                swap to R12
         DROP  R15                    .. as base
         USING SPIERET,R12            .. register
         LA    R1,S99RBPTR            R1 --> svc 99 parms
         DYNALLOC                     allocate DDSNAP
         OPEN  (DUMPDCB,(OUTPUT))
         SNAP  DCB=DUMPDCB,PDATA=ALL  produce a snap dump
         ABEND 999                    .. and then abend
DUMPDCB  DCB   DDNAME=DDSNAP,DSORG=PS,RECFM=VBA,MACRF=(W),             X
               BLKSIZE=882,LRECL=125
         DS    0F
         DC    C'REGS'
REGS     DC    16F'0'
         DC    C'PSW '
PSW      DC    2F'0'
S99RBPTR DC    AL1(128),AL3(S99RB)    dynalloc parameters
S99RB    DC    X'14010000'            .. (SVC 99) see
         DC    X'00000000'            .. MVS SPL
         DC    AL4(S99TUPTR)          .. Job Management
         DC    X'00000000'
         DC    X'00000000'
S99TUPTR DC    AL4(S99TU1)            --> text unit 1
         DC    AL1(128),AL3(S99TU2)   --> text unit 2
S99TU1   DC    X'0001'                text unit 1 (DDNAME)
         DC    X'0001'
         DC    X'0006'
         DC    C'DDSNAP'
S99TU2   DC    X'0018'                text unit 2 (SYSOUT)
         DC    X'0001'
         DC    X'0001'
         DC    C'W'                   print class for sysout
         END


David Welch
Systems Programmer
National Mutual Group Computing (New Zealand) © Xephon 1989

