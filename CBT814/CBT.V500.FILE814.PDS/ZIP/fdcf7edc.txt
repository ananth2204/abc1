How to simulate return codes

      //STEP1     EXEC   PGM=RCORWAIT,PARM='R=8,W=60'
      //SYSPRINT  DD     SYSOUT=*

      //STEP1     EXEC   PGM=RCORWAIT
      //SYSPRINT  DD     SYSOUT=*
      //SYSIN     DD     *
      W=60,R=8

SOURCE CODE FOR RCORWAIT

         SPLEVEL SET=2
RCORWAIT CSECT
RCORWAIT AMODE 24
RCORWAIT RMODE 24
         SAVE  (14,12),,RCORWAIT_&SYSDATE.&SYSTIME
         LR    R12,R15            LOAD ADDRESS
         USING RCORWAIT,R12       BASE REGISTER
         LR    R2,R1              PARAMETERLIST ADDRESS
         ST    R13,SAVEAREA+4     ADDRESS OF CALLER'S SAVEAREA
         LA    R15,SAVEAREA       ADDRESS OF OWN SAVEAREA
         ST    R15,8(,R13)
         LR    R13,R15            SAVEAREA ADDRESS; END OF HOUSEKEEPING
*        PROCESS PARAMETER INFORMATION FROM EXEC STATEMENT
         L     R2,0(,R1)          ADDRESS OF PARAMETER
         OPEN  (SYSPRINT,(OUTPUT)) OPEN LIST DATASET
         LH    R3,0(,R2)          LENGTH OF PARAMETER
         LTR   R3,R3              NO PARM IF ZERO
         BNZ   PARMFND            PARAMETER FOUND
         WTO   '***************************************',ROUTCDE=11
         WTO   '*                                     *',ROUTCDE=11
         WTO   '*  NO EXEC STATEMENT PARAMETER FOUND  *',ROUTCDE=11
         WTO   '*    TRYING TO OPEN SYSIN DATASET     *',ROUTCDE=11
         WTO   '*                                     *',ROUTCDE=11
         WTO   '***************************************',ROUTCDE=11
*        TRY TO GET CONTROL PARAMETERS FROM SYSIN DATASET
         OPEN  (INFILE)
         LTR   R15,R15            RETURN CODE FROM OPEN
         BZ    SYSINFND           ZERO IF OPEN SUCCESSFUL
         WTO   '***E*R*R*O*R***E*R*R*O*R***E*R*R*O*R***',ROUTCDE=11
         WTO   '*                                     *',ROUTCDE=11
         WTO   '*      OPEN SYSIN DATASET FAILED      *',ROUTCDE=11
         WTO   '*         EXECUTION TERMINATED        *',ROUTCDE=11
         WTO   '*                                     *',ROUTCDE=11
         WTO   '***E*R*R*O*R***E*R*R*O*R***E*R*R*O*R***',ROUTCDE=11
         LA    R10,16
         ST    R10,RCCODE
         B     RETURN
*        READ SYSIN DATASET AND MOVE CONTENTS TO PARMAREA
SYSINFND DS    0H
         GET   INFILE
         LR    R2,R1
         MVC   PARMAREA,0(R2)
         MVC   PARMAREA+50(14),=C'SYSIN DATASET '
EOF      DS    0H
         CLOSE INFILE
         B     PARMSOK
*        MOVE PARAMETER FROM EXEC STATEMENT TO PARMAREA
PARMFND  DS    0H
         LA    R2,2(,R2)           BEGINNING OF PARAMETER
         BCTR  R3,0                LENGTH - 1 FOR MOVE
         EX    R3,PARMMOVE
         MVC   PARMAREA+50(14),=C'EXEC STATEMENT'
*        NOW PARMAREA IS OK, PARAMETERS ARE FROM EXEC STATEMENT
*        OR FROM SYSIN FILE
PARMSOK  DS    0H
         MVC   WTOTEXT,PARMAREA    MOVE PARAMETERS INTO WTO
         BAL   R11,WTO
*        TRY TO FIND VALUE FOR RETURN CODE
SEEKRC   DS    0H
         LA    R2,PARMAREA         ADDRESS OF THE PARAMETER AREA
         LA    R3,80               LOOP COUNTER
RCLOOP   DS    0H
         CLC   PRC,0(R2)           TRY TO FIND KEYWORD R=
         BE    RCFND               IF FOUND
         CLI   0(R2),C' '          END OF PARAMETER STRING ?
         BE    RCNOTFND            YES
         LA    R2,1(,R2)           NEXT BYTE
         BCT   R3,RCLOOP
*        RETURN CODE PARAMETER NOT GIVEN
RCNOTFND DS    0H
         MVC   WTOTEXT(L'TEXT1),TEXT1     MESSAGE TEXT
         BAL   R11,WTO
         B     SEEKABND
*        RETURN CODE FOUND, CONTINUE PROCESSING
RCFND    DS    0H
         MVC   WTOTEXT(15),0(R2)   SHOW RETURN CODE
         MVC   WTOTEXT+50(11),=C'RETURN CODE'
         BAL   R11,WTO
         LA    R8,RCERROR          RETURN IF ERRONEOUS RC VALUE
         BAL   R11,FINDEND         GO TO FIND THE END
         C     R9,=F'10'           NO VALUE ?
         BL    CONVRC              SOMETHING LEFT
RCERROR  DS    0H
         MVC   WTOTEXT(L'TEXT2),TEXT2     MESSAGE TEXT
         BAL   R11,WTO
         MVC   WTOTEXT(L'TEXT3),TEXT3     MESSAGE TEXT
         BAL   R11,WTO
         B     SEEKABND
*        RETURN CODE CONVERSION FROM CHARACTER TO BINARY
CONVRC   DS    0H
         LA    R2,2(,R2)           BEGINNING OF NUMERIC
         LA    R11,10
         SR    R11,R9              LENGTH OF NUMERIC VALUE
         BCTR  R11,0               -1 FOR PACK
         EX    R11,PACK
         CVB   R11,DOUBLE
         ST    R11,RCCODE          STORE TO VARIABLE
SEEKABND DS    0H
         LA    R2,PARMAREA         ADDRESS OF THE PARAMETER AREA
         LA    R3,80               LOOP COUNTER
ABLOOP   DS    0H
         CLC   PABND,0(R2)         TRY TO FIND KEYWORD A=
         BE    ABFND               IF FOUND
         CLI   0(R2),C' '          END OF PARAMETER STRING ?
         BE    ABNOTFND            YES
         LA    R2,1(,R2)           NEXT BYTE
         BCT   R3,ABLOOP
*        ABEND CODE PARAMETER NOT GIVEN
ABNOTFND DS    0H
         MVC   WTOTEXT(L'TEXT5),TEXT5     MESSAGE TEXT
         BAL   R11,WTO
         B     SEEKWAIT
*        ABEND CODE FOUND, CONTINUE PROCESSING
ABFND    DS    0H
         MVC   WTOTEXT(15),0(R2)   SHOW ABEND CODE
         MVC   WTOTEXT+50(11),=C'ABEND CODE '
         BAL   R11,WTO
         CLI   2(R2),C'U'          USER ABEND REQUIRED ?
         BE    ABCONT
         CLI   2(R2),C'S'          SYSTEM ABEND REQUIRED ?
         BE    ABSYST
         MVC   WTOTEXT(L'TEXT7),TEXT7     MESSAGE TEXT
         BAL   R11,WTO
         B     ABERROR
ABSYST   DS    0H
         MVC   SYSABEND,2(R2)      MOVE SYSTEM ABEND CODE
         B SEEKWAIT                NO MORE CHECKING FOR SYST ABEND
*        DO SOME ADDITIONAL CHECKING FOR USER ABEND CODES
ABCONT   DS    0H
         MVC   ABNDTYPE,2(R2)      STORE TYPE OF ABEND
         LA    R2,1(,R2)           BEGINNING OF NUMERIC VALUE
         LA    R8,ABERROR          RETURN IF ERRONEOUS AB VALUE
         BAL   R11,FINDEND         GO TO FIND THE END
         C     R9,=F'10'           NO VALUE ?
         BL    CONVAB              SOMETHING LEFT
ABERROR  DS    0H
         MVC   WTOTEXT(L'TEXT6),TEXT6     MESSAGE TEXT
         BAL   R11,WTO
         B     SEEKWAIT
*        ABEND CODE CONVERSION FROM CHARACTER TO BINARY
CONVAB   DS    0H
         LA    R2,2(,R2)           BEGINNING OF NUMERIC
         LA    R11,10
         SR    R11,R9              LENGTH OF NUMERIC VALUE
         BCTR  R11,0               -1 FOR PACK
         EX    R11,PACK
         CVB   R8,DOUBLE
         C     R8,=F'4095'         4095 IS MAX VALUE
         BNH   STACODE
         L     R8,=F'4095'         4095 IS MAX VALUE
         MVC   WTOTEXT(L'TEXT16),TEXT16   MESSAGE TEXT
         BAL   R11,WTO
STACODE  DS    0H
         ST    R8,ABNDCODE         STORE TO VARIABLE
SEEKWAIT DS    0H
*        TRY TO FIND WAIT PARAMETER
SEEKWT   DS    0H
         LA    R2,PARMAREA         ADDRESS OF THE PARAMETER AREA
         LA    R3,80               LOOP COUNTER
WTLOOP   DS    0H
         CLC   PWAIT,0(R2)         TRY TO FIND KEYWORD R=
         BE    WTFND               IF FOUND
         CLI   0(R2),C' '          END OF PARAMETER STRING ?
         BE    WTNOTFND            YES
         LA    R2,1(,R2)           NEXT BYTE
         BCT   R3,WTLOOP
*        WAIT PARAMETER NOT GIVEN
WTNOTFND DS    0H
         MVC   WTOTEXT(L'TEXT8),TEXT8     MESSAGE TEXT
         BAL   R11,WTO
         B     ALLFND
*        WAIT PARAMETER FOUND, CONTINUE PROCESSING
WTFND    DS    0H
         MVC   WTOTEXT(15),0(R2)   SHOW WAIT PARAMETER
         MVC   WTOTEXT+50(14),=C'WAIT PARAMETER'
         BAL   R11,WTO
         CLI   2(R2),C'R'          MANUALLY CONTROLLED WAIT ?
         BNE   AUTOWAIT
         MVC   WAITR,2(R2)         MOVE MANUAL INDICATOR
         B     ALLFND
AUTOWAIT DS    0H
         LA    R8,WTERROR          RETURN IF ERRONEOUS RC VALUE
         BAL   R11,FINDEND         GO TO FIND THE END
         C     R9,=F'10'           NO VALUE ?
         BL    CONVWT              SOMETHING LEFT
WTERROR  DS    0H
         MVC   WTOTEXT(L'TEXT9),TEXT9     MESSAGE TEXT
         BAL   R11,WTO
         B     ALLFND
*        WAIT PARAMETER CONVERSION FROM CHARACTER TO BINARY
CONVWT   DS    0H
         LA    R2,2(,R2)           BEGINNING OF NUMERIC
         LA    R11,10
         SR    R11,R9              LENGTH OF NUMERIC VALUE
         BCTR  R11,0               -1 FOR PACK
         EX    R11,PACK
         CVB   R11,DOUBLE
         ST    R11,WAITT           STORE TO VARIABLE
ALLFND   DS    0H
SHOWVLS  DS    0H
*        DISPLAY RETURN CODE VALUE
         MVC   ACTDES,=CL20'RETURN CODE'
         MVI   ACTUNIT,C' '
         MVC   ACTUNIT+1(L'ACTUNIT-1),ACTUNIT
         L     R11,RCCODE
         CVD   R11,DOUBLE
         OI    DOUBLE+7,15
         UNPK  ACTVAL,DOUBLE
         BAL   R11,WTO
*        DISPLAY ABEND CODE VALUE
         MVC   ACTDES,=CL20'ABEND CODE'
         MVI   ACTUNIT,C' '
         MVC   ACTUNIT+1(L'ACTUNIT-1),ACTUNIT
         CLI   SYSABEND,C' '
         BE    DISPUSER
         MVC   ACTVAL(L'SYSABEND),SYSABEND
         B     DISPABND
DISPUSER DS    0H
         L     R11,ABNDCODE
         CVD   R11,DOUBLE
         OI    DOUBLE+7,15
         UNPK  ACTVAL,DOUBLE
         MVC   ACTUNIT(L'ABNDTYPE),ABNDTYPE
DISPABND DS    0H
         BAL   R11,WTO
DISPWAIT DS    0H
*        DISPLAY WAIT PARAMETER
         MVC   ACTDES,=CL20'WAIT PARAMETER'
         MVI   ACTUNIT,C' '
         MVC   ACTUNIT+1(L'ACTUNIT-1),ACTUNIT
         CLI   WAITR,C' '
         BE    TIMEWAIT
         MVC   ACTVAL,=C'REPLY'
         B     WAITWTO
TIMEWAIT DS    0H
         L     R11,WAITT
         CVD   R11,DOUBLE
         OI    DOUBLE+7,15
         UNPK  ACTVAL,DOUBLE
         MVC   TEXTWAIT,ACTVAL
WAITWTO  DS    0H
         BAL   R11,WTO
*        PROCESS WAIT REQUEST
WAITPROC DS    0H
         MVC   WTOTEXT(L'TEXT10),TEXT10   STARTING MESSAGE
         CLI   WAITR,C'R'          MANUAL WAIT ?
         BE    MANWAIT
         CLC   WAITT,=F'0'
         BE    ABNDPROC
         BAL   R11,WTO
         MVC   WTOTEXT(LTEXT12),TEXT12    STARTING MESSAGE
         BAL   R11,WTO
*        TIMED WAIT
         L     R10,WAITT
         MH    R10,=H'100'         SECONDS
         ST    R10,WAITT
         STIMER WAIT,BINTVL=WAITT
         B     WAITEND
*        MANUAL WAIT
MANWAIT  DS    0H
         BAL   R11,WTO
         MVC   REPECB,=F'0'
         WTOR  'THIS JOB WAITS UNTIL C IS REPLIED',REPLY,1,REPECB,     *
               ROUTCDE=11
         WAIT  ECB=REPECB
         OI    REPLY,X'40'         UPPER CASE
         CLI   REPLY,C'C'          CORRECT RESPONSE ?
         BNE   MANWAIT             NO, TRY AGAIN
WAITEND  DS    0H
         MVC   WTOTEXT(L'TEXT11),TEXT11   ENDING MESSAGE
         BAL   R11,WTO
*        PROCESS ABENDS
ABNDPROC DS    0H
         CLI   SYSABEND,C' '
         BNE   SYSAPROC
         CLC   ABNDCODE,=F'0'
         BE    RETURN
         MVC   WTOTEXT(L'TEXT13),TEXT13   ABENDING MESSAGE
         BAL   R11,WTO
         MVC   WTOTEXT(L'TEXT14),TEXT14
         BAL   R11,WTO
         L     R1,ABNDCODE         ABEND CODE
         LA    R0,128(0,R0)        DUMP INDICATOR
         SLL   R0,24(R0)
         OR    R1,R0
         SVC   13
*        PROCESS SYSTEM ABEND CODE
SYSAPROC DS    0H
         L     R3,ABENDNUM         NUMBER OF APPLIED ABENDS
         LA    R2,ABENDTBL         ADDRESS OF ABEND TABLE
SYSALOOP DS    0H
         CLC   SYSABEND,0(R2)      DESIRED ABEND FOUND ?
         BE    LETSGO              YES
         LA    R2,8(,R2)           NEXT ENTRY
         BCT   R3,SYSALOOP
         MVC   WTOTEXT(L'TEXT15),TEXT15
         BAL   R11, WTO
         B     RETURN
LETSGO   DS    0H
         MVC   WTOTEXT(L'TEXT13),TEXT13   ABENDING MESSAGE
         BAL   R11,WTO
         MVC   WTOTEXT(L'TEXT14),TEXT14
         BAL   R11,WTO
         L     R11,4(,R2)          ROUTINE ADDRESS
         BR    R11
*        CLOSE SYSPRINT AND RETURN (IF POSSIBLE ?)
RETURN   DS    0H
         CLOSE SYSPRINT
         L     R13,4(,R13)         CALLER'S SAVEAREA
         L     R15,RCCODE          LOAD WITH RETURN CODE
         L     R14,12(,R13)        STORE REGISTER 14
         LM    R0,R12,20(R13)      AND OTHERS
         BR    R14                 RETURN TO CALLER
*        SYSTEM ABEND PROCESSING
PROC0C1  DS    0H                  S0C1
         LA    R10,0
         BR    R10
PROC0C4  DS    0H                  S0C4
         LA    R10,0
         MVC   0(77,R10),176(R15)
PROCD37  DS    0H                  SD37
         OPEN  (SMALLDS,(OUTPUT))
PUTLOOP  DS    0H
         PUT   SMALLDS,WTOTEXT
         B     PUTLOOP
SMALLDS  DCB   MACRF=PM,DSORG=PS,DDNAME=SYSTD37,LRECL=80,BLKSIZE=80,   *
               RECFM=F
PROCB37  DS    0H                  SB37
         OPEN  (BIGGERDS,(OUTPUT))
B37LOOP  DS    0H
         PUT   BIGGERDS,WTOTEXT
         B     B37LOOP
BIGGERDS DCB   MACRF=PM,DSORG=PS,DDNAME=SYSTB37,LRECL=80,BLKSIZE=80,   *
               RECFM=F
*        SUBROUTINES
WTO      DS    0H
         LA    R1,WTOMSG
         SVC   35
         PUT   SYSPRINT,WTOTEXT
         MVI   WTOTEXT,C' '
         MVC   WTOTEXT+1(L'WTOTEXT-1),WTOTEXT
         BR    R11
*        TRY TO FIND WHERE KEYWORD ENDS
*        DELIMITER CAN BE BLANK OR COMMA
FINDEND  DS    0H
         LA    R10,2(,R2)          BYPASS KEYWORD
         LA    R9,10               LOOP COUNTER
ENDLOOP  DS    0H
         CLI   0(R10),C','         COMMA ?
         BE    DLMFND              YES
         CLI   0(R10),C' '         OR BLANK
         BE    DLMFND              YES
         TM    0(R10),X'F0'        IT MUST BE NUMERIC
         BO    LAR10               YES
         MVC   WTOTEXT(L'TEXT4),TEXT4     MESSAGE TEXT
         BAL   R11,WTO
         BR    R8                  ERROR RETURN
LAR10    DS    0H
         LA    R10,1(,R10)         NEXT BYTE
         BCT   R9,ENDLOOP
DLMFND   DS    0H
         BR    R11                 RETURN
PARMMOVE MVC   PARMAREA(0),0(R2)   MOVE EXEC STATEMENT PARAMETER
PACK     PACK  DOUBLE,0(0,R2)
***************  D A T A   A R E A S
DOUBLE   DC    D'0'
SAVEAREA DC    18F'0'
REPECB   DC    F'0'
RCCODE   DC    F'0'
ABNDCODE DC    F'0'
WAITT    DC    F'0'
WAITR    DC    C' '
PWAIT    DC    C'W='
PRC      DC    C'R='
PABND    DC    C'A='
REPLY    DC    C' '
ABNDTYPE DC    C' '
SYSABEND DC    CL4' '
INFILE   DCB   MACRF=GL,DDNAME=SYSIN,DSORG=PS,EODAD=EOF
SYSPRINT DCB   MACRF=PM,DDNAME=SYSPRINT,DSORG=PS,RECFM=FB,LRECL=80
*        SYSTEM ABEND TABLE
ABENDTBL DS    0F
         DC    C'S0C1',AL4(PROC0C1)
         DC    C'S0C4',AL4(PROC0C4)
         DC    C'SD37',AL4(PROCD37)
         DC    C'SB37',AL4(PROCB37)
ABTBLEND DS    0C
ABENDNUM DC    AL4((ABTBLEND-ABENDTBL)/8)  ELEMENTS IN TABLE
TEXT1    DC    C'RETURN CODE NOT DEFINED, WILL BE SET TO 0'
TEXT2    DC    C'RETURN CODE KEYWORD FOUND WITHOUT VALUE'
TEXT3    DC    C'RETURN CODE WILL BE SET TO 0'
TEXT4    DC    C'NUMERIC VALUE IS NOT NUMERIC'
TEXT5    DC    C'ABEND CODE NOT DEFINED, JOB WILL END NORMALLY'
TEXT6    DC    C'ABEND CODE KEYWORD FOUND WITHOUT VALUE, JOB WILL NOT AB
               BEND'
TEXT7    DC    C'ABEND TYPE IS NEITHER U (USER) ,NOR S (SYSTEM)'
TEXT8    DC    C'WAIT PARAMETER NOT FOUND, JOB WILL NOT WAIT'
TEXT9    DC    C'WAIT PARAMETER FOUND WITHOUT VALUE, NO WAIT REQUESTED'
TEXT10   DC    C'***** WAIT PROCESSING STARTING *****'
TEXT11   DC    C' ***** WAIT PROCESSING ENDING *****'
TEXT12   DC    C'THIS JOB WILL WAIT '
TEXTWAIT DC    CL5' '
         DC    C' SECONDS'
LTEXT12  EQU   *-TEXT12
TEXT13   DC    C' ***** THIS JOB WILL A B E N D *****'
TEXT14   DC    C' RETURN CODE VALUE IS NOT IN EFFECT '
TEXT15   DC    C'SYSTEM ABEND NOT FOUND IN TABLE, RETURN CODE IS USED'
TEXT16   DC    C'ABEND CODE VALUE GREATER THAN 4095, SET TO MAX  VALUE'
PARMAREA DC    CL80' '
WTOMSG   DS    0H
WTOLNG   DC    AL2(WTOEND-WTOTEXT)
         DC    X'8000'
WTOTEXT  DC    CL80'----------------------------------------------'
         DC    X'0000'
         DC    X'0020'
WTOEND   DS    0C
         ORG   WTOTEXT
ACTDES   DS    CL20
         DS    C
ACTVAL   DS    CL5
         DS    C
ACTUNIT  DS    CL10
         ORG
         END


Systems Programmer (Finland)                                                 c Xephon 1995

