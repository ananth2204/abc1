Controlling JES2 output limits through RACF


INTRODUCTION

Exit J00PRT1 provides a way, via RACF, to control the
amount of output a user can generate. This limit can be set by
creating a RACF resource name in the form of PLIMxxxx,
where the number of pages is xxxx times 1000. To prevent a
user from exceeding this amount, you set the user's RACF
access of this resource to 'none'.

For example, to create a page limit of 100,000 pages and limit
a user or group of users you would do the following:

1      Create a RACF resource name of PLIM0100.

2      Set the user's access to the resource name of PLIM0100 to
      'none'. If you have a group of users you want to limit,
      determine a RACF group that they belong to and set the
      group access of PLIM0100 to 'none'.


ALGORITHM USED

This exit was designed to work on 1000 page limits. When this
exit is entered (approximately every 1000 pages), the amount
of pages generated is obtained from JCTPAGES or JCTLINES,
depending on the mode. This value is rounded down to a
multiple of 1000 and then divided by 1000. The resulting
number is used to build a resource name in the form of
PLIMxxxx (see above). The user's ACEE is obtained and a
RACHECK is performed on the generated resource name to
determine the type of access. If access is allowed, the print
continues. If access is denied, the user's job abends with an
S722. If no resource name exists, the print continues.

Exit J009PRT1 will build a resource name and issue a
RACHECK each time it is called by JES2 (in this case, every
1000 pages or as set by ESTPAGE or ESTLINE in
JES2PARM).


REQUIREMENTS

1      A RACF resource name(s) must be created in the format
      given earlier.  In this shop, the resource name is created
      under the class of facility.

2      Set up JES2PARM to call the exit every 1000 pages or
      50,000 lines like this:

      ESTLNCT NUM=50,INT=50000,OPT=0    (ASSUME 50 LINES/PAGE)
      ESTPAGE NUM=1000,INT=1000,OPT=0
      LOADMOD(J009PRT1) STORAGE=CSA
      EXIT9 ROUTINE=(E009PRT1),ENABLE,TRACE=NO

      Note that a 1000-page interval is used at this shop. This
      can be changed to another value if it is considered more
      appropriate.


J009PRT1 SOURCE CODE

         COPY  $HASPGBL
J009PRT1 $MODULE ENVIRON=USER,                                         X
               SYSP=(GEN,GEN,DATA,GEN,GEN),                            X
               $HASPEQU,                                               X
               $PCE,               CONTAINS USING PCEDSECT,R13         X
               $CMB,                                                   X
               $CSA,                                                   X
               $IOT,                                                   X
               $BUFFER,                                                X
               $JCT,                                                   X
               $JQE,                                                   X
               $TAB,                                                   X
               $MIT,                                                   X
               $KIT,                                                   X
               $HCCT,                                                  X
               $SCAT,                                                  X
               $XECB,                                                  X
               $CADDR,                                                 X
               $PADDR,                                                 X
               $PSV,                                                   X
               $XIT,                                                   X
               RPL
***********************************************************************
*        MAIN ENTRY POINT OF EXIT 9                                   *
***********************************************************************
DATAAREA DSECT
         DS   0F
RACRLS2  DS   CL(LRACRLS1)
RESNAME  DS   CL39
DWORD    DS   D
WORKA1   DS   D
DATAEND  EQU  *-DATAAREA
J009PRT1 CSECT
         CVT DSECT=YES
         IHAASCB
         IHAASXB
         IHAACEE
E009PRT1 $ENTRY BASE=(R12),CSECT=YES
         SAVE  (14,12)                SAVE CALLER'S REGISTERS
         LR    R12,R15                LOAD FIRST BASE REGISTER
         USING JCT,R7                 SET UP ADDRESSABILITY TO JCT
         USING HCCT,R11               SET UP ADDRESSABILITY TO HCCT
         TM     JCTJOBFL,JCTBATCH     IS THIS A BATCH JOB?
         BNO    EXIT0                 NO, GET OUT.
         GETMAIN  RU,LV=DATAEND
         LR     R9,R1                 ADDRESS RETURNED IN R1
         USING  DATAAREA,R9
* FIND THE ACEE
         XR     R5,R5                 CLEAR OUT R5
         L      R6,16                 LOAD ADDRESS OF CVT
         USING  CVT,R6
         L      R6,CVTTCBP            POINT TO TCB WORDS
         DROP   R6
         L      R6,12(,R6)            LOAD ADDRESS OF CURRENT ASCB
         USING  ASCB,R6
         TM     ASCBFLG1,ASCBTERM+ASCBABNT IS ADDRESS SPACE ENDING
         BNZ    FREEIT                YES, LET IT END W/O S722
         L      R6,ASCBASXB           LOAD ADDRESS OF CURRENT ASXB
         DROP   R6
         USING  ASXB,R6
         L      R6,ASXBSENV           LOAD ADDRESS OF ACEE
         DROP   R6
         USING  ACEE,R6
         CLC    ACEEACEE(4),=C'ACEE'  IS THIS A VALID ACEE
         BNE    FREEIT                NO, LET IT SLIDE W/O RACHECK
         DROP   R6
         L      R3,JCTPAGES           OBTAIN THE NUMBER OF PAGES
         C      R3,F2                 IF THIS VALUE <= 2, LINE MODE
         BH     PAGEMODE              GREATER THAN 2, IN PAGEMODE
         L      R3,JCTLINES           OBTAIN THE NUMBER OF LINES
         XR     R2,R2                 CLEAR OUT R2 FOR DIVIDE.
         D      R2,F50                ASSUME 50 LINES PER PAGE.
*                                     R3 NOW CONTAINS NUMBER OF PAGES
PAGEMODE XR     R2,R2                 CLEAR OUT R2
         D      R2,F1000              CHANGE THE NUMBER OF PAGES TO
         CVD    R3,DWORD              MAKE THIS VALUE PRINTABLE
         UNPK   WORKA1(8),DWORD+4(4)
         OI     WORKA1+7,X'F0'        FIX THAT LAST BYTE
         MVI    RESNAME,C' '          BLANK OUT RESNAME
         MVC    RESNAME+1(L'RESNAME-1),RESNAME
         MVC    RESNAME(4),RESHIGH    MOVE IN PART OF RESOURCE NAM
         MVC    RESNAME+4(4),WORKA1+4 MOVE IN NUMBER PART OF NAME
         MVC    RACRLS2,RACRLS1       MOVE INTO GETMAINED AREA
         LA     R5,RESNAME
         RACHECK  ENTITY=((R5)),LOG=NOSTAT,                            X
               RELEASE=1.8.1,MF=(E,RACRLS2)
         LR     R5,R15                SAVE THE RESULT.
FREEIT   FREEMAIN RU,LV=DATAEND,A=(R9) FREE UP GETMAINED AREA
         DROP   R9
         C      R5,F8                 WAS THE USER REJECTED?
         BNE    EXIT0                 NO.  LET THE USER CONTINUE
         L      R0,ABEND              MUST BE BAD ACCESS. ABEND S722
*                                     RC=8
* RETURN TO CALLER
EXIT8    LA    R15,8                  SET RETURN CODE TO 8
         ST    R0,20(,R13)
         B     EXITPGM
EXIT0    XR    R15,R15                SET RETURN CODE TO 0
EXITPGM  RETURN (14,12),RC=(15)
*  DATA AREAS
RACRLS1  RACHECK  LOG=NOSTAT,RELEASE=1.8.1,CLASS='FACILITY',MF=L
LRACRLS1 EQU *-RACRLS1
RESHIGH  DC  CL4'PLIM'
F1000    DC  F'1000'
F50      DC  F'50'
F8       DC  F'8'
F2       DC  F'2'
         DS    0F
* VARIOUS FLAGS AND DEFINED AREAS
ABEND    DC    X'80',AL3(1)           ABEND WITH 722 W/O DUMP
PASS     DC    X'00',AL3(0)           LET IT SLIDE, BUT POST HASP MSG
         LTORG
         $MODEND
         END


Jeff Canipe
Systems Programmer
First Union National Bank (USA)                               c Xephon 1992

