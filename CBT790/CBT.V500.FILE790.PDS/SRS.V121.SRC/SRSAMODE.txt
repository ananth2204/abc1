         MACRO -- SRSAMODE -- SRS address mode switch
&LABEL   SRSAMODE &MODE,&R=R15
&HIBITON SETC  '=X''80000000'''
         AIF   ('&MODE' NE '24' AND '&MODE' NE '31').ERR1
         AIF   ('&R' EQ '').ERR2
&SKIPIT  SETC  '$$$$'.'&SYSNDX'    Build end-label
&NAME    SETC  '&LABEL'
&NAME    LA    &R,&SKIPIT          Pick up address for mode switch
         AIF   ('&MODE' EQ '24').NOAND
         O     &R,&HIBITON         Set addr for 31-bit mode
.NOAND   ANOP
         BSM   R0,&R               BSM to set address mode
&SKIPIT  DS    0H                  End of SRSAMODE processing
         MEXIT
.*
.*       ERROR MACRO NOTES
.*
.ERR1    MNOTE 8,'ERROR: Invalid address mode specified for SRSAMODE'
         MEXIT
.ERR2    MNOTE 8,'ERROR: Invalid work register specified for SRSAMODE'
         MEND
