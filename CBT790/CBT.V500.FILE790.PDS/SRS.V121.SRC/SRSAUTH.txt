*PROCESS RENT,FLAG(PAGE0)                                          V121
SRSAUTH TITLE '- SYSOUT Retrieval Services (SRS) State-switch rtn'
SRSAUTH CSECT , ATTR=RENT,REUS,REFR
SRSAUTH AMODE 31
SRSAUTH RMODE ANY
*=====================================================================*
*                                                                     *
*  MODULE NAME:  SRSAUTH                                              *
*                                                                     *
*  DESCRIPTIVE-NAME: SYSOUT Retrieval Services (SRS) State-switch rtn *
*                                                                     *
*  STATUS: MVS/SP Version 4 and higher                                *
*                                                                     *
*  OPERATION:                                                         *
*                                                                     *
*    SRSAUTH provides APF authorization/unauthorization switching     *
*    services to SRSTASK.  SRSTASK must be supervisor, key zero to    *
*    issue the SAPI request to JES2.  It switches to key 8, problem   *
*    state to call the user program to process each spool record.     *
*                                                                     *
*    The SRSDRVR program attaches one SRSAUTH and passes its ECB      *
*    address to the SRSTASK program.                                  *
*                                                                     *
*                                                                     *
*    ATTRIBUTES:                                                      *
*                                                                     *
*      Location:  resident in own address space                       *
*      State:  problem & supervisor, must be APF authorized           *
*      Amode:  31-bit                                                 *
*      Rmode:  Any                                                    *
*      Key:  key 0,8                                                  *
*      Mode:  task                                                    *
*      Serialization: none                                            *
*      Type:  Reentrant                                               *
*                                                                     *
*  Written By: Dave Danner                                      @A11202
*              Summit Technical Services, Inc.                  @A11202
*                                                               @A11202
*          January, 1998                                              *
*                                                                     *
*  Change log:                                                        *
*=====================================================================*
         USING SRSAUTH,R15          Temporary base
         B     START                Branch around eyecatcher
         DC    C'SRSAUTH_&SYSDATE._&SYSTIME. '                     V121
         DC    C'COPYRIGHT 1998-2004 DAVID W DANNER '              V121
         DC    C'ALL RIGHTS RESERVED'                              V121
START    DS    0H
         DROP  R15                  Drop temporary addressability
         SAVE  (14,12)              Save regs
         LR    R12,R15              Set base register
         USING SRSAUTH,R12
         LR    R6,R1                Save address of authorization parm
         USING SRSABLK,R6           And address it
         MODESET MODE=SUP           Get supervisor state
         MODESET EXTKEY=ZERO        Get PSW key zero
* Locate our JSCB
         L     R15,PSATOLD-PSA(,0)     Get our TCB address
         L     R5,TCBJSCB-TCB(,R15)    Get our JSCB address
         USING IEZJSCB,R5
         L     R5,JSCBACT              Get address of active JSCB
         TM    JSCBOPTS,JSCBAUTH    Are we APF authorized ?
         BNO   NOAPF                No, can't continue
*        WTO   'SRS700I SRS Authorization Services active'
*
WAIT     DS    0H
         WAIT  ECB=SRSAAECB         Wait for request
         NI    SRSAAECB,X'FF'-ECBPOST  Turn off post bit
         TM    SRSAREQ,SRSAAUTH     Authorization request ?
         BO    AUTH                 Yes, do it
         TM    SRSAREQ,SRSAUNAU     Un-authorization request ?
         BO    UNAUTH               Yes, do it
         TM    SRSAREQ,SRSATERM     Termination request ?
         BO    TERM                 Yes, do it
         WTO   'SRS710E Unknown request type - SRSAUTH'
         B     REQDONE
         SPACE 3
AUTH     DS    0H
**       WTO   'SRSAUTH - APF Authorization ON'
         OI    JSCBOPTS,JSCBAUTH    Make task authorized
         B     REQDONE
         SPACE 3
UNAUTH   DS    0H
**       WTO   'SRSAUTH - APF Authorization OFF'
         NI    JSCBOPTS,X'FF'-JSCBAUTH    Make task unauthorized
         B     REQDONE
         SPACE 3
REQDONE  DS    0H
         POST  SRSARECB             Let requestor know we are done
         B     WAIT                 Wait for next request
         SPACE 3
NOAPF    DS    0H
         WTO   'SRS711E SRSAUTH not invoked APF authorized',DESC=1
         SPACE 3
TERM     DS    0H
*        WTO   'SRS701I SRS Authorization Services terminated'     V134
         RETURN (14,12),,RC=0        RETURN TO CALLER
* MAPPING MACROS
         SRSABLK                   MAP SRS Task Data Block
         PRINT NOGEN
         IHAPSA                    MAP PSA
         IHAASCB                   MAP ASCB
         CVT   DSECT=YES           MAP CVT
         IKJTCB
         IEFJESCT
         IEFJSSIB
         IEZJSCB
CSCB     DSECT
         IEECHAIN                  MAP CSCB
         DCBD DSORG=PS             MAP DCB
         IHAECB
         IEZCOM
         IEZCIB
         IHASDWA
         YREGS
         END
