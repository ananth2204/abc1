*Process Margins(2,72,1),Nonumber,Optimize(TIME),A,X,Ag,System(MVS);
 /********************************************************************/
 /*                                                                  */
 /*   RACauth                                                        */
 /*   ------------------------------------------------------------   */
 /*   Authenticate a user against RACF via NC-PASS                   */
 /*                                                                  */
 /*   ------------------------------------------------------------   */
 /*   Usage:                                                         */
 /*     rc = RACauth(RACFid, Password, NewPassword, Token)           */
 /*                                                                  */
 /*     Where                                                        */
 /*     - RACFid and Password are the RACF ID and password we are    */
 /*       attempting to authenticate.                                */
 /*     - NewPassword is a new password.  Supplied only if changing  */
 /*       the RACF ID's password.                                    */
 /*     - Token is the value currently displayed on the security     */
 /*       token associated with this RACF ID                         */
 /*                                                                  */
 /*   ------------------------------------------------------------   */
 /*                                                                  */
 /*   Modification History:                                          */
 /*     29Jun2007 SYO   Initial version                              */
 /*                                                                  */
 /********************************************************************/

1RACauth: Procedure
          (Rsvd1, Rsvd2, Rsvd3, Rsvd4, Args, EvalBlk@)
          Options(Main,Reentrant,NoExecOps) Reorder;
   Declare
     Rsvd1                   Pointer,
     Rsvd2                   Pointer,
     Rsvd3                   Pointer,
     Rsvd4                   Pointer,
     Args                    Fixed Binary(31,0),
     EvalBlk@                Pointer,

     ArgUserID               Character(255) Varying Aligned Initial(''),
     ArgPassword             Character(255) Varying Aligned Initial(''),
     ArgNewPassword          Character(255) Varying Aligned Initial(''),
     ArgToken                Character(255) Varying Aligned Initial(''),

     Debug                   Bit(1) Aligned Initial(False),
     HiVal                   Pointer Aligned Initial(SysNull),
     I                       Fixed Binary(31,0) Initial(0),
     ParmOffset              Fixed Binary(31,0) Initial(0),
     ParmString              Character(1024) Aligned Initial(' '),
     Pau                     Bit(1) Aligned Initial(False),
     RC                      Fixed Binary(31,0) Initial(0),
     RClen                   Fixed Binary(31,0) Initial(0),
     RCodeData               Character(255) Aligned Initial(' '),
     RCodeLen                Fixed Binary(31,0) Initial(0),
     VarName                 Character(255) Aligned Initial(' '),
     VarValue                Character(255) Aligned Initial(' '),
     WorkArea                Character(4096) Aligned Initial(' '),
     WorkString              Character(255) Varying Aligned Initial(''),

     InitFunc                Character(8) Aligned Initial(' '),
     InitParmModName         Character(8) Aligned Initial(' '),
     InitParmListAddr        Pointer Aligned Initial(SysNull),
     InitUserFldAddr         Pointer Aligned Initial(SysNull),
     InitRsvdPtr             Pointer Aligned Initial(SysNull),
     InitEnvBlkAddr          Pointer Aligned Initial(SysNull),
     InitRsn                 Fixed Binary(31,0) Initial(0),
     InitStrgWkAddr          Pointer Aligned Initial(SysNull),
     InitRC                  Fixed Binary(31,0) Initial(0),
     InitECTaddr             Pointer Aligned Initial(SysNull),

     01 EvalBlk              Based(EvalBlk@),
       02 EvPad1             Fixed Binary(31,0) Unaligned,
       02 EvSize             Fixed Binary(31,0) Unaligned,
       02 EvLen              Fixed Binary(31,0) Unaligned,
       02 EvPad2             Fixed Binary(31,0) Unaligned,
       02 EvData             Character(255),

     01 ShVBlock             Unaligned,
       02 ShVNext            Fixed Binary(31,0) Initial(0),
       02 ShVUser            Fixed Binary(31,0) Initial(0),
       02 ShVCode            Character(1),
       02 ShVRet             Character(1),
       02 Filler             Character(2),
       02 ShVBufL            Fixed Binary(31,0),
       02 ShVNamA            Pointer,
       02 ShVNamL            Fixed Binary(31,0),
       02 ShVValA            Pointer,
       02 ShVValL            Fixed Binary(31,0),

     Addr                    Builtin,
     Length                  Builtin,
     Null                    Builtin,
     PLIRetC                 Builtin,
     PLIRetV                 Builtin,
     Repeat                  Builtin,
     Substr                  Builtin,
     SysNull                 Builtin,
     Translate               Builtin,
     Trim                    Builtin,
     Unspec                  Builtin,
     UpperCase               Builtin,
     Verify                  Builtin,

     IRXEXC                  Entry
                             External Options(Assembler,Inter,Retcode),

     IRXINIT                 Entry
                             External Options(Assembler,Inter,Retcode),

     SEXMITOS                Entry
                             External Options(Assembler,Inter,Retcode)
   ;

   Declare
     Sysprint                File Stream Print Output
   ;

   Declare
     Digits                  Character(10) Static Aligned
                             Initial('0123456789'),
     False                   Bit(1) Static Aligned Initial('0'B),
     True                    Bit(1) Static Aligned Initial('1'B)
   ;

 /*===================================================================*/
 /*   Initialization.                                                 */
 /*===================================================================*/
 Unspec(HiVal) = 'FFFFFFFF'BX;

 RC = 0;
 RClen = (EvalBlk.EvSize * 8) - 16;
 RCodeData = 'FAIL 9999';
 RCodeLen = 9;

 /* Process the arguments --------------------------------------------*/
 RC = ProcessArgs(Args);

 /* Load address of IRXxxxxx entry points ----------------------------*/
 If (RC = 0) Then
   Do;
     Fetch IRXINIT;
     Fetch IRXEXC;

     InitFunc = 'FINDENVB';
     InitParmModName = '        ';
     InitParmListAddr = SysNull;
     InitUserFldAddr = SysNull;
     InitRsvdPtr = SysNull;
     InitEnvBlkAddr = SysNull;
     InitRsn = 0;
     InitStrgWkAddr = SysNull;
     InitRC = 0;
     InitECTaddr = SysNull;
     Call IRXINIT(InitFunc, InitParmModName, InitParmListAddr,
          InitUserFldAddr, InitRsvdPtr, InitEnvBlkAddr, InitRsn,
          InitStrgWkAddr, InitRC, InitECTaddr);

     If (InitRsn ¬= 0) Then Do;
         RCodeData = ' ';
         Put String(RCodeData) Edit
           ('FAIL IRXINIT RC=',InitRC,' RSN=',InitRsn)
           (A,P'99999',A,P'99999');
         RCodeLen = 32;
         RC = 16;
         GoTo Quit;
       End;
   End  /*  of "If(RC=0)Then Do"  */;

 /*===================================================================*/
 /*   Process the request.                                            */
 /*===================================================================*/
 /* Do some sanity checks --------------------------------------------*/
   If (ArgUserID = '') Then Do;
       /*---------- 1   5   10   15   20   25 ----------*/
       RCodeData = 'FAIL Null RACF ID is invalid';
       RCodeLen = 28;
       RC = 16;
       GoTo Quit;
     End /* of "If(token-null)Then Do"  */;

 /* Authenticate the user --------------------------------------------*/
   WorkString = 'FUNCTION=LOGON' ||
                ',USERID=' || ArgUserID ||
                ',PASSWORD=' || ArgPassword;
   If (ArgToken ¬= '') Then Do;
       WorkString = WorkString ||
                ',PRN=' || ArgToken;
     End  /*  of "If(ArgToken¬='')Then Do"  */;
   If (ArgNewPassword ¬= '') Then Do;
       WorkString = WorkString ||
                ',NEWPASS=' || ArgNewPassword;
     End  /*  of "If(ArgNewPassword¬='')Then Do"  */;
   WorkString = WorkString ||
                ',XMSID=XMS1,APPL=RACAUTH,TERMID=' ||
                ArgUserID || ',RETAREA=';

   ParmOffset = Length(WorkString);

   ParmString = WorkString || Repeat(' ',63) || ';';

   Call SEXMITOS(ParmString, WorkArea);
   RC = PliRetV();

   If (Substr(ParmString, ParmOffset+1, 4) = '0000') Then Do;
       Put String(RCodeData) Edit
         ('OK ', RC, ' ', Substr(ParmString,ParmOffset+1,4))
         (A,P'9999',A,A);
       RCodeLen = 3 + 4 + 1 + 4;
       RC = 0;
     End  /*  of "If(RC=0)Then Do"  */;
   Else Do;
       Put String(RCodeData) Edit
         ('FAIL ', RC, ' ', Substr(ParmString,ParmOffset+1,4))
         (A,P'9999',A,A);
       RCodeLen = 5 + 4 + 1 + 4;
       RC = 16;
     End  /*  of "If(RC=0)Then...Else Do"  */;

 /*===================================================================*/
 /*   Clean up and quit.                                              */
 /*===================================================================*/
 /* Set return code in Evaluation Block ------------------------------*/
 Quit:
   If (RClen > RCodeLen) Then Do;
       EvalBlk.EvData = Substr(RCodeData, 1, RCodeLen);
       EvalBlk.EvLen = RCodeLen;
     End  /*  of "If(RClen>RCodeLen)Then Do"  */;
   Else Do;
       EvalBlk.EvData = '-1';
       EvalBlk.EvLen = 2;
     End;

   /* Set R15 --------------------------------------------------------*/
   Call PliRetC(0);

   /* Quit -----------------------------------------------------------*/
   Return;

1/*********************************************************************/
 /*                                                                   */
 /*   ProcessArgs                                                     */
 /*   -------------------------------------------------------------   */
 /*   Process the input arguments.                                    */
 /*                                                                   */
 /*********************************************************************/

   ProcessArgs:  Procedure(Args) Options(Reentrant) Reorder
                 Returns(Fixed Binary(31,0));
     Declare
       Args                  Fixed Binary(31,0),

       Arg#                  Fixed Binary(31,0) Initial(0),
       I                     Fixed Binary(31,0) Initial(0),
       Pau                   Bit(1) Aligned Initial(False),
       WorkStr               Character(255) Varying Aligned Initial(''),

       ArgStrPtr             Pointer Aligned Initial(Null),
       01 ArgStr(255)        Based(ArgStrPtr),
         02 Ptr              Pointer Unaligned,
         02 Len              Fixed Binary(31,0) Unaligned,
       ArgData               Character(255) Based,

       RC                    Fixed Binary(31,0) Initial(0)
     ;

     ArgStrPtr = Addr(Args);

     Pau = False;
     Arg# = 0;
     I = 0;
     Do While(¬ Pau);
       I = I + 1;

       If (ArgStr(I).Ptr = HiVal) Then
         Pau = True;
       Else Do;
           WorkStr = Substr(ArgStr(I).Ptr->ArgData,1,ArgStr(I).Len);

           Arg# = Arg# + 1;
           Select (Arg#);
             When (1)  Do;
                 If (Substr(WorkStr, 1, 1) = '*') Then Do;
                     Debug = True;
                     WorkStr = Substr(WorkStr, 2);
                   End;
                 ArgUserID = UpperCase(WorkStr);
               End  /*  of "When(1) Do"  */;
             When (2)  Do;
                 ArgPassword = WorkStr;
                 /* don't UPPERCASE after move to z/OS 1.7 */
                 ArgPassword = UpperCase(ArgPassword);
               End  /*  of "When(2) Do"  */;
             When (3)  Do;
                 ArgNewPassword = WorkStr;
                 /* don't UPPERCASE after move to z/OS 1.7 */
                 ArgNewPassword = UpperCase(ArgNewPassword);
               End  /*  of "When(3) Do"  */;
             When (4)  Do;
                 ArgToken = WorkStr;
                 If (Verify(ArgToken, Digits) ¬= 0) Then Do;
                     /*---------- 1   5   10   15   20   25 ----------*/
                     RCodeData = 'FAIL Token not numeric';
                     RCodeLen = 22;
                     RC = 16;
                     Pau = True;
                   End /* of "If(token-not-numeric)Then Do"  */;
               End  /*  of "When(4) Do"  */;
             Otherwise Do;
                 /*---------- 1   5   10   15   20   25 --------------*/
                 RCodeData = 'FAIL too many arguments';
                 RCodeLen = 23;
                 RC = 16;
                 Pau = True;
               End  /*  of "Otherwise Do"  */;
           End  /*  of "Select(Arg#)"  */;
         End  /*  of "If(not-end-of-args)Then...Else Do"  */;
     End  /*  of "Do While(¬Pau)"  */;

     Return(RC);
   End  /*  of procedure "ProcessArgs"  */;


 End  /*  of Procedure "RACauth" */;
