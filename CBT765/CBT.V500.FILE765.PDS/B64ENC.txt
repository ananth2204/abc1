*Process Margins(2,72,1),Nonumber,Optimize(TIME),A,X,Ag,System(MVS);
 /********************************************************************/
 /*                                                                  */
 /*   B64enc                                                         */
 /*   ------------------------------------------------------------   */
 /*   Encode an arbitrary string into Base64 format.                 */
 /*                                                                  */
 /*   ------------------------------------------------------------   */
 /*   Usage:                                                         */
 /*     outString = B64enc(inString)                                 */
 /*                                                                  */
 /*   Where:                                                         */
 /*     inString is an arbitrary string up to 24,567 bytes in        */
 /*     length.  outString will be the Base64-encoded inString.      */
 /*                                                                  */
 /*   ------------------------------------------------------------   */
 /*                                                                  */
 /*   Modification History:                                          */
 /*     20Aug2007 SYO   Initial version                              */
 /*                                                                  */
 /********************************************************************/

1B64enc: Procedure
         (Rsvd1, Rsvd2, Rsvd3, Rsvd4, Args, EvalBlk@@)
         Options(Main,Reentrant,NoExecOps) Reorder;
   Declare
     Rsvd1                   Pointer,
     Rsvd2                   Pointer,
     Rsvd3                   Pointer,
     Rsvd4                   Pointer,
     Args                    Fixed Binary(31,0),
     EvalBlk@@               Pointer,

     ArgIn                   Character(32756) Varying Aligned Init(''),

     Bits24                  Bit(24) Aligned Initial('000000'BX),
     Debug                   Bit(1) Aligned Initial(False),
     HiVal                   Pointer Aligned Initial(SysNull),
     I                       Fixed Binary(31,0) Initial(0),
     Index4(4)               Fixed Binary(15,0) Initial((4)(0)),
     ParmOffset              Fixed Binary(31,0) Initial(0),
     ParmString              Character(1024) Aligned Initial(' '),
     Pau                     Bit(1) Aligned Initial(False),
     RC                      Fixed Binary(31,0) Initial(0),
     RClen                   Fixed Binary(31,0) Initial(0),
     RCodeData               Character(32756) Varying Aligned Init(''),
     RCodeLen                Fixed Binary(31,0) Initial(0),
     VarName                 Character(255) Aligned Initial(' '),
     VarValue                Character(255) Aligned Initial(' '),
     WorkArea                Character(4096) Aligned Initial(' '),
     WorkString              Character(255) Varying Aligned Initial(''),

     InitFunc                Character(8) Aligned Initial(' '),
     InitParmModName         Character(8) Aligned Initial(' '),
     InitParmListAddr        Pointer Aligned Initial(SysNull),
     InitUserFldAddr         Pointer Aligned Initial(SysNull),
     InitRsvdPtr             Pointer Aligned Initial(SysNull),
     InitEnvBlkAddr          Pointer Aligned Initial(SysNull),
     InitRsn                 Fixed Binary(31,0) Initial(0),
     InitStrgWkAddr          Pointer Aligned Initial(SysNull),
     InitRC                  Fixed Binary(31,0) Initial(0),
     InitECTaddr             Pointer Aligned Initial(SysNull),

     RltFunc                 Character(8) Aligned Initial(' '),
     RltEvalBlk@             Pointer Aligned Initial(SysNull),
     RltRClen                Fixed Binary(31,0) Initial(0),
     RltEnvBlk@              Pointer Aligned Initial(SysNull),
     RltRC                   Fixed Binary(31,0) Initial(0),

     EvalBlk@                Pointer Aligned Initial(SysNull),
     01 EvalBlk              Based(EvalBlk@),
       02 EvPad1             Fixed Binary(31,0) Unaligned,
       02 EvSize             Fixed Binary(31,0) Unaligned,
       02 EvLen              Fixed Binary(31,0) Unaligned,
       02 EvPad2             Fixed Binary(31,0) Unaligned,
       02 EvData             Character(32756),

     01 ShVBlock             Unaligned,
       02 ShVNext            Fixed Binary(31,0) Initial(0),
       02 ShVUser            Fixed Binary(31,0) Initial(0),
       02 ShVCode            Character(1),
       02 ShVRet             Character(1),
       02 Filler             Character(2),
       02 ShVBufL            Fixed Binary(31,0),
       02 ShVNamA            Pointer,
       02 ShVNamL            Fixed Binary(31,0),
       02 ShVValA            Pointer,
       02 ShVValL            Fixed Binary(31,0),

     Addr                    Builtin,
     Length                  Builtin,
     Null                    Builtin,
     PLIRetC                 Builtin,
     PLIRetV                 Builtin,
     Repeat                  Builtin,
     Substr                  Builtin,
     SysNull                 Builtin,
     Translate               Builtin,
     Trim                    Builtin,
     Unspec                  Builtin,
     UpperCase               Builtin,
     Verify                  Builtin,

     IRXEXC                  Entry
                             External Options(Assembler,Inter,Retcode),
     IRXINIT                 Entry
                             External Options(Assembler,Inter,Retcode),
     IRXRLT                  Entry
                             External Options(Assembler,Inter,Retcode)
   ;

   Declare
     Sysprint                File Stream Print Output
   ;

   Declare
     B64alphabet             Character(65) Static Aligned Initial
                             (('ABCDEFGHIJKLMNOPQRSTUVWXYZ' ||
                               'abcdefghijklmnopqrstuvwxyz' ||
                               '0123456789' ||
                               '+/=')),
     False                   Bit(1) Static Aligned Initial('0'B),
     True                    Bit(1) Static Aligned Initial('1'B)
   ;

 /*===================================================================*/
 /*   Initialization.                                                 */
 /*===================================================================*/
 EvalBlk@ = EvalBlk@@;
 Unspec(HiVal) = 'FFFFFFFF'BX;

 RC = 0;
 RClen = (EvalBlk.EvSize * 8) - 16;
 RCodeData = 'FAIL 9999';
 RCodeLen = Length(RCodeData);

 /* Process the arguments --------------------------------------------*/
 RC = ProcessArgs(Args);
 If (RC ¬= 0) Then Go To Quit;
 If (Length(ArgIn) > 24566) Then Do;
     RcodeData = 'FAIL Input length exceeds maximum 24,566 bytes';
     RCodeLen = Length(RCodeData);
     Go To Quit;
   End;

 /* Load address of IRXxxxxx entry points ----------------------------*/
 If (RC = 0) Then
   Do;
     Fetch IRXINIT;
     Fetch IRXEXC;
     Fetch IRXRLT;

     InitFunc = 'FINDENVB';
     InitParmModName = '        ';
     InitParmListAddr = SysNull;
     InitUserFldAddr = SysNull;
     InitRsvdPtr = SysNull;
     InitEnvBlkAddr = SysNull;
     InitRsn = 0;
     InitStrgWkAddr = SysNull;
     InitRC = 0;
     InitECTaddr = SysNull;
     Call IRXINIT(InitFunc, InitParmModName, InitParmListAddr,
          InitUserFldAddr, InitRsvdPtr, InitEnvBlkAddr, InitRsn,
          InitStrgWkAddr, InitRC, InitECTaddr);

     If (InitRsn ¬= 0) Then Do;
         Put String(RCodeData) Edit
           ('FAIL IRXINIT RC=',InitRC,' RSN=',InitRsn)
           (A,P'99999',A,P'99999');
         RCodeLen = Length(RCodeData);
         GoTo Quit;
       End;
   End  /*  of "If(RC=0)Then Do"  */;

 /*===================================================================*/
 /*   Encode the input string.                                        */
 /*===================================================================*/
 RCodeData = '';
 Do While(ArgIn ¬= '');
   /* Extract the next 3 input characters ----------------------------*/
   Bits24 = '000000'BX;
   Select(Length(ArgIn));
     When (1) Do;
         Substr(Bits24,1,8) = Unspec(Substr(ArgIn,1,1));
         ArgIn = '';
         Unspec(Index4(1)) = '00'BX || '00'B || Substr(Bits24,1,6);
         Unspec(Index4(2)) = '00'BX || '00'B || Substr(Bits24,7,6);
         Index4(3) = 64;
         Index4(4) = 64;
       End;
     When (2) Do;
         Substr(Bits24,1,16) = UnSpec(Substr(ArgIn,1,2));
         ArgIn = '';
         Unspec(Index4(1)) = '00'BX || '00'B || Substr(Bits24,1,6);
         Unspec(Index4(2)) = '00'BX || '00'B || Substr(Bits24,7,6);
         Unspec(Index4(3)) = '00'BX || '00'B || Substr(Bits24,13,6);
         Index4(4) = 64;
       End;
     Otherwise Do;
         Bits24 = Unspec(Substr(ArgIn,1,3));
         If (Length(ArgIn) ¬= 3) Then
           ArgIn = Substr(ArgIn, 4);
         Else
           ArgIn = '';
         Unspec(Index4(1)) = '00'BX || '00'B || Substr(Bits24,1,6);
         Unspec(Index4(2)) = '00'BX || '00'B || Substr(Bits24,7,6);
         Unspec(Index4(3)) = '00'BX || '00'B || Substr(Bits24,13,6);
         Unspec(Index4(4)) = '00'BX || '00'B || Substr(Bits24,19,6);
       End;
   End  /*  of "Select"  */;

   /* Output as 4 bytes ----------------------------------------------*/
   Do I = 1 To 4;
     RCodeData = RCodeData ||
                 Substr(B64alphabet, Index4(I)+1, 1);
   End;
 End  /*  of "Do While(ArgIn¬='')"  */;
 RCodeLen = Length(RCodeData);

 /*===================================================================*/
 /*   Clean up and quit.                                              */
 /*===================================================================*/
 /* Set return data in Evaluation Block ------------------------------*/
 Quit:
   If (RClen < RCodeLen) Then Do;
       RltFunc = 'GETBLOCK';
       RltEvalBlk@ = SysNull;
       RltRClen = ((Trunc(RCodeLen / 8) + 1) * 8) + 16;
       RltEnvBlk@ = SysNull;
       RltRC = 0;
       Call IRXRLT(RltFunc, RltEvalBlk@, RltRClen, RltEnvBlk@, RltRC);
       EvalBlk@ = RltEvalBlk@;
     End;
   Substr(EvalBlk.EvData, 1, RCodeLen) = RCodeData;
   EvalBlk.EvLen = RCodeLen;

   /* Set R15 --------------------------------------------------------*/
   Call PliRetC(0);

   /* Quit -----------------------------------------------------------*/
   Return;

1/*********************************************************************/
 /*                                                                   */
 /*   ProcessArgs                                                     */
 /*   -------------------------------------------------------------   */
 /*   Process the input arguments.                                    */
 /*                                                                   */
 /*********************************************************************/

   ProcessArgs:  Procedure(Args) Options(Reentrant) Reorder
                 Returns(Fixed Binary(31,0));
     Declare
       Args                  Fixed Binary(31,0),

       Arg#                  Fixed Binary(31,0) Initial(0),
       I                     Fixed Binary(31,0) Initial(0),
       Pau                   Bit(1) Aligned Initial(False),
       WorkStr               Character(32756) Varying Aligned Init(''),

       ArgStrPtr             Pointer Aligned Initial(Null),
       01 ArgStr(255)        Based(ArgStrPtr),
         02 Ptr              Pointer Unaligned,
         02 Len              Fixed Binary(31,0) Unaligned,
       ArgData               Character(255) Based,

       RC                    Fixed Binary(31,0) Initial(0)
     ;

     ArgStrPtr = Addr(Args);

     Pau = False;
     Arg# = 0;
     I = 0;
     Do While(¬ Pau);
       I = I + 1;

       If (ArgStr(I).Ptr = HiVal) Then
         Pau = True;
       Else Do;
           WorkStr = Substr(ArgStr(I).Ptr->ArgData,1,ArgStr(I).Len);

           Arg# = Arg# + 1;
           Select (Arg#);
             When (1)  Do;
                 ArgIn = WorkStr;
               End  /*  of "When(1) Do"  */;
             Otherwise Do;
                 /*---------- 1   5   10   15   20   25 --------------*/
                 RCodeData = 'FAIL too many arguments';
                 RCodeLen = Length(RCodeData);
                 RC = 16;
                 Pau = True;
               End  /*  of "Otherwise Do"  */;
           End  /*  of "Select(Arg#)"  */;
         End  /*  of "If(not-end-of-args)Then...Else Do"  */;
     End  /*  of "Do While(¬Pau)"  */;

     Return(RC);
   End  /*  of procedure "ProcessArgs"  */;


 End  /*  of Procedure "B64enc" */;
