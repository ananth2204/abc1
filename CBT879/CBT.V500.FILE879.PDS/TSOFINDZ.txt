TSOFIND  CSECT
         B     12(0,15)
         DC    AL1(7)
         DC    CL7'TSOFIND'
         STM   14,12,12(13)
         USING TSOFIND,12,11
         LR    12,15
         LR    15,13
         CNOP  0,4
         BAL   13,*+76
         DC    18F'0'
         ST    15,4(13)
         ST    13,8(15)
         LA    11,1(12,15)
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
BEFORE   NI    DASDSW,X'00'                        CLEAR SWITCH
         NI    SEEK,X'00'                          CLEAR SWITCH
         NI    DCBSW,X'00'                         CLEAR SWITCH
         NI    SOME,X'00'                          CLEAR SWITCH
         NI    FOUNDSW,X'00'                       CLEAR SWITCH
WTOR1    EQU   *
         TPUT  FIND01,L'FIND01                     PROMPT FOR DSN
         LA    R2,DSNAME
         MVI   0(R2),C' '                          BLANK
         MVC   1(43,R2),0(R2)                       OUT PART
         MVC   FIND13+22(7),=C'IS    '               OF GDG MESSAGE
         TGET  DSNAME,L'DSNAME                     GET DSN
         NI    SEEK,X'00'                          CLEAR SWITCH
         OC    DSNAME,=44X'40'                     UPPERCASE REPLY
         CLC   =C'END',DSNAME                      ARE WE FINISHED
         BE    FINISH
GOOD     EQU   *
         NI    GDG,X'00'                           CLEAR SWITCH
GDGTEST  LA    R7,44
         LA    R6,DSNAME
LHB      CLI   0(R6),C'('                          IS
         BNE   INCR1                                DSN
         OI    GDG,X'FF'                             A
         CLI   1(R6),C'+'                             GDG
         BNE   INCR1
         OI    SEEK,X'FF'                          GDG +
INCR1    LA    R6,1(R6)
         BCT   R7,LHB
         NI    TWNTY1,X'00'
         SR    R15,R15
         LOCATE NAMED                               GO SEARCH CATALOG
         C     R15,=F'36'
         BH    ERR10
         B     TABLE(R15)                       RC  BRANCH TABLE
TABLE    B     FOUND                           (0)  GOT SOMETHING
         B     ERR1                            (4)  CVOL NOT MOUNTED
         B     ERR2                            (8)  NOT FOUND
ERR3     NOP   ERR3                            (12) INDEX/ALIAS FOUND
         B     ERR4                            (16) INDEX NOT DS FOUND
         B     ERR5                            (20) DSN SYNTAX ERROR
         B     ERR6                            (24) CATALOG I/O ERROR
ERR7     NOP   ERR7                            (28) TTR OUT OF RANGE
ERR8     NOP   ERR8                            (32) AREA NOT DOUBLEWORD
         NOP   ERR8                            (36) VSAM
ERR10    NOP   ERR8                            (38) DFSMSHSM ERROR
ERR1     EQU   *
         CVD   R15,DOUBLE
         OI    DOUBLE+7,X'0F'
         UNPK  FIND16+25(3),DOUBLE+6(2)
         MVI   FIND16+25,C' '
         TPUT  FIND16,L'FIND16
         B     FINISH
         MVC   FIND15+14(6),AREA+259
WTOC     TPUT  FIND15,L'FIND15
         B     FINISH
ER7      EQU   *
         ABEND 44,DUMP
ERR6     EQU   *
         TPUT  FIND11,L'FIND11
ERR5     EQU   *
         TPUT  FIND06,L'FIND06
         B     WTOR1
ERR4     EQU   *
         TPUT  FIND10,L'FIND10
         B     WTOR1
FIND01   DC    C'FIND=> ENTER DSN '
FIND15   DC    C'FIND=> CVOL = XXXXXX NOT MOUNTED'
FIND16   DC    C'FIND=> LOCATE RETURN CODE NN'
FIND11   DC    C'FIND=> PERM I/O ERROR WHILE READING CATALOG'
FIND06   DC    C'FIND=> SYNTAX FAIL OF DSN   '
FIND10   DC    C'FIND=> INDEX LEVEL NOT DATASET FOUND'
FIND1A   DC    C'FIND=> ENTER OPTION (S)EARCH, (D)CB, (N)O, OR (E)ND'
FIND08   DC    C'FIND=> CATALOGD TO OTHER THAN 3400/3330'
FIND09   DC    C'FIND=> XXXXXX     '
FIND13   DC    C'FIND=> GENERATION NO. IS       XXXXXXXX'
FIND05   DC    C'FIND=> DSN CATALOGD TO XXXXXX    '
FIND03   DC    C'FIND=> DSN NOT CATALOGD '
FIND38   DC    C'FIND=> NOT PERMITTED TO LOCATE DSN'
FIND07   DC    C'FIND=> DSN NOT FOUND ANYWHERE '
FIND12   DC    C'FIND=> I/O ERROR ON XXXXXX '
FIND04   DC    C'FIND=> DSN FOUND ON XXXXXX            '
FIND1B   DC    C'FIND=> DCB=(RECFM=                                    C
                             '
FIND1C   DC    C'FIND=> OPTIONS ARE - SEARCH,DCB,NO,END'

         B     WTOR2
WTOR2    TPUT  FIND1A,L'FIND1A
         LA    R3,OPTION
         MVI   0(R3),C' '
         MVC   1(7,R3),0(R3)
         NI    DCBSW,X'00'
         NI    SEEK,X'00'
         NI    SOME,X'00'
         TGET  OPTION,L'OPTION
         LR    R2,R1
         BCTR  R2,0
         OC    OPTION,=8X'40'
         LA    R1,OPTION
         EX    R2,CEND
         BE    FINISH
         EX    R2,CNO
         BE    BEFORE
         EX    R2,CHELP
         BE    HELPWTO
         EX    R2,CSEARCH
         BE    SEARCHIT
         CLI   DASDSW,X'FF'
         BNE   WTOR2
         EX    R2,CDCB
         BE    SEADCB
         B     WTOR2
FOUND    OI    FOUNDSW,X'FF'
         CLI   SEEK,X'FF'
         BE    ISITGDG
         TM    AREA+4,X'80' TAPE
         BO    T3400
         TM    AREA+4,X'20' DASD
         BNO   WT08
         OI    DASDSW,X'FF'
         B     D3330
WT08     TPUT  FIND08,L'FIND08
         B     WTOR1
ZERO     EQU   *
         ABEND 32,DUMP
T3400    NI    DASDSW,X'00'
         CLC   =X'0000',AREA
         BE    ZERO
         SR    R2,R2
         LH    R2,AREA
TFG      CLC   AREA(2),=X'0015'
         BL    DONT
         OI    TWNTY1,X'FF'                    MORE THAN 20 VOLUMES
         LH    R2,=H'20'                       1ST 20 IN THIS LIST
DONT     EQU   *
         LA    R3,AREA+6
MOVES    MVC   FIND09+7(6),0(R3)
         LA    R3,12(R3)
WTOX     TPUT  FIND09,L'FIND09
         BCT   R2,MOVES
         AGO   .TAPE1  SKIP OLD SYSCTLG CODE - CAN'T DO MORE THAN 20
         CLI   TWNTY1,X'FF'
         BNE   ISITGDG
         NI    TWNTY1,X'00'
         MVC   TTR(3),AREA+252
         MVC   VOLSER(6),AREA+259
         LOCATE OTHERCAT
         B     TABLE2(R15)                     RC  BRANCH TABLE
TABLE2   B     T3400                          (0)
         B     ERR1                           (4)  CVOL NOT MOUNTED
ER2      NOP   ER2                            (8)  NOT FOUND
ER3      NOP   ER3                            (12) INDEX/ALIAS FOUND
ER4      NOP   ER4                            (16) INDEX NOT DS FOUND
ER5      NOP   ER5                            (20) DSN SYNTAX ERROR
         B     ERR6                           (24) CATALOG I/O ERROR
         B     ER7                            (28) TTR OUT OF RANGE
ER8      NOP   ER8                            (32) AREA NOT DOUBLEWORD
.TAPE1   ANOP
ISITGDG  EQU   *
         NI    TWNTY1,X'00'
         CLI   GDG,X'FF'
         BNE   WTOR2
         LA    R6,DSNAME+44
COMP     CLI   0(R6),C'.'     LOOK FOR LAST DOT
         BE    DOT
         BCT   R6,COMP
DOT      MVC   FIND13+30(9),0(R6)
         CLI   SEEK,X'FF'     WAS THERE A '+'
         BNE   WTOG
         MVC   FIND13+22(7),=C'WILL BE'
WTOG     TPUT  FIND13,L'FIND13
         B     WTOR2
D3330    EQU   *
         LA    R3,AREA+6
MOVEZ    MVC   FIND05+23(6),0(R3)
WTOZ     TPUT  FIND05,L'FIND05
         B     ISITGDG
ERR2     NI    DASDSW,X'00'
         C     R0,=A(X'38')                  NOT AUTH
         BNE   WT03
         TPUT  FIND38,L'FIND38
         B     WT03A
WT03     TPUT  FIND03,L'FIND03
WT03A    NI    FOUNDSW,X'00'
         NI    SEEK,X'00'
         NI    SOME,X'00'
         B     WTOR2
SEADCB   OI    DCBSW,X'FF'
         B     TCVT
SEARCHIT NI    DCBSW,X'00'
TCVT     L     R2,CVTPTR                     CVT
         TM    CVTDCB-CVTMAP(R2),X'80'       XA OR LATER, CVTMVSE
         BO    UCBXA                          YES, BRANCH
         L     R2,40(,R2)                    PRE-XA CVTILK2 -> 1ST UCB
MORE     CLC   0(2,R2),=X'FFFF'              IS IT LAST UCB
         BNE   OTHER
ENDSCAN  CLI   SOME,X'FF'                    FOUND ON ANY VOLUME?
         BE    WTOR2                         YES, BRANCH
         NI    DASDSW,X'00'                  NO, SAY NOT FOUND
         NI    DCBSW,X'00'
         NI    FOUNDSW,X'00'
         TPUT  FIND07,L'FIND07
         B     WTOR2
OTHER    CLC   0(2,R2),=X'0000'
         BNE   GETUCB
INCR     LTR   R2,R2
         BZ    INCRXA
         LA    R2,2(R2)
         B     MORE
GETUCB   LH    R4,0(R2)
         N     R4,=X'0000FFFF'
         TM    18(R4),X'20'                   IS IT DASD
         BZ    INCR
         TM    3(R4),X'80'
         BZ    INCR
         CLC   28(6,R4),=X'000000000000'
         BE    INCR
         MVC   VOLSER(6),28(R4)               UCBVOLI
         B     OKVOL
UCBXA    SR    R2,R2                          XA OR LATER
         XC    DEVWORK(100),DEVWORK
INCRXA   UCBSCAN COPY,WORKAREA=DEVWORK,DEVCLASS=DASD,RANGE=ALL,        X
               DEVNCHAR=DEVNCHAR,UCBAREA=DEVAREA,DYNAMIC=YES
         LTR   R15,R15
         BNZ   ENDSCAN
         LA    R4,DEVAREA
         TM    3(R4),X'80'                    UCBSTAT,UCBONLI
         BZ    INCRXA                         BRANCH IF NOT ONLINE
         MVC   VOLSER(6),28(R4)               UCBVOLI
OKVOL    MVC   FIND12+20(6),VOLSER
         LA    R1,AREA1
         MVI   0(R1),C' '
         MVC   1(147,R1),0(R1)
         OBTAIN PACDSCB
         B     TABLEA(R15)                     RC  BRANCH TABLE
TABLEA   B     GOTIT                          (0)
WRONG1   NOP   WRONG1                         (4)  VOL NOT MOUNTED
         B     INCR                           (8)  NOT FOUND
         B     WRONG3                         (12) I/O ERROR ON VOLUME
WRONG4   NOP   WRONG4                         (16) INVALID AREA
WRONG5   NOP   WRONG5                         (20) CCHH NOT IN VTOC
WRONG6   NOP   WRONG6                         (24) FORMAT 8 AND NOTOK
WRONG7   NOP   WRONG7                         (28) ERR IN EADSCB=NOTOK
WRONG3   EQU   *
WTOM     TPUT  FIND12,L'FIND12
         ABEND 77,DUMP
GOTIT    EQU   *
         LA    R3,AREA1+1
MOVEY    MVC   FIND04+20(6),VOLSER
WTOY     TPUT  FIND04,L'FIND04
         OI    SOME,X'FF'
         OI    DASDSW,X'FF'
         CLI   DCBSW,X'FF'
         BNE   INCR
DROPSY   LA    R7,FIND1B+19
         MVI   0(R7),C' '
         MVC   1(42,R7),0(R7)
         LA    R8,AREA1+40
         LA    R7,FIND1B+18
TU       TM    0(R8),X'C0'
         BO    MU
TF       TM    0(R8),X'80'
         BO    MF
TV       TM    0(R8),X'40'
         BO    MV
TT       TM    0(R8),X'20'
         BO    MT
TB       TM    0(R8),X'10'
         BO    MB
TS       TM    0(R8),X'08'
         BO    MS
TA       TM    0(R8),X'04'
         BO    MA
TM       TM    0(R8),X'02'
         BO    MM
         B     MCOM
MU       MVI   0(R7),C'U'
         LA    R7,1(R7)
         B     TT
MF       MVI   0(R7),C'F'
         LA    R7,1(R7)
         B     TT
MV       MVI   0(R7),C'V'
         LA    R7,1(R7)
         B     TT
MT       MVI   0(R7),C'T'
         LA    R7,1(R7)
         B     TB
MB       MVI   0(R7),C'B'
         LA    R7,1(R7)
         B     TS
MS       MVI   0(R7),C'S'
         LA    R7,1(R7)
         B     TA
MA       MVI   0(R7),C'A'
         LA    R7,1(R7)
         B     TM
MM       MVI   0(R7),C'M'
         LA    R7,1(R7)
MCOM     MVC   0(9,R7),=C',BLKSIZE='
         LA    R7,9(R7)
BLKSIZE  LH    R6,AREA1+42
         CLC   =X'0000',AREA1+42
         BE    NONE
         CVD   R6,DOUBLE
         MVC   WORK,PATTERN
         ED    WORK,DOUBLE+4
         LA    R4,WORK
COMPA    CLI   0(R4),C'*'
         BNE   NOTAST
         LA    R4,1(R4)
         B     COMPA
NOTAST   LA    R5,WORK+8
         SR    R5,R4
         BCTR  R5,0
         EX    R5,MOVEIT
         LA    R7,1(R5,R7)
NONE     MVC   0(7,R7),=C',LRECL='
         LA    R7,7(R7)
LRECL    LH    R6,AREA1+44
         CLC   =X'0000',AREA1+44
         BE    NON
         CVD   R6,DOUBLE
         MVC   WORK,PATTERN
         ED    WORK,DOUBLE+4
         LA    R4,WORK
COMPB    CLI   0(R4),C'*'
         BNE   NOTASTR
         LA    R4,1(R4)
         B     COMPB
NOTASTR  LA    R5,WORK+8
         SR    R5,R4
         BCTR  R5,0
         EX    R5,MOVEIT
         LA    R7,1(R5,R7)
NON      MVC   0(7,R7),=C',DSORG='               START DOING DSORG
         LA    R7,7(R7)
DSORG    LA    R8,AREA1+38
TIS      TM    0(R8),X'80'                       TEST FOR IS
         BO    MIS
TPS      TM    0(R8),X'40'                       TEST FOR PS
         BO    MPS
TDA      TM    0(R8),X'20'                       TEST FOR DA
         BO    MDA
TPO      TM    0(R8),X'02'                       TEST FOR PO
         BO    MPO
TUU      TM    0(R8),X'01'                       TEST FOR U
         BO    MUU
         B     MRHB
MIS      MVC   0(2,R7),=C'IS'                    SET IS
         LA    R7,2(R7)
         B     TUU
MPS      MVC   0(2,R7),=C'PS'                    SET PS
         LA    R7,2(R7)
         B     TUU
MDA      MVC   0(2,R7),=C'DA'                    SET DA
         LA    R7,2(R7)
         B     TUU
MPO      MVC   0(2,R7),=C'PO'                    SET PO
         LA    R7,2(R7)
         B     TUU
MUU      MVI   0(R7),C'U'                        SET U
         LA    R7,1(R7)
MRHB     MVI   0(R7),C')'                        END BRACKET
DCBWTO   TPUT  FIND1B,L'FIND1B                   WRITE DCB INFO
         B     INCR
PATTERN  DC    X'5C20202020202120'
DOUBLE   DS    D
MOVEIT   MVC   0(0,R7),0(R4)
WORK     DS    D
HELPWTO  TPUT  FIND1A,L'FIND1A
         B     WTOR2
* CONSTANTS IN THESE EX/CLC INSTRUCTIONS ARE DEFINED AS 8 BYTES LONG
* TO AVOID S0C4 ADDRESSING ABENDS
CEND     CLC   0(0,R1),=C'END     '
CNO      CLC   0(0,R1),=C'NO      '
CDCB     CLC   0(0,R1),=C'DCB     '
CSEARCH  CLC   0(0,R1),=C'SEARCH  '
CHELP    CLC   0(0,R1),=C'HELP    '
REPLY    DC    CL1' '
ANS      DC    CL1' '
TWNTY1   DC    X'00'
FOUNDSW  DC    X'00'
DASDSW   DC    X'00'
SEEK     DC    X'00'
SOME     DC    X'00'
GDG      DC    X'00'
DCBSW    DC    X'00'
DSNAME   DC    CL44' '
OPTION   DC    CL8' '
VOLSER   DC    CL6'XXXXXX'
AREA     DS    0D
         DS    265C' '
AREA1    DS    0D
         DS    148C' '
TTR      DC    H'0'
         DC    X'00'
NAMED    CAMLST NAME,DSNAME,,AREA
PACDSCB  CAMLST SEARCH,DSNAME,VOLSER,AREA1
         AGO   .TAPE2    SKIP OLD SYSCTLG CODE TO READ CATALOG BY TTR
OTHERCAT CAMLST BLOCK,TTR,VOLSER,AREA
.TAPE2   ANOP
FINISH   EQU   *
         L     13,4(13)
         LM    14,12,12(13)
         LA    15,0(0,0)
         BR    14
DEVAREA  DC    0D'0',12F'0'
DEVWORK  DC    0D'0',25F'0'
DEVNCHAR DC    CL4' '
*        CVT   DSECT=YES
CVTPTR   EQU   16
CVT      DSECT
CVTMAP   EQU   *
         ORG   CVT+116
CVTDCB   DS    XL1
         END
