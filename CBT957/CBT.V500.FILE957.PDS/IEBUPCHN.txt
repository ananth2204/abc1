IEBUPCHN TITLE 'I E B U P C H N  ***  FORMAT IEBUPDTX DIRECTORY CHAINS'
*  MACRO TO GENERATE SUBROUTINE ENTRANCE LINKAGE.  SPECIFY NAME OF
*  18 WORD SAVE AREA.
*
         MACRO
&N       ENTER &SA
&N       SAVE  (14,12),,*
         LR    R12,R15
         AIF   (T'&N EQ 'O').NOEP
         USING &N,R12
         AGO   .EPDONE
.NOEP    USING &SYSECT,R12
.EPDONE  ANOP
         AIF   (T'&SA EQ 'O').NOSA
         LR    R15,R13
         LA    R13,&SA
         ST    R15,4(,R13)
         ST    R13,8(,R15)
.NOSA    MEND
*
*
*  GENERATE CALLING SEQUENCE TO INTERNAL SUBROUTINES.
*
         MACRO
&N       XCAL  &RTN,&A
&N       L     R15,=A(&RTN)
         AIF   (T'&A EQ 'O').B
         AIF   ('&A'(1,1) EQ '(').REG
         LA    R1,&A
         AGO   .B
.REG     LR    R1,&A(1)
.B       BALR  R14,R15
         MEND
*
         PRINT NOGEN                                             82067
IEBUPCHN START 0                                                 82067
         REGEQU ,                                                80002
         ENTER SA1
         L     R11,DATAADR        ADDRESS COMMON DATA AREA
         USING DATAAREA,R11
*
         XPROPEN PWA              OPEN SYSPRINT
         BXLE  R15,R15,PRTOPEN                                   82067
         WTO   '*** SYSPRINT DD NOT AVAILABLE ***',ROUTCDE=11    82067
         ABEND 1403                                              82067
         SPACE 1                                                 82067
PRTOPEN  LA    R2,DEFDDNAM   POINT TO LIBRARY DDNAMES            82067
         LA    R3,DEFDDNUM   GET NUMBER OF ENTRIES               82067
DEFDDLUP MVC   DCBDDNAM-IHADCB+DIRDCB,0(R2)  TRY DD NAME         82067
         DEVTYPE DCBDDNAM-IHADCB+DIRDCB,DB                       82067
         BXH   R15,R15,DEFDDBUM                                  82067
         CLI   DB+2,X'20'    DIRECT ACCESS ?                     82067
         BNE   DEFDDBUM                                          82067
         RDJFCB (DIRDCB)     TRY THE JFCB                        82067
         BXH   R15,R15,DEFDDBUM                                  82067
         MVC   HNAMEDSN,JFCBDSNM                                 82067
         MVC   HNAMEVOL,JFCBVOLS    COMPLETE THE TITLE           82067
         OI    JFCBTSDM,JFCNWRIT+JFCNDSCB  KEEP DSCB CLEAN       82067
         NI    JFCBIND1,255-JFCPDS  NO MEMBER PROCESSING         82067
         B     GOTOOPEN                                          82067
DEFDDBUM LA    R2,8(,R2)                                         82067
         BCT   R3,DEFDDLUP   TRY ANOTHER DDNAME                  82067
         WTO   '*** NO USABLE LIBRARY DD CARD SUPPLIED ***',           *
               ROUTCDE=11                                        82067
         ABEND 213                                               82067
GOTOOPEN OPEN  DIRDCB,TYPE=J      TO READ PDS DIRECTORY          82067
         XPRHEAD PWA,LIST=HEADING                                82067
         XPREJECT PWA        WRITE FIRST PAGE TITLE              82067
         GETMAIN VU,LA=GMLIST,A=DIRGMA
         LM    R0,R1,DIRGMA  GET ADDRESS/LENGTH OF GETMAINED AREA
         XR    R14,R14                                           82067
         XR    R15,R15                                           82067
         MVCL  R0,R14        MAKE DUMPS PRETTIER                 82067
         MVC   DFADR(8),DIRGMA    ADR/LEN OF GETMAINED AREA
         XCAL  DIRFMT             READ AND FORMAT DIRECTORY
         CLOSE DIRDCB
         FREEMAIN V,A=DFADR       FREE UNUSED STORAGE
         FREEPOOL DIRDCB
*
         DEVTYPE DCBDDNAM-IHADCB+SYSPUNCH,DB  PUNCH DD PRESENT ? 82130
         BXH   R15,R15,NOPUNCH    NO; DON'T OPEN IT              82067
         OPEN  (SYSPUNCH,OUTPUT)
NOPUNCH  XCAL  DIRLIST            LIST DIRECTORY                 82067
         TM    DCBOFLGS-IHADCB+SYSPUNCH,DCBOFOPN  PUNCH OPEN ?   82067
         BZ    NOPUNCH2      NO; SKIP CLOSING IT                 82067
         CLOSE SYSPUNCH
         FREEPOOL SYSPUNCH
*
NOPUNCH2 L     R0,DFADR                                          82067
         L     R1,DIRGMA          ADDRESS TO FREE
         SR    R0,R1              LENGTH TO FREE
         FREEMAIN R,LV=(0),A=(1)
         XPRCLOSE PWA
         B     RETURN
*
DATAADR  DC    A(DATAAREA)        ADDRESS OF COMMON AREA
GMLIST   DC    A(2048,406900)
SA1      DS    18F                MAIN SAVE AREA
         TITLE 'DIRFMT - READ AND FORMAT CHAINED PDS DIRECTORY'
*  INPUTS -
*        1.    DIRDCB   OPEN DCB TO READ DIRECTORY
*        2.    DFADR    ADDRESS OF AREA TO START DIRECTORY IN
*        3.    DFLEN    LENGTH OF THIS AREA
*
*  OUTPUTS -
*        1.    DFSTART  ADDRESS OF DIRECTORY BUILT IN CORE
*        2.    DFALLOC  ADDRESS OF @LLOCATR DIRECTORY ENTRY OR ZERO
*        3.    DFADR    ADDRESS OF REMAINING UNUSED CORE
*        4.    DFLEN    LENGTH OF ABOVE
*
DIRFMT   ENTER SA2
         XC    DFALLOC,DFALLOC    ALLOCATOR NOT YET FOUND
         L     R3,DFADR           IN CORE DIRECTORY LIST POINTER
         LR    R5,R3
         A     R5,DFLEN           LIMIT ADDRESS
         ST    R3,DFSTART
*
*  READ DIRECTORY AND CHAIN INTO CORE AREA.
*
DIRGET   GET   DIRDCB             READ A DIRECTORY BLOCK
         LH    R7,0(,R1)          LENGTH OF THIS DIRECTORY BLOCK
         LA    R2,2(,R1)          POINTER TO MEMBER ENTRY
         AR    R7,R1              LIMIT ADDRESS
         BCTR  R7,0
         USING PDSDATA,R2
         USING PDSDSECT,R3
*  BE CAREFUL WITH ABOVE USINGS - THEY OVERLAP.
*
DIRDBLK  SR    R6,R6
         ST    R6,VERPTR          SET NO VERSIONS THIS MAIN MEMBER
         NI    PDSC,X'1F'         KEEP ONLY LENGTH BITS
         IC    R6,PDSC            GET USER DATA LENGTH/2
         LA    R6,PDSUSER-PDSDATA(R6,R6)  MEMBER ENTRY LENGTH
         LA    R0,PDSDATA-PDSDSECT+3(R6,R3)  NEXT ADDRESS + 3
         SRL   R0,2
         SLL   R0,2               ROUND UP TO FULLWORD BOUNDARY
         CR    R0,R5
         BH    DIRE1              ERROR - RAN OUT OF CORE
         LA    R1,255(,R6)        LENGTH-1 FOR MVC
         LA    R15,MAXMOVE-1   SET MAXIMUM MOVE LENGTH           82067
         CR    R1,R15        VALID ?                             82067
         BL    *+6           YES                                 82067
         LR    R1,R15        ELSE TRUNCATE                       82067
         EX    R1,DIRMV1          FETCH MEMBER ENTRY
         CLC   PDSNAME,=C'@LLOCATR'  LOOK FOR ALLOCATOR ENTRY
         BNE   *+8
         ST    R3,DFALLOC         SAVE ALLOCATOR'S ADDRESS
         CLI   PDSNAME,X'FF'      TEST FOR END OF DIRECTORY
         BE    DIREOF
         ST    R0,MAINPTR         SET CHAIN AHEAD TO NEXT ENTRY
         LR    R3,R0              BUMP POINTER
         BXLE  R2,R6,DIRDBLK      GO DEBLOCK NEXT MEMBER ENTRY
         B     DIRGET             OR START NEXT DIRECTORY BLOCK
*
DIRMV1   MVC   PDSDATA-PDSDSECT(0,R3),PDSDATA  EXECUTED INSTRUCTION
*
DIRE1    XPRNTLIN PWA,TEXT=DIRE1T,LENGTH=L'DIRE1T,SPB=2
         XPRCLOSE PWA
         ABEND 11,DUMP
DIRE1T   DC    C'*** INSUFFICIENT STORAGE TO HOLD DIRECTORY ***'
*
DIREOF   XC    MAINPTR,MAINPTR    END CHAIN FOR GOOD LUCK
         DROP  R2,R3
         AH    R0,=H'7'           ROUND UP TO DOUBLEWORD
         SRL   R0,3
         SLL   R0,3
         ST    R0,DFADR           UPDATE FREE AREA ADDRESS
         SR    R5,R0
         ST    R5,DFLEN           AND LENGTH
*
*  SCAN IN-CORE DIRECTORY FOR VERSIONS, AND RE-LINK THEM ACCORDINGLY.
*
         L     R1,DFALLOC         ADDRESS OF @LLOCATR
         LTR   R1,R1              TEST IF FOUND
         BZ    RETURN             NO - NO VERSIONS POSSIBLE
         MVC   DFLIBID,PDSUSER-PDSDSECT(R1)  FOR VERSION NAME PATTERN
         LA    R2,DFSTART         DIRECTORY MAIN SCAN POINTER
         USING PDSDSECT,R2
*
FMT1N    L     R2,MAINPTR         ADVANCE POINTER
         CLI   PDSNAME,X'FF'      TEST FOR END OF DIRECTORY
         BE    RETURN
         CLI   PDSC,DRECSIZE/2    TEST IF LOTS OF USER DATA
         BL    FMT1N              NO - CAN'T BE A PRODUCTION ALIAS
         CLC   PDSNAME,REALNAME   TEST IF EXTERNAL=INTERNAL NAME
         BNE   FMT1N              NO - THIS A VERSION. WANT PROD ALIAS
         CLC   DFLIBID,LIBNAME    TEST IN RIGHT LIBRARY
         BNE   FMT1N              NO - PROBABLE ERROR.  SKIP.
         UNPK  DFMEMID,MEMBID     COMPLETE VERSION NAME PREFIX
         OI    DFMEMID+L'DFMEMID-1,C'0'
*
         LA    R4,DFSTART
FMT2L    L     R3,0(,R4)          SECONDARY POINTER. R4 BACK POINTER.
         CLI   PDSNAME-PDSDSECT(R3),X'FF'  TEST FOR END OF DIR.
         BE    FMT1N
         CLI   PDSC-PDSDSECT(R3),DRECSIZE/2  TEST AMOUNT OF USER DATA
         BL    FMT2N              NOT ENOUGH TO BE CHAINED
         CLC   DFLIBID(L'DFLIBID+L'DFMEMID),PDSNAME-PDSDSECT(R3)
         BH    FMT2N              WRONG MEMBER - KEEP LOOKING.
         BL    FMT1N              MAIN MEMBER HAS NO CHAIN. STOP.
         MVZ   ZTEST,PDSNAME+5-PDSDSECT(R3)  BE SURE VERSION NUMERIC
         CLC   ZTEST,=C'000'
         BNE   FMT2N
         CLC   PDSNAME,REALNAME-PDSDSECT(R3)  TEST EXTERNAL NAME OK
         BNE   FMT2N              NO - PROBABLE ERROR - SKIP IT.
*
*  NOW HAVE R2 -> PRODUCTION ALIAS,
*  R3 -> FIRST CHAIN ELEMENT,
*  R4 - > PRECEDING MAIN MEMBER.
*  PULL CHAIN OF VERSION ENTRIES OUT OF MAIN DIRECTORY,
*  LINK TO PRODUCTION ALIAS' ENTRY.
*
         ST    R3,VERPTR          LINK VERSION TO PRODUCTION ALIAS
FMT3N    LR    R5,R3              SAVE PREV. VERSION ENTRY PTR.
         L     R3,0(,R3)          ADVANCE VERSION POINTER
         ST    R3,0(,R4)          PREV. ENTRY NOW OUT OF MAIN CHAIN
         XC    0(4,R5),0(R5)      TEMPORARILY REMOVE LINK FIELD
FMT3XN   CLC   DFLIBID(L'DFLIBID+L'DFMEMID),PDSNAME-PDSDSECT(R3)
         BNE   FMT1N              REACHED END OF THIS MEMBER'S VERSIONS
         CLI   PDSC-PDSDSECT(R3),DRECSIZE/2  SEE IF CHAINED
         BL    FMT3X              NO - JUST HAS CLOSE NAME. JUMP IT.
         CLC   PDSNAME,REALNAME-PDSDSECT(R3)  VALIDATE NAMES
         BNE   FMT3X              NO - JUMP IT.
         MVZ   ZTEST,PDSNAME+5-PDSDSECT(R3)
         CLC   ZTEST,=C'000'
         BNE   FMT3X
*  R3 POINTS TO ANOTHER GOOD VERSION.  BRING IT ALONG.
         ST    R3,0(,R5)          PUT LINK BACK
         B     FMT3N
*  NON-VERSION ELEMENT SOMEHOW HAPPENED TO BE IN COLLATING SEQUENCE
*  AMOUNG THE VERSION ENTRIES.  LEAVE IT IN MAIN CHAIN.
FMT3X    LR    R4,R3              ADVANCE MAIN POINTER
         L     R3,0(,R3)          AND ELEMENT SEEKER.
         B     FMT3XN
*
FMT2N    LR    R4,R3              ADVANCE MAIN POINTER
         B     FMT2L              AND CONTINUE VERSION SCAN
         DROP  R2
         TITLE 'DIRLIST - PRINT PDS DIRECTORY'
DIRLIST  ENTER SA2
         LA    R2,DFSTART         DIRECTORY POINTER
DIR1N    L     R2,0(,R2)          POINT NEXT DIRECTORY ENTRY
         USING PDSDSECT,R2
         CLI   PDSNAME,X'FF'      TEST FOR END OF DIRECTORY
         BE    RETURN
*
         MVC   PDVNAME,BLANKS
         MVC   PDMNAME,PDSNAME    PLUG MAIN MEMBER NAME
         MVC   LISTNAME,PDSNAME
         CLC   LISTNAME,=C'@LLOCATR'   DO NOT PUNCH FOR ALLOCATOR
         BE    NOPUAL
         TM    DCBOFLGS-IHADCB+SYSPUNCH,DCBOFOPN  PUNCH OPEN ?   82067
         BZ    NOPUAL        NO; SKIP 013 ABEND                  82067
         PUT   SYSPUNCH,LISTCARD  PUNCH ./ LIST FOR IEBUPDTX
NOPUAL   EQU   *
         LA    R3,VERPTR          POINTER TO ANY VERSIONS
         LA    R1,PDSDATA
         SPACE 1                                                 82067
*        INSERTION ADDED TO MAKE LISTING FOR SCRATCHED PRODUCTION
*        ALIAS PRETTIER                                          82067
         TM    PDSC,9        LONG ENOUGH FOR CHAIN ENTRY ?       82067
         BNO   DIR2N         NO                                  82067
         CLC   DFLIBID,PDSNAME  POSSIBLE CHAIN ENTRY ?           82067
         BNE   DIR2N         NO                                  82067
         CLC   DFLIBID,LIBNAME  SELF-ID CORRECT ?                82067
         BNE   DIR2N         NO                                  82067
         UNPK  DFMEMID,MEMVER                                    82067
         OI    DFMEMID+2,C'0'    FORCE ZONE                      82067
         CLC   DFMEMID,PDSNAME+5   SAME VERSION NUMBER ?         82067
         BNE   DIR2N         NO                                  82067
         UNPK  DFMEMID,MEMBID                                    82067
         OI    DFMEMID+2,C'0'                                    82067
         CLC   DFMEMID,PDSNAME+2   SAME MEMBER ID ?              82067
         BNE   DIR2N         NO                                  82067
         MVC   PDMISMEM,REALNAME   IDENTIFY MISSING MEMBER       82067
         XPRLIST PWA,LIST=PDMISS   SHOW ON LISTING               82067
         LA    R1,PDSDATA    RESTORE R1                          82067
         DROP  R2                                                82067
         USING PDSDSECT,R14                                      82067
         LR    R14,R2        COPY CURRENT ADDRESS                82067
MISSLOP  LR    R15,R14       SAVE LAST GOOD ADDRESS              82067
         ICM   R14,7,MAINPTR+1   GET NEXT ENTRY                  82067
         BZ    MISSLAST      NO MORE                             82067
         CLC   PDSNAME(5),PDSNAME-PDSDSECT(R2)  SAME MEMBER ?    82067
         BH    MISSLAST      NO; END OF CHAIN                    82067
         CLC   LIBNAME,LIBNAME-PDSDSECT(R2)  SAME PRODUCTION NAME ?
         BE    MISSLOP       YES; GROUP IT                       82067
         DROP  R14                                               82067
         USING PDSDSECT,R2   RESTORE MAPPING                     82067
MISSLAST CR    R15,R2        ANY ENTRIES FOUND ?                 82067
         BE    DIR2N         NO; PROCESS NORMALLY                82067
         MVC   VERPTR,MAINPTR   MOVE CHAIN OVER                  82067
         ST    R14,MAINPTR   MAKE NEW MAIN CHAIN                 82067
         XC    MAINPTR-PDSDSECT(4,R15),MAINPTR-PDSDSECT(R15) KILL CHAIN
         MVC   PDMNAME,BLANKS   FIX LISTING                      82067
         MVC   PDVNAME,PDSNAME-PDSDSECT(R2)                      82067
*
DIR2N    BAL   R14,PDEDIT         FORMAT TTR AND USER DATA
         XPRLIST PWA,LIST=PDLINE  PRINT PDS LINE
         L     R3,0(,R3)          POINT NEXT VERSION
         LTR   R3,R3              TEST IF ANY
         BZ    DIR1N              NO - ON TO NEXT MAIN MEMBER
         MVC   PDMNAME,BLANKS
         MVC   PDVNAME,PDSNAME-PDSDSECT(R3)
         CLC   PDSTTR,PDSTTR-PDSDSECT(R3)
         BNE   *+10
         MVC   PDPP,=C'->'        FLAG PRODUCTION ALIAS
         LA    R1,PDSDATA-PDSDSECT(,R3)
         B     DIR2N
*
         DROP  R2
*
*  SUBROUTINE TO EDIT PDS ENTRY POINTED TO BY R1 INTO THE
*  PRINT LINE FIELDS FOR TTR AND USER DATA.
*
PDEDIT   STM   R2,R4,PDESAVE      SAVE A FEW REGISTERS
         UNPK  HEXWORK(7),PDSTTR-PDSDATA(4,R1)
         TR    HEXWORK(6),HEXEDTAB
         MVC   PDTTR(4),HEXWORK
         MVI   PDTTR+4,C'.'
         MVC   PDTTR+5(2),HEXWORK+4
         MVC   PDUSER,PDUSER-1    CLEAR USER DATA AREA
*
         SR    R2,R2
         IC    R2,PDSC-PDSDATA(,R1)
         AR    R2,R2              LENGTH OF USER DATA
         BZ    PDERET             RETURN IF NONE
*
         LA    R0,4*(PDREM/9)     MAX USER DATA CAN PRINT
         CR    R2,R0              COMPARE SIZE U.D. TO MAX.
         BNH   *+6                O.K. WILL FIT
         LR    R2,R0              TRUNCATE U.D. TO FIT ON ONE LINE
         LA    R1,PDSUSER-PDSDATA(,R1)
         LA    R3,0(R2,R1)        LIMIT
         LA    R2,2               INCREMENT
         SR    R3,R2
         LA    R4,PDUSER-4        PRINT LINE POINTER
         MVI   PDESW,0            INITIALIZE ALTERNATOR
*
PDENEXT  XI    PDESW,1            TEST IF A NEW FULLWORD
         BZ    *+8                NO
         LA    R4,1(,R4)          SPACE EVERY OTHER HALFWORD
         LA    R4,4(,R4)          ADVANCE TO PRINT NEXT HALFWORD
         UNPK  HEXWORK(5),0(3,R1)
         TR    HEXWORK(4),HEXEDTAB
         MVC   0(4,R4),HEXWORK
         BXLE  R1,R2,PDENEXT
PDERET   LM    R2,R4,PDESAVE
         BR    R14                RETURN
*
HEXWORK  DC    D'0'
HEXEDTAB EQU   *-C'0'
         DC    C'0123456789ABCDEF'
PDESAVE  DS    3F
PDESW    DC    X'0'
*
*  PRINT LINE FOR PDS DIRECTORY LISTING
*
PDMNAME  DS    0CL8               MAIN MEMBER NAME
         DC    C' '
PDPP     DC    CL2' '             PRODUCTION ALIAS INDICATED BY ->
         DC    C' '
PDVNAME  DC    CL8' '             VERSION ELEMENT NAME
         DC    CL2' '
PDTTR    DC    CL7' '             TT.R
         DC    C' '
PDREM    EQU   9*((120+PDMNAME-*)/9)
PDUSER   DC    XL(PDREM)'0'
PDLLEN   EQU   *-PDMNAME          LENGTH OF PRINT LINE
*
PDLINE   XPRLDEF TEXT=PDMNAME,LENGTH=PDLLEN
PDMISS   XPRLDEF TEXT=PDMIST,LENGTH=PDMLEN                       82067
PDMIST   DC    C'******* PRODUCTION ALIAS FOR MEMBER '           82067
PDMISMEM DC    CL8' ',C' NOT FOUND *******'                      82067
PDMLEN   EQU   *-PDMIST                                          82067
         SPACE 1                                                 82130
PUNEXIT  DC    0A(0),X'85',AL3(PUNDIT)  PUNCH EXIT               82130
         USING IHADCB,R1                                         82130
         USING PUNDIT,R15    DECLARE PUNCH DCB OPEN EXIT         82130
PUNDIT   TM    DCBRECFM,JFCFMREC  RECFM PRESENT ?                82130
         BNZ   *+8           YES                                 82130
         OI    DCBRECFM,JFCFIX+JFCRFB  SET FB                    82130
         ICM   R0,3,DCBLRECL                                     82130
         BNZ   *+8                                               82130
         MVI   DCBLRECL+1,80                                     82130
         ICM   R0,3,DCBBLKSI                                     82130
         BZR   R14                                               82130
         MVC   DCBBLKSI,DCBLRECL                                 82130
         BR    R14           RETURN                              82130
         DROP  R15,R1                                            82130
         TITLE 'COMMON DATA AREA'
         PRINT NOGEN
         DROP  R12
DB       DS    D             DOUBLE WORD WORK WORD               82067
DATAAREA DS    0D
BLANKS   DC    CL8' '
DIRDCB   DCB   BLKSIZE=256,LRECL=256,RECFM=F,DSORG=PS,MACRF=GL,        X
               DDNAME=SYSUT2,EXLST=EXLIST                        82067
DEFDDNAM DC    CL8'SYSUT2',CL8'SYSLIB',CL8'SYSUT1'               82067
DEFDDNUM EQU   (*-DEFDDNAM)/8    NUMBER OF ALTERNATE NAMES       82067
PWA      XPRDCB  DDNAME=SYSPRINT
*
SYSPUNCH DCB   DDNAME=SYSPUNCH,DSORG=PS,MACRF=PM,EXLST=PUNEXIT   82130
LISTCARD DC    C'./ C    '
LISTNAME DC    CL8' ',CL64' '
*
DIRGMA   DC    A(0)               ADDRESS OF GETMAIN AREA
DIRGML   DC    A(0)               LENGTH OF ABOVE AREA
*
DFALLOC  DC    A(0)               ADDRESS OF @LLOCATR PDS ENTRY
DFSTART  DC    A(0)               START OF IN CORE DIRECTORY LIST
DFADR    DC    A(0)               ADDRESS OF FREE AREA
DFLEN    DC    A(0)               LENGTH OF ABOVE
*
DFLIBID  DC    CL2' '             CONSTRUCTED VERSION NAME PREFIX
DFMEMID  DC    CL3' '
*
ZTEST    DC    C'000'
*
SA2      DS    18F                SAVE AREA FOR SUBROUTINES
*
RETURN   L     R13,4(,R13)
         L     R14,12(,R13)
         SR    R15,R15
         RETURN (2,12),T
*
EXLIST   DC    0A(0),X'87',AL3(INFMJFCB)  RDJFCB ADDRESS         82067
         IEFJFCBN ,          EXPAND THE JFCB                     82067
         SPACE 1                                                 82067
HEADING  XPRLDEF TEXT=HNAME,LENGTH=HNAMEX-HNAME,SPA=3            82067
HNAME    DC    C'IEBUPDTX DIRECTORY LISTING OF '                 82067
HNAMEDSN DC    CL44' ',C' ON '                                   82067
HNAMEVOL DC    CL6' '                                            82067
HNAMEX   EQU   *                                                 82067
*
         DCBD  DSORG=PO,DEVD=DA
*
PDSDSECT DSECT
MAINPTR  DS    A                  ADDRESS NEXT MEMBER ENTRY
VERPTR   DS    A                  ADDRESS OF VERSION ENTRIES
PDSDATA  EQU   *
PDSNAME  DS    CL8                MEMBER NAME
PDSTTR   DS    XL3                TTR
PDSC     DS    X                  INDICATORS AND U.D. LENGTH/2
PDSUSER  DS    0XL62              USER DATA
SSSI     DS    XL4
REALNAME DS    XL8                REAL PRINT NAME OF THIS MEMBER
LIBNAME  DS    XL2                LIBRARY IDENTIFICATION LETTERS
MEMBID   DS    PL2                ID NUMBER FOR THIS MEMBER
MEMVER   DS    PL2                VERSION NO. (HIGH OR IDENT)
DRECSIZE EQU   *-SSSI             SIZE OF EXPANDED USER DATA FIELD
MAXMOVE  EQU   *-PDSDATA     MAXIMUM SIZE TO MOVE                82067
         END   IEBUPCHN                                          82067
