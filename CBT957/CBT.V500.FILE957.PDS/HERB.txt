*  IEBUPDTX USER EXIT ROUTINE TO STICK MINUS SIGNS INTO MACRO
*  STATEMENTS, SO COMMENTS WILL PRINT NICELY.
*  WRITTEN BEGRUDGINGLY FOR HERB WEINER IN THE INTEREST OF SCIENCE.
*
HERB     START 0
         USING *,12
         PRINT NOGEN
         RDEF
         SAVE  (14,12),,*
         LR    R12,R15
*
*  TEST FOR COMMENT CARDS AND IGNORE THEM.
*
         CLI   6(R2),C'*'
         BE    NOCHANGE
         CLI   7(R2),C'*'
         BE    NOCHANGE
*
*  SCAN FOR OPERAND FIELD.
*
         MVI   FINDB+C' ',2       TRT TABLE
         LR    R6,R2              MOVE POINTER TO SAFETY
         SR    R2,R2              SO TRT BEHAVES
         TRT   6(50,R6),FINDB     SCAN NAME FIELD
         BZ    NOCHANGE
         TRT   1(14,R1),SKIPB     SEARCH TO OP-CODE
         BZ    NOCHANGE
         LR    R8,R1              SAVE POINTER TO BEGINNING OF OP-CODE.
         TRT   1(10,R1),FINDB     SCAN OP-CODE
*  CHECK FOR AND IGNORE CARDS WITH ASSEMBLER PSEUDO-OPS (EG AIF, AGO).
*  THE OP-CODE LENGTH IS USED TO SHORTEN THE CHECKING.  NOTE THAT
*  ASSEMBLER MACRO OP-CODES ONLY COME IN LENGTHS OF 3, 4, AND 5.
         LR    R7,R1              ADDRESS OF BLANKS FOLLOWING OP-CODE
         SR    R7,R8              COMPUTE LENGTH OF OP-CODE
         SLL   R7,2
         B     *(R7)              LENGTH CONTROLS BRANCH
         B     OK                 1
         B     OK                 2
         B     TEST3              3
         B     TEST4              4
         B     TEST5              5
         B     OK                 6
         NOP   OK                 7
OK       EQU   *                  8
*
         LA    R1,1(,R1)
         LA    R5,73(,R6)         LENGTH SO STOP BY COL. 68
         SR    R5,R1
         EX    R5,TRT1            SEARCH FOR OPERAND FIELD
         BZ    NOCHANGE
*
*  SCAN THE OPERAND FIELD.
*
SCANOP   MVI   FINDB+C' ',2
         LA    R5,74(,R6)         WANT BLANK BY COL. 69
         SR    R5,R1              LENGTH FOR TRT
         EX    R5,TRT2            SEARCH FOR BLANK OR QUOTE
         BZ    NOCHANGE
         BCT   R2,EOPR            BRANCH IF FOUND THE BLANK
*  THIS QUOTE WILL NOT BE MATCHED IF PRECEDING LETTER IS I, N, K, L, S,
*  T.  (ASSEMBLER ATTRIBUTES).
         LR    R7,R1
         S     R7,=F'2'
         LA    R1,1(,R1)
         CLI   1(R7),C'I'
         BL    QSCAN
         CLI   1(R7),C'T'
         BH    QSCAN
         CLI   1(R7),C'P'
         BE    QSCAN
         CLI   0(R7),C'&&'
         BE    QSCAN
         CLI   0(R7),C'A'
         BL    SCANOP+4
*  SCAN QUOTED STRING IN OPERAND FIELD.
QSCAN    MVI   FINDB+C' ',0       SET TABLE TO IGNORE BLANKS
QUOTEL   LA    R5,73(,R6)         WANT ENDING QUOTE BY COL. 68
         SR    R5,R1              LENGTH FOR TRT
         BM    NOCHANGE
         EX    R5,TRT2
         BZ    NOCHANGE
         LA    R1,1(,R1)          POINT PAST THE QUOTE
         CLI   0(R1),C''''        TEST FOR DOUBLED QUOTES
         BNE   SCANOP
         LA    R1,1(,R1)
         B     QUOTEL
*
*  OPERAND SCANNED. R1 POINTS TO THE BLANK FOLLOWING IT.
*  IF CARD HAS A COMMENT, AND THE SPACE, INSERT A HERBACIOUS MINUS SIGN
*
EOPR     LA    R1,1(,R1)
         CLI   0(R1),C' '         TEST IF NEEDS MINUS INSERTED
         BNE   NOCHANGE
         LR    R7,R1              SAVE POINTER
         LA    R5,76(,R6)         WANT COMMENT BY COLUMN 71
         SR    R5,R1
         EX    R5,TRT1
         BZ    NOCHANGE
*
*  MOVE CARD IMAGE TO REPLACEMENT AREA AND MODIFY IT THERE.
*
         MVC   6(72,R3),6(R6)
         SR    R7,R6
         AR    R7,R3
         MVI   0(R7),C'-'         HERE IT IS
         LA    R15,8              REPLACE RETURN CODE
         RETURN  (2,12),T
*
NOCHANGE SR    R15,R15
         RETURN  (2,12),T
*
*
*  ROUTINES TO CHECK FOR ASSEMBLER MACRO OPS, WHICH WE WANT IGNORED.
*  ORGANIZATION IS BY OP-CODE LENGTH.
*  R8 POINTS TO THE OP-CODE BEING TESTED.
*
TEST3    CLC   0(3,R8),=C'AIF'
         BE    NOCHANGE
         CLC   0(3,R8),=C'AGO'
         BE    NOCHANGE
         B     OK
*
TEST5    CLC   0(5,R8),=C'MACRO'
         BE    NOCHANGE
         CLC   0(5,R8),=C'MNOTE'
         BE    NOCHANGE
         CLC   0(5,R8),=C'MEXIT'
         BE    NOCHANGE
         B     OK
*
TEST4    LA    R9,AOPS            TABLE POINTER
         LA    R10,4              INCREMENT
         LA    R11,LAOP           LIMIT
         MVC   FW,0(R8)           FETCH OP CODE TO FULL-WORD
         L     R0,FW              * AND PUT IN REGISTER FOR EFFICIENCY
T4LOOP   CL    R0,0(,R9)          COMPARE OP-CODE AGAINST TABLE ENTRIES
         BE    NOCHANGE
         BXLE  R9,R10,T4LOOP
         B     OK
*
FW       DC    F'0'
AOPS     DC    C'ANOP'
         DC    C'SETC'
         DC    C'SETB'
         DC    C'SETA'
         DC    C'LCLC'
         DC    C'LCLB'
         DC    C'LCLA'
         DC    C'GBLC'
         DC    C'GBLB'
         DC    C'GBLA'
         DC    C'ACTR'
LAOP     DC    C'MEND'
*
*
TRT1     TRT   0(0,R1),SKIPB
TRT2     TRT   0(0,R1),FINDB
*
SKIPB    DC    64X'1',X'0',191X'1'
FINDB    DC    125X'0',X'1',130X'0'
         END
