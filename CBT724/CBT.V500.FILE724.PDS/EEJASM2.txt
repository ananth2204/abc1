//EEJASM2  JOB 1,JAFFE,CLASS=A,MSGCLASS=T,NOTIFY=&SYSUID
//*MAIN CLASS=QUIKPROD,SYSTEM=MVS60
//APP1    EXEC PGM=GIMSMP,REGION=64M
//SMPCSI   DD  DSN=MVS.GLOBAL.CSI,DISP=SHR
//SMPCNTL  DD  *
  SET BOUNDARY(GLOBAL) .
    RECEIVE SYSMODS LIST .
  SET BOUNDARY(MVST) .
    APPLY S(EEJASM2) REDO /* CHECK */ .
/*
//SMPPTFIN DD *
++USERMOD(EEJASM2) /*
 *START OF SPECIFICATIONS**************************************
 *                                                            *
 * USERMOD NAME = EEJASM2                                     *
 *                                                            *
 * DESCRIPTIVE NAME = Mods to Structured Programming Macros.  *
 *                    for HLASM 1.5 Toolkit Release.          *
 *                                                            *
 * FUNCTION =                                                 *
 *                                                            *
 *    These modifications were dramatically simplified after  *
 *    APAR PK01283, since IBM adopted most of my suggestions. *
 *                                                            *
 *    The only modification remaining is the NEXTWHEN macro.  *
 *    This macro allows you to pass control from one WHEN     *
 *    clause to the next WHEN clause or the OTHRWISE clause.  *
 *                                                            *
 * AUTHOR =                                                   *
 *                                                            *
 *    Edward E. Jaffe                                         *
 *    Phoenix Software International                          *
 *    5200 W. Century Blvd., Suite 800                        *
 *    Los Angeles, CA 90045 U.S.A.                            *
 *    edjaffe@phoenixsoftware.com                             *
 *                                                            *
 *END OF SPECIFICATIONS****************************************
 */ .
++VER(Z038) FMID(JMQ415A)
            PRE(UK06158)
            .
++MACUPD(ASMMSP) .
./ CHANGE NAME=ASMMSP
        MACRO  ,                                               *EEJASM2
.***************************************************************EEJASM2
.*   Macro:   NEXTWHEN                                        **EEJASM2
.*   Author:  Edward E. Jaffe                                 **EEJASM2
.*   Company: Phoenix Software International, Inc.            **EEJASM2
.*   License: This macro is "freeware".                       **EEJASM2
.***************************************************************EEJASM2
         NEXTWHEN ,                                            *EEJASM2
         PUSH  PRINT,NOPRINT                                   *EEJASM2
         PRINT NOMCALL,NOPRINT                                 *EEJASM2
         COPY  ASMMGBLV                                        *EEJASM2
         LCLA  &LBLIND,&IDX                                    *EEJASM2
&IDX     SETA  &ASMA_NI                                        *EEJASM2
.CHKNEST ANOP  ,                                               *EEJASM2
         AIF   ('&ASMA_NEST(&IDX)'(5,*) EQ 'SELECT').IDXOK     *EEJASM2
&IDX     SETA  &IDX-1                                          *EEJASM2
         AIF   (&IDX GE 1).CHKNEST                             *EEJASM2
         AGO   .BADLOC                                         *EEJASM2
.IDXOK   ANOP  ,                                               *EEJASM2
         AIF   ('&ASMA_NEST(&IDX)'(3,1) EQ ' ').BADLOC         *EEJASM2
         AIF   ('&ASMA_NEST(&IDX)'(4,1) EQ 'O').BADLOC         *EEJASM2
&LBLIND  SETA  &ASMA_ST(&IDX) GET INDEX FOR PREV LEVEL         *EEJASM2
         B&ASMA_REL.C 15,&ASMA_LIND(&LBLIND) GOTO NEXT WHEN    *EEJASM2
         AGO   .EXIT                                           *EEJASM2
.BADLOC  ANOP  ,                                               *EEJASM2
         MNOTE 8,'Improperly placed NEXTWHEN. Must appear inside WHEN c+
               lause'                                          *EEJASM2
.EXIT    ANOP  ,                                               *EEJASM2
         POP   PRINT,NOPRINT                                   *EEJASM2
         MEND  ,                                               *EEJASM2
./ ENDUP
//
