         TITLE 'Y2KGETD - GET CURRENT SYSTEM DATE'
*                                                                     *
*   YY   YY   222   KK   KK  GGGGG  EEEEEE TTTTTTTT DDDDDD            *
*   YY   YY  2   2  KK  KK  GG   GG EE        TT    DD   DD           *
*   YY   YY      2  KK KK   GG   GG EE        TT    DD   DD           *
*    YY YY       2  KKKK    GG      EEEE      TT    DD   DD           *
*     YYY     222   KKKK    GG  GGG EE        TT    DD   DD           *
*     YY     2      KK KK   GG   GG EE        TT    DD   DD           *
*     YY     2      KK  KK  GG   GG EE        TT    DD   DD           *
*     YY     22222  KK   KK  GGGGG  EEEEEEE   TT    DDDDDD            *
*                                                                     *
* ******************************************************************* *
* ******************************************************************* *
*    THIS SUBROUTINE RETRIEVES THE CURRENT DATE FROM THE OPERATING    *
*    SYSTEM AND RETURNS IT IN 3 FORMATS                               *
*                                                                     *
*    1.  THREE FIELDS ARE PASSED AS PARAMETERS TO THE ROUTINE:        *
*        1)  AN 8 BYTE FIELD WHICH WILL RECEIVE THE CURRENT GREGORIAN *
*            DATE (IN ZONED-DECIMAL FORMAT),                          *
*        2)  A 7 BYTE FIELD WHICH WILL RECEIVE THE CURRENT JULIAN     *
*            DATE (IN ZONED-DECIMAL FORMAT), AND                      *
*        3)  A 7 BYTE FIELD WHICH WILL RECEIVE THE ASTRONOMICAL       *
*            NUMBER (IN ZONED-DECIMAL FORMAT) FOR THE CURRENT DATE.   *
*                                                                     *
*        SUGGESTED CALLING SYNTAX FOR ALC CALLERS:                    *
*                                                                     *
*                      CALL  Y2KGETD,(DATEG)                          *
*                         . . .                                       *
*             DATEG    DS    ZL8'00000000'                            *
*             DATEJ    DS    ZL7'0000000'                             *
*             ANUM     DS    ZL7'0000000'                             *
*                                                                     *
*        SUGGESTED CALLING SYNTAX FOR COBOL CALLERS:                  *
*                                                                     *
*             01  GETD-PARAMETERS.                                    *
*                 02  DATEG PIC 9(8) VALUE 0.                         *
*                 02  DATEJ PIC 9(7) VALUE 0.                         *
*                 02  ANUM  PIC 9(7) VALUE 0.                         *
*             CALL 'Y2KGETD' USING GETD-PARAMETERS.                   *
*                                                                     *
*    2.  UPON SUCCESSFUL EXECUTION, THE CURRENT DATE IN GREGORIAN     *
*        (MMDDYYYY) AND JULIAN (YYYYDDD) WILL BE PLACED IN FIELDS     *
*        1 AND 2, RESPECTIVELY, AND THE ASTRONOMICAL NUMBER FOR THE   *
*        CURRENT DATE WILL BE PLACED IN FIELD 3.                      *
*                                                                     *
* ******************************************************************* *
* ******************************************************************* *
         EJECT
Y2KGETD  CSECT
*         PRINT NOGEN
         EYEC  'RETURN SYSTEM DATE'
*
         SAVE  (14,12)                 SAVE CALLER'S REGISTERS
*
         LR    R12,R15                 LOAD BASE WITH ENTRY ADDRESS
         USING Y2KGETD,R12             ESTABLISH ADDRESSABILITY
*
         L     R11,0(,R1)              LOAD A(PARAMETERS)
         USING $PARMS,R11              ESTABLISH ADDRESSABILITY
*
         GETMAIN R,LV=$DYNLEN          GETMAIN DYNAMIC WORKAREA
         LR    R10,R1                  SAVE A(MEMORY)
*
         ST    R13,4(,R10)             STORE A(CALLER'S SAVE AREA)
         ST    R10,8(,R13)             BACK CHAIN A(MY SAVE AREA)
         LR    R13,R10                 SET A(MY SAVE AREA)
         USING $DYNMEM,R13             ESTABLISH ADDRESSABILITY
*
*    INSERT EYECATCHER MARKS AT BEGIN/END OF GETMAIN'D AREA.
*
         MVC   TOPFLAG,=C'Y2KGETD GETMAIN BEGINS->'
         MVC   BOTFLAG,=C'<-Y2KGETD GETMAIN ENDS'
*
*    RETRIEVE SYSTEM DATE
*
*         XC    SYSTIME,SYSTIME         CLEAR DATE/TIME RETURN AREA
*         TIME  DEC,SYSTIME,LINKAGE=SYSTEM,DATETYPE=MMDDYYYY,
*               MF=(E,TIMEPL)
*
*    MODIFIED FOR MVS3.8J WHICH LACKS CURRENT TIME MACRO FUNCTIONALITY
*
          TIME  DEC                     GET DECIMAL DATE/TIME VALUE
          STM   R0,R1,SYSTWRK           STORE IN WORK AREA
          AP    SYSTWRK+4(4),=PL4'1900000' ADD 19 TO CENTURY BYTE
          UNPK  SYSTIME,SYSTWRK         UNPACK DATE/TIME FIELDS
          OI    SYSTIME+15,X'F0'        CLEAR SIGN
          LA    R1,SYSTIME              RETRIEVE
          MVC   JDATEJ,9(R1)              DATE VALUE
          MVC   PDATEJ,9(R1)                FOR SUBROUTINE
          LA    R1,JTOGPARM             STORE
          ST    R1,PLIST                  IN
          LA    R1,PLIST                    PARAMETER
          L     R15,=V(Y2KJTOG)               LIST
          BALR  R14,R15                         AND CALL
          MVC   PDATEG,JDATEG           STORE GREGORIAN DATE
          B     MVSCONT                 AND CONTINUE NORMALLY
*
*    UNPACK FIELDS FROM MACRO CALL INTO OUTPUT AREA
*
         UNPK  Z5,SYSTIME+8(3)         UNPACK YEAR
         MVC   PDATEG+4(4),Z5          MOVE TO OUTPUT FIELD
         UNPK  Z5,SYSTIME+10(3)        UNPACK MONTH, DAY
         MVC   PDATEG(4),Z5            MOVE TO OUTPUT FIELD
*
*    CALL Y2KGTOJ TO GET JULIAN VERSION OF DATE.
*
         MVC   GDATEG,PDATEG           LOAD DATE TO PASS
         LA    R1,GTOJPARM             ADDRESS OF Y2KGTOJ PARMS
         ST    R1,PLIST                STORE IN DYNAMIC AREA
         LA    R1,PLIST                ADDRESS TO PASS
         L     R15,=V(Y2KGTOJ)         ADDRESS OF ROUTINE
         BALR  R14,R15                 CALL ROUTINE
*
*    MOVE RETURNED JULIAN DATE TO OUTPUT AREA.
*
         MVC   PDATEJ,GDATEJ           MOVE JULIAN TO OUTPUT AREA
*
MVSCONT  EQU   *
*
*    CALL Y2KGTOA TO GET ASTRONOMICAL NUMBER OF DATE.
*
         MVC   ADATEG,PDATEG           LOAD DATE TO PASS
         LA    R1,GTOAPARM             ADDRESS OF Y2KGTOA PARMS
         ST    R1,PLIST                STORE IN DYNAMIC AREA
         LA    R1,PLIST                ADDRESS TO PASS
         L     R15,=V(Y2KGTOA)         ADDRESS OF ROUTINE
         BALR  R14,R15                 CALL ROUTINE
*
*    MOVE RETURNED ASTRO NUMBER TO OUTPUT AREA.
*
         MVC   PANUM,ANUM              MOVE TO OUTPUT AREA
*
*    RESTORE REGISTERS, FREE STORAGE, AND RETURN
*
RETURN   LR    R1,R13                  LOAD ADDRESS OF GETMAINED AREA
         L     R13,4(,R13)             RELOAD PREVIOUS SAVE AREA
         LA    R0,$DYNLEN              LOAD LENGTH OF GETMAINED AREA
         FREEMAIN R,LV=(0),A=(1)       FREE GETMAINED AREA
         RETURN (14,12),RC=0           RETURN TO CALLER WITH RC=0
*
         LTORG                         LITERAL POOL
         EJECT
* ******************************************************************* *
* ***************************** EQUATES ***************************** *
* ******************************************************************* *
         YREGS
* ******************************************************************* *
         EJECT
* ******************************************************************* *
* ***************************** DSECTS ****************************** *
* ******************************************************************* *
$DYNMEM  DSECT                         DYNAMICALLY ALLOCATED MEMORY
SAVEAREA DS    18F                     REGISTER SAVE AREA
TOPFLAG  DS    CL24                    EYECATCHER
*SYSTIME  TIME  LINKAGE=SYSTEM,MF=L     PARAMETER LIST FOR TIME MACRO
*
* MODIFIED FOR MVS3.8J
*
SYSTIME  DS    CL16
SYSTWRK  DS    D
JTOGPARM DS    0H
JDATEJ   DS    CL7
JRC      DS    CL1
JDATEG   DS    CL8
*
Z5       DS    ZL5                     WORK FIELD
PLIST    DS    A                       PARMLIST ADDR FOR SUB CALLS
GTOJPARM DS    0H                      PARAMETERS FOR Y2KGTOJ
GDATEG   DS    CL8                       INPUT DATE
GRC      DS    CL1                       RETURN CODE
GDATEJ   DS    CL7                       JULIAN DATE
GTOAPARM DS    0H                      PARAMETERS FOR Y2KGTOA
ADATEG   DS    CL8                       INPUT DATE
ARC      DS    CL1                       RETURN CODE
ANUM     DS    ZL7                       ASTRO NUMBER
BOTFLAG  DS    CL22                    EYECATCHER
*
$DYNLEN  EQU   (*-$DYNMEM)             LENGTH OF GETMAIN'D AREA
*
$PARMS   DSECT                         PARAMETERS TO CALLER
PDATEG   DS    CL8                     GREGORIAN DATE
PDATEJ   DS    CL7                     JULIAN DATE
PANUM    DS    ZL7                     ASTRONOMICAL NUMBER
* ******************************************************************* *
         END
