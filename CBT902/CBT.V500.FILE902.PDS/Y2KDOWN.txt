         TITLE 'Y2KDOWN - DETERMINES DAY NUMBER FOR DATE'
*                                                                     *
*   YY   YY   222   KK   KK DDDDDD   OOOOO  WW   WW NN   NN           *
*   YY   YY  2   2  KK  KK  DD   DD OO   OO WW   WW NNN  NN           *
*   YY   YY      2  KK KK   DD   DD OO   OO WW   WW NNN  NN           *
*    YY YY       2  KKKK    DD   DD OO   OO WW   WW NNNN NN           *
*     YYY     222   KKKK    DD   DD OO   OO WW W WW NN NNNN           *
*     YY     2      KK KK   DD   DD OO   OO WWWWWWW NN  NNN           *
*     YY     2      KK  KK  DD   DD OO   OO WWW WWW NN  NNN           *
*     YY     22222  KK   KK DDDDDD   OOOOO   W   W  NN   NN           *
*                                                                     *
* ******************************************************************* *
* ******************************************************************* *
*    THIS SUBROUTINE DETERMINES THE INTEGRAL NUMBER OF THE DAY        *
*    OF THE WEEK A GREGORIAN DATE (MMDDYYYY) FALLS ON                 *
*                                                                     *
*    1.  VALID INPUT YEARS FOR THIS ROUTINE ARE THE YEARS 1601        *
*        THROUGH 3399 A. D. (INCLUSIVE)                               *
*                                                                     *
*    2.  VALID INPUT MONTH VALUES FOR THIS ROUTINE ARE 01 THROUGH 12. *
*                                                                     *
*    3.  VALID INPUT DAY VALUES FOR THIS ROUTINE ARE 01 THROUGH 31    *
*        WITH EXCEPTIONS FOR THE MONTHS LISTED:                       *
*             MONTH          MAXIMUM DAY VALUE                        *
*              04                    30                               *
*              06                    30                               *
*              09                    30                               *
*              11                    30                               *
*              02                    28                               *
*              02 DURING LEAP YEAR   29                               *
*                                                                     *
*    4.  THE DATE FORMAT MUST BE MMDDYYYY, WHERE MM = THE 2 DIGIT     *
*        MONTH VALUE, DD = THE 2 DIGIT DAY VALUE, AND YYYY = THE      *
*        4 DIGIT YEAR VALUE.                                          *
*                                                                     *
*    5.  THREE FIELDS ARE PASSED AS PARAMETERS TO THE ROUTINE:        *
*        1)  AN 8 BYTE FIELD CONTAINING THE DATE (IN ZONED-DECIMAL    *
*            FORMAT) TO BE PROCESSED,                                 *
*        2)  A 1 BYTE ZONED-DECIMAL RETURN CODE, AND                  *
*        3)  A 1 BYTE FIELD IN WHICH THE INTEGRAL DAY NUMBER (IN      *
*            ZONED-DECIMAL FORMAT) WILL BE PLACED.                    *
*        THE ROUTINE WILL NOT ALTER THE YEAR PASSED TO IT.            *
*                                                                     *
*        SUGGESTED CALLING SYNTAX FOR ALC CALLERS:                    *
*                                                                     *
*                      CALL  Y2KDOWN,(DATE)                           *
*                         . . .                                       *
*             DATE     DS    ZL8'04151997'                            *
*             RC       DS    ZL1'0'                                   *
*             DOWN     DS    ZL1'0'                                   *
*                                                                     *
*        SUGGESTED CALLING SYNTAX FOR COBOL CALLERS:                  *
*                                                                     *
*             01  DOWN-PARAMETERS.                                    *
*                 02  DATE PIC 9(8) VALUE 04151997.                   *
*                 02  RC   PIC 9(1) VALUE 0.                          *
*                 02  DOWN PIC 9(1) VALUE 0.                          *
*             CALL 'Y2KDOWN' USING DOWN-PARAMETERS.                   *
*                                                                     *
*    6.  THE POSSIBLE VALUES FOR THE RETURN CODE FIELD ARE:           *
*                                                                     *
*        0 INDICATES SUCCESSFUL EXECUTION OF THE ROUTINE.             *
*                                                                     *
*        2 INDICATES INVALID DATA WAS FOUND IN THE DATE FIELD.        *
*        INVALID DATA ARE DETERMINED IF THE FIELD'S LOW ORDER         *
*        BYTE'S ZONE CONTAINS AN INVALID SIGN, (NOT ONE OF            *
*        X'C', X'A', X'E', OR X'F'), OR IF THE PRECEDING BYTES'       *
*        ZONES ARE OTHER THAN X'F', OR IF ANY BYTE'S LOW ORDER        *
*        NIBBLE CONTAINS A VALUE GREATER THAN X'9'.                   *
*                                                                     *
*        4 INDICATES THE VALUE OF THE YEAR SPECIFIED WAS NOT IN       *
*        THE RANGE SPECIFIED IN 1 (ABOVE).                            *
*                                                                     *
*        6 INDICATES THE VALUE OF THE MONTH SPECIFIED WAS NOT IN      *
*        THE RANGE SPECIFIED IN 2 (ABOVE).                            *
*                                                                     *
*        8 INDICATES THE VALUE OF THE DAY SPECIFIED WAS NOT IN        *
*        THE RANGE SPECIFIED IN 3 (ABOVE).                            *
*                                                                     *
*    4.  UPON SUCCESSFUL EXECUTION, THE INTEGRAL DAY NUMBER ON WHICH  *
*        THE INPUT DATE FALLS (0=MONDAY, 1=TUESDAY, ... 6=SUNDAY)     *
*        WILL BE PLACED IN THE 3RD PARAMETER FIELD.  IF THE RETURN    *
*        CODE IS A NON-ZERO VALUE (UNSUCCESSFUL EXECUTION), THE       *
*        3RD PARAMETER FIELD WILL CONTAIN THE VALUE OF 9 TO PROTECT   *
*        AGAINST UNINTENTIONAL USE OF AN INVALID RESULT.              *
*                                                                     *
* ******************************************************************* *
* ******************************************************************* *
         EJECT
Y2KDOWN  CSECT
*         PRINT NOGEN
         EYEC  'RETURN DAY OF WEEK FOR DATE'
*
         SAVE  (14,12)                 SAVE CALLER'S REGISTERS
*
         LR    R12,R15                 LOAD BASE WITH ENTRY ADDRESS
         USING Y2KDOWN,R12             ESTABLISH ADDRESSABILITY
*
         L     R11,0(,R1)              LOAD A(PARAMETERS)
         USING $PARMS,R11              ESTABLISH ADDRESSABILITY
*
         GETMAIN R,LV=$DYNLEN          GETMAIN DYNAMIC WORKAREA
         LR    R10,R1                  SAVE A(MEMORY)
*
         ST    R13,4(,R10)             STORE A(CALLER'S SAVE AREA)
         ST    R10,8(,R13)             BACK CHAIN A(MY SAVE AREA)
         LR    R13,R10                 SET A(MY SAVE AREA)
         USING $DYNMEM,R13             ESTABLISH ADDRESSABILITY
*
*    INSERT EYECATCHER MARKS AT BEGIN/END OF GETMAIN'D AREA.
*
         MVC   TOPFLAG,=C'Y2KDOWN GETMAIN BEGINS->'
         MVC   BOTFLAG,=C'<-Y2KDOWN GETMAIN ENDS'
*
*    INITIALIZE RETURN CODE AND DAY OF WEEK NUMBER.
*
         MVI   PRC,C'0'                ASSUME GOOD RETURN
         MVI   PDOWN,C'9'              UNDETERMINED DOWN
*
*    RETRIEVE ASTRONOMICAL NUMBER FOR DATE PASSED
*
         MVC   GDATE,PDATE             LOAD DATE TO PASS
         LA    R1,GTOAPARM             ADDRESS OF Y2KGTOA PARMS
         ST    R1,PLIST                STORE IN DYNAMIC AREA
         LA    R1,PLIST                ADDRESS TO PASS
         L     R15,=V(Y2KGTOA)         ADDRESS OF ROUTINE
         BALR  R14,R15                 CALL ROUTINE
*
*    SEE IF DATE WE RECEIVED FROM CALLER PASSED EDITS IN Y2KGTOA.
*    THIS MEANS WE DON'T HAVE TO REPEAT THE EDIT CODE HERE <G>!
*
         MVC   PRC,RCG                 ADOPT RECEIVED RETURN CODE
         CLI   RCG,C'0'                WAS EXECUTION SUCCESSFUL?
         BNE   RETURN                  IF NOT, EXIT TO CALLER
*
*    INTEGRAL DAY NUMBER IS THE REMAINDER OF ASTRO NUMBER / 7.
*
P1OK     PACK  WORK(8),ANUM(7)         PACK ASTRO NUMBER
         DP    WORK,=P'7'              DIVIDE BY 7
         UNPK  PDOWN,WORK+7(1)         UNPACK REMAINDER
         OI    PDOWN,X'F0'             REMOVE SIGN
*
*    RESTORE REGISTERS, FREE STORAGE, AND RETURN
*
RETURN   LR    R1,R13                  LOAD ADDRESS OF GETMAINED AREA
         L     R13,4(,R13)             RELOAD PREVIOUS SAVE AREA
         LA    R0,$DYNLEN              LOAD LENGTH OF GETMAINED AREA
         FREEMAIN R,LV=(0),A=(1)       FREE GETMAINED AREA
         RETURN (14,12),RC=0           RETURN TO CALLER WITH RC=0
*
         LTORG
         EJECT
* ******************************************************************* *
* ***************************** EQUATES ***************************** *
* ******************************************************************* *
         YREGS
* ******************************************************************* *
         EJECT
* ******************************************************************* *
* ***************************** DSECTS ****************************** *
* ******************************************************************* *
$DYNMEM  DSECT                         DYNAMICALLY ALLOCATED MEMORY
SAVEAREA DS    18F                     REGISTER SAVE AREA
TOPFLAG  DS    CL24                    EYECATCHER
PLIST    DS    A                       PARMLIST ADDR FOR SUB CALLS
GTOAPARM DS    0H                      PARAMETERS FOR Y2KGTOA
GDATE    DS    CL8                       DATE INPUT
RCG      DS    CL1                       RETURN CODE
ANUM     DS    ZL7                       ASTRO NUMBER
WORK     DS    PL8                     WORK FIELD FOR DIVISION
BOTFLAG  DS    CL22                    EYECATCHER
*
$DYNLEN  EQU   (*-$DYNMEM)             LENGTH OF GETMAIN'D AREA
*
$PARMS   DSECT                         PARAMETERS FROM/TO CALLER
PDATE    DS    CL8                     DATE TO BE PROCESSED
PRC      DS    CL1                     RETURN CODE (0/2/4/6/8)
PDOWN    DS    CL1                     INTEGRAL DAY NUMBER
* ******************************************************************* *
         END
