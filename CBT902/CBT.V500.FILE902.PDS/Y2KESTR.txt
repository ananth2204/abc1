         TITLE 'Y2KESTR - DETERMINES DATE ON WHICH EASTER FALLS'
* ******************************************************************* *
* ******************************************************************* *
*  THIS PROGRAM IS COPYRIGHTED BY:  MR. JAY MOSELEY, CCP              *
*                                   200 HEYWOOD AVE 1503              *
*                                   SPARTANBURG, SC 29307-1792        *
*  NO USE MAY BE MADE OF THE DESIGN, CODING STRUCTURE, OR ANY PART    *
*  THEREOF WITHOUT THE EXPRESS WRITTEN PERMISSION OF THE AUTHOR IN    *
*  ADVANCE.  (1996, 1997, 1998, 1999, 2000)                           *
* ******************************************************************* *
* ******************************************************************* *
*                                                                     *
*   YY   YY   222   KK   KK EEEEEE   SSSSS TTTTTTTT RRRRRR            *
*   YY   YY  2   2  KK  KK  EE      SS   SS   TT    RR   RR           *
*   YY   YY      2  KK KK   EE      SS        TT    RR   RR           *
*    YY YY       2  KKKK    EEEE     SSSSS    TT    RR   RR           *
*     YYY     222   KKKK    EE           SS   TT    RRRRRR            *
*     YY     2      KK KK   EE      SS   SS   TT    RR RR             *
*     YY     2      KK  KK  EE      SS   SS   TT    RR  RR            *
*     YY     22222  KK   KK EEEEEEE  SSSSS    TT    RR   RR           *
*                                                                     *
* ******************************************************************* *
* ******************************************************************* *
*    THIS SUBROUTINE DETERMINES THE DATE ON WHICH EASTER FALLS        *
*    FOR A GIVEN YEAR                                                 *
*                                                                     *
*    1.  VALID INPUT YEARS FOR THIS ROUTINE ARE THE YEARS 1601        *
*        THROUGH 3399 A. D. (INCLUSIVE)                               *
*                                                                     *
*    2.  THREE FIELDS ARE PASSED AS PARAMETERS TO THE ROUTINE:        *
*        1)  A 4 BYTE FIELD CONTAINING THE YEAR TO BE PROCESSED       *
*            (IN ZONED-DECIMAL FORMAT),                               *
*        2)  A 1 BYTE ZONED-DECIMAL RETURN CODE, AND                  *
*        3)  AN 8 BYTE FIELD WHICH WILL RECEIVE THE GREGORIAN DATE    *
*            ON WHICH EASTER FALLS (IN ZONED-DECIMAL FORMAT).         *
*        THE ROUTINE WILL NOT ALTER THE YEAR PASSED TO IT.            *
*                                                                     *
*        SUGGESTED CALLING SYNTAX FOR ALC CALLERS:                    *
*                                                                     *
*                      CALL  Y2KESTR,(YEAR)                           *
*                         . . .                                       *
*             YEAR     DS    ZL4'1997'                                *
*             RC       DS    ZL1'0'                                   *
*             EASTER   DS    CL8'00000000'                            *
*                                                                     *
*        SUGGESTED CALLING SYNTAX FOR COBOL CALLERS:                  *
*                                                                     *
*             01  ESTR-PARAMETERS.                                    *
*                 02  YEAR   PIC 9(4) VALUE 1997.                     *
*                 02  RC     PIC 9(1) VALUE 0.                        *
*                 02  EASTER PIC 9(8) VALUE 0.                        *
*             CALL 'Y2KESTR' USING ESTR-PARAMETERS.                   *
*                                                                     *
*    3.  THE POSSIBLE VALUES FOR THE RETURN CODE FIELD ARE:           *
*                                                                     *
*        0 INDICATES SUCCESSFUL EXECUTION OF THE ROUTINE.             *
*                                                                     *
*        2 INDICATES INVALID DATA WAS FOUND IN THE YEAR FIELD.        *
*        INVALID DATA ARE DETERMINED IF THE FIELD'S LOW ORDER         *
*        BYTE'S ZONE CONTAINS AN INVALID SIGN, (NOT ONE OF            *
*        X'C', X'A', X'E', OR X'F'), OR IF THE PRECEDING BYTES'       *
*        ZONES ARE OTHER THAN X'F', OR IF ANY BYTE'S LOW ORDER        *
*        NIBBLE CONTAINS A VALUE GREATER THAN X'9'.                   *
*                                                                     *
*        4 INDICATES THE VALUE OF THE YEAR SPECIFIED WAS NOT IN       *
*        THE RANGE SPECIFIED IN 1 (ABOVE).                            *
*                                                                     *
*    4.  UPON SUCCESSFUL EXECUTION, THE GREGORIAN DATE (MMDDYYYY)     *
*        ON WHICH EASTER FALLS FOR THE GIVEN YEAR WILL BE PLACED      *
*        IN THE 3RD PARAMETER FIELD.  IF THE RETURN CODE IS A         *
*        NON-ZERO VALUE (UNSUCCESSFUL EXECUTION), THE OUTPUT DATE     *
*        FIELD WILL CONTAIN ZEROS.                                    *
*                                                                     *
* ******************************************************************* *
* ******************************************************************* *
         EJECT
Y2KESTR  CSECT
         PRINT NOGEN
         EYEC  'RETURN EASTER DATE FOR GIVEN YEAR'
*
         SAVE  (14,12)                 SAVE CALLER'S REGISTERS
*
         LR    R12,R15                 LOAD BASE WITH ENTRY ADDRESS
         USING Y2KESTR,R12             ESTABLISH ADDRESSABILITY
*
         L     R11,0(,R1)              LOAD A(PARAMETERS)
         USING $PARMS,R11              ESTABLISH ADDRESSABILITY
*
         GETMAIN R,LV=$DYNLEN          GETMAIN DYNAMIC WORKAREA
         LR    R10,R1                  SAVE A(MEMORY)
*
         ST    R13,4(,R10)             STORE A(CALLER'S SAVE AREA)
         ST    R10,8(,R13)             BACK CHAIN A(MY SAVE AREA)
         LR    R13,R10                 SET A(MY SAVE AREA)
         USING $DYNMEM,R13             ESTABLISH ADDRESSABILITY
*
*    INSERT EYECATCHER MARKS AT BEGIN/END OF GETMAIN'D AREA.
*
         MVC   TOPFLAG,=C'Y2KESTR GETMAIN BEGINS->'
         MVC   BOTFLAG,=C'<-Y2KESTR GETMAIN ENDS'
*
*    INITIALIZE RETURN CODE AND EASTER OUTPUT DATE.
*
         MVI   PRC,C'0'                ASSUME GOOD RETURN
         MVC   PRC+1(8),PRC            ZERO RETURN DATE
*
*    TEST YEAR FOR INVALID CHARACTER
*
         LA    R2,PYEAR                ADDRESS PASSED YEAR
         LA    R3,3                    LENGTH MINUS 1
P1VLOOP  CLI   0(R2),X'F0'             TEST LESS THAN C'0'
         BL    P1ERR1                  IF YES, YEAR CONTAINS BAD DATA
         CLI   0(R2),X'F9'             TEST GREATER THAN C'9'
         BH    P1ERR1                  IF YES, YEAR CONTAINS BAD DATA
         LA    R2,1(0,R2)              ADDRESS NEXT BYTE
         BCT   R3,P1VLOOP              CONTINUE TESTING
         IC    R3,0(,R2)               LOAD RIGHT-MOST CHARACTER
         LR    R4,R3                   COPY INTO R4
         N     R3,=F'15'               ISOLATE DIGIT PORTION
         CH    R3,=H'9'                TEST GREATER THAN 9
         BH    P1ERR1                  IF YES, YEAR CONTAINS BAD DATA
         SRL   R4,4                    ISOLATE ZONE PORTION
         CH    R4,=H'12'               TEST FOR 'C' ZONE
         BE    P1OK1                   IF YES, YEAR IS VALID NUMERIC
         CH    R4,=H'13'               TEST FOR 'D' ZONE
         BE    P1OK1                   IF YES, YEAR IS VALID NUMERIC
         CH    R4,=H'15'               TEST FOR 'F' ZONE
         BE    P1OK1                   IF YES, YEAR IS VALID NUMERIC
*
*    YEAR CONTAINED AN INVALID CHARACTER
*
P1ERR1   MVI   PRC,C'2'                INDICATE BAD DATA
         B     RETURN                  AND RETURN TO CALLER
*
*    TEST YEAR FOR VALID RANGE
*
P1OK1    PACK  P3(3),PYEAR             PACK PASSED YEAR
         CP    P3(3),=P'1601'          IS YEAR PRIOR TO 1601?
         BL    P1ERR2                  IF YES, YEAR NOT VALID
         CP    P3(3),=P'3399'          IS YEAR AFTER 3399?
         BNH   P1OK                    IF NOT, YEAR IS VALID
*
*    YEAR CONTAINED AN INVALID VALUE (OUT OF RANGE)
*
P1ERR2   MVI   PRC,C'4'                INDICATE BAD VALUE
         B     RETURN                  AND RETURN TO CALLER
*
*    STEP 1: COMPUTE CENTURY NUMBER + 1.
*
P1OK     PACK  CENT,PYEAR(2)           DIVIDE BY 100
         AP    CENT,=P'1'              ADD 1
*
*    STEP 2: COMPUTE NON-LEAP-YEAR CENTESIMALS.
*
         ZAP   P5,P3                   EXPAND FULL YEAR
         SP    P5,=P'1600'             SUBTRACT 1600
         DP    P5,=P'400'              DIVIDE BY 400
         ZAP   T2,P5+1(2)              TAKE QUOTIENT
         MP    T2,=P'3'                MULTIPLY BY 3
         ZAP   P5,P5+3(2)              TAKE REMAINDER
         DP    P5,=P'100'              DIVIDE BY 100
         ZAP   T3,P5+2(1)              SAVE QUOTIENT
         ZAP   NONL,T2                 COMBINE THE
         AP    NONL,T3                   TWO ANSWERS
*
*    STEP 3: COMPUTE YEAR'S POSITION IN METONIC CYCLE.
*
         ZAP   P5,P3                   EXPAND FULL YEAR
         DP    P5,=P'19'               DIVIDE BY 19
         AP    P5+3(2),=P'1'           ADD 1 TO REMAINDER
         ZAP   MET,P5+3(2)             SAVE ANSWER
*
*    STEP 4: COMPUTE CORRECTION FOR METONIC CYCLE.
*
         ZAP   P5,CENT                 EXPAND CENTURY NUMBER
         MP    P5,=P'8'                MULTIPLY BY 8
         AP    P5,=P'5'                ADD 5
         DP    P5,=P'25'               DIVIDE BY 25
         SP    P5(3),=P'5'             SUBTRACT 5
         SP    P5(3),NONL              SUBTRACT NONL
         ZAP   METC,P5+1(2)            SAVE ANSWER
*
*    STEP 5: COMPUTE DAY COUNT MODIFIER.
*
         ZAP   P5,P3                   EXPAND FULL YEAR
         MP    P5,=P'5'                MULTIPLY BY 5
         DP    P5,=P'4'                DIVIDE BY 4
         SP    P5(4),NONL              SUBTRACT NONL
         SP    P5(4),=P'10'            SUBTRACT 10
         ZAP   DMOD,P5+1(3)            SAVE ANSWER
*
*    STEP 6: COMPUTE THE EPACT.
*
         ZAP   P5,MET                  TAKE THE METONIC POSITION
         MP    P5,=P'11'               MULTIPLY BY 11
         AP    P5,=P'20'               ADD 20
         AP    P5,METC                 ADD THE METONIC CORRECTION
         DP    P5,=P'30'               DIVIDE BY 30
         ZAP   EPACT,P5+3(2)           SAVE THE ANSWER
*
*    STEP 7: DETERMINE THE EPACT VALUE.
*
         CP    EPACT,=P'25'            IS EPACT 25?
         BNE   EADJ                    NO
*
*    STEP 8: DETERMINE THE METONIC CYCLE POSITION FOR EPACT ADJUSTMENT.
*
         CP    MET,=P'11'              IS METONIC CYCLE 11?
         BNH   EADJ                    NO
         AP    EPACT,=P'1'             ADD 1 TO EPACT
*
*    STEP 9: DETERMINE THE EPACT VALUE FOR ADJUSTMENT.
*
EADJ     CP    EPACT,=P'24'            IS EPACT 24?
         BNE   DAYV                    NO
         AP    EPACT,=P'1'             ADD 1 TO EPACT
*
*    STEP 10: COMPUTE VALUE OF DAY.
*
DAYV     ZAP   DAY,=P'44'              INITIALIZE DAY TO 44
         SP    DAY,EPACT               SUBTRACT EPACT
*
*    STEP 11: DETERMINE DAY VALUE FOR ADJUSTMENT.
*
         CP    DAY,=P'21'              IS DAY 21 OR GREATER?
         BNL   CANDA                   YES
         AP    DAY,=P'30'              ADD 30 TO DAY
*
*    STEP 12: FIND MODIFIED DAY VALUE.
*
CANDA    ZAP   P4,DAY                  EXPAND DAY
         AP    P4,=P'7'                ADD 7
         ZAP   P5,DAY                  EXPAND DAY
         AP    P5,DMOD                 ADD DMOD
         DP    P5,=P'7'                DIVIDE BY 7
         SP    P4,P5+4(1)              SUBTRACT REMAINDER
         ZAP   DAY,P4+2(2)             SAVE ANSWER
*
*    STEP 13: FIND EXACT DATE:  IF DAY LESS THAN/EQUAL 31, EASTER IS
*    03/DAY/YR ELSE EASTER IS 04/(DAY - 31)/YR.
*
         MVC   PEASTER(2),=C'03'       ASSUME MONTH IS MARCH
         CP    DAY,=P'31'              IS DAY GREATER THAN 31?
         BNH   MARCH                   NO
         MVC   PEASTER(2),=C'04'       MONTH IS APRIL
         SP    DAY,=P'31'              ADJUST DAY
MARCH    UNPK  Z3,DAY                  UNPACK DAY
         OI    Z3+2,X'F0'              CLEAR SIGN
         MVC   PEASTER+2(2),Z3+1       MOVE DAY
         MVC   PEASTER+4(4),PYEAR      MOVE YEAR
*
*    RESTORE REGISTERS, FREE STORAGE, AND RETURN
*
RETURN   LR    R1,R13                  LOAD ADDRESS OF GETMAINED AREA
         L     R13,4(,R13)             RELOAD PREVIOUS SAVE AREA
         LA    R0,$DYNLEN              LOAD LENGTH OF GETMAINED AREA
         FREEMAIN R,LV=(0),A=(1)       FREE GETMAINED AREA
         RETURN (14,12),RC=0           RETURN TO CALLER WITH RC=0
*
         LTORG
         EJECT
* ******************************************************************* *
* ***************************** EQUATES ***************************** *
* ******************************************************************* *
         YREGS
* ******************************************************************* *
         EJECT
* ******************************************************************* *
* ***************************** DSECTS ****************************** *
* ******************************************************************* *
$DYNMEM  DSECT                         DYNAMICALLY ALLOCATED MEMORY
SAVEAREA DS    18F                     REGISTER SAVE AREA
TOPFLAG  DS    CL24                    EYECATCHER
P3       DS    PL3                     WORK AREA
P4       DS    PL4                     WORK AREA
P5       DS    PL5                     WORK AREA
Z3       DS    ZL3                     WORK AREA
CENT     DS    PL2                     CENTURY NUMBER
T2       DS    PL2                     INTERMEDIATE ANSWER
T3       DS    PL1                     INTERMEDIATE ANSWER
NONL     DS    PL2                     INTERMEDIATE ANSWER
MET      DS    PL2                     INTERMEDIATE ANSWER
METC     DS    PL2                     INTERMEDIATE ANSWER
DMOD     DS    PL3                     INTERMEDIATE ANSWER
EPACT    DS    PL2                     INTERMEDIATE ANSWER
DAY      DS    PL2                     INTERMEDIATE ANSWER
BOTFLAG  DS    CL22                    EYECATCHER
*
$DYNLEN  EQU   (*-$DYNMEM)             LENGTH OF GETMAIN'D AREA
*
$PARMS   DSECT                         PARAMETERS FROM/TO CALLER
PYEAR    DS    CL4                     YEAR TO BE PROCESSED
PRC      DS    CL1                     RETURN CODE (0/2/4)
PEASTER  DS    CL8                     DATE OF EASTER
* ******************************************************************* *
         END
