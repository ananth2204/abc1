 /* REXX DHOST - Displays the current host command table             */
 /*------------------------------------------------------------------*/
 /* Get the current ASCB address.                                    */
 /*-+----1----+----2----+----3----+----4----+----5----+----6----+----*/
  ascbad = c2x(storage('224',4))
 /*------------------------------------------------------------------*/
 /* Get the current ASXB address.                                    */
 /*-+----1----+----2----+----3----+----4----+----5----+----6----+----*/
  ascbasxb = d2x(x2d(ascbad)+x2d('6c'))
  asxbad = c2x(storage(ascbasxb,4))
 /*------------------------------------------------------------------*/
 /* Get the current LWA (Logon Work Area) address.                   */
 /*-+----1----+----2----+----3----+----4----+----5----+----6----+----*/
  asxblwa = d2x(x2d(asxbad)+x2d('14'))
  lwaad = c2x(storage(asxblwa,4))
 /*------------------------------------------------------------------*/
 /* Get the current ECT (Environment Control Table) address.         */
 /*-+----1----+----2----+----3----+----4----+----5----+----6----+----*/
  lwapect = d2x(x2d(lwaad)+x2d('20'))
  ectad = c2x(storage(lwapect,4))
 /*------------------------------------------------------------------*/
 /* Get the current REXX environment control block address.          */
 /*-+----1----+----2----+----3----+----4----+----5----+----6----+----*/
  ectenvbk = d2x(x2d(ectad)+x2d('30'))
  envbkad = c2x(storage(ectenvbk,4))
  say 'Environment Block Address='envbkad
 /*------------------------------------------------------------------*/
 /* Get the parmblock address.                                       */
 /*-+----1----+----2----+----3----+----4----+----5----+----6----+----*/
  pabkadad = d2x(x2d(envbkad)+16)
  pabkad = c2x(storage(pabkadad,4))
 /*------------------------------------------------------------------*/
 /* Get the host command environment table header address.           */
 /*-+----1----+----2----+----3----+----4----+----5----+----6----+----*/
  sbcmtbadad = d2x(x2d(pabkad)+20)
  sbcmtbad = c2x(storage(sbcmtbadad,4))
 /*------------------------------------------------------------------*/
 /* Get the number of entries in the table.                          */
 /*-+----1----+----2----+----3----+----4----+----5----+----6----+----*/
  hocmencnad = d2x(x2d(sbcmtbad)+8)
  hocmencn = c2d(storage(hocmencnad,4))
 /*------------------------------------------------------------------*/
 /* Loop through the entries displaying each one in character        */
 /* and in printable hex.                                            */
 /*-+----1----+----2----+----3----+----4----+----5----+----6----+----*/
  hocmenad = c2x(storage(sbcmtbad,4))
  do i = 1 to hocmencn                /* for each used entry...     */
     hocmen = storage(hocmenad,32)    /* extract the entry          */
     say hocmen
     say c2x(hocmen)
     SAY
     hocmenad = d2x(x2d(hocmenad)+32) /* point to next entry        */
  end                                 /* end of used entry loop     */
