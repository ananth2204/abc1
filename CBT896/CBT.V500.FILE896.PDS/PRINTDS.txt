PROC 0 DEBUG

/* ----------------------------------------------------------------- */
/*                                                                   */
/* -----------------------------------                               */
/* TSO/E PRINTDS ISPF DIALOG FRONT-END                               */
/* -----------------------------------                               */
/*                                                                   */
/* - UPDATED 10SEP90, WILLIAM SMITH, CHARLES SCHWAB TECH. SERVICES   */
/*   . TESTED WITH ISPF AND ISPF/PDF 3.1 AND MVS/ESA TSO/E 2.1.1     */
/*   . STREAMLINED PROCESSING BY ELIMINATING REDUNDANT/UNNECESSARY   */
/*     PANEL AND CLIST PROCESSING LOGIC                              */
/*                                                                   */
/* - UPDATED 20NOV89, WILLIAM SMITH, CHARLES SCHWAB TECH. SERVICES   */
/*   . ORIGINALLY WRITTEN AS AN ISPF DIALOG MANAGER FRONT-END FOR    */
/*     USE WITH "VTAM PRINTER SUPPORT"; UPDATED AND MODIFIED TO      */
/*     FRONT-END THE TSO/E 'PRINTDS' COMMAND PROCESSOR               */
/*   . TESTED WITH ISPF AND ISPF/PDF 3.1 AND MVS/XA TSO/E 2.1.0      */
/*                                                                   */
/* ------------------------------------                              */
/* RETURN CODES FOR THE PRINTDS COMMAND                              */
/* ------------------------------------                              */
/*                                                                   */
/*   0  PROCESSING SUCCESSFUL                                        */
/*   4  PROCESSING COMPLETED, BUT A WARNING MESSAGE HAS BEEN         */
/*      ISSUED.                                                      */
/*   8  THE INPUT, OUTPUT, OR SYSOUT DATA SET COULD NOT BE USED.     */
/*  12  AN ERROR OCCURRED DURING THE PROCESSING OF THE PRINTDS       */
/*      COMMAND.                                                     */
/*  16  THE INSTALLATION EXIT REQUESTED TERMINATION OF THE PRINTDS   */
/*      COMMAND.                                                     */
/*                                                                   */
/* ----------------------------------------------------------------- */

IF &DEBUG = DEBUG THEN +
   CONTROL MSG NOFLUSH CONLIST SYMLIST LIST
ELSE +
   CONTROL NOMSG NOFLUSH NOLIST

/* ----------------------------------------------------------------- */
/* SET INITIAL SWITCHES, AND DISPLAY THE PANEL FOR THE FIRST TIME.   */
/* ----------------------------------------------------------------- */

ISPEXEC CONTROL ERRORS RETURN
SET &ERRORSW = OFF

ISPEXEC DISPLAY PANEL(SCH0P200)
SET &RC = &LASTCC

/* ----------------------------------------------------------------- */
/* PROCESS THE FIELDS OF THE PANEL AND GENERATE THE PRINTDS COMMAND  */
/* AS MANY TIMES AS REQUIRED.  REDISPLAY THE PANEL AFTER EACH DATA   */
/* SET IS PROCESSED.                                                 */
/* ----------------------------------------------------------------- */

DO WHILE &RC = 0

/* ----------------------------------------------------------------- */
/* ESTABLISH AN ERROR EXIT FOR THE PRINTDS COMMAND.  IF PRINTDS      */
/* SHOULD GENERATE A RETURN CODE > 0, (S013 - MISSING MEMBER, NON-   */
/* EXISTANT DATA SET, I/O ERROR, ETC.,), DISPLAY A MESSAGE THAT      */
/* PRINTDS FAILED AND THEN RE-DISPLAY THE PRINTDS OPTIONS PANEL.     */
/* ----------------------------------------------------------------- */

  CONTROL MSG
  ERROR +
    DO
      SET &PRTRC = &LASTCC
      IF &PRTRC > 0 THEN +
        SET &ERRORSW = ON
      RETURN
    END

/* ----------------------------------------------------------------- */

  PRINTDS &DSNAME +
          &DEST +
          &COPIES +
          &PRINTOPT +
          &SYSOUT +
          &FORMS +
          &TITLE +
          &OUTDES +
          &FCB +
          &WRITER +
          &SPACING +
          &PAGELEN +
          &TMARGIN +
          &BMARGIN +
          &OPTIONS

/* ----------------------------------------------------------------- */
/* TERMINATE THE ERROR EXIT UPON RETURN.  IF THE ERROR EXIT WAS      */
/* TAKEN, RE-DISPLAY THE PANEL TO ALLOW THE USER TO CORRECT THE      */
/* FIELD(S) IN ERROR, DISPLAY AN ERROR MESSAGE, AND EXIT IF REQUEST- */
/* ED BY THE USER.                                                   */
/*                                                                   */
/* OTHERWISE, THE USER'S PARAMETERS SHOULD BE PROCESSED, A MESSAGE   */
/* LOGGED, AND AN INFORMATIONAL MESSAGE DISPLAYED THAT PRINTDS HAS   */
/* SUCCESSFULLY COMPLETED ITS PROCESSING.                            */
/* ----------------------------------------------------------------- */

  ERROR OFF
  IF &ERRORSW = ON THEN +
    DO
      ISPEXEC DISPLAY CURSOR(DSN) MSG(SCHP020C)
      SET &RC = &LASTCC
      IF &RC >= 8 THEN +
        DO
          ISPEXEC SETMSG MSG(SCHP020A)
          EXIT
        END
      ELSE +
        SET &ERRORSW = OFF
    END
  ELSE +
    DO
      CONTROL NOMSG
      ISPEXEC LOG MSG(SCHP020B)
      ISPEXEC DISPLAY CURSOR(DSN) MSG(SCHP029)
      SET &RC = &LASTCC
      IF &RC >= 8 THEN +
        DO
          ISPEXEC SETMSG MSG(SCHP020A)
          EXIT
        END
    END
END
