/* REXX       */

/*********************************************************************/
/*                                                                   */
/*                                                                   */
/*   (C) COPYRIGHT ISIS GES.M.B.H                                    */
/*                                                                   */
/*                                                                   */
/*   PROCEDURENAME.......: CHANGEXX                                  */
/*                                                                   */
/*   FUNCTION ...........: executes a general change of an item      */
/*                         in all members of the passed PDS DS       */
/*                                                                   */
/*   NOTES ..............:  1. The base mode is address ISPEXEC      */
/*                                                                   */
/*   CHANGES.............:  1. 97.04.10 member name selection        */
/*                                                                   */
/*                                                                   */
/*                                                                   */
/*                                                                   */
/*************************************************** ISIS 1996.08 ****/
   cstar  = '*'

   ADDRESS ISPEXEC

   parse arg dsn memmask from to .                       /*m:97.04.10.*/
   dsn = translate(dsn)
   memmask = translate(memmask)                          /*n:97.04.10.*/

   "LMINIT  DATAID(EDITDSN) DATASET('"dsn"') ENQ(SHR)"
   if rc > 0 ,
   then do
      say "RC="rc", was returned from LMINIT of dataset "dsn
      exit 12
        end

   "VPUT (from) PROFILE"
   "VPUT (to  ) PROFILE"

   /* start of changes to get the prefix string           n:97.04.10.*/
   pos_star     = pos(cstar,memmask)
   if pos_star > 1 ,
   then memmask_pref = substr(memmask,1,pos_star-1)
   else do
      if pos_star = 1 ,
      then memmask_pref = ''
      else memmask_pref = memmask
        end
   /* end   of changes to get the prefix string           n:97.04.10.*/

   "LMOPEN  DATAID("EDITDSN")"
   if rc > 0 ,
   then do
      say "RC="rc", was returned from LMOPEN of dataset "dsn
      exit 12
        end
   "LMMLIST DATAID("EDITDSN") MEMBER(EDITMEM)"
   do while (rc Â¬= 8)
      if rc > 0 ,
      then do
         say "RC="rc", was returned from LMMLIST dataset "dsn  ,
             "("editmem")"
         exit 12
           end
      /* start of changes to include member selection     n:97.04.10.*/
      if pos_star > 1  ,
      then editmem_pref = substr(editmem,1,pos_star-1)
      else do
         if pos_star = 1
         then editmem_pref = ''
         else editmem_pref = editmem
           end
      if editmem_pref = memmask_pref ,
      then "EDIT DATAID("EDITDSN") MEMBER("EDITMEM") MACRO(MCHANGE)"
      /* end   of changes to include member selection     n:97.04.10.*/
      if rc > 0 ,
      then do
         say "RC="rc", was returned from editing dataset "dsn  ,
             "("editmem")"
         exit 12
           end
      "LMMLIST DATAID("EDITDSN") MEMBER(EDITMEM)"
   end
   "LMCLOSE DATAID("EDITDSN")"

   return 0

