         MACRO
         YREGS
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         MEND
         COPY  IEABRC
         YREGS                    REGISTER EQUATES
         EJECT
REXXRACF CSECT ESTABLISH CSECT
REXXRACF AMODE 31
REXXRACF RMODE ANY
         SAVE  (14,12),,REXXRACF-&SYSDATE-&SYSTIME
         LR    R12,R15            LOAD R12 W/EPA ADDRESS
         USING REXXRACF,R12
         LR    R9,R1              SAVE R1
         GETMAIN RU,LV=WORKLEN
         LR    R8,R1
         LR    R10,R1             SAVE POINTER IN R8 AND IN R10
         LR    R1,R9              SAVE BACK R1
         LARL  R4,INTOWORK        MOVE TO WORKAREA
         LA    R5,WORKLEN
         LR    R9,R5              LENGTH IN BOTH REGS
         MVCL  R8,R4              INITIALIZE WORKAREA
         USING WORKAREA,R8
         LR    R8,R10             SAVE BACK R8
         LA    R15,SAVEAREA       LOAD ADDR OF SAVEAREA
         ST    R15,8(,R13)        MY SAVEAREA ADDR INTO CALLER'S
         ST    R13,4(,R15)        CALLER SAVEAREA ADDR INTO MINE
         LR    R13,R15            LOAD MY SAVEAREA ADDR
         L     R2,0(,R1)
         LH    R3,0(,R2)          PICK LENGTH OF STRING
         LTR   R3,R3
         BRP   STRLOPM1           LENGTH POSITIV
         LGFI  R2,NOPARM          NO PARAMETER, LENGTH NOT POSITIV
         BRU   RETURN             NO PARAMETER
STRLOPM1 LA    R2,1(0,R2)         ADDRESS OF STRING - 1
         SR    R4,R4
STRLOP   DS    0H
         LA    R2,1(,R2)
         CLI   0(R2),C' '         IS IT BLANK ?
         BRNE  STRLOP01           1-ST NO-BLANK FOUND
         BRCT  R3,STRLOP
         LGFI  R2,NOPARM          NO PARAMETER
         BRU   RETURN             NO PARAMETER
* LEADING BLANKS SKIPPED R2 POINTS TO 1-ST NON-BLANK
STRLOP01 DS    0H
         LR    R6,R2              SAVE START POSITION
         BCTR  R2,R0
         LA    R5,ENTYNAME
         BCTR  R5,R0
STRLOP02 DS    0H
         LA    R2,1(,R2)
         CLI   0(R2),C' '         IS IT BLANK?
         BRE   STRLOP03           1-ST BLANK FOUND
         IC    R4,0(,R2)          PICK NEXT CHARACTER
         LA    R5,1(,R5)
         STC   R4,0(,R5)
         BRCT  R3,STRLOP02
         LGFI  R2,ONEPARM         ONE PARAMETER
         BRU   RETURN
* 1-ST PARAMETER COPIED
STRLOP03 DS    0H
         SR    R6,R2
         LPR   R6,R6              PARAMETER LENGTH IN R6
         CFI   R6,MAXLN1          1-ST PAR MAX LENGTH
         BRNH  STRLOP35
         LGFI  R2,LNGPARM1        1-ST PARM TOO LONG
         BRU   RETURN
STRLOP35 BCTR  R2,R0              SUBTRACT 1
STRLOP04 DS    0H
         LA    R2,1(,R2)          PICK NEXT CHARACTER
         CLI   0(R2),C' '         IS IT BLANK ?
         BRNE  STRLOP05           OUT IF NOT
         BRCT  R3,STRLOP04
         LGFI  R2,ONEPARM         ONE PARAMETER
         BRU   RETURN             NO PARAMETER
* BLANKS BETWEEN 1-ST AND 2-ND PARAMETER SKIPPED
STRLOP05 DS    0H
         LR    R5,R2              SAVE START POSITION OF 2-ND PARM
         BCTR  R2,R0
STRLOP06 DS    0H
         LA    R2,1(,R2)          PICK NEXT CHARACTER
         CLI   0(R2),C' '         IS IT BLANK ?
         BRE   STRLOP07           OUT IF YES
         BRCT  R3,STRLOP06
         LGFI  R2,TWOPARM         TWO PARAMETER
         BRU   RETURN
STRLOP07 DS    0H
         LR    R6,R5              SAVE START ADDR USED IN EXCHKATR
         SR    R5,R2
         LPR   R5,R5              IN R5 LENGTH OF 2-ND OPERAND
         LARL  R7,ATTRTABL        ATTRIBUTE TABLE ADDRESS
         USING ATTRDSCT,R7
STRLOP08 DS    0H
         CLC   ATTRTBND(TABENTLN),ATTRLEN ARE WE AT END OF TABLE
         BRNE  STRLOP85           NOT END
         LGFI  R2,INVPARM         INVALID 2-ND PARAMETER
         BRU   RETURN             YES, ARG2 IN ERROR, INVALID
STRLOP85 SR    R15,R15            CLEAR R15 FOR IC
         IC    R15,ATTRLEN        ELSE GET LENGTH OF EBCDIC ENTRY
         CR    R5,R15             IS THE SAME LENGTH
         BRNE  STRLOP09           NO, TAKE NEXT PARM
         BCTR  R15,R0             DECREMENT R15 FOR EXECUTE
         EX    R15,EXCHKATR       EXECUTE CLC OF ARG3 TO TABLE ENTRY
         BRE   STRLOP10           IF MATCH, GET ATTR BYTE
STRLOP09 DS    0H
         LA    R7,TABENTLN(,R7)   ELSE BUMP TO NEXT TABLE ENTRY
         BRU   STRLOP08           LOOP TO CHECK NEXT TABLE ENTRY
STRLOP10 DS    0H
         SR    R5,R5              CLEAR REGISTER
         IC    R5,ATTRHEX         INSERT ATTR VALUE
*        DROP  R7
         BCTR  R2,R0
STRLOP11 DS    0H
         LA    R2,1(,R2)          PICK NEXT CHARACTER
         CLI   0(R2),C' '         IS IT BLANK ?
         BRNE  STRLOP12           OUT IF NO
         BRCT  R3,STRLOP11
         LGFI  R2,TWOPARM         TWO PARAMETER
         BRU   RETURN
* BLANKS BETWEEN 2-ND AND 3-RD PARAMETER SKIPPED
STRLOP12 DS    0H
         LR    R7,R2              SAVE START POSITION
         LA    R6,CLASSNAM        COPY HIER
         BCTR  R2,R0              SUBTRACT 1
         BCTR  R6,R0              SUBTRACT 1
STRLOP13 DS    0H
         LA    R2,1(,R2)
         LA    R6,1(,R6)
         CLI   0(R2),C' '         IS IT BLANK ?
         BRE   STRLOP14           OUT IF YES
         IC    R4,0(,R2)
         STC   R4,0(,R6)          STORE
         BRCT  R3,STRLOP13
         LA    R2,1(,R2)          SO THAT LENGTH OK IF NO TRAIL BLANK
STRLOP14 DS    0H
         SR    R2,R7              LENGTH OF 3-RD PARAM
         STC   R2,CLASSLEN
         CFI   R2,MAXLN3          LENGTH MAX 8 BYTE
         BRNH  SETREGS
         LGFI  R2,LNGPARM3        3-RD PARAM TOO LONG
         BRU   RETURN
SETREGS  DS    0H
         LA    R3,RACFWKA         LOAD A(RACF WORKAREA) FOR RACROUTE
         LA    R4,ENTYNAME        LOAD A(ENTITY NAME) FOR RACROUTE
         LA    R6,CLASSLEN        LOAD A(LEN,CLASS NAME) FOR RACROUTE
         LA    R1,RACROUTE        LOAD PARAMETER ADDRESS FOR RACROUTE
         BRU   CHEKAUTH           JUMP OVER CONST
         DC    C'11111122222222333333333'
CHEKAUTH DS    0H
         RACROUTE REQUEST=AUTH,WORKA=(R3),ATTR=(R5),                   X
               ENTITY=((R4)),CLASS=(R6),                               X
               MF=(E,(R1))
         LTR   R2,R15             SAVE RETURN CODE
RETURN   DS    0H
         L     R13,SAVEAREA+4     LOAD R13 W/ADDR OF CALLERS S/A
         FREEMAIN RU,LV=WORKLEN,A=(R8)
         LR    R15,R2             LOAD BACK RETURN CODE
         RETURN (14,12),RC=(15)   RETURN TO OS WITH RETCODE IN R15
* FROM HIER CONSTANTS
EXCHKATR CLC   ATTRVAL(0),0(R6)   EXECUTED CLC OF ARG3 TO TABLE ENTRY
ATTRTABL DS    0F                 ATTRIBUTE TABLE---------------]
*ATTRDFLT DC    AL1(1),CL8'B       ',X'01'   DEFAULT ATTR ]
ATTRDFLT DC    AL1(4),CL8'READ    ',X'02'   DEFAULT ATTR ]
         DC    AL1(6),CL8'UPDATE  ',X'04'                ]
         DC    AL1(7),CL8'CONTROL ',X'08'                ]
         DC    AL1(5),CL8'ALTER   ',X'80'                ]
ATTRTBND DC    X'FF',8X'FF',X'FF' END OF TABLE INDICATOR ]
TABENTLN EQU   *-ATTRTBND    LENGTH OF A TABLE ENTRY-----]
         LTORG
* CONSTANTS COPIED INTO WORKAREA
INTOWORK DS    0F                 COPY INTO WORKAREA
         DC    18F'2'             SHOULD BE FIRST IN WORKAREA
         DC    512X'40'           RACROUTE WORK AREA
         DC    CL256' '           ENTITY NAME
         DC    AL1(L'CLASSNAM)    CLASSNAME LENGTH
         DC    CL256' '           CLASSNAME
* CONSTANTS END
*        RACROUTE REQUEST=AUTH,RACFIND=YES,MF=L
         RACROUTE REQUEST=AUTH,RACFIND=YES,MF=L
WORKLEN  EQU   *-INTOWORK         SO LONG WILL BE COPIED
NOPARM   EQU   4000               NO PARAMETER SPECIFIED
ONEPARM  EQU   4001               1  PARAMETER SPECIFIED
TWOPARM  EQU   4002               2  PARAMETER SPECIFIED
INVPARM  EQU   4003               2-ND PARM INVALID
LNGPARM1 EQU   4004               1-ST PARM TOO LONG
LNGPARM3 EQU   4005               3-RD PARM TOO LONG
MAXLN1   EQU   128                MAX LENGTH 1-ST PARM
MAXLN3   EQU   8                  MAX LENGTH 3-RD PARM
*
WORKAREA DSECT                    WORK AREA
* CONSTANTS COPIED FROM INTOWORK
SAVEAREA DS    18F                SHOULD BE FIRST IN WORKAREA
RACFWKA  DC    512X'40'           RACROUTE WORK AREA
ENTYNAME DC    CL256' '           ENTITY NAME
CLASSLEN DC    AL1(L'CLASSNAM)    CLASSNAME LENGTH
CLASSNAM DC    CL256' '           CLASSNAME
* CONSTANTS END COPIED FROM INTOWORK
*RACROUTE RACROUTE REQUEST=AUTH,RACFIND=YES,MF=L
RACROUTE RACROUTE REQUEST=AUTH,RACFIND=YES,MF=L
         DSECT
ATTRDSCT DSECT                    ATTRTABL MAPPING
ATTRLEN  DS    AL1                LENGTH OF EBCDIC ATTR
ATTRVAL  DS    CL8                EBCDIC ATTR VALUE
ATTRHEX  DS    X                  HEX EQUIVALENT OF EBCDIC ATTR
         DSECT
         END
