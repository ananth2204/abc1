000100 IDENTIFICATION DIVISION.
000200   PROGRAM-ID.    SYS005P0.
000300   INSTALLATION.  OCCIDENTAL CHEMICAL CORPORATION.
000400 DATE-COMPILED.   DATE.
000500 REMARKS. WRITTEN BY :
000600                             ROBERT P DZIALAK
000700                             OCCIDENTAL CHEMICAL CORPORATION
000800                             360 RAINBOW BLVD. SOUTH
000900                             NIAGARA FALLS , NEW YORK 14303
001000                             (716) 286-3251
001100*
001200* THIS PROGRAM CAN BE USED TO OBTAIN PRINT OUTPUT OF JCL CONTAINED
001300* ON THE LIBSPOOL ON-LINE DATA SET. SELECTION IS DONE USING A
001400* INPUT CARD WITH THE GENERIC JOB NAME OF THE JOB(S) OF INTEREST.
001500*
001600*  FORMAT OF THE INPUT SELECTION CARD IS AS FOLLOWS:
001700*          1111111111222222222233333333333444444444
001800* 1234567890123456789012345678901234567890123456789
001900* JOBNAME=NNNNNNNN,SELECT=ALL
002000* JOBNAME=NNNNNNNN,SELECT=LAST
002100* JOBNAME=NNNNNNNN,SELECT=TODAY-NN,START TIME=HH.MM
002200*
002300* IF NO SELECT OPTION IS SPECIFIED THEN SELECT=LAST IS ASSUMED.
002400*
002500* WHERE :NNNNNNNN - 1-8 CHARACTER GENERIC JOBNAME
002600*        ALL      - PRINT ALL JOBS WHICH MATCH THIS GENERIC JOB
002700*                   NAME
002800*        LAST     - PRINT THE MOST RECIENTLY RUN JOBS WHICH MATCH
002900*                   THIS GENERIC JOBNAME
003000*        TODAY-NN - PRINT ALL JOBS WHICH MATCH THIS GENERIC JOB
003100*                   NAME WHICH STARTED EXECUTION TODAY THRU NN
003200*                   DAYS PRIOR TO TODAY.
003300*                EG. TODAY-00 ===> SELECT JOBS BEGINNING EXECUTION
003400*                                  TODAY ONLY
003500*                    TODAY-01 ===> SELECT JOBS BEGINNING EXECUTION
003600*                                  TODAY AND YESTERDAY
003700*                             ETC.
003800*    OPTIONAL:
003900*        START TIME=HH.MM - START SELECTING JOBS ON THE OLDEST DAY
004000*                           AT THE HOUR (HH) AND MINUTE (MM) SHOWN
004100*
004200* IF AN ERROR IS FOUND IN THE FORMAT OF THE SELECTION INPUT CARD
004300* THEN AN ERROR REPORT IS PRODUCED SHOWING THE FORMAT OF THE INPUT
004400* CARD ALONG WITH THE INPUT CARD(S) IN ERROR.
004500*
004600* A 'LIBSPOOL MODULES SELECTED FOR PRINT REPORT' IS ALSO PRODUCED
004700* SHOWING THE JCL MODULES THAT WERE SELECTED FOR PRINT WITH THE
004800* CORRESPONDING SELECTION INPUT CARD.
004900*
005000* THE FOLLOWING USER WRITTEN SUBROUTINES ARE CALLED BY THIS
005100* PROGRAM:
005200*
005300*   SYS005PA - THIS ROUTINE IS PASSED THE JOB NAME, JOB NUMBER,
005400*              GREGORIAN RUN DATE, START RUN HOUR AND MINUTE, AND
005500*              END RUN HOUR AND MINUTE. THIS INFORMATION IS
005600*              FORMATTED ONTO A SEPARATOR PAGE WHICH IS WRITTEN
005700*              PRIOR TO THE JCL. IT SHOULD HELP LOCATE AND
005800*              IDENTIFY JCL MODULES THAT WERE SELECTED FOR
005900*              PRINTING.
006000*
006100*   SYS005PB - THIS ROUTINE IS USED TO OPEN, CLOSE, AND WRITE
006200*              THE RECORDS WRITTEN TO THE JCLPRINT DDNAME. IT IS
006300*              USED BY THE SYS005PA ROUTINE FOR THE SEPARATOR
006400*              PAGE AND BY THIS PROGRAM FOR THE JCL.
006500*
006600*
006700 ENVIRONMENT DIVISION.
006800  CONFIGURATION SECTION.
006900    SOURCE-COMPUTER. IBM-370.
007000    OBJECT-COMPUTER. IBM-370.
007100  INPUT-OUTPUT SECTION.
007200    FILE-CONTROL.
007300       SELECT SELECTCD              ASSIGN TO UT-S-SELECTCD.
007400       SELECT SELCTRPT              ASSIGN TO UT-S-SELCTRPT.
007500       SELECT ERRORRPT              ASSIGN TO UT-S-ERRORRPT.
007600 DATA DIVISION.
007700  FILE SECTION.
007800   FD  SELECTCD
007900       BLOCK CONTAINS 0 RECORDS
008000       LABEL RECORDS ARE STANDARD.
008100   01  SELECTCD-REC.
008200       02 JOB-NAME-LIT            PIC X(8).
008300       02 JOB-TO-SELECT.
008400          03 JOB-CHAR             PIC X    OCCURS 8 TIMES.
008500       02 SELECT-LIT              PIC X(8).
008600       02 SELECTION-MODE.
008700          03 SELECT-ID            PIC X(6).
008800          03 RELTVE-START-DATE    PIC 99.
008900          03 REL-START-DATE-XX REDEFINES RELTVE-START-DATE PIC XX.
009000       02 STRT-TME-LIT            PIC X(12).
009100       02 START-HH                PIC 99.
009200       02 FILLER                  PIC X.
009300       02 START-MM                PIC 99.
009400       02 END-TME-LIT             PIC X(10).
009500       02 END-HH                  PIC 99.
009600       02 FILLER                  PIC X.
009700       02 END-MM                  PIC 99.
009800       02 FILLER                  PIC X(16).
009900   FD  SELCTRPT
010000       BLOCK CONTAINS 0 RECORDS
010100       LABEL RECORDS ARE STANDARD.
010200   01  SELCTRPT-REC               PIC X(133).
010300   FD  ERRORRPT
010400       BLOCK CONTAINS 0 RECORDS
010500       LABEL RECORDS ARE STANDARD.
010600   01  ERRORRPT-REC               PIC X(133).
010700  WORKING-STORAGE SECTION.
010800   77  MO-CTR                     PIC S9(9) COMP VALUE +0.
010900   77  MOD-COUNT                  PIC S9(9) COMP VALUE +0.
011000   77  IDLE-DAYS                  PIC S9(9) COMP VALUE +0.
011100   77  LEAPTEST                   PIC S9(9) COMP VALUE +0.
011200   77  LEAP-RMNDR                 PIC S9(9) COMP VALUE +0.
011300   77  LEAP-TEST                  PIC S9(9) COMP VALUE +0.
011400   77  HOURS-ONLINE               PIC S9(9) COMP VALUE +0.
011500   77  JOB-DAYS                   PIC S9(9) COMP VALUE +0.
011600   77  JOB-HOUR                   PIC S9(9) COMP VALUE +0.
011700   77  CURRENT-DAYS               PIC S9(9) COMP VALUE +0.
011800   77  CURRENT-HOUR               PIC S9(9) COMP VALUE +0.
011900   77  RUN-HOUR                   PIC S9(9) COMP VALUE +0.
012000   77  BASE-MCD-CODE              PIC S9(9) COMP VALUE +0.
012100   77  PAGE-COUNT                 PIC S9(3) COMP-3 VALUE +0.
012200   77  LINE-COUNT                 PIC S9(3) COMP-3 VALUE +60.
012300   77  ERRORRPT-LINE-CTR          PIC S9(3) COMP-3 VALUE +60.
012400   77  ERRORRPT-PAGE-COUNT        PIC S9(3) COMP-3 VALUE +0.
012500   77  JOB-CTR                    PIC S9(3) COMP-3 VALUE +0.
012600   77  TABENTRY                   PIC S9(3) COMP-3 VALUE +0.
012700   77  SELECT-CARD-COUNT          PIC S9(3) COMP-3 VALUE +0.
012800   77  SEL-EOF-SW                 PIC S9    COMP-3 VALUE +0.
012900   77  ERRORRPT-OPEN-SW           PIC S9    COMP-3 VALUE +0.
013000   77  JOB-COMPARE-SW             PIC S9    COMP-3 VALUE +0.
013100   77  NO-MATCH-SW                PIC S9    COMP-3 VALUE +0.
013200   77  START-JUL-DATE             PIC 999      VALUE 0.
013300   77  REL-START-DATE-WS          PIC XX       VALUE SPACES.
013400   77  JOB-1                      PIC X(1)     VALUE SPACES.
013500   77  JOB-2                      PIC X(2)     VALUE SPACES.
013600   77  JOB-3                      PIC X(3)     VALUE SPACES.
013700   77  JOB-4                      PIC X(4)     VALUE SPACES.
013800   77  JOB-5                      PIC X(5)     VALUE SPACES.
013900   77  JOB-6                      PIC X(6)     VALUE SPACES.
014000   77  JOB-7                      PIC X(7)     VALUE SPACES.
014100   77  JOB-8                      PIC X(8)     VALUE SPACES.
014200   77  END-HH-WS                  PIC XX       VALUE SPACES.
014300   77  END-MM-WS                  PIC XX       VALUE SPACES.
014400*
014500   01  OPENID                     PIC X(4)     VALUE 'OPEN'.
014600   01  RITEID                     PIC X(4)     VALUE 'RITE'.
014700   01  CLSEID                     PIC X(4)     VALUE 'CLSE'.
014800*
014900   01  CARD-AREA-1                PIC X(80)    VALUE SPACES.
015000*
015100   01  CARD-AREA-2-DATA.
015200       02  CONTINUE-BYTE          PIC X.
015300       02  CARD-AREA-2            PIC X(79)    VALUE SPACES.
015400*
015500   01  BLANK-LINE                 PIC X(133)   VALUE SPACES.
015600*
015700   01  PRINT-AREA.
015800       02 FILLER                  PIC X.
015900       02 FILLER                  PIC X(79).
016000       02 PRINT-AREA-2            PIC X(53).
016100*
016200   01  HEADER-DATA.
016300       02 HDR-DTA-JBNME           PIC X(8).
016400       02 FILLER                  PIC X        VALUE '@'.
016500       02 HDR-DTA-JOB-NMBR        PIC X(4).
016600       02 FILLER                  PIC X        VALUE '@'.
016700       02 HDR-DTA-GREG-DTE        PIC X(8).
016800       02 FILLER                  PIC X        VALUE '@'.
016900       02 HDR-DTA-RUN-TIME.
017000           03 HDR-DTA-STRT-TIME       PIC X(5).
017100           03 HDR-DTA-DASH            PIC X        VALUE '-'.
017200           03 HDR-DTA-END-TIME        PIC X(5).
017300       02 FILLER                  PIC X        VALUE 'Â¬'.
017400*
017500   01  OPN-RETURN-CODE            PIC  X       VALUE SPACE.
017600*
017700   01  OPN-AREA                   PIC  X(16)   VALUE SPACE.
017800*
017900   01  MOD-NAME                   PIC  X(8)    VALUE SPACE.
018000*
018100   01  MOD-RETURN-CODE            PIC  X       VALUE SPACE.
018200*
018300   01  MOD-AREA.
018400       02 MOD-PASSWORD            PIC X(04).
018500       02 DATE-ADDED              PIC X(06).
018600       02 LAST-UP-DATE.
018700          03 LST-UP-MM            PIC 99.
018800          03 LST-UP-DD            PIC 99.
018900          03 LST-UP-YY            PIC 99.
019000       02 LAST-UP-HH              PIC 99.
019100       02 LAST-UP-MM              PIC 99.
019200       02 FILLER                  PIC XXX.
019300       02 JOBNAME                 PIC X(8).
019400       02 JOB-CHAR-1 REDEFINES JOBNAME.
019500          03 JOBNAME-1            PIC X(1).
019600          03 FILLER               PIC X(7).
019700       02 JOB-CHAR-2 REDEFINES JOBNAME.
019800          03 JOBNAME-2            PIC X(2).
019900          03 FILLER               PIC X(6).
020000       02 JOB-CHAR-3 REDEFINES JOBNAME.
020100          03 JOBNAME-3            PIC X(3).
020200          03 FILLER               PIC X(5).
020300       02 JOB-CHAR-4 REDEFINES JOBNAME.
020400          03 JOBNAME-4            PIC X(4).
020500          03 FILLER               PIC X(4).
020600       02 JOB-CHAR-5 REDEFINES JOBNAME.
020700          03 JOBNAME-5            PIC X(5).
020800          03 FILLER               PIC X(3).
020900       02 JOB-CHAR-6 REDEFINES JOBNAME.
021000          03 JOBNAME-6            PIC X(6).
021100          03 FILLER               PIC X(2).
021200       02 JOB-CHAR-7 REDEFINES JOBNAME.
021300          03 JOBNAME-7            PIC X(7).
021400          03 FILLER               PIC X(1).
021500       02 JOB-CHAR-8 REDEFINES JOBNAME.
021600          03 JOBNAME-8            PIC X(8).
021700       02 USERID                  PIC X(7).
021800       02 FILLER                  PIC X(8).
021900       02 MODLNAME.
022000          03 FILLER               PIC X(4).
022100          03 JOBNUMB              PIC 9999.
022200       02 JUL-RUN-DATE            PIC X(5).
022300       02 RUN-DATE REDEFINES JUL-RUN-DATE.
022400          03 JUL-RUN-YY           PIC 99.
022500          03 JUL-RUN-DDD          PIC 999.
022600       02 RUN-STR-HH              PIC XX.
022700       02 RUN-STR-MM              PIC XX.
022800       02 RUN-END-HH              PIC XX.
022900       02 RUN-END-MM              PIC XX.
023000       02 FILLER                  PIC X(21).
023100       02 LAST-ACCESS             PIC X(06).
023200       02 FILLER                  PIC X(06).
023300*
023400   01  START-TIME-WS.
023500       02  START-HH-WS            PIC XX       VALUE SPACES.
023600       02  START-MM-WS            PIC XX       VALUE SPACES.
023700*
023800   01  START-TIME-JOB-WS.
023900       02  START-HH-JOB-WS        PIC XX       VALUE SPACES.
024000       02  START-MM-JOB-WS        PIC XX       VALUE SPACES.
024100*
024200   01  WS-DATE.
024300       02 MM                      PIC 99.
024400       02 DD                      PIC 99.
024500       02 YY                      PIC 99.
024600*
024700   01  WS-GREGORIAN-DATE.
024800       02 GREG-MM                 PIC 99.
024900       02 FILLER                  PIC X.
025000       02 GREG-DD                 PIC 99.
025100       02 FILLER                  PIC X.
025200       02 GREG-YY                 PIC 99.
025300*
025400   01  DATE-CONVERTED-WS.
025500       02 CONVERT-YY                  PIC 99.
025600       02 DATE-CONVERTED              PIC 999.
025700*
025800   01  WS-CUR-JULIAN-DATE.
025900       02 CUR-JUL-YY                  PIC 99.
026000       02 CUR-JUL-DDD                 PIC 999.
026100*
026200   01  WS-CUR-TIME.
026300       02 CUR-HH                      PIC 99.
026400       02 CUR-MM                      PIC 99.
026500       02 FILLER                      PIC X(4).
026600*
026700   01  CALENDAR-TABLE.
026800       02 FILLER                  PIC S9999  COMP VALUE +31.
026900       02 FEB                     PIC S9999  COMP VALUE +28.
027000       02 FILLER                  PIC S9999  COMP VALUE +31.
027100       02 FILLER                  PIC S9999  COMP VALUE +30.
027200       02 FILLER                  PIC S9999  COMP VALUE +31.
027300       02 FILLER                  PIC S9999  COMP VALUE +30.
027400       02 FILLER                  PIC S9999  COMP VALUE +31.
027500
027600       02 FILLER                  PIC S9999  COMP VALUE +31.
027700       02 FILLER                  PIC S9999  COMP VALUE +30.
027800       02 FILLER                  PIC S9999  COMP VALUE +31.
027900       02 FILLER                  PIC S9999  COMP VALUE +30.
028000       02 FILLER                  PIC S9999  COMP VALUE +31.
028100*
028200   01  CALENDAR-DAYS REDEFINES CALENDAR-TABLE.
028300       02 MTH-DAY                 PIC S9999  COMP OCCURS 12 TIMES.
028400*
028500   01  LAST-JOB-TABLE.
028600       02 JOB-TABENTRY OCCURS 500 TIMES.
028700          03 LAST-MOD-NAME        PIC X(8).
028800          03 LAST-JOB-NAME        PIC X(8).
028900*
029000   01  NO-INPUT.
029100       02 FILLER                  PIC X.
029200       02 FILLER                  PIC X(132)     VALUE
029300       '***** NO INPUT DATA FOUND *****'.
029400*
029500   01  SEL-HDR-1.
029600       02 FILLER                  PIC X.
029700       02 FILLER                  PIC X(5)      VALUE 'DATE='.
029800       02 R-DATE                  PIC X(8).
029900       02 FILLER                  PIC X(6)      VALUE ',TIME='.
030000       02 R-HH                    PIC X(2).
030100       02 FILLER                  PIC X         VALUE '.'.
030200       02 R-MM                    PIC X(2).
030300       02 FILLER                  PIC X(24)     VALUE SPACES.
030400       02 FILLER                  PIC X(47)     VALUE
030500       'LIBSPOOL  MODULES  SELECTED  FOR  PRINT  REPORT'.
030600       02 FILLER                  PIC X(27)     VALUE SPACES.
030700       02 FILLER                  PIC X(5)      VALUE 'PAGE '.
030800       02 PAGE-NO                 PIC ZZ9.
030900*
031000   01  SEL-HDR-2.
031100       02 FILLER                  PIC X.
031200       02 FILLER                  PIC X(06)     VALUE 'JOB - '.
031300       02 INVKNG-JOB-NM-R         PIC X(08)     VALUE SPACES.
031400       02 FILLER                  PIC X(17)     VALUE
031500       '  PROG - SYS005P0'.
031600       02 FILLER                  PIC X(84)     VALUE SPACES.
031700       02 FILLER                  PIC X(17)     VALUE
031800       'SEQUENCE - RANDOM'.
031900*
032000   01  SEL-HDR-3.
032100       02 FILLER                  PIC X.
032200       02 FILLER                  PIC X(100)    VALUE
032300       '           JOB              LIBRARIAN   RUN DATE  START
032400-      'END                               '.
032500       02 FILLER                  PIC X(32)     VALUE SPACES.
032600*
032700   01  SEL-HDR-4.
032800       02 FILLER                  PIC X.
032900       02 FILLER                  PIC X(113)    VALUE
033000       'JOB NAME  NUMBER  USER ID  MODULE NAME  MM/DD/YY  HH.MM  H
033100-      'H.MM      ********************  SELECTION  CARD  USED  '.
033200       02 FILLER                  PIC X(19)     VALUE
033300       '*******************'.
033400*
033500   01  SEL-DETAIL.
033600       02 FILLER                      PIC X.
033700       02 JOBNAME-R                   PIC X(8).
033800       02 FILLER                      PIC XXX    VALUE SPACES.
033900       02 JOBNUMB-R                   PIC ZZZ9.
034000       02 FILLER                      PIC XXX    VALUE SPACES.
034100       02 USERID-R                    PIC X(7).
034200       02 FILLER                      PIC XXX    VALUE SPACES.
034300       02 MODNAME-R                   PIC X(8).
034400       02 FILLER                      PIC X(4)   VALUE SPACES.
034500       02 GREG-DT.
034600           03 RUN-MM-R                PIC XX.
034700           03 FILLER-SLA1             PIC X      VALUE '/'.
034800           03 RUN-DD-R                PIC XX.
034900           03 FILLER-SLA2             PIC X      VALUE '/'.
035000           03 RUN-YY-R                PIC XX.
035100       02 FILLER                      PIC XX     VALUE SPACES.
035200       02 NOT-SCH-R.
035300           03 START-TIME.
035400               04 STRT-HH-R           PIC XX.
035500               04 FILLER-PER1         PIC X      VALUE '.'.
035600               04 STRT-MM-R           PIC XX.
035700           03 FILLER                  PIC X(2)   VALUE SPACES.
035800           03 END-TIME.
035900               04 END-HH-R            PIC XX.
036000               04 FILLER-PER2         PIC X      VALUE '.'.
036100               04 END-MM-R            PIC XX.
036200           03 FILLER                  PIC X(6)   VALUE SPACES.
036300       02 SELECT-CARD-R               PIC X(64).
036400*
036500   01  ERR-HDR-1.
036600       02 FILLER                  PIC X.
036700       02 FILLER                  PIC X(5)      VALUE 'DATE='.
036800       02 E-DATE                  PIC X(8).
036900       02 FILLER                  PIC X(6)      VALUE ',TIME='.
037000       02 E-HH                    PIC XX.
037100       02 FILLER                  PIC X         VALUE '.'.
037200       02 E-MM                    PIC XX.
037300       02 FILLER                  PIC X(19)     VALUE SPACES.
037400       02 FILLER                  PIC X(60)     VALUE
037500       'LIBSPOOL  MODULE  SELECTION  FOR  PRINT  EXCEPTION  REPORT
037600-      '  '.
037700       02 FILLER                  PIC X(21)     VALUE SPACES.
037800       02 FILLER                  PIC X(5)      VALUE 'PAGE '.
037900       02 E-PAGE-NO               PIC ZZ9.
038000*
038100   01  DASH-LINE                  PIC X(133)    VALUE ALL'-'.
038200*
038300   01  FMT-1.
038400       02 FILLER                  PIC X(08).
038500       02 FILLER                  PIC X(100) VALUE
038600     '   FORMAT OF THE SELECT CARD IS AS FOLLOWS:
038700-    '                                        '.
038800       02 FILLER                  PIC X(25)  VALUE SPACES.
038900*
039000   01  FMT-2.
039100       02 FILLER                  PIC X(09)  VALUE '  CARD   '.
039200       02 FILLER                  PIC X(100) VALUE
039300     '         1111111112222222222233333333333444444444
039400-    '                                        '.
039500       02 FILLER                  PIC X(24)  VALUE SPACES.
039600*
039700   01  FMT-3.
039800       02 FILLER                  PIC X(09)  VALUE ' COLUMNS '.
039900       02 FILLER                  PIC X(100) VALUE
040000     '1234567890123456789012345678901234567890123456789
040100-    '                                        '.
040200       02 FILLER                  PIC X(24)  VALUE SPACES.
040300*
040400   01  FMT-4.
040500       02 FILLER                  PIC X(09)  VALUE SPACES.
040600       02 FILLER                  PIC X(100) VALUE
040700     'JOBNAME=NNNNNNNN,SELECT=ALL
040800-    '                                        '.
040900       02 FILLER                  PIC X(24)  VALUE SPACES.
041000*
041100   01  FMT-5.
041200       02 FILLER                  PIC X(09)  VALUE SPACES.
041300       02 FILLER                  PIC X(100) VALUE
041400     'JOBNAME=NNNNNNNN,SELECT=LAST
041500-    '                                        '.
041600       02 FILLER                  PIC X(24)  VALUE SPACES.
041700*
041800   01  FMT-6.
041900       02 FILLER                  PIC X(09)  VALUE SPACES.
042000       02 FILLER                  PIC X(100) VALUE
042100     'JOBNAME=NNNNNNNN,SELECT=TODAY-NN,START TIME=HH.MM
042200-    '                                        '.
042300       02 FILLER                  PIC X(24)  VALUE SPACES.
042400*
042500   01  FMT-7.
042600       02 FILLER                  PIC X(08)  VALUE SPACES.
042700       02 FILLER                  PIC X(100) VALUE
042800     ' IF NO SELECT OPTION IS SPECIFIED THEN SELECT=LAST IS ASSUME
042900-    'D.                                      '.
043000       02 FILLER                  PIC X(25)  VALUE SPACES.
043100*
043200   01  FMT-8.
043300       02 FILLER                  PIC X(08)  VALUE SPACES.
043400       02 FILLER                  PIC X(100) VALUE
043500     ' WHERE :NNNNNNNN - 1-8 CHARACTER GENERIC JOB NAME
043600-    '                                       '.
043700       02 FILLER                  PIC X(25)  VALUE SPACES.
043800*
043900   01  FMT-9.
044000       02 FILLER                  PIC X(08)  VALUE SPACES.
044100       02 FILLER                  PIC X(100) VALUE
044200     '        ALL      - PRINT ALL JOBS WHICH MATCH THIS GENERIC J
044300-    'OB NAME                                 '.
044400       02 FILLER                  PIC X(25)  VALUE SPACES.
044500*
044600   01  FMT-10.
044700       02 FILLER                  PIC X(08)  VALUE SPACES.
044800       02 FILLER                  PIC X(100) VALUE
044900     '        LAST     - PRINT THE MOST RECENTLY RUN JOBS WHICH MA
045000-    'TCH THIS GENERIC JOB NAME               '.
045100       02 FILLER                  PIC X(25)  VALUE SPACES.
045200*
045300   01  FMT-11.
045400       02 FILLER                  PIC X(08)  VALUE SPACES.
045500       02 FILLER                  PIC X(100) VALUE
045600     '        TODAY-NN - PRINT ALL JOBS WHICH MATCH THIS GENERIC J
045700-    'OB NAME WHICH STARTED EXECUTION TODAY TH'.
045800       02 FILLER                  PIC X(25)  VALUE
045900     'RU NN DAYS PRIOR TO TODAY'.
046000*
046100   01  FMT-12.
046200       02 FILLER                  PIC X(08)  VALUE SPACES.
046300       02 FILLER                  PIC X(100) VALUE
046400     '           EG. TODAY-00 ===> SELECT JOBS BEGINNING EXECUTION
046500-    ' TODAY ONLY                             '.
046600       02 FILLER                  PIC X(25)  VALUE SPACES.
046700*
046800   01  FMT-13.
046900       02 FILLER                  PIC X(08)  VALUE SPACES.
047000       02 FILLER                  PIC X(100) VALUE
047100     '               TODAY-01 ===> SELECT JOBS BEGINNING EXECUTION
047200-    ' TODAY AND YESTERDAY                    '.
047300       02 FILLER                  PIC X(25)  VALUE SPACES.
047400*
047500   01  FMT-14.
047600       02 FILLER                  PIC X(08)  VALUE SPACES.
047700       02 FILLER                  PIC X(100) VALUE
047800     '                        ETC.
047900-    '                                        '.
048000       02 FILLER                  PIC X(25)  VALUE SPACES.
048100*
048200   01  FMT-15.
048300       02 FILLER                  PIC X(08)  VALUE SPACES.
048400       02 FILLER                  PIC X(100) VALUE
048500     '       OPTIONAL
048600-    '                                        '.
048700       02 FILLER                  PIC X(25)  VALUE SPACES.
048800*
048900   01  FMT-16.
049000       02 FILLER                  PIC X(08)  VALUE SPACES.
049100       02 FILLER                  PIC X(110) VALUE
049200     '   START TIME=HH.MM - START SELECTING JOBS ON TODAY-NN WHICH
049300-    ' BEGAN EXECUTION ON OR AFTER HH.MM (MILITARY TIME)'.
049400       02 FILLER                  PIC X(15)  VALUE SPACES.
049500*
049600   01  FMT-17.
049700       02 FILLER                  PIC X(08)  VALUE SPACES.
049800       02 FILLER                  PIC X(100) VALUE
049900     '    SELETION CARDS FOUND TO BE IN ERROR FOLLOW.
050000-    '                                        '.
050100       02 FILLER                  PIC X(25)  VALUE SPACES.
050200*
050300   01  ERROR-RPT-REC-WS.
050400       02 FILLER                  PIC X(09)  VALUE SPACES.
050500       02 ERRORRPT-REC-WS         PIC X(100) VALUE SPACES.
050600       02 FILLER                  PIC X(24)  VALUE SPACES.
050700*
050800 LINKAGE SECTION.
050900   01  PARM-JOB-NAME.
051000       02 PARM-LENGTH             PIC S9(4) COMP.
051100       02 PARMS.
051200           03 INVOKING-JOB-NAME       PIC X(8).
051300 PROCEDURE DIVISION USING PARM-JOB-NAME.
051400     IF PARM-LENGTH GREATER 0
051500        MOVE INVOKING-JOB-NAME TO INVKNG-JOB-NM-R.
051600*    READY TRACE.
051700     MOVE CURRENT-DATE TO R-DATE E-DATE.
051800*
051900     MOVE TIME-OF-DAY TO WS-CUR-TIME.
052000     MOVE CUR-HH TO R-HH E-HH.
052100     MOVE CUR-MM TO R-MM E-MM.
052200*
052300     ACCEPT WS-CUR-JULIAN-DATE FROM DAY.
052400*    DISPLAY 'CURRENT JULIAN DATE = ' WS-CUR-JULIAN-DATE.
052500     ACCEPT WS-CUR-TIME FROM TIME.
052600*    DISPLAY 'CURRENT TIME (HHMMSSTT) = ' WS-CUR-TIME.
052700*
052800     OPEN INPUT SELECTCD.
052900     OPEN OUTPUT SELCTRPT.
053000     CALL 'SYS005PB' USING OPENID.
053100*
053200     CALL 'MASTOPN' USING OPN-AREA, OPN-RETURN-CODE.
053300     IF OPN-RETURN-CODE NOT = '0'
053400          DISPLAY 'LIBRARIAN INPUT FILE COULD NOT BE OPENED'
053500          DISPLAY 'OPEN RETURN CODE =' OPN-RETURN-CODE
053600          GO TO END-RUN.
053700*
053800     PERFORM SEL-HDR-PAR THRU SEL-HDR-EXIT.
053900     GO TO READ-SELECTCD.
054000*
054100 READ-SELECT-CARDS.
054200     CALL 'MASTCLS'.
054300*
054400     CALL 'MASTOPN' USING OPN-AREA, OPN-RETURN-CODE.
054500     IF OPN-RETURN-CODE NOT = '0'
054600          DISPLAY 'LIBRARIAN INPUT FILE COULD NOT BE OPENED'
054700          DISPLAY 'OPEN RETURN CODE =' OPN-RETURN-CODE
054800          GO TO END-RUN.
054900*
055000 READ-SELECTCD.
055100     IF SEL-EOF-SW = 1
055200             GO TO END-RUN.
055300*
055400     READ SELECTCD AT END
055500         MOVE 1 TO SEL-EOF-SW.
055600*    DISPLAY 'SELECT CARD = ' SELECTCD-REC.
055700*
055800     IF SEL-EOF-SW = 1
055900         IF SELECT-CARD-COUNT = 0
056000             GO TO SELECT-CARD-ERROR
056100         ELSE
056200             GO TO END-RUN.
056300*
056400     ADD 1 TO SELECT-CARD-COUNT.
056500     MOVE 0 TO JOB-CTR.
056600*
056700     IF JOB-NAME-LIT NOT = 'JOBNAME='
056800         GO TO SELECT-CARD-ERROR.
056900     IF SELECT-LIT = SPACES OR ',SELECT='
057000         NEXT SENTENCE
057100     ELSE
057200         GO TO SELECT-CARD-ERROR.
057300*
057400     IF JOB-CHAR (8) = SPACE
057500         IF JOB-CHAR (7) = SPACE
057600             IF JOB-CHAR (6) = SPACE
057700                 IF JOB-CHAR (5) = SPACE
057800                     IF JOB-CHAR (4) = SPACE
057900                         IF JOB-CHAR (3) = SPACE
058000                             IF JOB-CHAR (2) = SPACE
058100                                 IF JOB-CHAR (1) = SPACE
058200                                     GO TO SELECT-CARD-ERROR
058300                                 ELSE
058400                                     MOVE JOB-TO-SELECT TO JOB-1
058500                                     MOVE 1 TO JOB-COMPARE-SW
058600                             ELSE
058700                                 MOVE JOB-TO-SELECT TO JOB-2
058800                                 MOVE 2 TO JOB-COMPARE-SW
058900                         ELSE
059000                             MOVE JOB-TO-SELECT TO JOB-3
059100                             MOVE 3 TO JOB-COMPARE-SW
059200                     ELSE
059300                         MOVE JOB-TO-SELECT TO JOB-4
059400                         MOVE 4 TO JOB-COMPARE-SW
059500                 ELSE
059600                     MOVE JOB-TO-SELECT TO JOB-5
059700                     MOVE 5 TO JOB-COMPARE-SW
059800             ELSE
059900                 MOVE JOB-TO-SELECT TO JOB-6
060000                 MOVE 6 TO JOB-COMPARE-SW
060100         ELSE
060200             MOVE JOB-TO-SELECT TO JOB-7
060300             MOVE 7 TO JOB-COMPARE-SW
060400     ELSE
060500         MOVE JOB-TO-SELECT TO JOB-8
060600         MOVE 8 TO JOB-COMPARE-SW.
060700*
060800     IF SELECT-ID = 'ALL'
060900         GO TO PRINT-ALL.
061000     IF SELECT-ID = 'LAST'
061100         GO TO PRINT-ONLY-LAST.
061200     IF SELECT-ID = 'TODAY-'
061300         GO TO PRINT-TODAY-AND-MORE.
061400     IF SELECT-ID = SPACES
061500         GO TO PRINT-ONLY-LAST.
061600*
061700 SELECT-CARD-ERROR.
061800*    DISPLAY 'SELECT ERROR'.
061900     IF ERRORRPT-OPEN-SW = 0
062000         MOVE 1 TO ERRORRPT-OPEN-SW
062100         OPEN OUTPUT ERRORRPT
062200     ELSE
062300         GO TO REPORT-BAD-SELECT-CARD.
062400*
062500     IF ERRORRPT-LINE-CTR GREATER 50
062600         PERFORM ERR-HDR-PAR THRU ERR-HDR-EXIT.
062700*
062800     WRITE ERRORRPT-REC FROM DASH-LINE AFTER POSITIONING 2 LINES.
062900     WRITE ERRORRPT-REC FROM FMT-1  AFTER POSITIONING 1 LINES.
063000     WRITE ERRORRPT-REC FROM FMT-2  AFTER POSITIONING 2 LINES.
063100     WRITE ERRORRPT-REC FROM FMT-3  AFTER POSITIONING 1 LINES.
063200     WRITE ERRORRPT-REC FROM FMT-4  AFTER POSITIONING 2 LINES.
063300     WRITE ERRORRPT-REC FROM FMT-5  AFTER POSITIONING 1 LINES.
063400     WRITE ERRORRPT-REC FROM FMT-6  AFTER POSITIONING 1 LINES.
063500     WRITE ERRORRPT-REC FROM FMT-8  AFTER POSITIONING 2 LINES.
063600     WRITE ERRORRPT-REC FROM FMT-9  AFTER POSITIONING 1 LINES.
063700     WRITE ERRORRPT-REC FROM FMT-10 AFTER POSITIONING 1 LINES.
063800     WRITE ERRORRPT-REC FROM FMT-11 AFTER POSITIONING 1 LINES.
063900     WRITE ERRORRPT-REC FROM FMT-12 AFTER POSITIONING 2 LINES.
064000     WRITE ERRORRPT-REC FROM FMT-13 AFTER POSITIONING 1 LINES.
064100     WRITE ERRORRPT-REC FROM FMT-14 AFTER POSITIONING 1 LINES.
064200     WRITE ERRORRPT-REC FROM FMT-15 AFTER POSITIONING 2 LINES.
064300     WRITE ERRORRPT-REC FROM FMT-16 AFTER POSITIONING 2 LINES.
064400     WRITE ERRORRPT-REC FROM FMT-7  AFTER POSITIONING 2 LINES.
064500     WRITE ERRORRPT-REC FROM DASH-LINE AFTER POSITIONING 1 LINES.
064600     WRITE ERRORRPT-REC FROM FMT-17 AFTER POSITIONING 3 LINES.
064700 REPORT-BAD-SELECT-CARD.
064800     IF ERRORRPT-LINE-CTR GREATER 50
064900         PERFORM ERR-HDR-PAR THRU ERR-HDR-EXIT.
065000*
065100     WRITE ERRORRPT-REC FROM FMT-2  AFTER POSITIONING 3 LINES.
065200     WRITE ERRORRPT-REC FROM FMT-3  AFTER POSITIONING 1 LINES.
065300     MOVE SELECTCD-REC TO ERRORRPT-REC-WS.
065400     WRITE ERRORRPT-REC FROM ERROR-RPT-REC-WS
065500                                    AFTER POSITIONING 2 LINES.
065600*
065700     GO TO READ-SELECTCD.
065800*
065900 PRINT-ALL.
066000*
066100     MOVE SPACES TO MOD-NAME.
066200     PERFORM GET-NEXT-MODULE THRU GET-NEXT-MODULE-XIT.
066300     IF MOD-RETURN-CODE = '1'
066400       IF JOB-CTR = 0
066500           PERFORM RPT-SELECT-DETAIL THRU RPT-SELECT-DETAIL-XIT
066600           GO TO READ-SELECT-CARDS
066700        ELSE
066800           GO TO READ-SELECT-CARDS.
066900*
067000     GO TO ALL-JOB-1
067100           ALL-JOB-2
067200           ALL-JOB-3
067300           ALL-JOB-4
067400           ALL-JOB-5
067500           ALL-JOB-6
067600           ALL-JOB-7
067700           ALL-JOB-8
067800           DEPENDING ON JOB-COMPARE-SW.
067900 ALL-JOB-1.
068000     IF JOB-1 NOT = JOBNAME-1
068100         GO TO PRINT-ALL.
068200     GO TO READ-JCL-1.
068300 ALL-JOB-2.
068400     IF JOB-2 NOT = JOBNAME-2
068500         GO TO PRINT-ALL.
068600     GO TO READ-JCL-1.
068700 ALL-JOB-3.
068800     IF JOB-3 NOT = JOBNAME-3
068900         GO TO PRINT-ALL.
069000     GO TO READ-JCL-1.
069100 ALL-JOB-4.
069200     IF JOB-4 NOT = JOBNAME-4
069300         GO TO PRINT-ALL.
069400     GO TO READ-JCL-1.
069500 ALL-JOB-5.
069600     IF JOB-5 NOT = JOBNAME-5
069700         GO TO PRINT-ALL.
069800     GO TO READ-JCL-1.
069900 ALL-JOB-6.
070000     IF JOB-6 NOT = JOBNAME-6
070100         GO TO PRINT-ALL.
070200     GO TO READ-JCL-1.
070300 ALL-JOB-7.
070400     IF JOB-7 NOT = JOBNAME-7
070500         GO TO PRINT-ALL.
070600     GO TO READ-JCL-1.
070700 ALL-JOB-8.
070800     IF JOB-8 NOT = JOBNAME-8
070900         GO TO PRINT-ALL.
071000     GO TO READ-JCL-1.
071100*
071200*
071300 READ-JCL-1.
071400     ADD 1 TO JOB-CTR.
071500     PERFORM RPT-SELECT-DETAIL THRU RPT-SELECT-DETAIL-XIT.
071600     PERFORM READ-THIS-JCL THRU READ-JCL-XIT.
071700     GO TO PRINT-ALL.
071800*
071900*
072000 PRINT-ONLY-LAST.
072100     MOVE SPACES TO MOD-NAME.
072200     PERFORM GET-NEXT-MODULE THRU GET-NEXT-MODULE-XIT.
072300*
072400     IF MOD-RETURN-CODE = '1'
072500          GO TO PRINTOFF-TABLE-ENTRIES.
072600*
072700     GO TO LAST-JOB-1
072800           LAST-JOB-2
072900           LAST-JOB-3
073000           LAST-JOB-4
073100           LAST-JOB-5
073200           LAST-JOB-6
073300           LAST-JOB-7
073400           LAST-JOB-8
073500           DEPENDING ON JOB-COMPARE-SW.
073600 LAST-JOB-1.
073700     IF JOB-1 NOT = JOBNAME-1
073800         GO TO PRINT-ONLY-LAST.
073900     GO TO TABLE-THIS-JOB.
074000 LAST-JOB-2.
074100     IF JOB-2 NOT = JOBNAME-2
074200         GO TO PRINT-ONLY-LAST.
074300     GO TO TABLE-THIS-JOB.
074400 LAST-JOB-3.
074500     IF JOB-3 NOT = JOBNAME-3
074600         GO TO PRINT-ONLY-LAST.
074700     GO TO TABLE-THIS-JOB.
074800 LAST-JOB-4.
074900     IF JOB-4 NOT = JOBNAME-4
075000         GO TO PRINT-ONLY-LAST.
075100     GO TO TABLE-THIS-JOB.
075200 LAST-JOB-5.
075300     IF JOB-5 NOT = JOBNAME-5
075400         GO TO PRINT-ONLY-LAST.
075500     GO TO TABLE-THIS-JOB.
075600 LAST-JOB-6.
075700     IF JOB-6 NOT = JOBNAME-6
075800         GO TO PRINT-ONLY-LAST.
075900     GO TO TABLE-THIS-JOB.
076000 LAST-JOB-7.
076100     IF JOB-7 NOT = JOBNAME-7
076200         GO TO PRINT-ONLY-LAST.
076300     GO TO TABLE-THIS-JOB.
076400 LAST-JOB-8.
076500     IF JOB-8 NOT = JOBNAME-8
076600         GO TO PRINT-ONLY-LAST.
076700     GO TO TABLE-THIS-JOB.
076800*
076900 TABLE-THIS-JOB.
077000     MOVE 0 TO TABENTRY.
077100 INCR-TABENTRY.
077200     ADD 1 TO TABENTRY.
077300     IF TABENTRY GREATER JOB-CTR
077400         ADD 1 TO JOB-CTR
077500         MOVE MODLNAME TO LAST-MOD-NAME (JOB-CTR)
077600         MOVE JOBNAME  TO LAST-JOB-NAME (JOB-CTR)
077700         GO TO PRINT-ONLY-LAST.
077800     IF JOBNAME = LAST-JOB-NAME (TABENTRY)
077900         MOVE MODLNAME TO LAST-MOD-NAME (TABENTRY)
078000         MOVE JOBNAME  TO LAST-JOB-NAME (TABENTRY)
078100         GO TO PRINT-ONLY-LAST.
078200     GO TO INCR-TABENTRY.
078300*
078400 PRINTOFF-TABLE-ENTRIES.
078500     IF JOB-CTR = 0
078600         PERFORM RPT-SELECT-DETAIL THRU RPT-SELECT-DETAIL-XIT
078700         GO TO READ-SELECT-CARDS.
078800*
078900     CALL 'MASTCLS'.
079000*
079100     CALL 'MASTOPN' USING OPN-AREA, OPN-RETURN-CODE.
079200     IF OPN-RETURN-CODE NOT = '0'
079300          DISPLAY 'LIBRARIAN INPUT FILE COULD NOT BE OPENED'
079400          DISPLAY 'OPEN RETURN CODE =' OPN-RETURN-CODE
079500          GO TO END-RUN.
079600*
079700     MOVE 0 TO TABENTRY.
079800 PRINT-LAST-JOBS.
079900     ADD 1 TO TABENTRY.
080000     MOVE LAST-MOD-NAME (TABENTRY) TO MOD-NAME.
080100     PERFORM GET-NEXT-MODULE THRU GET-NEXT-MODULE-XIT.
080200     PERFORM RPT-SELECT-DETAIL THRU RPT-SELECT-DETAIL-XIT.
080300*
080400     PERFORM READ-THIS-JCL THRU READ-JCL-XIT.
080500     IF TABENTRY LESS JOB-CTR
080600         GO TO PRINT-LAST-JOBS.
080700     GO TO READ-SELECT-CARDS.
080800*
080900*
081000 PRINT-TODAY-AND-MORE.
081100     IF REL-START-DATE-XX = SPACES
081200         MOVE CUR-JUL-DDD TO START-JUL-DATE
081300         GO TO CK-START-TIME.
081400     IF REL-START-DATE-XX LESS '00'
081500         GO TO SELECT-CARD-ERROR
081600     ELSE
081700         IF REL-START-DATE-XX GREATER '99'
081800             GO TO SELECT-CARD-ERROR.
081900     COMPUTE START-JUL-DATE = CUR-JUL-DDD - RELTVE-START-DATE.
082000 CK-START-TIME.
082100     IF STRT-TME-LIT = SPACES
082200         MOVE 'XX' TO START-HH-WS  START-MM-WS
082300         GO TO LOOK-FOR-JOBS.
082400     IF STRT-TME-LIT NOT = ',START TIME='
082500         GO TO SELECT-CARD-ERROR.
082600     IF START-HH LESS '00'
082700         GO TO SELECT-CARD-ERROR
082800     ELSE
082900         IF START-MM GREATER '99'
083000             GO TO SELECT-CARD-ERROR.
083100     MOVE START-HH TO START-HH-WS.
083200     MOVE START-MM TO START-MM-WS.
083300*CK-END-TIME.
083400*    IF END-TME-LIT = SPACES
083500*        MOVE 24 TO END-HH-WS
083600*        MOVE 0 TO END-HH-WS
083700*        GO TO LOOK-FOR-JOBS.
083800*    IF END-TME-LIT NOT = ',END TIME='
083900*        GO TO SELECT-CARD-ERROR.
084000*    IF END-HH LESS '00'
084100*        GO TO SELECT-CARD-ERROR
084200*    ELSE
084300*        IF END-MM GREATER '99'
084400*            GO TO SELECT-CARD-ERROR.
084500*    MOVE END-HH TO END-HH-WS.
084600*    MOVE END-MM TO END-MM-WS.
084700 LOOK-FOR-JOBS.
084800     MOVE SPACES TO MOD-NAME.
084900     PERFORM GET-NEXT-MODULE THRU GET-NEXT-MODULE-XIT.
085000*
085100     IF MOD-RETURN-CODE = '1'
085200       IF JOB-CTR = 0
085300           PERFORM RPT-SELECT-DETAIL THRU RPT-SELECT-DETAIL-XIT
085400           GO TO READ-SELECT-CARDS
085500        ELSE
085600           GO TO READ-SELECT-CARDS.
085700*
085800     GO TO TODAY-AND-MORE-1
085900           TODAY-AND-MORE-2
086000           TODAY-AND-MORE-3
086100           TODAY-AND-MORE-4
086200           TODAY-AND-MORE-5
086300           TODAY-AND-MORE-6
086400           TODAY-AND-MORE-7
086500           TODAY-AND-MORE-8
086600           DEPENDING ON JOB-COMPARE-SW.
086700 TODAY-AND-MORE-1.
086800     IF JOB-1 NOT = JOBNAME-1
086900         GO TO LOOK-FOR-JOBS.
087000     GO TO CK-CUTOFF-TIME.
087100 TODAY-AND-MORE-2.
087200     IF JOB-2 NOT = JOBNAME-2
087300         GO TO LOOK-FOR-JOBS.
087400     GO TO CK-CUTOFF-TIME.
087500 TODAY-AND-MORE-3.
087600     IF JOB-3 NOT = JOBNAME-3
087700         GO TO LOOK-FOR-JOBS.
087800     GO TO CK-CUTOFF-TIME.
087900 TODAY-AND-MORE-4.
088000     IF JOB-4 NOT = JOBNAME-4
088100         GO TO LOOK-FOR-JOBS.
088200     GO TO CK-CUTOFF-TIME.
088300 TODAY-AND-MORE-5.
088400     IF JOB-5 NOT = JOBNAME-5
088500         GO TO LOOK-FOR-JOBS.
088600     GO TO CK-CUTOFF-TIME.
088700 TODAY-AND-MORE-6.
088800     IF JOB-6 NOT = JOBNAME-6
088900         GO TO LOOK-FOR-JOBS.
089000     GO TO CK-CUTOFF-TIME.
089100 TODAY-AND-MORE-7.
089200     IF JOB-7 NOT = JOBNAME-7
089300         GO TO LOOK-FOR-JOBS.
089400     GO TO CK-CUTOFF-TIME.
089500 TODAY-AND-MORE-8.
089600     IF JOB-8 NOT = JOBNAME-8
089700         GO TO LOOK-FOR-JOBS.
089800     GO TO CK-CUTOFF-TIME.
089900*
090000 CK-CUTOFF-TIME.
090100     IF JUL-RUN-DATE = 'YYDDD'
090200         GO TO USE-UPDATE-DATE-TIME.
090300*
090400     IF JUL-RUN-DDD LESS START-JUL-DATE
090500         GO TO LOOK-FOR-JOBS.
090600     IF START-HH-WS = 'XX'
090700         GO TO REPORT-MODULE.
090800     IF JUL-RUN-DDD = START-JUL-DATE
090900         MOVE RUN-STR-HH TO START-HH-JOB-WS
091000         MOVE RUN-STR-MM TO START-MM-JOB-WS
091100         IF START-TIME-JOB-WS LESS START-TIME-WS
091200             GO TO LOOK-FOR-JOBS.
091300     GO TO REPORT-MODULE.
091400*
091500 USE-UPDATE-DATE-TIME.
091600     MOVE LAST-UP-DATE TO WS-DATE.
091700     DIVIDE YY BY 4 GIVING LEAP-TEST REMAINDER LEAP-RMNDR.
091800     IF LEAP-RMNDR = 0
091900         MOVE 29 TO FEB.
092000 CONVERT-DATE.
092100     MOVE DD TO DATE-CONVERTED
092200     MOVE 0 TO MO-CTR.
092300 INCR-MO-CTR.
092400     ADD 1 TO MO-CTR.
092500     IF MO-CTR LESS MM
092600         ADD MTH-DAY (MO-CTR) TO DATE-CONVERTED
092700         GO TO INCR-MO-CTR.
092800*
092900     IF DATE-CONVERTED LESS START-JUL-DATE
093000         GO TO LOOK-FOR-JOBS.
093100     IF START-HH-WS = 'XX'
093200         GO TO REPORT-MODULE.
093300     IF DATE-CONVERTED = START-JUL-DATE
093400         MOVE LAST-UP-HH TO START-HH-JOB-WS
093500         MOVE LAST-UP-MM TO START-MM-JOB-WS
093600         IF START-TIME-JOB-WS LESS START-TIME-WS
093700             GO TO LOOK-FOR-JOBS.
093800*
093900 REPORT-MODULE.
094000     ADD 1 TO JOB-CTR.
094100     PERFORM RPT-SELECT-DETAIL THRU RPT-SELECT-DETAIL-XIT.
094200     PERFORM READ-THIS-JCL THRU READ-JCL-XIT.
094300     GO TO LOOK-FOR-JOBS.
094400*
094500*
094600 GET-NEXT-MODULE.
094700*
094800     CALL 'MASTMOD' USING MOD-NAME, MOD-RETURN-CODE, MOD-AREA.
094900*
095000     IF MOD-RETURN-CODE = '9' OR '2'
095100          DISPLAY 'LIBRARIAN INPUT FILE MODULE LOCATE PROBLEM'
095200          DISPLAY 'READ RETURN CODE =' MOD-RETURN-CODE
095300          MOVE 0 TO MOD-COUNT
095400          GO TO END-RUN.
095500 GET-NEXT-MODULE-XIT. EXIT.
095600*
095700*
095800 READ-THIS-JCL.
095900*
096000     MOVE JOBNAME TO HDR-DTA-JBNME.
096100     MOVE JOBNUMB TO HDR-DTA-JOB-NMBR.
096200     MOVE GREG-DT TO HDR-DTA-GREG-DTE.
096300     IF JUL-RUN-DATE = 'YYDDD'
096400         MOVE 'JOB NOT RUN' TO HDR-DTA-RUN-TIME
096500     ELSE
096600         MOVE '-' TO HDR-DTA-DASH
096700         MOVE START-TIME TO HDR-DTA-STRT-TIME
096800         MOVE END-TIME TO HDR-DTA-END-TIME.
096900     CALL 'SYS005PA' USING HEADER-DATA.
097000*
097100 READ-NXT-JCL.
097200     CALL 'MASTRCD' USING CARD-AREA-1, MOD-RETURN-CODE.
097300*
097400     IF MOD-RETURN-CODE = '9' OR '2'
097500          DISPLAY 'LIBRARIAN INPUT FILE MODULE LOCATE PROBLEM'
097600          DISPLAY 'READ RETURN CODE =' MOD-RETURN-CODE
097700          MOVE 0 TO MOD-COUNT
097800          GO TO END-RUN.
097900     IF MOD-RETURN-CODE = '1'
098000          GO TO READ-JCL-XIT.
098100*
098200     MOVE CARD-AREA-1 TO PRINT-AREA.
098300*
098400 READ-JCL-2.
098500     CALL 'MASTRCD' USING CARD-AREA-2-DATA, MOD-RETURN-CODE.
098600*
098700     IF MOD-RETURN-CODE = '9' OR '2'
098800          DISPLAY 'LIBRARIAN INPUT FILE MODULE LOCATE PROBLEM'
098900          DISPLAY 'READ RETURN CODE =' MOD-RETURN-CODE
099000          MOVE 0 TO MOD-COUNT
099100          GO TO END-RUN.
099200     IF MOD-RETURN-CODE = '1'
099300          CALL 'SYS005PB' USING RITEID PRINT-AREA
099400          GO TO READ-JCL-XIT.
099500*
099600     IF CONTINUE-BYTE GREATER '9'
099700          MOVE CARD-AREA-2 TO PRINT-AREA-2
099800          CALL 'SYS005PB' USING RITEID PRINT-AREA
099900          GO TO READ-NXT-JCL.
100000*
100100     CALL 'SYS005PB' USING RITEID PRINT-AREA
100200     MOVE CARD-AREA-2-DATA TO PRINT-AREA.
100300     GO TO READ-JCL-2.
100400 READ-JCL-XIT. EXIT.
100500*
100600*
100700 SEL-HDR-PAR.
100800     ADD 1 TO PAGE-COUNT.
100900     MOVE PAGE-COUNT TO PAGE-NO.
101000     WRITE SELCTRPT-REC FROM SEL-HDR-1 AFTER POSITIONING 0 LINES.
101100     WRITE SELCTRPT-REC FROM SEL-HDR-2 AFTER POSITIONING 1 LINES.
101200     WRITE SELCTRPT-REC FROM SEL-HDR-3 AFTER POSITIONING 2 LINES.
101300     WRITE SELCTRPT-REC FROM SEL-HDR-4 AFTER POSITIONING 1 LINES.
101400     MOVE 5 TO LINE-COUNT.
101500 SEL-HDR-EXIT. EXIT.
101600*
101700*
101800 ERR-HDR-PAR.
101900     ADD 1 TO ERRORRPT-PAGE-COUNT.
102000     MOVE ERRORRPT-PAGE-COUNT TO E-PAGE-NO.
102100     WRITE ERRORRPT-REC FROM ERR-HDR-1 AFTER POSITIONING 0 LINES.
102200     WRITE ERRORRPT-REC FROM SEL-HDR-2 AFTER POSITIONING 1 LINES.
102300     MOVE 2 TO ERRORRPT-LINE-CTR.
102400 ERR-HDR-EXIT. EXIT.
102500*
102600*
102700 RPT-SELECT-DETAIL.
102800     IF JOB-CTR = 0
102900      MOVE 1 TO NO-MATCH-SW
103000      MOVE '      NO JOBS FOUND TO MATCH THIS SELECTION CARD ==>'
103100      TO SEL-DETAIL
103200      GO TO MVE-SEL-CARD.
103300*    DISPLAY MOD-AREA.
103400     MOVE SPACES TO SEL-DETAIL.
103500     MOVE JOBNAME  TO JOBNAME-R.
103600     MOVE JOBNUMB  TO JOBNUMB-R.
103700     MOVE USERID   TO USERID-R.
103800     MOVE MODLNAME TO MODNAME-R.
103900     MOVE LST-UP-MM TO RUN-MM-R.
104000     MOVE LST-UP-DD TO RUN-DD-R.
104100     MOVE LST-UP-YY TO RUN-YY-R.
104200     MOVE '/' TO FILLER-SLA1 FILLER-SLA2.
104300     IF RUN-STR-HH = 'HH'
104400         GO TO SHOW-NOT-RUN.
104500     MOVE RUN-STR-HH TO STRT-HH-R.
104600     MOVE RUN-STR-MM TO STRT-MM-R.
104700     MOVE RUN-END-HH TO END-HH-R.
104800     MOVE RUN-END-MM TO END-MM-R.
104900     MOVE '.' TO FILLER-PER1 FILLER-PER2.
105000     GO TO MVE-SEL-CARD.
105100 SHOW-NOT-RUN.
105200     MOVE 'JOB NOT SCHEDULED' TO NOT-SCH-R.
105300 MVE-SEL-CARD.
105400     MOVE SELECTCD-REC TO SELECT-CARD-R.
105500     WRITE SELCTRPT-REC FROM SEL-DETAIL AFTER POSITIONING 2 LINES
105600     ADD 2 TO LINE-COUNT.
105700 RPT-SELECT-DETAIL-XIT. EXIT.
105800*
105900*
106000 END-RUN.
106100     CALL 'MASTCLS'.
106200     CLOSE SELECTCD.
106300     CALL 'SYS005PB' USING CLSEID.
106400     CLOSE SELCTRPT.
106500     IF ERRORRPT-OPEN-SW = 1
106600         CLOSE ERRORRPT
106700         MOVE +4 TO RETURN-CODE
106800     ELSE
106900         IF NO-MATCH-SW = 1
107000             MOVE +4 TO RETURN-CODE
107100         ELSE
107200             MOVE +0 TO RETURN-CODE.
107300*
107400     STOP RUN.
