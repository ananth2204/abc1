000010 IDENTIFICATION DIVISION.
000020   PROGRAM-ID.    OPS4320G.
000030   INSTALLATION.  HOOKER CHEMICAL AND PLASTICS COMPANY.
000040 DATE-COMPILED.   DATE.
000050 REMARKS. WRITTEN BY :
000060                             ROBERT P DZIALAK
000070                             HOOKER CHEMICAL CENTER
000080                             360 RAINBOW BLVD. SOUTH
000090                             NIAGARA FALLS , NEW YORK 14303
000100                             (716) 286-3251
000110 ENVIRONMENT DIVISION.
000120  CONFIGURATION SECTION.
000130    SOURCE-COMPUTER. IBM-370.
000140    OBJECT-COMPUTER. IBM-370.
000150  INPUT-OUTPUT SECTION.
000160    FILE-CONTROL.
000170       SELECT DLMCARD               ASSIGN TO UT-S-DLMCARD.
000180       SELECT COPYCARD              ASSIGN TO UT-S-COPYCARD.
000190       SELECT MCDCODE               ASSIGN TO UT-S-MCDCODE.
000200 DATA DIVISION.
000210  FILE SECTION.
000220   FD  DLMCARD
000230       BLOCK CONTAINS 0 RECORDS
000240       LABEL RECORDS ARE STANDARD.
000250   01  DLMCARD-REC                PIC X(80).
000260   FD  COPYCARD
000270       BLOCK CONTAINS 0 RECORDS
000280       LABEL RECORDS ARE STANDARD.
000290   01  COPYCARD-REC               PIC X(80).
000300   FD  MCDCODE
000310       BLOCK CONTAINS 0 RECORDS
000320       LABEL RECORDS ARE STANDARD.
000330   01  MCDCODE-REC.
000340       02 MCD-BASE                PIC 9999.
000350       02 MCD-BASE-X REDEFINES MCD-BASE PIC XXXX.
000360       02 FILLER                  PIC X(76).
000370  WORKING-STORAGE SECTION.
000380   77  MO-CTR                     PIC S9(9) COMP VALUE +0.
000390   77  MOD-COUNT                  PIC S9(9) COMP VALUE +0.
000400   77  DATE-CONVERTED             PIC S9(9) COMP VALUE +0.
000410   77  IDLE-DAYS                  PIC S9(9) COMP VALUE +0.
000420   77  LEAPTEST                   PIC S9(9) COMP VALUE +0.
000430   77  LEAP-RMNDR                 PIC S9(9) COMP VALUE +0.
000440   77  HOURS-ONLINE               PIC S9(9) COMP VALUE +0.
000450   77  JOB-DAYS                   PIC S9(9) COMP VALUE +0.
000460   77  JOB-HOUR                   PIC S9(9) COMP VALUE +0.
000470   77  CURRENT-DAYS               PIC S9(9) COMP VALUE +0.
000480   77  CURRENT-HOUR               PIC S9(9) COMP VALUE +0.
000490   77  RUN-HOUR                   PIC S9(9) COMP VALUE +0.
000500   77  BASE-MCD-CODE              PIC S9(9) COMP VALUE +0.
000510   01  OPN-RETURN-CODE            PIC  X       VALUE SPACE.
000520   01  OPN-AREA                   PIC  X(16)   VALUE SPACE.
000530   01  MOD-NAME                   PIC  X(8)    VALUE SPACE.
000540   01  MOD-RETURN-CODE            PIC  X       VALUE SPACE.
000550   01  MOD-AREA.
000560       02 MOD-PASSWORD            PIC X(04).
000570       02 DATE-ADDED              PIC X(06).
000580       02 LAST-UP-DATE.
000590          03 LST-UP-MM            PIC 99.
000600          03 LST-UP-DD            PIC 99.
000610          03 LST-UP-YY            PIC 99.
000620       02 LAST-UP-HH              PIC 99.
000630       02 LAST-UP-MM              PIC 99.
000640       02 FILLER                  PIC XXX.
000650       02 JOBNAME                 PIC X(8).
000660       02 USERID                  PIC X(7).
000670       02 FILLER                  PIC X(8).
000680       02 MODLNAME                PIC X(8).
000690       02 JUL-RUN-DATE            PIC X(5).
000700       02 RUN-DATE REDEFINES JUL-RUN-DATE.
000710          03 JUL-RUN-YY           PIC 99.
000720          03 JUL-RUN-DDD          PIC 999.
000730       02 RUN-STR-HH              PIC 99.
000740       02 RUN-STR-MM              PIC 99.
000750       02 RUN-END-HH              PIC 99.
000760       02 RUN-END-MM              PIC 99.
000770       02 FILLER                  PIC X(21).
000780       02 LAST-ACCESS             PIC X(06).
000790       02 FILLER                  PIC X(06).
000800   01  WS-DATE.
000810       02 MM                      PIC 99.
000820       02 DD                      PIC 99.
000830       02 YY                      PIC 99.
000840   01  WS-GREGORIAN-DATE.
000850       02 GREG-MM                 PIC 99.
000860       02 FILLER                  PIC X.
000870       02 GREG-DD                 PIC 99.
000880       02 FILLER                  PIC X.
000890       02 GREG-YY                 PIC 99.
000900   01  CUR-GREG-MM-DD.
000910       02 CUR-GREG-MM                 PIC 99.
000920       02 CUR-GREG-DD                 PIC 99.
000930   01  MCD-CODE-FACTOR REDEFINES CUR-GREG-MM-DD PIC 9999.
000940   01  WS-CUR-JULIAN-DATE.
000950       02 CUR-JUL-YY                  PIC 99.
000960       02 CUR-JUL-DDD                 PIC 999.
000970   01  WS-CUR-TIME.
000980       02 CUR-HH                      PIC 99.
000990       02 CUR-MM                      PIC 99.
001000       02 FILLER                      PIC X(4).
001010   01  CALENDAR-TABLE.
001020       02 FILLER                  PIC S9999  COMP VALUE +31.
001030       02 FEB                     PIC S9999  COMP VALUE +28.
001040       02 FILLER                  PIC S9999  COMP VALUE +31.
001050       02 FILLER                  PIC S9999  COMP VALUE +30.
001060       02 FILLER                  PIC S9999  COMP VALUE +31.
001070       02 FILLER                  PIC S9999  COMP VALUE +30.
001080       02 FILLER                  PIC S9999  COMP VALUE +31.
001090
001100       02 FILLER                  PIC S9999  COMP VALUE +31.
001110       02 FILLER                  PIC S9999  COMP VALUE +30.
001120       02 FILLER                  PIC S9999  COMP VALUE +31.
001130       02 FILLER                  PIC S9999  COMP VALUE +30.
001140       02 FILLER                  PIC S9999  COMP VALUE +31.
001150   01  CALENDAR-DAYS REDEFINES CALENDAR-TABLE.
001160       02 MTH-DAY                 PIC S9999  COMP OCCURS 12 TIMES.
001170   01  OPT-CARD.
001180       02 FILLER                  PIC X(04)      VALUE '-OPT'.
001190       02 FILLER                  PIC X(76)      VALUE SPACES.
001200   01  UTL-CARD.
001210       02 FILLER                  PIC X(12) VALUE '-OPT UTILITY'.
001220       02 FILLER                  PIC X(68)      VALUE SPACES.
001230   01  MCD-CARD.
001240       02 FILLER                  PIC X(05)      VALUE '-MCD '.
001250       02 MCD-CD                  PIC 9999.
001260       02 FILLER                  PIC X(71)      VALUE SPACES.
001270   01  NOPC-CARD.
001280       02 FILLER                  PIC X(09)    VALUE '-OPT NOPC'.
001290       02 FILLER                  PIC X(71)      VALUE SPACES.
001300   01  DLM-CARD.
001310       02 FILLER                  PIC X(05)      VALUE '-DLM '.
001320       02 DLM-NAME                PIC X(8).
001330       02 FILLER                  PIC X          VALUE ','.
001340       02 DLM-PASSWORD            PIC X(8).
001350       02 FILLER                  PIC X(58)      VALUE SPACES.
001360   01  COPY-CARD.
001370       02 FILLER                  PIC X(06)      VALUE '-COPY '.
001380       02 COPY-NAME               PIC X(8).
001390       02 FILLER                  PIC X(12) VALUE ',STATUS=TEST'.
001400       02 FILLER                  PIC X(54)      VALUE SPACES.
001410   01  END-CARD.
001420       02 FILLER                  PIC X(04)      VALUE '-END'.
001430       02 FILLER                  PIC X(76)      VALUE SPACES.
001440 LINKAGE SECTION.
001450   01  CUT-OFF.
001460       02 PARM-LENGTH             PIC S9(4) COMP.
001470       02 PARMS.
001480           03 HOUR-WORD               PIC X(6).
001490           03 CUT-OFF-HOURS           PIC 999.
001500           03 MOD-WORD                PIC X(9).
001510           03 CUT-OFF-MODULES         PIC 999.
001520 PROCEDURE DIVISION USING CUT-OFF.
001530     IF PARM-LENGTH = 21
001540        IF HOUR-WORD = 'HOURS='
001550           IF MOD-WORD = ',MODULES='
001560              IF CUT-OFF-HOURS NUMERIC
001570                  IF CUT-OFF-MODULES NUMERIC
001580                      GO TO PARMS-OK.
001590     DISPLAY 'INPUT PARAMETERS TO PROGRAM OPS4320G INVALID'
001600     DISPLAY 'PARM LENGTH =' PARM-LENGTH.
001610     DISPLAY 'PARAMETERS =' PARMS.
001620     MOVE 1 TO CUT-OFF-MODULES.
001630     GO TO STOP-RUN.
001640 PARMS-OK.
001650     DISPLAY 'CUT OFF HOURS =' CUT-OFF-HOURS.
001660     DISPLAY 'CUT OFF MODULES =' CUT-OFF-MODULES.
001670     ACCEPT WS-CUR-JULIAN-DATE FROM DAY.
001680     DISPLAY 'CURRENT JULIAN DATE = ' WS-CUR-JULIAN-DATE.
001690     ACCEPT WS-CUR-TIME FROM TIME.
001700     DISPLAY 'CURRENT TIME (HHMMSSTT) = ' WS-CUR-TIME.
001710*
001720     OPEN INPUT MCDCODE.
001730     READ MCDCODE AT END
001740         MOVE 0 TO MCD-CD
001750         GO TO END-MCD.
001760     IF MCD-BASE-X = 'NONE'
001770         MOVE 0 TO MCD-CD
001780         GO TO END-MCD.
001790     IF MCD-BASE NOT NUMERIC
001800         DISPLAY 'MCD BASE NUMBER INVALID=' MCD-BASE
001810         DISPLAY 'MUST BE NUMERIC (4 NUMBERS) OR THE WORD NONE'
001820         CLOSE MCDCODE
001830         GO TO STOP-RUN.
001840     MOVE MCD-BASE TO BASE-MCD-CODE.
001850     MOVE CURRENT-DATE TO WS-GREGORIAN-DATE.
001860     MOVE GREG-MM TO CUR-GREG-MM.
001870     MOVE GREG-DD TO CUR-GREG-DD.
001880     COMPUTE MCD-CD = BASE-MCD-CODE + MCD-CODE-FACTOR.
001890 END-MCD.
001900     CLOSE MCDCODE.
001910*
001920     DIVIDE CUR-JUL-YY BY 4 GIVING LEAPTEST REMAINDER LEAP-RMNDR.
001930     IF LEAP-RMNDR = 0
001940         MOVE 29 TO FEB.
001950*
001960     IF CUR-MM GREATER 29
001970         ADD 1 TO CUR-HH.
001980     COMPUTE CURRENT-DAYS = (CUR-JUL-YY * 365) +
001990                            (CUR-JUL-YY / 4) + CUR-JUL-DDD - 1.
002000     COMPUTE CURRENT-HOUR = CURRENT-DAYS * 24 + CUR-HH
002010     DISPLAY 'CURRENT-HOUR = ' CURRENT-HOUR.
002020*
002030     OPEN OUTPUT COPYCARD.
002040     OPEN OUTPUT DLMCARD.
002050*
002060     WRITE COPYCARD-REC FROM UTL-CARD.
002070     IF MCD-CD NOT = 0
002080         WRITE COPYCARD-REC FROM MCD-CARD.
002090     WRITE COPYCARD-REC FROM NOPC-CARD.
002100*
002110     WRITE DLMCARD-REC FROM OPT-CARD.
002120     IF MCD-CD NOT = 0
002130         WRITE DLMCARD-REC FROM MCD-CARD.
002140     WRITE DLMCARD-REC FROM NOPC-CARD.
002150*
002160     CALL 'MASTOPN' USING OPN-AREA, OPN-RETURN-CODE.
002170     IF OPN-RETURN-CODE NOT = '0'
002180          DISPLAY 'LIBRARIAN INPUT FILE COULD NOT BE OPENED'
002190          DISPLAY 'OPEN RETURN CODE =' OPN-RETURN-CODE
002200          GO TO END-RUN.
002210*
002220 GET-NEXT-MODULE.
002230     MOVE SPACES TO MOD-NAME.
002240*
002250     CALL 'MASTMOD' USING MOD-NAME, MOD-RETURN-CODE, MOD-AREA.
002260     IF MOD-RETURN-CODE = '9' OR '2'
002270          DISPLAY 'LIBRARIAN INPUT FILE MODULE LOCATE PROBLEM'
002280          DISPLAY 'READ RETURN CODE =' MOD-RETURN-CODE
002290          MOVE 0 TO MOD-COUNT
002300          GO TO END-RUN.
002310     IF MOD-RETURN-CODE = '1'
002320          GO TO END-RUN.
002330*
002340 USE-ADD-DATE-TIME.
002350     MOVE LAST-UP-DATE TO WS-DATE.
002360     PERFORM CONVERT-DATE THRU CONVERT-EXIT.
002370     MOVE DATE-CONVERTED TO JOB-DAYS.
002380*
002390     IF LAST-UP-MM GREATER 29
002400         ADD 1 TO LAST-UP-HH.
002410     COMPUTE JOB-HOUR = JOB-DAYS * 24 + LAST-UP-HH.
002420     COMPUTE HOURS-ONLINE = CURRENT-HOUR - JOB-HOUR.
002430*
002440     IF JUL-RUN-DATE = 'YYDDD'
002450          DISPLAY 'JOB FOLLOWING NOT SCHEDULED'.
002460     IF LAST-UP-MM GREATER 29
002470         SUBTRACT 1 FROM LAST-UP-HH.
002480     DISPLAY 'JOB-HOUR=' JOB-HOUR
002490             '  HOURS-ONLINE=' HOURS-ONLINE
002500             '  MODULE NAME=' MODLNAME
002510             '  JOBNAME=' JOBNAME
002520             '  ADD DATE=' LAST-UP-DATE
002530             '  ADD TIME=' LAST-UP-HH '.' LAST-UP-MM.
002540*
002550     IF HOURS-ONLINE GREATER CUT-OFF-HOURS
002560         GO TO RITE-DLM-COPY
002570     ELSE
002580         GO TO GET-NEXT-MODULE.
002590*
002600 RITE-DLM-COPY.
002610     IF MOD-COUNT GREATER 494
002620      DISPLAY '** MAXIMUM MODULES TO BE TAKEN OFFLINE REACHED **'
002630      DISPLAY '**   495 MODULES IS THE LIMIT IN ANY ONE RUN   **'
002640        GO TO END-RUN.
002650*
002660     ADD 1 TO MOD-COUNT.
002670*
002680     MOVE MOD-NAME TO COPY-NAME.
002690     WRITE COPYCARD-REC FROM COPY-CARD.
002700*
002710     MOVE MOD-NAME TO DLM-NAME.
002720     MOVE MOD-PASSWORD TO DLM-PASSWORD.
002730     WRITE DLMCARD-REC FROM DLM-CARD.
002740*
002750     GO TO GET-NEXT-MODULE.
002760*
002770 CONVERT-DATE.
002780     COMPUTE DATE-CONVERTED = (365 * YY) + (YY / 4) + DD - 1.
002790     MOVE 0 TO MO-CTR.
002800 INCR-MO-CTR.
002810     ADD 1 TO MO-CTR.
002820     IF MO-CTR LESS MM
002830         ADD MTH-DAY (MO-CTR) TO DATE-CONVERTED
002840         GO TO INCR-MO-CTR.
002850 CONVERT-EXIT. EXIT.
002860*
002870 END-RUN.
002880     WRITE DLMCARD-REC FROM END-CARD.
002890     WRITE COPYCARD-REC FROM END-CARD.
002900     CLOSE DLMCARD.
002910     CLOSE COPYCARD.
002920 STOP-RUN.
002930     IF MOD-COUNT GREATER CUT-OFF-MODULES
002940         MOVE +0 TO RETURN-CODE
002950     ELSE
002960         MOVE +999 TO RETURN-CODE.
002970     DISPLAY 'MODULES TO BE TAKEN OFFLINE = ' MOD-COUNT.
002980*
002990     STOP RUN.
