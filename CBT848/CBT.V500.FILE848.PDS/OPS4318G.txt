         TITLE 'LIBSPOOL: OPS4318G - LIBRARIAN FILE INQUIRY'
         PUNCH ' INCLUDE SYSLIB(TSOFAIR)'                      06/09/83
         PUNCH ' ORDER OPS4318G '                              06/09/83
         PUNCH ' ENTRY OPS4318G'                               06/09/83
*/****************************************************************** /*
*/*                                                                  */
*/*       WRITTEN BY :             ROBERT P DZIALAK                  */
*/*                                OCCIDENTAL CHEMICAL CORPORATION   */
*/*                                360 RAINBOW BLVD. SOUTH           */
*/*                                NIAGARA FALLS , NEW YORK 14303    */
*/*                                (716) 286-3251                    */
*/*                                                                  */
*/*       SJB  06/09/83            MODS FOR USE AT ODC               */
*/*                                                                  */
*/*       SJB  06/17/83            ADDED CODE FOR STAX (ATTENTION)   */
*/*                                                                  */
*/*       SJB  06/23/83            CHANGE DISPLAY TO ADD JOB STATUS  */
*/*                                                                  */
*/****************************************************************** /*
OPS4318G CSECT
ON       EQU   X'80'                                           06/17/83
         PRINT NOGEN
         STM   R14,R12,12(R13)
         LR    R12,R15
         USING OPS4318G,R12
         ST    R13,SAVEAREA+4
         LR    R2,R13
         LA    R13,SAVEAREA
         ST    R13,8(R2)
*
*   THIS PROGRAM IS PASSED A PARAMETER LIST AT ENTRY REG1 POINTS TO
*   AN ADDRESS THAT POINTS TO THE FOLLOWING
*
*        1) A HALF WORD THAT HAS THE LENGTH OF THE PARAMETER LIST.
*           MAXIMUM LENGTH IS 100 BYTES.
*        2) PARAMETER VALUE (A-G) - 1 BYTE
*        3) USER-ID - 7 BYTES (ASSUMED TO ALWAYS BE 7 BYTES).
*        4) THE NAME BASED ON THE PARMETER VALUE PASSED (0-8 BYTES).
*
         L     R2,0(R1)                R2 = ADDR. OF PASSED PARMS
         MVC   PARM(8),2(R2)           LOAD PARM VALUE (A-I), TSOUID
         CLI   PARM,C'E'               ARE ALL MODULES TO BE DISPLAYED?
         BE    OPENPRT                 NO NAME ON PARM = E
         SR    R3,R3                   CLEAR R3
         IC    R3,1(R2)                PUT PARM LENGTH IN R3
         SH    R3,=H'9'                SUBTRACT 1 FOR PARM
*                                      SUBTRACT 7 FOR USER ID
*                                      SUBTRACT 1 FOR MVC AND CLC
         CH    R3,=H'7'                COMPARE R3 TO 7
         BNH   MVENAME             BIF <= 7                    06/17/83
         LH    R3,=H'7'            USE 7 IF IT IS TOO BIG      06/17/83
MVENAME  DS    0H                                              06/17/83
         STH   R3,NAMELEN          SAVE LENGTH OF NAME         06/17/83
         MVC   NAME(*-*),10(R2)    MOVE IN 1ST BYTE            06/17/83
         EX    R3,*-6              MOVE IN THE REST            06/17/83
*        OPEN  (SNAPDCB,(OUTPUT))
OPENPRT  DS    0H
         SPACE 3                                               06/17/83
         MVI   ATTNFLG,0           ZERO ATTN FLAG              06/17/83
         MVI   FLAG,0              ZERO PGM  FLAG              08/23/83
         STAX  ATTNEXIT,MF=(E,STAXLIST)                        06/17/83
         SPACE 3                                               06/17/83
         OPEN  (TSODCB,(OUTPUT))   OPEN OUTPUT FILE TO TERMINAL
         SPACE 3                                               06/17/83
         CLI   ATTNFLG,ON          WAS ATTENTION HIT           06/17/83
         BE    CLSELIBR                                        06/17/83
         SPACE 3                                               06/17/83
*
* THE LIBRARIAN MACROS MASTOPN,MASTMOD AND MASTCLS ARE EXPLAINED IN
* THE LIBRARIAN /VS OS SYSTEM REFERENCE MANUAL APPENDIX B - FILE
* ACCESS INTERFACE ROUTINES.
*
         CALL  MASTOPN,(0,OPNRETCD,JCLFILE),VL
         CLI   OPNRETCD,C'0'
         BE    CHKPARMS                BAD OPEN IF GREATER THAN 0
*
         XC    TRMLINE,TRMLINE           CLEAR LINE
         PUT   TSODCB,TRMLINE            WRITE BLANK LINE
         MVC   TRMLINE(38),BADOPEN       INDICATE BAD OPEN OF LIBSPOOL
         PUT   TSODCB,TRMLINE            WRITE TO TERMINAL
         B     CLSELIBR                  QUIT
*
CHKPARMS CLI   PARM,C'A'      CHECK
         BE    PARMA            PARM
         CLI   PARM,C'B'          VALUE
         BE    PARMB                AND
         CLI   PARM,C'C'              BRANCH
         BE    PARMC                    TO
         CLI   PARM,C'D'                  THE
         BE    PARMD                        CODE
         CLI   PARM,C'E'                      TO
         BE    PARME                            HANDLE
         CLI   PARM,C'F'                          THIS
         BE    PARMF                                PARM
         CLI   PARM,C'G'                              VALUE
         BE    PARMG
*
         XC    TRMLINE,TRMLINE
         PUT   TSODCB,TRMLINE          WRITE BLANK LINE
         MVC   TRMLINE(11),BADPARM
         MVC   TRMLINE+12(1),PARM
         PUT   TSODCB,TRMLINE          WRITE TO TERMINAL BAD PARM
         B     CLSELIBR
*
PARMA    EQU   *               ********* JOB NAME SELECTION ***********
LOOK4NME BAL   R4,GETNXTMD               GET INFO ON NEXT MODULE
         CLI   MODRETCD,C'1'             END OF FILE?
         BE    DSPLYEND                  YES, GO DISPLAY EOF
         LH    R3,NAMELEN          GET NAMELEN -1              06/17/83
         CLC   JOBNAME(*-*),NAME   DUMMY CLC                   06/17/83
         EX    R3,*-6              IS THIS THE JOBNAME?        06/17/83
         BNE   LOOK4NME                  NO,GET NEXT MODULE
         BAL   R5,PRINTIT                YES, SEND DATA TO THE TERMINAL
         B     LOOK4NME                  GET NEXT MODULE
*
PARMB    EQU   *               ********* USER ID SELECTION ************
LOOK4UID BAL   R4,GETNXTMD
         CLI   MODRETCD,C'1'             END OF FILE?
         BE    DSPLYEND                  YES, GO DISPLAY EOF
CHKUID   CLC   USERID(7),NAME
         BNE   LOOK4UID
         BAL   R5,PRINTIT
         B     LOOK4UID
*
PARMC    EQU   *                 ******* JOB NUMBER SELECTION *********
LOOK4JNO BAL   R4,GETNXTMD
         CLI   MODRETCD,C'1'             END OF FILE?
         BE    DSPLYEND                  YES, GO DISPLAY EOF
CHKJNO   CLC   JOBNUMBR(4),NAME
         BNE   LOOK4JNO
         BAL   R5,PRINTIT
         B     LOOK4JNO
*
PARMD    EQU   *                 ******* MODULE NAME SELECTION ********
         CLI   ATTNFLG,ON          WAS ATTENTION HIT           06/17/83
         BE    CLSELIBR                                        06/17/83
         CALL  MASTMOD,(NAME,MODRETCD,MODINFO),VL
         CLI   MODRETCD,C'0'             MODULE FOUND ?
         BH    DSPLYEND                  NO, GO DISPLAY EOF
         BAL   R5,PRINTIT
         B     CLSELIBR
*
PARME    EQU   *                  ****** ALL MODULE SELECTION *********
GETALL   MVC   NAME(8),SPACES            SET UP SEARCH NAME FIELD
         BAL   R4,GETNXTMD
         CLI   MODRETCD,C'1'             END OF FILE?
         BE    DSPLYEND                  YES, GO DISPLAY EOF
PRINTALL BAL   R5,PRINTIT
         B     GETALL
*
PARMF    EQU   *                ******** DISPLAY MODULE TO TERMINAL ***
         CLI   ATTNFLG,ON          WAS ATTENTION HIT           06/17/83
         BE    CLSELIBR                                        06/17/83
         CALL  MASTMOD,(NAME,MODRETCD,MODINFO),VL
         CLI   MODRETCD,C'1'
         BL    FOUNDITF
         B     NOTHEREF
*
* A MASTMOD RETURN CODE GREATER THAN ZERO INDICATES A PROBLEM
*    1) MODULE COULD NOT BE FOUND
*    2) LIBRARIAN JCL DATA SET NOT SUCCESSFULLY OPENED VIA MASTOPN
*    9) ILLOGICAL CONDITION ON LIBRARIAN DATA SET
*
NOTHEREF EQU   *
         XC    TRMLINE,TRMLINE           CLEAR LINE
         PUT   TSODCB,TRMLINE            WRITE BLANK LINE
         MVC   TRMLINE(16),MODNOTFD
         PUT   TSODCB,TRMLINE            INDICATE MODULE NOT FOUND
         MVI   RETURNCD,X'04'            RETURN CODE TO 4 IF NO HITS
         B     CLSELIBR
*
* A MASTRCD RETURN CODE GREATER THAN ZERO INDICATES A PROBLEM
*    1) NO CARDS LEFT IN THE MODULE
*    2) CALL MADE AFTER 'END MODULE' WAS REACHED
*    9) ILLOGICAL CONDITION ON LIBRARIAN DATA SET
*
FOUNDITF EQU   *
         CLI   ATTNFLG,ON          WAS ATTENTION HIT           06/17/83
         BE    CLSELIBR                                        06/17/83
         CALL  MASTRCD,(MODINFO,MODRETCD),VL
         CLI   MODRETCD,C'0'
         BH    CLSELIBR                       EXIT
         CLI   MODINFO,X'FE'                  CHECK FOR CONTINUATION
         BNE   RITELNE1
         MVC   TRMLINE+1(78),MODINFO+1
         PUT   TSODCB,TRMLINE
         B     FOUNDITF
RITELNE1 MVC   TRMLINE(79),MODINFO
         PUT   TSODCB,TRMLINE
         MVC   TRMLINE(1),MODINFO+79        SAVE LAST CHAR IF CONTINUED
         B     FOUNDITF
*
PARMG    EQU   *                  ****** WRITE MODULE TO HARDCOPY *****
         CLI   ATTNFLG,ON          WAS ATTENTION HIT           06/17/83
         BE    CLSELIBR                                        06/17/83
         CALL  MASTMOD,(NAME,MODRETCD,MODINFO),VL
         CLI   MODRETCD,C'1'
         BL    OPENHCPY
         B     NOTHEREG
*
* A MASTMOD RETURN CODE GREATER THAN ZERO INDICATES A PROBLEM
*    1) MODULE COULD NOT BE FOUND
*    2) LIBRARIAN JCL DATA SET NOT SUCCESSFULLY OPENED VIA MASTOPN
*    9) ILLOGICAL CONDITION ON LIBRARIAN DATA SET
*
NOTHEREG EQU   *
         XC    TRMLINE,TRMLINE           CLEAR LINE
         PUT   TSODCB,TRMLINE            WRITE BLANK LINE
         MVC   TRMLINE(16),MODNOTFD
         PUT   TSODCB,TRMLINE            INDICATE MODULE NOT FOUND
         MVI   RETURNCD,X'04'            RETURN CODE TO 4 IF NO HITS
         B     CLSELIBR
*
* A MASTRCD RETURN CODE GREATER THAN ZERO INDICATES A PROBLEM
*    1) NO CARDS LEFT IN THE MODULE
*    2) CALL MADE AFTER 'END MODULE' WAS REACHED
*    9) ILLOGICAL CONDITION ON LIBRARIAN DATA SET
*
OPENHCPY OPEN  (PRINTDCB,(OUTPUT))
         CLI   ATTNFLG,ON          WAS ATTENTION HIT           06/17/83
         BE    CLSELIBR                                        06/17/83
         CALL  MASTRCD,(MODINFO,MODRETCD),VL
         CLI   MODRETCD,C'0'
         BH    CLOSEPRT
         MVC   MODINFO+5(51),LSPRTHDR        IDENTIFY THIS JCL PRINT
         MVC   MODINFO+42(8),NAME
         B     MVE4PRT
FOUNDITG EQU   *
         CLI   ATTNFLG,ON          WAS ATTENTION HIT           06/17/83
         BE    CLSELIBR                                        06/17/83
         CALL  MASTRCD,(MODINFO,MODRETCD),VL
         CLI   MODRETCD,C'0'
         BH    CLOSEPRT
MVE4PRT  MVC   PRINTLNE(80),MODINFO
         MVC   PRINTLNE+80(53),SPACES
         CLI   ATTNFLG,ON          WAS ATTENTION HIT           06/17/83
         BE    CLSELIBR                                        06/17/83
         CALL  MASTRCD,(MODINFO,MODRETCD),VL
         CLI   MODRETCD,C'0'
         BH    ENDPRNT
         CLI   MODINFO,X'FE'                  CHECK FOR CONTINUATION
         BNE   PRNTLAST
         MVC   PRINTLNE+80(53),MODINFO+1
         PUT   PRINTDCB,PRINTLNE
         B     FOUNDITG
PRNTLAST PUT   PRINTDCB,PRINTLNE
         B     MVE4PRT
*
ENDPRNT  PUT   PRINTDCB,PRINTLNE
CLOSEPRT CLOSE PRINTDCB
         XC    TRMLINE,TRMLINE           CLEAR LINE
         PUT   TSODCB,TRMLINE            WRITE BLANK LINE
         MVC   TRMLINE(31),QUE4PRNT
         PUT   TSODCB,TRMLINE         INDICATE MODULE QUEUED FOR PRINT
         B     CLSELIBR                  EXIT
*
DSPLYEND XC    TRMLINE,TRMLINE           CLEAR LINE
         PUT   TSODCB,TRMLINE            WRITE BLANK LINE
         MVC   TRMLINE(11),ENDOFDTA      INDICATE END OF DATA
         PUT   TSODCB,TRMLINE            WRITE TO TERMINAL
*        SNAP  DCB=SNAPDCB,ID=3,STORAGE=(DUMPSTRT,DUMPEND),PDATA=(REGS)
         L     R7,HITCOUNT               LOAD COUNT OF HITS
         LTR   R7,R7                     CHECK IF ZERO
         BNZ   CLSELIBR                  EXIT IF NAMES HAVE BEEN FOUND
         XC    TRMLINE,TRMLINE           CLEAR LINE
         PUT   TSODCB,TRMLINE            WRITE BLANK LINE
         MVC   TRMLINE(16),NOMATCH       INDICATE NO MATCHES
         PUT   TSODCB,TRMLINE            WRITE TO TERMINAL
         MVI   RETURNCD,X'04'            RETURN CODE TO 4 IF NO HITS
*        SNAP  DCB=SNAPDCB,ID=1,STORAGE=(DUMPSTRT,DUMPEND),PDATA=(REGS)
*
CLSELIBR CALL  MASTCLS
*        CLOSE SNAPDCB
         CLOSE TSODCB
         STAX  MF=(E,STAXLIST)     CANCEL ATTENTION EXIT       06/17/83
         SR    R15,R15
         IC    R15,RETURNCD
         L     R13,SAVEAREA+4
         L     R14,12(R13)
         LM    R0,R12,20(R13)
         BR    R14
*
GETNXTMD MVC   FINDNAME(8),SPACES
         CLI   ATTNFLG,ON          WAS ATTENTION HIT           06/17/83
         BE    CLSELIBR                                        06/17/83
         CALL  MASTMOD,(FINDNAME,MODRETCD,MODINFO),VL
         CLI   MODRETCD,C'1'
         BH    LIBRPROB
         BR    R4
*
* A MASTMOD RETURN CODE GREATER THAN ZERO INDICATES A PROBLEM
*    1) MODULE COULD NOT BE FOUND
*    2) LIBRARIAN JCL DATA SET NOT SUCCESSFULLY OPENED VIA MASTOPN
*    9) ILLOGICAL CONDITION ON LIBRARIAN DATA SET
*
LIBRPROB EQU   *
         XC    TRMLINE,TRMLINE           CLEAR LINE
         PUT   TSODCB,TRMLINE            WRITE BLANK LINE
         MVC   TRMLINE(22),LIBRPRBM      INDICATE PROBLEM
         PUT   TSODCB,TRMLINE            WRITE TO TERMINAL
*        SNAP  DCB=SNAPDCB,ID=2,STORAGE=(DUMPSTRT,DUMPEND),PDATA=(REGS)
         B     CLSELIBR
*
PRINTIT  TM    FLAG,$HEADDON       HAS TITLE LINES BEEN ISSUED 08/23/83
         BO    SKIPHDR             YES, THEN SKIP              08/23/83
         OI    FLAG,$HEADDON       INDICATE TITLE DONE         08/23/83
         PUT   TSODCB,HDRLINE1
         PUT   TSODCB,HDRLINE2
SKIPHDR  DS    0H                                              08/23/83
         MVC   HSTDATA,SPACES                                  08/23/83
         CALL  MASTHST,(HSTDATA,MODRETCD),VL                   08/23/83
         MVC   PRTLINE(79),SPACES  BLANK OUT RECORD            08/23/83
         MVC   RPTJOBNM,JOBNAME    PREPARE PRINT LINE TO BE    08/23/83
         MVC   RPTJOB#,JOBNUMBR    SENT TO THE TERMINAL        08/23/83
         MVC   RPTUSER,USERID                                  08/23/83
         MVC   RPTLIBNM,MODULENM                               08/23/83
         CLC   RUNDATE(5),=C'YYDDD'                            08/23/83
         BNE   SHOWTIME                                        08/23/83
         MVC   RPTRUNDT(18),NOTSCHED                           08/23/83
         B     PUT2TERM                                        08/23/83
SHOWTIME MVC   RPTRUNDT(18),SPACES                             08/23/83
         BAL   R6,JULCNVRT                                     08/23/83
         MVC   RPTRUNDT,GREGDTE                                08/23/83
         MVC   RPTSTIME+0(2),STRTHOUR                          08/23/83
         MVI   RPTSTIME+2,C'.'                                 08/23/83
         MVC   RPTSTIME+3(2),STRTMIN                           08/23/83
         MVC   RPTETIME+0(2),ENDHOUR                           08/23/83
         MVI   RPTETIME+2,C'.'                                 08/23/83
         MVC   RPTETIME+3(2),ENDMIN                            08/23/83
         MVC   RPTJSTAT,HSTMSG                                 08/23/83
PUT2TERM PUT   TSODCB,PRTLINE
         L     R7,HITCOUNT               LOAD COUNT OF HIT SO FAR
         LA    R7,1(R7)                  INCREMENT BY 1
         ST    R7,HITCOUNT               SAVE IT
         BR    R5
*
JULCNVRT MVC  YRUNPACK+3(5),RUNDATE    GET JULIAN RUN DATE
         PACK YRPACK,YRUNPACK          PACK IT
         L    R1,YRPACK                PUT IT IN R1
*
         SR   R8,R8
         PACK DATEP+6(2),YRUNPACK+3(2)
         CVB  R9,DATEP
         D    R8,=F'4'                 DIVIDE YEAR BY 4
         LTR  R8,R8                    ZERO REMAINDER INDICATES LEAP YR
         BNZ  FINDDAY
CHANGFEB MVI  FEB,X'1D'
*
FINDDAY  SLA   R1,16                    REMOVE YEAR DDDF0000
         SRA   R1,16                    MOVE BACK 0000DDDF
         XC    DUBLWD,DUBLWD            CLEAR DUBLWD
         ST    R1,DUBLWD+4
         CVB   R1,DUBLWD                PACKED TO BINARY
         LA    R7,1                     SET MONTH CTR TO 1
         LA    R8,CALENDAR
         SR    R9,R9
GETDYMTH IC    R9,0(R8)                 INSERT DAYS IN MONTH IN REG 9
         CR    R1,R9                    DAY OF YEAR - DAYS IN MONTH
         BNH   GOTDYMTH
         SR    R1,R9                    SUBTRACT MONTHS DAYS FROM R1
         LA    R7,1(R7)                 INCREMENT TO NEXT MONTH
         LA    R8,1(R8)                 INCREMENT TO NEXT MONTH
         B     GETDYMTH
GOTDYMTH BAL   R11,CNVRTX
         MVC   GREGDTE+0(2),DWORD+2     MOVE MONTH
         LR    R7,R1
         BAL   R11,CNVRTX
         MVC   GREGDTE+3(2),DWORD+2     MOVE DAY
         MVC   GREGDTE+6(2),YRUNPACK+3  MOVE YEAR
         MVI   GREGDTE+2,C'/'
         MVI   GREGDTE+5,C'/'
XITDTECV BR    R6
*
CNVRTX   CVD   R7,DWORD
         UNPK  DWORD(4),DWORD+5(3)
         OI    DWORD+3,X'F0'
         BR    R11
         SPACE 3                                               06/17/83
         USING *,R15                                           06/17/83
ATTNEXIT L     R1,8(,R1)           GET @ OF USER PARM AREA     06/17/83
         MVI   0(R1),ON            TURN IT ON                  06/17/83
         BR    R14                 AND RETURN                  06/17/83
         DROP  R15                                             06/17/83
*
GREGDTE  DC    D'0'
DATEP    DC    D'0'
YRUNPACK DC    X'F0F0F0F0F0F0F0F0'
DUBLWD   DC    D'0'
DWORD    DC    D'0'
DBLTIME  DC    D'0'
YRPACK   DC    F'0'
FULLTIME DC    F'0'
JULDATE  DC    F'0'
CALENDAR DC    X'1F'            NUMBER OF DAYS IN JAN.
FEB      DC    X'1C'            NUMBER OF DAYS IN FEB.
         DC    X'1F'            NUMBER OF DAYS IN MAR.
         DC    X'1E'            NUMBER OF DAYS IN APR.
         DC    X'1F'            NUMBER OF DAYS IN MAY
         DC    X'1E'            NUMBER OF DAYS IN JUNE
         DC    X'1F'            NUMBER OF DAYS IN JULY
         DC    X'1F'            NUMBER OF DAYS IN AUG.
         DC    X'1E'            NUMBER OF DAYS IN SEPT
         DC    X'1F'            NUMBER OF DAYS IN OCT.
         DC    X'1E'            NUMBER OF DAYS IN NOV.
         DC    X'1F'            NUMBER OF DAYS IN DEC.
*
TSODCB  DCB   DSORG=PS,MACRF=(PM),DDNAME=SCREEN,RECFM=FB,              X
               BLKSIZE=1580,LRECL=79
PRINTDCB DCB   DSORG=PS,MACRF=(PM),DDNAME=PRINTO,RECFM=FBA,            X
               BLKSIZE=2660,LRECL=133
SNAPDCB  DCB   DSORG=PS,MACRF=(W),DDNAME=SNAPDUMP,RECFM=VBA,           X
               BLKSIZE=1632,LRECL=125
         EJECT ,                                               06/17/83
ATTNFLG  DC    X'00'               INDICATES ATTN KEY HIT      06/17/83
STAXLIST STAX  ATTNEXIT,USADDR=ATTNFLG,MF=L                    06/17/83
*
DUMPSTRT DS    0F
HITCOUNT DC    F'0'
SAVEAREA DC    18F'0'
NAMELEN  DC    H'8'                LENGTH OF NAME FIELD        06/17/83
NAME     DC    CL8' '
SPACES   DC    CL133' '
MODINFO  DC    CL100' '
OPNRETCD DC    C' '
MODRETCD DC    C' '
JCLFILE  DC    C'JCLFILE '
JCLFILEB DC    C'        '
RETURNCD DC    X'00'
PARMEQU  DC    CL5'PARM='
PARM     DC    C'0'
TSOUID   DC    CL7' '
FNDNMEQU DC    CL10'FINDNAME ='
FINDNAME DC    CL8' '
FLAG     DC    X'00'                                                SJB
$HEADDON EQU   X'80'                                                SJB
DUMPEND  EQU   *
HSTDATA  DC    CL88' '                                         08/23/83
HSTMSG   EQU   HSTDATA+5                                       08/23/83
PRINTLNE DC    CL133' '
TRMLINE  DC    CL79' '
PRTLINE  DC    CL79' '
HDRLINE1 DC    CL79' '
         ORG   HDRLINE1
RPTJOBNM EQU   PRTLINE+*-HDRLINE1,8                            08/23/83
         DC    CL9'  JOB    '                                  08/23/83
RPTJOB#  EQU   PRTLINE+*-HDRLINE1,4                            08/23/83
         DC    CL5'JOB  '                                      08/23/83
RPTUSER  EQU   PRTLINE+*-HDRLINE1,7                            08/23/83
         DC    CL8' USER   '                                   08/23/83
RPTLIBNM EQU   PRTLINE+*-HDRLINE1,8                            08/23/83
         DC    CL9'LIBRARY '                                   08/23/83
RPTRUNDT EQU   PRTLINE+*-HDRLINE1,8                            08/23/83
         DC    CL9'RUN-DATE '                                  08/23/83
RPTSTIME EQU   PRTLINE+*-HDRLINE1,5                            08/23/83
         DC    CL6'START '                                     08/23/83
RPTETIME EQU   PRTLINE+*-HDRLINE1,5                            08/23/83
         DC    CL6' END  '                                     08/23/83
RPTJSTAT EQU   PRTLINE+*-HDRLINE1,22                           08/23/83
         DC    CL22'JOB COMPLETION '                           08/23/83
         ORG   HDRLINE1+L'HDRLINE1                             08/23/83
HDRLINE2 DC    CL79' '                                         08/23/83
         ORG   HDRLINE2
         DC    CL9' NAME    '                                  08/23/83
         DC    CL5'NUM. '                                      08/23/83
         DC    CL8'  ID    '                                   08/23/83
         DC    CL9' NAME   '                                   08/23/83
         DC    CL9'MM/DD/YY '                                  08/23/83
         DC    CL6'TIME  '                                     08/23/83
         DC    CL6'TIME  '                                     08/23/83
         DC    CL22'   STATUS    '                             08/23/83
         ORG   HDRLINE2+L'HDRLINE2                             08/23/83
LSPRTHDR DC   CL51'***** LIBSPOOL JCL LISTING OF MODULE NNNNNNNN *****'
ENDOFDTA DC    CL11'END OF DATA'
BADOPEN  DC    CL38'OPEN PROBLEM FOR LIBSPOOL JCL DATA SET'
QUE4PRNT DC    CL31'JCL MODULE NOW QUEUED FOR PRINT'
BADPARM  DC    CL11'BAD PARM = '
NOMATCH  DC    CL16'NO MATCHES FOUND'
MODNOTFD DC    CL16'MODULE NOT FOUND'
LIBRPRBM DC    CL22'MASMOD RETURN CODE BAD'
NOTSCHED DC    CL18'JOB NOT SCHEDULED'
         LTORG
JOBNAME  EQU   MODINFO+23,8
JOBNUMBR EQU   MODINFO+50,4
USERID   EQU   MODINFO+31,7
MODULENM EQU   MODINFO+46,8
RUNDATE  EQU   MODINFO+54,5
STRTHOUR EQU   MODINFO+59,2
STRTMIN  EQU   MODINFO+61,2
ENDHOUR  EQU   MODINFO+63,2
ENDMIN   EQU   MODINFO+65,2
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
