         MACRO
&NM      FDLITEM &ARGS
.*
.*   BUILD ONE DISPLAY ITEM DEFINITION FOR THE SCLINE SERVICE
.*
.*   USE AFTER  FDLINE ...END=name                    1/n
.*   FDLITEM LABEL:,(FIELD ATTRIBS),LBL:,TEXT.....    2/n
.*   DC or whatever                                   m/n
.*   FDLINE *END             GENERATE CLOSING NAME    n/n
.*
.*   ATTRIBUTES:
.*
.*   IN, OUT, INT(ENSE OUT),
.*   RED, BLUE, GREEN, CYAN, TURQ(OISE), WHITE, YELLOW
.*   BLACK (NON-DISPLAY) - FORCES INPUT MODE                    GP09353
.*
.*   THIS MACRO GENERATES NEITHER THE LINE NOR FLAG FIELDS; USE FDLINE
.*   FOR THOSE
.*
         GBLC  &ZZZLEND      PRIOR/NEXT END LABEL
         GBLC  &ZZZFLAB      NEXT FIELD LABEL
         GBLC  &ZZZFLAN      FDLINE  LABEL                      GP09358
         LCLC  &LABEL,&C,&FLAB,&D                               GP09358
         LCLB  &F0,&F1,&F2,&F3,&F4,&F5,&F6,&F7
         LCLB  &INP,&INT,&BLACK                                 GP09353
         LCLA  &I,&J,&K,&L,&M,&N
         AIF   ('&ARGS' EQ '*END').CLOSER
&N       SETA  N'&SYSLIST
&LABEL   SETC  '&NM'
&I       SETA  0
         AIF   (&N GT 0).LOOP     HAVE MULTIPLE ARGUMENTS
         MNOTE 0,'FDLITEM: NO ARGUMENTS SUPPLIED'
         AGO   .DONE
.LOOP    AIF   (&I GE &N).DONE
&I       SETA  &I+1
&C       SETC  '&SYSLIST(&I)'
         AIF   ('&C' EQ '').LOOP
         AIF   ('&C'(1,1) EQ '(').FIELD
         AIF   ('&C'(1,1) EQ '''').STRING
         AIF   ('&C'(K'&C,1) EQ ':').LABEL
         AIF   ('&C'(K'&C,1) EQ '-').OFFSET
         AIF   ('&C'(1,1) EQ 'C').COUNT
         AIF   ('&C'(1,1) EQ 'X').HEXER
         AIF   ('&C'(1,1) GE '0' AND '&C'(1,1) LE '9').COUNT
.ARGBAD  MNOTE 8,'FDLITEM: ARGUMENT &I INVALID - &C NOT A VALID ENTRY'
         AGO   .LOOP
.*
.STRING  ANOP  ,
&LABEL   DC    C&C
&LABEL   SETC  ''
         AGO   .LOOP
.*
.COUNT   AIF   ('&C'(K'&C,1) NE '''').COUNTS
&LABEL   DC    &C
&LABEL   SETC  ''
         AGO   .LOOP
.COUNTS  ANOP  ,
&LABEL   DC    &C' '
&LABEL   SETC  ''
         AGO   .LOOP
.HEXER   AIF   ('&C'(K'&C,1) NE '''').HEXED
&LABEL   DC    &C
&LABEL   SETC  ''
         AGO   .LOOP
.HEXED   ANOP  ,
&LABEL   DC    &C'0'
&LABEL   SETC  ''
         AGO   .LOOP
.*
.FIELD   ANOP  ,
&J       SETA  0
&L       SETA  N'&SYSLIST(&I)  NUMBER OF SUBARGUMENTS
&F0      SETB  0             MUST BE OFF
&F1      SETB  0             MUST BE OFF (RANGE 01-3F)
&F2      SETB  0
&F3      SETB  0        1 0  OUTPUT  ;  1 1  OUT INTENSE
&F4      SETB  0        0 1  INPUT   ;  0 0  NON-DISPLAY
&F5      SETB  0             COLOR: GREEN
&F6      SETB  0             COLOR: RED
&F7      SETB  0             COLOR: BLUE
&INP     SETB  0             INPUT                              GP09353
&INT     SETB  0             INTENSE                            GP09353
&BLACK   SETB  0             NON-DISPLAY                        GP09353
.FDLOOP  AIF   (&J GE &L).EXPFLD
&J       SETA  &J+1
&C       SETC  '&SYSLIST(&I,&J)'
         AIF   ('&C' EQ '').FDLOOP
.*DEFER* AIF   ('&C' EQ 'REVERSE').REVERSE
.*DEFER* AIF   ('&C' EQ 'BLINK').BLINK
.*DEFER* AIF   ('&C' EQ 'UNDER').UNDER
.*DEFER* AIF   ('&C' EQ 'DFLT').NULL
         AIF   ('&C' EQ 'BL').BLUE
         AIF   ('&C' EQ 'RE').RED
         AIF   ('&C' EQ 'PI').PINK
         AIF   ('&C' EQ 'MA').PINK
         AIF   ('&C' EQ 'GR').GREEN
         AIF   ('&C' EQ 'CY').TURQ
         AIF   ('&C' EQ 'TU').TURQ
         AIF   ('&C' EQ 'YE').YELLOW
         AIF   ('&C' EQ 'WH').WHITE
         AIF   ('&C' EQ 'BK').BLACK                             GP09353
         AIF   ('&C' EQ 'ND').BLACK                             GP09353
         AIF   ('&C' EQ 'IN').INPUT
         AIF   ('&C' EQ 'BLUE').BLUE
         AIF   ('&C' EQ 'RED').RED
         AIF   ('&C' EQ 'PINK').PINK
         AIF   ('&C' EQ 'MAGENTA').PINK
         AIF   ('&C' EQ 'GREEN').GREEN
         AIF   ('&C' EQ 'CYAN').TURQ
         AIF   ('&C' EQ 'TURQ').TURQ
         AIF   ('&C' EQ 'TURQOISE').TURQ
         AIF   ('&C' EQ 'YELLOW').YELLOW
         AIF   ('&C' EQ 'WHITE').WHITE
         AIF   ('&C' EQ 'BLACK').BLACK                          GP09353
         AIF   ('&C' EQ 'INPUT').INPUT
         AIF   ('&C' EQ 'OUT').OUTPUT
         AIF   ('&C' EQ 'INT').INTENSE
         AIF   ('&C' EQ 'INTENSE').INTENSE
       MNOTE 8,'FDLITEM: ARGUMENT &I,&J INVALID - &C NOT A VALID ENTRY'
         AGO   .FDLOOP
.BLUE    ANOP  ,
&F5      SETB  0
&F6      SETB  0
&F7      SETB  1
         AGO   .FDLOOP
.*
.RED     ANOP  ,
&F5      SETB  0
&F6      SETB  1
&F7      SETB  0
         AGO   .FDLOOP
.*
.PINK    ANOP  ,
&F5      SETB  0
&F6      SETB  1
&F7      SETB  1
         AGO   .FDLOOP
.*
.GREEN   ANOP  ,
&F5      SETB  1
&F6      SETB  0
&F7      SETB  0
         AGO   .FDLOOP
.*
.TURQ    ANOP  ,
&F5      SETB  1
&F6      SETB  0
&F7      SETB  1
         AGO   .FDLOOP
.*
.YELLOW  ANOP  ,
&F5      SETB  1
&F6      SETB  1
&F7      SETB  0
         AGO   .FDLOOP
.*
.WHITE   ANOP  ,
&F5      SETB  1
&F6      SETB  1
&F7      SETB  1
         AGO   .FDLOOP
.*
.BLACK   ANOP  ,                                                GP09353
&F5      SETB  0                                                GP09353
&F6      SETB  0                                                GP09353
&F7      SETB  1             FORCE NON-ZERO                     GP09353
&BLACK   SETB  1                                                GP09353
         AGO   .FDLOOP
.*
.INPUT   ANOP
&F3      SETB  0
&F4      SETB  1
&INP     SETB  1                                                GP08356
         AIF   (&INT).CONFII                                    GP08356
         AGO   .FDLOOP
.*
.OUTPUT  ANOP  ,
&F3      SETB  1
&F4      SETB  0
         AGO   .FDLOOP
.*
.INTENSE ANOP  ,
&F4      SETB  1
&INT     SETB  1                                                GP08356
         AIF   (&INP).CONFII                                    GP08356
&F3      SETB  1
         AGO   .FDLOOP
.CONFII  MNOTE *,'FDLITEM: INTENSE INPUT NOT SUPPORTED'         GP08356
         AGO   .FDLOOP                                          GP08356
.*
.EXPFLD  AIF   (NOT &BLACK).EXPFLD1                             GP09353
&F4      SETB  0             FORCED INPUT MODE                  GP09353
         AIF   (NOT &F3).EXPFLD2                                GP09353
         MNOTE 4,'FDLITEM: CONFLICTING NON-DISPLAY & OUTPUT ATTR &I'
.EXPFLD1 AIF   (&F3 OR &F4).EXPFLD2
&F3      SETB  1             OUTPUT IS DEFAULT
.EXPFLD2 ANOP  ,
&LABEL   DC    B'&F0&F1&F2&F3&F4&F5&F6&F7'
&LABEL   SETC  ''
&INP     SETB  0                                                GP08356
&INT     SETB  0                                                GP08356
         AGO   .LOOP
.*
.LABEL   AIF   ('&LABEL' EQ '').LABNEW
         MNOTE 0,'FDLITEM: CONSECUTIVE LABELS &LABEL AND &C '
&LABEL   DS    0X
.LABNEW  ANOP  ,
&LABEL   SETC  '&C'(1,K'&C-1)
         AGO   .LOOP
.*
.OFFSET  ANOP  ,                                                GP09358
&D       SETC  '&C'(1,K'&C-1)                                   GP09358
&D       EQU   *-&ZZZFLAN    FIELD OFFSET (NO LEN)              GP09358
         AGO   .LOOP                                            GP09358
.*
.DONE    AIF   ('&LABEL' EQ '').MEND
&LABEL   DS    0X
         AGO   .MEND
.*
.CLOSER  AIF   ('&LABEL  ' EQ '').CLOSED
&ZZZFLAB DC    X'00'         ERROR ?
&ZZZFLAB SETC  ''
.CLOSED  AIF   ('&ZZZLEND' EQ '').MEND
&ZZZLEND DC    X'00'
&ZZZLEND SETC  ''
.*
.MEND    MEND  ,
