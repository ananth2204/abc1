         MACRO ,
&NM      PARKEYFG &TEXT,&FLAG,&OFF,&ON,&LEN=,&MIN=3
.*--------------------------------------------------------------------*
.*  THIS MACRO BUILDS ON/OFF FLAG SETTINGS FOR KEYWORD PROCESSING.    *
.*  USE IN CONJUNCTION WITH @PARSER/PARSE, AND PARKEYBX SERVICES,     *
.*  AND THE SUBVERB SUBROUTINE.                                       *
.*--------------------------------------------------------------------*
         GBLB  &ZZZOPTF
         GBLC  &ZZZOPTL
         AIF   ('&LEN' EQ '').NOLOVR
&ZZZOPTL SETC  '&LEN'
.NOLOVR  AIF   ('&ZZZOPTL' NE '').HAVELEN
&ZZZOPTL SETC  '1'
.HAVELEN AIF   (&ZZZOPTF).LATER
OFOFTXT  EQU   0,8,C'C'      VERB
OFOFMIN  EQU   8,1,C'F'      MINIMUM LENGTH (-1)
OFOFLEN  EQU   9,1,C'F'      OFF/ON DATA LENGTH (-1)
OFOFSAD  EQU  10,2,C'S'      FLAG ADDRESS S-CON
OFOFOFF  EQU  12,1,C'X'      FLAGS TO BE RESET
OFOFONN  EQU  13,1,C'X'      FLAGS TO BE SET
&ZZZOPTF SETB  1             DEFINITIONS DONE
.LATER   AIF   ('&TEXT'(1,1) EQ '''').QUOTED
&NM      DC    CL8'&TEXT',AL1(&MIN-1,&ZZZOPTL-1),SL2(&FLAG),AL(&ZZZOPTL*
               )(0-(&OFF+1),&ON+0)
         MEXIT ,
.QUOTED  ANOP  ,
&NM      DC    CL8&TEXT,AL1(&MIN-1,&ZZZOPTL-1),SL2(&FLAG),AL(&ZZZOPTL)(*
               0-(&OFF+1),&ON+0)
         MEND  ,
