         MACRO ,
&NM      FMT   &VALUE,&OLN,&SIGN=Y,&CENT=N,&GROUP=N,                   *
               &FILL=N,&LJUST=N,&SPACE=N,&TRUNC=N,&TYPE=DEC
.**********************************************************************
.*
.*  THIS MACRO INVOKES NUMERIC FORMATTING SERVICES (VIA @FORMATS)
.*
.*  FIRST POSITIONAL IS ADDRESS/NAME OF VALUE
.*  SECOND POSITIONAL USED TO SPECIFY (FORCED) OUTPUT LENGTH
.*
.*  OTHER SERVICES:    SIGN=Y - NEGATIVE # PREFIXED BY MINUS(DEFAULT)
.*     CENT=Y  - FORMAT A DECIMAL NUMBER WITH TWO PLACES WWW.PP
.*     GROUP=Y - FORMAT IN GROUPS OF THREE 123,456
.*     FILL=Y  - FILL WITH LEADING ZEROES  00012300
.*     LJUST=Y - LEFT JUSTIFY THE NUMBER, THEN ABUT TEXT
.*     SPACE=Y - ONE SPACE BETWEEN NUMBER AND TEXT
.*     TRUNC=N - OVERFLOW (***) WHEN SIGNIFICANT DIGITS LOST (CC=4)
.*
.*  EXCEPT FOR LJUST=Y, ALL OUTPUT FIELDS WILL BE THE MAXIMUM LENGTH
.*  POSSIBLE (TO MAINTAIN COMMON ALIGNMENT) DEPENDING ON THE INPUT
.*  LENGTH.  OUTPUT LENGTH INCLUDES SIGN (UNLESS SIGN=N) AND COMMAS
.*  AND PERIODS.  OR SPECIFY EXPLICIT OUTPUT LENGTH (NUM,LEN)
.*  LJUST=Y WITH SHORT LENGTH TRUNCATES ON RIGHT, ELSE LEFT.
.*
.**********************************************************************
         GBLC  &MACPLAB
&MACPLAB SETC  '&NM'
         LCLA  &EPYT,&OPTS
         LCLA  &K,&I,&J
         LCLB  &O01,&O02,&O04,&O08,&O10,&O20,&O40,&O80
         LCLC  &L
&L       SETC  'L'''
&O80     SETB  ('&SIGN' EQ 'Y' OR '&SIGN' EQ 'YES')
&O40     SETB  ('&LJUST' EQ 'Y' OR '&LJUST' EQ 'YES')
&O20     SETB  ('&SPACE' EQ 'Y' OR '&SPACE' EQ 'YES')
&O10     SETB  ('&FILL' EQ 'Y' OR '&FILL' EQ 'YES')
&O08     SETB  ('&TRUNC' EQ 'Y' OR '&TRUNC' EQ 'YES')
.*&O04     SETB  ('&O04' EQ 'Y' OR '&O04' EQ 'YES')
&O02     SETB  ('&GROUP' EQ 'Y' OR '&GROUP' EQ 'YES')
&O01     SETB  ('&CENT' EQ 'Y' OR '&CENT' EQ 'YES')
&OPTS    SETA  &O80*128+&O40*64+&O20*32+&O10*16
&OPTS    SETA  &OPTS+&O08*8+&O04*4+&O02*2+&O01
         AIF   ('&TYPE' EQ 'DEC' OR '&TYPE' EQ 'PACKED').LADD
&EPYT    SETA  1
         AIF   ('&TYPE' EQ 'ADEC' OR '&TYPE' EQ 'ABSDEC').LADD
&EPYT    SETA  2
         AIF   ('&TYPE' EQ 'INT' OR '&TYPE' EQ 'INTEGER').LADD
&EPYT    SETA  3
         AIF   ('&TYPE' EQ 'AINT' OR '&TYPE' EQ 'ABSINT').LADD
&EPYT    SETA  5
         AIF   ('&TYPE' EQ 'HEX').LADD
&EPYT    SETA  6
         AIF   ('&TYPE' EQ 'SBIN' OR '&TYPE' EQ 'SBIT').LADD
&EPYT    SETA  7
         AIF   ('&TYPE' EQ 'BIN' OR '&TYPE' EQ 'BINARY').LADD
         MNOTE 8,'INVALID NUMERIC TYPE=&TYPE '
&EPYT    SETA  4             DO AS HEX
.LADD    AIF   ('&OLN' EQ '').NOLOAD
         MACPARM R0,12,=Y(&OLN*256),OP=ICM,MODE=THREE USER'S LENGTH
         MACPARM R0,3,=Y(&EPYT*256+&OPTS),OP=ICM,MODE=THREE TYPE
         AGO   .TESTCC
.NOLOAD  MACPARM R0,&EPYT*256+&OPTS,OP=LA   LOAD FORMATTING SELECTION
.TESTCC  AIF   (N'&VALUE LT 2).NONUML
         MACPARM R1,&VALUE(1),OP=LA LOAD ADDRESS OF PACKED COUNTER
         MACPARM R0,4,=AL1(&VALUE(2)),OP=ICM,MODE=THREE
         AGO   .CMNUML
.NONUML  MACPARM R1,&VALUE,OP=LA LOAD ADDRESS OF PACKED COUNTER
         MACPARM R0,4,=AL1(&L&VALUE),OP=ICM,MODE=THREE
.CMNUML  MACPARM R15,=V(@FORMATS),OP=L
         BASR  R14,R15       CALL FORMATTING SERVICES
         MEND  ,
