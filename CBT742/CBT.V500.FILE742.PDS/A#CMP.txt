/* Rexx program to supplement the ISPF edit COMPARE command, allowing
*  the user to compare against a previous versions of an archived
*  dataset or member.
*  Usage: Install using #CONFIG.
*  The macro will just call the BUILTIN COMPARE command unless the
*  argument string is a valid version string.
*.
*. 2009/03/27 Show results of builtin compare - new ISPF edit feature.
*. 2005/08/09 Created by bsquare
*/
address ISREdit
"Macro (Parm)"
"MACRO_MSG = ON"
ZEdMsgNo = ''
if Parm = '' then signal Settings         /* Stupid ISPF! */
parse VAR Parm RV Tail
if Translate(Left(RV,1)) = 'V' & Pos('.',RV) > 0 then
   RV = SubStr(RV,2)
if \DataType(RV,'N') then "BuiltIn COMPARE" Parm
else do
   "(Vol) = VOLUME"
   "(DSN) = DATASET"
   "(Mem) = MEMBER"
   "(Changed) = DATA_CHANGED"
   ArcKey = Vol':'DSN'('Mem')'
   address ISPExec
   "TBGet archive"
   "TBTop &ArcTbl"
   if RV < 0 then do
      if DataType(RV,'W') = 0 then signal Err   /* not a whole number */
      do (Changed = 'NO') - RV
         "TBSkip &ArcTbl"
         if RC <> 0 then signal Err
      end
      RV = Vers
   end; else do
      parse VAR RV VV '.' LL
      RV = Right(VV,2,0)'.'Right(LL,2,0)
      do until Vers = RV
         "TBSkip &ArcTbl"
         if RC <> 0 then signal Err
      end
   end
   Vers = 'VERSION('Vers') USER('User') MODIFIED('Mod')'
   ViewDS = A#Make(ArcKey,Vers)
   if ViewDS = '' then signal Err
   address ISREdit "BUILTIN COMPARE" ViewDS Tail
   om = Msg('OFF')
   address TSO "Delete" ViewDS
   call Msg om
end
if ZEdMsgNo <> '' then do
   ZEdLMsg = ZEdiLMsg
   ZEdSMsg = ZEdiSMsg
   address ISPExec "SetMsg Msg(ISRZ000)"
end
exit

Err:
   ZEdLMsg = 'The archive does not contain version' RV
   ZEdSMsg = 'Not found'
   address ISPExec "SetMsg Msg(ISRZ000)"
   exit

Settings:
   address ISPExec
   "VGet (ZEdUpdOp ZUpXSiz ZUpdLab)"
   ZUpXSizT = ZUpXSiz
   ZUpdLabT = ZUpdLab
   parse VAR ZEdUpdOp RefMovr 2 AnyC 3 Mixed 4 DPPlCmt 5,
         DPPsCmt 6 DPAdCmt 7 DPACmt 8 DPFtCmt 9 DPCbCmt 10 DPBlkCl
   "AddPop"
   do until RC <> 0
      "Display Panel(ISREUPP)"
   end
   "RemPop"
   "VGet (ZVerb)"
   if ZVerb <> 'END' then exit
   ZEdUpdOp = RefMovr||AnyC||Mixed,
   || DPPlCmt||DPPsCmt||DPAdCmt||DPACmt||DPFtCmt||DPCbCmt||DPBlkCl
   ZUpXSiz = ZUpXSizT
   ZUpdLab = ZUpdLabT
   "VPut (ZEdUpdOp ZUpXSiz ZUpdLab) PROFILE"
   exit
