         PRINT NOGEN
***********************************************************************
* Title : Print ACBLIB directory in a sensible form                   *
*                                                                     *
* Submitter : Dougie Lawson                                           *
*             Consultant Systems Programmer                           *
*             IBM UK                                                  *
*                                                                     *
* Phone : 01256 341061                                                *
*                                                                     *
* JCL:-                                                               *
*                                                                     *
*    //       EXEC PGM=ACBLIST                                        *
*    //STEPLIB  DD DSN=***************,DISP=SHR                       *
*    //ACBLIB   DD DSN=********.ACBLIB,DISP=SHR                       *
*    //SYSPRINT DD SYSOUT=*,LRECL=133,BLKSIZE=1334,RECFM=VBA   @DGL01 *
*    //SYSUDUMP DD SYSOUT=*                                           *
*                                                                     *
* Amended : Neil Price  TNT Express Worldwide       29/11/1996 ** NRP *
*           - Initialise print-line to spaces                         *
*           - Removed IMS 1.2, added 3.x (not documented in ACBDIR)   *
*           - Changed DMB size calulation from MH to SLL              *
*           - Adjusted PSB size multiplier to 16 to agree with ACBGEN *
*              as per PDIRSHFT in macro DFSPDIR                       *
*           - Added storage sizes to report                           *
*           - Minor cosmetic changes incl. added comments             *
* Amended : Neil Price  TNT Express Worldwide       24/05/2001 ** NRP1*
*           - Adjusted PSB size multiplier to 64 for V7 only          *
*              as per PDIRSHFT in macro DFSPDIR                       *
*           - Removed IMS 1.3 and 2.2, added 6.1 and 7.1              *
* Amended : Dougie Lawson, IBM                      03/02/2003 @DGL01 *
*           - PHDAM, PHIDAM, PSINDEX support                   @DGL01 *
*           - removed IMS 3.1, 4.1                             @DGL01 *
*           - added IMS 8.1                                    @DGL01 *
*           - renumbered source code                           @DGL01 *
* Amended : Dougie Lawson, IBM                      09/09/2004 @DGL02 *
*           - added IMS 9.1                                    @DGL02 *
***********************************************************************
         REQUATE                                               ** NRP
ACBLIST  CSECT
ACBLIST  AMODE 31
ACBLIST  RMODE ANY
         BAKR  R14,0
         USING ACBLIST,R12
         LR    R12,R15
         STORAGE OBTAIN,LENGTH=4096,LOC=BELOW
         USING DSA,R13
         LR    R13,R1
         LA    R2,AC2LISTB
         L     R3,=A(AC2LISTL)
         LA    R4,AC3LISTB
         L     R5,=A(AC3LISTL)
         MVCL  R4,R2
         MVI   PRTLINE,C' '                                    ** NRP
         MVC   PRTLINE+1(LPRTLINE-1),PRTLINE                   ** NRP
         LA    R2,DIRJFCBA
         ST    R2,DIREXLST
         MVI   DIREXLST,X'07'
         LA    R2,DIREXLST
         STCM  R2,B'0111',DIRBLK+37
         OI    OPENLIST+4,X'80'
         OPEN  (DIRBLK,,SYSPRINT,OUTPUT),MF=(E,OPENLIST)
         TM    SYSPRINT+48,X'10'
         BO    SYSPOK
         WTO   MF=(E,MSG1)
         OI    CLSLIST,X'80'
         CLOSE (DIRBLK),MF=(E,CLSLIST)
         LA    R15,16
         PR
SYSPOK   DS    0H
         TIME  DEC
         STM   R0,R1,TIMEDATE
         UNPK  UNPKTIME,TIMEDATE
         TR    HDRDATE,UNPKTIME
         MVI   HDRANSI,C'1'
         OI    OPENLIST,X'80'
         RDJFCB (DIRBLK),MF=(E,OPENLIST)
         MVC   HDRDSN,DIRJFCBA
         PUT   SYSPRINT,HDRLINE
         PUT   SYSPRINT,TITLE
         MVI   PRTANSI,C'0'
         XC    LINECNT,LINECNT
         MVC   PRTLINE,=Y(LPRTLINE)
         SLR   R2,R2
EACHDIR  DS    0H
         GET   DIRBLK,INARE             READ A RECORD
         LA    R7,INARE+2               SKIP LENGTH NIBBL
         LA    R2,INARE
         AH    R2,INARE                 ADD THE LENGTH.
EACHMEM  DS    0H
         CLC   LASTMEM,0(R7)            HIGH VALUES? (END OF DIRECTORY)
         BE    ENDIT                    YES
         MVC   PRTNAME,0(R7)
         BAL   R9,EACHBLK
         CR    R2,R7                    IS NEXT BLOCK IN BUFFER
         BNH   EACHDIR                  NO GET NEXT DIRECTORY BLOCK
         B     EACHMEM                  YES PROCESS NEXT MEMBER
ENDIT    DS    0H
         OI    CLSLIST+4,X'80'
         CLOSE (DIRBLK,,SYSPRINT),MF=(E,CLSLIST)
         PR
EACHBLK  DS    0H
         USING ACBNTRY,R7
         UNPK  UNPADATE,ACBDATE
         MVC   PRTDATE,PATADATE
         TR    PRTDATE,UNPADATE
         MVO   WKATIME,ACBTIME
         OI    WKATIME+4,X'0F'
         UNPK  UNPATIME,WKATIME
         MVC   PRTTIME,PATATIME
         TR    PRTTIME,UNPATIME
         CLI   ACBRLVL,ACBREL91                               , @DGL02
         BE    IMS91                                          , @DGL02
         CLI   ACBRLVL,ACBREL81                               , @DGL01
         BE    IMS81                                          , @DGL01
         CLI   ACBRLVL,ACBREL71                                ** NRP1
         BE    IMS71                                           ** NRP1
         CLI   ACBRLVL,ACBREL61                                ** NRP1
         BE    IMS61                                           ** NRP1
         CLI   ACBRLVL,ACBREL51
         BE    IMS51
*        CLI   ACBRLVL,ACBREL41                               , @DGL01
*        BE    IMS41                                          , @DGL01
*        CLI   ACBRLVL,ACBREX41                               , @DGL01
*        BE    IMS41                                          , @DGL01
*        CLI   ACBRLVL,ACBREL31                               , @DGL01
*        BE    IMS31                                          , @DGL01
*        CLI   ACBRLVL,ACBREX31                               , @DGL01
*        BE    IMS31                                          , @DGL01
*        CLI   ACBRLVL,ACBREL30                               , @DGL01
*        BE    IMS31                                          , @DGL01
         MVC   PRTRELL,=C'UNK'
         B     RELDONE
IMS91    DS    0H                                             , @DGL02
         MVC   PRTRELL,=C'9.1'                                , @DGL02
         MVI   PSBMULT+1,64         Change multiplier to 64   , @DGL02
         B     RELDONE                                        , @DGL02
IMS81    DS    0H                                             , @DGL01
         MVC   PRTRELL,=C'8.1'                                , @DGL01
         MVI   PSBMULT+1,64         Change multiplier to 64   , @DGL01
         B     RELDONE                                        , @DGL01
IMS71    DS    0H                                              ** NRP1
         MVC   PRTRELL,=C'7.1'                                 ** NRP1
         MVI   PSBMULT+1,64             Change multiplier to 64** NRP1
         B     RELDONE                                         ** NRP1
IMS61    DS    0H                                              ** NRP1
         MVC   PRTRELL,=C'6.1'                                 ** NRP1
         B     RELDONE                                         ** NRP1
IMS51    DS    0H
         MVC   PRTRELL,=C'5.1'
         B     RELDONE
*MS41    DS    0H                                             , @DGL01
*        MVC   PRTRELL,=C'4.1'                                , @DGL01
*        B     RELDONE                                        , @DGL01
*MS31    DS    0H                                             , @DGL01
*        MVC   PRTRELL,=C'3.1'                                , @DGL01
RELDONE  DS    0H
         TM    ACBFLAG,ACBDMB
         BZ    PSB
         MVC   PRTTYPE,=C'DMB '
         TM    ACBFLAG,ACBMSDB
         BO    MSDB
         TM    ACBFLAG,ACBDEDB
         BO    DEDB
* Flag settings copied from V7 acbdir macro                   , @DGL01
*ACBL2FLG DS    XL1 .                                         , @DGL01
*ACB2PDB EQU   X'80'           Not sure on this flag meaning  , @DGL01
*ACBHDPD EQU   X'40'              PARTITIONED HDAM DB         , @DGL01
*ACBHIPD EQU   X'20'              PARTITIONED HIDAM DB        , @DGL01
* use ACBFLAG+1 because V5 and V6 maclib doesn't have flags   , @DGL01
         TM    ACBFLAG+1,X'40'                                , @DGL01
         BO    PHDAM                                          , @DGL01
         TM    ACBFLAG+1,X'20'                                , @DGL01
         BO    PHIDAM                                         , @DGL01
         TM    ACBFLAG+1,X'80'                                , @DGL01
         BO    PDB                                            , @DGL01
         MVC   PRTDMBT,=C'DL/1'
         B     TYPEDONE
PDB      DS    0H                                             , @DGL01
         MVC   PRTDMBT,=C'PDB '                               , @DGL01
         B     TYPEDONE                                       , @DGL01
PHDAM    DS    0H                                             , @DGL01
         MVC   PRTTYPE,=C'PHDAM'                              , @DGL01
         B     TYPEDONE                                       , @DGL01
PHIDAM   DS    0H                                             , @DGL01
         MVC   PRTTYPE,=C'PHIDAM'                             , @DGL01
         B     TYPEDONE                                       , @DGL01
MSDB     DS    0H
         MVC   PRTDMBT,=C'MSDB'
         B     TYPEDONE
DEDB     DS    0H
         MVC   PRTDMBT,=C'DEDB'
         B     TYPEDONE
PSB      DS    0H
         MVC   PRTTYPE,=C'PSB     '
TYPEDONE DS    0H
* FORMAT TTR                                                   ** NRP
         XC    WORKA1,WORKA1
         MVC   WORKA1+1,ACBTTR
         MVO   WORKA2,WORKA1
         UNPK  WORKA3,WORKA2
         NC    WORKA3,PATTERN
         TR    WORKA3,TRANTAB
         MVC   PRTTTR,WORKA3+3
         LH    R4,ACBSIZE
         TM    ACBFLAG,ACBDMB           Is it a DMB?           ** NRP1
         BZ    PSBSTOR                  No - it's a PSB        ** NRP1
* Process DMB                                                  ** NRP1
         SLL   R4,3                     Multiply by 8          ** NRP1
         CVD   R4,PACKSIZE
         UNPK  PRTSIZE,PACKSIZE+4
         OI    PRTSIZE+7,X'F0'
         MVC   PRTCSA,=CL8' '                                  ** NRP
         MVC   PRTSAS,=CL8' '                                  ** NRP
         MVC   PRTPSBW,=CL8' '                                 ** NRP
         B     PRTDONE                                         ** NRP
* Process PSB                                                  ** NRP1
PSBSTOR  DS    0H                                              ** NRP
         MH    R4,PSBMULT               Multiply by 16 or 64   ** NRP1
         CVD   R4,PACKSIZE                                     ** NRP1
         UNPK  PRTSIZE,PACKSIZE+4                              ** NRP1
         OI    PRTSIZE+7,X'F0'                                 ** NRP1
* CSA SIZE                                                     ** NRP
         LH    R5,ACBPCBS                                      ** NRP
         MH    R5,PSBMULT               Multiply by 16 or 64   ** NRP1
         CVD   R5,PACKSIZE                                     ** NRP
         UNPK  PRTCSA,PACKSIZE+4                               ** NRP
         OI    PRTCSA+7,X'F0'                                  ** NRP
* SAS SIZE                                                     ** NRP
         SR    R4,R5                                           ** NRP
         CVD   R4,PACKSIZE                                     ** NRP
         UNPK  PRTSAS,PACKSIZE+4                               ** NRP
         OI    PRTSAS+7,X'F0'                                  ** NRP
* PSBW SIZE                                                    ** NRP
         LH    R4,ACBXWA                                       ** NRP
         MH    R4,PSBMULT               Multiply by 16 or 64   ** NRP1
         CVD   R4,PACKSIZE                                     ** NRP
         UNPK  PRTPSBW,PACKSIZE+4                              ** NRP
         OI    PRTPSBW+7,X'F0'                                 ** NRP
  SPACE                                                      , @DGL01
PRTDONE  DS    0H                                              ** NRP
         BAL   R10,PRINT
         AH    R7,=Y(ACBDSLN)
         BR    R9
         DROP  R7
* PRINT Subroutine                                             ** NRP
PRINT    DS    0H
         PUT   SYSPRINT,PRTLINE
         MVI   PRTANSI,C' '
         LH    R3,LINECNT
         AH    R3,=H'1'
         STH   R3,LINECNT
         CH    R3,=H'50'
         BNL   PRINTHDR
         BR    R10
PRINTHDR DS    0H
         PUT   SYSPRINT,HDRLINE
         PUT   SYSPRINT,TITLE
         MVI   PRTANSI,C'0'
         XC    LINECNT,LINECNT
         BR    R10
  SPACE                                                      , @DGL01
PATADATE DC    X'000105020304'
PATATIME DC    X'0001080203080405090607'
PATTERN  DC    9X'0F'
TRANTAB  DC    C'0123456789ABCDEF'
LASTMEM  DC    X'FFFFFFFFFFFFFFFF'
MSG1     WTO   '*** SYSPRINT DD OPEN FAILURE, RUN ABANDONED ***',      *
               ROUTCDE=11,MF=L
TITLE    DC    Y(LTITLE),H'0'
         DC    C'-'
         DC    CL8'ACB NAME'
         DC    CL4' '
         DC    CL6' TTR  '
         DC    CL2' '
         DC    CL8'ACB-TYPE'
         DC    CL2' '
         DC    CL19' DATE     TIME'
         DC    CL2' '
         DC    CL8' SIZE '
         DC    CL2' '
         DC    CL3'REL'
         DC    CL2' '
         DC    CL8' PSBW '
         DC    CL2' '
         DC    CL8' CSA  '
         DC    CL2' '
         DC    CL8' SAS  '
LTITLE   EQU   *-TITLE
         DS    0D
AC2LISTB EQU   *
         DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=PM,RECFM=VBA
         DCB   DDNAME=ACBLIB,DSORG=PS,RECFM=U,LRECL=256,MACRF=(GM),    C
               EODAD=ENDIT
         DC    H'16'                                           ** NRP1
#DRLINE  DC    Y(L#DRLINE),H'0'
         DS    C' '
         DC    C' '
         DC    X'00011002031004050F0F0A0B110C0D0E'
         DC    C' - ACB DIRECTORY LIST FOR LIBRARY - '
         DC    CL44' '
L#DRLINE EQU   *-#DRLINE
         DC    CL15' ',C' :.'
         DC    CL5' ',C'.'
         DC    CL8' ',C':.'
AC2LISTL EQU   *-AC2LISTB
         LTORG
DSA      DSECT
         DS    18F
AC3LIST  EQU   *
TIMEDATE DS    PL8
PACKSIZE DS    2F
WKATIME  DC    CL5' '
WORKA1   DS    F
WORKA2   DS    XL5
WORKA3   DS    XL9
LINECNT  DS    H
OPENLIST OPEN  (DIRBLK,,SYSPRINT,OUTPUT),MF=L
CLSLIST  CLOSE (DIRBLK,,SYSPRINT),MF=L
DIREXLST DS    F
DIRJFCBA DS    0F,176C
         DS    0D
AC3LISTB EQU   *
SYSPRINT DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=PM,RECFM=VBA
DIRBLK   DCB   DDNAME=ACBLIB,DSORG=PS,RECFM=U,LRECL=256,MACRF=(GM),    C
               EODAD=ENDIT
PSBMULT  DC    H'16'                                           ** NRP1
HDRLINE  DC    H'0',H'0'
HDRANSI  DS    C
         DC    C' '
HDRDATE  DC    X'00011002031004050F0F0A0B110C0D0E'
         DC    C' - ACB DIRECTORY LIST FOR LIBRARY - '
HDRDSN   DC    CL44' '
UNPKTIME DC    CL15' ',C' :.'
UNPADATE DC    CL5' ',C'.'
UNPATIME DC    CL8' ',C':.'
AC3LISTL EQU   *-AC3LISTB
PRTLINE  DC    H'0',H'0'
PRTANSI  DS    C
PRTNAME  DS    CL8
         DS    CL4
PRTTTR   DS    CL6
         DS    CL2
PRTTYPE  DS    0CL8                                            ** NRP
         DS    CL4                                             ** NRP
PRTDMBT  DS    CL4
         DS    CL2
PRTDATE  DS    CL6
         DS    CL2
PRTTIME  DS    CL11
         DS    CL2
PRTSIZE  DS    CL8
         DS    CL2
PRTRELL  DS    CL3
         DS    CL2                                             ** NRP
PRTPSBW  DS    CL8                                             ** NRP
         DS    CL2                                             ** NRP
PRTCSA   DS    CL8                                             ** NRP
         DS    CL2                                             ** NRP
PRTSAS   DS    CL8                                             ** NRP
LPRTLINE EQU   *-PRTLINE
         DS    0F                                            , @DGL01
INARE    DC    256C' '
*C3LISTS EQU   *-AC3LIST
         ACBDIR DSECT=YES,PREFIX=ACB
*CBREL30 EQU   X'30'                                         , @DGL01
*CBREL31 EQU   X'31'                                         , @DGL01
*CBREX31 EQU   X'3A'                                         , @DGL01
*CBREL41 EQU   X'32'                                         , @DGL01
*CBREX41 EQU   X'3B'                                         , @DGL01
ACBREL51 EQU   X'50'
ACBREL61 EQU   X'61'                                           ** NRP1
ACBREL71 EQU   X'71'                                           ** NRP1
ACBREL81 EQU   X'81'                                         , @DGL01
ACBREL91 EQU   X'91'                                         , @DGL02
         END
