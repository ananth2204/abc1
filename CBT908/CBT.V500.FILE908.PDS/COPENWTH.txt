/* REXX NAME:  COPENWTH ----------------------------------------------*/
/* Invoke ISPF Client Server FILEXFER to download a file to           */
/*    WORKFILE.TXT in the working directory used by the OUTLOOK       */
/*    dialog, open the  working directory in an Explorer window, and  */
/*    prompt the user to select "Open With" to open the file with the */
/*    program of his choice.                                          */
/* -------------------------------------------------------------------*/
/* TRACE I */
ARG DATA_SET_NAME
'ISPEXEC CONTROL ERRORS RETURN'
ADDRESS ISPEXEC
"VGET ZWSCON SHARED"
IF ZWSCON = ' ' THEN
  DO
    SAY 'NO WORKSTATION CONNECTION.'
    SAY 'PRESS THE ENTER KEY AND INITIATE A WORKSTATION CONNECTION ...'
    SAY '  AFTER VERIFYING THAT THE ISPF WORKSTATION AGENT IS ACTIVE. '
    "SELECT PGM(ISPWSCD) PARM(WSCON,PANEL)"
    "VGET ZWSCON SHARED"
  END
IF ZWSCON = ' ' THEN
  DO
    SAY 'NO WORKSTATION CONNECTION.  AN ACTIVE WORKSTATION ...'
    SAY '  CONNECTION IS REQUIRED FOR INTERACTIVE MODE.'
    EXIT
  END

VAR1 = DATA_SET_NAME /* MAINFRAME FILE NAME */
ADDRESS ISPEXEC
"VGET (OLPATHA OLPATHB) PROFILE"
IF OLPATHA = ' ' THEN
  DO
    SAY 'THIS DIALOG USES THE WORKING DIRECTORY FOR THE OUTLOOK DIALOG.'
    SAY ' PLEASE INVOKE THE OUTLOOK DIALOG, SUPPLY A WORKING DIRECTORY,'
    SAY ' AND THEN RETRY OPENWITH.'
    EXIT
  END

/* VAR1 = "'"||DATA_SET_NAME||"'" */
VAR1 = DATA_SET_NAME
VAR3 = "WORKFILE.TXT" /* WORKSTATION FILE NAME */
/* SAY 'VAR1 = ' VAR1 */
/* SAY 'VAR3 = ' VAR3 */
IF OLPATHB > ' ' THEN                            /* PRESERVE TRAILING   */
  VAR2 = LEFT(OLPATHA,74)||OLPATHB               /* SPACES ON PATH PART */
ELSE                                             /* A WHEN PATH PART B  */
  VAR2 = OLPATHA||OLPATHB                        /* IS SUPPLIED.        */
VAR3 = VAR2||"\"||VAR3
/* 'VPUT (VAR1 VAR2)' */

'FILEXFER HOST(VAR1) WS(VAR3) TO(WS) TEXT'
IF RC /= 0 THEN
  DO
    SAY 'DOWNLOAD NOT SUCCESSFUL.  MICROSOFT EXPLORER WILL  ...'
    SAY '  NOT BE INVOKED.  FILEXFER RETURN CODE = ' RC
    EXIT
  END

'ISPEXEC SELECT WSCMD(CMD /C START EXPLORER &VAR2) INVIS'
SAY ' '
SAY 'Right click on WORKFILE.TXT in the Explorer window that has been'
SAY ' opened and select "Open with" and the program of your choice.'
EXIT
