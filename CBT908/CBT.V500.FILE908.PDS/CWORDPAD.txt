/* REXX NAME:  CWORDPAD ----------------------------------------------*/
/* INVOKE ISPF CLIENT SERVER FILEXFER TO DOWNLOAD A FILE TO           */
/*    WORKFILE.TXT IN THE DIRECTORY CONTAINING THE WSA AND THEN       */
/*    DISPLAY THE DOWNLOADED FILE IN MICROSOFT WORDPAD.               */
/* -------------------------------------------------------------------*/
/* TRACE I */
ARG DATA_SET_NAME
'ISPEXEC CONTROL ERRORS RETURN'
ADDRESS ISPEXEC
"VGET ZWSCON SHARED"
IF ZWSCON = ' ' THEN
  DO
    SAY 'NO WORKSTATION CONNECTION.'
    SAY 'PRESS THE ENTER KEY AND INITIATE A WORKSTATION CONNECTION ...'
    SAY '  AFTER VERIFYING THAT THE ISPF WORKSTATION AGENT IS ACTIVE. '
    "SELECT PGM(ISPWSCD) PARM(WSCON,PANEL)"
    "VGET ZWSCON SHARED"
  END
IF ZWSCON = ' ' THEN
  DO
    SAY 'NO WORKSTATION CONNECTION.  AN ACTIVE WORKSTATION ...'
    SAY '  CONNECTION IS REQUIRED FOR INTERACTIVE MODE.'
    EXIT
  END

VAR1 = DATA_SET_NAME /* MAINFRAME FILE NAME */
VAR2 = WORKFILE.TXT  /* WORKSTATION FILE NAME */
'FILEXFER HOST(VAR1) WS(VAR2) TO(WS) TEXT'
IF RC /= 0 THEN
  DO
    SAY 'DOWNLOAD NOT SUCCESSFUL.  MICROSOFT WORDPAD WILL  ...'
    SAY '  NOT BE INVOKED.  FILEXFER RETURN CODE = ' RC
    EXIT
  END

'ISPEXEC SELECT WSCMD(CMD /C START WRITE &VAR2) INVIS'
SAY 'THE FILE SHOULD APPEAR ON YOUR PC WORKSTATION WITHIN A FEW ...'
SAY '   SECONDS. IF NOT, PRESS THE ENTER KEY AND CHECK FOR PROMPTS.'
EXIT
