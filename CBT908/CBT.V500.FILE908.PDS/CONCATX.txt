/************************ rexx **************************************/
/* CONCATX  * This will concatenate a passed data set name to a     */
/************ passed DDNAME.                                        */
/* The exec will reallocate the DDNAME placing the passed data set  */
/* first.  LIBDEFF will be used if the DDNAME begins with ISP and a */
/* ISPF environment exists.                                         */
/*                                                                  */
/*  Jim Halley                                                      */
/*  modified to eliminate dups                                      */
/*  modified to allow a dataset or ddname to be entered if invalid  */
/*                                                                  */
/* Parms can be passed when calling or the user will be prompted.   */
/*                                                                  */
/* 'CONCATX ddname dataset.name'   or  'CONCATX'   <- will prompt.  */
/********************************************************************/

Trace Off
Address TSO
/* trace i */
/* trace o */
Parse Upper Arg ddname daname .           /* get the parm info */
say ddname
say daname

If SYSDSN(daname) <> 'OK' Then
  Do While SYSDSN(daname) <> 'OK'
    Say 'Please enter the DATASET name to be concatenated or end'
    Parse Upper Pull daname
    if daname= 'END' then
      exit
  End
If ddname = '' Then
  Do While ddname = ''
    Say 'Please enter the DDNAME to concatenated to.'
    Parse Upper Pull ddname
    if ddname = 'END' then
      exit
  End

If Substr(daname,1,1) <> "'" Then                /* add id and "'"s */
  daname = "'" || Userid()"." || daname || "'"
dsns = daname
dsno = daname

x = OUTTRAP(ln.,'*','NOCONCAT')
'LISTA STATUS'

Do i = 2 To ln.0                       /* loop through LISTA output */
  Parse Var ln.i dsn ddn .             /* look for our ddname       */
  If ddn = '' Then
    Do
      i = i + 1
      Parse Var ln.i 3 ddn 12 disp .
      If ddn <> '' Then curddn = ddn    /* found new DD */
    End
  Else
    curddn = ddn
  If curddn = ddname Then
    if dsno<> "'"dsn"'" Then
      dsns = dsns "'"dsn"'"           /* build dsn list */
End i

If Substr(ddname,1,3) <> 'ISP' Then
  'ALLOC DD('ddname') DA('dsns') SHR REUSE'         /* regular TSO  */
Else
  If Sysvar(SYSISPF) = 'ACTIVE' Then
    Address ISPEXEC 'LIBDEF' ddname 'DATASET ID('daname')'
  Else
    Do
     'ALLOC DD('ddname') DA('dsns') SHR REUSE'
    End
EXIT
