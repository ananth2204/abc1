/* REXX name:  EMAIL$  -----------------------------------------------*/
/* Invoke ISPF Client Server FILEXFER to download file interactively  */
/* and then attach it to a new email.                                 */
/* -------------------------------------------------------------------*/
/* TRACE I */
ADDRESS ISPEXEC
"VGET ZWSCON SHARED"

IF ZWSCON = ' ' THEN
  DO
    SAY 'No workstation connection.'
    SAY 'Press the ENTER key and initiate a workstation connection ...'
    SAY '  after verifying that the ISPF Workstation Agent is active. '
    "SELECT PGM(ISPWSCD) PARM(WSCON,PANEL)"
    "VGET ZWSCON SHARED"
  END

IF ZWSCON = ' ' THEN
  DO
    SAY 'No workstation connection.  An active workstation ...'
    SAY '  connection is required for Interactive mode.'
    EXIT
  END
ADDRESS ISPEXEC
"VGET (OLMFDSN OLPATHA OLPATHB OLWSWRK OLTXT) PROFILE"
VAR1 = "'"||OLMFDSN||"'"
IF OLPATHB > ' ' THEN                            /* PRESERVE TRAILING   */
  VAR2 = LEFT(OLPATHA,74)||OLPATHB||"\"||OLWSWRK /* SPACES ON PATH PART */
ELSE                                             /* A WHEN PATH PART B  */
  VAR2 = OLPATHA||OLPATHB||"\"||OLWSWRK          /* IS SUPPLIED.        */
'VPUT (VAR1 VAR2)'
IF OLTXT = T THEN
  'FILEXFER HOST(VAR1) WS(VAR2) TO(WS) TEXT'
ELSE
  'FILEXFER HOST(VAR1) WS(VAR2) TO(WS) BINARY'
IF RC /= 0 THEN
  DO
    SAY 'DOWNLOAD NOT SUCCESSFUL.  EMAIL WILL ...'
    SAY '  NOT BE INVOKED.  FILEXFER RETURN CODE = ' RC
    EXIT
  END
/* Customize below for your particular email product */
VAR2 = '"'||VAR2||'"'  /* Outlook requires quotation marks */
'ISPEXEC SELECT WSCMD(CMD /C START OUTLOOK /a &VAR2) INVIS'
say 'The file has been attached to a new email.'
OLPATHX = VAR2
"VPUT (OLPATHX) PROFILE"
/* 'ISPEXEC SELECT WSCMD(CMD /C DEL &OLPATHX) INVIS' */
