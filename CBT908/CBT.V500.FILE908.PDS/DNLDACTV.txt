/* REXX name:  DNLDACTV ----------------------------------------------*/
/* Invoke ISPF Client Server FILEXFER to download file interactively  */
/* -------------------------------------------------------------------*/
/* TRACE I */
ADDRESS ISPEXEC
"VGET ZWSCON SHARED"

IF ZWSCON = ' ' THEN
  DO
    SAY 'No workstation connection.'
    SAY 'Press the ENTER key and initiate a workstation connection ...'
    SAY '  after verifying that the ISPF Workstation Agent is active. '
    "SELECT PGM(ISPWSCD) PARM(WSCON,PANEL)"
    "VGET ZWSCON SHARED"
  END

IF ZWSCON = ' ' THEN
  DO
    SAY 'No workstation connection.  An active workstation ...'
    SAY '  connection is required for Interactive mode.'
    EXIT
  END
ADDRESS ISPEXEC
"VGET (DLMFDSN DLPATHA DLPATHB DLWSWRK DLTXT) PROFILE"
VAR1 = "'"||DLMFDSN||"'"
IF DLPATHB > ' ' THEN                            /* PRESERVE TRAILING   */
  VAR2 = LEFT(DLPATHA,74)||DLPATHB||"\"||DLWSWRK /* SPACES ON PATH PART */
ELSE                                             /* A WHEN PATH PART B  */
  VAR2 = DLPATHA||DLPATHB||"\"||DLWSWRK          /* IS SUPPLIED.        */
'VPUT (VAR1 VAR2)'
IF DLTXT = T THEN
  'FILEXFER HOST(VAR1) WS(VAR2) TO(WS) TEXT'
ELSE
  'FILEXFER HOST(VAR1) WS(VAR2) TO(WS) BINARY'
say 'Download process has completed. '
