/* REXX name:  UPLDBTCH                                               */
/* Invoke ISPF Client Server FILEXFER to upload file/member in batch  */
/* -------------------------------------------------------------------*/
/* TRACE I */
ADDRESS ISPEXEC
"VGET (DLMFDSN DLPATHA DLPATHB DLWSDSN DLTXT DLAUTO) PROFILE"
"VGET (ZGUITCP)" /* GET WORKSTATION IP ADDRESS FROM WSCON */
ADDRESS TSO
IP = ZGUITCP
DYYMMDD = DATE('O') /* GET CURRENT DATE IN YY/MM/DD FORMAT */
DYYMMDD = SUBSTR(DYYMMDD,1,2)||SUBSTR(DYYMMDD,4,2)||SUBSTR(DYYMMDD,7,2)
DYYMMDD = "D"||DYYMMDD
THHMMSS = TIME() /* GET CURRENT TIME IN HH:MM:SS FORMAT */
THHMMSS = SUBSTR(THHMMSS,1,2)||SUBSTR(THHMMSS,4,2)||SUBSTR(THHMMSS,7,2)
THHMMSS = "T"||THHMMSS
USEID = SYSVAR(SYSUID) /* GET THE USER'S USER ID */
SYSPFX = SYSVAR(SYSPREF) /* GET THE SYSTEM PREFIX  */
/* CONSTRUCT A UNIQUE NAME FOR THE FILEXFER REXX PDS */
IF USEID = SYSPFX THEN
  CTLDSN = USEID||".UPLOAD."||DYYMMDD||"."||THHMMSS
ELSE
  CTLDSN = SYSPFX||"."||USEID||".UPLOAD."||DYYMMDD||"."||THHMMSS
REXXPDS = CTLDSN /* SAVE THE GENERATED FILE NAME TO USE IN JCL */
CTLDSN = "'"||CTLDSN||"'"
DLWSWRK = DLWSDSN
/* -- check for existance of mainframe target file */
DSN = DLMFDSN
parse var DSN DSN '(' MBR
DSN = "'"||DSN||"'"
 if sysdsn(dsn) <> "OK" then
    do
      say "The target mainframe file does not exist ..."
      say " ... "
      say "Copy and paste" DSN
      say "  into Other Data Set on the following Data Set Utility"
      say "  screen and select primary option "A" to allocate your file."
      say " ... "
      SAY "Supply appropriate data set attributes and then press  "
      SAY "  PF3 after your file is allocated to continue the upload"
      SAY "  process."
      say " ... "
      SAY "Press the Enter key to continue to the Data Set Utility panel."
      ADDRESS ISPEXEC
      "SELECT PGM(ISRUDA) PARM(ISRUDA2) SCRNAME(DSUTIL)"
    END
/* -- end of check for existance of mainframe target file */
MF = '"'||"'"||DLMFDSN||"'"||'"'
DLPATHA = "'"||DLPATHA||"'"
IF DLPATHB > ' ' THEN
  DLPATHB = "'"||DLPATHB||"'"
DLWSWRK = "'"||DLWSWRK||"'"
IF1 = "IF DLPATHB > ' ' THEN"
IF2 = "  PC = LEFT(DLPATHA,74)||DLPATHB||'\'||DLWSWRK"
IF3 = "ELSE"
IF4 = "  PC = DLPATHA||DLPATHB||'\'||DLWSWRK"
IF DLTXT = T THEN
DLCMD = '"'||"ISPEXEC FILEXFER HOST(MF) WS(PC) TO(HOST) TEXT"||'"'
ELSE
DLCMD = '"'||"ISPEXEC FILEXFER HOST(MF) WS(PC) TO(HOST) BINARY"||'"'
CTL. = ''
CTL.1 = "/* REXX */"
CTL.2 = "MF =" MF
CTL.3 = "A =" DLPATHA
CTL.4 = "B =" DLPATHB
CTL.5 = "C =" DLWSWRK
CTL.6 = "DLPATHA = A"
CTL.7 = "DLPATHB = B"
CTL.8 = "DLWSWRK = C"
CTL.9 = IF1
CTL.10 = IF2
CTL.11 = IF3
CTL.12 = IF4
CTL.13 = "IP =" IP
CTL.14 = "'"'ISPEXEC WSCON IP(IP) CODEPAGE(0037) CHARSET(0697) NOGUIDSP'"'"
CTL.15 = "SAY '"'WSCON    RETURN CODE: '"'  RC"
CTL.16 = DLCMD
CTL.17 = "SAY '"'FILEXFER RETURN CODE: '"'  RC"
CTL.18 = "ISPEXEC WSDISCON"
ADDRESS TSO
/* ALLOCATE A PDS TO HOLD THE GENERATED REXX EXEC */
"ALLOC F(DATAOUT) DA("CTLDSN") NEW CATALOG LRECL(80)
 BLKSIZE(8000) RECFM(F B) DSORG(PO) TRACKS SPACE(1 1) DIR(1)"
IF RC = 0 THEN      /* IF FREE  WAS SUCCESSFUL */
      DO
        NOP
      END
    ELSE
      DO
        SAY "ERROR DURING ALLOCATE OF PDS " RC
      END
"FREE F(DATAOUT)"
IF RC = 0 THEN      /* IF FREE  WAS SUCCESSFUL */
      DO
        NOP
      END
    ELSE
      DO
        SAY "ERROR DURING FREE OF PDS " RC
      END
/* CONSTRUCT ALLOCATION STATEMENT FOR THE PDS MEMBER CREATED ABOVE */
MEMNAME = FILEXFER
XALL = '"ALLOC DA('
XALL = XALL || ''''
XALL = XALL || REXXPDS
XALL = XALL || '('
XALL = XALL || STRIP(MEMNAME)
XALL = XALL || ')'
XALL = XALL || ''''
XALL = XALL || ') F(FILEXFER) OLD"'
INTERPRET XALL /* EXECUTE THE ALLOCATE COMMAND */

IF RC = 0 THEN /* IF OPEN OF PDS MEMBER WAS SUCCESSFUL  */
      DO
        NOP
      END
    ELSE
      DO
        SAY "ERROR DURING ALLOCATE OF PDS MEMBER " RC
      END

/* WRITE THE FILEXFER GENERATED REXX STATEMENTS TO THE PDS MEMBER */
"EXECIO * DISKW FILEXFER (STEM CTL. FINIS"

IF RC = 0 THEN      /* IF WRITE WAS SUCCESSFUL         */
      DO
        NOP
      END
    ELSE
      DO
        SAY "ERROR DURING 1ST EXECIO ... DISKW, RETURN CODE IS " RC
      END

"FREE F(FILEXFER)"
IF RC = 0 THEN      /* IF FREE  WAS SUCCESSFUL         */
      DO
        NOP
      END
    ELSE
      DO
        SAY "ERROR DURING FREE OF PDS " RC
      END

IF DLAUTO = N THEN DO /* BEGINNING OF MANUAL SUBMIT LOGIC */
ADDRESS ISPEXEC
"FTOPEN TEMP"
"FTINCL FILEXFER"
"FTCLOSE"
"VGET ZTEMPF"
"EDIT DATASET('"ZTEMPF"') "      /* SHOW JCL ON ISPF EDIT PANEL     */
END
IF DLAUTO = Y THEN DO /* BEGINNING OF AUTOMATIC SUBMIT LOGIC */
ADDRESS ISPEXEC
"FTOPEN TEMP"
"FTINCL FILEXFER"
"FTCLOSE"
"VGET ZTEMPF"
ADDRESS TSO "SUBMIT '"ZTEMPF"'"    /* SUBMIT THE JCL           */
END /* end of automatic submit logic */
EXIT 0
