/* REXX */
/* Program  Name..: GOOGLE                                           */
/* Original Author: Jim Halley                                       */
/* Original Date..: April 4, 2012                                    */
/* Program Purpose: Invoke Google search with or without a search    */
/*                  argument; optionally get search argument from    */
/*                  the "word" (delimited by spaces) cursor is on.   */
/* The ISPF Work Station Agent must be installed and configured.     */
/*-------------------------------------------------------------------*/
/* Maintenance Log:                                                  */
/*-------------------------------------------------------------------*/
/*                                                                   */
/* DATE       PROGRAMMER   COMMENT                                   */
/* ----       ----------   -------                                   */
/*                                                                   */
/* 10-15-2012 J. Halley    Add "INVIS" for Windows 7 compatibility   */
/* 08-09-2012 J. Halley    Add optional cursor sensitivity; e.g.     */
/*                         Google an error code like with QuickRef.  */
/* 04-13-2012 J. Halley    With a nod to Rick Silver.                */
/*                                                                   */
/*-------------------------------------------------------------------*/
/* Trace Options  'O'  = Off                                         */
/*                '?I' = Interactive Debug                           */
/*                'A'  = Display all clauses before execution        */
/*-------------------------------------------------------------------*/
trace('O')
'ISPEXEC CONTROL ERRORS RETURN'

Address ispexec
'VGET (ZSCREENI,ZSCREENC,ZENVIR)' /* Extract screen image,
                                     cursor pos and ISPF level  */
If substr(zenvir,5,4) <4.5 Then
  Call get_zscreen_values
If substr(zscreeni,zscreenc+1,1) <> ' ' Then
  Do  /* Extract search argument from screen */
    name=word(substr(zscreeni,1+lastpos(' ',zscreeni,zscreenc)),1)
    name=translate(strip(substr(name,1,42)))
    srcharg = name /* Why capture only the first 42 characters? */
  End
ELSE
  Parse Arg srcharg
  IF srcharg = '' THEN
    DO
      SAY 'Enter optional search argument or just press Enter'
      PULL srcharg
    END

ADDRESS ISPEXEC
"VGET ZWSCON SHARED"

IF ZWSCON = ' ' THEN
  DO
    SAY 'No workstation connection.'
    SAY 'Press the ENTER key and initiate a workstation connection ...'
    SAY '  after verifying that the ISPF Workstation Agent is active. '
    "SELECT PGM(ISPWSCD) PARM(WSCON,PANEL)"
    "VGET ZWSCON SHARED"
  END

IF ZWSCON = ' ' THEN
  DO
    SAY 'No workstation connection.  An active workstation ...'
    SAY '  connection is required for Interactive mode.'
    EXIT
  END

hplink1='http://www.google.com/search?q='
hplink2=srcharg

http=hplink1||hplink2
http=strip(http,b)

 "ISPEXEC SELECT WSCMD(CMD /C START IEXPLORE "http") INVIS"
Return
