VSAM utility programs

The following two programs were developed because we
needed to set a return code that depended on the status of a file.
The first program, VSMP01, is used to set a return code of 0 if
the file (normally a VSAM space managed work file) contains
any records, and a return code of 8 if the file is empty.  The
execution of our jobstream can now be based on this return
code.  Prior to this program and VSE's conditional JCL, an
operator would need to watch for VSAM open errors and/or
program abends to determine the correct job flow.

The second program, VSMP02, opens a VSAM file for output.
If it cannot open the file a return code of 8 is generated.  If the
file can be opened, a 0 return code is generated.  This program
is useful to determine if a file is currently open by another job,
for example CICS.  The file to be tested must be defined with
shareoption 3 and have a corresponding DLBL in the jobstream
or in standard labels.  The filename of the cluster must be
supplied to this program using the PARM function of the //
EXEC statement.


VSMP01

* THIS PROGRAM WILL READ CATALOG INFO TO DETERMINE IF ANY RECORDS
* EXIST FOR THE ASSOCIATED FILE.  THE ONLY REQUIREMENT TO EXECUTE IS
* A DLBL FOR FILENAME... "VSMP01" WITH THE CORRESPONDING FILE-ID OF
* THE FILE TO BE TESTED.
* USED ENDRBA WITH TESTCB BECAUSE VSAM SPACE MANAGER
* NEVER APPEARS TO UPDATE NLOGR CATALOG FIELD... ALWAYS 0.
VSAMCHK  START 0
         COPY  EQUREG
         BALR  3,0
         USING *,3
         OPEN  CONSOLE
         OPEN  BLOCK
         LA    R7,OPERR            LOAD ADDR OF OPEN ERROR MESSAGE
         LA    R5,0                SET RETURN CODE = 0
         LTR   R15,R15             GOOD OPEN?
         BNZ   END                 NO... END
         TESTCB ACB=BLOCK,AM=VSAM,                                     C
               ENDRBA=0,                                               C
               ERET=END
         BE    GOTNUN              IF CC = 0 THEN WE HAVE NO DATA
GOTSUM   LA    R5,0                SET RC = 0
         LA    R7,DATAFND          LOAD ADDR OF DATA FOUND MSG
         B     END                 YES.. EXIT WITH RC = 0
GOTNUN   LA    R7,NODATA           NO.. LOAD ADDR OF NO RECS MESSAGE
         LA    R5,8                     AND SET RETURN CODE = 8
END      EQU   *
         CLOSE BLOCK
         MVC   CONWRK,0(R7)
         PUT   CONSOLE,CONWRK      DISPLAY CONSOLE MESSAGE
         CLOSE CONSOLE
         LR    R15,R5              MOVE IN RETURN CODE
         EOJ   RC=(15)             END OF JOB.. SET RETURN CODE
OPERR    DC    CL80'VSMP01 - VSAM DATA SET OPEN ERROR    - NO ACTION'
SHOWERR  DC    CL80'VSMP01 - VSAM DATA SET SHOWCB ERROR  - NO ACTION'
DATAFND  DC    CL80'VSMP01 - VSAM DATA SET CONTAINS DATA - NO ACTION'
NODATA   DS    0CL80
         DC    CL09'VSMP01 - '
         DC    CL71'VSAM DATA SET CONTAINS 0 RECORDS - RC=8'
DISPLAY  DS    0D
NLOGR    DS    F
         DS    F
CONSOLE  DTFCN DEVADDR=SYSLOG,                                         C
               IOAREA1=CONSIO,                                         C
               WORKA=YES
CONSIO   DS    CL80
CONWRK   DS    CL80
BLOCK    ACB   DDNAME=VSMP01,                                          C
               MACRF=(ADR,SEQ,OUT)
LIST     RPL   ACB=BLOCK
         END   VSAMCHK
/*

Example JCL

// DLBL VSMP01,'PROD.VSAM.MYFILE',,VSAM,CAT=USRCAT1
// EXEC VSMP01,SIZE=VSMP01
/* A RETURN CODE OF 0 IMPLIES RECORDS ARE PRESENT
// IF $RC NE 0 THEN
// GOTO STEP2
/. STEP1
.
.
/. STEP2
.


VSMP01

* THIS PROGRAM WILL OPEN A VSAM FILE FOR OUTPUT.  IF AN OPEN ERROR
* OCCURS IT PLACES A VALUE OF '08' INTO REGISTER 15.  (RC = 08)
* A PARM='FILENAME' IS REQUIRED ON THE // EXEC CARD
* ASSOCIATED DLBL FOR FILENAME MUST BE ACCESSIBLE.
VSMP02   START 0
         COPY  EQUREG
         BALR  3,0
         USING *,3
         COMRG
         ST    R6,PARMSAVE
         USING PRMDSECT,R6
         MVC   FNAME,PARMDATA
         TR    FNAME,TABLE
         MODCB ACB=BLOCK,DDNAME=(*,FNAME)
* OPEN ACB
         OPEN  CONSOLE
         OPEN  BLOCK
         LA    R7,OPGOOD           LOAD ADDR OF OPEN MESSAGE
         LA    R5,0                SET RETURN CODE = 0
         LTR   R15,R15             GOOD OPEN?
         BZ    END                 NO... GET OUT OF TOWN
         LA    R7,OPERR            LOAD ADDR OF OPEN ERROR MESSAGE
         LA    R5,8                SET RC = 8 (OPEN ERROR)
END      EQU   *
         CLOSE BLOCK
         MVC   CONWRK,0(R7)
         PUT   CONSOLE,CONWRK      DISPLAY CONSOLE MESSAGE
         CLOSE CONSOLE
         LR    R15,R5              MOVE IN RETURN CODE
         EOJ   RC=(15)             END OF JOB.. SET RETURN CODE
OPGOOD   DC    CL80'VSMP02 - VSAM DATA SET CLOSED        - RC = 00  '
OPERR    DC    CL80'VSMP02 - VSAM DATA SET OPEN ERROR    - RC = 08  '
TABLE    DC    256X'40'
         ORG   TABLE+X'C1'
         DC    X'C1C2C3C4C5C6C7C8C9'
         ORG   TABLE+X'D1'
         DC    X'D1D2D3D4D5D6D7D8D9'
         ORG   TABLE+X'E2'
         DC    X'E2E3E4E5E6E7E8E9'
         ORG   TABLE+X'F0'
         DC    X'F0F1F2F3F4F5F6F7F8F9'
         ORG   TABLE+256
DISPLAY  DS    0D
NLOGR    DS    F
PARMSAVE DC    F'0'
FNAME    DC    CL8'        '
         DS    F
CONSOLE  DTFCN DEVADDR=SYSLOG,                                         C
               IOAREA1=CONSIO,                                         C
               WORKA=YES
CONSIO   DS    CL80
CONWRK   DS    CL80
BLOCK    ACB   DDNAME=VSMP02,                                          C
               MACRF=(ADR,SEQ,OUT)
LIST     RPL   ACB=BLOCK
PRMDSECT DSECT
PARMADDR DS    F
PARMLEN  DS    H
PARMDATA DS    CL8
VSMP02   CSECT ,
         END   VSMP02

Example JCL

// DLBL MYFILE,'PROD.VSAM.MYFILE',,VSAM,CAT=USRCAT1
.
// EXEC VSMP02,PARM='MYFILE'
// IF $RC EQ 0 THEN
// GOTO NOTOPEN
// PAUSE MYFILE IS OPEN IN ANOTHER PARTITION (CHECK CICS).
/. NOTOPEN
.
.
.

Mark Johnson
Systems Programmer (USA)

