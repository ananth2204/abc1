Calculating read hit and write hit ratios

When the 3990-3 cache controller was first installed, we could
not easily see the read hit ratios (and subsequently write hit
figures) for the various devices. After giving some thought to
the problem, I decided to make use of the IDCAMS program
that lists all the counts. Our peak hour is 10:30 to 11:30, which
was the period that I wanted to monitor.

The first step was to collect the data. This was achieved by
writing a program to read the output from IDCAMS, and
extract and store the data. The program is automatically
triggered at 10:30 and 11:30, with the output being written to
separate datasets.

The second step was to process the two output files,
performing calculations to work out the appropriate ratios, and
write the results to an output file, which was then stored on a
SAS dataset for subsequent analysis.

The JCL for the run at the start of the period is given below.

//job card
//STEP1 EXEC PGM=IDCAMS
//SYSPRINT DD DSN=&&FRODO,UNIT=3390,SPACE=(TRK,(2,2),RLSE),
// DISP=(,PASS),
// DCB=(RECFM=FBA,LRECL=133,BLKSIZE=1330)
//SYSIN    DD *
   LISTDATA-
   COUNTS-
   VOLUME(volser)-
   ALL-
   UNIT(3390)
/*
//CACHE  EXEC PGM=TECJL40
//STEPLIB DD loadlib
//SYSUDUMP DD SYSOUT=X
//SYSPRINT DD SYSOUT=X
//SYSOUT   DD SYSOUT=X
//PRINT23  DD DSN=start dataset,DISP=OLD
//CARD01   DD DSN=&&FRODO,DISP=OLD
// DCB=(RECFM=FBA,LRECL=133,BLKSIZE=1330)

The JCL for the run at the end of the period is shown below,
and includes the calculation and SAS steps.

//job card
//STEP1 EXEC PGM=IDCAMS
//SYSPRINT DD DSN=&&FRODO,UNIT=WORK,SPACE=(TRK,(2,2),RLSE),
// DISP=(,PASS),
// DCB=(RECFM=FBA,LRECL=133,BLKSIZE=1330)
//SYSIN    DD *
   LISTDATA-
   COUNTS
   VOLUME(volser)-
   ALL-
   UNIT(3390)
/*
//CACHE   EXEC PGM=TECJL40
//STEPLIB  DD loadlib
//SYSUDUMP DD SYSOUT=X
//SYSPRINT DD SYSOUT=*
//SYSOUT   DD SYSOUT=*
//PRINT23  DD DSN=end datset,DISP=OLD
//CARD01   DD DSN=&&FRODO,DISP=OLD
//CACHEP  EXEC PGM=TECJL41
//STEPLIB  DD loadlib
//SYSUDUMP DD SYSOUT=X
//SYSPRINT DD SYSOUT=X
//SYSOUT   DD SYSOUT=X
//PRINT23  DD SYSOUT=8
//PRINT26  DD SYSOUT=8
//PRINT25  DD DISP=(,PASS),DSN=&&BILBO,
// SPACE=(TRK,(5,5),RLSE),UNIT=WORK,
// DCB=(RECFM=FB,LRECL=69,BLKSIZE=6900)
//CARD01 DD  DISP=OLD,DSN=start dataset
//CARD02 DD  DISP=OLD,DSN=end dataset
//SASSTEP  EXEC SAS,OPTIONS='MARCO NODSNFERR'
//WORK     DD UNIT=WORK,SPACE=(CYL,(5,5))
//FT11F001 DD SYSOUT=*
//FT12F001 DD SYSOUT=*
//FT13F001 DD SYSOUT=*
//FT14F001 DD SYSOUT=*
//FT15F001 DD SYSOUT=*
//FT16F001 DD SYSOUT=*
//FT17F001 DD SYSOUT=*
//FT18F001 DD SYSOUT=*
//SORTWK01 DD SPACE=(CYL,(&SORT)),UNIT=WORK
//SORTWK02 DD SPACE=(CYL,(&SORT)),UNIT=WORK
//SORTWK03 DD SPACE=(CYL,(&SORT)),UNIT=WORK
//SASE     DD DISP=OLD,DSN=sas data file
//SASD     DD DISP=OLD,DSN=&&BILBO
//SASLIB   DD DISP=SHR,DSN=saslib
//SOURCLIB DD DISP=SHR,DSN=sas source lib
//SYSIN    DD DSN=sas source pds

The extract program source was written in COBOL.

 IDENTIFICATION DIVISION.
*************************
 PROGRAM-ID                 TECJL40.
 AUTHOR.                    JOHN LARRY.
 REMARKS                    READ IDCAMS OUTPUT FOR CACHE DATA.
 ENVIRONMENT DIVISION.
**********************
 CONFIGURATION SECTION.
 SPECIAL-NAMES.
     C01 IS TO-TOP-OF-PAGE.
 INPUT-OUTPUT SECTION.
 FILE-CONTROL.
     SELECT B100-OUTFILE ASSIGN TO PRINT23.
     SELECT A100-CARDFILE  ASSIGN TO CARD01.
 DATA DIVISION.
***************
 FILE SECTION.
 FD  B100-OUTFILE
     LABEL RECORDS ARE STANDARD
     BLOCK CONTAINS 0 RECORDS.
 01  B1-PRINTLINE1.
     05 B1-DISK.
        10 B1-X       PIC X.
        10 FILLER     PIC XX.
        10 B1-DATE.
           15 B1-DATE-DD PIC XX.
           15 B1-DATE-S1 PIC X.
           15 B1-DATE-MM PIC XX.
           15 B1-DATE-S2 PIC X.
           15 B1-DATE-YY PIC XX.
     05 FILLER        PIC XX.
     05 B1-TOTREAD    PIC X(8).
     05 FILLER        PIC X.
     05 B1-TOTCACH    PIC X(8).
     05 FILLER        PIC X.
     05 B1-TOTWRIT    PIC X(8).
     05 B1-ACTIVE     PIC X(11).
     05 B1-TOT-FW     PIC X(8).
     05 B1-FW-ACTIVE  PIC X(11).
     05 FILLER        PIC X(11).
 FD  A100-CARDFILE
     BLOCK CONTAINS 0 RECORDS
     LABEL RECORDS ARE STANDARD.
 01  A1-CARD-IN.
     05 FILLER        PIC X.
     05 A1-IDCAMS     PIC X(6).
     05 FILLER        PIC X(82).
     05 A1-DATE.
        10 A1-DATE-MM PIC XX.
        10 A1-DATE-S1 PIC X.
        10 A1-DATE-DD PIC XX.
        10 A1-DATE-S2 PIC X.
        10 A1-DATE-YY PIC XX.
     05 FILLER        PIC X(30).
 01  A2-CARD-IN.
     05 FILLER        PIC X.
     05 FILLER        PIC X(20).
     05 A2-VOLUME-LIT PIC X(6).
     05 FILLER        PIC X.
     05 A2-VOLUME     PIC X(6).
     05 FILLER        PIC X(14).
     05 A2-VOLUME-ID  PIC X(2).
     05 FILLER        PIC X(83).
 01  A3-CARD-IN.
     05 FILLER        PIC X.
     05 A3-TOTALS     PIC X(6).
     05 FILLER        PIC X(15).
     05 A3-TOT-READ   PIC X(8).
     05 FILLER        PIC X(4).
     05 A3-TOT-CREAD  PIC X(8).
     05 FILLER        PIC X(4).
     05 A3-TOT-WRITE  PIC X(8).
     05 FILLER        PIC X(4).
     05 A3-DASD-FW    PIC X(8).
     05 FILLER        PIC X(67).
 01  A4-CARD-IN.
     05 FILLER        PIC X(109).
 01  A5-CARD-IN.
     05 FILLER        PIC X(19).
     05 A5-CACH       PIC X(8).
     05 FILLER        PIC X(9).
     05 A5-ACTIVE     PIC X(11).
     05 FILLER        PIC X(62).
 01  A6-CARD-IN.
     05 FILLER        PIC X(19).
     05 A6-DFW        PIC X(16).
     05 FILLER        PIC X(1).
     05 A6-ACTIVE     PIC X(11).
     05 FILLER        PIC X(62).
 WORKING-STORAGE SECTION.
*************************
 77  W-VERSION    PIC X(16)     VALUE  "PROGRAM TECJL40".
 77  W-X                  PIC 9         VALUE 0.
 01  W-DISK-ID.
     05 W-VOLUME          PIC X(6).
     05 FILLER            PIC XX        VALUE " 7".
     05 W-VOLUME-ID       PIC XX.
     05 FILLER            PIC X         VALUE " ".
     EJECT
 PROCEDURE DIVISION.
********************
 010M-MAINLINE SECTION.
***********************
     OPEN INPUT  A100-CARDFILE.
     OPEN OUTPUT B100-OUTFILE.
     MOVE SPACES TO B1-PRINTLINE1.
     MOVE 0 TO W-X.
     PERFORM 030D-READ.
     CLOSE A100-CARDFILE
           B100-OUTFILE.
     STOP RUN.
 010M-EXIT.
     EXIT.
 030D-READ SECTION.
     READ A100-CARDFILE
       AT END
       GO TO 030D-READ-EXIT.
     IF A1-IDCAMS = "IDCAMS"
        IF W-X = 0
            MOVE 1 TO W-X
            MOVE A1-DATE-DD TO B1-DATE-DD
            MOVE A1-DATE-MM TO B1-DATE-MM
            MOVE A1-DATE-YY TO B1-DATE-YY
            MOVE A1-DATE-S1 TO B1-DATE-S1
            MOVE A1-DATE-S2 TO B1-DATE-S2
            MOVE "X" TO B1-X
            WRITE B1-PRINTLINE1
            MOVE SPACES TO B1-PRINTLINE1.
     IF A2-VOLUME-LIT = "VOLUME"
        IF W-X NOT = 1
            PERFORM 999E-ABEND
        ELSE
            MOVE 2 TO W-X
            MOVE A2-VOLUME TO W-VOLUME
            MOVE A2-VOLUME-ID TO W-VOLUME-ID
            MOVE W-DISK-ID TO B1-DISK.
     IF A3-TOTALS = "TOTALS"
        IF W-X NOT = 2
            PERFORM 999E-ABEND
        ELSE
            MOVE 3 TO W-X
            MOVE A3-TOT-READ  TO B1-TOTREAD
            MOVE A3-TOT-CREAD TO B1-TOTCACH
            MOVE A3-TOT-WRITE TO B1-TOTWRIT
            MOVE A3-DASD-FW   TO B1-TOT-FW
            PERFORM 050D-EXAMINE.
     IF A5-CACH = "CACHING:"
        IF W-X NOT = 3
            PERFORM 999E-ABEND
        ELSE
            MOVE 4 TO W-X
            MOVE A5-ACTIVE TO B1-ACTIVE.
     IF A6-DFW = "DASD FAST WRITE:"
        IF W-X NOT = 4
            PERFORM 999E-ABEND
        ELSE
            MOVE 1 TO W-X
            MOVE A6-ACTIVE TO B1-FW-ACTIVE
            WRITE B1-PRINTLINE1
            MOVE SPACES TO B1-PRINTLINE1.
     GO TO 030D-READ.
 030D-READ-EXIT.
     EXIT.
 050D-EXAMINE.
     EXAMINE B1-TOTREAD REPLACING LEADING SPACES BY ZERO.
     EXAMINE B1-TOTCACH REPLAICNG LEADING SPACES BY ZERO.
     EXAMINE B1-TOTWRIT REPLACING LEADING SPACES BY ZERO.
     EXAMINE B1-TOT-FW  REPLACING LEADING SPACES BY ZERO.
 O50D-EXAMINE-EXIT.
     EXIT.
 999E-ABEND SECTION.
     DISPLAY W-X A1-CARD-IN.
     CALL "X".

The calculation step was also written in COBOL.

 IDENTIFICATION DIVISION.
*************************
 PROGRAM-ID.                TECJL41.
 AUTHOR.                    JOHN LARRY.
 REMARKS                    CACHE RATIO CALCULATOR FROM 2
                            FILES.
 ENVIRONMENT DIVISION.
**********************
 CONFIGURATION SECTION.
 SPECIAL-NAMES
     C01 IS TO-TOP-OF-PAGE.
 INPUT-OUTPUT SECTION.
 FILE-CONTROL.
     SELECT B100-PRINTFILE ASSIGN TO PRINT23.
     SELECT B300-DISKFILE  ASSIGN TO PRINT25.
     SELECT B400-PRINTFILE ASSIGN TO PRINT26.
     SELECT A100-CARDFILE  ASSIGN TO CARD01.
     SELECT A200-CARDFILE  ASSIGN TO CARD02.
 DATA DIVISION.
***************
 FILE SECTION.
 FD  B100-PRINTFILE
     LABEL RECORDS ARE STANDARD.
 01  P-PRINTLINE1.
     05 P1-CC         PIC X.
     05 P1-DISK       PIC X(11).
     05 FILLER        PIC XXXX.
     05 P1-RP-RATIO   PIC ZZZZZ.99BBBBBB.
     05 P1-CACHE      PIC ZZZZZZZ.99BBBBBB.
     05 FILLER        PIC X(7).
     05 P1-RW-RATIO   PIC ZZZZZ.99BBBBBB.
     05 FILLER        PIC X(5).
     05 P1-ACTIVE     PIC X(11).
     05 FILLER        PIC X(3).
     05 P1-TOTREAD    PIC Z(10)9.
     05 FILLER        PIC X(5).
     05 P1-TOTCREAD   PIC Z(10)9.
     05 FILLER        PIC X(5).
     05 P1-TOTWRITE   PIC Z(10)9.
     05 FILLER        PIC X(4).
 FD  B300-DISKFILE
     LABEL RECORDS ARE STANDARD.
 01  B3-LINE.
     05 B3-DATE       PIC X(7).
     05 B3-DISK.
        10 B3-DISK-NAME PIC X(6).
        10 FILLER       PIC X.
        10 B3-DISK-NUM  PIC XXX.
        10 FILLER       PIC X.
     05 B3-RP         PIC 9V99.
     05 B3-RH         PIC 9V99.
     05 B3-RW         PIC 9(5)V99.
     05 B3-DA         PIC X.
     05 B3-TOT-READ   PIC 9(8).
     05 B3-TOT-CACH   PIC 9(8).
     05 B3-TOT-WRIT   PIC 9(8).
     05 B3-SORT-IND   PIC X.
     05 B3-FW-ACT     PIC X.
     05 B3-FW-RATIO   PIC 9V99.
     05 B3-TOT-FW     PIC 9(8).
 FD  B400-PRINTFILE
     LABEL RECORDS ARE STANDARD.
 01  P-PRINTLINE4.
     05 P4-CC         PIC X.
     05 P4-DISK       PIC X(11).
     05 FILLER        PIC XXXX.
     05 P4-FW-RATIO   PIC ZZZZZ.99BBBBBB.
     05 P4-FW-WRITE   PIC ZZZZZZZ9.
     05 FILLER        PIC X(13).
     05 P4-TOT-WRITE  PIC ZZZZZZZ9.
     05 FILLER        PIC X(11).
     05 P4-ACTIVE     PIC X(11).
     05 FILLER        PIC X(50).
 FD  A100-CARDFILE
     BLOCK CONTAINS 0 RECORDS
     LABEL RECORDS ARE STANDARD.
 01  A1-CARD-IN.
     05 A1-DISK.
        10 A1-X       PIC X.
        10 FILLER     PIC XX.
        10 A1-DATE    PIC X(8).
     05 FILLER        PIC XX.
     05 A1-TOTREAD    PIC 9(8).
     05 FILLER        PIC X.
     05 A1-TOTCACH    PIC 9(8).
     05 FILLER        PIC X.
     05 A1-TOTWRIT    PIC 9(8).
     05 A1-ACTIVE     PIC X(11).
     05 A1-TOT-FW     PIC 9(8).
     05 A1-FW-ACTIVE  PIC X(11).
     05 FILLER        PIC X(11).
 FD  A200-CARDFILE
     BLOCK CONTAINS 0 RECORDS
     LABEL RECORDS ARE STANDARD.
 01  A2-CARD-IN.
     05 A2-DISK.
        10 A2-X       PIC X.
        10 FILLER     PIC XX.
        10 A2-DATE    PIC X(8).
     05 FILLER        PIC XX.
     05 A2-TOTREAD    PIC 9(8).
     05 FILLER        PIC X.
     05 A2-TOTCACH    PIC 9(8).
     05 FILLER        PIC X.
     05 A2-TOTWRIT    PIC 9(8).
     05 A2-ACTIVE     PIC X(11).
     05 A2-TOT-FW     PIC 9(8).
     05 A2-FW-ACTIVE  PIC X(11).
     05 FILLER        PIC X(11).
 WORKING-STORAGE SECTION.
*************************
 77  W-VERSION            PIC X(16)  VALUE  "PROGRAM TECJL41".
 77  W-DISKS              PIC S9999      COMP-3 VALUE ZERO.
 77  W-DISKS-EX           PIC S9999      COMP-3 VALUE ZERO.
 77  W-FW-DISKS           PIC S9999      COMP-3 VALUE ZERO.
 77  W-TOT-READS          PIC S9(11)     COMP-3 VALUE ZERO.
 77  W-TOT-FW-WRITES      PIC S9(11)     COMP-3 VALUE ZERO.
 77  W-TOT-CACHE          PIC S9(11)     COMP-3 VALUE ZERO.
 77  W-TOT-FW             PIC S9(11)     COMP-3 VALUE ZERO.
 77  W-TOT-GRAND          PIC S9(11)V999 COMP-3 VALUE ZERO.
 77  W-FW-TOT-GRAND       PIC S9(11)V999 COMP-3 VALUE ZERO.
 77  W-ACC-RATIO          PIC S9(11)V999 COMP-3 VALUE ZERO.
 77  W-ACC-RATIO-EX       PIC S9(11)V999 COMP-3 VALUE ZERO.
 77  W-FW-ACC-RATIO       PIC S9(11)V999 COMP-3 VALUE ZERO.
 77  W-TOT-RATIO          PIC S9(11)V999 COMP-3 VALUE ZERO.
 77  W-FW-TOT-RATIO       PIC S9(11)V999 COMP-3 VALUE ZERO.
 77  W1-TOTREAD           PIC 9(8).
 77  W1-TOTCACH           PIC 9(8).
 77  W1-TOTWRIT           PIC 9(8).
 77  W1-TOTFW             PIC 9(8).
 01  W-VARIABLES.
     05 W-RP-RATIO     PIC 9V9999  COMP VALUE 0.
     05 W-READ-RATIO   PIC 9V9999  COMP VALUE 0.
     05 W-RW-RATIO     PIC 99V9999 COMP VALUE 0.
     05 W-FW-RATIO     PIC 9V9999  COMP VALUE 0.
     05 W-DATE.
        10 W-DATE-DD      PIC XX.
        10 W-DATE-MM      PIC XXX.
        10 W-DATE-YY      PIC XX.
 01  TITLE1.
     05 FILLER            PIC X(40) VALUE "1".
     05 FILLER            PIC X(43) VALUE
     " PEAK HOUR CACHE RATIOS FOR JOHN LARRY FOR ".
     05 T-DATE.
        10 T-DATE-DD      PIC XX.
        10 FILLER         PIC X.
        10 T-DATE-MM      PIC XX.
        10 FILLER         PIC X.
        10 T-DATE-YY      PIC XX.
 01  TITLE1A.
     05 FILLER            PIC X(11) VALUE "0   DISKS".
     05 FILLER            PIC XXX.
     05 FILLER            PIC X(16) VALUE "READ PROPORTION ".
     05 FILLER            PIC XX.
     05 FILLER            PIC X(16) VALUE " READ HIT RATIO".
     05 FILLER            PIC XX.
     05 FILLER            PIC X(18) VALUE " READ/WRITE RATIO".
     05 FILLER            PIC X(13).
     05 FILLER            PIC X(18).
              VALUE "        TOTAL READ".
     05 FILLER            PIC X(2).
     05 FILLER            PIC X(18) VALUE " TOTAL CACHE READ".
     05 FILLER            PIC X(18) VALUE " TOTAL WRITTEN".
 01  TITLE1B.
     05 FILLER            PIC X(11) VALUE "0    DISKS".
     05 FILLER            PIC XXX.
     05 FILLER            PIC X(16) VALUE "WRITE HITS ".
     05 FILLER            PIC XX.
     05 FILLER            PIC X(16) VALUE " TOTAL DASD FW".
     05 FILLER            PIC XX.
     05 FILLER            PIC X(18) VALUE " TOTAL WRITTEN".
     05 FILLER            PIC X(13).
 01  TOTAL1.
     05 FILLER     PIC X(29) VALUE "0READ HIT RATIO OF TOTALS".
     05 W-AVERAGE         PIC Z.99.
 01  TOTAL2.
     05 FILLER PIC X(29) VALUE "0AVERAGE OF READ HIT RATIOS ".
     05 W-AVERAGE2        PIC Z.99.
     05 FILLER     PIC X(34) VALUE
         " (EXCLUDING VOLUMES < 1000 READS)".
 01  TOTAL3.
     05 FILLER     PIC X(29) VALUE "0NO OF ACTIVE DISKS ".
     05 W-NO-DISKS        PIC ZZ9.
 01  TOTAL4.
     05 FILLER     PIC X(29) VALUE "0DASD FW RATIO OF TOTALS".
     05 W-FW-AVERAGE      PIC Z.99.
 01  TOTAL5.
     05 FILLER     PIC X(29) VALUE "0AVERAGE OF FW RATIOS ".
     05 W-FW-AVERAGE2     PIC Z.99.
 01  TOTAL6.
     05 FILLER     PIC X(29) VALUE "0NO OF FW ACTIVE DISKS ".
     05 W-NO-FW-DISKS        PIC ZZ9.
     EJECT
 PROCEDURE DIVISION.
********************
 010M-MAINLINE SECTION.
***********************
     OPEN INPUT  A100-CARDFILE
                 A200-CARDFILE.
     OPEN OUTPUT B100-PRINTFILE
                 B300-DISKFILE
                 B400-PRINTFILE.
     READ A100-CARDFILE
       AT END
       CALL "X".
     IF A1-X NOT = "X"
       CALL "X".
     READ A200-CARDFILE
       AT END
       CALL "X".
     IF A2-X NOT = "X"
       CALL "X".
     MOVE A1-DATE TO T-DATE.
     PERFORM 050D-SET-MONTH.
     WRITE P-PRINTLINE1 FROM TITLE1  AFTER POSITIONING P1-CC.
     WRITE P-PRINTLINE1 FROM TITLE1A AFTER POSITIONING P1-CC.
     WRITE P-PRINTLINE4 FROM TITLE1  AFTER POSITIONING P4-CC.
     WRITE P-PRINTLINE4 FROM TITLE1B AFTER POSITIONING P4-CC.
     MOVE SPACES TO P-PRINTLINE1.
     MOVE SPACES TO P-PRINTLINE4.
     WRITE P-PRINTLINE1 AFTER POSITIONING P1-CC.
     WRITE P-PRINTLINE4 AFTER POSITIONING P4-CC.
     PERFORM 015D-READ THRU 015D-READ-EXIT.
     COMPUTE W-TOT-GRAND = W-TOT-CACHE / W-TOT-READS.
* EXCLUDE DISKS WITH LESS THAN 1000 READS
     COMPUTE W-TOT-RATIO = (W-ACC-RATIO - W-ACC-RATIO-EX)
                         / (W-DISKS - W-DISKS-EX).
     IF W-TOT-FW-WRITES = 0
         MOVE 0 TO W-FW-TOT-GRAND
     ELSE
         COMPUTE W-FW-TOT-GRAND = W-TOT-FW / W-TOT-FW-WRITES.
     IF W-FW-DISKS = 0
         MOVE 0 TO W-FW-TOT-RATIO
     ELSE
         COMPUTE W-FW-TOT-RATIO = W-FW-ACC-RATIO / W-FW-DISKS.
     MOVE W-TOT-GRAND TO W-AVERAGE.
     MOVE W-FW-TOT-GRAND TO W-FW-AVERAGE.
     WRITE P-PRINTLINE1 FROM TOTAL1 AFTER POSITIONING P1-CC.
     WRITE P-PRINTLINE4 FROM TOTAL4 AFTER POSITIONING P4-CC.
     MOVE W-TOT-RATIO TO W-AVERAGE2.
     MOVE W-FW-TOT-RATIO TO W-FW-AVERAGE2.
     WRITE P-PRINTLINE1 FROM TOTAL2 AFTER POSITIONING P1-CC.
     WRITE P-PRINTLINE4 FROM TOTAL5 AFTER POSITIONING P4-CC.
     MOVE W-DISKS TO W-NO-DISKS.
     MOVE W-FW-DISKS TO W-NO-FW-DISKS.
     WRITE P-PRINTLINE1 FROM TOTAL3 AFTER POSITIONING P1-CC.
     WRITE P-PRINTLINE4 FROM TOTAL6 AFTER POSITIONING P4-CC.
     CLOSE A100-CARDFILE
           A200-CARDFILE
           B100-PRINTFILE
           B300-DISKFILE
           B400-PRINTFILE.
     STOP RUN.
 010M-EXIT.
     EXIT.
 015D-READ SECTION.
     READ A100-CARDFILE
       AT END
       GO TO 015D-READ-EXIT.
     READ A200-CARDFILE
       AT END
       GO TO 015D-READ-EXIT.
     IF A1-DISK NOT = A2-DISK
        DISPLAY "FILE MISMATCH " A1-DISK A2-DISK
        CALL "X".
     COMPUTE W1-TOTREAD = A2-TOTREAD - A1-TOTREAD.
     COMPUTE W1-TOTWRIT = A2-TOTWRIT - A1-TOTWRIT.
     COMPUTE W1-TOTCACH = A2-TOTCACH - A1-TOTCACH.
     COMPUTE W1-TOTFW   = A2-TOT-FW  - A1-TOT-FW.
     MOVE W1-TOTREAD TO P1-TOTREAD.
     MOVE W1-TOTCACH TO P1-TOTCREAD.
     MOVE W1-TOTWRIT TO P1-TOTWRITE.
     IF W1-TOTREAD = 0 AND W1-TOTWRIT = 0
         MOVE 0 TO W-RP-RATIO
     ELSE
          COMPUTE W-RP-RATIO = W1-TOTREAD
                            / (W1-TOTREAD + W1-TOTWRIT).
     IF W1-TOTWRIT = 0
         MOVE 0 TO W-RW-RATIO
     ELSE
          COMPUTE W-RW-RATIO = W1-TOTREAD / W1-TOTWRIT.
     IF W1-TOTREAD = 0
         MOVE 0 TO W-READ-RATIO
     ELSE
         COMPUTE W-READ-RATIO = W1-TOTCACH / W1-TOTREAD.
     IF W1-TOTWRIT = 0
         MOVE 0 TO W-FW-RATIO
     ELSE
         COMPUTE W-FW-RATIO = W1-TOTFW /  W1-TOTWRIT.
     MOVE A1-DISK TO P1-DISK.
     MOVE A1-DISK TO P4-DISK.
     IF (W1-TOTREAD NOT = 0 AND W1-TOTCACH NOT = 0)
       AND A1-ACTIVE NOT = "DEACTIVATED"
         COMPUTE W-ACC-RATIO = W-ACC-RATIO + W-READ-RATIO
         ADD W1-TOTREAD TO W-TOT-READS
         ADD W1-TOTCACH TO W-TOT-CACHE
         ADD 1 TO W-DISKS.
     IF (W1-TOTREAD < 1000 AND W1-TOTCACH NOT = 0)
       AND A1-ACTIVE NOT = "DEACTIVATED"
         COMPUTE W-ACC-RATIO-EX = W-ACC-RATIO-EX + W-READ-RATIO
         ADD 1 TO W-DISKS-EX.
     IF W1-TOTFW NOT = 0
       AND A1-FW-ACTIVE NOT = "DEACTIVATED"
         COMPUTE W-FW-ACC-RATIO = W-FW-ACC-RATIO + W-FW-RATIO
         ADD W1-TOTFW TO W-TOT-FW
         ADD W1-TOTWRIT TO W-TOT-FW-WRITES
         ADD 1 TO W-FW-DISKS.
     MOVE W-RP-RATIO TO P1-RP-RATIO.
     MOVE W-RW-RATIO TO P1-RW-RATIO.
     MOVE W-READ-RATIO TO P1-CACHE.
     MOVE A1-ACTIVE TO P1-ACTIVE.
     WRITE P-PRINTLINE1 AFTER POSITIONING P1-CC.
     MOVE SPACES TO P-PRINTLINE1.
     MOVE W-FW-RATIO TO P4-FW-RATIO.
     MOVE W1-TOTFW TO P4-FW-WRITE.
     MOVE W1-TOTWRIT TO P4-TOT-WRITE.
     MOVE A1-FW-ACTIVE TO P4-ACTIVE.
     WRITE P-PRINTLINE4 AFTER POSITIONING P4-CC.
     MOVE SPACES TO P-PRINTLINE4.
     MOVE W-DATE TO B3-DATE.
     MOVE A1-DISK TO B3-DISK.
     MOVE W-RP-RATIO TO B3-RP.
     MOVE W-READ-RATIO TO B3-RH.
     MOVE W-RW-RATIO TO B3-RW.
     MOVE A1-ACTIVE TO B3-DA.
* The following code assists in sorting the DASD devices into a
* desired order based on the device number.
    IF B3-DISK-NUM < "700"
        MOVE "2" TO B3-SORT-IND
    ELSE
       IF B3-DISK-NUM < "71A"
           MOVE "1" TO B3-SORT-IND
    ELSE
       IF B3-DISK-NUM < "710"
           MOVE "4" TO B3-SORT-IND
    ELSE
       IF B3-DISK-NUM < "72A"
           MOVE "3" TO B3-SORT-IND
    ELSE
       IF B3-DISK-NUM < "720"
           MOVE "6" TO B3-SORT-IND
    ELSE
       IF B3-DISK-NUM < "73A"
           MOVE "5" TO B3-SORT-IND
    ELSE
       IF B3-DISK-NUM < "730"
           MOVE "8" TO B3-SORT-IND
    ELSE
       MOVE "7" TO B3-SORT-IND.
    MOVE W1-TOTREAD TO B3-TOT-READ.
    MOVE W1-TOTCACH TO B3-TOT-CACH.
    MOVE W1-TOTWRIT TO B3-TOT-WRIT.
    MOVE W1-TOTFW TO B3-TOT-FW.
    MOVE W-FW-RATIO TO B3-FW-RATIO
    MOVE A1-FW-ACTIVE TO B3-FW-ACT.
    WRITE B3-LINE.
    GO TO 015D-READ.
015D-READ-EXIT.
    EXIT.
050D-SET-MONTH SECTION.
    MOVE T-DATE-DD TO W-DATE-DD.
    MOVE T-DATE-YY TO W-DATE-YY.
    IF T-DATE-MM = 01
        MOVE "JAN" TO W-DATE-MM.
    IF T-DATE-MM = 02
        MOVE "FEB" TO W-DATE-MM.
    IF T-DATE-MM = 03
        MOVE "MAR" TO W-DATE-MM.
    IF T-DATE-MM = 04
        MOVE "APR" TO W-DATE-MM.
    IF T-DATE-MM = 05
        MOVE "MAY" TO W-DATE-MM.
    IF T-DATE-MM = 06
        MOVE "JUN" TO W-DATE-MM.
    IF T-DATE-MM = 07
        MOVE "JUL" TO W-DATE-MM.
    IF T-DATE-MM = 08
        MOVE "AUG" TO W-DATE-MM.
    IF T-DATE-MM = 09
        MOVE "SEP" TO W-DATE-MM.
    IF T-DATE-MM = 10
        MOVE "OCT" TO W-DATE-MM.
    IF T-DATE-MM = 11
        MOVE "NOV" TO W-DATE-MM.
    IF T-DATE-MM = 12
        MOVE "DEC" TO W-DATE-MM.
050D-SET-MONTH-EXIT.
    EXIT.

SAS code for the step that stores the data for later processing is
shown below.

* CACHEUPD - CACHE RATIO FILE UPDATE PROGRAM;
* PRODUCTION CODE;
TSO ALLOC F(SASE) DA(dataset for storing cache data) OLD; RUN;
OPTIONS NOFMTERR;
OPTIONS NOTEXT82;
DATA CACHE;
INFILE SASD;
INPUT
     @1   DATE DATE7.
     @8   DISKNAME $6.
     @15  DISKNUM  $3.
     @19  READPROP 3.2
     @22  READHIT  3.2
     @25  READWRIT 7.2
     @32  ACTIVE   $1.
     @33  TOTREAD  8.
     @41  TOTCACH  8.
     @49  TOTWRIT  8.
     @57  SORTIND  $1.
     @58  FWACTIVE $1.
     @59  FWRATIO  3.2
     @62  TOTFW    8.
     ;
RUN;
PROC SORT NODUP DATA = CACHE;
BY DATE SORTIND DISKNUM;
RUN;
PROC SORT NODUP DATA = SASE.CACHERAT;
BY DATE SORTIND DISKNUM;
RUN;
DATA SASE.CACHERAT;
UPDATE SASE.CACHERAT (IN=ODSET)
       WORK.CACHE (IN =IDSET) ;
BY DATE SORTIND DISKNUM;
OUTPUT SASE.CACHERAT;
RUN;
TSO FREE  F(SASE); RUN;


John Larry
Performance Analyst
Zurich Insurance Company (UK)

