Delete VSAM user catalog

When deleting a VSAM user catalog, the catalog must be
empty.  This program will read an input file created by
IDCAMS LIST SPC and create an output file containing all
cluster names.  The output file is then used as input to an
IDCAMS job assigning SYSIPT to the file.  After completion
of this job, the user catalog is empty and can be deleted.

All user catalog names must start with 'UCAT.', or any name
you choose, and all cluster names must use 'DATA' and
'INDEX'.

Name examples:

o      User catalog       - UCAT.ONLINE
o      Cluster               - ONLINE.STOP.HOLD
o      Data                  - ONLINE.STOP.HOLD.DATA
o      Index                 - ONLINE.STOP.HOLD.INDEX

This program can also be used for creating cluster names for
massive deletes.

         BALR  R12,0
         USING *,R12
         OPEN  INFILE,OUTFILE
READ     EQU   *   ...READ SYSLST FILE...
         MVI   LINEO,X'40'                CLEAR
         MVC   LINEO+1(79),LINEO          CLEAR
         MVC   LINEO+1(3),=C'DEL'         SET CONSTANT
         LA    R9,LINEO+5                 LOAD OUTPUT DISPLACEMENT
         GET   INFILE,LINEI               READ SYSLST FILE
         CLC   LINEI+10(7),=C'DSN----'    IS THIS "DATASET" NAME
         BNE   READ                         NO--BRANCH
         LA    R7,LINEI+17                LOAD NAME DISPLACEMENT
         CLC   0(5,R7),=C'UCAT.'          IS THIS "USER CATALOG"
         BE    READ                         YES--BRANCH
MVCLOOP  EQU   *   ...LOOP WHILE MOVING NAME...
         CLI   0(R7),C'.'                 IS THIS "PERIOD"
         BNE   MVCCHAR                      NO--BRANCH
         LA    R7,1(,R7)                  BUMP DISPLACEMENT BY 1
         CLC   0(5,R7),=C'DATA '          IS THIS "DATA" NAME
         BE    SETCL                        YES--BRANCH
         CLC   0(6,R7),=C'INDEX '         IS THIS "INDEX" NAME
         BE    READ                         YES--BRANCH
         BCTR  R7,0                       SET DISPLACEMENT DOWN BY 1
MVCCHAR  EQU   *   ...MOVE NAME CHARACTER...
         MVC   0(1,R9),0(R7)              MOVE CHARACTER TO OUTPUT
         LA    R7,1(,R7)                  BUMP INPUT TO NEXT CHARACTER
         LA    R9,1(,R9)                  BUMP OUTPUT TO NEXT CHARACTER
         B     MVCLOOP                      --BRANCH
SETCL    EQU   *   ...SET IDCAMS PARMS...
         MVC   0(7,R9),=C' CL PRG'         MOVE CONSTANT TO OUTPUT
         PUT   OUTFILE,LINEOUT            WRITE OUTPUT FROM WORK AREA
         EXCP  WRITE                      WRITE NAME TO LISTING
         WAIT  WRITE
         B     READ                         --BRANCH
EOJ      EQU   *   ...END OF JOB...
         CLOSE INFILE,OUTFILE             CLOSE FILES
         EOJ
         DS   OD
WRITE    CCB   SYSLST,WCCW
WCCW     CCW   9,LINEO,X'20',80
INFILE   DTFSD BLKSIZE=121,DEVADDR=SYS010,WORKA=YES,RECFORM=FIXUNB,    X
               TYPEFLE=INPUT,DEVICE=3380,EOFADDR=EOJ
*
OUTFILE  DTFSD BLKSIZE=88,DEVADDR=SYS011,WORKA=YES,RECFORM=FIXUNB,     X
               TYPEFLE=OUTPUT,DEVICE=3380
LINEI    DC    CL121' '
LINEOUT  DS   0CL88
         DC    CL8' '
LINEO    DC    CL80' '
         LTORG
         END


JCL EXAMPLES FOR RUNNING THE JOB

The files are defined using CA-DYNAM and allocated on
3380s.  The job name is VSAMLIST.  It will create a VSAM
LIST SPC spooling list on DASD.

// JOB VSAMLIST
// LOG
// DLBL IJSYSLS,'VSAM.DEL.SYSLST',1,SD
// EXTENT SYSLST,TEMPWK,,,1,105
ASSGN SYSLST,DISK,VOL=338020,SHR
// EXEC IDCAMS,SIZE=128K
   LISTC SPC CAT(UCAT.ONLINE)ALL
/*
CLOSE SYSLST,FEE
/&

The job name is VSAMCLUS.  It will create a VSAM file
containing the names of clusters to be deleted.

// JOB VSAMCLUS
LIBDEF PHASE,SEARCH=BOMLIBS.CICS
// DLBL INFILE,'INFOPAC.SYSLST(D)',1
// EXTENT SYS010,TEMPWK,,,1,1
// ASSGN SYS010,DISK,VOL=338020,SHR
// DLBL OUTFILE,'INFOPAC,SYSLST,SYSIPT(D)',1
// EXTENT SYS011,TEMPWK,,,1,120
// ASSGN SYS011,DISK,VOL=338020,SHR
// EXEC INFODEL,SIZE=128K
/*
/&

The job name is VSAMDEL.  It will use the input file,
SYSIPT, for cluster deletes.

// JOB VSAMDEL
// DLBL IJSYSUC,'UCAT.ONLINE',,VSAM
// DLBL IJSYSIN,'INFOPAC.SYSLST.SYSIPT(D)',1
// EXTENT SYSIPT,TEMPWK,,,1,120
 ASSGN SYSIPT,DISK,VOL=338020,SHR
// EXEC IDCAMS,SIZE=128K
/*
/&
CLOSE SYSIPT,FEC
*END PRINT        147 RECORDS

Jesse Joyner
Systems Programmer
Bank of Mississippi (USA)

