Editing VSAM and long SAM files

The 'editing by wrap around' program enables us to edit PS
and VSAM ESDS and KSDS datasets of any record length. If
the length of the record is greater than 9 and less than 256, the
user can select whether he wants to edit the record as a single
line record or a wrapped record. The program is short and
contains some comments, so I will not describe it in detail.

However, one deficiency in the program is that when editing
KSDS, if you want to add a new record to the file, you must
insert the record in the key sequence. The program can be
enhanced by adding a step to call DFSORT to sort the
intermediate file before REPROing to the new KSDS.

Another problem occurs when using the program to edit VSAM
files. In the EDBEEBOO program, when editing a VSAM file
using single line record format (ie without wrapping), the
VSAM file is copied into a variable length file before editing
and copied back to VSAM after editing. During the editing,
using the ISPF editor, all the trailing blanks are converted to
null characters. If the last field of the original file contains
some trailing blanks or allows blanks as valid values, it will be
truncated. When the VSAM file is used with COBOL
programs, errors can occur. I solve the problem by REPROing
the VSAM file into a fixed length file by amending the
statement at line 39. I change

      IF ALLOCNEW(OFILE,DDOF,ULRECL,'V') = 0 THEN DO

to

      IF ALLOCNEW(OFILE,DDOF,ULRECL,'F') = 0 THEN DO

However, all the records in the VSAM file after editing will be
the same length.


EDBEEBOO EXEC

/* REXX */
/* TRACE ?R */
ARG IFILE TR
ADDRESS ISPEXEC "CONTROL ERRORS RETURN"
ADDRESS ISPEXEC "VGET (ZUSER) SHARED"
WPLN = 50
OPT    = 1
IFILE = TRANSLATE(IFILE)
IF LENGTH(IFILE) = 0 THEN DO
  CUR    = "CURSOR(DDD)"
  DDD    = ""
  MSG    = ""
  ADDRESS ISPEXEC "DISPLAY PANEL(EDBEEB$P) "MSG CUR
  IF  RC > 8 THEN  SAY "BEEBOO : DISPLAY PANEL FAILED"
END
IF LENGTH(IFILE) = 0 THEN DO
  RETURN 8
END
UNAME = TIME('S')
TFILE = ZUSER".SDBEEBOO.TFT"UNAME".TEMP"
VFILE = ZUSER".VSBEEBOO.VFT"UNAME".TEMP"
OFILE = ZUSER".SDBEEBOO.OFT"UNAME".TEMP"
DDIF = 'BEEBOOIF'
DDTF = 'BEEBOOTF'
DDOF = 'BEEBOOOF'
IF ABBREV('TRACE',TR,1) THEN TRACE ?R
IF VALDSN() ¬= 0 THEN RETURN
SELECT
  WHEN UDSORG = 'PO' & ULRECL > 9 & ULRECL < 256 & OPT = '1' &,
       (URECFM = 'FB' | URECFM = 'VB' | URECFM = 'FBA') THEN
    ADDRESS ISPEXEC "EDIT DATASET("IFILE")"
  WHEN UDSORG = 'PS' & ULRECL > 9 & ULRECL < 256 & OPT = '1' &,
       (URECFM = 'FB' | URECFM = 'VB' | URECFM = 'FBA') THEN
    ADDRESS ISPEXEC "EDIT DATASET("IFILE")"
  WHEN UDSORG = 'PS' | UDSORG = 'VS' | UDSORG = 'VK' THEN DO
    ADDRESS TSO "FREE FILE("DDIF")"
    ADDRESS TSO "ALLOC DATASET("IFILE") FILE("DDIF") OLD"
    IF RC = 0 THEN DO
      IF ALLOCNEW(OFILE,DDOF,ULRECL,'V') = 0 THEN DO
          EDITRC = 0
          SELECT
             WHEN UDSORG = 'VS' & ULRECL>9 & ULRECL<256 & OPT='1' THEN
               CALL EDITVSAM
             WHEN UDSORG = 'VK' & ULRECL>9 & ULRECL<256 & OPT='1' THEN
               CALL EDITVSAM
             OTHERWISE
               CALL EDITABN
          END
          IF EDITRC = 0 THEN
              CALL DELFILE OFILE DDOF
          ELSE
              SAY '******** IF YOUR ORIGINAL FILE IS CORRUPTED,',
                  'THE FILE' OFILE 'MAY HELP YOU'
      END
      ADDRESS TSO "FREE FILE("DDIF")"
    END
  END
  OTHERWISE DO
      SAY " IN THIS RELEASE, "
      SAY " ONLY SUPPORTS PS DATASETS OF FB/VB ORGANIZATION"
      SAY "     AND     PO DATASETS OF FB/VB ORG. >9 & <256"
      SAY "     AND     VS DATASETS "
      SAY "     AND     VK DATASETS "
      SAY " "IFILE " IS AN "SYSDSORG "DATASET"
      SAY " AND HAS AN "URECFM "ORGANIZATION"
      RETURN 4
  END
END
RETURN
END
/* PROCEDURE VALDSN :- GEN THE DATASET ATTRIBUTE                 */
VALDSN:
IF SYSDSN(IFILE) ¬= 'OK' THEN DO
  SAY "BEEBOO : DATASET "IFILE" DOES NOT EXIST"
  RETURN 4
END
X=LISTDSI(IFILE "DIRECTORY")
UKEYSTART = 0
UKEYEND = 0
SELECT
  WHEN SYSREASON = 0 THEN DO
     UDSORG = SYSDSORG
     URECFM = SYSRECFM
     ULRECL = SYSLRECL
     IF URECFM = ' VB ' THEN ULRECL = ULRECL - 4
  END
  WHEN SYSREASON = 12 THEN DO
     X=OUTTRAP("LINE.")
     ADDRESS TSO "LISTC ENT("IFILE") ALL"
     X=OUTTRAP("OFF")
     UDNAME = SUBSTR(LINE.8,17)
     IF SUBSTR(LINE.9,8,5) = 'INDEX' THEN DO
       UDSORG = 'VK'
       UKEYSTART = GETWORD(LINE.20,31) + 1
       UKEYEND = UKEYSTART + GETWORD(LINE.19,31) - 1
       ULRECL = GETWORD(LINE.20,60)
       UINAME = SUBSTR(LINE.9,17)
     END
     ELSE DO
       UDSORG = 'VS'
       ULRECL = GETWORD(LINE.19,60)
     END
     URECFM = 'FB'
     END
  OTHERWISE
   DO
     SAY "BEEBOO : LISTDSI "IFILE" FAILED . . . "
     SAY "BEEBOO : RETURN CODE = "RC
     SAY "BEEBOO : REASON CODE = "SYSREASON
     RETURN 4
   END
 END
RETURN 0
GETWORD: PROCEDURE
ARG LINE,ENDCOL
STCOL = ENDCOL
DO WHILE SUBSTR(LINE,STCOL,1) ¬='-'
   STCOL = STCOL - 1
END
LEN = ENDCOL - STCOL
STCOL = STCOL + 1
RETURN SUBSTR(LINE,STCOL,LEN)
/* PROCEDURE EDITVSAM :- EDIT A VSAM WITH RECORD LENGTH > 9 & < 256 */
EDITVSAM:
IF REPRODS(DDIF,DDOF) = 0 THEN DO
   ADDRESS ISPEXEC "EDIT DATASET ( "OFILE" ) ",
                 " PROFILE(BEEBOON) MACRO(EDBEEBVM)"
   SELECT
     WHEN RC = 4 THEN SAY "BEEBOO : FILE NOT MODIFIED"
     WHEN RC = 0 THEN EDITRC = REPROVS (DDOF,DDIF)
     OTHERWISE      SAY "BEEBOO : EDIT TEMP FILE FAILED... RC ="RC
   END
END
RETURN
/* PROCEDURE EDITABN - EDIT A FILE WITH RECORD LENGTH > 255        */
EDITABN:
IF UDSORG = 'PS' THEN DO
   IF BBEDIT(DDIF,DDOF) = 0 THEN EDITRC = REPRODS(DDOF,DDIF)
END
ELSE
DO
   IF REPRODS(DDIF,DDOF) = 0 THEN
      IF BBEDIT(DDOF,DDOF) = 0 THEN EDITRC = REPROVS(DDOF,DDIF)
END
RETURN
/* PROCEDURE BBEDIT - CONVERT INDD, EDIT AND STORED AS OUTDD       */
BBEDIT:
ARG INDD,OUTDD
RETCODE = 4
IF ALLOCNEW(TFILE,DDTF,WPLN+22,'F') = 0 THEN DO
 IF FORMAT1(INDD,DDTF) = 0 THEN DO
    ADDRESS ISPEXEC "EDIT DATASET("TFILE")",
                 " PROFILE(BEEBOO) MACRO(EDBEEBOM)"
    SELECT
      WHEN RC = 4 THEN SAY "BEEBOO : FILE NOT MODIFIED"
      WHEN RC = 0 THEN RETCODE = FORMAT2(DDTF,OUTDD)
      OTHERWISE       SAY "BEEBOO : EDIT TEMP FILE FAILED... RC ="RC
    END
 END
 CALL DELFILE TFILE DDTF
END
RETURN RETCODE
/* PROCEDURE ALLOCNEW */
ALLOCNEW: PROCEDURE
ARG DS,FILE,LRECL,FMT
IF FMT = 'V' THEN LRECL = LRECL + 4
BLKS = 6144%LRECL*LRECL
IF FMT = 'V' THEN BLKS = BLKS + 4
ADDRESS TSO "FREE FILE("FILE")"
ADDRESS TSO "ALLOC DATASET("DS") FILE("FILE")",
    "NEW CATALOG UNIT(DISK) TRACKS SPACE(10,10)",
    "DSORG(PS) BLKSIZE("BLKS") LRECL("LRECL") RECFM("FMT",B)"
SAY "BEEBOO : ALLOCATE NEW DATASET "DS FILE "... RC = "RC
RETURN RC
/* PROCEDURE DELFILE */
DELFILE: PROCEDURE
ARG DS DD
  ADDRESS TSO "FREE FILE ("DD") DELETE"
  SAY "BEEBOO : DELETE PILE "DS"  ...... RC = "RC
RETURN RC
/* PROCEDURE REPRODS */
REPRODS:
ARG IDD, ODD
X=OUTTRAP ("LINE.")
ADDRESS TSO "REPRO IFILE("IDD") OFILE("ODD")"
RETCODE = RC
X=OUTTRAP ("OFF")
IF POS ("VSAM OPEN RETURN CODE IS 160",LINE.2) > 0 THEN
RETCODE = 0
IF RETCODE ¬= 0 THEN SAY "BEEBOO : REPRO FILE FAILED"
RETURN RETCODE
/* PROCEDURE REPROVS */
REPROVS:
ARG IDD,ODD
SAY "BEEBOO : DEFINE TEMP VSAM FILE"
ADDRESS TSO "DEFINE CLUSTER(NAME("VFILE") MODEL ("IFILE") "
IF RC = 0 THEN DO
  SAY "BEEBOO : DELETE THE ORGINAL FILE"
  ADDRESS TSO "DELETE "IFILE;
  IF RC = 0 THEN DO
      SAY "BEEBOO : DEFINE THE ORIGINAL FILE"
      IF UDSORG = 'VS' THEN
        ADDRESS TSO "DEFINE CLUSTER(NAME("IFILE")",
                  "MODEL("VFILE"))",
                  "DATA(NAME("UDNAME"))"
      ELSE
        ADDRESS TSO "DEFINE CLUSTER(NAME("IFILE")" ,
                  "MODEL("VFILE"))",
                  "DATA (NAME("UDNAME"))",
                  "INDEX(NAME("UINAME"))"
      IF RC = 0 THEN DO
        SAY "BEEBOO : COPY THE EDITING TEMP FILE INTO ORIGINAL FILE"
         X=OUTTRAP("LINE.")
        ADDRESS TSO "REPRO IFILE("IDD") ODS("IFILE")"
        RETCODE = RC
        X=OUTTRAP("OFF")
        IF UDSORG = 'VK' &,
        POS("NUMBER OF RECORDS PROCESSED WAS 0", LINE.1) > 0 THEN DO
             SAY LINE. 1
        END
        SAY "BEEBOO : DELETE TEMP VSAM FILE"
        ADDRESS TSO "DELETE "VFILE
  END
END
IF RETCODE ¬= 0 THEN SAY "BEEBOO : REPRO FILE FAILED"
RETURN RETCODE
/* PROCEDURE FORMAT1  CONVERT INDD TO OUTDD OF LENGTH 72           */
FORMAT1:
ARG IDD,ODD
LINE50.1 = ' < 'PAD(ULRECL,4)'>  <'PAD(UREYSTART,4)'-'PAD(UKEYEND,4)'>'
K=1
N = (ULRECL-1)%WPLN + 1
BUFNO = 100
READPTR = 1
ADDRESS TSO "EXECIO "BUFNO" DISKR "IDD READPTR" (STEM LINE.)"
STOP=LINE.0
IF STOP = 0 THEN DO
  STOP = 1
  LINE.1 = ""
  LINE50.1 = LINE50.1" NEW DATASET"
END
DO UNTIL STOP = 0
 SAY ' BEEBOO : PROCESSING RECORD ' READPTR
 DO J = 1 TO STOP
     DO I = 1 TO N
       K = K + 1
       C1 = WPLN*(I-1)+1
       C2 = WPLN*I
       IF C2 > ULRECL THEN C2 = ULRECL
       IF I = 1 THEN
         LINE50.K = ,
   '<'PAD(READPTR,6)'> <0001-'PAD(C2,4)'><'SUBSTR(LINE.J,1,WPLN) '>'
       ELSE
         LINE50 . K = ,
     '<     > <'PAD(C1,4)'-'PAD(C2,4)'><'SUBSTR(LINE.J,C1,WPLN) '>'
     END
     READPTR = READPTR+1
 END
 K=K+1
 LINE50.K=""
 "EXECIO * DISKW "ODD" (STEM LINE50.)"
 K = 0
 ADDRESS TSO "EXECIO "BUFNO" DISKR "IDD READPTR" (STEM LINE.)"
 STOP=LINE.0
END
SAY ' BEEBOO : TOTAL PROCESSED RECORD ' READPTR - 1
ADDRESS TSO "EXECIO 0 DISKR "IDD" (FINIS)"
ADDRESS TSO "EXECIO 0 DISKW "ODD" (FINIS)"
RETURN RC
PAD:PROCEDURE
ARG NUM, DIGIT
RETURN RIGHT(NUM,DIGIT,'0')
/* PROCEDURE FORMAT2   CONVERT IDD OF LENGTH 72 TO OUTDD        */
FORMAT2:
ARG IDD,ODD
N = (ULRECL-1)%WPLN + 1
BUFNO = N * 100
K=BUFNO+1
LINE.K = ""
K = 1
LINE.K = ""
J = 0
READPTR = 2
WRITECOUNT = 0
ADDRESS TSO "EXECIO 2 DISKR "IDD "1 (STEM LINE50.)"
IF LINE50.0 > 1 THEN DO
  ADDRESS TSO "EXECIO "BUFNO" DISKR "IDD READPTR" (STEM LINE50.)"
  DO UNTIL LINE50.0 = 0
    DO I = 1 TO LINE50.0
      IF J < N THEN
        LINE.K = LINE.K""SUBSTR(LINE50.I,22,WPLN)
      ELSE DO
        LINE.K = SUBSTR(LINE.K,1,ULRECL)
        IF K = 100 THEN DO
           WRITECOUNT = WRITECOUNT + K
           "EXECIO * DISKW "ODD" (STEM LINE.)"
           SAY ' BEEBOO : PROCESSED RECORD ' WRITECOUNT
           K = 0
        END
        K = K + 1
        J = 0
        LINE . K = SUBSTR (LINE50.I,22,WPLN)
      END
      J=J+1
    END
    READPTR = READPTR + LINE50.0
    ADDRESS TSO "EXECIO "BUFNO" DISKR "IDD READPTR" (STEM LINE50.) "
  END
  IF K > 0 THEN DO
     WRITECOUNT = WRITECOUNT + K
     LINE.K = SUBSTR(LINE.K,1,ULRECL)
     K=K+1
     LINE.K = ""
     "EXECIO * DISKW "ODD" (STEM LINE.)"
     SAY 'BEEBOO : TOTAL PROCESSED RECORD ' WRITECOUNT
  END
END
ADDRESS TSO "EXECIO 0 DISKR "IDD" (FINIS)"
IF WRITECOUNT > 0 THEN
   ADDRESS TSO "EXECIO 0 DISKW "ODD" (FINIS)"
ELSE DO
   ADDRESS TSO "ALLOC DUMMY FILE(BEEBOODM) OLD"
   ADDRESS TSO "REPRO IFILE(BEEBOODM) OFILE("ODD")"
   ADDRESS TSO "FREE FILE(BEEBOODM)"
END
RETURN RC


EDBEEB$P PANEL

)ATTR
   % TYPE(TEXT)  INTENS(HIGH)
   + TYPE(TEXT)  INTENS(LOW)
   _ TYPE(INPUT) INTENS(HIGH)  CAPS(ON)   JUST(LEFT)
)BODY
%------------------EDIT BY RECORD WRAPPING-----------------------------
%COMMAND ===>_ZCMD
%
+              <DSN>%===>_Z                                +
+             OPTION%===>_Z+    ( 1, 2 )
+        WRAP LENGTH%===>_Z  +  ( 10 - 230 )

+ THIS APPLICATION IS DESIGNED TO OVERCOME THE LENGTH LIMITATION OF THE
+ ISPF EDITOR, BY PRODUCING A WRAPPED DATASET OF THE INPUT <DSN> FOR
+ EDITING.
+     USE %OPTION 1+ TO HAVE FUNCTION PERFORMED IN FULL.
+     USE %OPTION 2+ IF YOU WANT TO EDIT THE DATASET IN WRAPPED FORMAT, +
+ EVEN IF YOU CAN EDIT THE DATASET IN NORMAL FORMAT.
+  %TO SKIP THIS PANEL, TYPE THE FOLLOWING AND OPTION 1 WILL BE USED :+
+        TSO EDBEEBOO datasetname
+  %IN THIS RELEASE,+
+   ONLY SUPPORTS PS DATASETS OF FB/VB ORGANIZATION
+        AND PO DATASETS OF FB/VB ORG. >9 & <256 IN NON-WRAP FORMAT
+        AND VS,VK DATASETS
)INIT
  .ZVARS = '(DDD OPT WPLN)'
)PROC
   VER (&DDD,NONBLANK,DSNAME)
   VER (&OPT,NONBLANK,RANGE,1,2)
   VER (&WPLN,NONBLANK,RANGE,10,230)
   &TM = TRUNC(&DDD,1)
IF (&TM = '''')
   &TT = .TRAIL
   &IFILE = TRUNC(&TT,'''')
IF (&TM ¬= '''')
   &IFILE = &DDD
)END


EDBEEBOM MACRO

/* REXX */
ADDRESS ISREDIT 'MACRO'
ADDRESS ISPEXEC 'CONTROL ERRORS RETURN'
ADDRESS ISREDIT
'AUTOSAVE = OFF PROMPT'
'(MLEN) = DATA WIDTH'
'BOUNDS = 22 'MLEN
'NUM = OFF'
'CAPS = ON'
'TABSLINE = <21,*,22,->'
'TABS = ON ALL'
RETURN RC


EDBEEBVM MACRO

/* REXX */
ADDRESS ISREDIT 'MACRO'
ADDRESS ISPEXEC 'CONTROL ERRORS RETURN'
ADDRESS ISREDIT
'AUTOSAVE = OFF PROMPT'
'NUM = OFF'
'CAPS = ON'
RETURN RC

Fung Mang Hui
Analyst/Programmer (Hong Kong)

