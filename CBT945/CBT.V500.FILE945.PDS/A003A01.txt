Recreating VSAM file definitions

How many times have you needed the source definition
statements for a VSAM file, but after searching everywhere
decided they were nowhere to be found?  In turn, you probably
recreated the definition manually by examining all the detail
from a catalog listing of the file.

After being frustrated by this a couple of times, I decided to
write a program that would dynamically recreate the definition
statements for me.  The program, called VSGETDEF,
interfaces with IDCAMS and extracts information from the
output from LISTCAT.  The recreated definition statements are
output to a PDS member described in the JCL.  By extracting
information from LISTCAT, instead of going directly into the
ICF catalog, the program is fairly well insulated against catalog
structure changes.  This is illustrated by the fact that the
program was originally written under DFP 2.3 but continues to
function without modification with DFP 3.2.

The VSAM files for which you want definitions recreated are
input to the program via a REQUESTS DD statement.  In
addition to the VSAM file, the program looks for three flags
relative to each definition being produced.  The first flag
indicates whether or not to include alternate index definitions in
the output.  A value of Y indicates that they are to be included.
This can be very beneficial if the file has associated AIXs
which also need their definitions recreated.  The AIX itself
could be input individually to get that single definition if
desired.  Next is the path flag.  Again, if there are associated
path definitions, the information is extracted to recreate the
paths, and the flag value is Y.  Finally there is the catalog flag.
In most shops the alias high-level qualifier of a dataset is used
to put a file definition into the proper catalog.  Thus, it is not
necessary, and in fact undesirable, to code the CATALOG
statement.  If you change the name of a cluster after recreating
its definition you don't have to be concerned about possibly
changing the catalog.  VSGETDEF will only produce
CATALOG statements if a value of Y is coded in the catalog
flag.  Logic for all VSAM file types (INDEXED,
NONINDEXED, NUMBERED, and LINEAR) is included.
Output from the program is complete in that it handles all
possible DEFINE parameters as of DFP 2.3.


SAMPLE JCL TO RUN VSGETDEF

//EISHMANX JOB  (),'VSGETDEF',MSGCLASS=1
//*
//STEP1    EXEC PGM=IEFBR14
//DD1      DD   DSN=EISHMAN.TEMP,DISP=(,CATLG),
//  UNIT=SYSWK,SPACE=(CYL,1),
//  DCB=(LRECL=80,BLKSIZE=6800,RECFM=FB)
//*
//STEP2    EXEC PGM=VSGETDEF
//STEPLIB  DD   DSN=EISHMAN.LIB.LOAD,DISP=SHR
//IDCSYSIN DD   DSN=EISHMAN.TEMP,DISP=(OLD,DELETE)
//SYSIN    DD   DSN=EISHMAN.TEMP,DISP=OLD
//DEFSTMTS DD   DSN=EISHMAN.LIB.DATA(VSGETDEF),DISP=SHR
//SYSOUT   DD   SYSOUT=*
//SYSPRINT DD   UNIT=VIO,SPACE=(CYL,1)
//*     REQUESTS INPUT LAYOUT BY COLUMN:
//*           1 - Y = INCLUDE ALTERNATE INDEX DEFINITIONS
//*               N = EXCLUDE ALTERNATE INDEX DEFINITIONS (DEFAULT)
//*           2 - Y = INCLUDE PATH DEFINITIONS
//*               N = EXCLUDE PATH DEFINITIONS (DEFAULT)
//*           3 - Y = INCLUDE CATALOG DEFINITION STATEMENT
//*               N = EXCLUDE CATALOG DEFINITION STATEMENT (DEFAULT)
//*        4-47 - VSAM DATASET NAME, LEFT JUSTIFIED
//REQUESTS DD  *
YYNEISHMAN.TEST.VSAM.FILE
NNYSYS1.MAN1
NNYMVSSMPE.GLOBAL.CSI


SAMPLE OUTPUT

/*     CLUSTER DEFINITION FOLLOWS FOR:                   */
/*     EISHMAN.TEST.VSAM.FILE                            */
DEFINE CLUSTER (NAME(EISHMAN.TEST.VSAM.FILE                  ) -
INDEXED    -
SPANNED    -
SHAREOPTIONS(2 3) -
KEYS(064 0000) -
RECORDSIZE(00080 00080) -
FREESPACE(010 010) -
MASTERPW(   MSTPW) -
ATTEMPTS(05) -
UPDATEPW(   UPDPW) -
CONTROLPW(  CNTLPW) -
READPW(    RDPW) -
CODE(  EICODE) -
AUTHORIZATION(  ERNIES DATA                                  ) -
BUFFERSPACE(08704) -
    ERASE -
RECOVERY -
WRITECHECK   -
ORDERED   -
CYLINDERFAULT -
DESTAGEWAIT -
TO(1995155) -
OWNER( EISHMAN) -
KEYRANGE( -
(X'F040404040404040404040404040404040404040404040404040404040404040+
4040404040404040404040404040404040404040404040404040404040404040' -
X'F1404040404040404040404040404040404040404040404040404040404040404+
040404040404040404040404040404040404040404040404040404040404040') -
) -
 NOREUSE) -
DATA ( -
NAME(EISHMAN.TEST.VSAM.FILE.DATE                             ) -
VOLUMES(WORK01 WORK02                                        ) -
EXCEPTIONEXIT(   E1MOD) -
CONTROLINTERVALSIZE(04096) -
TRACKS   (0004 0004) ) -
INDEX ( -
NAME(EISHMAN.TEST.VSAM.FILE.INDEX                            ) -
REPLICATE   -
IMBED   -
VOLUMES(WORK01 WORK02                                        ) -
CONTROLINTERVALSIZE(00512) -
EXCEPTIONEXIT(   E1MOD) -
TRACKS   (0001 0001)) -
CATALOG(SYS1.VTST002.TCAT                                    )
/*     ALTERNATE INDEX DEFINITION FOLLOWS FOR:          */
/*     EISHMAN.TEST.VSAM.FILE.AIX                       */
DEFINE AIX (NAME(EISHMAN.TEST.VSAM.FILE.AIX                  ) -
RELATE(EISHMAN.TEST.VSAM.FILE                                ) -
NOUPGRADE -
UNIQUEKEY    -
SHAREOPTIONS(2 3) -
KEYS(064 0005) -
RECORDSIZE(00080 00080) -
FREESPACE(000 000) -
MASTERPW(   ERNIE) -
ATTEMPTS(05) -
UPDATEPW(   UPDPW) -
CONTROLPW(   ERNIE) -
READPW(   ERNIE) -
CODE(  EICODE) -
AUTHORIZATION(   ERNIE DATA                                  ) -
BUFFERSPACE(08704) -
  NOERASE -
RECOVERY -
NOWRITECHECK -
UNORDERED -
CYLINDERFAULT -
TO(1995155) -
OWNER( EISHMAN) -
 NOREUSE) -
DATA ( -
NAME(EISHMAN.TEST.VSAM.FILE.AIX.DATA                         ) -
VOLUMES(WORK03                                               ) -
EXCEPTIONEXIT(   E1MOD) -
CONTROLINTERVALSIZE(04096) -
TRACKS   (0002 0002)) -
INDEX ( -
NAME(EISHMAN.TEST.VSAM.FILE.AIX.INDEX                        ) -
NOREPLICATE -
NOIMBED -
VOLUMES(WORK03)                                              ) -
CONTROLINTERVALSIZE(00512) -
EXCEPTIONEXIT(   E1MOD) -
TRACKS   (0001 0001)) -
CATALOG(SYS1.VTST002.TCAT                                    )
/*     PATH DEFINITION FOLLOWS FOR:                   */
/*     EISHMAN.TEST.VSAM.FILE.PATH                    */
DEFINE PATH (NAME(EISHMAN.TEST.VSAM.FILE.PATH                ) -
PATHENTRY(EISHMAN.TEST.VSAM.FILE.AIX                         ) -
NOUPDATE ) -
CATALOG(SYS1.VTST002.TCAT                                    )


VSGETDEF

       IDENTIFICATION DIVISION.
       PROGRAM-ID. VSGETDEF.
       AUTHOR.     ERNIE ISHMAN.
       ENVIRONMENT DIVISION.
       CONFIGURATION SECTION.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
           SELECT REQUESTS ASSIGN TO REQUESTS.
           SELECT DEFSTMTS ASSIGN TO DEFSTMTS.
           SELECT IDCSYSIN ASSIGN TO IDCSYSIN.
           SELECT SYSPRINT ASSIGN TO SYSPRINT.
       DATA DIVISION.
       FILE SECTION.
       FD  IDCSYSIN
           LABEL RECORDS ARE STANDARD
           RECORDING MODE F
           BLOCK 0 RECORDS.
       01  IDCSYSIN-RECORD     PIC X(80).
       FD  DEFSTMTS
           LABEL RECORDS ARE STANDARD
           RECORDING MODE F
           BLOCK 0 RECORDS.
       01  DEFSTMTS-RECORD     PIC X(80).
       FD  SYSPRINT
           LABEL RECORDS ARE STANDARD
           RECORDING MODE V
           BLOCK 0 RECORDS.
       01  SYSPRINT-RECORD     PIC X(125).
       FD  REQUESTS
           LABEL RECORDS ARE STANDARD
           RECORDING MODE F
           BLOCK 0 RECORDS.
       01  REQUESTS-RECORD.
           03  RR-AIX-FLAG          PIC X.
           03  RR-PATH-FLAG         PIC X.
           03  RR-CATALOG-FLAG      PIC X.
           03  RR-DSN               PIC X(44).
           03  FILLER               PIC X(33).
       WORKING-STORAGE SECTION.
       01  LISTC-RECORD.
           03  FILLER                  PIC X.
           03  LR-COLS-2-4             PIC X(3).
           03  LR-COLS-5-61.
               05  FILLER              PIC XX.
               05  LR-COLS-7-12        PIC X(6).
               05  FILLER              PIC X(5).
               05  LR-COLS-18-61.
                   07  FILLER          PIC X(9).
                   07  LR-COLS-27-61-GROUP.
                       09  LR-COLS-27-61 OCCURS 35 TIMES PIC X.
                   07  LR-COLS-27-61-REDEF REDEFINES
                       LR-COLS-27-61-GROUP.
                       09  LR-COLS-27-32   PIC X(6).
                       09  FILLER          PIC X(21).
                       09  LR-COLS-54-57   PIC X(4).
                       09  FILLER          PIC X.
                       09  LR-COLS-59-61   PIC X(3).
           03  LR-COLS-5-61-REDEF REDEFINES LR-COLS-5-61.
               05  LR-COLS-5-8         PIC X(4).
               05  LR-COLS-9-17        PIC X(9).
               05  LR-COLS-18          PIC X.
               05  FILLER              PIC X.
               05  LR-COLS-20          PIC X.
               05  FILLER              PIC X(4).
               05  LR-COLS-25-32-GROUP.
                   07  LR-COLS-25-32 OCCURS 8 TIMES PIC X.
               05  FILLER              PIC X(5).
               05  LR-COLS-38-47       PIC X(10).
               05  LR-COLS-48-50       PIC X(3).
               05  FILLER              PIC XX.
               05  LR-COLS-53-61.
                   07  FILLER          PIC X.
                   07  LR-COLS-54-61 OCCURS 8 TIMES PIC X.
               05  LR-COLS-53-61-REDEF REDEFINES LR-COLS-53-61.
                   07  FILLER          PIC X(4).
                   07  LR-COLS-57-61 OCCURS 5 TIMES PIC X.
           03  FILLER                  PIC X(5).
           03  LR-COLS-67-76           PIC X(10).
           03  FILLER                  PIC X(4).
           03  LR-COLS-81-90.
               05  FILLER                  PIC XX.
               05  LR-COLS-83-90 OCCURS 8 TIMES PIC X.
           03  FILLER                  PIC X(5).
           03  LR-COLS-96-102          PIC X(7).
           03  FILLER                  PIC X(9).
           03  LR-COLS-112-119-GROUP.
               05  LR-COLS-112-119 OCCURS 8 TIMES PIC X.
           03  LR-COLS-112-119-REDEF REDEFINES LR-COLS-112-119-GROUP.
               05  FILLER              PIC X(3).
               05  LR-COLS-115-119 OCCURS 5 TIMES PIC X.
           03  FILLER                  PIC XX.
       01  LISTC-RECORD-REDEF REDEFINES LISTC-RECORD.
           03  FILLER                  PIC X(30).
           03  LR-COLS-31-120 OCCURS 90 TIMES PIC X.
           03  FILLER                  PIC X.
       01  LISTC-COMMAND.
           03  FILLER           PIC X(15)     VALUE ' LISTC ENTRIES('.
           03  LISTC-DSN        PIC X(44).
           03  FILLER           PIC X(5)      VALUE ') ALL'.
       01  CLUSTER-STATEMENT.
           03  FILLER           PIC X(22)
                                VALUE ' DEFINE CLUSTER (NAME('.
           03  CLUSTER-NAME     PIC X(44).
           03  FILLER           PIC X(3)      VALUE ') -'.
       01  AIX-STATEMENT.
           03  FILLER           PIC X(18)
                                VALUE ' DEFINE AIX (NAME('.
           03  AIX-NAME         PIC X(44).
           03  FILLER           PIC X(3)      VALUE ') -'.
       01  PATH-NAME-STATEMENT.
           05  FILLER           PIC X(19)
                                VALUE ' DEFINE PATH (NAME('.
           05  PATH-NAME        PIC X(44).
           05  FILLER           PIC X(3)      VALUE ') -'.
       01  PATH-ENTRY-STATEMENT.
           03  FILLER           PIC X(11)
                                VALUE ' PATHENTRY('.
           03  PATH-ENTRY       PIC X(44).
           03  FILLER           PIC X(3)      VALUE ') -'.
       01  RELATE-STATEMENT.
           03  FILLER           PIC X(8)
                                VALUE ' RELATE('.
           03  RELATE-NAME      PIC X(44).
           03  FILLER           PIC X(3)      VALUE ') -'.
       01  CATALOG-STATEMENT.
           03  FILLER  PIC X(9)  VALUE ' CATALOG('.
           03  CATALOG           PIC X(44).
           03  FILLER  PIC X     VALUE ')'.
       01  DATA-NAME-STATEMENT.
           03  FILLER  PIC X(6)  VALUE ' NAME('.
           03  DATA-NAME         PIC X(44).
           03  FILLER  PIC X(3)  VALUE ') -'.
       01  INDEX-NAME-STATEMENT.
           03  FILLER  PIC X(6)  VALUE ' NAME('.
           03  INDEX-NAME        PIC X(44).
           03  FILLER  PIC X(3)  VALUE ') -'.
       01  SHROPTIONS-STATEMENT.
           03  FILLER  PIC X(14) VALUE ' SHAREOPTIONS('.
           03  XREGSHR           PIC X.
           03  FILLER  PIC X     VALUE ' '.
           03  XSYSSHR           PIC X.
           03  FILLER  PIC X(3)  VALUE ') -'.
       01  VOLUME-DATA-STATEMENT.
           03  FILLER  PIC X(9) VALUE ' VOLUMES('.
           03  VOLUME-DATA-GROUP.
               05  DATA-VOLUME PIC X(7) OCCURS 5 TIMES.
           03  FILLER  PIC X(3)  VALUE ') -'.
       01  KEYS-STATEMENT.
           03  FILLER  PIC X(6) VALUE ' KEYS('.
           03  KEYLEN PIC X OCCURS 3 TIMES.
           03  FILLER  PIC X     VALUE ' '.
           03  RKP PIC X OCCURS 4 TIMES.
           03  FILLER  PIC X(3)  VALUE ') -'.
       01  RECORDSIZE-STATEMENT.
           03  FILLER  PIC X(12) VALUE ' RECORDSIZE('.
           03  AVGLRECL PIC X OCCURS 5 TIMES.
           03  FILLER  PIC X     VALUE ' '.
           03  MAXLRECL PIC X OCCURS 5 TIMES.
           03  FILLER  PIC X(3)  VALUE ') -'.
       01  VOLUME-INDEX-STATEMENT.
           03  FILLER  PIC X(9) VALUE ' VOLUMES('.
           03  VOLUME-INDEX-GROUP.
               05  VOLUME-INDEX PIC X(7) OCCURS 5 TIMES.
           03  FILLER  PIC X(3)  VALUE ') -'.
       01  BUFSPACE-STATEMENT.
           03  FILLER  PIC X(13) VALUE ' BUFFERSPACE('.
           03  BUFSPACE PIC X OCCURS 5 TIMES.
           03  FILLER  PIC X(3)  VALUE ') -'.
       01  EXCPEXIT-DATA-STATEMENT.
           03  FILLER  PIC X(15) VALUE ' EXCEPTIONEXIT('.
           03  EXCPEXIT-DATA-GROUP.
               05  EXCPEXIT-DATA PIC X OCCURS 8 TIMES.
           03  FILLER  PIC X(3)  VALUE ') -'.
       01  EXCPEXIT-INDEX-STATEMENT.
           03  FILLER  PIC X(15) VALUE ' EXCEPTIONEXIT('.
           03  EXCPEXIT-INDEX-GROUP.
               05  EXCPEXIT-INDEX PIC X OCCURS 8 TIMES.
           03  FILLER  PIC X(3)  VALUE ') -'.
       01  CISIZE-DATA-STATEMENT.
           03  FILLER  PIC X(21) VALUE ' CONTROLINTERVALSIZE('.
           03  CISIZE-DATA PIC X OCCURS 5 TIMES.
           03  FILLER  PIC X(3)  VALUE ') -'.
       01  CISIZE-INDEX-STATEMENT.
           03  FILLER  PIC X(21) VALUE ' CONTROLINTERVALSIZE('.
           03  CISIZE-INDEX PIC X OCCURS 5 TIMES.
           03  FILLER  PIC X(3)  VALUE ') -'.
       01  FREESPACE-STATEMENT.
           03  FILLER  PIC X(11) VALUE ' FREESPACE('.
           03  CIFREESP PIC X OCCURS 3 TIMES.
           03  FILLER  PIC X     VALUE ' '.
           03  CAFREESP PIC X OCCURS 3 TIMES.
           03  FILLER  PIC X(3)  VALUE ') -'.
       01  MASTER-PASSWORD-STATEMENT.
           03  FILLER  PIC X(10) VALUE ' MASTERPW('.
           03  MASTER-PASSWORD PIC X OCCURS 8 TIMES.
           03  FILLER  PIC X(3)  VALUE ') -'.
       01  UPDATE-PASSWORD-STATEMENT.
           03  FILLER  PIC X(10) VALUE ' UPDATEPW('.
           03  UPDATE-PASSWORD PIC X OCCURS 8 TIMES.
           03  FILLER  PIC X(3)  VALUE ') -'.
       01  CONTROL-PASSWORD-STATEMENT.
           03  FILLER  PIC X(11) VALUE ' CONTROLPW('.
           03  CONTROL-PASSWORD PIC X OCCURS 8 TIMES.
           03  FILLER  PIC X(3)  VALUE ') -'.
       01  READ-PASSWORD-STATEMENT.
           03  FILLER  PIC X(8)  VALUE ' READPW('.
           03  READ-PASSWORD PIC X OCCURS 8 TIMES.
           03  FILLER  PIC X(3)  VALUE ') -'.
       01  PASSWORD-CODE-STATEMENT.
           03  FILLER  PIC X(6)  VALUE ' CODE('.
           03  PASSWORD-CODE PIC X OCCURS 8 TIMES.
           03  FILLER  PIC X(3)  VALUE ') -'.
       01  ATTEMPTS-STATEMENT.
           03  FILLER  PIC X(10) VALUE ' ATTEMPTS('.
           03  ATTEMPTS PIC X OCCURS 2 TIMES.
           03  FILLER  PIC X(3)  VALUE ') -'.
       01  AUTHORIZATION-STATEMENT.
           03  FILLER  PIC X(15) VALUE ' AUTHORIZATION('.
           03  USVR PIC X OCCURS 8 TIMES.
           03  FILLER PIC X VALUE ' '.
           03  USAR-GROUP.
               05  USAR PIC X OCCURS 35 TIMES.
           03  FILLER  PIC X(3)  VALUE ') -'.
       01  REPLICATE-STATEMENT.
           03  FILLER          PIC X VALUE ' '.
           03  REPLICATE PIC X(11).
           03  FILLER          PIC XX VALUE ' -'.
       01  UPGRADE-STATEMENT.
           03  FILLER          PIC X VALUE ' '.
           03  UPGRADE         PIC X(9).
           03  FILLER          PIC XX VALUE ' -'.
       01  UPDATE-STATEMENT.
           03  FILLER          PIC X VALUE ' '.
           03  UPDATE          PIC X(9).
           03  FILLER          PIC XX   VALUE ') '.
           03  UPDATE-DASH     PIC X.
       01  ERASE-STATEMENT.
           03  FILLER          PIC X VALUE ' '.
           03  ERASE PIC X(9).
           03  FILLER          PIC XX VALUE ' -'.
       01  RECOVERY-STATEMENT.
           03  FILLER          PIC X VALUE ' '.
           03  RECOVERY PIC X(8).
           03  FILLER          PIC XX VALUE ' -'.
       01  CLUSTER-TYPE-STATEMENT.
           03  FILLER          PIC X VALUE ' '.
           03  CLUSTER-TYPE    PIC X(10).
           03  FILLER          PIC XX VALUE ' -'.
       01  WRITECHECK-STATEMENT.
           03  FILLER          PIC X VALUE ' '.
           03  WRITECHECK PIC X(12).
           03  FILLER          PIC XX VALUE ' -'.
       01  IMBED-STATEMENT.
           03  FILLER          PIC X VALUE ' '.
           03  IMBED         PIC X(7).
           03  FILLER          PIC XX VALUE ' -'.
       01  ORDER-STATEMENT.
           03  FILLER          PIC X VALUE ' '.
           03  ORDERED PIC X(9).
           03  FILLER          PIC XX VALUE ' -'.
       01  REUSE-STATEMENT.
           03  FILLER          PIC X VALUE ' '.
           03  REUSE           PIC X(8).
           03  FILLER          PIC X(3) VALUE ') -'.
       01  UNIQUEKEY-STATEMENT.
           03  FILLER          PIC X VALUE ' '.
           03  UNIQUEKEY PIC X(12).
           03  FILLER          PIC XX VALUE ' -'.
       01  SPANNED-STATEMENT.
           03  FILLER          PIC X VALUE ' '.
           03  SPANNED PIC X(10).
           03  FILLER          PIC XX VALUE ' -'.
       01  DATA-STATEMENT.
           03  FILLER          PIC X(9) VALUE ' DATA ( -'.
       01  INDEX-STATEMENT.
           03  FILLER          PIC X(10) VALUE ' INDEX ( -'.
       01  STAGE-STATEMENT.
           03  FILLER          PIC X VALUE ' '.
           03  STAGE           PIC X(13).
           03  FILLER          PIC XX VALUE ' -'.
       01  DESTAGE-STATEMENT  PIC X(14) VALUE ' DESTAGEWAIT -'.
       01  PAREN-STATEMENT    PIC X(4)  VALUE ' ) -'.
       01  ALLOCATION-DATA-STATEMENT.
           03  FILLER                 PIC X VALUE ' '.
           03  ALLOCATION-TYPE-DATA   PIC X(9).
           03  FILLER                 PIC X   VALUE '('.
           03  PRIMARY-DATA OCCURS 4 TIMES PIC X.
           03  FILLER                 PIC X   VALUE ' '.
           03  SECONDARY-DATA OCCURS 4 TIMES PIC X.
           03  FILLER  PIC X(3)  VALUE ')) '.
           03  ALLOCATION-DATA-DASH   PIC X.
       01  ALLOCATION-INDEX-STATEMENT.
           03  FILLER          PIC X VALUE ' '.
           03  ALLOCATION-TYPE-INDEX PIC X(9).
           03  FILLER                 PIC X   VALUE '('.
           03  PRIMARY-INDEX OCCURS 4 TIMES PIC X.
           03  FILLER                 PIC X   VALUE ' '.
           03  SECONDARY-INDEX OCCURS 4 TIMES PIC X.
           03  FILLER  PIC X(3)  VALUE ')) '.
           03  ALLOCATION-INDEX-DASH  PIC X.
       01  TO-STATEMENT.
           03  FILLER                 PIC X(4)  VALUE ' TO('.
           03  TO-YEAR                PIC X(4).
           03  TO-DAY                 PIC X(3).
           03  FILLER  PIC X(3)  VALUE ') -'.
       01  OWNER-STATEMENT.
           03  FILLER  PIC X(7) VALUE ' OWNER('.
           03  OWNER OCCURS 8 TIMES PIC X.
           03  FILLER  PIC X(3)  VALUE ') -'.
       01  KEYRANGE-STATEMENT.
           03  FILLER  PIC X(10) VALUE ' KEYRANGE('.
           03  FILLER          PIC XX VALUE ' -'.
       01  KEYRANGE-GROUP.
           03  KEYRANGE-DETAIL OCCURS 5 TIMES.
               05  LOW-KEYRANGE-DETAIL-1.
                   07  LOW-KEYRANGE-1 OCCURS 71 TIMES PIC X.
               05  LOW-KEYRANGE-DETAIL-2.
                   07  LOW-KEYRANGE-2 OCCURS 71 TIMES PIC X.
               05  HIGH-KEYRANGE-DETAIL-1.
                   07  HIGH-KEYRANGE-1 OCCURS 71 TIMES PIC X.
               05  HIGH-KEYRANGE-DETAIL-2.
                   07  HIGH-KEYRANGE-2 OCCURS 71 TIMES PIC X.
       01  COMMENT-STATEMENT-1.
           03  FILLER  PIC X(8)  VALUE ' /*'.
           03  FILLER  PIC X(49) VALUE SPACES.
           03  FILLER  PIC XX    VALUE '*/'.
       01  COMMENT-STATEMENT-2.
           03  FILLER  PIC X(8)  VALUE ' /*'.
           03  COMMENT-DSN  PIC X(44).
           03  FILLER  PIC X(7)  VALUE '     */'.
       01  COMMENT-STATEMENT-3.
           03  FILLER  PIC X(8)  VALUE ' /*'.
           03  FILLER  PIC X(31) VALUE
               'CLUSTER DEFINITION FOLLOWS FOR:'.
           03  FILLER  PIC X(18) VALUE SPACES.
           03  FILLER  PIC XX    VALUE '*/'.
       01  COMMENT-STATEMENT-4.
           03  FILLER  PIC X(8)  VALUE ' /*'.
           03  FILLER  PIC X(39) VALUE
               'ALTERNATE INDEX DEFINITION FOLLOWS FOR:'.
           03  FILLER  PIC X(10) VALUE SPACES.
           03  FILLER  PIC XX    VALUE '*/'.
       01  COMMENT-STATEMENT-5.
           03  FILLER  PIC X(8)  VALUE ' /*'.
           03  FILLER  PIC X(28) VALUE
               'PATH DEFINITION FOLLOWS FOR:'.
           03  FILLER  PIC X(21) VALUE SPACES.
           03  FILLER  PIC XX    VALUE '*/'.
       01  EOF-REQUESTS-FLAG    PIC 9 COMP VALUE 0.
           88  EOF-REQUESTS                VALUE 1.
       01  EOF-SYSPRINT-FLAG    PIC 9 COMP VALUE 0.
           88  EOF-SYSPRINT                VALUE 1.
       01  END-OF-KEYRANGE-FLAG PIC 9 COMP VALUE 0.
           88  END-OF-KEYRANGE             VALUE 1.
       01  KEYRANGE-FIRST-LINE-FLAG PIC 9 COMP VALUE 0.
           88  KEYRANGE-FIRST-LINE             VALUE 0.
       01  KEYRANGE-FLAG        PIC 9 COMP VALUE 0.
           88  NEED-KEYRANGE               VALUE 1.
       01  EXCPEXIT-DATA-FLAG   PIC 9 COMP VALUE 0.
           88  NEED-EXCPEXIT-DATA          VALUE 1.
       01  EXCPEXIT-INDEX-FLAG  PIC 9 COMP VALUE 0.
           88  NEED-EXCPEXIT-INDEX         VALUE 1.
       01  OWNER-FLAG           PIC 9 COMP VALUE 0.
           88  NEED-OWNER                  VALUE 1.
       01  TO-FLAG              PIC 9 COMP VALUE 0.
           88  NEED-TO                     VALUE 1.
       01  DESTAGE-FLAG              PIC 9 COMP VALUE 0.
           88  NEED-DESTAGE                VALUE 1.
       01  STAGE-FLAG                PIC 9 COMP VALUE 0.
           88  NEED-STAGE                  VALUE 0.
       01  VOLUME-INDEX-FLAG         PIC 9 COMP VALUE 0.
           88  NEED-VOLUME-INDEX           VALUE 1.
       01  PROCESS-AIX-FLAG          PIC 9 COMP VALUE 0.
           88  PROCESSING-AIX              VALUE 1.
       01  CONTROL-PASSWORD-FLAG     PIC 9 COMP VALUE 0.
           88  NEED-CONTROL-PASSWORD       VALUE 1.
       01  UPDATE-PASSWORD-FLAG      PIC 9 COMP VALUE 0.
           88  NEED-UPDATE-PASSWORD        VALUE 1.
       01  READ-PASSWORD-FLAG        PIC 9 COMP VALUE 0.
           88  NEED-READ-PASSWORD          VALUE 1.
       01  PASSWORD-CODE-FLAG        PIC 9 COMP VALUE 0.
           88  NEED-PASSWORD-CODE          VALUE 1.
       01  AUTHORIZATION-FLAG        PIC 9 COMP VALUE 0.
           88  NEED-AUTHORIZATION          VALUE 1.
       01  PROTECTION-GROUP-FLAG     PIC 9 COMP VALUE 0.
           88  NEED-PROTECTION-GROUP       VALUE 1.
       01  CLUSTER-TYPE-FLAG    PIC 9 COMP VALUE 0.
           88  CLUSTER-INDEXED             VALUE 1.
           88  CLUSTER-NONINDEXED          VALUE 2.
           88  CLUSTER-NUMBERED            VALUE 3.
           88  CLUSTER-LINEAR              VALUE 4.
       01  SAVE-AIX-NAME-GROUP.
           03  SAVE-AIX-NAME OCCURS 10 TIMES PIC X(44).
       01  SAVE-PATH-INFO-GROUP.
           03  SAVE-PATH-INFO OCCURS 5 TIMES.
               05  SAVE-PATH-NAME       PIC X(44).
               05  SAVE-PATH-ENTRY      PIC X(44).
               05  SAVE-UPDATE          PIC X(9).
               05  SAVE-CATALOG         PIC X(44).
       01  AIX-CNT           COMP   PIC 99  VALUE 0.
       01  VOL-CNT           COMP   PIC 9   VALUE 0.
       01  PATH-CNT          COMP   PIC 9   VALUE 0.
       01  KEYRANGE-CNT      COMP   PIC 9   VALUE 0.
       01  KEYRANGE-WORK-CNT COMP   PIC 99  VALUE 0.
       01  WORK-INDEX-1      COMP   PIC 999 VALUE 0.
       01  WORK-INDEX-2      COMP   PIC 999 VALUE 0.
       01  IDCAMS                   PIC X(6) VALUE 'IDCAMS'.
       PROCEDURE DIVISION.
       100-MAINLINE.
           OPEN INPUT REQUESTS
                OUTPUT DEFSTMTS.
           PERFORM 270-READ-REQUESTS.
           MOVE RR-DSN TO LISTC-DSN CLUSTER-NAME AIX-NAME COMMENT-DSN.
           PERFORM 140-PROCESS-REQUESTS UNTIL EOF-REQUESTS.
           PERFORM 120-SHUTDOWN.
       120-SHUTDOWN.
           CLOSE REQUESTS DEFSTMTS.
           STOP RUN.
       140-PROCESS-REQUESTS.
           OPEN OUTPUT IDCSYSIN.
           WRITE IDCSYSIN-RECORD FROM LISTC-COMMAND.
           CLOSE IDCSYSIN.
           CALL IDCAMS.
           IF RETURN-CODE NOT = 0
               DISPLAY '--    BAD RETURN CODE FROM IDCAMS     --'
               DISPLAY '-- CHECK FOR MISSPELLED CLUSTER NAME  --'
               DISPLAY '-- CLUSTER NAMED USED FOR LISTCAT:    --'
               DISPLAY '-- ' LISTC-DSN
               MOVE 12 TO RETURN-CODE
               PERFORM 120-SHUTDOWN
           ELSE
               MOVE 0 TO OWNER-FLAG VOL-CNT KEYRANGE-CNT PATH-CNT
                         EXCPEXIT-DATA-FLAG EXCPEXIT-INDEX-FLAG
                         TO-FLAG DESTAGE-FLAG STAGE-FLAG
                         EOF-SYSPRINT-FLAG KEYRANGE-FLAG
                         PROCESS-AIX-FLAG PROTECTION-GROUP-FLAG
                         CONTROL-PASSWORD-FLAG UPDATE-PASSWORD-FLAG
                         READ-PASSWORD-FLAG PASSWORD-CODE-FLAG
                         AUTHORIZATION-FLAG CLUSTER-TYPE-FLAG
               MOVE SPACES TO VOLUME-DATA-GROUP VOLUME-INDEX-GROUP
                              KEYRANGE-GROUP EXCPEXIT-DATA-GROUP
                              EXCPEXIT-INDEX-GROUP
               OPEN INPUT SYSPRINT
               PERFORM 280-READ-SYSPRINT
               PERFORM 170-PRIME-DEFINE-FIELDS
                       UNTIL EOF-SYSPRINT
               CLOSE SYSPRINT
               PERFORM 190-WRITE-DEFINE-STATEMENTS
               IF AIX-CNT > 0
                   MOVE SAVE-AIX-NAME(AIX-CNT) TO
                        LISTC-DSN AIX-NAME COMMENT-DSN
                   SUBTRACT 1 FROM AIX-CNT
               ELSE
                   PERFORM 270-READ-REQUESTS
                   MOVE RR-DSN TO LISTC-DSN CLUSTER-NAME
                                  AIX-NAME COMMENT-DSN.
       170-PRIME-DEFINE-FIELDS.
           IF LR-COLS-7-12 = 'IN-CAT'
               MOVE LR-COLS-18-61 TO CATALOG
           ELSE
           IF LR-COLS-38-47 = 'EXPIRATION'
               PERFORM 260-CHECK-EXPIRATION-DATE
           ELSE
           IF LR-COLS-2-4 = 'AIX'
               MOVE 1 TO PROCESS-AIX-FLAG
           ELSE
           IF LR-COLS-9-17 = 'CLUSTER--'
               MOVE LR-COLS-18-61 TO RELATE-NAME
           ELSE
           IF LR-COLS-9-17 = 'NOUPGRADE'
               MOVE LR-COLS-9-17 TO UPGRADE
           ELSE
           IF LR-COLS-9-17 = 'UPGRADE  '
               MOVE LR-COLS-9-17 TO UPGRADE
           ELSE
           IF LR-COLS-7-12 = 'PROTEC' AND LR-COLS-18 = ' '
               PERFORM 200-GET-PROTECTION-GROUP
           ELSE
           IF LR-COLS-7-12 = '  AIX-' AND RR-AIX-FLAG = 'Y'
               ADD 1 TO AIX-CNT
               IF AIX-CNT > 10
                   DISPLAY '--  VSGETDEF CANNOT HANDLE FILES WITH --'
                   DISPLAY '--  MORE THAN TEN ALTERNATE INDEXES   --'
                   MOVE 12 TO RETURN-CODE
                   PERFORM 120-SHUTDOWN
               ELSE
                   MOVE LR-COLS-18-61 TO SAVE-AIX-NAME(AIX-CNT)
           ELSE
           IF LR-COLS-7-12 = '  OWNE' AND LR-COLS-25-32(3) NOT = '('
               MOVE 1 TO WORK-INDEX-1 OWNER-FLAG
               PERFORM 259-GET-OWNER 8 TIMES
           ELSE
           IF LR-COLS-5-8  = 'DATA'
               MOVE LR-COLS-18-61 TO DATA-NAME
               PERFORM 175-GET-DATA-COMPONENT-FIELDS UNTIL
                   EOF-SYSPRINT.
           IF NOT EOF-SYSPRINT
              PERFORM 280-READ-SYSPRINT.
       175-GET-DATA-COMPONENT-FIELDS.
           IF LR-COLS-9-17 = 'KEYLEN---'
               MOVE 6 TO WORK-INDEX-1
               MOVE 1 TO WORK-INDEX-2
               PERFORM 176-GET-KEYLEN 3 TIMES
               MOVE 1 TO WORK-INDEX-1
               PERFORM 178-GET-AVGLRECL 5 TIMES
               MOVE 4 TO WORK-INDEX-1
               MOVE 1 TO WORK-INDEX-2
               PERFORM 181-GET-BUFSPACE 5 TIMES
               MOVE 1 TO WORK-INDEX-1
               PERFORM 183-GET-CISIZE-DATA 5 TIMES
           ELSE
           IF LR-COLS-9-17 = 'RKP------'
               MOVE 5 TO WORK-INDEX-1
               MOVE 1 TO WORK-INDEX-2
               PERFORM 177-GET-RKP 4 TIMES
               MOVE 1 TO WORK-INDEX-1
               PERFORM 179-GET-MAXLRECL 5 TIMES
               IF LR-COLS-83-90(3) = '('
                   MOVE SPACES TO EXCPEXIT-DATA-GROUP
               ELSE
                   MOVE 1 TO WORK-INDEX-1
                   PERFORM 182-GET-EXCPEXIT-DATA 8 TIMES
           ELSE
           IF LR-COLS-48-50 = '%CI'
               MOVE 3 TO WORK-INDEX-1
               MOVE 1 TO WORK-INDEX-2
               PERFORM 184-GET-CIFREESP 3 TIMES
           ELSE
           IF LR-COLS-48-50 = '%CA'
               MOVE 3 TO WORK-INDEX-1
               MOVE 1 TO WORK-INDEX-2
               PERFORM 185-GET-CAFREESP 3 TIMES
           ELSE
           IF LR-COLS-9-17 = 'SHROPTNS('
               MOVE LR-COLS-18 TO XREGSHR
               MOVE LR-COLS-20 TO XSYSSHR
               PERFORM 185-CHECK-DATA-ATTRIBUTES
           ELSE
           IF LR-COLS-9-17 = 'SPACE-TYP'
               PERFORM 212-GET-DATA-ALLOCATION
           ELSE
           IF LR-COLS-9-17 = 'VOLSER---'
               ADD 1 TO VOL-CNT
               IF VOL-CNT > 5
                   DISPLAY '--  VSGETDEF CANNOT HANDLE FILES WITH --'
                   DISPLAY '--  MORE THAN FIVE VOLUMES SPECIFIED  --'
                   MOVE 12 TO RETURN-CODE
                   PERFORM 120-SHUTDOWN
               ELSE
                   MOVE LR-COLS-27-32 TO DATA-VOLUME(VOL-CNT)
           ELSE
           IF LR-COLS-9-17 = 'LOW-KEY--'
               ADD 1 TO KEYRANGE-CNT
               MOVE 1 TO KEYRANGE-FLAG WORK-INDEX-1
               MOVE 0 TO END-OF-KEYRANGE-FLAG KEYRANGE-FIRST-LINE-FLAG
               MOVE ' ' TO LOW-KEYRANGE-1(KEYRANGE-CNT 1)
               MOVE '(' TO LOW-KEYRANGE-1(KEYRANGE-CNT 2)
               MOVE 'X' TO LOW-KEYRANGE-1(KEYRANGE-CNT 3)
               MOVE '''' TO LOW-KEYRANGE-1(KEYRANGE-CNT 4)
               MOVE 5 TO KEYRANGE-WORK-CNT
               PERFORM 215-GET-LOW-KEY UNTIL END-OF-KEYRANGE
           ELSE
           IF LR-COLS-9-17 = 'HIGH-KEY-'
               MOVE 1 TO WORK-INDEX-1
               MOVE 0 TO END-OF-KEYRANGE-FLAG KEYRANGE-FIRST-LINE-FLAG
               MOVE ' ' TO HIGH-KEYRANGE-1(KEYRANGE-CNT 1)
               MOVE 'X' TO HIGH-KEYRANGE-1(KEYRANGE-CNT 2)
               MOVE '''' TO HIGH-KEYRANGE-1(KEYRANGE-CNT 3)
               MOVE 4 TO KEYRANGE-WORK-CNT
               PERFORM 216-GET-HIGH-KEY UNTIL END-OF-KEYRANGE
           ELSE
           IF LR-COLS-5-8  = 'INDE'
               MOVE LR-COLS-18-61 TO INDEX-NAME
               MOVE 0 TO VOL-CNT
               PERFORM 180-GET-INDEX-COMPONENT-FIELDS UNTIL
                   EOF-SYSPRINT.
           IF NOT EOF-SYSPRINT
              PERFORM 280-READ-SYSPRINT.
       176-GET-KEYLEN.
           IF LR-COLS-25-32(WORK-INDEX-1) = '-'
               MOVE 0 TO KEYLEN(WORK-INDEX-2)
           ELSE
               MOVE LR-COLS-25-32(WORK-INDEX-1) TO
                    KEYLEN(WORK-INDEX-2).
           ADD 1 TO WORK-INDEX-1 WORK-INDEX-2.
       177-GET-RKP.
           IF LR-COLS-25-32(WORK-INDEX-1) = '-'
               MOVE 0 TO RKP(WORK-INDEX-2)
           ELSE
               MOVE LR-COLS-25-32(WORK-INDEX-1) TO
                    RKP(WORK-INDEX-2).
           ADD 1 TO WORK-INDEX-1 WORK-INDEX-2.
       178-GET-AVGLRECL.
           IF LR-COLS-57-61(WORK-INDEX-1) = '-'
               MOVE 0 TO AVGLRECL(WORK-INDEX-1)
           ELSE
               MOVE LR-COLS-57-61(WORK-INDEX-1) TO
                    AVGLRECL(WORK-INDEX-1).
           ADD 1 TO WORK-INDEX-1.
       179-GET-MAXLRECL.
           IF LR-COLS-57-61(WORK-INDEX-1) = '-'
               MOVE 0 TO MAXLRECL(WORK-INDEX-1)
           ELSE
               MOVE LR-COLS-57-61(WORK-INDEX-1) TO
                    MAXLRECL(WORK-INDEX-1).
           ADD 1 TO WORK-INDEX-1.
       180-GET-INDEX-COMPONENT-FIELDS.
           IF LR-COLS-9-17 = 'KEYLEN---'
               MOVE 1 TO WORK-INDEX-1
               PERFORM 217-GET-CISIZE-INDEX 5 TIMES
           ELSE
           IF LR-COLS-9-17 = 'RKP------'
               IF LR-COLS-83-90(3) = '('
                   MOVE SPACES TO EXCPEXIT-INDEX-GROUP
               ELSE
                   MOVE 1 TO WORK-INDEX-1
                   PERFORM 218-GET-EXCPEXIT-INDEX 8 TIMES
           ELSE
           IF LR-COLS-9-17 = 'SHROPTNS('
               IF LR-COLS-96-102 = 'NOREPLI'
                   MOVE 'NOREPLICATE' TO REPLICATE
               ELSE
                   MOVE 'REPLICATE' TO REPLICATE
           ELSE
           IF LR-COLS-9-17 = 'SPACE-TYP'
               PERFORM 220-GET-ALLOCATION-INDEX
           ELSE
           IF LR-COLS-9-17 = 'VOLSER---'
               MOVE 1 TO WORK-INDEX-1 VOLUME-INDEX-FLAG
               PERFORM 223-CHECK-VOLUME-INDEX VOL-CNT TIMES
               IF NEED-VOLUME-INDEX
                   ADD 1 TO VOL-CNT
                   IF VOL-CNT > 5
                     DISPLAY '--  VSGETDEF CANNOT HANDLE FILES WITH --'
                     DISPLAY '--  MORE THAN FIVE VOLUMES SPECIFIED  --'
                     MOVE 12 TO RETURN-CODE
                     PERFORM 120-SHUTDOWN
                   ELSE
                       MOVE LR-COLS-27-32 TO
                            VOLUME-INDEX(VOL-CNT)
               ELSE
                   NEXT SENTENCE
           ELSE
               IF LR-COLS-5-8  = 'PATH' AND RR-PATH-FLAG = 'Y'
                       PERFORM 195-GET-PATH-INFO UNTIL EOF-SYSPRINT.
           IF NOT EOF-SYSPRINT
               PERFORM 280-READ-SYSPRINT.
       181-GET-BUFSPACE.
           IF LR-COLS-83-90(WORK-INDEX-1) = '-'
               MOVE 0 TO BUFSPACE(WORK-INDEX-2)
           ELSE
               MOVE LR-COLS-83-90(WORK-INDEX-1) TO
                    BUFSPACE(WORK-INDEX-2).
           ADD 1 TO WORK-INDEX-1 WORK-INDEX-2.
       182-GET-EXCPEXIT-DATA.
           IF LR-COLS-83-90(WORK-INDEX-1) = '-'
               MOVE SPACE TO EXCPEXIT-DATA(WORK-INDEX-1)
           ELSE
               MOVE 1 TO EXCPEXIT-DATA-FLAG
               MOVE LR-COLS-83-90(WORK-INDEX-1) TO
                    EXCPEXIT-DATA(WORK-INDEX-1).
           ADD 1 TO WORK-INDEX-1.
       183-GET-CISIZE-DATA.
           IF LR-COLS-115-119(WORK-INDEX-1) = '-'
               MOVE 0 TO CISIZE-DATA(WORK-INDEX-1)
           ELSE
               MOVE LR-COLS-115-119(WORK-INDEX-1) TO
                    CISIZE-DATA(WORK-INDEX-1).
           ADD 1 TO WORK-INDEX-1.
       184-GET-CIFREESP.
           IF LR-COLS-57-61(WORK-INDEX-1) = '-'
               MOVE 0 TO CIFREESP(WORK-INDEX-2)
           ELSE
               MOVE LR-COLS-57-61(WORK-INDEX-1) TO
                    CIFREESP(WORK-INDEX-2).
           ADD 1 TO WORK-INDEX-1 WORK-INDEX-2.
       185-GET-CAFREESP.
           IF LR-COLS-57-61(WORK-INDEX-1) = '-'
               MOVE 0 TO CAFREESP(WORK-INDEX-2)
           ELSE
               MOVE LR-COLS-57-61(WORK-INDEX-1) TO
                    CAFREESP(WORK-INDEX-2).
           ADD 1 TO WORK-INDEX-1 WORK-INDEX-2.
       185-CHECK-DATA-ATTRIBUTES.
           MOVE LR-COLS-25-32-GROUP TO RECOVERY.
           MOVE LR-COLS-53-61 TO ERASE.
           IF LR-COLS-67-76 = 'INDEXED   '
               MOVE 1 TO CLUSTER-TYPE-FLAG
               MOVE 'INDEXED' TO CLUSTER-TYPE
           ELSE
               IF LR-COLS-67-76 = 'NONINDEXED'
                   MOVE 2 TO CLUSTER-TYPE-FLAG
                   MOVE 'NONINDEXED' TO CLUSTER-TYPE
               ELSE
                   IF LR-COLS-67-76 = 'NUMBERED  '
                       MOVE 3 TO CLUSTER-TYPE-FLAG
                       MOVE 'NUMBERED' TO CLUSTER-TYPE
                   ELSE
                       IF LR-COLS-67-76 = 'LINEAR'
                           MOVE 4 TO CLUSTER-TYPE-FLAG
                           MOVE 'LINEAR' TO CLUSTER-TYPE.
           IF LR-COLS-81-90 = 'NOWRITECHK'
               MOVE 'NOWRITECHECK' TO WRITECHECK
           ELSE
               MOVE 'WRITECHECK' TO WRITECHECK.
           MOVE LR-COLS-96-102 TO IMBED.
           PERFORM 280-READ-SYSPRINT.
           MOVE LR-COLS-9-17 TO ORDERED.
           MOVE LR-COLS-25-32-GROUP TO REUSE.
           MOVE LR-COLS-38-47 TO SPANNED.
           IF LR-COLS-53-61 = 'UNIQUEKEY'
               MOVE 'UNIQUEKEY' TO UNIQUEKEY
           ELSE
               IF LR-COLS-53-61 = 'ONUNIQKEY'
                   MOVE 'NONUNIQUEKEY' TO UNIQUEKEY.
           IF LR-COLS-53-61 = ' CYLFAULT' OR
              LR-COLS-67-76 = 'CYLFAULT  '
                   MOVE 'CYLINDERFAULT' TO STAGE
           ELSE
               IF LR-COLS-53-61 = '     BIND' OR
                  LR-COLS-67-76 = 'BIND      '
                   MOVE LR-COLS-53-61 TO STAGE
               ELSE
                   MOVE 1 TO STAGE-FLAG.
           IF LR-COLS-81-90 = 'DSTGWAIT  ' OR
              LR-COLS-67-76 = 'DSTGWAIT  ' OR
              LR-COLS-53-61 = ' DSTGWAIT'
                   MOVE 1 TO DESTAGE-FLAG.
       190-WRITE-DEFINE-STATEMENTS.
           WRITE DEFSTMTS-RECORD FROM COMMENT-STATEMENT-1.
           IF PROCESSING-AIX
               WRITE DEFSTMTS-RECORD FROM COMMENT-STATEMENT-4
               WRITE DEFSTMTS-RECORD FROM COMMENT-STATEMENT-2
               WRITE DEFSTMTS-RECORD FROM COMMENT-STATEMENT-1
               WRITE DEFSTMTS-RECORD FROM AIX-STATEMENT
               WRITE DEFSTMTS-RECORD FROM RELATE-STATEMENT
               WRITE DEFSTMTS-RECORD FROM UPGRADE-STATEMENT
               WRITE DEFSTMTS-RECORD FROM UNIQUEKEY-STATEMENT
           ELSE
               WRITE DEFSTMTS-RECORD FROM COMMENT-STATEMENT-3
               WRITE DEFSTMTS-RECORD FROM COMMENT-STATEMENT-2
               WRITE DEFSTMTS-RECORD FROM COMMENT-STATEMENT-1
               WRITE DEFSTMTS-RECORD FROM CLUSTER-STATEMENT
               WRITE DEFSTMTS-RECORD FROM CLUSTER-TYPE-STATEMENT
               IF NOT CLUSTER-LINEAR
                   WRITE DEFSTMTS-RECORD FROM SPANNED-STATEMENT.
           WRITE DEFSTMTS-RECORD FROM SHROPTIONS-STATEMENT.
           IF CLUSTER-INDEXED
               WRITE DEFSTMTS-RECORD FROM KEYS-STATEMENT.
           IF NOT CLUSTER-LINEAR
               WRITE DEFSTMTS-RECORD FROM RECORDSIZE-STATEMENT
               WRITE DEFSTMTS-RECORD FROM FREESPACE-STATEMENT.
           IF NEED-PROTECTION-GROUP
               WRITE DEFSTMTS-RECORD FROM MASTER-PASSWORD-STATEMENT
               WRITE DEFSTMTS-RECORD FROM ATTEMPTS-STATEMENT.
           IF NEED-UPDATE-PASSWORD
               WRITE DEFSTMTS-RECORD FROM UPDATE-PASSWORD-STATEMENT.
           IF NEED-CONTROL-PASSWORD
               WRITE DEFSTMTS-RECORD FROM CONTROL-PASSWORD-STATEMENT.
           IF NEED-READ-PASSWORD
               WRITE DEFSTMTS-RECORD FROM READ-PASSWORD-STATEMENT.
           IF NEED-PASSWORD-CODE
               WRITE DEFSTMTS-RECORD FROM PASSWORD-CODE-STATEMENT.
           IF NEED-AUTHORIZATION
               WRITE DEFSTMTS-RECORD FROM AUTHORIZATION-STATEMENT.
           WRITE DEFSTMTS-RECORD FROM BUFSPACE-STATEMENT.
           WRITE DEFSTMTS-RECORD FROM ERASE-STATEMENT.
           WRITE DEFSTMTS-RECORD FROM RECOVERY-STATEMENT.
           WRITE DEFSTMTS-RECORD FROM WRITECHECK-STATEMENT.
           WRITE DEFSTMTS-RECORD FROM ORDER-STATEMENT.
           IF NEED-STAGE
               WRITE DEFSTMTS-RECORD FROM STAGE-STATEMENT.
           IF NEED-DESTAGE
               WRITE DEFSTMTS-RECORD FROM DESTAGE-STATEMENT.
           IF NEED-TO
               WRITE DEFSTMTS-RECORD FROM TO-STATEMENT.
           IF NEED-OWNER
               WRITE DEFSTMTS-RECORD FROM OWNER-STATEMENT.
           IF NEED-KEYRANGE
               WRITE DEFSTMTS-RECORD FROM KEYRANGE-STATEMENT
               MOVE 0 TO WORK-INDEX-1
               PERFORM 211-WRITE-KEYRANGE KEYRANGE-CNT TIMES
               WRITE DEFSTMTS-RECORD FROM PAREN-STATEMENT.
           WRITE DEFSTMTS-RECORD FROM REUSE-STATEMENT.
           WRITE DEFSTMTS-RECORD FROM DATA-STATEMENT.
           WRITE DEFSTMTS-RECORD FROM DATA-NAME-STATEMENT.
           WRITE DEFSTMTS-RECORD FROM VOLUME-DATA-STATEMENT.
           IF NEED-EXCPEXIT-DATA
               WRITE DEFSTMTS-RECORD FROM EXCPEXIT-DATA-STATEMENT.
           IF NOT CLUSTER-LINEAR
               WRITE DEFSTMTS-RECORD FROM CISIZE-DATA-STATEMENT.
           IF CLUSTER-INDEXED OR RR-CATALOG-FLAG = 'Y'
               MOVE '-' TO ALLOCATION-DATA-DASH
           ELSE
               MOVE ' ' TO ALLOCATION-DATA-DASH.
           WRITE DEFSTMTS-RECORD FROM ALLOCATION-DATA-STATEMENT.
           IF CLUSTER-INDEXED
               PERFORM 210-WRITE-INDEX-STATEMENTS.
           IF RR-CATALOG-FLAG = 'Y'
               WRITE DEFSTMTS-RECORD FROM CATALOG-STATEMENT.
           MOVE 0 TO WORK-INDEX-1.
           PERFORM 192-WRITE-PATH-NAME-STATEMENTS PATH-CNT TIMES.
       192-WRITE-PATH-NAME-STATEMENTS.
           ADD 1 TO WORK-INDEX-1.
           MOVE SAVE-PATH-NAME(WORK-INDEX-1) TO
                PATH-NAME COMMENT-DSN.
           WRITE DEFSTMTS-RECORD FROM COMMENT-STATEMENT-1.
           WRITE DEFSTMTS-RECORD FROM COMMENT-STATEMENT-5.
           WRITE DEFSTMTS-RECORD FROM COMMENT-STATEMENT-2.
           WRITE DEFSTMTS-RECORD FROM COMMENT-STATEMENT-1.
           WRITE DEFSTMTS-RECORD FROM PATH-NAME-STATEMENT.
           MOVE SAVE-PATH-ENTRY(WORK-INDEX-1) TO PATH-ENTRY.
           WRITE DEFSTMTS-RECORD FROM PATH-ENTRY-STATEMENT.
           MOVE SAVE-UPDATE(WORK-INDEX-1) TO UPDATE.
           IF RR-CATALOG-FLAG = 'Y'
               MOVE '-' TO UPDATE-DASH
               WRITE DEFSTMTS-RECORD FROM UPDATE-STATEMENT
               MOVE SAVE-CATALOG(WORK-INDEX-1) TO CATALOG
               WRITE DEFSTMTS-RECORD FROM CATALOG-STATEMENT
           ELSE
               MOVE ' ' TO UPDATE-DASH
               WRITE DEFSTMTS-RECORD FROM UPDATE-STATEMENT.
       195-GET-PATH-INFO.
           IF LR-COLS-5-8  = 'PATH'
               ADD 1 TO PATH-CNT
               IF PATH-CNT > 5
                 DISPLAY '--  VSGETDEF CANNOT HANDLE FILES WITH --'
                 DISPLAY '--  MORE THAN FIVE PATHS SPECIFIED    --'
                 MOVE 12 TO RETURN-CODE
                 PERFORM 120-SHUTDOWN
               ELSE
                   MOVE LR-COLS-18-61 TO SAVE-PATH-NAME(PATH-CNT).
           IF LR-COLS-9-17 = 'AIX------'
               MOVE LR-COLS-18-61 TO SAVE-PATH-ENTRY(PATH-CNT)
           ELSE
           IF LR-COLS-9-17 = 'CLUSTER--'
               MOVE LR-COLS-18-61 TO SAVE-PATH-ENTRY(PATH-CNT)
           ELSE
           IF LR-COLS-7-12 = 'IN-CAT'
               MOVE LR-COLS-18-61 TO SAVE-CATALOG(PATH-CNT)
           ELSE
           IF LR-COLS-9-17 = 'NOUPDATE '
               MOVE LR-COLS-9-17 TO SAVE-UPDATE(PATH-CNT)
           ELSE
           IF LR-COLS-9-17 = 'UPDATE   '
               MOVE LR-COLS-9-17 TO SAVE-UPDATE(PATH-CNT).
           PERFORM 280-READ-SYSPRINT.
       200-GET-PROTECTION-GROUP.
           PERFORM 280-READ-SYSPRINT.
           MOVE 1 TO WORK-INDEX-1 PROTECTION-GROUP-FLAG.
           PERFORM 201-GET-MASTER-PASSWORD 8 TIMES.
           IF LR-COLS-54-61(3) NOT = '('
               MOVE 1 TO WORK-INDEX-1 UPDATE-PASSWORD-FLAG
               PERFORM 202-GET-UPDATE-PASSWORD 8 TIMES.
           IF LR-COLS-83-90(3) NOT = '('
               MOVE 1 TO WORK-INDEX-1 PASSWORD-CODE-FLAG
               PERFORM 203-GET-PASSWORD-CODE 8 TIMES.
           PERFORM 280-READ-SYSPRINT.
           IF LR-COLS-25-32(3) NOT = '('
               MOVE 1 TO WORK-INDEX-1 CONTROL-PASSWORD-FLAG
               PERFORM 204-GET-CONTROL-PASSWORD 8 TIMES.
           IF LR-COLS-54-61(3) NOT = '('
               MOVE 1 TO WORK-INDEX-1 READ-PASSWORD-FLAG
               PERFORM 205-GET-READ-PASSWORD 8 TIMES.
           IF LR-COLS-83-90(7) = '-'
               MOVE 0 TO ATTEMPTS(1)
           ELSE MOVE LR-COLS-83-90(7) TO ATTEMPTS(1).
           IF LR-COLS-83-90(8) = '-'
               MOVE 0 TO ATTEMPTS(2)
           ELSE MOVE LR-COLS-83-90(8) TO ATTEMPTS(2).
           IF LR-COLS-112-119(3) NOT = '('
               MOVE 1 TO WORK-INDEX-1 AUTHORIZATION-FLAG
               PERFORM 206-GET-USVR 8 TIMES.
           PERFORM 280-READ-SYSPRINT.
           MOVE SPACES TO USAR-GROUP.
           IF LR-COLS-27-61(1) NOT = '('
               MOVE 1 TO WORK-INDEX-1
               PERFORM 207-GET-USAR 35 TIMES.
       201-GET-MASTER-PASSWORD.
           IF LR-COLS-25-32(WORK-INDEX-1) NOT = '-'
               MOVE LR-COLS-25-32(WORK-INDEX-1) TO
                    MASTER-PASSWORD(WORK-INDEX-1)
           ELSE
               MOVE SPACE TO MASTER-PASSWORD(WORK-INDEX-1).
           ADD 1 TO WORK-INDEX-1.
       202-GET-UPDATE-PASSWORD.
           IF LR-COLS-54-61(WORK-INDEX-1) NOT = '-'
               MOVE LR-COLS-54-61(WORK-INDEX-1) TO
                    UPDATE-PASSWORD(WORK-INDEX-1)
           ELSE
               MOVE SPACE TO UPDATE-PASSWORD(WORK-INDEX-1).
           ADD 1 TO WORK-INDEX-1.
       203-GET-PASSWORD-CODE.
           IF LR-COLS-83-90(WORK-INDEX-1) NOT = '-'
               MOVE LR-COLS-83-90(WORK-INDEX-1) TO
                    PASSWORD-CODE(WORK-INDEX-1)
           ELSE
               MOVE SPACE TO PASSWORD-CODE(WORK-INDEX-1).
           ADD 1 TO WORK-INDEX-1.
       204-GET-CONTROL-PASSWORD.
           IF LR-COLS-25-32(WORK-INDEX-1) NOT = '-'
               MOVE LR-COLS-25-32(WORK-INDEX-1) TO
                    CONTROL-PASSWORD(WORK-INDEX-1)
           ELSE
               MOVE SPACE TO CONTROL-PASSWORD(WORK-INDEX-1).
           ADD 1 TO WORK-INDEX-1.
       205-GET-READ-PASSWORD.
           IF LR-COLS-54-61(WORK-INDEX-1) NOT = '-'
               MOVE LR-COLS-54-61(WORK-INDEX-1) TO
                    READ-PASSWORD(WORK-INDEX-1)
           ELSE
               MOVE SPACE TO READ-PASSWORD(WORK-INDEX-1).
           ADD 1 TO WORK-INDEX-1.
       206-GET-USVR.
           IF LR-COLS-112-119(WORK-INDEX-1) NOT = '-'
               MOVE LR-COLS-112-119(WORK-INDEX-1) TO
                    USVR(WORK-INDEX-1)
           ELSE
               MOVE SPACE TO USVR(WORK-INDEX-1).
           ADD 1 TO WORK-INDEX-1.
       207-GET-USAR.
           IF LR-COLS-27-61(WORK-INDEX-1) NOT = '-'
               MOVE LR-COLS-27-61(WORK-INDEX-1) TO
                    USAR(WORK-INDEX-1)
           ELSE
               MOVE SPACE TO USAR(WORK-INDEX-1).
           ADD 1 TO WORK-INDEX-1.
       210-WRITE-INDEX-STATEMENTS.
           WRITE DEFSTMTS-RECORD FROM INDEX-STATEMENT.
           WRITE DEFSTMTS-RECORD FROM INDEX-NAME-STATEMENT.
           WRITE DEFSTMTS-RECORD FROM REPLICATE-STATEMENT.
           WRITE DEFSTMTS-RECORD FROM IMBED-STATEMENT.
           WRITE DEFSTMTS-RECORD FROM VOLUME-INDEX-STATEMENT.
           WRITE DEFSTMTS-RECORD FROM CISIZE-INDEX-STATEMENT.
           IF NEED-EXCPEXIT-INDEX
               WRITE DEFSTMTS-RECORD FROM EXCPEXIT-INDEX-STATEMENT.
           IF RR-CATALOG-FLAG = 'Y'
               MOVE '-' TO ALLOCATION-INDEX-DASH
           ELSE
               MOVE ' ' TO ALLOCATION-INDEX-DASH.
           WRITE DEFSTMTS-RECORD FROM ALLOCATION-INDEX-STATEMENT.
       211-WRITE-KEYRANGE.
           ADD 1 TO WORK-INDEX-1.
           WRITE DEFSTMTS-RECORD FROM
                 LOW-KEYRANGE-DETAIL-1(WORK-INDEX-1).
           IF LOW-KEYRANGE-1(WORK-INDEX-1 69) = '+'
               WRITE DEFSTMTS-RECORD FROM
                     LOW-KEYRANGE-DETAIL-2(WORK-INDEX-1).
           WRITE DEFSTMTS-RECORD FROM
                 HIGH-KEYRANGE-DETAIL-1(WORK-INDEX-1).
           IF HIGH-KEYRANGE-1(WORK-INDEX-1 69) = '+'
               WRITE DEFSTMTS-RECORD FROM
                     HIGH-KEYRANGE-DETAIL-2(WORK-INDEX-1).
       212-GET-DATA-ALLOCATION.
           IF LR-COLS-25-32-GROUP = 'CYLINDER'
               MOVE 'CYLINDERS' TO ALLOCATION-TYPE-DATA
           ELSE
               MOVE 'TRACKS' TO ALLOCATION-TYPE-DATA.
           PERFORM 280-READ-SYSPRINT.
           MOVE 5 TO WORK-INDEX-1.
           MOVE 1 TO WORK-INDEX-2.
           PERFORM 213-GET-PRIMARY-DATA 4 TIMES.
           MOVE 5 TO WORK-INDEX-1.
           MOVE 1 TO WORK-INDEX-2.
           PERFORM 214-GET-SECONDARY-DATA 4 TIMES.
       213-GET-PRIMARY-DATA.
           IF LR-COLS-25-32(WORK-INDEX-1) = '-'
               MOVE 0 TO PRIMARY-DATA(WORK-INDEX-2)
           ELSE
               MOVE LR-COLS-25-32(WORK-INDEX-1) TO
                    PRIMARY-DATA(WORK-INDEX-2).
           ADD 1 TO WORK-INDEX-1 WORK-INDEX-2.
       214-GET-SECONDARY-DATA.
           IF LR-COLS-25-32(WORK-INDEX-1) = '-'
               MOVE 0 TO SECONDARY-DATA(WORK-INDEX-2)
           ELSE
               MOVE LR-COLS-25-32(WORK-INDEX-1) TO
                    SECONDARY-DATA(WORK-INDEX-2).
           ADD 1 TO WORK-INDEX-1 WORK-INDEX-2.
       215-GET-LOW-KEY.
           IF KEYRANGE-WORK-CNT = 69
               MOVE '+' TO LOW-KEYRANGE-1(KEYRANGE-CNT 69)
               MOVE 1 TO KEYRANGE-FIRST-LINE-FLAG
               MOVE SPACE TO LOW-KEYRANGE-2(KEYRANGE-CNT 1)
               MOVE 2 TO KEYRANGE-WORK-CNT.
           IF WORK-INDEX-1 = 91
               PERFORM 280-READ-SYSPRINT
               IF LR-COLS-9-17 NOT =  '         '
                   MOVE 1 TO END-OF-KEYRANGE-FLAG
               ELSE
                   MOVE 1 TO WORK-INDEX-1
           ELSE
               IF LR-COLS-31-120(WORK-INDEX-1) NOT = SPACE
                   IF KEYRANGE-FIRST-LINE
                       MOVE LR-COLS-31-120(WORK-INDEX-1) TO
                            LOW-KEYRANGE-1(KEYRANGE-CNT
                            KEYRANGE-WORK-CNT)
                       ADD 1 TO WORK-INDEX-1 KEYRANGE-WORK-CNT
                   ELSE
                       MOVE LR-COLS-31-120(WORK-INDEX-1) TO
                            LOW-KEYRANGE-2(KEYRANGE-CNT
                            KEYRANGE-WORK-CNT)
                       ADD 1 TO WORK-INDEX-1 KEYRANGE-WORK-CNT
               ELSE
                   IF KEYRANGE-FIRST-LINE
                       MOVE '''' TO LOW-KEYRANGE-1
                                (KEYRANGE-CNT KEYRANGE-WORK-CNT)
                       ADD 1 TO KEYRANGE-WORK-CNT
                       MOVE ' ' TO LOW-KEYRANGE-1
                                (KEYRANGE-CNT KEYRANGE-WORK-CNT)
                       ADD 1 TO KEYRANGE-WORK-CNT
                       MOVE '-' TO LOW-KEYRANGE-1
                                (KEYRANGE-CNT KEYRANGE-WORK-CNT)
                       MOVE 1 TO END-OF-KEYRANGE-FLAG
                   ELSE
                       MOVE '''' TO LOW-KEYRANGE-2
                                (KEYRANGE-CNT KEYRANGE-WORK-CNT)
                       ADD 1 TO KEYRANGE-WORK-CNT
                       MOVE ' ' TO LOW-KEYRANGE-2
                                (KEYRANGE-CNT KEYRANGE-WORK-CNT)
                       ADD 1 TO KEYRANGE-WORK-CNT
                       MOVE '-' TO LOW-KEYRANGE-2
                                (KEYRANGE-CNT KEYRANGE-WORK-CNT)
                       MOVE 1 TO END-OF-KEYRANGE-FLAG.
       216-GET-HIGH-KEY.
           IF KEYRANGE-WORK-CNT = 69
               MOVE '+' TO HIGH-KEYRANGE-1(KEYRANGE-CNT 69)
               MOVE 1 TO KEYRANGE-FIRST-LINE-FLAG
               MOVE SPACE TO HIGH-KEYRANGE-2(KEYRANGE-CNT 1)
               MOVE 2 TO KEYRANGE-WORK-CNT.
           IF WORK-INDEX-1 = 91
               PERFORM 280-READ-SYSPRINT
               IF LR-COLS-9-17 NOT =  '         '
                   MOVE 1 TO END-OF-KEYRANGE-FLAG
               ELSE
                   MOVE 1 TO WORK-INDEX-1
           ELSE
               IF LR-COLS-31-120(WORK-INDEX-1) NOT = SPACE
                   IF KEYRANGE-FIRST-LINE
                       MOVE LR-COLS-31-120(WORK-INDEX-1) TO
                            HIGH-KEYRANGE-1(KEYRANGE-CNT
                            KEYRANGE-WORK-CNT)
                       ADD 1 TO WORK-INDEX-1 KEYRANGE-WORK-CNT
                   ELSE
                       MOVE LR-COLS-31-120(WORK-INDEX-1) TO
                            HIGH-KEYRANGE-2(KEYRANGE-CNT
                            KEYRANGE-WORK-CNT)
                       ADD 1 TO WORK-INDEX-1 KEYRANGE-WORK-CNT
               ELSE
                   IF KEYRANGE-FIRST-LINE
                       MOVE '''' TO HIGH-KEYRANGE-1
                                (KEYRANGE-CNT KEYRANGE-WORK-CNT)
                       ADD 1 TO KEYRANGE-WORK-CNT
                       MOVE ')' TO HIGH-KEYRANGE-1
                                (KEYRANGE-CNT KEYRANGE-WORK-CNT)
                       ADD 1 TO KEYRANGE-WORK-CNT
                       MOVE ' ' TO HIGH-KEYRANGE-1
                                (KEYRANGE-CNT KEYRANGE-WORK-CNT)
                       ADD 1 TO KEYRANGE-WORK-CNT
                       MOVE '-' TO HIGH-KEYRANGE-1
                                (KEYRANGE-CNT KEYRANGE-WORK-CNT)
                       MOVE 1 TO END-OF-KEYRANGE-FLAG
                   ELSE
                       MOVE '''' TO HIGH-KEYRANGE-2
                                (KEYRANGE-CNT KEYRANGE-WORK-CNT)
                       ADD 1 TO KEYRANGE-WORK-CNT
                       MOVE ')' TO HIGH-KEYRANGE-2
                                (KEYRANGE-CNT KEYRANGE-WORK-CNT)
                       ADD 1 TO KEYRANGE-WORK-CNT
                       MOVE ' ' TO HIGH-KEYRANGE-2
                                (KEYRANGE-CNT KEYRANGE-WORK-CNT)
                       ADD 1 TO KEYRANGE-WORK-CNT
                       MOVE '-' TO HIGH-KEYRANGE-2
                                (KEYRANGE-CNT KEYRANGE-WORK-CNT)
                       MOVE 1 TO END-OF-KEYRANGE-FLAG.
       217-GET-CISIZE-INDEX.
           IF LR-COLS-115-119(WORK-INDEX-1) = '-'
               MOVE 0 TO CISIZE-INDEX(WORK-INDEX-1)
           ELSE
               MOVE LR-COLS-115-119(WORK-INDEX-1) TO
                    CISIZE-INDEX(WORK-INDEX-1).
           ADD 1 TO WORK-INDEX-1.
       218-GET-EXCPEXIT-INDEX.
           IF LR-COLS-83-90(WORK-INDEX-1) = '-'
               MOVE SPACE TO EXCPEXIT-INDEX(WORK-INDEX-1)
           ELSE
               MOVE 1 TO EXCPEXIT-INDEX-FLAG
               MOVE LR-COLS-83-90(WORK-INDEX-1) TO
                    EXCPEXIT-INDEX(WORK-INDEX-1).
           ADD 1 TO WORK-INDEX-1.
       220-GET-ALLOCATION-INDEX.
           IF LR-COLS-25-32-GROUP = 'CYLINDER'
               MOVE 'CYLINDERS' TO ALLOCATION-TYPE-INDEX
           ELSE
               MOVE 'TRACKS' TO ALLOCATION-TYPE-INDEX.
           PERFORM 280-READ-SYSPRINT.
           MOVE 5 TO WORK-INDEX-1.
           MOVE 1 TO WORK-INDEX-2.
           PERFORM 221-GET-PRIMARY-INDEX 4 TIMES.
           MOVE 5 TO WORK-INDEX-1.
           MOVE 1 TO WORK-INDEX-2.
           PERFORM 222-GET-SECONDARY-INDEX 4 TIMES.
       221-GET-PRIMARY-INDEX.
           IF LR-COLS-25-32(WORK-INDEX-1) = '-'
               MOVE 0 TO PRIMARY-INDEX(WORK-INDEX-2)
           ELSE
               MOVE LR-COLS-25-32(WORK-INDEX-1) TO
                    PRIMARY-INDEX(WORK-INDEX-2).
           ADD 1 TO WORK-INDEX-1 WORK-INDEX-2.
       222-GET-SECONDARY-INDEX.
           IF LR-COLS-25-32(WORK-INDEX-1) = '-'
               MOVE 0 TO SECONDARY-INDEX(WORK-INDEX-2)
           ELSE
               MOVE LR-COLS-25-32(WORK-INDEX-1) TO
                    SECONDARY-INDEX(WORK-INDEX-2).
           ADD 1 TO WORK-INDEX-1 WORK-INDEX-2.
       223-CHECK-VOLUME-INDEX.
           IF LR-COLS-27-32 = VOLUME-INDEX(WORK-INDEX-1)
               MOVE 0 TO VOLUME-INDEX-FLAG.
           ADD 1 TO WORK-INDEX-1.
       259-GET-OWNER.
           IF LR-COLS-25-32(WORK-INDEX-1) NOT = '-'
               MOVE LR-COLS-25-32(WORK-INDEX-1) TO
                    OWNER(WORK-INDEX-1)
           ELSE
               MOVE SPACE TO OWNER(WORK-INDEX-1).
           ADD 1 TO WORK-INDEX-1.
       260-CHECK-EXPIRATION-DATE.
           IF LR-COLS-54-57 NOT = '0000'
               MOVE LR-COLS-54-57 TO TO-YEAR
               MOVE LR-COLS-59-61 TO TO-DAY
               MOVE 1 TO TO-FLAG.
       270-READ-REQUESTS.
           READ REQUESTS AT END
                MOVE '1' TO EOF-REQUESTS-FLAG.
       280-READ-SYSPRINT.
           READ SYSPRINT INTO LISTC-RECORD AT END
                MOVE '1' TO EOF-SYSPRINT-FLAG.

Ernie Ishman
Senior Systems Programmer
Geisinger System Services (USA)

