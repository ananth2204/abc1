Displaying the status of CICS VSAM files

The following two CICS programs display the status of CICS
VSAM files which use local shared resources. A common
cause of poor response time or the reason why some
transactions appear to be 'hanging' is that they are waiting for a
VSAM buffer or string. These programs enable the user to
display on-line the status of VSAM files, ie if they are waiting
for a buffer or string, how many strings are currently active for
each file, the highest number of strings that were active, the
number of times the file had to wait for a string or buffer, plus
relevant VSAM catalog information, CI/CA splits, extents,
EXCPs, and DSORGs.

The programs are written in Assembler and invoked by a
transaction called QLSR. The program ITAS is called by
QLSR to display active tasks.


QLSR

DFHEISTG DSECT ,
* QLSR IS AN ON-LINE CICS TRANSACTION WHICH MAY BE USED TO DISPLAY
* THE STATUS OF CICS VSAM FILES USING VSAM LOCAL SHARED RESOURCES
* FOR TUNING AND PROBLEM DETERMINATION PURPOSES.
* THE TRANSACTION DISPLAYS THE FOLLOWING INFORMATION FOR EACH FILE:
* - IF THE FILE IS WAITING FOR A BUFFER, AND IF SO, HOW MANY
* - THE HIGHEST NUMBER OF BUFFERS FOR WHICH THE FILE HAD TO WAIT
* - THE NUMBER OF TIMES THE FILE WAITED FOR A BUFFER
* - THE NUMBER OF STRINGS WHICH ARE CURRENTLY ACTIVE FOR THE FILE
* - THE HIGHEST NUMBER OF STRINGS FOR WHICH THE FILE HAD TO WAIT
* - THE NUMBER OF TIMES THE FILE WAITED FOR A STRING
* - THE NUMBER OF STRINGS ALLOCATED TO THE FILE (STRNO= IN FCT)
* - THE DATASET ORGANIZATION
* - THE NUMBER OF I/O'S PERFORMED AGAINST THE FILE
* - THE CONTROL INTERVAL SIZE
* - THE NUMBER OF CI SPLITS, CA SPLITS AND EXTENTS (ON REQUEST)
*
* THE TRANSACTION IS ISSUED IN ONE OF THE FOLLOWING WAYS.
* QLSR          : THE STATUS OF ALL CURRENTLY OPEN FILES IS DISPLAYED
* QLSR *        : THE STATUS OF ALL VSAM FILES DEFINED IN THE FCT
* QLSR EXCP     : ALL FILES WHICH HAVE ENCOUNTERED A WAIT-ON-BUFFER OR
*               : A WAIT-ON-STRING CONDITION
* QLSR ACTV     : ALL FILES WHICH CURRENTLY HAVE AN ACTIVE STRING, AND
*               : A LIST OF ACTIVE TRANSACTIONS.
* QLSR FILENAME : THE STATUS OF THE FILE 'FILENAME'
* IF ',S' IS SPECIFIED AFTER THE TRANSACTION NAME OR ANY OF THE
* OPERANDS THEN THE NUMBER OF CI/CA SPLITS AND EXTENTS IS DISPLAYED IN
* PLACE OF THE BUFFER STATUS.
*
* PF AND CONTROL KEYS:
* CLEAR      : EXIT THE TRANSACTION
* ENTER      : DISPLAYS THE NEXT SCREEN OF DATA, IF ANY
* PF12       : REFRESH CURRENT DISPLAY
* PF10       : WHEN IN THE 'ACTV' DISPLAY, PF10 INVOKES THE CICS CEMT
*            : TRANSACTION WITH THE 'I TAS' OPERAND. FROM HERE YOU MAY
*            : PURGE A TRANSACTION YOU SUSPECT MAY BE CAUSING A PROBLEM
*
* NOTE : THIS TRANSACTION SHOULD HAVE A CICS SECURITY KEY EQUAL TO
*        THAT OF THE CICS CEMT TRANSACTION, BECAUSE WHEN PF10 IS
*        PRESSED WE LINK DIRECTLY TO THE CEMT PROGRAM, DFHEMTA, AND
*        SO BYPASS TRANSACTION SECURITY KEY CHECKING.
         DFHEISTG ,                     COPY CICS BOOKS
         PRINT NOGEN,OFF
         COPY  DFHCSADS                 CSA
         COPY  DFHFCTDS                 FCT
         COPY  DFHTCADS                 TCA
         COPY  DFHTCTTE                 TCTTE
         COPY  DFHTIOA                  TIO AREA
         PRINT ON
TRANSID  DS    CL4                      MAP SCREEN
FILLER   DS    CL1
DATASID  DS    CL7
         DS    CL1
STATUSOP DS    CL1
TCTTEAR  EQU   10                       TCT BASE REG
TIOABAR  EQU   9                        TIO BASE REG
FCTDSBAR EQU   5                        FCT BASE REG
QLSR     CSECT ,
         PRINT NOGEN
         L     TCTTEAR,TCAFCAAA        ADDRESSABILITY
         L     TIOABAR,TCTTEDA
         MVC   DSIDSAVE(7),=CL7' '     CLEAR INTERNAL STORAGE AREAS
         MVC   STATSOPT(1),=CL1' '
         MVI   SWITCH,X'00'
         LA    4,9                     MAX LENGTH OF OPERANDS
         LA    5,DATASID               ADDRESS OF OPERAND
         LA    6,DSIDSAVE              DATASET NAME HOLDING AREA
GETDSN   CLI   0(5),C','               A COMMA ?
         BE    GOTDSN                  YES, GOT DSN
         CLI   0(5),X'00'              HEX 00, YES END OF INPUT
         BE    GOTDSN
         MVC   0(1,6),0(5)             NO, MOVE IN 1 CHAR
         LA    5,1(5)                  BUMP
         LA    6,1(6)
         BCT   4,GETDSN                CHECK NEXT CHAR
         DROP  9,10                    DROP TERMINAL IO ADDRESSABILITY
GOTDSN   CLC   0(2,5),=CL2',S'         ',S' ENTERED ?
         BNE   *+8                     NO
         MVI   STATSOPT,C'S'           YES, TAKE A NOTE
         MVC   HEADT2(6),=CL6'CURWBF'  INITALIZE HEADER LINE
         MVC   HEADT3(6),=CL6'HIGWBF'
         MVC   HEADT4(6),=CL6'TOTWBF'
         EXEC  CICS HANDLE CONDITION END(EXIT) DSIDERR(NOTFND)
         EXEC  CICS HANDLE AID PF12(RESHW) PF10(CALLCEMT) CLEAR(EXIT1)
         EXEC  CICS IGNORE CONDITION LENGERR
         EXEC  CICS ADDRESS CSA(5) ,   GET ADDRESS OF CSA
         ST    5,CSAADR                SAVE IT
RESET    MVC   DSID(7),DSIDSAVE        DATA SET NAME
         LA    9,81                    R9 = CURRENT SCREEN BUF ADDR
         MVC   LINEO+3(7),DSID         DATASET NAME TO OUTPUT LINE
         L     5,CSAADR                CSA ADDRESS
         USING DFHCSADS,5              ADDRESSABILITY
         L     5,CSAFCTBA              FCT ADDRESS
         ST    5,FCTADDR               SAVE IT
SENDHDR0 CLI   STATSOPT,C'S'           ',S' SPECIFIED ?
         BNE   SENDHDR                 NO
         MVC   HEADT2(6),=CL6'CISPLT'  YES, CHANGE HEADER LINE
         MVC   HEADT3(6),=CL6'CASPLT'
         MVC   HEADT4(6),=CL6'EXTENT'
SENDHDR  EXEC  CICS SEND FROM(HEADER) LENGTH(80) ERASE
GOTCSAA  CLI   DSID,C' '               NO DSN GIVEN ?
         BE    STFCTBR                 YES, DISPLAY ALL OPEN VSAM FILES
         CLC   DSID(4),=CL4'EXCP'      EXCEPTIONS ONLY ?
         BE    SETEXCPT                YES
         CLC   DSID(4),=CL4'ACTV'      ACTIVE FILES ONLY ?
         BE    SETACTV                 YES
         CLI   DSID,C'*'               ALL FILES ?
         BNE   GETFCTE                 NO, SCAN FCT FOR A GIVEN FILEID
         OI    SWITCH,ALLFILES         YES, TAKE A NOTE
         B     STFCTBR                 START FCT BROWSE
SETACTV  OI    SWITCH,ACTVSTRS         SET ACTIVE FILES ONLY
         EXEC  CICS HANDLE AID ENTER(RESHW) , CHANGE EFFECT OF ENTER
         B     STFCTBR                 START FCT BROWSE
SETEXCPT OI    SWITCH,EXCEPTS          SET EXCEPTIONS ONLY FLAG
         OI    SWITCH,ALLFILES
STFCTBR  EXEC  CICS INQUIRE DATASET START ,  START FCT BROWSE
         OI    SWITCH,BROWFCT                TAKE A NOTE
NEXTFCTE BAL   7,GETNFCT               GET NEXT FCT ENTRY
GETFCTE  TM    SWITCH,BROWFCT          BROWSING FCT ?
         BO    *+8                     YES, SKIP NEXT INS
         BAL   7,GETSTRGS              GET NO. OF STRING ALLOCATED
         L     5,FCTADDR               FCT ADDRESS
         USING DFHFCTDS,5              ADDRESSABILITY
GETFCT   CLC   FCTDSID(7),DSID         IS THIS THE FILENAME REQUIRED?
         BE    GOTFCTE                 YES
NEXTFCT  CLC   0(8,5),=XL8'FF'         END OF FCT ?
         BE    NOTFND
         LH    7,FCTDSTEL              LENGTH OF THIS ENTRY
         LTR   7,7                     LENGTH = 0 ?
         BZ    NOTFND                  YES, DSN NOT FOUND
         AR    5,7                     NO, POINT TO NEXT ENTRY
         B     GETFCT                  GO CHECK IT
GOTFCTE  LH    4,FCTDSCBW              NO. BUFFERS FILE IS WAITING FOR
         ST    4,CURWBUF
         LH    4,FCTDSHBW              HIGH NO. OF BUFFER WAITS
         ST    4,HIGWBUF
         L     4,FCTDSTBW              TOTAL NO. OF BUFFER WAITS
         ST    4,TOTWBUF
         L     4,FCTDSSRC              CI SIZE
         ST    4,CISIZE
         LH    4,FCTDSASC              ACTIVE STRING COUNT
         ST    4,ACTVSTR
         L     4,FCTDSTSW              COUNT OF WAIT ON STRING COND.
         ST    4,TOTWSTR
         LH    4,FCTDSHSW              HIGHEST COUNT OF WAIT ON STRING
         ST    4,HIGWSTR
         LA    5,FCTDSACB              ADDRESS OF VSAM ACB
         ST    5,ACBADR
         MVC   EXCPS(4),=F'0'          CLEAR EXCP WORK FIELD
         ST    13,SAVE13               SAVE R13
         LA    13,SAVEAREA             SAVE AREA FOR MACRO
         SHOWCB ACB=(5),FIELDS=(NEXCP),  GET EXCP COUNT                C
               LENGTH=4,AREA=EXCPS
         TESTCB ACB=(5),OPENOBJ=PATH   IS FILE A PATH ENTRY ?
         L     13,SAVE13               RESTORE R13
         BE    NOCHECK                 YES, NO CI/CA STATS FOR A PATH
         ST    13,SAVE13               SAVE R13
         LA    13,SAVEAREA             SAVE AREA FOR MACRO
         SHOWCB ACB=(5),FIELDS=(NEXCP,NCIS,NSSS,NEXT),                 C
               LENGTH=16,AREA=EXCPS
         L     13,SAVE13               RESTORE R13
         TM    SWITCH,BROWFCT          BROWSING FCT ?
         BO    NOCHECK                 YES, IGNORE RC (DATASET CLOSED)
         LTR   15,15                   NO, DID SHOW WORK ?
         BNZ   SHOWERR                 NO - ERROR
NOCHECK  TM    SWITCH,EXCEPTS          EXCP OPERAND GIVEN ?
         BNO   CHKIFACT                NO
         CLC   CURWBUF(12),=XL12'00'   YES, ALL BUFFERS STATS 0 ?
         BNE   CONVDATA                NO BUFFER EXCEPTION OCCURRED
         CLC   HIGWSTR(8),=XL12'00'    ALL STRING STATS 0
         BE    NEXTFCTE                YES, NO EXECPTIONS FOR THIS FILE
         B     CONVDATA                NO, DISPLAY THIS FILE
CHKIFACT TM    SWITCH,ACTVSTRS         ACTV OPERAND GIVEN ?
         BNO   CONVDATA                NO, SHOW THIS FILE
         CLC   ACTVSTR(4),=XL12'00'    YES, IS THE ACTIVE STRING CNT 0?
         BE    NEXTFCTE                YES, IGNORE
CONVDATA CLI   STATSOPT,C'S'           ',S' OPTION GIVEN ?
         BNE   NOSTATS
         MVC   CURWBUF(4),CISPLIT      YES, INSERT VALUES
         MVC   HIGWBUF(4),CASPLIT
         MVC   TOTWBUF(4),EXTENTS
         MVI   CISPLIT,X'80'           CLEAR TO MINUS VALUE
         MVC   CISPLIT+1(11),CISPLIT
         MVC   HEADT2(6),=CL6'CISPLT'  CHANGE HEADER LINE
         MVC   HEADT3(6),=CL6'CASPLT'
         MVC   HEADT4(6),=CL6'EXTENT'
*        CHANGE 7 WORDS TO PRINTABLE AND INSERT IN DISPLAY LINE
NOSTATS  LA    4,7                     NO. OF WORDS TO CONVERT
         LA    5,CURWBUF               FIRST WORD
         LA    6,LINEO+10              ADDRESS IN OUTPUT LINE
CONV     L     7,0(5)                  CONVERT TO PRINTABLE
         LTR   7,7                     IS VALUE NEGATIVE ?
         BM    NOTAPPL                 WHEN YES THEN WAS NOT SET
         CVD   7,DWORD                 NO, CONVERT TO PRINTABLE
         ED    MASK(10),DWORD+3
         MVC   0(5,6),MASK+5           MOVE TO OUTPUT LINE
         MVC   MASK(11),MASKS          RESTORE EDIT MASK
BUMP     LA    5,4(5)                  NEXT WORD
         LA    6,7(6)                  BUMP ADDRESS IN OUTPUT LINE
         BCT   4,CONV                  DO IT 7 TIMES
         B     SETDAORG
NOTAPPL  MVC   0(5,6),=CL5'  N/A'      VALUE NOT SET (PATH OR CLOSED)
         MVC   0(4,5),=XL4'00'         RE-INIT VALUE
         B     BUMP
SETDAORG MVC   2(4,6),DAORG            MOVE IN DATASET ORG
         LA    6,7(6)                  BUMP
         L     7,EXCPS                 NO. OF I/O'S
         CLC   OSTAT,DFHVALUE(OPEN)    IS THE FILE OPEN ?
         BE    CONVEXCP                YES
         MVC   1(7,6),=CL7'CLOSED'     NO, SHOW IN DISPLAY
         B     CONVCISZ
CONVEXCP CVD   7,DWORD                 CONVERT EXCP COUNT TO PRINTABLE
         ED    MASK(10),DWORD+3
         MVC   0(7,6),MASK+3
         MVC   MASK(11),MASKS
CONVCISZ LA    6,7(6)                  CONVERT CISIZE TO PRINTABLE
         L     7,CISIZE
         CVD   7,DWORD
         ED    MASK(10),DWORD+3
         MVC   0(6,6),MASK+4
         MVC   MASK(11),MASKS
         MVC   LINEO+3(7),DSID         INSERT DATASET ID
         CH    9,=H'1863'              SCREEN FULL ?
         BL    SENDLINE                NO
         MVC   L20(2),=H'20'           YES, WAIT FOR ENTER KEY
         EXEC  CICS RECEIVE INTO(RBUF) LENGTH(L20)
         EXEC  CICS SEND FROM(HEADER) LENGTH(80) ERASE
         LA    9,81                    RESET BUFFER POS POINTER
SENDLINE BAL   7,SETSBA                SET 3270 BUFFER ADDR CODE
         EXEC  CICS SEND FROM(LINEO) LENGTH(80) CTLCHAR(WCC)
         MVI   LINEO+3,C' '            CLEAR LINE
         MVC   LINEO+4(75),LINEO+3
         TM    SWITCH,BROWFCT          BROWSING FCT ?
         BO    NEXTFCTE                YES, GET NEXT ENTRY
EXIT     MVI   LINEO+1,C' '
         MVC   LINEO+2(L'LINEO-2),LINEO+1
         BAL   7,SETSBA
         MVC   LINEO+3(10),ENDOFDAT
         EXEC  CICS SEND FROM(LINEO) LENGTH(80) CTLCHAR(WCC)
         TM    SWITCH,ACTVSTRS         DISPLAY ACIVE FILES ONLY ?
         BNO   EXIT0                   NO, EXIT
         LA    9,80(9)                 YES, BUMP SCREEN BUFFER ADDRESS
         ST    9,STRTLINE              SAVE IN COMAREA
* CALL PROGRAM ITAS TO DISPLAY ACTIVE TASKS
         EXEC  CICS LINK PROGRAM('ITAS') COMMAREA(STRTLINE)            C
               LENGTH(L4)
EXIT0    EXEC  CICS RECEIVE INTO(RBUF) LENGTH(L20)
EXIT1    EXEC  CICS SEND FROM(BLANKS) LENGTH(L1) ERASE
         TM    SWITCH,BROWFCT          BROWSING FCT ?
         BNO   LEAVE                   NO
         EXEC  CICS INQUIRE DATASET END , * YES ISSUE END
LEAVE    EXEC  CICS RETURN ,              * RETURN TO CICS
GETNFCT  EXEC  CICS INQUIRE DATASET(DSID) NEXT OPENSTATUS(OSTAT)       C
               STRINGS(STRINGS) TYPE(AM) ACCESSMETHOD(ACCMET)
         MVC   DAORG(4),=CL4' '
         CLC   ACCMET,DFHVALUE(VSAM)   IS THIS A VSAM DATASET ?
         BNE   GETNFCT                 NO, IGNORE
         CLC   OSTAT,DFHVALUE(OPEN)    IS IT OPEN ?
         BE    GETAM                   YES, GET ACCESS METHOD
         TM    SWITCH,ALLFILES         DISPLAY ANYWAY ?
         BOR   7                       YES RETURN (CAN'T GET ACC METH)
         B     GETNFCT                 NO, IGNORE
GETAM    CLC   AM,DFHVALUE(KSDS)       KEY  SEQ DATASET ?
         BNE   *+12                    NO
         MVC   DAORG(4),=CL4'KSDS'
         BR    7
         CLC   AM,DFHVALUE(ESDS)       ENTRY SEQ DATASET ?
         BNE   *+12
         MVC   DAORG(4),=CL4'ESDS'
         BR    7
         CLC   AM,DFHVALUE(RRDS)       RELATIVE RECORD SEQ DATASET ?
         BNE   *+12
         MVC   DAORG(4),=CL4'RRDS'
         BR    7
         MVC   DAORG(4),=CL4'    '     DON'T KNOW THEN
         BR    7
RESHW    TM    SWITCH,BROWFCT          RESET (PF12 HAS BEEN PRESSED)
         BNO   RESET
         EXEC  CICS INQUIRE DATASET END
         B     RESET
GETSTRGS EXEC  CICS INQUIRE DATASET(DSID) STRINGS(STRINGS) TYPE(AM)    C
               OPENSTATUS(OSTAT)
         B     GETAM
SETSBA   LR    6,9                     SCREEN BUFFER OFFSET
         STC   6,ANSWER+1              CONVERT TO A 3270 SBA ORDER
         SRL   6,6                                                      20
         STC   6,ANSWER                                                 30
         NI    ANSWER+1,X'3F'                                           40
         TR    ANSWER(2),TAB                                            50
         MVC   LINEO+1(2),ANSWER
         LA    9,80(9)
         BR    7                                                        80
ANSWER   DC    X'0000'                                                  20
TAB      DC    X'40C1C2C3C4C5C6C7C8C94A4B'                              30
         DC    X'4C4D4E4F50D1D2D3D4D5D6D7'                              40
         DC    X'D8D95A5B5C5D5E5F6061E2E3'                              50
         DC    X'E4E5E6E7E8E96A6B6C6D6E6F'                              60
         DC    X'F0F1F2F3F4F5F6F7F8F97A'                                70
         DC    X'7B7C7D7E7F'                                            80
* LINK TO CICS CEMT TRANSACTION (PF10 KEY WAS PRESSED IN ACTV DISPLAY)
CALLCEMT TM    SWITCH,BROWFCT          BROWSING FCT ?
         BNO   LINKCEMT                YES, END BROWSE FIRST
         EXEC  CICS INQUIRE DATASET END
LINKCEMT EXEC  CICS LINK PROGRAM('DFHEMTA') COMMAREA(CEMTPARM)
         B     RESET
NOTFND   TM    SWITCH,BROWFCT
         BO    EXIT
         EXEC  CICS SEND FROM(NFND) LENGTH(80) ERASE
         B     EXIT
SHOWERR  EXEC  CICS SEND FROM(SERR) LENGTH(80) ERASE
         B     EXIT
NFND     DC    CL80'NOT FOUND'
SERR     DC    CL80'SHOW ERROR/DATASET CLOSED'
ENDOFDAT DC    CL10'** END **'
DWORD    DC    1D'0'                   WORK DWORD
CURWBUF  DC    1F'0'                   CURRENT WAIT BUFFER COUNTER
HIGWBUF  DC    1F'0'                   HIGHEST NO. OF WAIT ON BUF
TOTWBUF  DC    1F'0'                   NO. OF WAIT ON BUFFER
ACTVSTR  DC    1F'0'                   NO. OF CURRENTLY ACTIVE STRINGS
HIGWSTR  DC    1F'0'                   HIGHEST NO. OF CONCURRENT WAITS
TOTWSTR  DC    1F'0'                   TOTAL NO. OF WAIT ON STRING
STRINGS  DC    1F'0'                   NO. STRINGS ALLOCATED
DAORG    DC    CL4' '                  DATASET ORGANISATION
EXCPS    DC    1F'0'                   I/O COUNT
CISPLIT  DC    1F'0'                   NO. CI SPLITS
CASPLIT  DC    1F'0'                   NO. CA SPLITS
EXTENTS  DC    1F'0'                   NO. OF EXTENTS
CISIZE   DC    1F'0'                   CI SIZE
CSAADR   DC    1F'0'                   CSA ADDRESS
FCTADDR  DC    1F'0'                   FCT ADDRESS
ACBADR   DC    1F'0'                   ACB ADDRESS
OSTAT    DC    1F'0'                   OPEN STATUS
AM       DC    1F'0'                   ACCESS METHOD
ACCMET   DC    1F'0'                      "     "
SAVE13   DC    1F'0'                   SAVE AREAS
SAVEAREA DC    18F'0'
STRTLINE DC    1F'0'                   COMMAREA FOR ITAS (SCRN BUF ADR)
DSID     DC    CL8' '                  DATASET ID
DSIDSAVE DC    CL8' '                     "     " HOLDER
RBUF     DC    CL20' '                 REPLY BUFFER
STATSOPT DC    C' '                    OPTION INDICATOR
L20      DC    AL2(20)                 LENGTHS
L1       DC    AL2(1)
L4       DC    AL2(4)
BLANKS   DC    C' '
WCC      DC    X'F1'                   WRITE CONTROL CHARACHER
SWITCH   DC    X'00'
BROWFCT  EQU   X'01'                   BROWSING FCT
ALLFILES EQU   X'02'                   DISPLAY ALL FILES
EXCEPTS  EQU   X'04'                   EXECEPTIONS ONLY
ACTVSTRS EQU   X'08'                   ACTIVE FILES ONLY
CEMTPARM DC    AL4(COMMADR)            COMMAREA FOR CEMT TRANS
         DC    AL4(LCOMMAND)
         DC    AL4(OPTBYADR)
         DC    AL4(0)
         DC    AL4(0)
         DS    0H
COMMADR  DC    C'I TAS'                CEMT OPERAND
LCOMMAND DC    H'5'                    OPERAND LENGTH
OPTBYADR DC    X'80'                   RESPONSE IN BUFFER
LINEO    DC    X'11C150',79C' '        OUTPUT LINE WITH SBA
MASK     DC    X'4020202020202020212020',C'  '     EDIT MASKS
MASKS    DC    X'4020202020202020212020',C'  '
HEADER   DC    X'1DF0'
         DC    CL80'FILENAME CURWBF HIGWBF TOTWBF ACTSTR HIGWST TOTWST C
               STRNGS DAORG  EXCPS  CISIZE'
HEADT2   EQU   HEADER+2+9
HEADT3   EQU   HEADER+2+16
HEADT4   EQU   HEADER+2+23
         END   QLSR


ITAS

DFHEISTG DSECT ,
 * THIS PROGRAM IS CALLED BY THE QLSR TRANSACTION WHEN IT IS DISPLAYING
 * THE ACTIVE FILE LIST. IT DISPLAYS A LIST OF TRANSACTIONS ON THE
 * SCREEN ALONG WITH THE ACTIVE FILE NAMES. THIS MAY HELP THE USER
 * TO DETERMINE WHICH FILES THE TRANSACTIONS ARE CURRENTLY USING.
 * THE PROGRAM CALLS THE CICS CEMT TRANSACTION WITH THE 'I TAS' OPERAND
 * AND AN OPTION BYTE IS SET SO THAT THE RESPONSE IS RETURNED TO A
 * BUFFER. THE RESPONSE IS THEN REFORMATED AND DISPLAYED.
          PRINT NOGEN,OFF
          DFHEISTG
          PRINT ON
 ITAS     CSECT ,
          PRINT NOGEN
          L     5,DFHEICAP              ADDRESS OF COMMAREA
          MVC   STRTLINE(4),0(5)        MOVE IN SCREEN BUFFER START ADR
          MVC   PARMS(20),SPARMS        INITIALISE CEMT COMMAREA
          MVC   OPTBYTE(1),=X'00'
          MVC   COMMAND(5),=CL5'I TAS'
          MVC   LCOMMAND(2),=H'5'
          MVC   LREPLBUF(2),=H'2000'
          MVC   L20(2),=H'20'           CALL CEMT
          EXEC  CICS LINK PROGRAM('DFHEMTA') COMMAREA(PARMS) LENGTH(L20)
          LH    6,REPLBUF+6             TOTAL LENGTH OF DATA
          SH    6,=H'6'                 LESS 6 FOR HEADER
          LH    4,REPLBUF+8             NO. LINES
          LA    5,REPLBUF+12            REPLY DATA
          L     9,STRTLINE              OFFSET ADDR ON SCREEN TO WRITE
          MVC   LINEO+3(73),TASKHEAD    MOVE IN HEADER
          B     SENDLINE                SEND IT
          USING REPLDS,8                MAP OUPUT LINE
 SETUPLIN LA    8,LINEO+3               OUTPUT LINE ADDRESS
          LA    10,3                    NO. OF TASKS PER LINE
 GETTASNR CLC   0(4,5),=CL4'TAS('       SCAN REPLY BUFF FOR 'TAS('
          BE    GOTTASNR                GOT IT ?
          LA    5,1(5)                  NO BUMP
          BCT   6,GETTASNR              CONTINUE SCAN
          B     SHOWTAS                 FORCE  REPLY BUFFER ,SHOW SCREEN
 GOTTASNR MVC   TASNRO(5),4(5)          MOVE IN TASKNO
 GETTRAN  CLC   0(4,5),=CL4'TRA('       SCAN FOR TRANS ID
          BE    GOTTRAN
          LA    5,1(5)
          BCT   6,GETTRAN
          B     SHOWTAS                 END OF REPLY BUF
 GOTTRAN  MVC   TRANO(4),4(5)           MOVE IN TRAN NAME
          CLC   24(3,5),=CL3'TAS'       IS TASK ASSOCIATED WITH A TERM?
          BNE   GETTERM                 YES
          LA    5,10(5)                 NO, NO TERMINAL ID THEN
          SH    6,=H'10'
          B    GOTTERM
 GETTERM  CLC   0(4,5),=CL4'FAC('       SCAN FOR TERMINAL ID
          BE    GOTTERM
          LA    5,1(5)
          BCT   6,GETTERM
          B     SHOWTAS                 END OF REPLY BUF
 GOTTERM  MVC   TERMO(4),4(5)           MOVE IN TERMINAL ID
          MVC   STATO(3),10(5)          MOVE IN TASK STATUS
          MVC   ATTRIB(2),=X'1DF0'      SET ATTRIBUTE LOW INTENSITY
          CLC   STATO(3),=CL3'ACT'      IS TASK ON THE ACTIVE CHAIN ?
          BNE   *+10                    NO, SKIP NEXT INS
          MVC   ATTRIB(2),=X'1DF8'      YES, HILIGHT THIS LINE
          LA    8,26(8)                 BUMP LINE POSISTION
          BCT   10,*+8                  DO 3 TIMES
          B     SENDLINE                SEND A LINE
 NEXT     BCT   6,GETTASNR              DO TO END OF REPLY DATA
          B     SHOWTAS                 END OF REPLY DATA
 EXIT     EXEC  CICS RETURN ,           RETURN TO CICS
 SENDLINE LR    11,6                    SAVE R6
          BAL   7,SETSBA                SET 3270 SBA ORDER
          LR    6,11                    RESTORE R6
          EXEC  CICS SEND FROM(LINEO) LENGTH(80) CTLCHAR(WCC)
          MVI   LINEO+3,C' '            CLEAR LINE
          MVC   LINEO+4(75),LINEO+3
          CH    9,=H'1861'              SCREEN FULL ?
          BH    EXIT                    YES, EXIT
          LTR   4,4                     ANY MORE LINES TO PROCESS ?
          BZ    EXIT                    NO
          BNP   EXIT                    NO
          B     SETUPLIN                YES, GO PROCESS
 SETSBA   LR    6,9                     CONVERT SCREEN OFFSET >3270 SBA
          STC   6,ANSWER+1
          SRL   6,6
          STC   6,ANSWER
          NI    ANSWER+1,X'3F'
          TR    ANSWER(2),TAB
          MVC   LINEO+1(2),ANSWER
          LA    9,80(9)
          BR    7
 ANSWER   DC    X'0000'
 TAB      DC    X'40C1C2C3C4C5C6C7C8C94A4B'
          DC    X'4C4D4E4F50D1D2D3D4D5D6D7'
          DC    X'D8D95A5B5C5D5E5F6061E2E3'
          DC    X'E4E5E6E7E8E96A6B6C6D6E6F'
          DC    X'F0F1F2F3F4F5F6F7F8F97A'
          DC    X'7B7C7D7E7F'
 SHOWTAS  SR    4,4                     FORCE END OF LINES
          B     SENDLINE
 STRTLINE DC    1F'0'                   BUFFER OFFSET (FROM QLSR PROG)
 L4       DC    AL2(4)                  LENGTHS
 L20      DC    AL2(20)
 PARMS    DC    A(COMMAND)              CEMT COMMAREA
          DC    A(LCOMMAND)
          DC    A(OPTBYTE)
          DC    A(REPLBUF)
          DC    A(LREPLBUF)
 SPARMS   DC    A(COMMAND)              CEMT COMMAREA SAVE
          DC    A(LCOMMAND)
          DC    A(OPTBYTE)
          DC    A(REPLBUF)
          DC    A(LREPLBUF)
 WCC      DC    X'F1'                   WRITE CONTROL CHARACTER
 LINEO    DC    X'114040',CL79' '       DISPLAY LINE
 COMMAND  DC    CL5'I TAS'              CEMT OPERAND
 LCOMMAND DC    H'5'                    LENGTH
 OPTBYTE  DC    X'00'                   RETURN DATA TO BUFFER
 LREPLBUF DC    H'2000'                 CEMT REPLY BUFFER LENGTH
          DS    0F
 REPLBUF  DC    2000C' '                CEMT REPLY BUFFER
 TASKHEAD DC    X'1DF0',CL73'TRAN  TASNO TERM STAT    TRAN  TASNO TERM SX
                TAT    TRAN  TASNO TERM STAT'
 REPLDS   DSECT ,                       MAP OUTPUT LINE
 ATTRIB   DS    XL2                     SBA
 TRANO    DS    CL6                     TASK NO.
 TASNRO   DS    CL6                     TRANNAME
 TERMO    DS    CL6                     TERMINAL ID
 STATO    DS    CL6                     STATUS
          END   ITAS


Richard Keane
Systems Programmer (Germany)

