Analysing files in a VSAM catalog

This program, which runs in a VSE environment, was designed
for day-to-day tracking of certain conditions of VSAM files
that are resident in a catalog. It presumes that the files were
well defined in terms of CISIZEs and Free Space (for indexed
files) when they were initially loaded. It detects only four types
of problem:

o      The number of extents
o      CI and CA splits
o      File empty or not allocated
o      The percentage of utilization.

The input data is obtained by reading an IDCAMS LISTCAT
ALL output listing.


CATANLZ JCL

// JOB CATANLZ
// DLBL IJSYSUC,'DESIRED.USERCAT',,VSAM
// DLBL IJSYSLS,'CATANLZ.INPUT',,SD
// EXTENT SYSLST,SYSWKx,,,inittrk,100
   ASSGN SYSLST,DISK,VOL=SYSWKx,SHR
// EXEC IDCAMS,SIZE=AUTO
   LISTCAT ALL -
     CATALOG(DESIRED.USERCAT)
/*
  CLOSE SYSLST,PRINTER
// ASSGN SYS030,DISK,VOL=SYSWKx,SHR
// DLBL DISK,'CATANLZ.INPUT'
// EXTENT SYS030
// ASSGN SYS030,DISK,VOL=SYSWKx,SHR
// ASSGN SYS020,SYSLST
// EXEC CATANLZ,SIZE=100K,PARM='USERCAT'
/&

The program uses a parameter as a page-header identifier.


CATANLZ PROGRAM

CATANLZ  START 0
         PRINT NOGEN
         BALR  12,0
         USING *,12
         L     2,0(1)                  GET PARM ADDRESS AND
         MVC   C01PARM,2(2)            MOVE CATALOG ID TO HEADER
         COMRG                         GET COMM.REGION ADDRESS
         MVC   C01DAT,0(1)             AND MOVE CURRENT DATE TO HEADER
         GETIME                        GET THE TIME ALSO
         ST    1,SAVR1                 AND EDIT IT
         MVC   AX3,MASK3
         ED    AX3,SAVR1
         MVC   C01TIM,AX3+2
         OPEN  DISK                    OPEN THE INPUT DATA FILE
         OPEN  PRINT                   AND REPORT
GETCLU   GET   DISK,REG                READ A RECORD
         CLC   REG+66(9),=C'SPACE-MAP' FOUND END STATISTICS?
         BE    FIM                     YES, DO FINAL PROCESSING
         CLC   REG+1(7),=C'CLUSTER'    FOUND A CLUSTER?
         BNE   GETCLU                  NO, CONTINUE LOOKING
         MVC   D01CLU,REG+17           YES, GET ITS NAME
GETCRE   GET   DISK,REG                LOOK FOR CREATION DATE
         CLC   REG+37(8),=C'CREATION'  FOUND IT?
         BNE   GETCRE                  NO, TRY ANOTHER
         MVC   D01CRE,REG+55           YES, SAVE IT
GETSPL   GET   DISK,REG                LOOK FOR CI-SPLITS
         CLC   REG+37(9),=C'SPLITS-CI' FOUND IT?
         BNE   GETSPL                  NO, CONTINUE LOOKING
         MVC   CAMPO1,REG+49           YES, GET THE NUMBER
         TR    CAMPO1,TRTAB            CHANGE '-'S TO ZEROS
         LA    R04,D01OCR              GET MESSAGE AREA ADDR.
         LA    R05,0                   AND ACTUAL MSG LENGTH
         CLC   CAMPO1,=C'000000000000' HAS ANY
         BNH   GETEXT                  NO, GO LOOKING EXTENT
         MVC   D01SPLCI,CAMPO1+7       YES, GET THE NUMBER
         MVC   0(7,R04),=C'CI-SPL '    MOVE MSG
         LA    R05,7                   ADD MSG LENGTH
         MVC   D01SPLTI,REG+44         AND SAY ITS A CISPLIT
GETEXT   GET   DISK,REG                LOOK FOR NUMBER OF EXTENTS
         CLC   REG+66(7),=C'EXTENTS'   FOUND IT?
         BNE   GETEXT                  NO, CONTINUE LOOKING
         MVC   D01EXT,REG+87           YES, GET THE NUMBER
         TR    D01EXT,TRTAB            CHANGE '-'S TO ZEROS
         CLC   D01EXT,=C'001'          MORE THAN ONE?
         BNH   GETASP                  NO, GO GETTING CA-SPLITS
         AR    R04,R05                 ADD MSG LENGTH
         MVC   0(9,R04),=C'MANY-EXT '  TO MANY EXTENTS FOUND
         LA    R05,9                   ADD MSG LENGTH
GETASP   MVC   CAMPO1,REG+49           MOVE NUMBER OF CA-SPLITS
         TR    CAMPO1,TRTAB            CHANGE '-'S TO ZEROS
         CLC   CAMPO1,=C'000000000000' HAS ANY
         BNH   GETHIA                  NO, GO GETTING HI-ALLOC RBA
         MVC   D01SPLCA,CAMPO1+7       YES, GET THE NUMBER
         AR    R04,R05                 ADD MSG LENGTH
         MVC   0(7,R04),=C'CA-SPL '    MOVE MSG
         LA    R05,7                   ADD MSG LENGTH
         MVC   D01SPLTA,REG+44         AND SAY ITS A CASPLIT
GETHIA   GET   DISK,REG                LOOK FOR HI-ALLOC RBA
         CLC   REG+66(4),=C'HI-A'      FOUND IT?
         BNE   GETHIA                  NO, CONTINUE LOOKING
         MVC   CAMPO1,REG+78           YES, SAVE IT
         TR    CAMPO1,TRTAB            CHANGE '-'S TO ZEROS
         PACK  ALLOC,CAMPO1            ADDS TO
         AP    TOTALL,ALLOC                 GRAND TOTAL
GETHIU   GET   DISK,REG                LOOK FOR HI-USED RBA
         CLC   REG+66(4),=C'HI-U'      FOUND IT?
         BNE   GETHIU                  NO, CONTINUE LOOKING
         MVC   CAMPO1,REG+78           YES, SAVE IT
         TR    CAMPO1,TRTAB            CHANGES '-'S TO ZEROS
         PACK  USED,CAMPO1             CONVERTS TO PACKED DECIMAL
         AP    TOTUSE,USED             ADDS TO GRAND TOTAL
         MP    USED,=P'1000'
         CP    ALLOC,=P'0'             HAS ALLOCATION?
         BH    ALLOCD                  YES, CALCULATE USAGE
         AR    R04,R05                 ADDS PREVIOUS MSGS LENGTH
         MVC   0(9,R04),=C'NO ALLOC '  MOVE WANING MESSAGE,
         LA    R05,9                   ADDS THIS MSG LENGTH
         B     GRAVA                   AND GO PRINTING A LINE
ALLOCD   DP    USED,ALLOC              CALCULATE USED %
         MVC   AX1,MASK
         ED    AX1,USED
         MVC   D01PER,AX1+2            AND MOVE TO REPORT LINE
         CLC   CAMPO1,=C'000000000000' IS FILE EMPTY?
         BNE   GRAVA                   NO, PRINT THE LINE
         AR    R04,R05                 ADDS MSG LENGTH
         MVC   0(11,R04),=C'FILE EMPTY '  AND MOVE MESSAGE
GRAVA    CP    CTLIN,=P'39'            IS HEADER TO BE PRINTED?
         BL    LDET                    NO, PRINT THE LINE
         AP    CTPAG,=P'1'             YES, ADDS A PAGE
         MVC   AX2,MASK2
         ED    AX2,CTPAG
         MVC   C01PAG,AX2+3
         PUT   PRINT,CAB01             PRINT HEADER LINES
         PUT   PRINT,CAB02
         ZAP   CTLIN,=P'0'             ZEROES LINE COUNTER
LDET     PUT   PRINT,DET01             PRINTS DETAIL LINE
         AP    CTLIN,=P'1'             ADDS TO LINE COUNTER
         MVC   D01OCR,=CL22' '         CLEARS MESSAGE AREA
         MVI   DET01,C' '              AND DETAIL LINE
         MVC   DET01+1(132),DET01
         B     GETCLU                  FIND ANOTHER CLUSTER
FIM      EQU   *
         GET   DISK,REG                LOOK FOR THE SPACE-MAP LINE
         CLC   REG+66(9),=C'SPACE-MAP' FOUND IT?
         BNE   FIM                     NO, CONTINUE LOOKING
         MVI   CHAVE,C'*'              YES, DO FINAL STATISTICS
FIMTOT   DP    TOTALL,=P'512'          CONVERTS TOTAL ALLOCATED AREA
         UNPK  T01ALC,TOTALL(8)        TO 512 BYTE BLOCKS
         OI    T01ALC+8,X'F0'
         DP    TOTUSE,P'512'           CONVERTS TOTAL USED AREA
         UNPK  T01USE,TOTUSE(8)        TO 512 BYTE BLOCKS
         OI    T01USE+8,X'F0'
         MVC   CAMPO1,REG+20
         TR    CAMPO1,TRTAB
         PACK  DATASP,CAMPO1
         UNPK  T01SPA,DATASP
         OI    T01SPA+8,X'F0'
         CLI   CHAVE,C'*'
         BNE   NAODSP
         SP    DATASP,TOTAL(8)
         UNPK  T01DIS,DATASP
         OI    T01DIS+8,X'F0'
NAODSP   PUT   PRINT,TOT01
         CLOSE DISK
         CLOSE PRINT
         EOJ
         LTORG
*
Editor's note: register equates go here.
*
CHAVE    DC    CL1' '
CTLIN    DC    PL3'60'
CTPAG    DC    PL3'0'
REG      DS    CL121
CAB01    DS    0CL133
         DC    CL001'1'
         DC    CL037'CATANLZ '
         DC    CL025'ANALYSING CATALOG:       '
C01PARM  DS    CL006
         DC    CL010'    DATE  '
C01DAT   DS    CL008
         DC    CL002' '
C01TIM   DS    CL008
         DC    CL016' '
         DC    CL005'PAGE '
C01PAG   DS    CL003
         DC    CL012' '
CAB02    DS    0CL133
         DC    CL001'0'
         DC    CL044'CLUSTER'
         DC    CL036'#OF EXT. CI/CA-SPLIT     PERCENT./. '
         DC    CL038'    WARNING MESSAGES'
         DC    CL014' CREAT.DATE'
DET01    DS    0CL133
         DC    CL001' '
D01CLU   DS    CL044
         DC    CL003' '
D01EXT   DS    CL003
         DC    CL002' '
D01SPLTI DS    CL002
         DC    CL001' '
D01SPLCI DS    CL005
         DC    CL001' '
D01SPLTA DS    CL002
         DC    CL001' '
D01SPLCA DS    CL005
         DC    CL005' '
D01PER   DS    CL005
         DC    CL005' '
D01OCR   DC    CL032' '
         DC    CL004' '
D01CRE   DS    CL006
         DC    CL006' '
TOT01    DS    0CL133
         DC    CL001'1'
         DC    CL018'ALLOCATED BLOCKS ='
T01ALC   DS    CL009
         DC    CL022'         USED BLOCKS ='
T01USE   DS    CL009
         DC    CL024'  ALLOCATED DATA-SPACE ='
T01SPA   DS    CL009
         DC    CL022'    BLOCKS AVAILABLE ='
T01DIS   DS    CL009
         DC    CL008' '
ALLOC    DS    CL007
USED     DS    CL010
TOTALL   DC    PL010'0'
TOTUSE   DC    PL010'0'
DATASP   DC    PL010'0'
CAMPO1   DS    CL12
MASK     DC    XL7'40202021206B20'
MASK2    DC    XL6'402020202020'
MASK3    DC    XL10'402120204B20204B2020'
AX1      DS    CL07
AX2      DS    CL06
AX3      DS    CL10
SAVR1    DS    F
TRTAB    DC    96X'00'
         DC    X'F0'
         DC    143X'00'
         DC    X'F0F1F2F3F4F5F6F7F8F9'
         DC    6X'00'
DISK     DTFSD BLKSIZE=121,                                            C
               DEVADDR=SYS030,                                         C
               EOFADDR=FIMTOT,                                         C
               IOAREA1=BUF1,                                           C
               RECFORM=FIXUNB,                                         C
               TYPEFLE=INPUT,                                          C
               WORKA=YES
PRINT    DTFPR BLKSIZE=133,                                            C
               CTLCHR=ASA,                                             C
               DEVADDR=SYS020,                                         C
               IOAREA1=BUF2,                                           C
               RECFORM=FIXUNB,                                         C
               WORKA=YES
BUF1     DS    CL121
BUF2     DS    CL133
         END

Cleantes Alves Leite Jr
Senior Systems Programmer (Brazil)

