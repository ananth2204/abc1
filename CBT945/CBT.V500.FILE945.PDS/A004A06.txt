Quick listing of VSAM files
This program can be used to print a more readable VSAM
catalog listing for evaluating the condition of CICS/VSAM
files.  The report is sorted alphabetically on name, and printed
with a space whenever the first three characters of the name
change.  Submit the JCL once for each catalog to be used.  A
control card of 'D' prints the data portion only, 'B' will print
both data and index, and 'A' will print the cluster, AIX, and
path names, along with both data and index entries.

VSAM gives space usage of files in tracks.  FBA users can
change space computations to match their devices number of
blocks per track. CKD users may need to make changes.  The
current settings are for 9335 DASD with 71 blocks per track,
426 blocks per cylinder.

* $$ JOB JNM=QUICKCAT,CLASS=0,DISP=D
* $$ LST DISP=H,CLASS=Q
// JOB QUICKCAT
// LIBDEF PHASE,CATALOG=USERLIB.GENERAL
// OPTION CATAL
   PHASE QUICKCAT,*
// EXEC FCOBOL,SIZE=400K
 CBL ADV,APOST,STATE
       IDENTIFICATION DIVISION.
       PROGRAM-ID.    QUICKCAT.
       AUTHOR.        TOM WEIDEMAN.
       DATE-WRITTEN.  10/22/91.
       DATE-COMPILED.
      * EXAMPLE JCL                                                   *
      * // DLBL IJSYSUC,'SYSWKB.USER.CATALOG',,VSAM,CAT=IJSYSCT       *
      * // DLBL CATALOG,'SYSWKB.USER.CATALOG',,VSAM,CAT=IJSYSUC       *
      * // EXEC QUICKCAT,SIZE=(AUTO,100K)                             *
      * B                                                             *
      * /*                                                            *
       ENVIRONMENT DIVISION.
       CONFIGURATION SECTION.
       SOURCE-COMPUTER.    IBM-4331.
       OBJECT-COMPUTER.    IBM-4331.
       SPECIAL-NAMES.      C01 IS TOP-OF-PAGE.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
           SELECT CAT-FILE ASSIGN TO SYS032-DA-FBA1-CATALOG
               ORGANIZATION IS INDEXED
               ACCESS MODE IS SEQUENTIAL
               RECORD KEY IS CAT-KEY.
           SELECT PRINTER ASSIGN TO SYS007-UR-1403-S.
           SELECT SORT-FILE ASSIGN TO SYS001-DA-FBA1-S-SORTWK1.
       DATA DIVISION.
       FILE SECTION.
       FD  CAT-FILE
           LABEL RECORDS ARE STANDARD
           RECORD CONTAINS 505 CHARACTERS
           DATA RECORD IS CAT-RECORD.
       01  CAT-RECORD.
           05  CAT-KEY.
               10  CAT-PREFIX      PIC XX.
               10  CAT-CI-NUM      PIC 9(4)    COMP.
               10  FILLER          PIC X(40).
           05  CAT-REC-TYPE        PIC X.
           05  CAT-REC-LEN         PIC 9(4)    COMP.
           05  FILLER              PIC X(2).
           05  ENTRY-NAME          PIC X(44).
           05  FILLER              PIC X(8).
           05  CREATE-DATE         PIC 9(5)    COMP-3.
           05  EXPIRE-DATE         PIC 9(5)    COMP-3.
           05  ATTRB-BYTE          PIC X.
           05  SHROPT-BYTE         PIC X.
           05  FILLER              PIC X(6).
           05  SPACE-PRI           PIC 9(4)    COMP.
           05  FILLER              PIC X.
           05  SPACE-SEC           PIC 9(4)    COMP.
           05  FILLER              PIC X.
           05  HU-RBA              PIC 9(9)    COMP.
           05  HA-RBA              PIC 9(9)    COMP.
           05  AVE-REC-L           PIC 9(9)    COMP.
           05  FILLER              PIC X(15).
           05  NUM-GROUP-PTRS      PIC X.
           05  FIVE-POINTER-FORMAT.
               10  FIVE-5-BYTE-PTRS     PIC X(25).
               10  FIVE-PTR-GROUP       PIC X(309).
               10  9TH-EXTENT-INFO      PIC X(20).
               10  FILLER               PIC XX.
           05  SIX-POINTER-FORMAT REDEFINES FIVE-POINTER-FORMAT.
               10  SIX-5-BYTE-PTRS      PIC X(30).
               10  SIX-PTR-GROUP        PIC X(309).
               10  FILLER               PIC X(17).
       FD  PRINTER
           LABEL RECORDS ARE OMITTED
           RECORD CONTAINS 132 CHARACTERS
           DATA RECORD IS PRINT-LINE.
       01  PRINT-LINE              PIC X(132).
       SD  SORT-FILE
           RECORD CONTAINS 132 CHARACTERS
           DATA RECORD IS SORT-RECORD.
       01  SORT-REC.
           05  SK1                 PIC XX.
           05  SK2                 PIC X(36).
           05  FILLER              PIC X(94).
       WORKING-STORAGE SECTION.
       01  FILLER      PIC X(27)   VALUE 'WORKING STORAGE STARTS HERE'.
       01  CARD-IN.
           05  PRT-SWT         PIC X     VALUE 'D'.
           05  FILLER          PIC X(79) VALUE  SPACES.
      * FIELD POSITIONS IN COL. 74-80 VALID WITH 5-POINTER FORMAT  *
      * ELSE ADD 5 FOR 6-POINTER FORMAT.                           *
       01  RECORD-OFFSET-AREA.
           05  FILLER              PIC X(10).
           05  KEY-LEN             PIC 9(4)    COMP.
           05  FREE-CA             PIC X.
           05  FREE-CI             PIC X.
           05  FILLER              PIC X(8).
           05  CI-SIZE             PIC 9(9)    COMP.
           05  MAX-REC-L           PIC 9(9)    COMP.
           05  FILLER              PIC X(30).
           05  NUM-EXTENTS         PIC 9(4)    COMP.
           05  REC-TOTAL           PIC 9(9)    COMP.
           05  REC-DELETED         PIC 9(9)    COMP.
           05  REC-INSERTED        PIC 9(9)    COMP.
           05  REC-UPDATED         PIC 9(9)    COMP.
           05  REC-RETRIEVED       PIC 9(9)    COMP.
           05  FREE-SPACE-BYTES    PIC 9(9)    COMP.
           05  CI-SPLITS           PIC 9(9)    COMP.
           05  CA-SPLITS           PIC 9(9)    COMP.
           05  EXCPS               PIC 9(9)    COMP.
           05  FILLER              PIC X(26).
           05  HIGH-USED-RBA       PIC 9(9)    COMP.
           05  FILLER              PIC X(203).
       01  HEAD-ONE.
           05  FILLER      PIC X(24)  VALUE 'QUICKCAT CATALOG LISTING'. '.
           05  FILLER      PIC X(32)  VALUE SPACES.
           05  CAT-OUT     PIC X(44).
           05  FILLER      PIC X(09)  VALUE  SPACES.
           05  FILLER      PIC X(5)   VALUE  'DATE '.
           05  DATE-O      PIC X(8).
           05  FILLER      PIC X(6)   VALUE  '  PAGE'.
           05  PAGE-O      PIC ZZZ9.
       01  HEAD-TWO.
           05  FILLER      PIC X(44)   VALUE
              '                                        CREA'.
           05  FILLER      PIC X(44)   VALUE
              'TE   SPACE  SPACE  EX  PCT.   -SPLIT-   FREE'.
           05  FILLER      PIC X(44)   VALUE
              'SPACE   CI   ---------RECORD-------- SHR REU'.
       01  HEAD-THREE.
           05  FILLER      PIC X(44)   VALUE
              'TYPE     ENTRY NAME                      DAT'.
           05  FILLER      PIC X(44)   VALUE
              'E     PRI    SEC   TS  FULL   CI   CA    CI '.
           05  FILLER      PIC X(44)   VALUE
              '  CA   SIZE  AVE-L MAX-L KEY-L TOTAL OPT SPD'.
       01  NAME-LINE.
           05  TYPE-N          PIC X.
           05  FILLER          PIC X      VALUE  SPACES.
           05  NAME-N          PIC X(44).
           05  FILLER          PIC X(86)   VALUE  SPACES.
       01  DETAIL-LINE.
           05  TYPE-O          PIC XX.
           05  NAME-O.
               10  NAME-P      PIC XXX.
               10  FILLER      PIC X(33).
           05  FILLER          PIC X.
           05  CDATE-O.
               10  MM-O        PIC XX.
               10  SLASH-1     PIC X.
               10  DD-O        PIC XX.
               10  SLASH-2     PIC X.
               10  YY-O        PIC XX.
           05  SPACE-PRI-O     PIC ZZZZZZ9.
           05  SPACE-SEC-O     PIC ZZZZZZ9.
           05  NUM-EXTENTS-O   PIC ZZZ9.
           05  PCT-FULL-O      PIC ZZZZ9.
           05  PCTSIGN1-O      PIC X.
           05  IDX-ED1.
               10  CI-SPLIT-O      PIC ZZZZ9.
               10  CA-SPLIT-O      PIC ZZZZ9.
               10  FREE-CI-O       PIC ZZZZ9.
               10  PCTSIGN2-O      PIC X.
               10  FREE-CA-O       PIC ZZZ9.
               10  PCTSIGN3-O      PIC X.
           05  CISIZE-O        PIC ZZZZZZ9.
           05  AVE-REC-L-O     PIC ZZZZZ9.
           05  MAX-REC-L-O     PIC ZZZZZ9.
           05  KEY-LEN-O       PIC ZZZZ9.
           05  REC-TOTAL-O     PIC ZZZZZZ9-.
           05  SHROPT-O        PIC ZZ9.
           05  FILLER          PIC XXX.
           05  REUSE-O         PIC XX.
      * WORK AREAS
       01  BIN-DATA            PIC S9(4) COMP.
       01  FILLER REDEFINES BIN-DATA.
           05  FILLER          PIC X.
           05  BIN-BYTE        PIC X.
       01  HIGH-USED-BLOCK     PIC 9(9)       COMP-3  VALUE 0.
       01  SPACE-PRI-SAVE      PIC 9(7)       COMP-3  VALUE 0.
       01  SPACE-SEC-SAVE      PIC 9(7)       COMP-3  VALUE 0.
       01  PCT-FULL-SAVE       PIC 9(5)       COMP-3  VALUE 0.
       01  LINE-COUNT          PIC 999        COMP-3  VALUE 99.
       01  PAGE-COUNT          PIC 999        COMP-3  VALUE 0.
       01  EOF                 PIC 9          COMP-3  VALUE 0.
       01  NAME-SAVE           PIC XXX        VALUE  SPACES.
      * DATE CONVERSION AREA *
       01  WRKDATE-CONVERSION.
           02  WRKDT-SUB               PIC S999 COMP.
           02  WRKDT-DOW               PIC S9 COMP-3.
           02  WRKDT-RESULT            PIC S9(7) COMP-3.
           02  WRKDT-WORK              PIC S9(7) COMP-3.
           02  WRKDT-WRK               REDEFINES WRKDT-WORK.
               03  FILLER              PIC XX.
               03  WRKDT-RMDR          PIC 999 COMP-3.
           02  WRKDT-TABLE             COMP-3.
               03  FILLER              PIC S999 VALUE +000.
               03  FILLER              PIC S999 VALUE +031.
               03  FILLER              PIC S999 VALUE +059.
               03  FILLER              PIC S999 VALUE +090.
               03  FILLER              PIC S999 VALUE +120.
               03  FILLER              PIC S999 VALUE +151.
               03  FILLER              PIC S999 VALUE +181.
               03  FILLER              PIC S999 VALUE +212.
               03  FILLER              PIC S999 VALUE +243.
               03  FILLER              PIC S999 VALUE +273.
               03  FILLER              PIC S999 VALUE +304.
               03  FILLER              PIC S999 VALUE +334.
               03  FILLER              PIC S999 VALUE +365.
           02  WRKDT-TBLE              REDEFINES WRKDT-TABLE.
               03  WRKDT-DOM           PIC S999 COMP-3 OCCURS 13.
           02  WRKDT-DATE              PIC 9(6).
           02  WRKDT-DATEX             REDEFINES WRKDT-DATE.
               03  WRKDT-MON           PIC 99.
               03  WRKDT-DAY           PIC 99.
               03  WRKDT-YR            PIC 99.
           02  WRKDT-JULIAN            PIC 9(7).
           02  WRKDT-JULIANX           REDEFINES WRKDT-JULIAN.
               03  WRKDT-JULYR         PIC 9(4).
               03  WRKDT-JULDAYS       PIC 999.
           02  WRKDV-NUMBER.
               03  WRKDV-DIG           PIC X.
               03  WRKDV-NBR           PIC X(5).
           02  WRKDV-TABLE.
               03  FILLER              PIC X(24)
                   VALUE '312831303130313130313031'.
           02  WRKDV-TBLE              REDEFINES WRKDV-TABLE.
               03  WRKDV-MON           PIC 99 OCCURS 12 TIMES.
           02  WRKDT-DATE1             PIC S9(7) COMP-3.
           02  WRKDT-DATE2             PIC S9(7) COMP-3.
           02  WRKDT-LEAP              PIC S9(7) COMP-3.
           02  WRKDT-LEAPX REDEFINES WRKDT-LEAP PIC S9(4)V999 COMP-3.
       01  FILLER      PIC X(25)   VALUE 'WORKING STORAGE ENDS HERE'.
       PROCEDURE DIVISION.
       START-PROGRAM.
           OPEN INPUT  CAT-FILE
                OUTPUT PRINTER.
           ACCEPT CARD-IN.
           IF  PRT-SWT EQUAL 'A' OR EQUAL 'B' OR EQUAL 'D'
               NEXT SENTENCE
           ELSE
               DISPLAY 'CONTROL CARD INVALID OR MISSING'
               DISPLAY 'MUST BE  "A" OR "B" OR "D"'
               GO TO STOP-PROGRAM.
           SORT SORT-FILE ON ASCENDING KEY SK2, SK1
               INPUT PROCEDURE IS READ-SORT-CAT THRU RSC-EXIT
               OUTPUT PROCEDURE IS PRINT-SORT-CAT THRU PSC-EXIT.
       STOP-PROGRAM.
           CLOSE CAT-FILE, PRINTER.
           STOP RUN.
       READ-SORT-CAT SECTION.
           PERFORM READ-LOOP THRU RL-EXIT
               UNTIL EOF EQUAL 1.
           GO TO RSC-EXIT.
       READ-LOOP.
           READ CAT-FILE
                AT END MOVE 1 TO EOF
                       GO TO RL-EXIT.
           IF  CAT-CI-NUM EQUAL 0 OR EQUAL 1
               GO TO RL-EXIT.
           IF  CAT-CI-NUM EQUAL 2 AND CAT-REC-TYPE EQUAL 'C'
               MOVE ENTRY-NAME TO CAT-OUT
               GO TO RL-EXIT.
           IF  CAT-REC-TYPE EQUAL 'C' OR EQUAL 'G' OR EQUAL 'R'
               IF  PRT-SWT EQUAL 'A'
                   MOVE CAT-REC-TYPE   TO TYPE-N
                   MOVE ENTRY-NAME TO NAME-N
                   MOVE NAME-LINE TO SORT-REC
                   RELEASE SORT-REC
                   GO TO RL-EXIT.
           IF  CAT-REC-TYPE EQUAL 'I'
               IF  PRT-SWT EQUAL 'A' OR EQUAL 'B'
                   PERFORM DETAIL-ROUTINE THRU DR-EXIT
                   GO TO RL-EXIT.
           IF  CAT-REC-TYPE EQUAL 'D'
               PERFORM DETAIL-ROUTINE THRU DR-EXIT.
       RL-EXIT. EXIT.
       DETAIL-ROUTINE.
           MOVE SPACES TO DETAIL-LINE.
           MOVE ZERO TO BIN-DATA.
           MOVE NUM-GROUP-PTRS TO BIN-BYTE.
           IF  BIN-DATA IS EQUAL 5
               MOVE FIVE-PTR-GROUP TO RECORD-OFFSET-AREA
           ELSE
              IF  BIN-DATA IS EQUAL 6
                  MOVE SIX-PTR-GROUP TO RECORD-OFFSET-AREA
              ELSE
                  DISPLAY 'UNDETERMINED RECORD TYPE ', ENTRY-NAME
                  GO TO DR-EXIT.
           MOVE CREATE-DATE  TO WRKDT-JULIAN.
           PERFORM GREGORIAN THRU GREGORIAN-EXIT.
           MOVE '/' TO SLASH-1, SLASH-2.
           MOVE WRKDT-MON TO MM-O.
           MOVE WRKDT-DAY TO DD-O.
           MOVE WRKDT-YR  TO YY-O.
           MULTIPLY SPACE-PRI BY 71 GIVING SPACE-PRI-SAVE.
           MULTIPLY SPACE-SEC BY 71 GIVING SPACE-SEC-SAVE.
           COMPUTE HIGH-USED-BLOCK = ( HU-RBA / 512 ) * 100.
           COMPUTE PCT-FULL-SAVE ROUNDED =
                                 ( HIGH-USED-BLOCK / SPACE-PRI-SAVE).
           IF  AVE-REC-L EQUAL -1 MOVE 0 TO AVE-REC-L.
           MOVE '???' TO REUSE-O.
           MOVE ZERO TO BIN-DATA.
           MOVE ATTRB-BYTE TO BIN-BYTE.
           IF BIN-DATA EQUAL  00 MOVE '--' TO REUSE-O.
           IF BIN-DATA EQUAL  32 MOVE 'R-' TO REUSE-O.
           IF BIN-DATA EQUAL  48 MOVE 'R-' TO REUSE-O.
           IF BIN-DATA EQUAL  64 MOVE '--' TO REUSE-O.
           IF BIN-DATA EQUAL  96 MOVE 'R-' TO REUSE-O.
           IF BIN-DATA EQUAL 112 MOVE 'R-' TO REUSE-O.
           IF BIN-DATA EQUAL 128 MOVE '-S' TO REUSE-O.
           IF BIN-DATA EQUAL 160 MOVE 'RS' TO REUSE-O.
           IF BIN-DATA EQUAL 192 MOVE '-S' TO REUSE-O.
           IF BIN-DATA EQUAL 224 MOVE 'RS' TO REUSE-O.
           MOVE ZERO TO BIN-DATA, SHROPT-O.
           MOVE SHROPT-BYTE TO BIN-BYTE.
           IF BIN-DATA EQUAL  32 MOVE 1 TO SHROPT-O.
           IF BIN-DATA EQUAL  96 MOVE 2 TO SHROPT-O.
           IF BIN-DATA EQUAL 160 MOVE 3 TO SHROPT-O.
           IF BIN-DATA EQUAL 224 MOVE 4 TO SHROPT-O.
           MOVE SPACE-PRI-SAVE         TO SPACE-PRI-O.
           MOVE SPACE-SEC-SAVE         TO SPACE-SEC-O.
           MOVE PCT-FULL-SAVE          TO PCT-FULL-O.
           MOVE AVE-REC-L              TO AVE-REC-L-O.
           MOVE REC-TOTAL              TO REC-TOTAL-O.
           MOVE CAT-REC-TYPE           TO TYPE-O.
           MOVE ENTRY-NAME             TO NAME-O.
           MOVE MAX-REC-L              TO MAX-REC-L-O.
           MOVE KEY-LEN                TO KEY-LEN-O.
           MOVE CI-SIZE                TO CISIZE-O.
           MOVE NUM-EXTENTS            TO NUM-EXTENTS-O.
           MOVE ZEROS                  TO BIN-DATA.
           MOVE FREE-CI                TO BIN-BYTE.
           MOVE BIN-DATA               TO FREE-CI-O.
           MOVE CI-SPLITS              TO CI-SPLIT-O.
           MOVE ZEROS                  TO BIN-DATA.
           MOVE FREE-CA                TO BIN-BYTE.
           MOVE BIN-DATA               TO FREE-CA-O.
           MOVE CA-SPLITS              TO CA-SPLIT-O.
           MOVE '%'             TO PCTSIGN1-O, PCTSIGN2-O, PCTSIGN3-O.
           MOVE DETAIL-LINE TO SORT-REC
           RELEASE SORT-REC.
       DR-EXIT. EXIT.
       RSC-EXIT. EXIT.
       PRINT-SORT-CAT SECTION.
           MOVE 0 TO EOF
               PERFORM PRINT-LOOP THRU PL-EXIT
               UNTIL EOF = 1.
           GO TO PSC-EXIT.
       PRINT-LOOP.
           RETURN SORT-FILE INTO DETAIL-LINE
               AT END MOVE 1 TO EOF
                   GO TO PL-EXIT.
           IF  LINE-COUNT IS GREATER THAN 70
               PERFORM HEADING-ROUTINE THRU HR-EXIT.
           IF  NAME-P NOT EQUAL NAME-SAVE
               MOVE NAME-P TO NAME-SAVE
               MOVE SPACES TO PRINT-LINE
               WRITE PRINT-LINE AFTER ADVANCING 1 LINES
               ADD 1 TO LINE-COUNT.
           IF  TYPE-O EQUAL 'I'
               MOVE SPACES TO IDX-ED1.
           WRITE PRINT-LINE FROM DETAIL-LINE
               AFTER ADVANCING 1 LINES
               ADD 1 TO LINE-COUNT.
       PL-EXIT. EXIT.
       HEADING-ROUTINE.
               ADD 1 TO PAGE-COUNT.
               MOVE PAGE-COUNT TO PAGE-O.
               MOVE CURRENT-DATE TO DATE-O.
               WRITE PRINT-LINE FROM HEAD-ONE
                   AFTER ADVANCING TOP-OF-PAGE.
               WRITE PRINT-LINE FROM HEAD-TWO
                   AFTER ADVANCING 3 LINES.
               WRITE PRINT-LINE FROM HEAD-THREE
                   AFTER ADVANCING 1 LINES.
               MOVE SPACES TO PRINT-LINE.
               MOVE SPACES TO NAME-SAVE.
               MOVE 5 TO LINE-COUNT.
       HR-EXIT. EXIT.
       PSC-EXIT. EXIT.
       GREGORIAN SECTION.
       GREGORIAN-START.
           IF WRKDT-JULIAN IS NOT NUMERIC
           OR WRKDT-JULIAN IS EQUAL TO 0
               GO TO GREGORIAN-EXIT.
           MOVE WRKDT-JULIAN TO WRKDT-LEAP.
           MOVE WRKDT-JULYR TO WRKDT-YR.
           MOVE WRKDT-JULYR TO WRKDT-WORK.
           MULTIPLY 250 BY WRKDT-WORK.
           IF WRKDT-RMDR EQUAL TO +000
               AND WRKDT-JULDAYS IS GREATER THAN 59
                   SUBTRACT 1 FROM WRKDT-JULDAYS.
           MOVE 1 TO WRKDT-SUB.
       GREG-LOOP.
           ADD 1 TO WRKDT-SUB.
           IF WRKDT-JULDAYS IS GREATER THAN 365
               MOVE 0 TO WRKDT-DATE
               GO TO GREGORIAN-EXIT.
           IF WRKDT-JULDAYS IS GREATER THAN WRKDT-DOM (WRKDT-SUB)
               GO TO GREG-LOOP.
           SUBTRACT 1 FROM WRKDT-SUB.
           MOVE WRKDT-SUB TO WRKDT-MON.
           IF WRKDT-RMDR IS EQUAL TO +000
               AND WRKDT-JULDAYS IS EQUAL TO 59
                   MOVE WRKDT-LEAP TO WRKDT-JULIAN.
           SUBTRACT WRKDT-DOM (WRKDT-SUB) FROM WRKDT-JULDAYS
               GIVING WRKDT-DAY.
           MOVE WRKDT-LEAP TO WRKDT-JULIAN.
       GREGORIAN-EXIT. EXIT.
/*
// EXEC LNKEDT
/*
/&
* $$ EOJ


Thomas W Weideman
System Programmer
Area 3-4 Computer Centre (USA)

