An easy way to print VSAM file contents

The main purpose of this VM EXEC is to simplify the use of
the AMSERV PRINT command. The PRVSAM EXEC may be
used to print VSAM files from catalogs located in any virtual
machine in the system. VSE or CMS may be accessed, and
possibly MVS too.

For a KSDS file, records may be retrieved by key (full or
partial). If the key is not specified, the RECS TO BE
SKIPPED field on the screen may be used to bypass the desired
number of records from the beginning of the file.

The NUMBER OF RECS field specifies how many records
will be printed. The default values shown on the screen may be
changed to fit your installation's needs (lines 15 to 25 of the
PRVSAM XEDIT).

An example of the screen is shown in Figure 1.


PRVSAM EXEC

/* PRVSAM EXEC: CALL PRVSAM XEDIT */
INICIO:
SET CMSTYPE HT
'X PRVSAM TRASH A (PROFILE PRVSAM'
'EXECIO * DISKR PRVSAM AMSERV A (FINIS STEM VAR.'
IF RC > 3 THEN EXIT 12
IF VAR.0 = 0 THEN EXIT 12
RESP = WORD(VAR.1,1)
IF RESP = 'AMSERV' THEN DO
   'X PRVSAM LISTING A'
   SIGNAL INICIO
   END
'ERASE PRVSAM AMSERV A'
SET CMSTYPE RT
EXIT 0


PRVSAM XEDIT

/* ----------------------------------------------------------------- */
/*   PROGRAM TO CREATE A FILE NAMED "PRVSAM LISTING A" USING THE     */
/*   AMSERV PROGRAM WITH OPTIONS SELECTED FROM A EASY TO USE         */
/*   SCREEN                                                          */
/* ----------------------------------------------------------------- */
set cmstype ht
inicio:
MESSAGE = 'BRIGHT FIELDS ARE MANDATORY'
reinicio:
'erase prvsam amserv a'
MAKEBUF
PUSH ' PRINT INFILE(WORK1)'
'EXECIO 1 DISKW PRVSAM AMSERV A 1 f 80 (FINIS'
DROPBUF
TABTELA.5.25 = 'VSEIPO'              /* DEFAULT VSE MACHINE           */
TABTELA.6.25 = 'VSAM.MASTER.CATALOG' /* DEFAULT MASTER CATALOG        */
TABTELA.7.25 = '350'                 /* DEFAULT MASTCAT ADDR          */
TABTELA.7.46 = 'RVSEIPO'             /* DEFAULT MASTCAT ADDR PASW.    */
TABTELA.8.25 = 'USER.CAT'            /* DEFAULT USER CATALOG          */
TABTELA.9.25 = '352'                 /* DEFAULT USERCAT ADDR          */
TABTELA.9.46 = 'RVSEIPO'             /* DEFAULT USERCAT ADDR PASW.    */
TABTELA.10.25 = ''                   /* DEFAULT FOR FILE NAME         */
TABTELA.11.25 = ''                   /* DEFAULT FOR KEY OF REC        */
TABTELA.12.25 = '00000'              /* DEFAULT FOR NUMBER OF REC SKIP*/
TABTELA.13.25 = '00000'              /* DEFAULT FOR NUMBER OF REC     */
VSE      = LEFT(TABTELA.5.25,8)
MASTER   = LEFT(TABTELA.6.25,36)
MASTADDR = LEFT(TABTELA.7.25,3)
MASTPSWD = LEFT(TABTELA.7.46,8)
CAT      = LEFT(TABTELA.8.25,36)
CATADDR  = LEFT(TABTELA.9.25,3)
CATPSWD  = LEFT(TABTELA.9.46,8)
ARQUIVO  = LEFT(TABTELA.10.25,36)
KEY      = LEFT(TABTELA.11.25,30)
SKIP     = LEFT(TABTELA.12.25,5)
NLRECS   = LEFT(TABTELA.13.25,5)
'COMMAND SET PF01 A'
'COMMAND SET PF03 QUIT'
'COMMAND SET PF12 QUIT'
'COMMAND SET CMDLINE OFF'
'COMMAND SET MSGLINE OFF'
'COMMAND SET CTLCHAR $ ESCAPE'
'COMMAND SET CTLCHAR % PROTECT BLUE HIGH'
'COMMAND SET CTLCHAR " PROTECT BLUE NOHIGH'
'COMMAND SET CTLCHAR @ NOPROTECT WHITE HIGH'
'COMMAND SET CTLCHAR > NOPROTECT WHITE NOHIGH'
'COMMAND SET CTLCHAR < NOPROTECT INVISIBLE'
PH  = '$%'
PN  = '$"'
NH  = '$@'
NN  = '$>'
NI  = '$<'
TELA1:
'COMMAND CURSOR SCREEN 5 25'
  'COMMAND SET RESERVED 1 NOHIGH'  ,
  '  =============================================='
  'COMMAND SET RESERVED 2 NOHIGH'  ,
  '   P R V S A M   -  SELECTIVE DUMP OF A VSAM FILE'
  'COMMAND SET RESERVED 3 NOHIGH'  ,
  '   ==============================================
  'COMMAND SET RESERVED 4 NOHIGH'
  'COMMAND SET RESERVED 5 HIGH'  ,
  '   VSE MACHINE NAME...:'NN||VSE||PN  ,
  '(MAY BE A CMS MACHINE THAT OWNS A VSAM CAT.)'
  'COMMAND SET RESERVED 6 HIGH'  ,
  '   MASTER CATALOG.....:'NN||MASTER||PN
  'COMMAND SET RESERVED 7 HIGH'  ,
  '   MAST.CAT ADDR......:'NN||MASTADDR||PN||,
  '  READ PASSWORD:'NI||MASTPSWD||PN
  'COMMAND SET RESERVED 8 HIGH'  ,
  '   USER CATALOG.......:'NN||CAT||PN
  'COMMAND SET RESERVED 9 HIGH'  ,
  '   USER CAT. ADDR.....:'NN||CATADDR||PN||,
  '  READ PASSWORD:'NI||CATPSWD||PN
  'COMMAND SET RESERVED 10 HIGH' ,
  '   FILE NAME..........:'NN||ARQUIVO||PN
  'COMMAND SET RESERVED 11 NOHIGH' ,
  '   KEY (FULL OR PART.):'NN||KEY||PN||,
  '(DEFAULT=FROM BEGINNING)'
  'COMMAND SET RESERVED 12 NOHIGH' ,
  '   RECS TO BE SKIPPED.:'NN||SKIP||PN
  'COMMAND SET RESERVED 13 NOHIGH' ,
  '  NUMBER OF RECS......:'NN||NLRECS||PN ,
  '                        (ZEROS=ALL RECS.)'
  'COMMAND SET RESERVED 14 NOHIGH'
  'COMMAND SET RESERVED 15 NOHIGH'
  'COMMAND SET RESERVED 16 NOHIGH' ' 'PH MESSAGE PN
  'COMMAND SET RESERVED 17 NOHIGH'
  'COMMAND SET RESERVED 18 NOHIGH'
  'COMMAND SET RESERVED 19 NOHIGH'
  'COMMAND SET RESERVED 20 NOHIGH'
  '                  ENTER   - CREATE "PRVSAM LISTING A"'
  'COMMAND SET RESERVED 21 NOHIGH' ,
  '                  PF1     - SPOOLS THE OUTPUT LISTING TO THE VIRTUAL'
  'COMMAND SET RESERVED 22 NOHIGH' ,
  '                             PRINTER, INSTEAD OF WRITING IT TO DISK'
  'COMMAND SET RESERVED 23 NOHIGH' ,
  '                  PF3/PF12 - QUIT'
  'COMMAND SET RESERVED 24 NOHIGH'
  'COMMAND READ ALL tag'
npars = queued()
OPTION = ''
do k=1 to npars
  PULL LINHA
  I = WORD(LINHA,2)
  J = WORD(LINHA,3)
   IF WORD(LINHA,1) = 'PFK' & (I = 3 | I = 12) THEN DO
     'QUIT'
     EXIT
     END
  ELSE
  IF WORD(LINHA,1) = 'PFK' & I = 1 THEN
     OPTION = 'PRINT'
  TABTELA.I.J = WORD(LINHA,4)
end
VSE      = LEFT(TABTELA.5.25,8)
MASTER   = LEFT(TABTELA.6.25,36)
MASTADDR = LEFT(TABTELA.7.25,3)
MASTPSWD = LEFT(TABTELA.7.46,8)
CAT      = LEFT(TABTELA.8.25,36)
CATADDR  = LEFT(TABTELA.9.25,3)
CATPSWD  = LEFT(TABTELA.9.46,8)
ARQUIVO  = LEFT(TABTELA.10.25,36)
KEY      = LEFT(TABTELA.11.25,30)
SKIP     = LEFT(TABTELA.12.25,5)
NLRECS   = LEFT(TABTELA.13.25,5)
NSKIP    = SKIP
NRECS    = NLRECS
IF SKIP   = 0 THEN NSKIP = ''
IF NLRECS = 0 THEN NRECS = ''
IF VSE = '' THEN DO
   MESSAGE = 'VIRTUAL MACHINE TO BE LINKED NOT SPECIFIED'
   SIGNAL TELA1
   END
IF MASTER = '' THEN DO
   MESSAGE = 'MASTER CATALOG NAME NOT SPECIFIED'
   SIGNAL TELA1
   END
IF MASTADDR = '' THEN DO
   MESSAGE = 'MASTER CATALOG ADDRESS NOT SPECIFIED'
   SIGNAL TELA1
   END
IF CAT = '' THEN DO
   MESSAGE = 'USER CATALOG NAME NOT SPECIFIED'
   SIGNAL TELA1
   END
IF CATADDR = '' THEN DO
   MESSAGE = 'USER CATALOG ADDRESS NOT SPECIFIED'
   SIGNAL TELA1
   END
IF ARQUIVO = '' THEN DO
   MESSAGE = 'FILE NAME NOT SPECIFIED'
   SIGNAL TELA1
   END
IF KEY ¬= '' & NSKIP ¬= '' THEN DO
   MESSAGE = '"SKIP" AND "KEY" CANNOT BE BOTH SPECIFIED'
   SIGNAL TELA1
   END
LINHA = ' PRINT INFILE(WORK1)'
IF KEY   ¬= '' THEN LINHA = LINHA||' FROMKEY('||KEY||')'
IF NSKIP ¬= '' THEN LINHA = LINHA||' SKIP('||SKIP||')'
IF NRECS ¬= '' THEN LINHA = LINHA||' COUNT('||NLRECS||')'
'EXECIO 1 DISKW PRVSAM AMSERV A 1 F 80 (FINIS VAR LINHA'
'EXECIO 0 CP (STRING LINK' VSE MASTADDR MASTADDR 'RR' MASTPSWD
IF RC <> 0 THEN DO
   MESSAGE = 'MASTER CATALOG NOT LINKED, ADDRESS OR PASSWORD INCORRECT'
   SIGNAL TELA1
   END
'EXECIO 0 CP (STRING LINK' VSE CATADDR CATADDR 'RR' CATPSWD
IF RC <> 0 THEN DO
   MESSAGE = 'USER CATALOG NOT LINKED, ADDRESS OR PASSWORD INCORRECT'
   SIGNAL TELA1
   END
'SET DOS ON'
'ACC' mastaddr 'G'
'ACC' cataddr 'H'
'ASSGN SYSCAT G'
'ASSGN SYS010 G'
'ASSGN SYS011 H'
'DLBL IJSYSCT G DSN' master '(SYS010 VSAM)'
'DLBL CATALOG H DSN' cat '(SYS011 VSAM)'
'DLBL WORK1 H DSN' ARQUIVO '(SYS011 VSAM CAT CATALOG)'
'SET DOS OFF'
'AMSERV PRVSAM (' OPTION
retcod=rc
IF OPTION = 'PRINT' THEN DO
   MESSAGE = 'AMSERV COMPLETED, RETURN CODE = ' RETCOD
   SIGNAL REINICIO
   END
MAKEBUF
PUSH 'AMSERV' RETCOD OPTION
'EXECIO 1 DISKW PRVSAM AMSERV A 1 f 80 (FINIS'
DROPBUF
'REL G (DET'
'REL H (DET'
'quit'
exit


Sergio de Oliveira Jurgensen
Senior Systems Analyst
Schlumberger Industrias (Brazil)             c Sergio de Oliveira Jurgensen 1992

