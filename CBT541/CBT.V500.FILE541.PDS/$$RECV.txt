        /* this rexx exec is expected to be invoked by the EXEC command
           thus:

           EXec 'dsname(RECEIVE)'

           Prompting will occur for hlq and optional volser.

           and it will then issue the TSO RECEIVE command for the
           the following members to create these new data sets:

           Member
           X$BZC       Xmitted bz2 c source
           X$BZH       Xmitted bz2 h includes
           X$BZO       Xmitted bz2 object files
           X$CCKDA     Xmitted cckddump and cckdload assembler source
           X$CCKDO     Xmitted object library for cckddump and
                       cckdload and compression utilities (bz2
                       and zlib)
           X$CCKDL     Xmitted load library containing cckddump
                       and cckdload
           X$ZC        Xmitted zlib c source
           X$ZH        Xmitted zlib h includes
           X$ZO        Xmitted zlib object files

           You will then need to copy these datasets/members into
           datasets for production use.

           After these data sets are created several of the
           members of this install data set will be browsed.
           */

        parse source x1 x2 x3 x4 dsn .
         x = PROMPT("ON") /* enable prompting */

        say "Enter desired hlq for target data sets",
            "(default is" sysvar("syspref")".CCKD):"
        pull hlq
        hlq = strip(hlq)

        if hlq = "" then hlq = sysvar("syspref")".CCKD.NEW"

        say "Enter optional volser for target data sets",
            "default is to allow standard allocation to",
            "find a volume."
        pull volser
        volser = strip(volser)

        if volser <> "" then vol = "vol("volser")"
                        else vol = ""

        Say "Using HLQ:" hlq
        if vol <> "" then
           Say "Using Volser:" volser

        call do_receive "X$BZC BZLIB.C"
        call do_receive "X$BZH BZLIB.H"
        call do_receive "X$BZO BZLIB.OBJ"
        call do_receive "X$CCKDA ASM"
        call do_receive "X$CCKDL LOAD"
        call do_receive "X$CCKDO OBJ"
        call do_receive "X$ZC ZLIB.C"
        call do_receive "X$ZH ZLIB.H"
        call do_receive "X$ZO ZLIB.OBJ"

        if sysvar('sysispf') <> "ACTIVE" then exit
        Address ISPEXEC
           "Browse dataset('"dsn"(@file541)')"
           "Browse dataset('"dsn"($$readme)')"
        Exit

        Do_Receive:
        arg mem targ
        queue "dsn('"hlq"."targ"')" vol
        "Receive inds('"dsn"("mem")')"
        return
