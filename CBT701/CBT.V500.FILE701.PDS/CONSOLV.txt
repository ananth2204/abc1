/* REXX  */
RET_CODE = 0
I = 0
/* READ THE DATASET PREPARED BY IRXJCL */
DO WHILE RET_CODE <> 2
   "EXECIO 1 DISKR INPDD1"
   IF RC = 2 THEN LEAVE
   RET_CODE = RC
   I = I + 1
   PARSE PULL COMMANDS.I
   COMMANDS.I = STRIP(COMMANDS.I)
   SAY COMMANDS.I
END
ADDRESS TSO
/* SET THE CONSOLE PROFILE */
"CONSPROF SOLDISPLAY(NO) SOLNUM(400)"
"CONSPROF UNSOLDISPLAY(NO) SOLNUM(400)"
"CONSOLE ACTIVATE NAME(SYSCONEX)"

/* FOR EVERY STATEMENT ENTERED IN THE SYSTSIN SEQUENCE */
DO C = 1 TO I

 CART_VAR = '000'C
 IF C > 9 THEN
    CART_VAR = '00'C
 CART = 'APPL'CART_VAR
 SAY 'CART='CART
 /* EXECUTE THE MESSAGES FROM THE SYSTSIN SEQUENCE */
 INTERPRET "'CONSOLE SYSCMD('COMMANDS.C') CART('"CART"')'"
 MESSAGE = 'F'

 /* UNTIL MSG_STEM VAR GET SATISFIED (MSG = 0 OR 4) */
 DO WHILE MESSAGE = 'F'
    /* GET THE SYSTEM'S SOL. MESSAGES               */
    INTERPRET 'MSG =' "GETMSG(''MSG_STEM.'',''SOL'','"CART"',,1)"
    IF MSG_STEM.0 = 'MSG_STEM.0' THEN
       ITERATE
    ELSE
       LEAVE
 END

 /* PUT THE COMMAND AND THE SYSTEM'S SOL. MESSAGE TO THE JES OUTPUT */
 SAY 'IZVEDENA KOMANDA:' COMMANDS.C
 /* MEANS: 'COMMAND EXECUTED' (CROATIAN) */
 SAY '-----------------------------------------------------'
 DO I = 1 TO MSG_STEM.0
    SAY MSG_STEM.I
 END
 SAY ' '
END

SAY MSG
/* DEACTIVATE SYSCONEX AND SET THE CONSOLE PROFILE AS IT WAS */
"CONSOLE DEACTIVATE"
"CONSPROF SOLDISPLAY(YES)"
"CONSPROF UNSOLDISPLAY(YES)"
