JES2 priority aging modification


The USERMOD

//*
//** USERMOD TO ADD CODE TO HASPMISC TO DISABLE PRIORITY AGING
//** FOR HELD JOBS.
//** USERMOD MODIFIES MODULE HASPMISC
//**
//*
//STEP01   EXEC SMPE
//SMPHOLD  DD  DUMMY
//SMPCNTL  DD  *
SET BDY(GLOBAL).
   RECEIVE S(LM00042) LIST .
SET BDY(AMVSTZN).
   APPLY   S(LM00042) .
/*
//SMPPTFIN DD  *
++USERMOD(LM00042).
++VER(Z038) FMID(HJE4430) PRE(UW01679,UW04215,UY93037).
++SRCUPD(HASPMISC) .
./ CHANGE  NAME=HASPMISC
MERCER1  DS    0H                                                MERCER
         CLI   JQEFLAG1,JQE1HLD1   SINGLE JOB HELD               MERCER
         BNE   MERCER2             NO, GO CHANGE PRIORITY        MERCER
         SLR   R7,R7               ZERO CLASS REGISTER           MERCER
         IC    R7,JQETYPE          PICKUP EXISTING CLASS         MERCER
         DS    0H                  MODIFY JOB QUEUE ELEMENT      MERCER
        $QMOD  (R6),(R7),          RESET JOB IN THE QUEUE        MERCERX
               ALONE,              DON'T RESET JOB BUSY BITS     MERCERX
               PCHANGE=NOTOK,      DISALLOW PHASE CHANGE         MERCERX
               PRIORITY=NOCHANGE   DO NOT CHANGE PRIORITY        MERCER
         B     MERCER3             GO CHECKPOINT                 MERCER
MERCER2  DS    0H                                                MERCER
MERCER3  DS    0H                                                MERCER
./ ENDUP
/*


          .
          .
***********************************************************************
*                                                                     *
*                 PRIORITY AGE THE JOB QUEUE                          *
*                                                                     *
***********************************************************************
         SPACE 1                                                   @136
GPRSCAN  SLR   R3,R3               PREPARE TO SCAN ALL JOB QUEUES  @130
         SH    R3,=Y($JQHEADL)     ADJUST FOR QUEUE LOOP           @133
         OI    $CKPTFG1,$CKPTPRI   SHOW PRIO AGING OCCURRED        @220
         SPACE 1
         USING JQE,R6              JOB QUEUE ADDRESSABILITY        @133
         SPACE 1
GPRQLOOP LA    R3,$JQHEADL(,R3)    INCREMENT INDEX INTO QUEUE'S    @133
         CH    R3,=Y($JQTYPES*$JQHEADL)  END OF QUEUE HEADS...     @133
         BNL   GPRSTART            IF SO, START OVER               @133
         L     R6,$JQHEADS(R3)     PREPARE TO SCAN NEXT JOB QUEUE  @133
         N     R6,$ZEROFFF         END OF CHAIN...                 @133
         BZ    GPRQLOOP            BRANCH IF YES                   @133
         LA    R6,$JQHEADS(R3)     SET HEAD ADDRESS AS JQE         @133
         SH    R6,=Y(JQENEXT-JQE)      ADJUST FOR JQE CHAIN LOOP   @133
         SPACE 1                                                   @311
GPRCLOOP L     R6,JQENEXT          GET NEXT JQE ON CHAIN           @133
         N     R6,$ZEROFFF         CLEAR FLAG AND TEST CHAIN END   @133
         BZ    GPRQLOOP            BR TO NEXT QUEUE IF END         @133
         AL    R6,$JOBQPTR         EXPAND JQE OFFSET TO ABS ADDR   @133
         SPACE 1                                                 @136EM
         CLI   JQETYPE,$HARDCPY    JQE ON HARDCOPY ?             @420DJ
         BE    GPRCLOOP            YES, IGNORE JQE             @420D009
MERCER1  DS    0H                                                MERCER
         CLI   JQEFLAG1,JQE1HLD1   SINGLE JOB HELD ?             MERCER
         BNE   MERCER2             NO, GO CHANGE PRIORITY        MERCER
         SLR   R7,R7               ZERO CLASS REGISTER           MERCER
         IC    R7,JQETYPE          PICKUP EXISTING CLASS         MERCER
         DS    0H                  MODIFY JOB QUEUE ELEMENT      MERCER
        $QMOD  (R6),(R7),          RESET JOB IN THE QUEUE        MERCERX
               ALONE,              DON'T RESET JOB BUSY BITS     MERCERX
               PCHANGE=NOTOK,      DISALLOW PHASE CHANGE         MERCERX
               PRIORITY=NOCHANGE   DO NOT CHANGE PRIORITY        MERCER
         B     MERCER3             GO CHECKPOINT THE JQE         MERCER
MERCER2  DS    0H                                                MERCER
         CLC   JQEPRIO,$PRIHIGH    COMPARE PRIO WITH UPPER LIMIT   @133
         BNL   GPRCLOOP            BRANCH IF GREATER THAN OR EQUAL @133
         CLC   JQEPRIO,$PRILOW     COMPARE PRIO WITH LOWER LIMIT   @133
         BL    GPRCLOOP            BR IF LESS -- DONE WITH JQE     @133
         IC    R2,JQEPRIO          INCREMENT
         LA    R2,1(,R2)            JOB QUEUE
         STC   R2,JQEPRIO            PRIORITY
         OI    JQEFLAG6,JQE6PRAG   SET PRIORITY AGING FLAG       @420DJ
         SPACE 1                                               @420D009
MERCER3  DS    0H                                                MERCER
        $CKPT  ID=JQE,ADDR=(R6)    CHECKPOINT THE JQE              @133
         SPACE 1                                               @420D009
         B     GPRCLOOP            BR TO GET NEXT JQE              @133
         SPACE 1                                               @420D009
         DROP  R6,R12              DROP JQE AND ROUTINE        @420D009
         EJECT                                                 @420D009
GPRSEC   DC    A(5400)             NUMBER OF SECONDS IN 1/16 OF A DAY  C
                                   86400 SECS(24 HRS)/16           @136
GPRINT   DC    A(540000)           TIME INTERVAL IN 100THS OF SECS @136
         LTORG
         SPACE 2                                                   @136
         DROP  ,                   KILL ALL ADDRESSABILITY         @136



//CTP     EXEC JES2ASM,MODULE=HASCTP                           @420LD03
//*                                                            @430P430
//************************************************************ @430P430
//*                                                          * @430P430
//*      The following steps assemble the IBM base JES2      * @430P430
//*      product source-distributed modules that are used    * @430P430
//*      in the JES2 address space, and in FSS address       * @430P430
//*      spaces (the HASPxxxx modules).                      * @430P430
//*                                                          * @430P430
//************************************************************ @430P430
.
.
//IRPL    EXEC JES2ASM,MODULE=HASPIRPL
//IRRE    EXEC JES2ASM,MODULE=HASPIRRE
//IRSI    EXEC JES2ASM,MODULE=HASPIRSI                         @430P430
//JOS     EXEC JES2ASM,MODULE=HASPJOS
//MISC    EXEC JES2ASM,IBMSRC='SYS2.TEST.IBMSRC',MODULE=HASPMISC  <===
//MSG     EXEC JES2ASM,MODULE=HASPMSG
//NATS    EXEC JES2ASM,MODULE=HASPNATS                         @410DRVR
//NET     EXEC JES2ASM,MODULE=HASPNET                          @410DRVR
//NJT     EXEC JES2ASM,MODULE=HASPNJT                          @420LNJE
.
.


Jeff Wootton
Technical Services Manager
William M Mercer Inc (USA)                c William M Mercer Inc

