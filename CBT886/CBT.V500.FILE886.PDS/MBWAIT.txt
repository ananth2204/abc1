MBWAIT   AMODE 31              ADDRESSABILITY MODE
MBWAIT   RMODE 24              RESIDENCY MODE
* HELLO
*****************************************************************
*                                                               *
*     *     *   ******   *     *    ***    *******  *******     *
*     **   **   *     *  *     *   *   *      *        *        *
*     * * * *   *     *  *     *  *     *     *        *        *
*     *  *  *   ******   *     *  *******     *        *        *
*     *     *   *     *  *  *  *  *     *     *        *        *
*     *     *   *     *  * * * *  *     *     *        *        *
*     *     *   ******    *   *   *     *  *******     *        *
*                                                               *
*****************************************************************
*                                                               *
*  THIS IS A STANDALONE UTILITY MEANT TO BE EXECUTED IN A       *
*  BATCH JOB AS A WAY OF DELAYING THE EXECUTION OF SUBSEQUENT   *
*  STEPS.  THE FOLLOWING STATEMENT WILL CAUSE A FIVE-MINUTE     *
*  WAIT:                                                        *
*                                                               *
*   //    EXEC PGM=MBWAIT,PARM='05'    FOR MINUTES              *
*                                                               *
*   //    EXEC PGM=MBWAIT,PARM=':05'   FOR SECONDS              *
*                                                               *
*  NOTE: THIS PROGRAM SHOULD NOT BE EXECUTED WITH ANY VALUE     *
*  LESS THAN '01' OR BE USED IN AN ONLINE ENVIRONMENT.          *
*                                                               *
*****************************************************************
*           C H A N G E   H I S T O R Y                         *
*****************************************************************
* 10/29/87  ORIGINAL VERSION FOR GENE BROWN.         SCHUETZ    *
*****************************************************************
* MB01   8/03/90   CHANGE TO SUPPORT SECONDS FOR TRUST IMPAC.   *
*                                                    SCHUETZ    *
*****************************************************************
* MB02   4/04/91   ADD WTO TO DISPLAY THE AMOUNT OF TIME THE    *
*                  CALLING JOB WILL BE WAITING.   TEGETHOFF     *
*****************************************************************
* MB03   4/04/91   ADD CHECK FOR MAX NUMBER OF MINUTES TO BE 09.*
*                                                 TEGETHOFF     *
*****************************************************************
* MB04   4/08/91   MAKE 31 BIT ADDRESSABLE        TEGETHOFF     *
*****************************************************************
*                                                                 MB04
MBWAIT   TITLE 'WAIT AWHILE'
MBWAIT   CSECT                           SET UP ADDRESSABILITY    MB04
         STM   R14,R12,12(R13)        SAVE CALLER'S REGISTERS     MB04
         LA    R14,0(,R14)            CLEAR R14                   MB04
         BSM   R14,0                  GET CALLER'S AMODE          MB04
         ST    R14,12(,R13)           SAVE AMODE
         LR    R11,R15
         USING MBWAIT,R11                                         MB04
         ST    R13,SAVEAREA+4                                     MB04
         LA    R2,SAVEAREA                                        MB04
         ST    R2,8(R13)                                          MB04
         LR    R13,R2                                             MB04
         XC    MODEWORD,MODEWORD      ZERO OUT MODEWORD           MB04
         ST    R14,SAVE14                                         MB04
         TM    SAVE14,128             CHECK CALLER'S AMODE        MB04
         BNO   SETMODE                IT IT 31 BIT?               MB04
         MVC   MODEWORD,=X'80000000'  YES, SET THE MODE BIT TO 31 MB04
SETMODE  LA    R15,GETJOBN                                        MB04
         O     R15,MODEWORD                                       MB04
         BSM   0,R15                                              MB04
*    GET JOBNAME FROM TIOT FOR THE WTO MESSAGES
GETJOBN  L     R3,16                   R3 ---> CVT            MB02
         L     R3,0(,R3)               R3 ---> NEW/OLD TCB    MB02
         L     R3,4(,R3)               R3 ---> MY TCB         MB02
         L     R3,12(,R3)              R3 ---> MY TIOT        MB02
         MVC   JOBNAME,0(R3)           MOVE JOBNAME           MB02
*
*    THESE CHECKS ARE NEEDED DUE TO THE DIFFERENT WAYS THAT PARMS ARE
*      PASSED INTO REG1 FROM COBOL2, COBOL VS AND JCL PASSING PARMS.
CHCKPARM LTR   R1,R1                   CHECK IF PARM PASSED-COBOL2 PGM
         BZ    RETURN16                NO PARM -> BRANCH TO RETURN16
         L     R8,0(R1)                LOAD NEXT TO GET PARM FOR COB VS
         LTR   R8,R8                   CHECK IF PARM PASSED COBOL VS
         BZ    RETURN16                NO PARM -> BRANCH TO RETURN16
         L     R8,0(R8)                LOAD NEXT TO GET PARM FOR JCL
         LTR   R8,R8                   CHECK IF PARM PASSED JCL PARM
         BZ    RETURN16                NO PARM -> BRANCH TO RETURN16
         SPACE
*
         L     R5,0(R1)                 LOAD PARM INTO R5
*
         CLI   2(R5),C':'                CHECK IF SECONDS         MB01
         BNE   MOVEMINS                  IF NOT -> MOVEMINS       MB01
         MVC   INTIME,3(R5)             MOVE # SECONDS TO INTIME  MB01
         MVC   BINTVL,=F'0100'           CHANGE STIMER INTVL->01  MB01
         MVC   UNITS,=C'SECONDS.'        FOR WTO MESSAGE          MB02
         B     PACK                                               MB01
MOVEMINS MVC   INTIME,2(R5)             MOVE # MINUTES TO INTIME  MB01
         MVC   UNITS,=C'MINUTES.'        FOR WTO MESSAGE          MB02
         CLC   INTIME,=C'09'             CHECK IF > 09 MINUTES    MB03
         BH    SETMAX                    IF > 09, -> SETMAX       MB03
         B     PACK                      BRANCH AROUND SETMAX     MB03
SETMAX   MVC   MSG04JOB,JOBNAME        MOVE JOBNAME FOR WTO04     MB02
         MVC   MSG04TME,INTIME         MOVE # OF UNITS - WTO04    MB02
         MVC   MSG04UNT,UNITS          MOVE MINS OR SECS-WTO04    MB02
         WTO   MF=(E,MSG04)             ISSUE MESSAGE TO WTO      MB02
         MVC   INTIME,=C'09'            MOVE VALUE '09' TO INTIME MB03
         MVC   RCODE,=F'4'              SET RC = 04 FOR WARNING   MB03
PACK     PACK  DOUBLE,INTIME             PACK INTIME TO DOUBLE FOR
         CVB   R4,DOUBLE                 CONVERSION TO BINARY
*
         MVC   MSGJOB,JOBNAME          MOVE JOBNAME FOR WTO       MB02
         MVC   MSGTIME,INTIME          MOVE # OF UNITS  - WTO     MB02
         MVC   MSGUNITS,UNITS          MOVE MINS OR SECS -WTO     MB02
*                                                                 MB02
WTO      WTO   MF=(E,MSG)               ISSUE MESSAGE TO WTO      MB02
STIMER   STIMER WAIT,BINTVL=BINTVL
         BCT   R4,STIMER
         B     CLOSE
RETURN16 MVC   RCODE,=F'16'          MOVE 16 INTO RETURN CODE AREA MB02
         MVC   MSG16JOB,JOBNAME        MOVE JOBNAME FOR WTO        MB02
         WTO   MF=(E,MSG16)             ISSUE MESSAGE TO WTO       MB02
         B     CLOSE
         SPACE
CLOSE    DS    0H
         L     R15,RCODE           LOAD R15 W/RETURN CODE AREA SAVED
         L     R13,SAVEAREA+4      R13 ---> CALLERS SAVEAREA       MB04
         ST    R15,16(R13)         STORE R15
         LM    R14,R12,12(R13)     RELOAD REGISTERS                MB04
         BSM   0,R14               BR BACK TO CALLER IN ORIG AMODE MB04
*
         LTORG
MODEWORD DC    F'0'
         SPACE
         DS    0F                  INSURE FULL WORD ALIGNMENT
DOUBLE   DS    D
RCODE    DS    F'0'
JOBNAME  DS    CL8                                            MB02
UNITS    DS    CL8                                            MB02
INTIME   DS    CL2                                            MB02
BINTVL   DC    F'6000'
         SPACE
MSG      WTO   'MBWAIT00 - JOB 19....26 - ISSUED MBWAIT FOR 48 51......*
               ',ROUTCDE=(11),DESC=(7),MF=L
MSGJOB   EQU   MSG+19,8
MSGTIME  EQU   MSG+48,2
MSGUNITS EQU   MSG+51,8
         SPACE
MSG04    WTO   'MBWAIT04 - JOB 19....26 - PASSED MBWAIT A PARM OF 54 55*
               ...    CHANGED TO MAX OF 9 MINUTES.',ROUTCDE=(11),      *
               DESC=(7),MF=L
MSG04JOB EQU   MSG04+19,8
MSG04TME EQU   MSG04+54,2
MSG04UNT EQU   MSG04+57,8
         SPACE
MSG16    WTO   'MBWAIT16 - JOB 19....26 - NO PARM PASSED.',            *
               ROUTCDE=(11),DESC=(7),MF=L
MSG16JOB EQU   MSG16+19,8
         SPACE
*
R0       EQU  0
R1       EQU  1
R2       EQU  2
R3       EQU  3
R4       EQU  4
R5       EQU  5
R6       EQU  6
R7       EQU  7
R8       EQU  8
R9       EQU  9
R10      EQU  10
R11      EQU  11
R12      EQU  12
R13      EQU  13
R14      EQU  14
R15      EQU  15
SAVEAREA DS   18F
SAVE14   DS   F
         END
