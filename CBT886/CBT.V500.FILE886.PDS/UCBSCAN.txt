*****************************************************************
* MODIFIED FOR OSA & OSAD DEVICES                   08/15/01 HFS.
* MODIFIED FOR PHYSICAL & LOGICAL CHANNEL PATHS.    08/22/01 HFS.
* ADD DASD27 STATEMENTS                             02/07/10 EJP.
*     DASD54 STATEMENTS - NOT VERIFIED AND MAY NEED ADJUSTING.
* ADD "AMODE" AND "RMODE" STATEMENTS                10/22/11 JVD.
*****************************************************************
UCBSCAN  CSECT ,
UCBSCAN  AMODE 31
UCBSCAN  RMODE 24
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SAVE  (14,12)             SAVE CALLERS REGS
         BALR  R12,0
         USING *,R12
         LA    R2,SAVEAREA
         ST    R2,8(,R13)
         ST    R13,SAVEAREA+4
         LR    R13,R2
         B     AROUND
OUTUCB   DCB DSORG=PS,MACRF=PM,DDNAME=OUTUCB,LRECL=132,RECFM=FB
AROUND   DS    0H
         OPEN  (OUTUCB,OUTPUT)
UCBRTN   EQU   *
         LA    R8,COMMEXT
         LA    R9,UCBWORK
         LA    R10,DCEWORK
         USING UCBCMEXT,R8         UCB COMMON EXT ADDRESSABILITY
         USING UCBOB,R9            UCB ADDRESSABILITY
         USING DCE,R10             DCE ADDRESSABILITY
         L     R3,CVTPTR                     POINT TO CVT
         USING CVTMAP,R3
         MVC   SYSNAME,CVTSNAME              PULL SYSNAME
         L     R4,CVTPCCAT                   POINT TO PCCA
         USING PCCA,R4
         LA    R5,16                         SET UP COUNT
         SR    R7,R7
NXTCPU   ICM   R6,B'1111',0(R4)              TEST
         BZ    NOCPU                         NOTHING, GO BUMP
         DROP  R4
         USING PCCA,R6                       SOMETHING THERE
         MVC   CPUID,PCCACPID+2              MOVE CPUID
         LA    R7,1(R7)
NOCPU    LA    R4,4(R4)                      BUMP
         BCT   R5,NXTCPU                     GO BACK
         MVI   CPUID,C'0'                    MAKE IT MATCH THE TAP
         CVD   R7,DBW                        COVERT IT TO DECIMAL
         UNPK  NUMCPS,DBW+6(2)               UNPACK IT
         OI    NUMCPS+2,X'F0'                CHANGE THE SIGN
         IOCINFO IOCTOKEN=TOKEN              GET CURRENT TOKEN
         XC    SCANWORK,SCANWORK             CLEAR WORKAREA
SEARCH   EQU   *
         MVC   ADDR,BLANKS                   CLEAR AREA AND GO ON
         MVC   TYPEMOD,BLANKS                CLEAR AREA AND GO ON
         MVC   VOLID,BLANKS                  CLEAR AREA AND GO ON
         MVC   CHPIDS,BLANKS                 CLEAR AREA AND GO ON
         UCBSCAN UCBAREA=UCBWORK,WORKAREA=SCANWORK,DCEAREA=DCEWORK,    *
               DCELEN=255,CMXTAREA=COMMEXT,DEVCLASS=ALL,DEVNCHAR=ADDR, *
               COPY,IOCTOKEN=TOKEN,DYNAMIC=YES,RANGE=ALL
         LTR   R15,R15                  RETURN CODE 0 ?
         BNZ   TIMETOGO                 NO, MUST BE END
         CLI   UCBID,X'CC'              RC=0, IS IT A COPY ?
         BNE   TIMETOGO                 NO, GET OUT
CHKONL   TM    UCBSTAT,X'80'            ONLINE?
         BNO   SEARCH                   NO, GET ANOTHER ONE
GETCHP   UCBINFO PATHINFO,PATHAREA=INFOAREA,DEVN=UCBCHAN,              *
               RETCODE=INFORTCD
         LA    R11,INFOAREA
         USING PATH,R11
         L     R2,PATH#CHPIDS
         LA    R11,PATHCHPIDARRAY
         DROP  R11
         USING PATHCHPID,R11
         LA    R3,CHPCHP1
CHPLOOP  DS    0H                       GET THE CHPIDS
         TM    PATHBITS,PATHLPM+PATHPAM            IS PATH ONLINE ?
         BC    14,NEXTCHP              NO, GET NEXT CHPID
         LH    R15,PATHCHPID            ASSOCIATED WITH THIS UCB
         SR    R14,R14
         SLDL  R14,28
         IC    R14,BTX(R14)
         STC   R14,0(,R3)
         SR    R14,R14
         SLDL  R14,4
         IC    R14,BTX(R14)
         STC   R14,1(,R3)
         LA    R3,3(,R3)
NEXTCHP  EQU   *
         LA    R11,28(,R11)
         BCT   R2,CHPLOOP
         TM    UCBDVCLS,X'20'           DASD ?
         BO    DASD                     GO TO DASD
         TM    UCBDVCLS,X'80'           TAPE ?
         BO    TAPE                     GO TO TAPE
         TM    UCBDVCLS,X'41'           CTC  ?
         BO    CTC                      GO TO CTC
         TM    UCBDVCLS,X'40'           COMM ?
         BO    COMM                     GO TO COMM
         TM    UCBDVCLS,X'10'           DISP ?
         BO    DISP                     GO TO DISP
         TM    UCBDVCLS,X'08'           UR   ?
         BO    UR                       GO TO UR
         B     SEARCH                   DON'T KNOW, GO BACK
*
*   DASD PROCESSING
*
DASD     DS    0H
         LA    R2,DASDTAB               POINT TO TABLE
         MVC   VOLID,UCBVOLI            MOVE VOLID
DASDLOOP DS    0H
         CLC   0(1,R2),DCEOBRDT         FOUND DEVTYPE MATCH?
         BE    DASDMOD                  YES, CHECK FURTHER
         LA    R2,ENTRYLEN(,R2)         NO MATCH, BUMP TABLE
         CLI   0(R2),0                  END OF TABLE ?
         BE    PRINTIT                  YES, GO PRINT
         B     DASDLOOP                 NO, GET NEXT
DASDMOD  DS    0H
         CLI   0(R2),X'32'              IS IT 3390-9?
         BNE   PRINTIT                  NO, CONTINUE
         CLI   DCEUDCYL,X'27'           3390-9? APPROX 10,000
         BE    PRINTIT                  YES, CONTINUE
DASD27   EQU   *
         LA    R2,ENTRYLEN(,R2)         BUMP TO MOD27 ENTRY
         CLI   DCEUDCYL,X'7F'           3390-27? APPROX 32000
         BE    PRINTIT                  YES, CONTINUE
DASD54   EQU   *
         LA    R2,ENTRYLEN(,R2)         BUMP TO MOD54 ENTRY
         CLI   DCEUDCYL,X'FF'           3390-54? APPROX 65000
         BE    PRINTIT                  YES, CONTINUE
DASDUNK  EQU   *
         LA    R2,ENTRYLEN(,R2)         BUMP TO LAST ENTRY
         BE    PRINTIT                  YES, CONTINUE
*
*   TAPE PROCESSING
*
TAPE     DS    0H
         LA    R2,TAPETAB               POINT TO TABLE
TAPLOOP  DS    0H
         CLC   0(1,R2),UCBUNTYP         LOOK UP TABLE
         BE    PRINTIT                  MATCH
         LA    R2,ENTRYLEN(,R2)         NO MATCH, BUMP TABLE
         CLI   0(R2),0                  END OF TABLE ?
         BE    PRINTIT                  YES, GO PRINT
         B     TAPLOOP                  NO, GET NEXT
*
*   CTC PROCESSING
*
CTC      DS    0H
         LA    R2,CTCTAB                POINT TO TABLE
CTCLOOP  DS    0H
         CLC   0(1,R2),UCBUNTYP         LOOK UP TABLE
         BE    PRINTIT                  MATCH
         LA    R2,ENTRYLEN(,R2)         NO MATCH, BUMP TABLE
         CLI   0(R2),0                  END OF TABLE ?
         BE    PRINTIT                  YES, GO PRINT
         B     CTCLOOP                  NO, GET NEXT
*
*   UR PROCESSING
*
UR       DS    0H
         LA    R2,URTAB                 POINT TO TABLE
URLOOP   DS    0H
         CLC   0(1,R2),UCBUNTYP         LOOK UP TABLE
         BE    PRINTIT                  MATCH ?
         LA    R2,ENTRYLEN(,R2)         NO MATCH, BUMP TABLE
         CLI   0(R2),0                  END OF TABLE ?
         BE    PRINTIT                  YES, GO PRINT
         B     URLOOP                   NO, GET NEXT
*
*   COMM PROCESSING
*
COMM     DS    0H
         LA    R2,COMMTAB               POINT TO TABLE
COMMLOOP DS    0H
         CLC   0(1,R2),UCBUNTYP         LOOK UP TABLE
         BE    PRINTIT                  MATCH
         LA    R2,ENTRYLEN(,R2)         NO MATCH, BUMP TABLE
         CLI   0(R2),0                  END OF TABLE
         BE    PRINTIT                  YES, GO PRINT
         B     COMMLOOP                 NO, GET NEXT
*
*   DISPLAY PROCESSING
*
DISP     DS    0H
         MVC   TYPEMOD,=C'3270   '      FORCE DEVICE TYPE
         PUT   OUTUCB,PRINTLN           PRINT IT
         B     SEARCH                   GO GET NEXT UCB
*
*   PRINT RESULTS
*
PRINTIT  DS    0H
         MVC   TYPEMOD,1(R2)            MOVE DEVICE TYPE
         PUT   OUTUCB,PRINTLN           PRINT
         B     SEARCH                   GO BACK
TIMETOGO DS    0H
         L     R13,SAVEAREA+4           RESTORE
         RETURN (14,12),RC=0            REGISTERS
         SPACE
BTX      DC    C'0123456789ABCDEF'
SAVEAREA DC    18F'0'
SCANWORK DS    CL100
UCBWORK  DS    CL48' '
TOKEN    DS    CL48' '
COMMEXT  DS    CL32' '
DCEWORK  DS    CL256' '
PRINTLN  DS    0CL132
ADDR     DC    CL4' '
FILL1    DC    CL1' '
TYPEMOD  DC    CL7' '
FILL2    DC    CL1' '
VOLID    DC    CL6' '
FILL3    DC    CL8' CHPIDS '
CHPIDS   DS    CL27'XX XX XX XX XX XX XX XX'
CHPCHP1  EQU   CHPIDS
CHPCHP2  EQU   CHPIDS+3
CHPCHP3  EQU   CHPIDS+6
CHPCHP4  EQU   CHPIDS+9
CHPCHP5  EQU   CHPIDS+12
CHPCHP6  EQU   CHPIDS+15
CHPCHP7  EQU   CHPIDS+18
CHPCHP8  EQU   CHPIDS+21
FILL5    DC    CL1' '
SYSNAME  DC    CL8' '
FILL6    DC    CL1' '
CPUID    DC    CL6' '
FILL7    DC    CL1' '
NUMCPS   DC    CL3' '
FILL8    DC    CL59' '
BLANKS   DC    CL132' '
INFOAREA DS    CL256
INFORTCD DS    F
DBW      DS    D
DASDTAB  DS    0H
         DC    X'0E',CL7'3380'
ENTRYLEN EQU *-DASDTAB
         DC    X'1E',CL7'3380-D'
         DC    X'21',CL7'3380-J'
         DC    X'23',CL7'3380-K'
         DC    X'2E',CL7'3380-E'
         DC    X'26',CL7'3390-1'
         DC    X'27',CL7'3390-2'
         DC    X'24',CL7'3390-3'
         DC    X'32',CL7'3390-9'
         DC    X'FF',CL7'3390-27'
         DC    X'FF',CL7'3390-54'
         DC    X'00',CL7'DASD'
TAPETAB  DS    0H
         DC    X'03',CL7'3400'
         DC    X'80',CL7'3480'
         DC    X'81',CL7'3490'
         DC    X'83',CL7'3590'
         DC    X'00',CL7'3390TAP'
CTCTAB   DS    0H
         DC    X'00',CL7'3088'
         DC    X'01',CL7'SCTC'
         DC    X'02',CL7'BCTC'
         DC    X'03',CL7'RS6K'
         DC    X'04',CL7'3172'
         DC    X'05',CL7'OSA'
         DC    X'06',CL7'OSA-D'
         DC    X'0F',CL7'OSA-R'
         DC    X'00',CL7'CTC'
URTAB    DS    0H
         DC    X'0F',CL7'AFP1'
         DC    X'09',CL7'3211'
         DC    X'0E',CL7'3800'
         DC    X'4C',CL7'3838'
         DC    X'19',CL7'3895'
         DC    X'11',CL7'4245'
         DC    X'13',CL7'4248'
         DC    X'0C',CL7'3525'
         DC    X'00',CL7'DUMMY'
COMMTAB  DS    0H
         DC    X'11',CL7'2701'
         DC    X'15',CL7'3745'
         DC    X'25',CL7'3705'
         DC    X'91',CL7'2702'
         DC    X'92',CL7'2701'
         DC    X'93',CL7'2703'
         DC    X'F1',CL7'3791'
         DC    X'00',CL7'COMM'
         PRINT OFF
PATH     DSECT
         IOSDPATH
         IOSDMAP
UCB      DSECT
         IEFUCBOB
DCE      DSECT
         IECDDCE
PATH2    DSECT
         IOSDUPFX
         CVT   DSECT=YES
         IHAPCCA
         PRINT ON
         END
