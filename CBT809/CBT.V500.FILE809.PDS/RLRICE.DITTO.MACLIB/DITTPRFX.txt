.* ------------------------------------------------------------------ *
.*                                                                    *
.*       ALL MODULES START WITH A COMMON PREFIX SET OF INSTRUCTIONS   *
.*       AND EYECATCHER.  THIS ALLOWS THE TRACE ROUTINE AND THE       *
.*       ABEND EXITS TO DETERMINE WHICH MODULE IS IN CONTROL.         *
.*                                                                    *
.* ------------------------------------------------------------------ *
         MACRO
&NAME    DITTPRFX &SAVE,&DESC,&TYPE=CSECT
         AIF   ('&TYPE' EQ 'DSECT').L0010
         AIF   (T'&SAVE EQ 'O').ERR0010
         AIF   (T'&DESC EQ 'O').ERR0020
         AIF   (T'&NAME EQ 'O').ERR0030
&NAME    CSECT
         USING &NAME,R12                                                ASE00100
         STM   R14,R12,12(R13)     SAVE ENTRY REGISTERS                 ASE00110
         LR    R12,R15             LOAD PERMANENT BASE                  ASE00120
         LR    R2,R13              SAVE OLD REGISTER SAVE AREA ADDRESS  ASE00130
         LA    R13,&SAVE           POINT TO OUR SAVE AREA               ASE00140
         ST    R13,8(R2)           CHAIN FORWARD                        ASE00150
         ST    R2,4(R13)           CHAIN BACKWARD                       ASE00160
         B     @@000001            BYPASS ID/TIME/DESCRIPTION
         DC    CL8'&NAME'          MODULE NAME
         DC    CL8'&SYSDATE'       DATE OF ASSEMBLY
         DC    CL6'&SYSTIME'       TIME OF ASSEMBLY
         DC    C&DESC
@@000001 DS    0H
         MEXIT
.L0010   ANOP
MODPRFX  DSECT
         STM   R14,R12,12(R13)     SAVE ENTRY REGISTERS                 ASE00110
         LR    R12,R15             LOAD PERMANENT BASE                  ASE00120
         LR    R2,R13              SAVE OLD REGISTER SAVE AREA ADDRESS  ASE00130
         LA    R13,0               POINT TO SAVE AREA                   ASE00140
         ST    R13,8(R2)           CHAIN FORWARD                        ASE00150
         ST    R2,4(R13)           CHAIN BACKWARD                       ASE00160
         B     0(R0)               BRANCH AROUND MOD NAME/TIME/DESC
PFXMODNM DS    CL8                 MODULE NAME
PFXMODDT DS    CL8                 ASSEMBLY DATE
PFXMODTM DS    CL6                 ASSEMBLY TIME
PFXMODDS DS    C                   DESCRIPTION
         MEXIT
.ERR0010 ANOP
         MNOTE 12,'SAVE AREA NAME IS REQUIRED'
         MEXIT
.ERR0020 ANOP
         MNOTE 12,'DESCRIPTION IS REQUIRED'
         MEXIT
.ERR0030 ANOP
         MNOTE 12,'CSECT NAME IS REQUIRED'
         MEXIT
         MEND
