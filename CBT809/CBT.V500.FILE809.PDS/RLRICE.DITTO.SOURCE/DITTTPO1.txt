* ------------------------------------------------------------------- *
*                                                                     *
*        TAPE OUTPUT MODULE.                                          *
*                                                                     *
*        THIS MODULE DOES 'NORMAL' TAPE OUTPUT.  OUTPUT CAN BE        *
*        COPIED BLOCK-FOR-BLOCK FROM INPUT TO OUTPUT OR CAN BE        *
*        RE-BLOCKED TO THE SPECIFIED BLOCK SIZE.  ON END OF FILE      *
*        A TAPE-MARK WILL BE WRITTEN TO THE OUTPUT.                   *
*                                                                     *
* ------------------------------------------------------------------- *
DITTTPO1 DITTPRFX TAPESAVE,'TAPE OUTPUT MODULE'
         USING DITTCOMM,R11        SPECIFY BASE
         DITTRACE ID=ENTRY         TRACE ENTRY
         L     R10,COMMOUTD        DYNAMIC BLOCK ADDRESS
         USING DYNBLOK,R10         SPECIFY BASE
         ST    R10,EXCPDYN         PASS DYNAMIC BLOCK ADDRESS TO EXCP
         TM    COMMFLAG,$COMMCLS   CLOSE FILE INDICATOR??
         BO    TAPE0090            YES
         TM    COMMFLAG,$COMM1ST   FIRST PASS??
         BNO   TAPE0010            NO
         MVC   TAPECRCD,ACOMMIOO   STARTING ADDRESS IN OUTPUT BLOCK
         MVC   TAPEREM,COMMBLSZ    CLEAR REMAINING BLOCK AMOUNT
         ZAP   TAPEBLKS,COMMP0     RESET BLOCK COUNT
TAPE0010 DS    0H
         ICM   R1,15,COMMIND       INPUT DYNAMIC BLOCK ADDRESS
         BZ    TAPE0020            NO DYNAMIC BLOCK FOR INPUT
         CLC   TAPETAPE,DYNDDNAM-DYNBLOK(R1)
         BNE   TAPE0020            NOT TAPE
         TM    DYNSTAT-DYNBLOK(R1),$DYNTM
         BO    TAPE0090            END OF THIS LOGICAL FILE
TAPE0020 DS    0H
         LA    R2,DYNDCB           POINT TO DCB
         USING IHADCB,R2           ADDRESS DCB
         DITTRACE ID=CCWINIT       TRACE INTIALIZE ROUTINE
         L     R1,ACOMMIOO         POINT TO INPUT BUFFER
         STCM  R1,7,WRITECCW+1     RELOCATE BUFFER ADDRESS
         STCM  R1,7,WTMCCW+1       RELOCATE BUFFER ADDRESS
TAPE0030 DS    0H
         TM    COMMOFLG,$REBLOCK   RE-BLOCKING RECORDS??
         BNO   TAPE0050            NO, OUTPUT INDIVIDUAL BLOCK
         CLC   COMMCRCL,COMMBLSZ   IS RECORD LONGER THAN MAX BLOCK??
         BH    TAPE0410            YES, NO WAY JOSE
         CLC   COMMCRCL,TAPEREM    WILL RECORD FIT IN THIS BLOCK??
         BH    TAPE0060            NO, OUTPUT THIS BLOCK
TAPE0040 DS    0H
         DITTRACE ID=ADDBLOCK      TRACE ADDITIONS TO BLOCK
         SR    R0,R0               CLEAR REGISTER
         SR    R1,R1               CLEAR REGISTER
         ICM   R0,3,TAPEREM        LOAD BLOCK SIZE LEFT
         ICM   R1,3,COMMCRCL       LENGTH OF NEW RECORD
         SR    R0,R1               SUBTRACT LENGTH OF NEW RECORD
         STH   R0,TAPEREM          SAVE LENGTH LEFT
TAPE0050 DS    0H
         DITTRACE ID=COPYRECD      TRACE COPY OF INPUT TO OUTPUT BLOCK
         L     R0,COMMCRCD         GET RECORD ADDRESS
         SR    R1,R1               CLEAR REGISTER
         ICM   R1,3,COMMCRCL       GET RECORD LENGTH
         L     R2,TAPECRCD         GET CURRENT OUTPUT ADDRESS
         LR    R3,R1               COPY RECORD ADDRESS
         MVCL  R2,R0               MOVE RECORD TO OUTPUT
         L     R2,TAPECRCD         LOAD CURRENT ADDRESS
         SR    R1,R1               CLEAR REGISTER
         ICM   R1,3,COMMCRCL       GET RECORD LENGTH
         AR    R2,R1               ADD CURRENT RECORD LENGTH
         ST    R2,TAPECRCD         SAVE TAPE OUTPUT ADDRESS
         TM    COMMOFLG,$REBLOCK   RE-BLOCKING RECORDS??
         BO    TAPE9900            YES, DON'T WRITE 'TILL BLOCK FULL
TAPE0060 DS    0H
         SR    R0,R0               CLEAR REGISTER
         ICM   R0,3,COMMCRCL       GET RECORD LENGTH
         TM    COMMOFLG,$REBLOCK   RE-BLOCKING RECORDS??
         BNO   TAPE0070            NO, USE CURRENT RECORD SIZE
         ICM   R0,3,COMMBLSZ       LOAD BLOCK SIZE
         SR    R1,R1               CLEAR REGISTER
         ICM   R1,3,TAPEREM        UNUSED PORTION
         SR    R0,R1               SUBTRACT UNUSED PORTION
TAPE0070 DS    0H
         STH   R0,WRITECCW+6       STORE THE RECORD LENGTH
TAPE0080 DS    0H
         DITTRACE ID=WRITE         TRACE WRITES TO TAPE
         MVC   DYNCCW,WRITECCW     INITIALIZE THE CCW
         LA    R1,EXCPBLOK         EXCP INTERFACE PARAMETERS
         L     R15,AEXCP           EXCP MODULE ENTRY POINT
         BALR  R14,R15             LINK TO EXCP MODULE
         CLI   EXCPSTAT,$EXCPNA    I/O ALLOWED?
         BE    TAPE0130            NOT ALLOWED
         TM    DYNSTAT,$DYNPROT    FILE PROTECT ON?
         BO    TAPE0190            YES
         TM    DYNSTAT,$DYNPEOT    END OF TAPE REACHED?
         BO    TAPE0160            YES
         CLI   EXCPSTAT,$EXCPOK    WRITE SUCCESSFUL??
         BNE   TAPE0400            NO
         AP    TAPEBLKS,COMMP1     ADD 1 TO BLOCKS WRITTEN TO TAPE
         SR    R1,R1               CLEAR REGISTER
         MVC   TAPECRCD,ACOMMIOO   SAVE CURRENT RECORD ADDRESS
         MVC   TAPEREM,COMMBLSZ    RESET BLOCK SIZE
         TM    COMMOFLG,$REBLOCK   RE-BLOCKING RECORDS??
         BO    TAPE0040            YES, INSERT NEW RECORD
         B     TAPE9900            EXIT
TAPE0090 DS    0H
         TM    COMMOFLG,$REBLOCK   RE-BLOCKING RECORDS??
         BNO   TAPE0100            NO, WRITE END OF FILE TAPE MARK
         CLC   TAPEREM,COMMBLSZ    IS FULL BLOCK STILL AVAILABLE??
         BE    TAPE0100            YES, LAST BLOCK IS EMPTY
         SR    R1,R1               CLEAR FOR LENGTH
         ICM   R1,3,COMMBLSZ       LOAD BLOCK SIZE
         SH    R1,TAPEREM          SUBTRACT UNUSED PORTION
         STH   R1,WRITECCW+6       STORE THE RECORD LENGTH
         MVC   DYNCCW,WRITECCW     COPY CCW
         DITTRACE ID=LASTWRT       TRACE LAST BLOCK WRITES
         LA    R1,EXCPBLOK         EXCP INTERFACE PARAMETERS
         L     R15,AEXCP           EXCP MODULE ENTRY POINT
         BALR  R14,R15             LINK TO EXCP MODULE
         CLI   EXCPSTAT,$EXCPOK    WRITE SUCCESSFUL??
         BNE   TAPE0400            NO
TAPE0100 DS    0H
         MVC   DYNCCW,WTMCCW       MOVE WTM CCW
         DITTRACE ID=WRITETM       TRACE TAPE MARK WRITES
         LA    R1,EXCPBLOK         EXCP INTERFACE PARAMETERS
         L     R15,AEXCP           EXCP MODULE ENTRY POINT
         BALR  R14,R15             LINK TO EXCP MODULE
         CLI   EXCPSTAT,$EXCPOK    WRITE SUCCESSFUL??
         BNE   TAPE0400            NO
         MVC   PRBLKS,BLKSEDWD     MOVE EDIT WORD
         ED    PRBLKS,TAPEBLKS     EDIT BLOCKS WRITTEN
         CLI   COMMENV,$ENVTSO     TSO ENVIRONMENT?
         BE    TAPE0110            YES
         MVC   PRTDATA(BLKSMSGL),BLKSMSG
         MVI   PRTCC,C'0'          MOVE CARRIAGE CONTROL
         LA    R1,PRTBLOK          POINT TO PRINT BLOCK
         L     R15,APRT            PRINT MODULE ENTRY POINT
         BALR  R14,R15             LINK TO PRINT MODULE
         B     TAPE0120
TAPE0110 DS    0H
         LA    R1,BLKSMSG          MESSAGE ADDRESS
         LA    R0,BLKSMSGL         MESSAGE LENGTH
         TPUT  (R1),(R0)           SEND BLOCKS WRITTEN MESSAGE
TAPE0120 DS    0H
         MVC   TAPECRCD,ACOMMIOO   STARTING ADDRESS IN OUTPUT BLOCK
         MVC   TAPEREM,COMMBLSZ    CLEAR REMAINING BLOCK AMOUNT
         ZAP   TAPEBLKS,COMMP0     RESET BLOCK COUNT
         B     TAPE9900            EXIT
TAPE0130 DS    0H
         DITTRACE ID=NOTALLOW
         CLI   COMMENV,$ENVTSO     TSO USER?
         BE    TAPE0150            YES
         CLI   COMMENV,$ENVJOB     BATCH JOB?
         BE    TAPE0140            YES
         WTO   'TAPE OUTPUT NOT ALLOWED WHILE TAPE IS AT PHYSICAL EOT',+
               CONSNAME=COMMCNAM
TAPE0140 DS    0H
         MVI   PRTCMMD,$PRTCMD     SET PRINTER COMMAND
         MVC   PRTDATA(NAMSGL),NAMSG
         LA    R1,PRTBLOK          PRINTER INTERFACE BLOCK
         L     R15,APRT            PRINT MODULE ENTRY POINT
         BALR  R14,R15             PRINT MESSAGE
         OI    COMMFLAG,$ABORT     SET ABORT FLAG
         B     TAPE9900            AND EXIT
TAPE0150 DS    0H
         MVC   MSG01(NAMSGL),NAMSG SET MESSAGE
         OI    COMMFLAG,$ABORT     SET ABORT FLAG
         B     TAPE9900            AND EXIT
TAPE0160 DS    0H
         DITTRACE ID=PEOT
         CLI   COMMENV,$ENVTSO     TSO USER?
         BE    TAPE0180            YES
         CLI   COMMENV,$ENVJOB     BATCH JOB?
         BE    TAPE0170            YES
         WTO   'PHYSICAL EOT REACHED ON OUTPUT COMMAND TERMINATED',    +
               CONSNAME=COMMCNAM
TAPE0170 DS    0H
         MVI   PRTCMMD,$PRTDATA    SET PRINTER COMMAND
         MVC   PRTDATA(PEOTMSGL),PEOTMSG
         LA    R1,PRTBLOK          PRINTER INTERFACE BLOCK
         L     R15,APRT            PRINT MODULE ENTRY POINT
         BALR  R14,R15             PRINT MESSAGE
         OI    COMMFLAG,$COMMEOF   SET EOF FLAG
         B     TAPE9900            AND EXIT
TAPE0180 DS    0H
         MVC   MSG01(PEOTMSGL),PEOTMSG
         OI    COMMFLAG,$COMMEOF   SET EOF FLAG
         B     TAPE9900            AND EXIT
TAPE0190 DS    0H
         DITTRACE ID=FILEPROT
         CLI   COMMENV,$ENVTSO     TSO USER?
         BE    TAPE0210            YES
         CLI   COMMENV,$ENVJOB     BATCH JOB?
         BE    TAPE0200            YES
         WTO   'OUTPUT TAPE IS FILE PROTECTED (NO WRITE RING)',        +
               CONSNAME=COMMCNAM
TAPE0200 DS    0H
         MVI   PRTCMMD,$PRTDATA    SET PRINTER COMMAND
         MVC   PRTDATA(PROTMSGL),PROTMSG
         LA    R1,PRTBLOK          PRINTER INTERFACE BLOCK
         L     R15,APRT            PRINT MODULE ENTRY POINT
         BALR  R14,R15             PRINT MESSAGE
         OI    COMMFLAG,$COMMEOF   SET EOF FLAG
         B     TAPE9900            AND EXIT
TAPE0210 DS    0H
         MVC   MSG01(PROTMSGL),PROTMSG
         OI    COMMFLAG,$COMMEOF   SET EOF FLAG
         B     TAPE9900            AND EXIT
         EJECT
* ------------------------------------------------------------------- *
*                                                                     *
*                    ERROR ROUTINES                                   *
*                                                                     *
* ------------------------------------------------------------------- *
TAPE0400 DS    0H
         LA    R3,DYNIOB           POINT TO IOB
         DITTRACE ID=WRITERR       TRACE I/O ERRORS
         DC    H'0'                ABEND
TAPE0410 DS    0H
         OI    COMMFLAG,$ABORT     ABORT CURRENT FUNCTION
         CLI   COMMENV,$ENVTSO     TSO ENVIRONMENT?
         BE    TAPE0420            YES
         MVC   PRTDATA(LONGMSGL),LONGMSG
         MVI   PRTCC,C'0'          MOVE CARRIAGE CONTROL
         LA    R1,PRTBLOK          POINT TO PRINT BLOCK
         L     R15,APRT            PRINT MODULE ENTRY POINT
         BALR  R14,R15             LINK TO PRINT MODULE
         CLI   COMMENV,$ENVSTC     RUNNING AS A STARTED TASK??
         BNE   TAPE9900            NO
         WTO   'RECORD ENCOUNTERED THAT IS LONGER THAN MAXIMUM OUTPUT B+
               LOCK SIZE',                                             +
               CONSNAME=COMMCNAM
         B     TAPE9900            EXIT
TAPE0420 DS    0H
         MVC   MSG02(LONGMSGL),LONGMSG
         B     TAPE9900            EXIT
         SPACE 2
* ------------------------------------------------------------------- *
*                                                                     *
*              MODULE EXIT POINT                                      *
*                                                                     *
* ------------------------------------------------------------------- *
TAPE9900 DS    0H
         DITTRACE ID=EXIT          TRACE EXIT
         L     R13,4(R13)          RESTORE REGISTER 13                  ASE01670
         LM    R14,R12,12(R13)     RESTORE ALL OTHER REGISTERS          ASE01680
         SR    R15,R15             GIVE GOOD RETURN CODE                ASE01690
         BR    R14                 RETURN TO OPERATING SYSTEM           ASE01700
         SPACE 2
* ------------------------------------------------------------------- *
*                                                                     *
*              WORKAREAS/CONSTANTS                                    *
*                                                                     *
* ------------------------------------------------------------------- *
WRITECCW CCW   X'01',*,SILI,0      WRITE CCW
WTMCCW   CCW   X'1F',*,SILI,24     WRITE TAPE MARK CCW
TAPESAVE DC    18F'0'              REGISTER SAVE AREA
TAPECRCD DC    A(0)                CURRENT RECORD ADDRESS
TAPEREM  DC    H'0'                LENGTH REMAINING IN RECORD
TAPEBLKS DC    PL5'0'              BLOCKS WRITTEN TO OUTPUT
BLKSEDWD DC    X'402020206B2020206B202120'
TAPETAPE DC    CL4'TAPE'           CONSTANT
LONGMSG  DC    C'* * * * * * TAPE RECORD ENCOUNTERED ON INPUT LONGER TH+
               AN MAXIMUM OUTPUT BLOCK SIZE * * * * * *'
LONGMSGL EQU   *-LONGMSG
BLKSMSG  DS    0C
PRBLKS   DC    CL12' '
         DC    C' BLOCKS AND TAPE MARK WRITTEN TO OUTPUT'
BLKSMSGL EQU   *-BLKSMSG
NAMSG    DC    C'TAPE OUTPUT NOT ALLOWED WHILE TAPE IS AT PHYSICAL EOT'
NAMSGL   EQU   *-NAMSG
PEOTMSG  DC    C'PHYSICAL EOT REACHED ON OUTPUT, COMMAND TERMINATED'
PEOTMSGL EQU   *-PEOTMSG
PROTMSG  DC    C'OUTPUT TAPE IS FILE PROTECTED (NO WRITE RING), COMMAND+
               TERMINATED'
PROTMSGL EQU   *-PROTMSG
         SPACE 2
* ------------------------------------------------------------------- *
*                                                                     *
*              PRINT MODULE INTERFACE BLOCK                           *
*                                                                     *
* ------------------------------------------------------------------- *
PRTBLOK  PRTBLOK
* ------------------------------------------------------------------- *
*                                                                     *
*              EXCP INTERFACE BLOCK                                   *
*                                                                     *
* ------------------------------------------------------------------- *
EXCPBLOK EXCPBLOK
         SPACE 2
* ------------------------------------------------------------------- *
*                                                                     *
*              REGISTER EQUATES                                       *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY  REGEQU
SILI     EQU   X'20'
         ABCODES
         EJECT
* ------------------------------------------------------------------- *
*                                                                     *
*              COMMON MODULE DSECT                                    *
*                                                                     *
* ------------------------------------------------------------------- *
DITTCOMM DITTCOMM TYPE=DSECT
         EJECT
* ------------------------------------------------------------------- *
*                                                                     *
*              DYNAMIC BLOCK DSECT                                    *
*                                                                     *
* ------------------------------------------------------------------- *
DYNBLOK  DYNBLOK TYPE=DSECT
         EJECT
* ------------------------------------------------------------------- *
*                                                                     *
*              MVS IOB DSECT                                          *
*                                                                     *
* ------------------------------------------------------------------- *
         IEZIOB
IOBLEN   EQU  *-IOB
* ------------------------------------------------------------------- *
*                                                                     *
*              MVS DCB DSECT                                          *
*                                                                     *
* ------------------------------------------------------------------- *
         DCBD DSORG=PS
DYNDCBL  EQU   *-IHADCB
         END  DITTTPO1
