* ------------------------------------------------------------------- *
*                                                                     *
*              PRINT MODULE                                           *
*                                                                     *
*      THIS MODULE DOES ALL PRINTING FOR DITTO... ALL COMMANDS,       *
*      MESSAGES, AND SYSOUT OUTPUT IS DONE BY THIS MODULE.  ANY       *
*      MODULE EXCEPT THE TRACE ROUTINE IN 'DITTCOMM' MAY LINK TO      *
*      THIS MODULE.                                                   *
*                                                                     *
* ------------------------------------------------------------------- *
DITTPRT  DITTPRFX PRTSAVE,'PRINTER MODULE'
         USING DITTCOMM,R11        ADDRESS IT
         LR    R10,R1              SAVE INPUT PARAMETER ADDRESS
         USING PRTBLOK,R10         SPECIFY BASE
         DITTRACE ID=ENTRY,        TRACE ENTRY                         +
               DATA1=PRTFLAG       .. CAPTURE FLAG STATUS
         TM    COMMFLAG,$COMMEOJ   END OF JOB IN PROGRESS??
         BNO   PRT0010             NO
         TM    PRTFLAG,$PRTOPEN    IS PRINT FILE OPEN??
         BNO   PRT9900             NO, EXIT
         DITTRACE ID=CLOSE         TRACE PRINTER CLOSES
         CLOSE PRINTER             OTHERWISE CLOSE PRINT FILE
         NI    PRTFLAG,255-$PRTOPEN TURN OFF OPEN FLAG
         B     PRT9900             EXIT THIS MODULE
PRT0010  DS    0H
         TM    PRTFLAG,$PRTOPEN    IS PRINT FILE OPEN??
         BO    PRT0020             YES, BYPASS OPEN
         DITTRACE ID=OPEN          TRACE PRINTER OPENS
         OPEN  (PRINTER,OUTPUT)    OPEN PRINTER FILE
         OI    PRTFLAG,$PRTOPEN    INDICATE PRINT FILE IS OPEN
PRT0020  DS    0H
         CLI   PRTCMMD,$PRTHEAD    FORCED HEADING??
         BE    PRT0080             YES
         CLI   PRTCMMD,$PRTDATA    PRINTING DATA?
         BE    PRT0040             YES..
* ------------------------------------------------------------------- *
*                                                                     *
*         PRINTING A COMMAND                                          *
*                                                                     *
* ------------------------------------------------------------------- *
         DITTRACE ID=PRINTCMD      PRINTING A COMMAND OR COMMAND MSG
         TM    PRTFLAG,$PRTDATA    WAS DATA PRINTED LAST TIME?
         BNO   PRT0030             NO
         BAL   R9,PRT0090          PRINT A NEW HEADING
PRT0030  DS    0H
         NI    PRTFLAG,255-$PRTDATA    TURN DATA FLAG OFF
         OI    PRTFLAG,$PRTCMD     TURN COMMAND FLAG ON
         BAL   R9,PRT0060          PRINT COMMAND
         B     PRT9900             AND EXIT
* ------------------------------------------------------------------- *
*                                                                     *
*         PRINTING DATA                                               *
*                                                                     *
* ------------------------------------------------------------------- *
PRT0040  DS    0H
         DITTRACE ID=PRTDATA
         TM    PRTFLAG,$PRTCMD     WAS A COMMAND PRINTED LAST TIME?
         BNO   PRT0050             NO
         BAL   R9,PRT0090          PRINT NEW HEADING
PRT0050  DS    0H
         NI    PRTFLAG,255-$PRTCMD TURN OFF COMMAND PRINTED
         OI    PRTFLAG,$PRTDATA    TURN ON DATA PRINTED
         BAL   R9,PRT0060          PRINT DATA
         B     PRT9900             AND EXIT
PRT0060  DS    0H
         PUT   PRINTER,PRTCC       PRINT THE REQUESTED LINE
         AP    COMMLINE,COMMP1     ADD 1 TO LINE COUNT
         CLI   PRTCC,C' '          SINGLE SPACE??
         BE    PRT0070             YES
         AP    COMMLINE,COMMP1     ADD 1 TO LINE COUNT
         CLI   PRTCC,C'0'          DOUBLE SPACE??
         BE    PRT0070             YES
         AP    COMMLINE,COMMP1     ADD 1 TO LINE COUNT
PRT0070  DS    0H
         MVI   PRTCC,C' '          CLEAR CARRIAGE CONTROL
         MVC   PRTDATA,PRTCC       CLEAR PRINT DATA
         CP    COMMLINE,COMMP60    IS PAGE FULL??
         BLR   R9                  NO, EXIT
         DITTRACE ID=PAGEFULL      PAGE FULL
         B     PRT0090
PRT0080  DS    0H
         DITTRACE ID=FORCEDHD      FORCED HEADING
         BAL   R9,PRT0090          PRINT HEADING
         B     PRT9900             AND EXIT
PRT0090  DS    0H
         DITTRACE ID=HEADING
         AP    PRTPAGE,COMMP1      ADD 1 TO PAGE COUNT
         MVC   PRPAGE,=X'4020206B202021'
         ED    PRPAGE,PRTPAGE      EDIT PAGE NUMBER
         PUT   PRINTER,DITTHEAD    PRINT HEADING
         ZAP   COMMLINE,COMMP1     RESET LINE COUNT
         AP    COMMLINE,COMMP1     RESET LINE COUNT
         BR    R9                  PRINTING DONE
* ------------------------------------------------------------------- *
*                                                                     *
*              MODULE EXIT POINT                                      *
*                                                                     *
* ------------------------------------------------------------------- *
PRT9900  DS    0H
         DITTRACE ID=EXIT          TRACE EXIT
         L     R13,4(R13)          RESTORE REGISTER 13                  ASE01670
         LM    R14,R12,12(R13)     RESTORE ALL OTHER REGISTERS          ASE01680
         SR    R15,R15             GIVE GOOD RETURN CODE                ASE01690
         BR    R14                 RETURN TO CALLER                     ASE01700
         EJECT
* ------------------------------------------------------------------- *
*                                                                     *
*              PRINT MODULE WORK AREAS                                *
*                                                                     *
* ------------------------------------------------------------------- *
PRTSAVE  DC    18F'0'              REGISTER SAVE AREA
* ------------------------------------------------------------------- *
*                                                                     *
*   THE BITS IN PRTFLAG ALSO HAVE DEFINITIONS IN THE 'PRTCMMD'        *
*   FIELD OF THE 'PRTBLOK' DSECT ($PRTCMD AND $PRTDATA).  IF NEW      *
*   FLAGS ARE DEFINED IN PRTFLAG, YOU MUST USE BITS OTHER THAN THE    *
*   ONES USED FOR $PRTCMD AND $PRTDATA.                               *
*                                                                     *
* ------------------------------------------------------------------- *
PRTFLAG  DC    X'00'               FLAGS/SWITCHES
$PRTOPEN EQU   X'80'               .. FILE OPEN
PRTPAGE  DC    PL3'0'              PAGE COUNTER
DITTHEAD DC    C'1'                CARRIAGE CONTROL
         DC    CL108'            OS/DITTO'
         DC    C'PAGE '
PRPAGE   DC    CL7' '              PAGE NUMBER
         SPACE 2
* ------------------------------------------------------------------- *
*                                                                     *
*              PRINT MODULE PRINTER DCB                               *
*                                                                     *
* ------------------------------------------------------------------- *
PRINTER  DCB   DDNAME=SYSPRINT,                                        +
               DSORG=PS,                                               +
               RECFM=FBA,                                              +
               LRECL=121,                                              +
               MACRF=PM
         SPACE 2
* ------------------------------------------------------------------- *
*                                                                     *
*              PRINT MODULE INTERCOMMUNICATION                        *
*                                                                     *
* ------------------------------------------------------------------- *
PRTBLOK  PRTBLOK TYPE=DSECT
         SPACE 2
* ------------------------------------------------------------------- *
*                                                                     *
*              REGISTER EQUATES                                       *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY  REGEQU
         EJECT
* ------------------------------------------------------------------- *
*                                                                     *
*              COMMON MODULE DSECT                                    *
*                                                                     *
* ------------------------------------------------------------------- *
DITTCOMM DITTCOMM TYPE=DSECT
         END  DITTPRT
