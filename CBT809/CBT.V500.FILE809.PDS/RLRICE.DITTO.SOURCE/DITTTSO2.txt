* ------------------------------------------------------------------- *
*                                                                     *
*              TAPE MAP 'TMAP' MODULE                                 *
*                                                                     *
*       THIS MODULE PERFORMS THE 'TMAP' FUNCTION FOR THE TSO          *
*       ENVIRONMENT.  'HDR1', 'HDR2', 'EOF1', AND 'EOF2' ARE WATCHED  *
*       FOR, AND WHEN ENCOUNTERED, THE RELATIVE FILE NUMBER AND FILE  *
*       NAME ARE PRINTED.  BY DEFAULT TMAP WILL STOP WHEN 2 TAPE      *
*       MARKS IN A ROW WITH NO INTERVIENING DATA ARE FOUND.  THE      *
*       OPTIONS 'FULL', 'FULLTAPE', OR 'ALL' WILL CAUSE THE TAPE TO   *
*       BE READ TO THE REFLECTIVE MARKER OR OFF THE END OF THE REEL.  *
*                                                                     *
* ------------------------------------------------------------------- *
DITTTSO2 DITTPRFX TMAPSAVE,'TAPE ''VTOC'' MODULE'
         USING DITTCOMM,R11        SPECIFY BASE
         DITTRACE ID=ENTRY         TRACE ENTRY
         TM    COMMFLAG,$COMMCLS   CLOSE FILES?
         BO    EXIT0000            YES.. EXIT
         ZAP   FILECT,COMMP0       ZERO FILES COUNTER
         MVI   TAPEFLAG,00         INITIALIZE FLAGS
         LA    R1,TBLEPARM         DEFINE TABLE PARMS
         L     R15,AISP            SPF INTERFACE ENTRY POINT
         BALR  R14,R15             LINK TO SPF TO DEFINE TABLE
         CH    R15,H4              DEFINE SUCCESSFUL?
         BH    ERR0010             NO
TAPE0010 DS    0H
         L     R10,COMMIND         DYNAMIC BLOCK ADDRESS
         USING DYNBLOK,R10         SPECIFY BASE
         ST    R10,EXCPDYN         PASS DYNAMIC BLOCK ADDRESS TO EXCP
         LA    R2,DYNDCB           DCB ADDRESS
         USING IHADCB,R2           DEFINE DSECT BASE
         DITTRACE ID=CCWINIT       TRACE INTIALIZE ROUTINE
         L     R1,ACOMMIOI         INPUT BUFFER
         STCM  R1,7,REWCCW+1       RELOCATE I/O ADDRESS
         STCM  R1,7,READCCW+1      RELOCATE I/O ADDRESS
         DITTRACE ID=REWIND        TRACE INITIAL REWIND
         MVC   DYNCCW,REWCCW       INITIALIZE THE CCW
         LA    R1,EXCPBLOK         EXCP INTERFACE BLOCK
         L     R15,AEXCP           EXCP MODULE ENTRY POINT
         BALR  R14,R15             LINK TO EXCP MODULE
         CLI   EXCPSTAT,$EXCPOK    EXCP SUCCESSFUL?
         BNE   TAPE0400            NO
         DITTRACE ID=GOODREW       REWIND SUCCESSFUL
TAPE0020 DS    0H
         DITTRACE ID=READFWD       TRACE READ FORWARD
         MVC   DYNCCW,READCCW      INITIALIZE THE CCW
         LA    R1,EXCPBLOK         EXCP INTERFACE BLOCK
         L     R15,AEXCP           EXCP MODULE ENTRY POINT
         BALR  R14,R15             LINK TO EXCP MODULE
         TM    DYNSTAT,$DYNPEOT    PHYSICAL END OF TAPE REACHED?
         BO    TAPE0230            YES
         CLI   DYNSTAT,$DYNTM      TAPE MARK READ?
         BE    TAPE0200            YES
         CLI   EXCPSTAT,$EXCPOK    EXCP SUCCESSFUL?
         BNE   TAPE0400            NO
         TM    TAPEFLAG,$TAPEFHD   FILE HEADING ALREADY PERFORMED?
         BO    TAPE0030            YES
         AP    FILECT,COMMP1       ADD 1 TO FILE COUNT
         MVC   PRFILE,COMMED2      INITIALIZE WITH EDIT WORD
         ED    PRFILE,FILECT       EDIT FILE NUMBER
         MVC   DATA(HEADMSGL),HEADMSG
         BAL   R9,ADD0000          ADD A LINE TO TABLE
         OI    TAPEFLAG,$TAPEFHD   SET HEADING FLAG
TAPE0030 DS    0H
         NI    TAPEFLAG,255-$TAPETM    TURN OFF TAPE MARK FLAG
         CLC   EXCPLEN,H80         WAS LENGTH 80?
         BNE   TAPE0040            NO
         TM    TAPEFLAG,$TAPEDTA   DATA FLAG ON??
         BO    TAPE0020            YES... READ ANOTHER BLOCK
         ICM   R1,7,READCCW+1      I/O AREA USED
         CLC   TAPEVOL1,0(R1)      'VOL1' RECORD??
         BE    VOL10000            YES
         CLC   TAPEHDR1,0(R1)      'HDR1' RECORD??
         BE    HDR10000            YES
         CLC   TAPEEOV1,0(R1)      'EOV1' RECORD??
         BE    EOV10000            YES
         CLC   TAPEEOF1,0(R1)      'EOF1' RECORD??
         BE    EOF10000            YES
         CLC   TAPEHDR2,0(R1)      'HDR2' RECORD??
         BE    HDR20000            YES
         CLC   TAPEEOF2,0(R1)      'EOF2' RECORD??
         BE    EOF20000            YES
         CLC   TAPEEOV2,0(R1)      'EOV2' RECORD??
         BE    EOV20000            YES
TAPE0040 DS    0H
         DITTRACE ID=DATA          TRACE DATA ENCOUNTERED
         TM    TAPEFLAG,$TAPEDTA   IS TAPE DATA FLAG ON??
         BO    TAPE0020            YES... READ ANOTHER BLOCK
         OI    TAPEFLAG,$TAPEDTA   SET DATA FLAG
         MVC   DATA(L'DATAMSG),DATAMSG
         BAL   R9,ADD0000          ADD A LINE TO TABLE
         B     TAPE0020            READ ANOTHER BLOCK FROM THE TAPE
* ------------------------------------------------------------------- *
*                                                                     *
*                    TAPE MARK HAS JUST BEEN READ                     *
*                                                                     *
* ------------------------------------------------------------------- *
TAPE0200 DS    0H
         DITTRACE ID=TAPEMARK,     TRACE TAPE MARK ENCOUNTERED         +
               DATA1=TAPEFLAG      .. CAPTURE FLAG SETTING
         TM    TAPEFLAG,$TAPETM    TAPE MARK FLAG ALREADY ON??
         BO    TAPE0210            YES ... TWO IN A ROW IS EOT
         OI    TAPEFLAG,$TAPETM    SET TAPE MARK FLAG
         NI    TAPEFLAG,255-$TAPEDTA   RESET DATA FLAG
         NI    TAPEFLAG,255-$TAPEFHD   RESET FILE HEADING FLAG
         B     TAPE0020            READ ANOTHER BLOCK
TAPE0210 DS    0H
         TM    TAPEFLAG,$TAPEEOT   LOGCIAL END OF TAPE ALREADY REACHED?
         BO    TAPE0220            DON'T RESEND LOGICAL EOT MESSAGE
         OI    TAPEFLAG,$TAPEEOT   SET LOGICAL EOT FLAG
         DITTRACE ID=TAPEPEOT      END OF TAPE
         MVC   DATA(L'LEOTMSG),LEOTMSG
         BAL   R9,ADD0000          LINK TO PRINT MODULE
TAPE0220 DS    0H
         CLC   FULL,COMMOPT        FULL OPTION?
         BE    TAPE0020            YES
         CLC   FULLTAPE,COMMOPT    FULLTAPE OPTION?
         BE    TAPE0020            YES
         CLC   ALL,COMMOPT         ALL OPTION?
         BE    TAPE0020            YES
         B     TAPE0240            STOP ON LOGICAL-END-OF-TAPE
TAPE0230 DS    0H
         DITTRACE ID=TAPEPEOT      PHYSICAL EOT REACHED
         MVC   DATA(L'PEOTMSG),PEOTMSG
         BAL   R9,ADD0000          LINK TO PRINT MODULE
TAPE0240 DS    0H
         OI    COMMFLAG,$COMMEOF   SET EOF FLAG
         DITTRACE ID=TBTOP         TRACE
         LA    R1,TOPPARM          PERFORM 'TBTOP'
         L     R15,AISP            SPF INTERFACE ENTRY POINT
         BALR  R14,R15             LINK TO SPF
         LTR   R15,R15             TBTOP SUCCESSFUL?
         BNZ   ERR0030             NO
         DITTRACE ID=TBDISPL       TRACE
         LA    R1,COMMPANL         PANEL NAME ADDRESS
         ST    R1,DISPPANL         SET PANEL NAME ADDRESS
         OI    DISPPANL,X'80'      SET END OF LIST
         LA    R1,DISPPARM         DISPLAY TABLE PARAMETERS
         L     R15,AISP            SPF INTERFACE ENTRY POINT
         BALR  R14,R15             LINK TO SPF
         LTR   R15,R15             RC=ZERO?
         BZ    EXIT0000            YES
         CH    R15,H8              END?
         BE    EXIT0000            YES
         CVD   R15,COMMDWRD        CONVERT TO DECIMAL
         MVC   MSG004RC,RCEDWD     INITIALIZE WITH EDIT WORD
         ED    MSG004RC,COMMDWRD+6 EDIT RETURN CODE
         LA    R1,MSG004           MESSAGE TEXT
         LA    R0,MSG004L          MESSAGE LENGTH
         B     ERR0090             ISSUE MESSAGE
         EJECT
* ------------------------------------------------------------------- *
*                                                                     *
*                    ERROR ROUTINES                                   *
*                                                                     *
* ------------------------------------------------------------------- *
TAPE0400 DS    0H
         LA    R3,DYNIOB           IOB USED FOR I/O
         DITTRACE ID=IOERR         TRACE I/O ERRORS
         DC    H'0'                ABEND
TAPE0410 DS    0H
         DITTRACE ID=ZERORECL      TRACE LRECL = ZERO
         DC    H'0'                ABEND
* ------------------------------------------------------------------- *
*                                                                     *
*              VOL1 RECORD PROCESSING                                 *
*                                                                     *
* ------------------------------------------------------------------- *
VOL10000 DS    0H
         USING VOL1,R1             DEFINE DSECT BASE
         DITTRACE ID=VOL1          TRACE VOL1
         MVC   PRVOL,VOL1VOL       MOVE VOLUME SERIAL NUMBER
         MVC   DATA(VOL1MSGL),VOL1MSG
         BAL   R9,ADD0000          ADD A LINE TO TABLE
         B     TAPE0020            READ ANOTHER BLOCK
* ------------------------------------------------------------------- *
*                                                                     *
*              HDR1 RECORD PROCESSING                                 *
*                                                                     *
* ------------------------------------------------------------------- *
HDR10000 DS    0H
         USING HDR1,R1             DEFINE DSECT BASE
         DITTRACE ID=HDR1          TRACE HDR1
         MVC   PRDSN,HDR1DSN       DATASET NAME
         MVC   PRVSEQ,HDR1VSEQ     VOLUME SEQUENCE
         MVC   PRDSEQ,HDR1DSEQ     DATASET SEQUENCE
         MVC   PRCDTE,HDR1CDTE     CREATE DATE
         MVC   PREXPD,HDR1EXPD     EXPIRATION DATE
         MVC   DATA(HDR1MSGL),HDR1MSG
         BAL   R9,ADD0000          ADD A LINE TO TABLE
         B     TAPE0020            READ ANOTHER BLOCK
* ------------------------------------------------------------------- *
*                                                                     *
*              EOF1 RECORD PROCESSING                                 *
*                                                                     *
* ------------------------------------------------------------------- *
EOF10000 DS    0H
         DITTRACE ID=EOF1          TRACE EOF1
         MVC   PRBLKC,HDR1BLKC     BLOCK COUNT
         MVC   DATA(EOF1MSGL),EOF1MSG
         BAL   R9,ADD0000          ADD A LINE TO TABLE
         B     TAPE0020            READ ANOTHER BLOCK
* ------------------------------------------------------------------- *
*                                                                     *
*              EOV1 RECORD PROCESSING                                 *
*                                                                     *
* ------------------------------------------------------------------- *
EOV10000 DS    0H
         DITTRACE ID=EOV1          TRACE EOV1
         MVC   DATA(L'EOV1MSG),EOV1MSG
         BAL   R9,ADD0000          ADD A LINE TO TABLE
         B     TAPE0020            READ ANOTHER BLOCK
* ------------------------------------------------------------------- *
*                                                                     *
*              HDR2 RECORD PROCESSING                                 *
*                                                                     *
* ------------------------------------------------------------------- *
HDR20000 DS    0H
         USING HDR2,R1             DEFINE DSECT BASE
         DITTRACE ID=HDR2          TRACE HDR2
         MVC   PRRF,HDR2RF         RECORD FORMAT
         MVC   PRBLSZ,HDR2BLSZ     BLOCK SIZE
         MVC   PRRSZ,HDR2RSZ       RECORD SIZE
         MVC   PRDEN,HDR2DEN       TAPE DENSITY
         MVC   PRJOB,HDR2JOB       JOB/JOB STEP
         MVC   DATA(HDR2MSGL),HDR2MSG
         BAL   R9,ADD0000          ADD A LINE TO TABLE
         B     TAPE0020            READ ANOTHER BLOCK
* ------------------------------------------------------------------- *
*                                                                     *
*              EOF2 RECORD PROCESSING                                 *
*                                                                     *
* ------------------------------------------------------------------- *
EOF20000 DS    0H
         DITTRACE ID=EOF2          TRACE EOF2
         MVC   DATA(L'EOF2MSG),EOF2MSG
         BAL   R9,ADD0000          ADD A LINE TO TABLE
         B     TAPE0020            READ ANOTHER BLOCK
* ------------------------------------------------------------------- *
*                                                                     *
*              EOV2 RECORD PROCESSING                                 *
*                                                                     *
* ------------------------------------------------------------------- *
EOV20000 DS    0H
         DITTRACE ID=EOV2          TRACE EOV2
         MVC   DATA(L'EOV2MSG),EOV2MSG
         BAL   R9,ADD0000          ADD A LINE TO TABLE
         B     TAPE0020            READ ANOTHER BLOCK
* ------------------------------------------------------------------- *
*                                                                     *
*              ADD ROWS TO SPF TABLE                                  *
*                                                                     *
* ------------------------------------------------------------------- *
ADD0000  DS    0H
         DITTRACE ID=TBADD,        TRACE TABLE ADD                     +
               DATA1=DATA,         .. FIRST 8 BYTES                    +
               DATA2=DATA+8        .. NEXT 8 BYTES
         LA    R1,ADDPARM          'TBADD' PARAMETERS
         L     R15,AISP            SPF INTERFACE ENTRY POINT
         BALR  R14,R15             LINK TO SPF
         LTR   R15,R15             TBADD SUCCESSFUL?
         BNZ   ERR0020             NO
         MVC   DATA,COMMBLKS       CLEAR VARIABLE 'DATA'
         BR    R9                  RETURN
* ------------------------------------------------------------------- *
*                                                                     *
*              SPF ERRORS                                             *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0010  DS    0H
         DITTRACE ID=CREFAIL,      TBCREATE FAILED                     +
               RDATA1=R15          .. CAPTURE RETURN CODE
         CVD   R15,COMMDWRD        CONVERT RETURN CODE TO DECIMAL
         MVC   MSG001RC,RCEDWD     INITIALIZE WITH EDIT WORD
         ED    MSG001RC,COMMDWRD+6 EDIT RETURN CODE
         LA    R1,MSG001           MESSAGE TEXT ADDRESS
         LA    R0,MSG001L          MESSAGE TEXT LENGTH
         TPUT  (R1),(R0)           SEND MESSAGE
         B     EXIT0000            AND EXIT
ERR0020  DS    0H
         DITTRACE ID=ADDFAIL,      TBCREATE FAILED                     +
               RDATA1=R15          .. CAPTURE RETURN CODE
         CVD   R15,COMMDWRD        CONVERT RETURN CODE TO DECIMAL
         MVC   MSG002RC,RCEDWD     INITIALIZE WITH EDIT WORD
         ED    MSG002RC,COMMDWRD+6 EDIT RETURN CODE
         LA    R1,MSG002           MESSAGE TEXT ADDRESS
         LA    R0,MSG002L          MESSAGE TEXT LENGTH
         B     ERR0090
ERR0030  DS    0H
         DITTRACE ID=TOPFAIL,      TBCREATE FAILED                     +
               RDATA1=R15          .. CAPTURE RETURN CODE
         CVD   R15,COMMDWRD        CONVERT RETURN CODE TO DECIMAL
         MVC   MSG003RC,RCEDWD     INITIALIZE WITH EDIT WORD
         ED    MSG003RC,COMMDWRD+6 EDIT RETURN CODE
         LA    R1,MSG003           MESSAGE TEXT ADDRESS
         LA    R0,MSG003L          MESSAGE TEXT LENGTH
         B     ERR0090
ERR0090  DS    0H
         TPUT  (R1),(R0)           SEND MESSAGE
         B     EXIT0000            AND EXIT
* ------------------------------------------------------------------- *
*                                                                     *
*              MODULE EXIT POINT                                      *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         DITTRACE ID=EXIT          TRACE EXIT
         L     R13,4(R13)          RESTORE REGISTER 13                  ASE01670
         LM    R14,R12,12(R13)     RESTORE ALL OTHER REGISTERS          ASE01680
         SR    R15,R15             GIVE GOOD RETURN CODE                ASE01690
         BR    R14                 RETURN TO CALLER                     ASE01700
         SPACE 2
* ------------------------------------------------------------------- *
*                                                                     *
*              WORKAREAS/CONSTANTS                                    *
*                                                                     *
* ------------------------------------------------------------------- *
REWCCW   CCW   X'07',*,SILI,1      REWIND CCW
READCCW  CCW   X'02',*,SILI,65535  READ FORWARD CCW
TMAPSAVE DC    18F'0'              REGISTER SAVE AREA
TBLEPARM DC    A(TBCREATE)         VERB ADDRESS
         DC    A(NTABLE)           TABLE NAME ADDRESS
         DC    A(0)                NO 'KEY' NAMES
         DC    A(NDATA)            VARIABLES LIST
         DC    A(NOWRITE)          IN CORE ONLY
         DC    A(REPLACE+X'80000000')
ADDPARM  DC    A(TBADD)            VERB ADDRESS
         DC    A(NTABLE+X'80000000')
TOPPARM  DC    A(TBTOP)            VERB ADDRESS
         DC    A(NTABLE+X'80000000')
DISPPARM DC    A(TBDISPL)          VERB ADDRESS
         DC    A(NTABLE)           TABLE NAME ADDRESS
DISPPANL DC    A(0)                PANEL NAME ADDRESS
H4       DC    H'4'                CONSTANT
H8       DC    H'8'                CONSTANT
H80      DC    H'80'               CONSTANT
TAPEFLAG DC    X'00'               SWITCHES/FLAGS
$TAPETM  EQU   X'80'               .. TAPE MARK JUST READ
$TAPEDTA EQU   X'40'               .. DATA FILE FOUND
$TAPEFHD EQU   X'20'               .. FILE HEADING PRINTED
$TAPEEOT EQU   X'10'               .. LOGICAL EOT REACHED
FILECT   DC    PL3'0'              FILES COUNTER
FULL     DC    CL8'FULL'           CONSTANT
FULLTAPE DC    CL8'FULLTAPE'       CONSTANT
ALL      DC    CL8'ALL'            CONSTANT
TBCREATE DC    CL8'TBCREATE'
TBADD    DC    CL8'TBADD'
TBTOP    DC    CL8'TBTOP'
TBDISPL  DC    CL8'TBDISPL'
NTABLE   DC    CL8'TMAPDATA'
NDATA    DC    C'(DATA)'
NOWRITE  DC    CL8'NOWRITE'
REPLACE  DC    CL8'REPLACE'
PANLNAME DC    CL8'DITTTSO2'
TAPEVOL1 DC    C'VOL1'             CONSTANT
TAPEHDR1 DC    C'HDR1'             CONSTANT
TAPEHDR2 DC    C'HDR2'             CONSTANT
TAPEEOF1 DC    C'EOF1'             CONSTANT
TAPEEOF2 DC    C'EOF2'             CONSTANT
TAPEEOV1 DC    C'EOV1'             CONSTANT
TAPEEOV2 DC    C'EOV2'             CONSTANT
MSG001   DC    C'TBCREATE FAILED, RETURN CODE='
MSG001RC DC    CL4' '
         DC    C' (DECIMAL)'
MSG001L  EQU   *-MSG001
MSG002   DC    C'TBADD FAILED, RETURN CODE='
MSG002RC DC    CL4' '
         DC    C' (DECIMAL)'
MSG002L  EQU   *-MSG002
MSG003   DC    C'TBTOP FAILED, RETURN CODE='
MSG003RC DC    CL4' '
         DC    C' (DECIMAL)'
MSG003L  EQU   *-MSG003
MSG004   DC    C'TBDISPL FAILED, RETURN CODE='
MSG004RC DC    CL4' '
         DC    C' (DECIMAL)'
MSG004L  EQU   *-MSG004
RCEDWD   DC    X'40202120'
HEADMSG  DS    0C
         DC    C'* * * * * * * * * * * * * * PHYSICAL FILE #'
PRFILE   DC    CL6' '
         DC    C' * * * * * * * * * * * * * * *'
HEADMSGL EQU   *-HEADMSG
LEOTMSG  DC    C'=============================== LOGICAL END OF TAPE ==+
               ====================='
PEOTMSG  DC    C'=============================== PHYSICAL END OF TAPE =+
               ====================='
DATAMSG  DC    C'DATA'
VOL1MSG  DS    0C
         DC    C'VOL1 VOLSER '
PRVOL    DC    CL6' '
VOL1MSGL EQU   *-VOL1MSG
HDR1MSG  DS    0C
         DC    C'HDR1 DSN='
PRDSN    DC    CL17' '
         DC    C' VOLSEQ='
PRVSEQ   DC    CL4' '
         DC    C' D/S SEQ='
PRDSEQ   DC    CL4' '
         DC    C' CREATE='
PRCDTE   DC    CL6' '
         DC    C' EXPIRE='
PREXPD   DC    CL6' '
HDR1MSGL EQU   *-HDR1MSG
HDR2MSG  DS    0C
         DC    C'HDR2 RECFM='
PRRF     DC    C' '
         DC    C' BLKSZ='
PRBLSZ   DC    CL5' '
         DC    C' LRECL='
PRRSZ    DC    CL5' '
         DC    C' DENSITY='
PRDEN    DC    C' '
         DC    C' JOB/JOBSTEP='
PRJOB    DC    CL17' '
HDR2MSGL EQU   *-HDR2MSG
EOF1MSG  DS    0C
         DC    C'EOF1 BLOCK COUNT='
PRBLKC   DC    CL6' '
EOF1MSGL EQU   *-EOF1MSG
EOF2MSG  DC    C'EOF2'
EOV1MSG  DC    C'EOV1'
EOV2MSG  DC    C'EOV2'
* ------------------------------------------------------------------- *
*                                                                     *
*              EXCP INTEFACE BLOCK                                    *
*                                                                     *
* ------------------------------------------------------------------- *
EXCPBLOK EXCPBLOK
         EJECT
* ------------------------------------------------------------------- *
*                                                                     *
*              TAPE LABEL DSECTS                                      *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY  DITTTLBL
         EJECT
* ------------------------------------------------------------------- *
*                                                                     *
*              REGISTER EQUATES                                       *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY REGEQU
SILI     EQU   X'20'
         ABCODES
         EJECT
* ------------------------------------------------------------------- *
*                                                                     *
*              COMMON MODULE DSECT                                    *
*                                                                     *
* ------------------------------------------------------------------- *
DITTCOMM DITTCOMM TYPE=DSECT
         EJECT
* ------------------------------------------------------------------- *
*                                                                     *
*              DYNAMIC BLOCK DSECT                                    *
*                                                                     *
* ------------------------------------------------------------------- *
DYNBLOK  DYNBLOK TYPE=DSECT
         EJECT
* ------------------------------------------------------------------- *
*                                                                     *
*              MVS IOB DSECT                                          *
*                                                                     *
* ------------------------------------------------------------------- *
         IEZIOB
IOBLEN   EQU  *-IOB
* ------------------------------------------------------------------- *
*                                                                     *
*              MVS DCB DSECT                                          *
*                                                                     *
* ------------------------------------------------------------------- *
         DCBD DSORG=PS
DYNDCBL  EQU  *-IHADCB
         END  DITTTSO2
