* ------------------------------------------------------------------- *
*                                                                     *
*              UNIT ALLOCATION                                        *
*                                                                     *
*    THIS MODULE INTERFACES WITH 'DITTDAIR' TO ALLOCATE OR RELEASE    *
*    DEVICES.  ALLOCATING A DEVICE AS A SEPARATE COMMAND PREVENTS     *
*    ANOTHER JOB OR TASK FROM ALLOCATING THE DEVICE BETWEEN DITTO     *
*    COMMANDS.                                                        *
*                                                                     *
*    WHEN INVOKED AS AN INPUT MODULE, THE REQUEST IS 'ALLOCATE'       *
*    (ACQ), WHEN INVOKED AS AN OUTPUT MODULE, THE REQUEST IS          *
*    'RELEASE' (REL).  THIS MODULE DOES NO I/O AND SIGNALS EOF AT     *
*    ITS EXIT.                                                        *
*                                                                     *
* ------------------------------------------------------------------- *
DITTALOC DITTPRFX ALOCSAVE,'ALLOCATE DEVICE'
         USING DITTCOMM,R11        SPECIFY BASE
         DITTRACE ID=ENTRY,        TRACE ENTRY                         +
               DATA1=COMMINU       .. TRACE CUU RECEIVED
         TM    COMMFLAG,$COMMCLS   'CLOSE' IN PROGRESS?
         BO    ALOC9900            YES.. NOTHING FOR US TO DO
ALOC0010 DS    0H
         MVI   DAIRTYPE,$DAIRCUU   LOCATE DYNBLOCK BY CUU
         MVC   DAIRCUU,COMMINU     MOVE UNIT ADDRESS
         C     R12,COMMINM         RUNNING AS INPUT MODULE?
         BE    ALOC0020            YES.. THEN FUNCTION IS 'ACQ'
         C     R12,COMMOUTM        RUNNING AS OUTPUT MODULE?
         BE    ALOC0030            YES.. THEN FUNCTION IS 'REL'
         ABEND ABEND013,DUMP,,USER ABEND WITH REASON
ALOC0020 DS    0H
         DITTRACE ID=ALOCACQ       TRACE CALLS TO DAIR MODULE
         MVI   DAIRCMMD,$DAIRACQ   ALLOCATION REQUEST
         B     ALOC0040
ALOC0030 DS    0H
         DITTRACE ID=ALOCREL       TRACE CALLS TO DAIR MODULE
         MVI   DAIRCMMD,$DAIRREL   RELEASE REQUEST
ALOC0040 DS    0H
         LA    R1,DAIRBLOK         DAIR REQUEST BLOCK
         L     R15,ADAIR           DAIR MODULE ADDRESS
         BALR  R14,R15             LINK TO DAIR MODULE
         CLI   DAIRSTAT,$DAIRSLE   DYNAMIC BLOCK NOT LOCATED?
         BE    ALOC0050            YES.. TEST FOR RELEASE FUNCTION
         CLI   DAIRSTAT,$DAIRSOK   DAIR SUCCESSFUL?
         BNE   ALOC0400            NO, EXAMINE FURTHER
         OI    COMMFLAG,$COMMEOF   SIGNAL EOF (STOP INPUT-OUTPUT)
         B     ALOC9900            EXIT
ALOC0050 DS    0H
         CLI   DAIRCMMD,$DAIRREL   WAS FUNCTION 'RELEASE'?
         BNE   ALOC0400            NO
         LA    R2,NOTALOCM         SET MESSAGE BLOCK ADDRESS
         B     ALOC0420            ISSUE MESSAGE(S)
* ------------------------------------------------------------------- *
*                                                                     *
*              ERROR ROUTINES                                         *
*                                                                     *
* ------------------------------------------------------------------- *
ALOC0400 DS    0H
         LA    R2,MSGTABLE         MESSAGE TABLE ADDRESS
         USING MSGDSECT,R2         DEFINE BASE
ALOC0410 DS    0H
         CLI   MSGSTAT,X'FF'       END OF TABLE?
         BE    ALOC0450            UNKNOWN STATUS FROM DITTDAIR
         CLC   MSGSTAT,DAIRSTAT    STATUS MESSAGE LOCATED?
         BE    ALOC0420            YES
         AH    R2,MSGLEN           NEXT MESSAGE
         B     ALOC0410            CONTINUE SEARCH
ALOC0420 DS    0H
         SR    R1,R1               CLEAR REGISTER
         ICM   R1,3,MSGWTO         LENGTH FROM WTO
         SH    R1,H5               MINUS WTO PREFIX LENGTH + 1
         CLI   COMMENV,$ENVTSO     RUNNING AS A TSO USER?
         BE    ALOC0440            YES
         EX    R1,PRTMSGM          MOVE MESSAGE TEXT
         MVI   PRTCC,C' '          CARRIAGE CONTROL
         MVI   PRTCMMD,$PRTCMD     MOVE COMMAND
         LA    R1,PRTBLOK          PRINT INTERFACE BLOCK
         L     R15,APRT            PRINT MODULE ADDRESS
         BALR  R14,R15             LINK TO PRINT MODULE
         CLI   COMMENV,$ENVSTC     RUNNING AS A STARTED TASK?
         BNE   ALOC0480            NO... CANCELLED ON ANY STATUS
         LA    R3,MSGWTO           WTO ADDRESS
         TM    MSGFLAG,$ABORT      ABORT ON THIS ERROR?
         BO    ALOC0470            YES
         ST    R3,PARMWTO          PASS ADDRESS TO PARAMETER MODULE
         MVI   PARMCMMD,$PARMEWTO  ISSUE PROVIDED WTO
ALOC0430 DS    0H
         MVC   PARMPARM,UNITID     'ID' FOR UNIT
         LA    R1,PARMBLOK         PARAMETER BLOCK
         L     R15,APARM           PARAMETER CONVERTER BLOCK
         BALR  R14,R15             LINK TO 'DITTPARM'
         TM    PARMFLAG,$PARMCAN   OPERATOR REQUEST 'CANCEL'?
         BO    ALOC0600            YES
         TM    PARMFLAG,$PARMOK    WAS A GOOD UNIT ENTERED?
         BO    ALOC0010            YES .. ATTEMPT TO REQUEST AGAIN
         B     ALOC0430            ASK FOR CUU AGAIN
ALOC0440 DS    0H
         EX    R1,TSOMSGM          MOVE MESSAGE TEXT TO 'MSG02'
         B     ALOC0480            AND EXIT
ALOC0450 DS    0H
         OI    COMMFLAG,$ABORT+$ABEND
         CLI   COMMENV,$ENVTSO     RUNNING IN TSO ENVIRONMENT?
         BE    ALOC0460            YES
         MVC   PRTDATA(ALOCM01L),ALOCM01 ERROR MESSAGE
         MVI   PRTCC,C' '          CARRIAGE CONTROL
         MVI   PRTCMMD,$PRTCMD     MOVE COMMAND
         LA    R1,PRTBLOK          PRINT INTERFACE BLOCK
         L     R15,APRT            PRINT MODULE ADDRESS
         BALR  R14,R15             LINK TO PRINT MODULE
         B     ALOC9900            AND EXIT
ALOC0460 DS    0H
         MVC   MSG01(ALOCM01L),ALOCM01
         B     ALOC9900            AND EXIT
ALOC0470 DS    0H
         WTO   MF=(E,(3)),         ISSUE WTO                           +
               CONSNAME=COMMCNAM   .. CONSOLE NAME
ALOC0480 DS    0H
         OI    COMMFLAG,$ABORT     ABORT THIS COMMAND
         B     ALOC9900            AND EXIT
ALOC0600 DS    0H
         OI    COMMFLAG,$ABORT     SET ABORT FLAG
         DITTRACE ID=CANCEL
         WTO   'ALLOCATE REQUEST CANCELLED',                           +
               CONSNAME=COMMCNAM   .. CONSOLE NAME
* ------------------------------------------------------------------- *
*                                                                     *
*              MODULE EXIT POINT                                      *
*                                                                     *
* ------------------------------------------------------------------- *
ALOC9900 DS    0H
         DITTRACE ID=EXIT          TRACE EXIT
         L     R13,4(R13)          RESTORE REGISTER 13                  ASE01670
         LM    R14,R12,12(R13)     RESTORE ALL OTHER REGISTERS          ASE01680
         SR    R15,R15             GIVE GOOD RETURN CODE                ASE01690
         BR    R14                 RETURN TO CALLER                     ASE01700
PRTMSGM  MVC   PRTDATA(0),MSGWTO+4 MOVE MESSAGE TEXT
TSOMSGM  MVC   MSG02(0),MSGWTO+4   MOVE MESSAGE TEXT
         SPACE 2
* ------------------------------------------------------------------- *
*                                                                     *
*              WORK AREAS/CONSTANTS                                   *
*                                                                     *
* ------------------------------------------------------------------- *
ALOCSAVE DC    18F'0'              REGISTER SAVE AREA
H5       DC    H'5'                CONSTANT
         PARMGEN GEN=NO            INVOKE PARMGEN TO DEFINE FIELD ID'S
UNITID   PARMID UNIT               GENERATE FIELD ID FOR 'UNIT'
ALOCM01  DC    C'UNKNOWN DITTDAIR STATUS RETURNED (INTERNAL ERROR)'
ALOCM01L EQU   *-ALOCM01
         EJECT
* ------------------------------------------------------------------- *
*                                                                     *
*              PRINT MODULE INTERFACE BLOCK                           *
*                                                                     *
* ------------------------------------------------------------------- *
PRTBLOK  PRTBLOK
         EJECT
* ------------------------------------------------------------------- *
*                                                                     *
*              DYNAMIC ALLOCATION/DE-ALLOCATION REQUEST BLOCK         *
*                                                                     *
* ------------------------------------------------------------------- *
DAIRBLOK DAIRBLOK
         EJECT
* ------------------------------------------------------------------- *
*                                                                     *
*              PARAMETER CONVERSION REQUEST BLOCK                     *
*                                                                     *
* ------------------------------------------------------------------- *
PARMBLOK PARMBLOK
         EJECT
* ------------------------------------------------------------------- *
*                                                                     *
*              ERROR MESSAGE/FLAGS                                    *
*                                                                     *
* ------------------------------------------------------------------- *
NOTALOCM STATMSG STAT=$DAIRSLE,                                        +
               FLAGS=,                                                 +
               MESSAGE='UNIT IS NOT ALLOCATED'
MSGTABLE DS    0F
         STATMSG STAT=$DAIRSDE,                                        +
               FLAGS=$ABORT,                                           +
               MESSAGE='DAIR ERROR (INTERAL ERROR)'
         STATMSG STAT=$DAIRSLE,                                        +
               FLAGS=,                                                 +
               MESSAGE='DYNAMIC BLOCK COULD NOT BE LOCATED'
         STATMSG STAT=$DAIRSIR,                                        +
               FLAGS=$ABORT,                                           +
               MESSAGE='INVALID DAIR REQUEST (INTERNAL ERROR)'
         STATMSG STAT=$DAIRSNU,                                        +
               FLAGS=,                                                 +
               MESSAGE='UNIT DOES NOT EXIST'
         STATMSG STAT=$DAIRSTE,                                        +
               FLAGS=,                                                 +
               MESSAGE='DEVICE IS NOT A SUPPORTED DEVICE TYPE'
         STATMSG STAT=$DAIRSRE,                                        +
               FLAGS=$ABORT,                                           +
               MESSAGE='INVALID RESOURCE TYPE (INTERNAL ERROR)'
         STATMSG STAT=$DAIRSDP,                                        +
               FLAGS=,                                                 +
               MESSAGE='DEVICE IS ALREADY ALLOCATED'
         STATMSG STAT=$DAIRSOF,                                        +
               FLAGS=,                                                 +
               MESSAGE='DEVICE IS OFFLINE'
         STATMSG STAT=$DAIRSAL,                                        +
               FLAGS=,                                                 +
               MESSAGE='DEVICE IS ALLOCATED TO ANOTHER USER'
         STATMSG STAT=$DAIRSUD,                                        +
               FLAGS=,                                                 +
               MESSAGE='3420 UNIT DOES NOT ALLOW 1600 OR 6250 BPI'
         STATMSG STAT=$DAIRS16,                                        +
               FLAGS=,                                                 +
               MESSAGE='3420 UNIT DOES NOT ALLOW 1600 BPI'
         STATMSG STAT=$DAIRS62,                                        +
               FLAGS=,                                                 +
               MESSAGE='3420 UNIT DOES NOT ALLOW 6250 BPI'
         STATMSG STAT=$DAIRSD1,                                        +
               FLAGS=$ABORT,                                           +
               MESSAGE='DENSITY MAY NOT BE SPECIFIED FOR 3480'
         STATMSG STAT=$DAIRSD2,                                        +
               FLAGS=$ABORT,                                           +
               MESSAGE='DENSITY MAY NOT BE SPECIFIED FOR DASD UNITS'
         DC    X'FFFF'
         EJECT
* ------------------------------------------------------------------- *
*                                                                     *
*              REGISTER EQUATES                                       *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY  REGEQU
         ABCODES
         EJECT
* ------------------------------------------------------------------- *
*                                                                     *
*              COMMON MODULE DSECT                                    *
*                                                                     *
* ------------------------------------------------------------------- *
DITTCOMM DITTCOMM TYPE=DSECT
         EJECT
* ------------------------------------------------------------------- *
*                                                                     *
*              MESSAGE DSECT                                          *
*                                                                     *
* ------------------------------------------------------------------- *
MSGDSECT DSECT
MSGLEN   DS   AL2              LENGTH OF THIS ENTRY
MSGSTAT  DS   X                CORRESPONDS TO 'DAIRSTAT' VALUE
MSGFLAG  DS   X                FLAGS
MSGWTO   DS   0F               WTO MESSAGE (IF RUNNING AS A STC)
         END  DITTALOC
