* ------------------------------------------------------------------- *
*                                                                     *
*       DASD VOLUME VTOC                                              *
*                                                                     *
*       THE PRINT LINES ARE BUILT AND PASSED TO 'DITTPRT' TO DO       *
*       THE ACTUAL PRINTING.                                          *
*                                                                     *
* ------------------------------------------------------------------- *
DITTVTOC DITTPRFX VTOCSAVE,'DASD VTOC'
         USING DITTCOMM,R11           ADDRESS IT
         DITTRACE ID=ENTRY            TRACE ENTRY
VTOC0010 DS    0H
         TM    COMMFLAG,$COMMEOF      EOF FLAG ON?
         BO    VTOC0150               YES
         CLI   COMMENV,$ENVTSO        TSO ENVIRONMENT?
         BNE   VTOC0015               NO
         LA    R1,TBCREATE_PARMS
         L     R15,AISP               ISPLINK ENTRY POINT
         BALR  R14,R15                CREATE TABLE
         MVC   DATA,COMMBLKS          INITIALIZE DATA
VTOC0015 DS    0H
         L     R10,COMMIND            DYNAMIC BLOCK ADDRESS
         USING DYNBLOK,R10
         USING IOB,DYNIOB
         ST    R10,EXCPDYN            PASS ADDRESS TO DITTEXCP
         L     R6,ACOMMIOI            INPUT I/O AREA ADDRESS
         MVC   IOBCC(5),VTOCPTR       SET VTOC ADDRESS
         BAL   R7,VTOC0100            READ
         MVC   IOBCC(5),23(R6)        COPY VTOC ADDRESS
         DITTRACE ID=VTOCADDR,                                         +
               DATA1=IOBCC
         BAL   R7,VTOC0100            READ
         USING FORMAT_4,R6
         MVC   VTOCXTNT,DS4VTOCE+2    SAVE VTOC EXTENTS
         DITTRACE ID=VTOCXTNT,                                         +
               DATA1=VTOCLOW,                                          +
               DATA2=VTOCHIGH
         MVC   PR_DSN,COMMBLKS
         MVC   PR_DSN(L'VTOC_DSN),VTOC_DSN
         MVC   PR_DSN+L'VTOC_DSN(6),DYNVOL
         LA    R2,VTOCLOW
         BAL   R8,VTOC0120            EDIT EXTENT
         MVC   PR_LOW,PR_EXTENT       COPY EDITED EXTENT
         LA    R2,VTOCHIGH
         BAL   R8,VTOC0120            EDIT EXTENT
         MVC   PR_HIGH,PR_EXTENT      COPY EDITED EXTENT
         MVC   PRTDATA(PR01_L),PR01
         BAL   R8,PRT0000             PRINT
         MVC   IOBCC(4),VTOCLOW       START AT LOW EXTENT
         MVI   IOBR,1                 FORCE RECORD TO 1
VTOC0020 DS    0H
         BAL   R7,VTOC0100            READ
         CLI   EXCPSTAT,$EXCPOK       I/O OK?
         BNE   VTOC0040               NO
         USING FORMAT_1,R6
         CLI   DS1FMTID,C'1'          FORMAT 1 RECORD?
         BNE   VTOC0060               NO
         MVC   PR_DSN,DS1DSNAM        COPY DATA SET NAME
         LA    R3,DS1EXT1             1ST EXTENT
         LA    R4,3                   NBR OF EXTENTS
         BAL   R7,VTOC0110
         OC    DS1PTRDS,DS1PTRDS      FORMAT-3 DISK ADDRESS?
         BZ    VTOC0060               NO
         MVC   SAVEFMT1,IOBCC         SAVE CURRENT FORMAT-1 ADDRESS
         MVC   IOBCC(5),DS1PTRDS      SET DISK ADDRESS
         BAL   R7,VTOC0100            READ THE FORMAT-3
         USING FORMAT_3,R6
         LA    R3,DS3EXT1             FIRST EXTENT
         LA    R4,4                   FOUR EXTENTS
         BAL   R7,VTOC0110            PROCESS THESE EXTENTS
         LA    R3,DS3ADEXT            LAST NINE EXTENTS
         LA    R4,9                   NINE EXTENTS
         BAL   R7,VTOC0110            PROCESS THESE EXTENTS
         MVC   IOBCC(5),SAVEFMT1      RESTORE FORMAT-1 ADDRESS
         B     VTOC0060
VTOC0030 DS    0H
         DITTRACE ID=WRAP
VTOC0040 DS    0H
         DITTRACE ID=VTOC0040,                                         +
               DATA1=IOBCC,                                            +
               DATA2=VTOCHIGH
         CLC   IOBCC(4),VTOCHIGH      AT END OF VTOC?
         BE    VTOC0070               YES
         SR    R1,R1
         ICM   R1,3,IOBHH             HEAD NUMBER
         LA    R1,1(,R1)              NEXT HEAD NBR
         CLM   R1,3,DYNHHEAD          AT HIGH HEAD NBR?
         BL    VTOC0050               NO
         ICM   R1,3,IOBCC             CYLINDER NBR
         LA    R1,1(,R1)              NEXT CYLINDER
         STCM  R1,3,IOBCC             UPDATE CYLINDER NBR
         DITTRACE ID=NEXT_CYL,                                         +
               RDATA1=R1
         XC    IOBHH,IOBHH            RESET HEAD
         MVI   IOBR,1                 RESET RECORD NBR
         B     VTOC0020
VTOC0050 DS    0H
         DITTRACE ID=NEXTHEAD,                                         +
               RDATA1=R1
         STCM  R1,3,IOBHH             UPDATE HEAD NBR
         MVI   IOBR,1                 RESET RECORD NBR
         B     VTOC0020
VTOC0060 DS    0H
         SR    R1,R1
         IC    R1,IOBR                RECORD NBR
         LA    R1,1(,R1)              NEXT RECORD
         STC   R1,IOBR                UPDATE RECORD NBR
         DITTRACE ID=NEXT_REC,                                         +
               RDATA1=R1
         B     VTOC0020
VTOC0070 DS    0H
         CLI   COMMENV,$ENVTSO        TSO ENVIRONMENT?
         BNE   VTOC0150               NO
         LA    R1,TBTOP_PARMS         TBTOP PARMS
         L     R15,AISP               ISPLINK ENTRY POINT
         BALR  R14,R15                ISSUE TBTOP
         LA    R1,TBDISP_PARMS        TBDISP PARMS
         L     R15,AISP               ISPLINK ENTRY POINT
         BALR  R14,R15                DISPLAY TABLE
         B     VTOC0150               EXIT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
VTOC0100 DS    0H
         DITTRACE ID=EXCP,                                             +
               DATA1=IOBCC
         SR    R1,R1
         IC    R1,IOBR
         BCTR  R1,0
         STC   R1,IOBR
         LA    R1,EXCPBLOK
         L     R15,AEXCP              DITTEXCP ENTRY POINT
         BALR  R14,R15                LINK TO DITTEXCP
         DITTRACE ID=EXCPSTAT,                                         +
               DATA1=EXCPSTAT
         SR    R1,R1
         IC    R1,IOBR
         LA    R1,1(,R1)
         STC   R1,IOBR
         CLC   0(5,R6),IOBCC          TRACK WRAP?
         BL    VTOC0030               YES.. GO TO NEXT HEAD
         DITTRACE ID=DATA,                                             +
               DATA1=0(R6),                                            +
               DATA2=52(R6)
         BR    R7
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
VTOC0110 DS    0H
         DITTRACE ID=EXTENT,                                           +
               RDATA1=R3,                                              +
               DATA2=2(R3)
         OC    2(8,R3),2(R3)          ALL ZERO?
         BZR   R7                     YES
         LA    R2,2(,R3)              EXTENT LOW
         BAL   R8,VTOC0120            FORMAT
         MVC   PR_LOW,PR_EXTENT       COPY EDITED EXTENT
         LA    R2,6(,R3)              EXTENT HIGH
         BAL   R8,VTOC0120            FORMAT
         MVC   PR_HIGH,PR_EXTENT      COPY EDITED EXTENT
         MVC   PRTDATA(PR01_L),PR01
         BAL   R8,PRT0000             PRINT
         MVC   PR_DSN,COMMBLKS        CLEAR THE DSN
         LA    R3,10(,R3)             NEXT EXTENT
         BCT   R4,VTOC0110            LOOP
         BR    R7
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
VTOC0120 DS    0H
         USING EXTENT,R2
         SR    R0,R0
         ICM   R0,3,EXTENT_C          CYLINDER NUMBER
         CVD   R0,COMMDWRD            CONVERT TO DECIMAL
         MVC   EDIT_AREA,EDIT_WORD    INITIALIZE
         ED    EDIT_AREA,COMMDWRD+5   EDIT CYLINDER
         MVC   PR_CYL,EDIT_AREA+2     COPY CYLINDER
         ICM   R0,3,EXTENT_H          HEAD NUMBER
         CVD   R0,COMMDWRD            CONVERT TO DECIMAL
         MVC   EDIT_AREA,EDIT_WORD    INITIALIZE
         ED    EDIT_AREA,COMMDWRD+5   EDIT HEAD NUMBER
         MVC   PR_HEAD,EDIT_AREA+4    COPY HEAD NUMBER
         BR    R8
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
VTOC0150 DS    0H
         DITTRACE ID=EOF
         OI    COMMFLAG,$COMMEOF      SET EOF
         B     VTOC9900               AND EXIT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
PRT0000  DS    0H
         CLI   COMMENV,$ENVTSO        TSO ENVIRONMENT?
         BE    PRT0010                YES
         MVI   PRTCMMD,$PRTDATA       INSERT PRINT COMMAND
         LA    R1,PRTBLOK             PRINT INTERFACE BLOCK
         L     R15,APRT               PRINT MODULE ENTRY POINT
         BALR  R14,R15                LINK TO PRINT MODULE
         BR    R8                     RETURN
PRT0010  DS    0H
         MVC   DATA(PR01_L),PR01      COPY DATA
         LA    R1,TBADD_PARMS         TBADD PARMS
         L     R15,AISP               ISPLINK ENTRY POINT
         BALR  R14,R15                ADD A ROW TO THE TABLE
         BR    R8
* ------------------------------------------------------------------- *
*                                                                     *
*              MODULE EXIT POINT                                      *
*                                                                     *
* ------------------------------------------------------------------- *
VTOC9900 DS    0H
         DITTRACE ID=EXIT             TRACE EXIT
         L     R13,4(R13)             RESTORE REGISTER 13               ASE01670
         LM    R14,R12,12(R13)        RESTORE ALL OTHER REGISTERS       ASE01680
         SR    R15,R15                GIVE GOOD RETURN CODE             ASE01690
         BR    R14                    RETURN TO CALLER                  ASE01700

* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0010  DS    0H
         DC    H'0'
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
VTOCSAVE DC    18F'0'              REGISTER SAVE AREA

TBCREATE_PARMS DS    0A
               DC    A(TBCREATE)         VERB ADDRESS
               DC    A(NTABLE)           TABLE NAME ADDRESS
               DC    A(0)                NO 'KEY' NAMES
               DC    A(NDATA)            VARIABLES LIST
               DC    A(NOWRITE)          IN CORE ONLY
               DC    A(REPLACE+X'80000000')

TBADD_PARMS    DS    0A
               DC    A(TBADD)            VERB ADDRESS
               DC    A(NTABLE+X'80000000')

TBTOP_PARMS    DS    0A
               DC    A(TBTOP)            VERB ADDRESS
               DC    A(NTABLE+X'80000000')

TBDISP_PARMS   DS    0A
               DC    A(TBDISPL)          VERB ADDRESS
               DC    A(NTABLE)
               DC    A(PANEL_NAME+X'80000000')

TBCREATE DC    CL8'TBCREATE'
TBADD    DC    CL8'TBADD'
TBTOP    DC    CL8'TBTOP'
TBDISPL  DC    CL8'TBDISPL'
NTABLE   DC    CL8'TSO1DATA'
NDATA    DC    CL8'(DATA)'
NOWRITE  DC    CL8'NOWRITE'
REPLACE  DC    CL8'REPLACE'
PANEL_NAME DC  CL8'DITTVTOC'

VTOCPTR  DC    X'0000000003'       VTOC POINTER (CYL 0, HEAD 0, REC 3)

VTOC_DSN DC    C'VTOC OF '

VTOCXTNT DS    0CL08
VTOCLOW  DS    CL4                 VTOC EXTENT LOW
VTOCHIGH DS    CL4                 VTOC EXTENT HIGH

SAVEFMT1 DS    XL5                 SAVED FORMAT 1 ADDRESS

PR01     DS    0C
         DC    C' '
PR_DSN   DC    CL44' '
         DC    CL02' '
PR_LOW   DC    CL07' '
         DC    CL02' '
PR_HIGH  DC    CL07' '
PR01_L   EQU   *-PR01

PR_EXTENT DS   0C
PR_CYL    DC   CL4' '
          DC   C'-'
PR_HEAD   DC   CL2' '

EDIT_AREA DC   XL6'F02020202020'

EDIT_WORD DC   XL6'F02020202020'

         LTORG

PRTBLOK  PRTBLOK
EXCPBLOK EXCPBLOK TYPE=CSECT

* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DITTCOMM DITTCOMM TYPE=DSECT

* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DYNBLOK  DYNBLOK  TYPE=DSECT

* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
FORMAT_1 DSECT
         DS    CL08              COUNT
         IECSDSL1 1
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
FORMAT_3 DSECT
         DS    CL08              COUNT
         IECSDSL1 3
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
FORMAT_4 DSECT
         DS    CL08              COUNT
         DS    CL44              VTOC FORMAT-4 "DSN"
         IECSDSL1 4

* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXTENT   DSECT
EXTENT_C DS    XL2
EXTENT_H DS    XL2

* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         IEZIOB
IOBLEN   EQU   *-IOB
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         DCBD  DSORG=PS
DYNDCBL  EQU   *-IHADCB
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY  REGEQU
         END   DITTVTOC
