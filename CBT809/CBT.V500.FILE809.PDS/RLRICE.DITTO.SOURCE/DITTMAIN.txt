* ------------------------------------------------------------------- *
*                                                                     *
*                   BATCH JOB AND STC MAINLINE                        *
*                                                                     *
*      IF DITTO IS RUNNING AS A BATCH JOB, MAIN WILL LINK TO THE      *
*      CARD INPUT COMMAND MODULE 'DITTCARD', AND COMMANDS WILL BE     *
*      READ FROM 'SYSIN'.  IF DITTO IS A STARTED TASK, MAIN WILL      *
*      LINK TO THE CONSOLE COMMAND INPUT MODULE 'DITTCONS', AND       *
*      COMMANDS WILL BE ENTERED INTERACTIVELY.                        *
*                                                                     *
*      THE COMMAND MODULE WILL THEN SIGNAL WHETHER OR NOT THE         *
*      COMMAND AND ALL REQUIRED PARAMETERS WERE ENTERED AND VALID,    *
*      OR NOT.  IF NOT AN ERROR MESSAGE IS PRINTED AND THE NEXT       *
*      COMMAND IS REQUESTED.  IF A GOOD COMMAND WAS ENTERED, THE      *
*      COMMAND MODULE WILL HAVE PLACED THE INPUT AND OUTPUT MODULE    *
*      ADDRESSES IN THE 'COMMON' MODULE 'DITTCOMM'.                   *
*                                                                     *
*      MAIN THEN ENTERS A INPUT-OUTPUT LOOP.  THE INPUT MODULE IS     *
*      INVOKED, THEN THE OUTPUT MODULE, BACK TO THE INPUT MODULE      *
*      AND SO ON UNTIL END-OF-FILE IS SIGNALLED.  THEN A 'CLOSE'      *
*      INDICATOR IS SET AND INPUT AND OUTPUT ARE INVOKED AGAIN TO     *
*      ALLOW THEM ANY 'CLEAN-UP' AND TO CLOSE THEIR FILES IF          *
*      NECESSARY.                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
DITTMAIN DITTPRFX MAINSAVE,'MAINLINE PROGRAM'
         USING DITTCOMM,R11        SPECIFY BASE
         USING DYNBLOK,R2          DEFINE DYNAMIC BLOCK BASE
         CLI   COMMENV,$ENVJOB     RUNNING AS A JOB?
         BE    INIT0010            YES... NO CIB TO WORRY ABOUT
         ICM   R1,15,COMMCCOM      COMMUNICATION PARM LIST PRESENT?
         BZ    ERR0010             NO...
         DITTRACE ID=CONSCOMM,     TRACE COMMUNICATION PARM LIST       +
               RDATA1=R1           .. CAPTURE COMM ADDRESS IN TRACE
         USING CCOM,R1             DEFINE BASE
         L     R1,COMCIBPT         CIB ADDRESS FROM COMM PARM LIST
         DITTRACE ID=CONSCIB,      TRACE CIB LOCATED                   +
               RDATA1=R1           .. CAPTURE CIB ADDRESS
         USING CIB,R1              DEFINE CIB BASE
         AH    R1,CIBXOFF          PLUS OFFSET TO CIBX
         USING CIBX,R1
         MVC   COMMCNAM,CIBXCNNM   SAVE CONSOLE NAME
         DITTRACE ID=CONSNAME,     TRACE CONSOLE NAME                  +
               DATA1=COMMCNAM      .. CAPTURE CONSOLE NAME
INIT0010 DS    0H
         MVI   PRTCMMD,$PRTHEAD    SET COMMAND
         LA    R1,PRTBLOK          POINT TO PRINTER PARAMETER BLOCK
         L     R15,APRT            ENTRY POINT OF PRINT MODULE
         BALR  R14,R15             LINK TO PRINT MODULE
MAIN0010 DS    0H
         DITTRACE ID=NEWCMMD       TRACE
         MVI   COMMPFLG,X'00'      CLEAR ANY OPTIONAL INDICATORS
         NI    COMMFLAG,255-$ABORT CLEAR ABORT FLAG IF SET
         NI    COMMFLAG,255-$COMMCER AND COMMAND ERROR FLAG IF SET
         CLI   COMMENV,$ENVJOB     RUNNING AS A JOB?
         BE    MAIN0020            YES
         CLI   COMMENV,$ENVSTC     RUNNING AS A STARTED TASK?
         BE    MAIN0030            YES
         WTO   'INTERNAL ERROR, ENVIRONMENT NOT DETERMINED'
         ABEND 2,DUMP,,USER
MAIN0020 DS    0H
         L     R15,ACARD           CARD-INPUT COMMAND MODULE
         BALR  R14,R15             ENTER CARD-INPUT MODULE
         B     MAIN0040            CHECK FOR 'EOJ'
MAIN0030 DS    0H
         L     R15,ACONS           CONSOLE COMMAND MODULE ENTRY POINT
         BALR  R14,R15             ENTER CONSOLE-INPUT
         B     MAIN0040            CHECK FOR 'EOJ'
MAIN0040 DS    0H
         TM    COMMFLAG,$COMMEOF   END OF FILE ENCOUNTERED?
         BO    MAIN9900            YES, EXIT
         TM    COMMFLAG,$COMMCER   INPUT COMMAND ERROR?
         BNO   MAIN0050            NO
         MVC   PRTDATA(MSG001L),MSG001
         BAL   R9,MAINPRT          PRINT MESSAGE
         B     MAIN0010            GO TO NEXT RECORD
* ------------------------------------------------------------------- *
*                                                                     *
*        LOCATE THE DYNAMIC BLOCKS FOR THE INPUT AND OUTPUT MODULES.  *
*        HAVING THIS CODE HERE RATHER THAN IN THE INPUT AND OUTPUT    *
*        MODULES ELEMINATES THE DUPLICATE CODE THAT WOULD HAVE TO BE  *
*        IN EACH MODULE.                                              *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0050 DS    0H
         CLC   COMMINM,AALOC       IS THE INPUT MODULE 'DITTALOC'?
         BE    MAIN0110            YES.. BYPASS
         OC    COMMINM,COMMINM     INPUT MODULE FOR THIS COMMAND?
         BZ    MAIN0080            NO
         OC    COMMINU,COMMINU     INPUT CUU PRESENT?
         BZ    MAIN0080            NO
         DITTRACE ID=ALOCIN
         MVI   DAIRCMMD,$DAIRLOC   LOCATE COMMAND
         MVI   DAIRTYPE,$DAIRCUU   LOCATE DYNBLOK BY CUU
         MVC   DAIRCUU,COMMINU     CUU TO LOCATE
         LA    R1,DAIRBLOK         DAIR MODULE PARAMETER BLOCK
         L     R15,ADAIR           DAIR MODULE ENTRY POINT
         BALR  R14,R15             LINK TO DAIR MODULE
         CLI   DAIRSTAT,$DAIRSOK   DAIR SUCCESSFUL?
         BE    MAIN0070            YES
         CLI   DAIRSTAT,$DAIRSLE   DYNBLOK NOT LOCATED?
         BE    MAIN0060            YES
         ABEND ABEND011,DUMP,,USER UNEXPECTED STATUS FROM DITTDAIR
MAIN0060 DS    0H
         DITTRACE ID=INNOTACQ
         MVC   PRTDATA(MSG007L),MSG007   MOVE MESSAGE
         BAL   R9,MAINPRT          PRINT MESSAGE
         CLI   COMMENV,$ENVSTC     RUNNING AS A STARTED TASK?
         BNE   MAIN0170            NO
         WTO   'INPUT UNIT NOT ALLOCATED, COMMAND ABORTED',            +
               CONSNAME=COMMCNAM   .. CONSOLE NAME
         B     MAIN0170            RE-INITIALIZE PARAMETERS
MAIN0070 DS    0H
         L     R1,COMMINM          INPUT MODULE ENTRY POINT
         USING MODPRFX,R1
         ICM   R2,15,DAIRAREA      DYNAMIC BLOCK ADDRESS
         CLC   PFXMODDS(4),DYNDDNAM     UNIT CONSISTENT?
         BNE   MAIN0280            NO..
         ST    R2,COMMIND          SAVE DYNAMIC BLOCK ADDRESS
MAIN0080 DS    0H
         OC    COMMOUTM,COMMOUTM   OUTPUT MODULE FOR THIS COMMAND?
         BZ    MAIN0110            NO
         OC    COMMOUTU,COMMOUTU   OUTPUT CUU PRESENT?
         BZ    MAIN0110            NO
         DITTRACE ID=ALOCOUT
         MVI   DAIRCMMD,$DAIRLOC   LOCATE COMMAND
         MVI   DAIRTYPE,$DAIRCUU   LOCATE DYNBLOK BY CUU
         MVC   DAIRCUU,COMMOUTU    CUU TO LOCATE
         LA    R1,DAIRBLOK         DAIR MODULE PARAMETER BLOCK
         L     R15,ADAIR           DAIR MODULE ENTRY POINT
         BALR  R14,R15             LINK TO DAIR MODULE
         CLI   DAIRSTAT,$DAIRSOK   DAIR SUCCESSFUL?
         BE    MAIN0100            YES
         CLI   DAIRSTAT,$DAIRSLE   DYNBLOK NOT LOCATED?
         BE    MAIN0090            YES
         ABEND ABEND011,DUMP,,USER UNEXPECTED STATUS FROM DITTDAIR
MAIN0090 DS    0H
         DITTRACE ID=OUTNTACQ
         MVC   PRTDATA(MSG008L),MSG008   MOVE MESSAGE
         BAL   R9,MAINPRT          PRINT MESSAGE
         CLI   COMMENV,$ENVSTC     RUNNING AS A STARTED TASK?
         BNE   MAIN0170            NO
         WTO   'OUTPUT UNIT NOT ALLOCATED, COMMAND ABORTED',           +
               CONSNAME=COMMCNAM   .. CONSOLE NAME
         B     MAIN0170            RE-INITIALIZE PARAMETERS
MAIN0100 DS    0H
         L     R1,COMMOUTM         OUTPUT MODULE ENTRY POINT
         ICM   R2,15,DAIRAREA      DYNAMIC BLOCK ADDRESS
         CLC   PFXMODDS(4),DYNDDNAM     UNIT CONSISTENT?
         BNE   MAIN0290            NO..
         ST    R2,COMMOUTD         SAVE OUTPUT DYNAMIC BLOCK ADDRESS
MAIN0110 DS    0H
         DITTRACE ID=FIRST,        TRACE ENTRY INTO I/O LOOP           +
               DATA1=COMMIND,      .. INPUT DYNAMIC BLOCK ADDRESS      +
               DATA2=COMMOUTD      .. OUTPUT DYNAMIC BLOCK ADDRESS
         OI    COMMFLAG,$COMM1ST   SIGNAL FIRST TIME
MAIN0120 DS    0H
         DITTRACE ID=CALLIN        TRACE
         ICM   R15,15,COMMINM      INPUT MODULE ADDRESS
         BZ    MAIN0130            ZERO .. DON'T ATTEMPT TO INVOKE IT
         BALR  R14,R15             INVOKE INPUT ROUTINE
         TM    COMMFLAG,$ABEND     ABEND SET BY INPUT?
         BO    MAIN0260            YES
         TM    COMMFLAG,$ABORT     ABORT SET BY INPUT?
         BO    MAIN0240            YES
* ------------------------------------------------------------------- *
*                                                                     *
*        NOTE THAT THE EOF FLAG MAY BE SET BY AN OUTPUT ROUTINE.      *
*        THIS IS THE CASE WHEN THE FUNCTION REQUIRES AN OUTPUT        *
*        ROUTINE ONLY SUCH AS 'REW' OR 'RUN'.                         *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0130 DS    0H
         TM    COMMFLAG,$COMMEOF   END OF FILE ON INPUT?
         BO    MAIN0150            YES ...
         DITTRACE ID=CALLOUT       TRACE
         ICM   R15,15,COMMOUTM     OUTPUT MODULE ADDRESS
         BZ    MAIN0140            ZERO .. DON'T ATTEMPT TO INVOKE IT
         BALR  R14,R15             INVOKE OUTPUT ROUTINE
         TM    COMMFLAG,$ABEND     ABEND SET BY OUTPUT?
         BO    MAIN0270            YES
         TM    COMMFLAG,$ABORT     ABORT SET BY OUTPUT?
         BO    MAIN0250            YES
         TM    COMMFLAG,$COMMEOF   EOF SET BY OUTPUT?
         BO    MAIN0150            YES
MAIN0140 DS    0H
         DITTRACE ID=FIRSTOFF      NO LONGER FIRST PASS
         NI    COMMFLAG,255-$COMM1ST TURN OFF FIRST TIME FLAG
         B     MAIN0120            LOOP 'TILL END OF FILE
MAIN0150 DS    0H
         DITTRACE ID=CLOSE         'CLOSE' IN PROGRESS
         NI    COMMFLAG,255-$COMM1ST TURN OFF FIRST TIME FLAG
         OI    COMMFLAG,$COMMCLS   INDICATE CLOSE FILES
         ICM   R15,15,COMMINM      INPUT MODULE ADDRESS
         BZ    MAIN0160            NO INPUT MODULE
         BALR  R14,R15             ALLOW INPUT TO DO ANY CLEAN-UP
MAIN0160 DS    0H
         ICM   R15,15,COMMOUTM     OUTPUT MODULE ADDRESS
         BZ    MAIN0170            NO OUTPUT MODULE
         BALR  R14,R15             ALLOW OUTPUT TO DO ANY CLEAN-UP
* ------------------------------------------------------------------- *
*                                                                     *
*           CURRENT COMMAND IS TERMINATING                            *
*                                                                     *
*    IF THE COMMAND IS BEING ABORTED, NO FURTHER MESSAGES ARE ISSUED. *
*    IF THE COMMAND HAS INPUT FROM TAPE OR DASD, AND AN 'END-OF-FILE' *
*    WAS REACHED (MAY HAVE BEEN END-OF-TAPE), ISSUE A MESSAGE TO      *
*    INDICATE THE REASON THE COMMAND STOPPED... THIS IS MAY HELP      *
*    ELIMINATE CONFUSION WHEN A NUMBER OF RECORDS WAS SPECIFIED ON    *
*    THE COMMAND, BUT EOF WAS REACHED BEFORE THE REQUESTED NUMBER OF  *
*    RECORDS WAS READ.                                                *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0170 DS    0H
         DITTRACE ID=TERMCMD
         TM    COMMFLAG,$ABORT     WAS ABORT FLAG SET?
         BO    MAIN0230            YES
         ICM   R2,15,COMMIND       INPUT DYNAMIC BLOCK PRESENT?
         BZ    MAIN0230            NO.. RE-INITIALIZE
         CLC   MAINTAPE,DYNDDNAM   WAS THIS A TAPE INPUT COMMAND?
         BE    MAIN0190            YES
         TM    COMMFLAG,$COMMFE    EOF FORCED TO TO EOF RECORD?
         BNO   MAIN0230            NO.. RE-INITIALIZE
         CLI   COMMENV,$ENVJOB     BATCH JOB?
         BE    MAIN0180            YES
         WTO   'EOF FOUND ON INPUT, COMMAND TERMINATED',               +
               CONSNAME=COMMCNAM
MAIN0180 DS    0H
         MVC   PRTDATA(MSG011L),MSG011
         BAL   R9,MAINPRT          PRINT MESSAGE
         B     MAIN0230            RE-INITIALIZE
MAIN0190 DS    0H
         TM    DYNSTAT,$DYNPEOT    PHYSICAL END OF TAPE REACHED?
         BO    MAIN0210            YES
         TM    COMMFLAG,$COMMFE    EOF FORCED BY TAPEMARK?
         BNO   MAIN0230            NO.. RE-INITIALIZE
         CLI   COMMENV,$ENVJOB     RUNNING AS A JOB?
         BE    MAIN0200            YES
         WTO   'TAPE MARK READ ON INPUT, COMMAND TERMINATED',          +
               CONSNAME=COMMCNAM
MAIN0200 DS    0H
         MVC   PRTDATA(MSG013L),MSG013
         BAL   R9,MAINPRT          PRINT MESSAGE
         B     MAIN0230            RE-INITIALIZE
MAIN0210 DS    0H
         CLI   COMMENV,$ENVJOB     BATCH JOB?
         BE    MAIN0220            YES
         WTO   'PHYSICAL END OF TAPE REACHED ON INPUT, COMMAND TERMINAT+
               ED',                                                    +
               CONSNAME=COMMCNAM
MAIN0220 DS    0H
         MVC   PRTDATA(MSG012L),MSG012
         BAL   R9,MAINPRT          PRINT MESSAGE
MAIN0230 DS    0H
         DITTRACE ID=REINIT
         NI    COMMFLAG,255-$COMMCLS TURN OFF CLOSE FLAG
         NI    COMMFLAG,255-$COMMEOF TURN OFF EOF FLAG
         NI    COMMFLAG,255-$COMMFE  TURN OFF FORCED EOF FLAG
         XC    COMMINM,COMMINM     CLEAR MODULE ADDRESS
         XC    COMMOUTM,COMMOUTM   CLEAR MODULE ADDRESS
         XC    COMMIND,COMMIND     CLEAR DYNAMIC BLOCK ADDRESS
         XC    COMMOUTD,COMMOUTD   CLEAR DYNAMIC BLOCK ADDRESS
         MVI   PARMCMMD,$PARMINT   INITIALIZE COMMAND
         LA    R1,PARMBLOK         PARAMETER CONVERTER PARAMETER BLOCK
         L     R15,APARM           PARAMETER CONVERTER ENTRY POINT
         BALR  R14,R15             LINK TO 'DITTPARM' TO RE-INITIALIZE
         B     MAIN0010            GET NEXT COMMAND
MAIN0240 DS    0H
         DITTRACE ID=ABORTIN       INPUT ABORTED CURRENT FUNCTION
         MVC   PRTDATA(MSG002L),MSG002   MOVE MESSAGE
         BAL   R9,MAINPRT          PRINT MESSAGE
         B     MAIN0170            RE-INITIALIZE PARAMETERS
MAIN0250 DS    0H
         DITTRACE ID=ABORTOUT      OUTPUT ABORTED CURRENT FUNCTION
         MVC   PRTDATA(MSG003L),MSG003   MOVE MESSAGE
         BAL   R9,MAINPRT          PRINT MESSAGE
         CLI   COMMENV,$ENVSTC     RUNNING AS A STARTED TASK?
         BNE   MAIN0010            NO
         WTO   'COMMAND ABORTED, CHECK SYSPRINT FOR EXPLANTION',       +
               CONSNAME=COMMCNAM   .. CONSOLE NAME
         B     MAIN0170            RE-INITIALIZE PARAMETERS
MAIN0260 DS    0H
         DITTRACE ID=ABENDIN       INPUT REQUESTED ABEND
         MVC   PRTDATA(MSG004L),MSG004   MOVE MESSAGE
         BAL   R9,MAINPRT          PRINT MESSAGE
         ABEND ABEND002,DUMP,,USER ABEND US
MAIN0270 DS    0H
         DITTRACE ID=ABENDOUT      OUTPUT REQUESTED ABEND
         MVC   PRTDATA(MSG005L),MSG005   MOVE MESSAGE
         BAL   R9,MAINPRT          PRINT MESSAGE
         ABEND ABEND003,DUMP,,USER ABEND US
MAIN0280 DS    0H
         DITTRACE ID=XINUNIT       INCONSISTENT UNIT TYPE AND FUNCTION
         MVC   PRTDATA(MSG009L),MSG009   MOVE MESSAGE
         BAL   R9,MAINPRT          PRINT MESSAGE
         CLI   COMMENV,$ENVSTC     RUNNING AS A STARTED TASK?
         BNE   MAIN0010            NO
         WTO   'INPUT UNIT TYPE INCONSISTENT WITH COMMAND, COMMAND ABOR+
               TED',                                                   +
               CONSNAME=COMMCNAM   .. CONSOLE NAME
         B     MAIN0170            RE-INITIALIZE PARAMETERS
MAIN0290 DS    0H
         DITTRACE ID=XOUTUNIT      INCONSISTENT UNIT TYPE AND FUNCTION
         MVC   PRTDATA(MSG010L),MSG010   MOVE MESSAGE
         BAL   R9,MAINPRT          PRINT MESSAGE
         CLI   COMMENV,$ENVSTC     RUNNING AS A STARTED TASK?
         BNE   MAIN0010            NO
         WTO   'OUTPUT UNIT TYPE INCONSISTENT WITH COMMAND, COMMAND ABO+
               RTED',                                                  +
               CONSNAME=COMMCNAM   .. CONSOLE NAME
         B     MAIN0170            RE-INITIALIZE PARAMETERS
ERR0010  DS    0H
         WTO   'COMM ADDRESS FROM EXTRACT IS ZERO, UNABLE TO DETERMINE +
               CONSOLE NAME'
         ABEND ABEND012,DUMP,,USER
MAINPRT  DS    0H
         MVI   PRTCMMD,$PRTCMD     SET COMMAND
         LA    R1,PRTBLOK          PRINT PARAMETER BLOCK
         L     R15,APRT            PRINT MODULE ENTRY POINT
         BALR  R14,R15             LINK TO PRINT MODULE
         BR    R9                  RETURN
MAIN9900 DS    0H
         DITTRACE ID=EXIT          TRACE
         L     R3,COMMDYHD         FIRST BLOCK ON CHAIN
MAIN9910 DS    0H
         LTR   R2,R3               ADDRESS PRESENT?
         BZ    MAIN9920            ALL HAVE BEEN RELEASED
         DITTRACE ID=RELDYN,       TRACE RELEASE                       +
               RDATA1=R2           .. SAVE DYNAMIC BLOCK ADDRESS
         L     R3,DYNNEXT          NEXT DYNAMIC BLOCK ADDRESS
         MVI   DAIRCMMD,$DAIRREL   SET COMMAND (RELEASE)
         MVI   DAIRTYPE,$DAIRCUU   SET TYPE OF LOCATE (BY CUU)
         MVC   DAIRCUU,DYNCUU      SET CUU
         LA    R1,DAIRBLOK         PARAMETER BLOCK ADDRESS
         L     R15,ADAIR           'DITTDAIR' ENTRY POINT
         BALR  R14,R15             RELEASE THIS RESOURCE
         CLI   DAIRSTAT,$DAIRSOK   SUCCESSFULLY RELEASED?
         BE    MAIN9910            YES
         MVC   MSG6DD,DYNDDNAM     COPY DD NAME
         UNPK  MSG615(3),DAIRR15+3(2)
         MVZ   MSG615,MAINHX0F     TURN OFF ZONES
         TR    MSG615,MAINHXCH     TRANSLATE TO PRINTABLE
         MVI   MSG615+2,C' '       RESTORE BLANK
         UNPK  MSG6ER(5),DAIRERR(3)
         MVZ   MSG6ER,MAINHX0F     TURN OFF ZONES
         TR    MSG6ER,MAINHXCH     TRANSLATE TO PRINTABLE
         MVI   MSG6ER+4,C' '       RESTORE BLANK
         UNPK  MSG6IN(5),DAIRINFO(3)
         MVZ   MSG6IN,MAINHX0F     TURN OFF ZONES
         TR    MSG6IN,MAINHXCH     TRANSLATE TO PRINTABLE
         MVI   MSG6IN+4,C' '       RESTORE BLANK
         MVC   PRTDATA(MSG006L),MSG006   MOVE MESSAGE
         MVI   PRTCC,C'0'          DOUBLE SPACE
         BAL   R9,MAINPRT          PRINT MESSAGE
         ABEND ABEND008,DUMP,,USER ABEND
MAIN9920 DS    0H
         DITTRACE ID=RELDONE       DYNAMIC RELEASES ARE COMPLETE
         NI    COMMFLAG,255-$COMMEOF      TURN EOF FLAG
         OI    COMMFLAG,$COMMEOJ   SIGNAL END OF JOB IN PROGRESS
         L     R15,ACDI1           CARD READ MODULE ENTRY POINT
         BALR  R14,R15             CLOSE SYSIN
         L     R15,ACDO1           CARD PUNCH MODULE ENTRY POINT
         BALR  R14,R15             CLSOE SYSPUNCH
         L     R15,APRT            PRINT MODULE ENTRY POINT
         BALR  R14,R15             CLOSE SYSPRINT
         TM    XTRFLAG,$XTROPEN    EXTERNAL TRACE FILE OPEN?
         BNO   MAIN9930            NO
         LA    R2,XTRDCB           EXTERNAL TRACE DCB
         CLOSE ((R2))              CLOSE EXTERNAL TRACE
MAIN9930 DS    0H
         L     R13,4(R13)          RESTORE REGISTER 13                  ASE01670
         LM    R14,R12,12(R13)     RESTORE ALL OTHER REGISTERS          ASE01680
         SR    R15,R15             GIVE GOOD RETURN CODE                ASE01690
         BR    R14                 RETURN TO OPERATING SYSTEM           ASE01700
         EJECT
* ------------------------------------------------------------------- *
*                                                                     *
*              PRINT MODULE INTERFACE                                 *
*                                                                     *
* ------------------------------------------------------------------- *
PRTBLOK  PRTBLOK
         SPACE 2
* ------------------------------------------------------------------- *
*                                                                     *
*              PARAMETER CONVERTER INTERFACE                          *
*                                                                     *
* ------------------------------------------------------------------- *
PARMBLOK PARMBLOK
         SPACE 2
* ------------------------------------------------------------------- *
*                                                                     *
*              DYNAMIC ALLOCATION INTERFACE                           *
*                                                                     *
* ------------------------------------------------------------------- *
DAIRBLOK DAIRBLOK TYPE=CSECT
         SPACE 2
* ------------------------------------------------------------------- *
*                                                                     *
*              CONSTANTS                                              *
*                                                                     *
* ------------------------------------------------------------------- *
MAINHX0F DC    8X'0F'
MAINHXCH DC    C'0123456789ABCDEF'
MAINTAPE DC    C'TAPE'
MSG001   DC    C'COMMAND/PARM ERROR, CONTINUING WITH NEXT COMMAND'
MSG001L  EQU   *-MSG001
MSG002   DC    C'COMMAND ABORTED BY INPUT PROCESSING, CONTINUING WITH N+
               EXT COMMAND'
MSG002L  EQU   *-MSG002
MSG003   DC    C'COMMAND ABORTED BY OUTPUT PROCESSING, CONTINUING WITH +
               NEXT COMMAND'
MSG003L  EQU   *-MSG003
MSG004   DC    C'EXECUTION ABORTED BY INPUT PROCESSING'
MSG004L  EQU   *-MSG004
MSG005   DC    C'EXECUTION ABORTED BY OUTPUT PROCESSING'
MSG005L  EQU   *-MSG005
MSG006   DC    CL21'DYNAMIC RELEASE FAILED, DDNAME='
MSG6DD   DC    CL08' '
         DC    CL06', R15='
MSG615   DC    CL02' '
         DC    CL09' DAIRERR='
MSG6ER   DC    CL04' '
         DC    CL10' DAIRINFO='
MSG6IN   DC    CL04' '
MSG006L  EQU   *-MSG006
MSG007   DC    C'INPUT UNIT NOT ALLOCATED, COMMAND WILL BE ABORTED'
MSG007L  EQU   *-MSG007
MSG008   DC    C'OUTPUT UNIT NOT ALLOCATED, COMMAND WILL BE ABORTED'
MSG008L  EQU   *-MSG008
MSG009   DC    C'INPUT UNIT INCONSISTENT WITH COMMAND, COMMAND ABORTED'
MSG009L  EQU   *-MSG009
MSG010   DC    C'OUTPUT UNIT INCONSISTENT WITH COMMAND, COMMAND ABORTED+
               '
MSG010L  EQU   *-MSG010
MSG011   DC    C'EOF FOUND ON INPUT, COMMAND TERMINATED'
MSG011L  EQU   *-MSG011
MSG012   DC    C'PHYSICAL END OF TAPE REACHED ON INPUT, COMMAND TERMINA+
               TED'
MSG012L  EQU   *-MSG012
MSG013   DC    C'TAPE MARK READ ON INPUT, COMMAND TERMINATED'
MSG013L  EQU   *-MSG013
         SPACE 2
* ------------------------------------------------------------------- *
*                                                                     *
*              WORK AREAS                                             *
*                                                                     *
* ------------------------------------------------------------------- *
MAINSAVE DC    18F'0'              REGISTER SAVE AREA
         SPACE 2
* ------------------------------------------------------------------- *
*                                                                     *
*              REGISTER EQUATES                                       *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY REGEQU
* ------------------------------------------------------------------- *
*                                                                     *
*              COMMON MODULE DSECT                                    *
*                                                                     *
* ------------------------------------------------------------------- *
DITTCOMM DITTCOMM TYPE=DSECT
         ABCODES
* ------------------------------------------------------------------- *
*                                                                     *
*              DYNAMIC RESOURCE CONTROL BLOCK                         *
*                                                                     *
* ------------------------------------------------------------------- *
DYNBLOK  DYNBLOK TYPE=SHORT
* ------------------------------------------------------------------- *
*                                                                     *
*              COMMUNICATION PARAMETER LIST                           *
*                                                                     *
* ------------------------------------------------------------------- *
CCOM     DSECT
         IEZCOM
         SPACE 2
* ------------------------------------------------------------------- *
*                                                                     *
*              CONSOLE INPUT BUFFER                                   *
*                                                                     *
* ------------------------------------------------------------------- *
CIB      IEZCIB
         END
