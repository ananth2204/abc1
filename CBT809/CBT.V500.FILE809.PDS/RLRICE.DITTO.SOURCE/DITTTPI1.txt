* ------------------------------------------------------------------- *
*                                                                     *
*              TAPE INPUT MODULE                                      *
*                                                                     *
*       THIS IS THE TAPE INPUT MODULE.  IT IS INVOKED BY THE          *
*       MAINLINE MODULE 'DITTMAIN'.  FOR BATCH AND STC MODES          *
*       RECORD DEBLOCKING WILL BE PERFORMED BY THIS MODULE IF         *
*       REQUESTED BY THE COMMAND (TPD, TDD,..).                       *
*                                                                     *
* ------------------------------------------------------------------- *
DITTTPI1 DITTPRFX TAPESAVE,'TAPE READER MODULE'
         USING DITTCOMM,R11        SPECIFY BASE
         DITTRACE ID=ENTRY         TRACE ENTRY
         TM    COMMFLAG,$COMMCLS   CLOSE FILE INDICATOR??
         BO    TAPE9900            YES
         TM    COMMFLAG,$COMM1ST   FIRST PASS??
         BNO   TAPE0010            NO
         ZAP   TAPERCDS,COMMP0     ZERO RECORD COUNTER
         ZAP   FILECT,COMMP0       ZERO FILES COUNTER
         XC    TAPECRCD,TAPECRCD   CLEAR CURRENT RECORD ADDRESS
         XC    TAPEREM,TAPEREM     CLEAR REMAINING BLOCK AMOUNT
TAPE0010 DS    0H
         L     R10,COMMIND         DYNAMIC BLOCK ADDRESS
         ST    R10,EXCPDYN         PASS DYNAMIC BLOCK ADRESS TO EXCP
         USING DYNBLOK,R10         SPECIFY BASE
         LA    R2,DYNDCB           DCB WITHIN DYNBLOK
         USING IHADCB,R2           DEFINE DSECT BASE
         DITTRACE ID=CCWINIT       TRACE INTIALIZE ROUTINE
         L     R1,ACOMMIOI         INPUT BUFFER
         STCM  R1,7,READCCW+1      RELOCATE BUFFER ADDRESS
         NI    COMMIFLG,255-$NEWBLOK TURN OFF NEW BLOCK INDICATOR
TAPE0020 DS    0H
         TM    COMMPFLG,$COMMRCS   RECORD COUNT GIVEN??
         BNO   TAPE0030            NO, BYPASS RECORD COUNT CHECK
         CP    TAPERCDS,COMMRCDS   READ ENOUGH RECORDS??
         BE    TAPE7030            YES, SIGNAL END OF FILE
TAPE0030 DS    0H
         TM    COMMIFLG,$DEBLOCK   DEBLOCKING RECORDS??
         BNO   TAPE0100            NO
         DITTRACE ID=NEXTRECD,     CALLED FOR NEXT RECORD              +
               DATA1=TAPECRCD,     .. CURRENT RECORD ADDRESS           +
               DATA2=TAPEREM       .. LENGTH REMAINING
         OC    TAPEREM,TAPEREM     ANY LENGTH REMAINING??
         BZ    TAPE0100            NO.. NEED ANOTHER RECORD
         DITTRACE ID=DEBLOCK       DEBLOCK NEXT RECORD
TAPE0040 DS    0H
         ICM   R3,15,TAPECRCD      CURRENT RECORD ADDRESS
         BZ    TAPE0410            ERROR
         SR    R2,R2               CLEAR REGISTER
         TM    COMMIFLG,$VREC      VARIABLE LENGTH RECORDS??
         BO    TAPE0050            YES
         ICM   R2,3,COMMLRCL       LOGICAL RECORD SIZE
         B     TAPE0060            CONTINUE
TAPE0050 DS    0H
         ICM   R2,3,0(R3)          RECORD SIZE
TAPE0060 DS    0H
         TM    COMMIFLG,$NEWBLOK   NEW BLOCK JUST READ??
         BO    TAPE0070            YES, LEAVE CURRENT ADDRESS
         AR    R3,R2               BRING INPUT ADDRESS FORWARD
TAPE0070 DS    0H
         TM    COMMIFLG,$VREC      VARIABLE LENGTH RECORDS??
         BNO   TAPE0080            NO, RECORD LENGTH WILL BE THE SAME
         ICM   R2,3,0(R3)          LENGTH OF NEW RECORD
         BZ    TAPE0410         ...RECORD LENGTH ZERO.. INVALID
TAPE0080 DS    0H
         CLM   R2,3,TAPEREM        LONGER THAN REMAINING LENGTH??
         BNH   TAPE0090            NO
         ICM   R2,3,TAPEREM        OTHERWISE LIMIT LENGTH
TAPE0090 DS    0H
         ST    R3,TAPECRCD         SAVE CURRENT RECORD ADDRESS
         ST    R3,COMMCRCD         PASS CURRENT RECORD ADDRESS
         STH   R2,COMMCRCL         PASS CURRENT RECORD LENGTH
         DITTRACE ID=LREC,         RECORD ADDRESS AND LENGTH           +
               RDATA1=R3,          .. RECORD'S ADDRESS                 +
               RDATA2=R2           .. RECORD'S LENGTH
         LH    R3,TAPEREM          REMAINING SIZE
         SR    R3,R2               COMPUTE REMAINING SIZE
         STH   R3,TAPEREM          SAVE REMAINING SIZE
         AP    TAPERCDS,COMMP1     ADD 1 TO RECORD COUNT
         B     TAPE9900            EXIT
TAPE0100 DS    0H
         DITTRACE ID=NEXTBLOK      READ NEXT PHYSICAL RECORD
         MVC   DYNCCW,READCCW      INITIALIZE THE CCW
         LA    R1,EXCPBLOK         EXCP INTERFACE PARAMETER LIST
         L     R15,AEXCP           EXCP MODULE ENTRY POINT
         BALR  R14,R15             LINK TO EXCP INTERFACE
         CLI   EXCPSTAT,$EXCPNA    I/O NOT ALLOWED?
         BE    TAPE0140            NOT ALLOWED
         TM    DYNSTAT,$DYNPEOT    PHYSICAL END OF TAPE?
         BO    TAPE7020            YES
         CLI   DYNSTAT,$DYNTM      TAPE MARK READ?
         BE    TAPE7000            YES
         OI    COMMIFLG,$NEWBLOK   TURN ON NEW BLOCK INDICATOR
         SR    R3,R3               CLEAR REGISTER
         ICM   R3,7,READCCW+1      I/O AREA ADDRESS
         ST    R3,TAPECRCD         SAVE CURRENT RECORD ADDRESS
         MVC   TAPEREM,EXCPLEN     SAVE DATA LENGTH
         TM    COMMIFLG,$DEBLOCK   DEBLOCKING RECORDS??
         BNO   TAPE0130            NO
         TM    COMMIFLG,$VREC      VARIABLE LENGTH RECORDS?
         BNO   TAPE0040            NO
         SR    R0,R0               CLEAR REGISTER
         ICM   R0,3,0(R3)          LENGTH FROM BDW (MAYBE)
         SH    R0,H4               MINUS BDW LENGTH
         BNH   TAPE0040            INSUFFICIENT LENGTH
         LA    R3,4(R3)            FIRST RDW IN THE BLOCK
         SR    R2,R2               CLEAR REGISTER
TAPE0110 DS    0H
         ICM   R2,3,0(R3)          RECORD LENGTH FROM RDW
         BZ    TAPE0410            RECORD WITH LENGTH = ZERO!
         SR    R0,R2               MINUS LENGTH OF THIS RECORD
         BM    TAPE0040            NEGATIVE... LENGTHS DON'T ADD UP
         BZ    TAPE0120            ZERO.. LENGTHS MATCH
         AR    R3,R2               NEXT RDW ADDRESS
         B     TAPE0110            LOOP
TAPE0120 DS    0H
         DITTRACE ID=ITADDSUP      COULDN'T THINK OF A BETTER COMMENT
         L     R3,TAPECRCD         BLOCK ADDRESS
         ICM   R2,3,0(R3)          BLOCK LENGTH
         SH    R2,H4               MINUS BDW LENGTH
         STCM  R2,3,TAPEREM        SET LENGTH REMAINING
         LA    R3,4(R3)            FIRST LOGICAL RECORD'S ADDRESS
         ST    R3,TAPECRCD         SET RECORD ADDRESS
         B     TAPE0040            DEBLOCK FIRST RECORD
TAPE0130 DS    0H
         DITTRACE ID=PHYRECD,      PHYSICAL RECORD                     +
               DATA1=TAPECRCD,     .. PHYSICAL BLOCK'S ADDRESS         +
               DATA2=TAPEREM       .. PHYSICAL BLOCK'S LENGTH
         MVC   COMMCRCD,TAPECRCD   PASS RECORD ADDRESS
         MVC   COMMCRCL,TAPEREM    PASS RECORD LENGTH
         AP    TAPERCDS,COMMP1     ADD 1 TO RECORD COUNT
         B     TAPE9900            EXIT
TAPE0140 DS    0H
         DITTRACE ID=NOTALLOW
         CLI   COMMENV,$ENVTSO     RUNNING AS A TSO USER?
         BE    TAPE0160            YES
         CLI   COMMENV,$ENVJOB     RUNNING AS A BATCH JOB?
         BE    TAPE0150            YES
         WTO   'READ NOT ALLOWED WHILE TAPE IS AT PHYSICAL EOT',       +
               CONSNAME=COMMCNAM
TAPE0150 DS    0H
         MVI   PRTCMMD,$PRTCMD     SET PRINT COMMAND
         MVC   PRTDATA(NAMSGL),NAMSG
         LA    R1,PRTBLOK          PRINT INTERFACE BLOCK
         L     R15,APRT            PRINT MODULE ENTRY POINT
         BALR  R14,R15             PRINT MESSAGE
         OI    COMMFLAG,$ABORT     SET ABORT FLAG
         B     TAPE9900            AND EXIT
TAPE0160 DS    0H
         MVC   MSG01(NAMSGL),NAMSG SET MESSAGE
         OI    COMMFLAG,$ABORT     SET ABORT FLAG
         B     TAPE9900            AND EXIT
* ------------------------------------------------------------------- *
*                                                                     *
*                    ERROR ROUTINES                                   *
*                                                                     *
* ------------------------------------------------------------------- *
TAPE0400 DS    0H
         LA    R3,DYNIOB           IOB USED FOR I/O
         DITTRACE ID=READERR       TRACE READ ERRORS
         ABEND ABEND017,DUMP,,USER
TAPE0410 DS    0H
         DITTRACE ID=ZERORECL      TRACE LRECL = ZERO
         ABEND ABEND018,DUMP,,USER
         EJECT
* ------------------------------------------------------------------- *
*                                                                     *
*                    TAPE MARK PROCESSING                             *
*                                                                     *
* NOTE: THE TAPE MARK FLAG IN 'DITTCOMM' WILL BE SET BY 'DITTEXCP'.   *
*                                                                     *
* ------------------------------------------------------------------- *
TAPE7000 DS    0H
         DITTRACE ID=TAPEMARK      TRACE TAPE MARKS
         AP    FILECT,COMMP1       ADD 1 TO FILE COUNT
         TM    COMMPFLG,$COMMFLS   MULTIPLE FILES??
         BNO   TAPE7010            NO
         CP    FILECT,COMMFILS     ENOUGH FILES PROCESSED??
         BL    TAPE9900            NO
TAPE7010 DS    0H
         TM    COMMPFLG,$COMMRCS   'RECORDS' IN EFFECT?
         BNO   TAPE7030            NO, NORMAL EOF
         DITTRACE ID=FORCEOF       EOF FORCED BY TAPE MARK
         OI    COMMFLAG,$COMMFE    EOF FORCED BY TAPE MARK
         B     TAPE7030            OTHERWISE SIGNAL 'EOF'
TAPE7020 DS    0H
         DITTRACE ID=PEOT
TAPE7030 DS    0H
         DITTRACE ID=EOF           TRACE SYSIN END OF FILE
         OI    COMMFLAG,$COMMEOF   SIGNAL END OF FILE
* ------------------------------------------------------------------- *
*                                                                     *
*              MODULE EXIT POINT                                      *
*                                                                     *
* ------------------------------------------------------------------- *
TAPE9900 DS    0H
         DITTRACE ID=EXIT          TRACE EXIT
         L     R13,4(R13)          RESTORE REGISTER 13                  ASE01670
         LM    R14,R12,12(R13)     RESTORE ALL OTHER REGISTERS          ASE01680
         SR    R15,R15             GIVE GOOD RETURN CODE                ASE01690
         BR    R14                 RETURN TO CALLER                     ASE01700
         SPACE 2
* ------------------------------------------------------------------- *
*                                                                     *
*              WORK AREAS/CONSTANTS                                   *
*                                                                     *
* ------------------------------------------------------------------- *
TAPESAVE DC    18F'0'              REGISTER SAVE AREA
TAPECRCD DC    A(0)                CURRENT RECORD ADDRESS
H4       DC    H'4'                CONSTANT
TAPEREM  DC    H'0'                LENGTH REMAINING IN RECORD
TAPERCDS DC    PL5'0'              RECORD COUNTER
FILECT   DC    PL5'0'              FILES COUNTER
READCCW  CCW   X'02',*,SILI,65535  READ FORWARD CCW
NAMSG    DC    C'READ NOT ALLOWED WHILE TAPE IS AT PHYSICAL EOT'
NAMSGL   EQU   *-NAMSG
* ------------------------------------------------------------------- *
*                                                                     *
*              PRINT REQUEST BLOCK                                    *
*                                                                     *
* ------------------------------------------------------------------- *
PRTBLOK  PRTBLOK
* ------------------------------------------------------------------- *
*                                                                     *
*              EXCP INTERFACE BLOCK                                   *
*                                                                     *
* ------------------------------------------------------------------- *
EXCPBLOK EXCPBLOK
         EJECT
* ------------------------------------------------------------------- *
*                                                                     *
*              REGISTER EQUATES                                       *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY  REGEQU
SILI     EQU   X'20'
         ABCODES
         EJECT
* ------------------------------------------------------------------- *
*                                                                     *
*              COMMON MODULE DSECT                                    *
*                                                                     *
* ------------------------------------------------------------------- *
DITTCOMM DITTCOMM TYPE=DSECT
         EJECT
* ------------------------------------------------------------------- *
*                                                                     *
*              DYNAMIC BLOCK DSECT                                    *
*                                                                     *
* ------------------------------------------------------------------- *
DYNBLOK  DYNBLOK TYPE=DSECT
         EJECT
* ------------------------------------------------------------------- *
*                                                                     *
*              MVS IOB DSECT                                          *
*                                                                     *
* ------------------------------------------------------------------- *
         IEZIOB
IOBLEN   EQU  *-IOB
* ------------------------------------------------------------------- *
*                                                                     *
*              MVS DCB DSECT                                          *
*                                                                     *
* ------------------------------------------------------------------- *
         DCBD DSORG=PS
DYNDCBL  EQU   *-IHADCB
         END  DITTTPI1
