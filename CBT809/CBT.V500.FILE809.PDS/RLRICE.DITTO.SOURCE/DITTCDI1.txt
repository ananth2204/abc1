* ------------------------------------------------------------------- *
*                                                                     *
*              CARD INPUT MODULE                                      *
*                                                                     *
*      THIS IS THE CARD-READER MODULE.  IT IS INVOKED BE EITHER       *
*      THE MAINLINE MODULE OR THE CARD-INPUT COMMAND MODULE.  BY      *
*      USING ONE MODULE TO READ THE SYSIN DATA WHETHER IT IS FOR      *
*      DATA OR COMMANDS, IT SIMPLIFIES THE LOGIC OF THE OTHER         *
*      MODULES.  THE CARD-INPUT COMMAND MODULE IS NOT COMPLICATED     *
*      WITH LOGIC TO DETERMINE IF IT IS TO FIND A COMMAND OR DATA     *
*      AND MAINLINE IS NOT COMPLICATED BY LOGIC TO SIGNAL A CARD      *
*      READER MODULE TO LOOK FOR COMMANDS OR FOR DATA.  THE CARD-     *
*      INPUT COMMAND MODULE SIGNALS THIS MODULE TO SEARCH FOR A       *
*      COMMAND BY LOADING A NON-ZERO VALUE IN REGISTER 1 BEFORE       *
*      LINKING TO THIS MODULE.  END-OF-FILE IS SIGNALED ON            *
*      REACHING END-OF-DATA,  WHEN THE NUMBER OF RECORDS TO PROCESS   *
*      IS REACHED, OR WHEN A NEW COMMAND IS ENCOUNTERD IN THE         *
*      INPUT STREAM.  THIS MODULE ASSUMES A RECORD LENGTH OF 80       *
*      FOR SYSIN.  COMMANDS ARE IDENTIFIED BY THE CHARACTERS          *
*      '$$DITTO' IN COLUMNS 1-7.   COLUMNS 73-80 ARE IGNORED ON       *
*      COMMANDS.                                                      *
*                                                                     *
* ------------------------------------------------------------------- *
DITTCDI1 DITTPRFX CARDSAVE,'CARD READER MODULE'
         USING DITTCOMM,R11        SPECIFY BASE
         DITTRACE ID=ENTRY         TRACE ENTRY
         TM    COMMFLAG,$COMMCLS   'CLOSE FILES' CALL?
         BO    CARD9900            YES... EXIT
         TM    COMMFLAG,$COMMEOJ   END OF JOB IN PROGRESS?
         BNO   CARD0010            NO
         TM    CARDFLAG,$CARDOPN   IS THE FILE OPEN?
         BNO   CARD9900            NO, EXIT
         DITTRACE ID=CLOSE         TRACE SYSIN CLOSE
         CLOSE SYSIN               CLOSE INPUT FILE
         NI    CARDFLAG,255-$CARDOPN FILE NOT OPEN
         B     CARD9900            EXIT
CARD0010 DS    0H
         TM    CARDFLAG,$CARDOPN   IS FILE OPEN
         BO    CARD0020            YES, BYPASS OPEN
         DITTRACE ID=OPEN          TRACE SYSIN OPEN
         OPEN  (SYSIN,INPUT)       OPEN SYSIN
         OI    CARDFLAG,$CARDOPN   FLAG OPEN
CARD0020 DS    0H
         TM    CARDFLAG,$CARDEOF   REAL EOF FOUND?
         BO    CARD8010            YES
         MVC   COMMCRCD,ACOMMIOI   INITIALIZE CURRENT ADDRESS
         XC    COMMCRCL,COMMCRCL   ZERO DATA LENGTH
         TM    COMMFLAG,$COMM1ST   FIRST TIME THROUGH?
         BNO   CARD0030            NO
         ZAP   CARDRCDS,COMMP0     INITIALIZE RECORD COUNTER
CARD0030 DS    0H
         NI    COMMFLAG,255-$COMMEOF TURN OFF EOF FLAG
         TM    CARDFLAG,$CARDNP    LAST CARD NOT PROCESSED?
         BO    CARD0040            NO
         DITTRACE ID=READ          TRACE SYSIN READ
         GET   SYSIN,CARDRECD      READ A CONTROL RECORD
         CLC   DITTABND,CARDRECD   ABEND REQUEST?
         BNE   CARD0040            NO
         DITTRACE ID=ABEND         TRACE JUST BEFORE ABENDING
         ABEND ABEND999,DUMP,,USER BLOW THIS THING
CARD0040 DS    0H
         NI    CARDFLAG,255-$CARDNP TURN OFF NOT PROCESSED FLAG
         C     R12,COMMINM         RUNNING AS A COMMAND?
         BE    CARD0100            YES.. READ DATA
         MVC   CARDM03D,CARDRECD   COPY CONTROL RECORD
         MVC   PRTDATA(CARDM03L),CARDM03
         BAL   R9,PRT0000          PRINT CONTROL STATEMENT
         CLC   CTLCARD,CARDRECD    IS THIS A CONTROL CARD?
         BNE   CARD0080            NO.. TEST FOR INCOMPLETE STATEMENT
         LA    R1,CARDRECD+8       POINT PAST CONTROL CARD ID
         L     R2,COMMCRCD         CURRENT DATA ADDRESS
         LH    R3,COMMCRCL         CURRENT LENGTH
         LA    R4,62               MAX DATA ON A STATEMENT
CARD0050 DS    0H
         CLI   0(R1),C' '          END OF STATEMENT?
         BE    CARD0060            YES
         CLC   CARDCONT,0(R1)      CONTINUATION ', ' ?
         BE    CARD0070            YES
         MVC   0(1,R2),0(R1)       MOVE DATA TO INPUT I/O AREA
         LA    R1,1(R1)            NEXT
         LA    R2,1(R2)            NEXT
         LA    R3,1(R3)            1 MORE IN LENGTH
         BCT   R4,CARD0050         LOOP
CARD0060 DS    0H
         DITTRACE ID=CMDEND,       TRACE END OF COMMAND STATEMENT      +
               RDATA1=R3,          .. CAPTURE COMMAND LENGTH           +
               DATA2=ACOMMIOI      .. CAPTURE SOME OF COMMAND
         NI    CARDFLAG,255-$CARDCTT   TURN OFF CONTINUATION FLAG
         STH   R3,COMMCRCL         SET DATA LENGTH
         MVC   COMMCRCD,ACOMMIOI   RESET DATA ADDRESS
         B     CARD9900            EXIT THIS MODULE
CARD0070 DS    0H
         DITTRACE ID=CONTINUE      TRACE CONTINUATION
         MVC   0(1,R2),0(R1)       MOVE DATA TO INPUT I/O AREA
         LA    R2,1(R2)            NEXT
         LA    R3,1(R3)            1 MORE IN LENGTH
         ST    R2,COMMCRCD         SAVE CURRENT ADDRESS
         STH   R3,COMMCRCL         SAVE CURRENT LENGTH
         OI    CARDFLAG,$CARDCTT   SET CONTINUATION FLAG
         B     CARD0030            READ NEXT RECORD
CARD0080 DS    0H
         TM    CARDFLAG,$CARDCTT   IMCOMPLETE COMMAND?
         BNO   CARD0090            NO
         MVC   PRTDATA(CARDM01L),CARDM01
         BAL   R9,PRT0000          PRINT MESSAGE
         NI    CARDFLAG,255-$CARDCTT   TURN OFF CONTINUATION FLAG
         B     CARD0020            READ UNTIL COMPLETE COMMAND
CARD0090 DS    0H
         MVC   PRTDATA(CARDM02L),CARDM02
         BAL   R9,PRT0000          PRINT MESSAGE
         B     CARD0020            READ UNTIL COMPLETE COMMAND
CARD0100 DS    0H
         CLC   CTLCARD,CARDRECD    IS THIS A CONTROL CARD?
         BE    CARD0130            YES
CARD0110 DS    0H
         TM    COMMPFLG,$COMMRCS   RECORD LIMIT GIVEN?
         BNO   CARD0120            NO
         CP    CARDRCDS,COMMRCDS   ENOUGH RECORDS PROCESSED?
         BE    CARD0140            YES
CARD0120 DS    0H
         LA    R1,CARDRECD         POINT PAST CONTROL CARD ID
         ST    R1,COMMCRCD         SET CURRENT RECORD ADDRESS
         MVC   COMMCRCL,H80        SET CURRENT RECORD LENGTH
         AP    CARDRCDS,COMMP1     ADD 1 TO RECORDS
         B     CARD9900            EXIT THIS MODULE
CARD0130 DS    0H
         OI    CARDFLAG,$CARDNP    LAST CARD NOT PROCESSED
CARD0140 DS    0H
         OI    COMMFLAG,$COMMEOF   SIGNAL PSEUDO EOF
         B     CARD9900            EXIT
CARD8000 DS    0H
         OI    CARDFLAG,$CARDEOF   SIGNAL REAL EOF
         TM    CARDFLAG,$CARDCTT   INCOMPLETE COMMAND AT EOF?
         BNO   CARD8010            NO
         MVC   PRTDATA(CARDM04L),CARDM04
         BAL   R9,PRT0000          PRINT MESSAGE
CARD8010 DS    0H
         DITTRACE ID=EOF           TRACE SYSIN END OF FILE
         OI    COMMFLAG,$COMMEOF   SIGNAL END OF FILE
         B     CARD9900            AND EXIT
* ------------------------------------------------------------------- *
*                                                                     *
*              MESSAGE PRINTING                                       *
*                                                                     *
* ------------------------------------------------------------------- *
PRT0000  DS    0H
         MVI   PRTCMMD,$PRTCMD     SET COMMAND
         LA    R1,PRTBLOK          PRINTER INTERFACE BLOCK
         L     R15,APRT            PRINT MODULE ENTRY POINT
         BALR  R14,R15             PRINT MESSAGE
         BR    R9                  RETURN
* ------------------------------------------------------------------- *
*                                                                     *
*              MODULE EXIT POINT                                      *
*                                                                     *
* ------------------------------------------------------------------- *
CARD9900 DS    0H
         DITTRACE ID=EXIT          TRACE EXIT
         L     R13,4(R13)          RESTORE REGISTER 13                  ASE01670
         LM    R14,R12,12(R13)     RESTORE ALL OTHER REGISTERS          ASE01680
         SR    R15,R15             GIVE GOOD RETURN CODE                ASE01690
         BR    R14                 RETURN TO CALLER                     ASE01700
         SPACE 2
* ------------------------------------------------------------------- *
*                                                                     *
*              WORK AREAS/CONSTANTS                                   *
*                                                                     *
* ------------------------------------------------------------------- *
CARDSAVE DC    18F'0'              REGISTER SAVE AREA
H80      DC    H'80'               CONSTANT
CARDFLAG DC    X'00'               SWITCHES/FLAGS
$CARDOPN EQU   X'80'               .. FILE OPEN INDICATOR
$CARDNP  EQU   X'40'               .. LAST CARD NOT PROCESSED
$CARDEOF EQU   X'20'               .. END OF FILE INDICATOR
$CARDCTT EQU   X'10'               .. COMMAND CONTINUATION
CTLCARD  DC    C'$$DITTO'          CONTROL CARD IDENTIFIER
DITTABND DC    C'$$ABEND'          ABEND REQUEST
CARDCONT DC    C', '               CONTINUATION
CARDM01  DC    C'** INCOMPLETE COMMAND, SKIPPING UNTIL COMPLETE COMMAND+
                STATEMENT'
CARDM01L EQU   *-CARDM01
CARDM02  DC    C'** INVALID DITTO CONTROL STATEMENT OR MISSPLACED SYSIN+
                DATA'
CARDM02L EQU   *-CARDM02
CARDM03  DC    C'CONTROL STATEMENT: '
CARDM03D DC    CL72' '
CARDM03L EQU   *-CARDM03
CARDM04  DC    C'** LAST CONTROL STATEMENT IS AN INCOMPLETE COMMAND'
CARDM04L EQU   *-CARDM04
CARDRCDS DC    PL5'0'              RECORD COUNTER
CARDRECD DC    CL80' '             SYSIN I/O AREA
         SPACE 2
* ------------------------------------------------------------------- *
*                                                                     *
*              READER DCB                                             *
*                                                                     *
* ------------------------------------------------------------------- *
SYSIN    DCB   DDNAME=SYSIN,                                           +
               EODAD=CARD8000,                                         +
               DSORG=PS,                                               +
               MACRF=GM,                                               +
               RECFM=FB,                                               +
               LRECL=80
* ------------------------------------------------------------------- *
*                                                                     *
*              PRINT MODULE INTERFACE BLOCK                           *
*                                                                     *
* ------------------------------------------------------------------- *
PRTBLOK  PRTBLOK
         EJECT
* ------------------------------------------------------------------- *
*                                                                     *
*              REGISTER EQUATES                                       *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY  REGEQU
         ABCODES
         SPACE 2
         EJECT
* ------------------------------------------------------------------- *
*                                                                     *
*              COMMON MODULE DSECT                                    *
*                                                                     *
* ------------------------------------------------------------------- *
DITTCOMM DITTCOMM TYPE=DSECT
         END  DITTCDI1
