* ------------------------------------------------------------------- *
*                                                                     *
*              CARD COMMAND INPUT ROUTINE                             *
*                                                                     *
*      This module processes card-input commands.  This module        *
*      is called by 'DITTMAIN' when DITTO is running as a JOB         *
*      (not a started task or TSO session).                           *
*                                                                     *
*      Since cards may be used as commands or as data, and to         *
*      keep this module from being too complicated, all card          *
*      reading is done by a separate module 'DITTCDI1'.               *
*                                                                     *
*      Since the same parameters will be required by card-input,      *
*      console-input, and TSO-input, parameter conversion is          *
*      performed by a separate module.  This was to eliminate as      *
*      much duplication of code as possible.  Also if a new           *
*      parameter is required, only one module would have to be        *
*      changed to convert it.  The parameter converter is 'DITTPARM'. *
*                                                                     *
*      If the command is undefined or all parameters are not          *
*      entered or are in error, messages will be printed identifying  *
*      the invalid command or parameter and error indicators will     *
*      be set in 'DITTCOMM'.                                          *
*                                                                     *
* ------------------------------------------------------------------- *
DITTCARD DITTPRFX CARDSAVE,'CARD-INPUT COMMAND INTERPRETER'
         USING DITTCOMM,R11        SPECIFY BASE
         DITTRACE ID=ENTRY         TRACE ENTRY
CARD0010 DS    0H
         DITTRACE ID=NEWCMMD       TRACE NEW COMMAND READS
         L     R15,ACDI1           CARD INPUT MODULE ENTRY POINT
         BALR  R14,R15             LINK TO CARD INPUT
         TM    COMMFLAG,$COMMEOF   END OF FILE?
         BO    CARD9900            YES, EXIT
         DITTRACE ID=CARD,                                             +
               DATA1=COMMCRCD+0,                                       +
               DATA2=COMMCRCD+8
         L     R3,COMMCRCD         CARD RECORD
         LH    R4,COMMCRCL         LENGTH OF I/O AREA
         LA    R1,0(R3,R4)         END OF I/O AREA +1
         BCTR  R1,0                END OF I/O AREA
CARD0020 DS    0H
         CLI   0(R3),C' '          AT LEAST A BLANK?
         BNE   CARD0030            YES
         LA    R3,1(R3)            TRY NEXT ONE
         BCT   R4,CARD0020         LOOP
         B     CARD0010            NOTHING FOUND, READ NEXT RECORD
CARD0030 DS    0H
         CLI   0(R1),C' '          AT LEAST A BLANK?
         BH    CARD0040            YES
         BCTR  R1,0                BACK UP
         BCT   R4,CARD0030         LOOP BACK
         B     CARD0010            NOTHING FOUND, READ NEXT RECORD
CARD0040 DS    0H
         BAL   R14,CARD8000        FIND COMMAND ON INPUT RECORD
         CLC   CARDXTR,CARDWORK    EXTERNAL TRACE REQUEST?
         BE    CARD3000            YES
         L     R5,ACMD             FUNCTION TABLE ADDRESS
         USING FUNCBLOK,R5         DEFINE ADDRESSIBLITY
         DITTRACE ID=SCANSTRT,     TRACE COMMAND TABLE SCAN START      +
               DATA1=CARDWORK      .. COMMAND BEING SEARCHED FOR
CARD0070 DS    0H
         DITTRACE ID=SCANCMD,      TRACE COMMAND TABLE SCAN            +
               DATA1=FUNCCMD       .. COMMAND IN CURRENT TABLE ENTRY
         CLI   FUNCELEN,X'FF'      END OF TABLE?
         BE    CARDERR1            YES, INVALID COMMAND
         TM    FUNCENV,$FUNCJOB    APPLICABLE TO BATCH JOB MODE?
         BNO   CARD0080            NO.. SKIP THIS COMMAND
         LH    R1,CARDWLEN         ENTERED LENGTH
         CH    R1,FUNCFLEN         FUNCTION LENGTH MATCH?
         BNE   CARD0080            NO
         BCTR  R1,0                ADJUST FOR EXECUTE
         EX    R1,CARDFCLC         CHECK FUNCTION CODE
         BE    CARD0090            COMMAND LOCATED
CARD0080 DS    0H
         AH    R5,FUNCELEN         ADD ENTRY LENGTH
         B     CARD0070            LOOP
CARD0090 DS    0H
         DITTRACE ID=CMDFND        TRACE COMMAND FOUND
         CLI   0(R3),C'?'          COMMAND EXAMPLE WANTED?
         BE    CARD0200            YES
         CLC   FUNCCMD,LISTFNC     XXX FUNCTION?
         BE    CARD1000            YES
         XC    CARDRQCT,CARDRQCT   CLEAR # REQ PARMS FOUND
         XC    CARDOPCT,CARDOPCT   CLEAR # OPT PARMS FOUND
         XC    REQTABLE,REQTABLE   CLEAR FOUND REQ PARM ID'S
CARD0100 DS    0H
         DITTRACE ID=PARMSCAN      TRACE PARAMETER SEARCH START
         LTR   R4,R4               ANY DATA LEFT IN RECORD?
         BZ    CARD0180            NO, TEST PARAMETERS
         MVC   CARDWORK,COMMBLKS   INITIALIZE WORK AREA
         BAL   R14,CARD8000        GET NEXT KEYWORD
         BAL   R14,CARD2000        SEARCH KEYWORDS
         CLI   CARDPID,X'FF'       VALID PARAMETER?
         BE    CARDERR2            NO.. INVALID KEYWORD
         LA    R6,FUNCREQ          REQUIRED PARAMETER ID'S
         SR    R2,R2               CLEAR REGISTER
         ICM   R2,3,FUNCNREQ       NUMBER OF REQUIRED PARAMETERS
         BZ    CARD0150            NO REQUIRED PARAMETERS
         DITTRACE ID=REQSCAN       TRACE REQUIRED PARM TABLE SCAN
CARD0110 DS    0H
         CLC   CARDPID,0(R6)       VALID REQUIRED PARM?
         BE    CARD0120            YES
         LA    R6,1(R6)            NEXT ENTRY
         BCT   R2,CARD0110         LOOP
         B     CARD0150            TRY FOR OPTIONAL KEYWORD
CARD0120 DS    0H
         LTR   R4,R4               ANY DATA LEFT IN RECORD?
         BZ    CARDERR5            NO, TEST PARAMETERS
         BAL   R14,CARD8000        GET OPERAND
         DITTRACE ID=REQPARM       TRACE REQUIRED PARM CONVERSION
         MVI   PARMCMMD,$PARMLOC   MOVE COMMAND
         MVC   PARMPARM,CARDPID    PASS FIELD ID
         MVC   PARMWORK,CARDWORK   PASS DATA
         MVC   PARMWLEN,CARDWLEN   PASS DATA LENGTH
         MVC   PARMOPN,FUNCOPN     NUMBER OF VALUES FOR OPTIONAL PARMS
         LA    R1,FUNCOPV          VALUES FOR OPTIONAL PARMS
         ST    R1,PARMOPV          PASS ADDRESS TO PARM CONVERTER
         LA    R1,PARMBLOK         PARAMETER CONVERTER PARMS
         L     R15,APARM           PARAMETER CONVERTER ENTRY POINT
         BALR  R14,R15             LINK TO PARM CONVERTER
         TM    PARMFLAG,$PARMOK    PARAMETER OKEE AND DOKEE?
         BNO   CARDERR3            NO...
         LA    R1,REQTABLE         FOUND PARM TABLE
CARD0130 DS    0H
         CLI   0(R1),X'00'         AVAILABLE SLOT?
         BE    CARD0140            YES
         CLC   CARDPID,0(R1)       SAME PARAMETER?
         BE    CARD0100            YES.. DON'T COUNT IT TWICE
         LA    R1,1(R1)            TRY NEXT BYTE
         B     CARD0130            LOOP
CARD0140 DS    0H
         MVC   0(1,R1),CARDPID     SAVE PARAMETER ID
         LH    R1,CARDRQCT         REQUIRED PARAMETERS FOUND
         LA    R1,1(R1)            ADD 1
         STH   R1,CARDRQCT         SAVE TOTAL
         B     CARD0100            PROCESS NEXT PARAMETER
CARD0150 DS    0H
         DITTRACE ID=OPTSCAN       TRACE OPTIONAL PARM TABLE SCAN
         SR    R2,R2               CLEAR REGISTER
         ICM   R2,3,FUNCNOPT       NUMBER OF OPTIONAL PARAMETERS
         BZ    CARDERR2            UNKNOWN KEYWORD
         LA    R6,FUNCOPT          FIRST OPTIONAL PARAMETER ID
CARD0160 DS    0H
         CLC   CARDPID,0(R6)       VALID PARM ID?
         BE    CARD0170            YES
         LA    R6,1(R6)            NEXT ENTRY
         BCT   R2,CARD0160         LOOP
         B     CARDERR2            KEYWORD IS UNKNOWN
CARD0170 DS    0H
         LTR   R4,R4               ANY DATA LEFT?
         BZ    CARDERR5            NO
         BAL   R14,CARD8000        GET OPERAND
         DITTRACE ID=OPTPARM       TRACE OPTIONAL PARM CONVERSION
         MVI   PARMCMMD,$PARMLOC   MOVE COMMAND
         MVC   PARMPARM,CARDPID    PASS FIELD ID
         MVC   PARMWORK,CARDWORK   PASS DATA
         MVC   PARMWLEN,CARDWLEN   PASS DATA LENGTH
         MVC   PARMOPN,FUNCOPN     NUMBER OF VALUES FOR OPTIONAL PARMS
         LA    R1,FUNCOPV          VALUES FOR OPTIONAL PARMS
         ST    R1,PARMOPV          PASS ADDRESS TO PARM CONVERTER
         LA    R1,PARMBLOK         PARAMETER CONVERTER PARMS
         L     R15,APARM           PARAMETER CONVERTER ENTRY POINT
         BALR  R14,R15             LINK TO PARAMETER CONVERTER
         TM    PARMFLAG,$PARMOK    PARAMETER OKEE AND DOKEE?
         BNO   CARDERR3            NO...
         LH    R1,CARDOPCT         OPTIONAL PARAMETERS FOUND
         LA    R1,1(R1)            ADD 1
         STH   R1,CARDOPCT         SAVE TOTAL
         B     CARD0100            PROCESS NEXT PARAMETER
CARD0180 DS    0H
         DITTRACE ID=PARMCHK       TRACE PARAMETER CHECK
         CLC   FUNCNREQ,CARDRQCT   ALL REQUIRED PARAMETERS FOUND?
         BNE   CARDERR4            NO
         SR    R1,R1               CLEAR REGISTER
         ICM   R1,3,FUNCIN1        INPUT MODULE ADDRESS DISPLACEMENT
         BZ    CARD0190            NO INPUT MODULE
         LA    R2,DITTCOMM(R1)     INPUT MODULE ADDRESS ADDRESS
         MVC   COMMINM,0(R2)       INPUT MODULE ADDRESS
         MVC   COMMIFLG,FUNCIFLG   INPUT MODULE FLAGS
CARD0190 DS    0H
         ICM   R1,3,FUNCOUT1       OUTPUT MODULE ADDRESS DISPLACEMENT
         BZ    CARD9900            NO OUTPUT MODULE
         LA    R2,DITTCOMM(R1)     OUTPUT MODULE ADDRESS ADDRESS
         MVC   COMMOUTM,0(R2)      OUTPUT MODULE ADDRESS
         MVC   COMMOFLG,FUNCOFLG   OUTPUT MODULE FLAGS
         B     CARD9900            EXIT
CARD0200 DS    0H
         MVC   CARDM06A,CARDWORK   MOVE COMMAND
         OC    CARDM06A,COMMBLKS   MAKE IT ALL PRINTABLE
         MVC   CARDM06B,FUNCEX1    MOVE EXAMPLE OF COMMAND
         LA    R1,CARDM06          POINT TO MESSAGE
         BAL   R9,CARD8200         LINK TO PRINT ROUTINE
         B     CARD0010            LOOK FOR NEXT COMMAND
         EJECT
* ------------------------------------------------------------------- *
*                                                                     *
*              LIST AVAILABLE COMMANDS                                *
*                                                                     *
* ------------------------------------------------------------------- *
CARD1000 DS    0H
         DITTRACE ID=LISTCMDS      TRACE START OF LIST
         L     R5,ACMD             FUNCTIONS TABLE ADDRESS
         MVI   PRTCMMD,$PRTHEAD    REQUEST HEADING
         LA    R1,PRTBLOK          PRINT INTERFACE BLOCK
         L     R15,APRT            PRINT MODULE ENTRY POINT
         BALR  R14,R15             LINK TO PRINT MODULE
CARD1010 DS    0H
         DITTRACE ID=LISTFUNC      TRACE EACH FUNCTION
         CLI   FUNCELEN,X'FF'      END OF TABLE?
         BE    CARD1030            YES
         TM    FUNCENV,$FUNCJOB    COMMAND APPLICABLE TO BATCH JOB?
         BNO   CARD1020            NO
         MVC   CARDM07A,FUNCCMD    MOVE COMMAND
         MVC   CARDM07B,FUNCDESC   MOVE DESCRIPTION
         MVI   PRTCMMD,$PRTCMD     SET COMMAND
         MVC   PRTDATA(CARDM07L),CARDM07
         MVI   PRTCC,C'0'          DOUBLE SPACE CARRIAGE CONTROL
         MVI   PRTCMMD,$PRTDATA    SET COMMAND
         LA    R1,PRTBLOK          PRINT INTERFACE BLOCK
         L     R15,APRT            PRINT MODULE ENTRY POINT
         BALR  R14,R15             LINK TO PRINT MODULE
         MVC   CARDM08A,FUNCEX1    MOVE EXAMPLE OF COMMAND
         MVC   PRTDATA(CARDM08L),CARDM08
         MVI   PRTCC,C' '          SINGLE SPACE CARRIAGE CONTROL
         LA    R1,PRTBLOK          PRINT INTERFACE BLOCK
         L     R15,APRT            PRINT MODULE ENTRY POINT
         BALR  R14,R15             LINK TO PRINT MODULE
CARD1020 DS    0H
         AH    R5,FUNCELEN         NEXT FUNCTION
         B     CARD1010            LIST NEXT FUNCTION
CARD1030 DS    0H
         MVC   PRTDATA(CARDM10L),CARDM10
         MVI   PRTCC,C'-'          TRIPLE SPACE CARRIAGE CONTROL
         MVI   PRTCMMD,$PRTDATA    SET COMMAND
         LA    R1,PRTBLOK          PRINT INTERFACE BLOCK
         L     R15,APRT            PRINT MODULE ENTRY POINT
         BALR  R14,R15             LINK TO PRINT MODULE
         LA    R2,KEYWDTBL         FIRST KEYWORD/PARM
         USING KEYWDSCT,R2         DEFINE BASE
CARD1040 DS    0H
         CLI   0(R2),X'FF'         END OF TABLE?
         BE    CARD0010            YES
         CLC   KEYWD,KEYWDPRM      KEYWORD AND ALIAS THE SAME?
         BE    CARD1050            DON'T PRINT IT
         MVC   CARDM11K,KEYWD      SET KEYWORD
         MVC   CARDM11A,KEYWDPRM   SET KEYWORD ALIAS
         MVC   PRTDATA(CARDM11L),CARDM11
         MVI   PRTCMMD,$PRTDATA    SET COMMAND
         LA    R1,PRTBLOK          PRINT INTERFACE BLOCK
         L     R15,APRT            PRINT MODULE ENTRY POINT
         BALR  R14,R15             LINK TO PRINT MODULE
CARD1050 DS    0H
         LA    R2,KEYWDDLN(R2)     NEXT KEYWORD
         B     CARD1040            PRINT ALL PARMS/ALIASES
         EJECT
* ------------------------------------------------------------------- *
*                                                                     *
*              LOCATE KEYWORDS                                        *
*                                                                     *
* ------------------------------------------------------------------- *
CARD2000 DS    0H
         LA    R2,KEYWDTBL         KEYWORD TABLE
         MVI   CARDPID,X'FF'       ASSUME INVALID KEYWORD
CARD2010 DS    0H
         CLI   KEYWDLEN,X'FF'      END OF TABLE?
         BER   R14                 YES
         LH    R1,KEYWDLEN         KEYWORD LENGTH
         CH    R1,CARDWLEN         MATCHING LENGTHS?
         BNE   CARD2020            NO
         BCTR  R1,0                ADJUST
         EX    R1,KEYWDCLC         KEYWORD LOCATED?
         BE    CARD2030            YES
CARD2020 DS    0H
         LA    R2,KEYWDDLN(R2)     NEXT KEYWORD
         B     CARD2010            CHECK IT
CARD2030 DS    0H
         MVC   CARDPID,KEYWDPID    MOVE PARAMETER ID
         BR    R14                 EXIT
KEYWDCLC CLC   KEYWD(0),CARDWORK   CHECK FOR MATCHING KEYWORDS
* ------------------------------------------------------------------- *
*                                                                     *
*              EXTERNAL TRACE                                         *
*                                                                     *
* ------------------------------------------------------------------- *
CARD3000 DS    0H
         DITTRACE ID=XTRACE
         BAL   R14,CARD8000        FIND TRACE OPTION (ON OR OFF)
         CLC   CARDON,CARDWORK     TURN TRACE ON?
         BE    CARD3010            YES
         CLC   CARDOFF,CARDWORK    TURN TRACE OFF?
         BE    CARD3030            YES
         LA    R1,CARDM15          INVALID EXTERNAL TRACE COMMAND
         B     CARD3050            PRINT MESSAGE
CARD3010 DS    0H
         TM    XTRFLAG,$XTRACE     TRACE ALREADY ON?
         BO    CARD3020            YES
         DITTRACE ID=TRACEON
         OI    XTRFLAG,$XTRACE     SIGNAL EXTERNAL TRACE ON
         LA    R1,CARDM09          TRACE ACTIVATED MESSAGE
         B     CARD3050            PRINT MESSAGE
CARD3020 DS    0H
         DITTRACE ID=DUPON
         LA    R1,CARDM12          TRACE ALREADY ACTIVE
         B     CARD3050            PRINT MESSAGE
CARD3030 DS    0H
         TM    XTRFLAG,$XTRACE     TRACE ON?
         BNO   CARD3040            NO
         DITTRACE ID=TRACEOFF
         LA    R1,CARDM13          TRACE DE-ACTIVATED MESSAGE
         NI    XTRFLAG,255-$XTRACE RESET FLAG
         B     CARD3050            PRINT MESSAGE
CARD3040 DS    0H
         DITTRACE ID=DUPOFF
         LA    R1,CARDM14          TRACE NOT ACTIVE MESSAE
CARD3050 DS    0H
         BAL   R9,CARD8200         PRINT MESSAGE
         B     CARD0010            READ NEXT COMMAND
* ------------------------------------------------------------------- *
*                                                                     *
*              INPUT PARSER                                           *
*         R1, R2, R15 ARE WORK REGISTERS                              *
*         R3 HAS CURRENT INPUT ADDRESS                                *
*         R4 LENGTH REMAINING ON INPUT                                *
*         R14 IS RETURN ADDRESS                                       *
*                                                                     *
*         ON EXIT 'CARDWORK' CONTAINS DATA FOUND TO NEXT DELIMITER    *
*         UP TO 50 BYTES MAX, 'CARDWLEN' WILL BE SET TO THE LENGTH.   *
*         R3 WILL BE UPDATED PAST THE DELIMITER AND R4 WILL REFLECT   *
*         LENGTH LEFT FOLLOWING THE DELIMITER.                        *
*                                                                     *
* ------------------------------------------------------------------- *
CARD8000 DS    0H
         ST    R14,PARSESAV        SAVE RETURN ADDRESS
         DITTRACE ID=PARSE
         XC    CARDWORK,CARDWORK   CLEAR WORK AREA
         XC    CARDWLEN,CARDWLEN   CLEAR LENGTH
         LTR   R4,R4               ANY DATA LEFT IN RECORD?
         BZ    CARD8040            NO..
         LA    R1,CARDWORK         WORK AREA
         SR    R2,R2               CLEAR LENGTH COUNTER
         LA    R15,L'CARDWORK      MAXIMUM LENGTH
CARD8010 DS    0H
         CLI   0(R3),C' '          BLANK?
         BE    CARD8030            YES
         CLI   0(R3),C','          COMMA?
         BE    CARD8030            YES
         CLI   0(R3),C'='          EQUAL SIGN?
         BE    CARD8030            YES
         MVC   0(1,R1),0(R3)       MOVE IT
         LA    R2,1(R2)            COUNT IT
         LA    R3,1(R3)            NEXT
         LA    R1,1(R1)            NEXT
         BCT   R4,CARD8020         DECREMENT COUNT
         STH   R2,CARDWLEN         SAVE CURRENT LENGTH
         B     CARD8040            LENGTH EXHAUSTED.. STOP SCAN
CARD8020 DS    0H
         BCT   R15,CARD8010        LOOP
CARD8030 DS    0H
         STH   R2,CARDWLEN         SAVE WORK AREA ACTIVE LENGTH
         LA    R3,1(R3)            SKIP DELIMITER
         BCTR  R4,0                REMOVE DELIMITER LENGTH
CARD8040 DS    0H
         DITTRACE ID=PARSEEND,     PARSE COMPLETE                      +
               DATA1=CARDWORK,     .. OUTPUT VALUE                     +
               DATA2=CARDWLEN      .. OUTPUT LENGTH
         L     R14,PARSESAV        RESTORE RETURN ADDRESS
         BR    R14                 LENGTH EXHAUSTED, EXIT
* ------------------------------------------------------------------- *
*                                                                     *
*          MESSAGE PRINTING                                           *
*                                                                     *
*        R1 SHOULD POINT TO A 2-BYTE LENGTH PREFIXING THE MESSAGE     *
*        TEXT.                                                        *
*                                                                     *
* ------------------------------------------------------------------- *
CARD8200 DS    0H
         MVI   PRTCMMD,$PRTCMD     PRINT COMMAND
         LH    R15,0(R1)           MESSAGE LENGTH
         BCTR  R15,0               ADJUST FOR EXECUTE
         EX    R15,MSGMVC          MOVE THE MESSAGE
         LA    R1,PRTBLOK          PRINT INTERFACE BLOCK
         L     R15,APRT            PRINT ROUTINE ENTRY POINT
         BALR  R14,R15             LINK TO PRINT ROUTINE
         BR    R9                  RETURN
MSGMVC   MVC   PRTDATA(0),2(R1)    MOVE MESSAGE
* ------------------------------------------------------------------- *
*                                                                     *
*              ERROR ROUTINES                                         *
*                                                                     *
* ------------------------------------------------------------------- *
CARDERR1 DS    0H
         OI    COMMFLAG,$COMMCER   SIGNAL COMMAND ERROR
         OC    CARDWORK,COMMBLKS   MAKE ALL POSITIONS PRINTABLE
         MVC   CARDM01M,CARDWORK   MOVE FUNCTION
         LA    R1,CARDM01          POINT TO MESAGE
         BAL   R9,CARD8200         LINK TO PRINT ROUTINE
         B     CARD9900            EXIT
CARDERR2 DS    0H
         OI    COMMFLAG,$COMMCER   SIGNAL COMMAND ERROR
         OC    CARDWORK,COMMBLKS   MAKE ALL POSITIONS PRINTABLE
         MVC   CARDM02M,CARDWORK   MOVE KEYWORD
         LA    R1,CARDM02          POINT TO MESSAGE
         BAL   R9,CARD8200         LINK TO PRINT ROUTINE
         B     CARD9900            EXIT
CARDERR3 DS    0H
         OI    COMMFLAG,$COMMCER   SIGNAL COMMAND ERROR
         L     R1,PARMMSGA         RETURNED MESSAGE'S ADDRESS
         BAL   R9,CARD8200         LINK TO PRINT ROUTINE
         OC    CARDWORK,COMMBLKS   MAKE ALL POSITIONS PRINTABLE
         MVC   CARDM03M,CARDWORK   MOVE OPERAND
         LA    R1,CARDM03          POINT TO MESSAGE
         BAL   R9,CARD8200         LINK TO PRINT ROUTINE
         B     CARD9900            EXIT
CARDERR4 DS    0H
         OI    COMMFLAG,$COMMCER   SIGNAL COMMAND ERROR
         LA    R1,CARDM04          POINT TO MESSAGE
         BAL   R9,CARD8200         LINK TO PRINT ROUTINE
         SR    R2,R2               CLEAR REGISTER
         ICM   R2,3,FUNCNREQ       NUMBER OF REQUIRED PARAMETERS
         LA    R6,FUNCREQ          FIRST REQUIRED PARAMETER
         DITTRACE ID=LISTREQ       TRACE LISTING MISSING PARMS
CARDE401 DS    0H
         LA    R1,REQTABLE         FOUND PARM TABLE
CARDE402 DS    0H
         CLI   0(R1),X'00'         EMPTY SLOT?
         BE    CARDE403            YES, NO NEED TO LOOK FURTHER
         CLC   0(1,R6),0(R1)       WAS THIS PARAMETER FOUND?
         BE    CARDE404            YES
         LA    R1,1(R1)            NEXT FOUND PARAMETER ID
         B     CARDE402            TRY THIS ONE
CARDE403 DS    0H
         MVI   PARMCMMD,$PARMMIS   MOVE COMMAND
         MVC   PARMPARM,0(R6)      MOVE PARAMETER ID
         LA    R1,PARMBLOK         PARAMETER CONVERTER PARMS
         L     R15,APARM           PARAMETER CONVERTER ENTRY POINT
         BALR  R14,R15             LINK TO PARAMETER CONVERTER
         L     R1,PARMMSGA         RETURNED MESSAGE ADDRESS
         BAL   R9,CARD8200         PRINT RETURNED MESSAGE
CARDE404 DS    0H
         LA    R6,1(R6)            NEXT PARAMETER
         BCT   R2,CARDE401         LOOK FOR THIS PARAMETER
         B     CARD9900            EXIT
CARDERR5 DS    0H
         OI    COMMFLAG,$COMMCER   SIGNAL COMMAND ERROR
         OC    CARDWORK,COMMBLKS   MAKE ALL POSITIONS PRINTABLE
         MVC   CARDM05M,CARDWORK   MOVE KEYWORD
         LA    R1,CARDM05          POINT TO MESSAGE
         BAL   R9,CARD8200         LINK TO PRINT ROUTINE
         B     CARD9900            EXIT
* ------------------------------------------------------------------- *
*                                                                     *
*              MODULE EXIT POINT                                      *
*                                                                     *
* ------------------------------------------------------------------- *
CARD9900 DS    0H
         DITTRACE ID=EXIT          TRACE EXITS
         L     R13,4(R13)          RESTORE REGISTER 13                  ASE01670
         LM    R14,R12,12(R13)     RESTORE ALL OTHER REGISTERS          ASE01680
         SR    R15,R15             GIVE GOOD RETURN CODE                ASE01690
         BR    R14                 RETURN TO CALLER                     ASE01700
* ------------------------------------------------------------------- *
*                                                                     *
*              CARD-INPUT FUNCTION EXECUTED INSTRUCTIONS              *
*                                                                     *
* ------------------------------------------------------------------- *
CARDFCLC CLC   FUNCCMD(0),CARDWORK  CORRECT COMMAND?
CARDCMVC MVC   PRTDATA+8(0),0(R15)  MOVE COMMAND(S) TO PRINT
         EJECT
* ------------------------------------------------------------------- *
*                                                                     *
*              CARD-INPUT FUNCTION WORK AREAS                         *
*                                                                     *
* ------------------------------------------------------------------- *
CARDSAVE DC    18F'0'              REGISTER SAVE AREA
PARSESAV DS    A                   PARSE ROUTINE RETURN ADDRESS
CARDWLEN DC    H'0'                CURRENT PARSER OUTPUT LENGTH
CARDRQCT DC    H'0'                REQUIRED PARAMETERS COUNT FOUND
CARDOPCT DC    H'0'                OPTIONAL PARAMETERS COUNT FOUND
CARDWORK DC    CL50' '             PARSER OUTPUT WORK AREA
REQTABLE DC    XL10'00'            FOUND REQUIRED PARAMETER TABLE
CARDPID  DC    X'FF'               PARAMETER ID FROM KEYWD TABLE
CARDXTR  DC    C'TRACE'            EXTERNAL TRACE COMMAND
CARDON   DC    C'ON'               CONSTANT
CARDOFF  DC    C'OFF'              CONSTANT
         SPACE 2
* ------------------------------------------------------------------- *
*                                                                     *
*              CARD-INPUT FUNCTION PRINT MODULE INTERFACE             *
*                                                                     *
* ------------------------------------------------------------------- *
PRTBLOK  PRTBLOK
         SPACE 2
* ------------------------------------------------------------------- *
*                                                                     *
*              CARD-INPUT FUNCTION PARAMETER SEARCH INTERFACE         *
*                                                                     *
* ------------------------------------------------------------------- *
PARMBLOK PARMBLOK
         SPACE 2
* ------------------------------------------------------------------- *
*                                                                     *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
LISTFNC  DC    CL4'XXX '
CARDM01  DC    AL2(CARDM01L-2)
         DC    C'INVALID CARD INPUT FUNCTION: '
CARDM01M DC    CL50' '
CARDM01L EQU   *-CARDM01
CARDM02  DC    AL2(CARDM02L-2)
         DC    C'INVALID KEYWORD IN CARD INPUT FUNCTION: '
CARDM02M DC    CL44' '
CARDM02L EQU   *-CARDM02
CARDM03  DC    AL2(CARDM03L-2)
         DC    C'INVALID OPERAND IN CARD INPUT FUNCTION: '
CARDM03M DC    CL44' '
CARDM03L EQU   *-CARDM03
CARDM04  DC    AL2(CARDM04L-2)
         DC    C'REQUIRED PARAMETERS OMITTED'
CARDM04L EQU   *-CARDM04
CARDM05  DC    AL2(CARDM05L-2)
         DC    C'PARAMETER SPECIFIED WITH NO OPERAND: '
CARDM05M DC    CL44' '
CARDM05L EQU   *-CARDM05
CARDM06  DC    AL2(CARDM06L-2)
         DC    C'EXAMPLE OF COMMAND- '
CARDM06A DC    CL8' '
         DC    C' - '
CARDM06B DC    CL80' '
CARDM06L EQU   *-CARDM06
CARDM07  DC    C'COMMAND: '
CARDM07A DC    CL4' '
         DC    CL18' '
CARDM07B DC    CL80' '
CARDM07L EQU   *-CARDM07
CARDM08  DC    C'EXAMPLE OF COMMAND:'
         DC    CL12' '
CARDM08A DC    CL80' '
CARDM08L EQU   *-CARDM08
CARDM09  DC    AL2(CARDM09L)
CARDM09M DC    C'EXTERNAL TRACE ACTIVATED'
CARDM09L EQU   *-CARDM09M
CARDM10  DC    CL20'KEYWORD ALIASES --  '
         DC    CL08' PARM   '
         DC    CL02' '
         DC    CL08' ALIAS  '
CARDM10L EQU   *-CARDM10
CARDM11  DC    CL20' '
CARDM11K DC    CL08' '
         DC    CL02' '
CARDM11A DC    CL08' '
CARDM11L EQU   *-CARDM11
CARDM12  DC    AL2(CARDM12L)
CARDM12M DC    C'EXTERNAL TRACE ALREADY ACTIVE'
CARDM12L EQU   *-CARDM12M
CARDM13  DC    AL2(CARDM13L)
CARDM13M DC    C'EXTERNAL TRACE DE-ACTIVATED'
CARDM13L EQU   *-CARDM13M
CARDM14  DC    AL2(CARDM14L)
CARDM14M DC    C'EXTERNAL TRACE NOT ACTIVE'
CARDM14L EQU   *-CARDM14M
CARDM15  DC    AL2(CARDM15L)
CARDM15M DC    C'INVALID TRACE COMMAND (USE ''TRACE ON'' OR ''TRACE OFF+
               '')'
CARDM15L EQU   *-CARDM15M
         SPACE 2
* ------------------------------------------------------------------- *
*                                                                     *
*              DEFINE PARAMETERS                                      *
*                                                                     *
* ------------------------------------------------------------------- *
         PARMGEN GEN=NO
* ------------------------------------------------------------------- *
*                                                                     *
*              DEFINE KEYWORDS                                        *
*                                                                     *
* ------------------------------------------------------------------- *
KEYWDTBL DS    0H
         KEYWD BLKSIZE,PARM=BLKSIZE
         KEYWD COUNT,PARM=RECORDS
         KEYWD CUUIN,PARM=CUUIN
         KEYWD CUUOUT,PARM=CUUOUT
         KEYWD DENSITY,PARM=DENSITY
         KEYWD DISKADDR,PARM=DISKADDR
         KEYWD FILES,PARM=FILES
         KEYWD FROMVOL,PARM=LOWVOL
         KEYWD HIGHVOL,PARM=HIGHVOL
         KEYWD IN,PARM=CUUIN
         KEYWD INCUU,PARM=CUUIN
         KEYWD LRECL,PARM=RECSIZE
         KEYWD LOWVOL,PARM=LOWVOL
         KEYWD OPT,PARM=OPT
         KEYWD OPTION,PARM=OPT
         KEYWD OUT,PARM=CUUOUT
         KEYWD OUTCUU,PARM=CUUOUT
         KEYWD RECSIZE,PARM=RECSIZE
         KEYWD RECORDS,PARM=RECORDS
         KEYWD RECS,PARM=RECORDS
         KEYWD TOVOL,PARM=HIGHVOL
         KEYWD UNIT,PARM=UNIT
         KEYWD VOLSER,PARM=LOWVOL
         DC    X'FFFF'
* ------------------------------------------------------------------- *
*                                                                     *
*              REGISTER EQUATES                                       *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY  REGEQU
         EJECT
* ------------------------------------------------------------------- *
*                                                                     *
*              FUNCTION TABLE DSECT                                   *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY  FUNCBLOK
         SPACE
* ------------------------------------------------------------------- *
*                                                                     *
*              KEYWORD TABLE DSECT                                    *
*                                                                     *
* ------------------------------------------------------------------- *
KEYWDSCT DSECT
KEYWDLEN DS    AL2                KEYWORD LENGTH
KEYWD    DS    CL8                KEYWORD
KEYWDPRM DS    CL8                INTERNAL PARM NAME
KEYWDPID DS    X                  KEYWORD PARAMETER ID
KEYWDDLN EQU   *-KEYWDSCT         TABLE ENTRY LENGTH
         EJECT
* ------------------------------------------------------------------- *
*                                                                     *
*              COMMON MODULE DSECT                                    *
*                                                                     *
* ------------------------------------------------------------------- *
DITTCOMM DITTCOMM TYPE=DSECT
         END  DITTCARD
