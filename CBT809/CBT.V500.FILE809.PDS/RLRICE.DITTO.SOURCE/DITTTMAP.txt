* ------------------------------------------------------------------- *
*                                                                     *
*              TAPE MAP 'TMAP' MODULE                                 *
*                                                                     *
*       THIS MODULE PERFORMS THE 'TMAP' FUNCTION.  'HDR1', 'HDR2',    *
*       'EOF1', AND 'EOF2' RECORDS ARE "WATCHED" FOR, AND WHEN        *
*       ENCOUNTERED, THE RELATIVE FILE NUMBER AND FILE NAME ARE       *
*       PRINTED.  BY DEFAULT TMAP WILL STOP WHEN 2 TAPE MARKS IN      *
*       A ROW WITH NO INTERVIENING DATA ARE FOUND (LOGICAL-END-OF-    *
*       TAPE).  THE OPTIONS 'FULL', 'FULLTAPE', OR 'ALL' WILL CAUSE   *
*       THE TAPE TO BE READ TO THE REFLECTIVE MARKER OR OFF THE END   *
*       OF THE REEL.                                                  *
*                                                                     *
* ------------------------------------------------------------------- *
DITTTMAP DITTPRFX TAPESAVE,'TAPE ''VTOC'' MODULE'
         USING DITTCOMM,R11        SPECIFY BASE
         DITTRACE ID=ENTRY         TRACE ENTRY
         TM    COMMFLAG,$COMMCLS   CLOSE FILES?
         BO    EXIT0000            YES.. EXIT
         ZAP   FILECT,COMMP0       ZERO FILES COUNTER
         MVI   TAPEFLAG,00         INITIALIZE FLAGS
TAPE0010 DS    0H
         L     R10,COMMIND         DYNAMIC BLOCK ADDRESS
         USING DYNBLOK,R10         SPECIFY BASE
         ST    R10,EXCPDYN         PASS DYNAMIC BLOCK ADDRESS TO EXCP
         LA    R2,DYNDCB           DCB ADDRESS
         USING IHADCB,R2           DEFINE DSECT BASE
         DITTRACE ID=CCWINIT       TRACE INTIALIZE ROUTINE
         L     R1,ACOMMIOI         INPUT BUFFER
         STCM  R1,7,REWCCW+1       RELOCATE I/O ADDRESS
         STCM  R1,7,READCCW+1      RELOCATE I/O ADDRESS
         DITTRACE ID=REWIND        TRACE INITIAL REWIND
         MVC   DYNCCW,REWCCW       INITIALIZE THE CCW
         LA    R1,EXCPBLOK         EXCP INTERFACE BLOCK
         L     R15,AEXCP           EXCP MODULE ENTRY POINT
         BALR  R14,R15             LINK TO EXCP MODULE
         CLI   EXCPSTAT,$EXCPOK    EXCP SUCCESSFUL?
         BNE   TAPE0400            NO
         DITTRACE ID=GOODREW       REWIND SUCCESSFUL
TAPE0020 DS    0H
         DITTRACE ID=READFWD       TRACE READ FORWARD
         MVC   DYNCCW,READCCW      INITIALIZE THE CCW
         LA    R1,EXCPBLOK         EXCP INTERFACE BLOCK
         L     R15,AEXCP           EXCP MODULE ENTRY POINT
         BALR  R14,R15             LINK TO EXCP MODULE
         CLI   EXCPSTAT,$EXCPNA    I/O NOT ALLOWED?
         BE    TAPE0050            NOT ALLOWED
         TM    DYNSTAT,$DYNPEOT    PHYSICAL EOT REACHED?
         BO    TAPE0220            YES
         CLI   DYNSTAT,$DYNTM      TAPE MARK READ?
         BE    TAPE0200            YES
         CLI   EXCPSTAT,$EXCPOK    EXCP SUCCESSFUL?
         BNE   TAPE0400            NO
         TM    TAPEFLAG,$TAPEFHD   FILE HEADING ALREADY PRINTED??
         BO    TAPE0030            YES
         BAL   R9,HEAD0000         PRINT FILE HEADING
         OI    TAPEFLAG,$TAPEFHD   SET HEADING FLAG
TAPE0030 DS    0H
         NI    TAPEFLAG,255-$TAPETM    TURN OFF TAPE MARK FLAG
         CLC   EXCPLEN,H80         WAS LENGTH 80?
         BNE   TAPE0040            NO
         TM    TAPEFLAG,$TAPEDTA   DATA FLAG ON??
         BO    TAPE0020            YES... READ ANOTHER BLOCK
         ICM   R1,7,READCCW+1      I/O AREA USED
         CLC   TAPEVOL1,0(R1)      'VOL1' RECORD??
         BE    VOL10000            YES
         CLC   TAPEHDR1,0(R1)      'HDR1' RECORD??
         BE    HDR10000            YES
         CLC   TAPEEOV1,0(R1)      'EOV1' RECORD??
         BE    EOV10000            YES
         CLC   TAPEEOF1,0(R1)      'EOF1' RECORD??
         BE    EOF10000            YES
         CLC   TAPEHDR2,0(R1)      'HDR2' RECORD??
         BE    HDR20000            YES
         CLC   TAPEEOF2,0(R1)      'EOF2' RECORD??
         BE    EOF20000            YES
         CLC   TAPEEOV2,0(R1)      'EOV2' RECORD??
         BE    EOV20000            YES
TAPE0040 DS    0H
         DITTRACE ID=DATA          TRACE DATA ENCOUNTERED
         TM    TAPEFLAG,$TAPEDTA   IS TAPE DATA FLAG ON??
         BO    TAPE0020            YES... READ ANOTHER BLOCK
         OI    TAPEFLAG,$TAPEDTA   SET DATA FLAG
         MVI   PRTCMMD,$PRTDATA    PRINT COMMAND
         MVI   PRTCC,C' '          SINGLE SPACE
         MVC   PRTDATA(L'DATAMSG),DATAMSG
         LA    R1,PRTBLOK          PRINT INTERFACE BLOCK
         L     R15,APRT            PRINT MODULE ENTRY POINT
         BALR  R14,R15             LINK TO PRINT MODULE
         B     TAPE0020            READ ANOTHER BLOCK FROM THE TAPE
TAPE0050 DS    0H
         DITTRACE ID=NOTALLOW      I/O NOT ALLOWED BY EXCP
         ABEND ABEND034,DUMP,,USER ABEND
* ------------------------------------------------------------------- *
*                                                                     *
*                    TAPE MARK HAS JUST BEEN READ                     *
*                                                                     *
* ------------------------------------------------------------------- *
TAPE0200 DS    0H
         DITTRACE ID=TAPEMARK,     TRACE TAPE MARK ENCOUNTERED         +
               DATA1=TAPEFLAG      .. CAPTURE FLAG SETTING
         NI    TAPEFLAG,255-$TAPEDTA   RESET DATA FLAG
         NI    TAPEFLAG,255-$TAPEFHD   RESET FILE HEADING FLAG
         TM    TAPEFLAG,$TAPETM    TAPE MARK FLAG ALREADY ON??
         BO    TAPE0210            YES ... TWO IN A ROW IS LOGICAL EOT
         OI    TAPEFLAG,$TAPETM    SET TAPE MARK FLAG
         B     TAPE0020            READ ANOTHER BLOCK
TAPE0210 DS    0H
         TM    TAPEFLAG,$TAPEEOT   ALREADY REACHED LEOT?
         BO    TAPE0020            DON'T SEND LEOT MESSAGE AGAIN
         DITTRACE ID=LEOT          LOGICAL END OF TAPE
         OI    TAPEFLAG,$TAPEEOT   INDICATE LOGICAL EOT REACHED
         MVI   PRTCMMD,$PRTDATA
         MVI   PRTCC,C'-'          TRIPLE SPACE
         MVC   PRTDATA(L'LEOTMSG),LEOTMSG
         LA    R1,PRTBLOK          PRINT INTERFACE BLOCK
         L     R15,APRT            PRINT MODULE ENTRY POINT
         BALR  R14,R15             LINK TO PRINT MODULE
         CLC   FULLTAPE,COMMOPT    FULL TAPE?
         BE    TAPE0020            YES, KEEP ON TRUCKIN'
         CLC   FULL,COMMOPT        FULL TAPE?
         BE    TAPE0020            YES, KEEP ON TRUCKIN'
         CLC   ALL,COMMOPT         FULL TAPE?
         BE    TAPE0020            YES, KEEP ON TRUCKIN'
         OI    COMMFLAG,$COMMEOF   OTHERWISE EOF ON 2 TAPE MARKS
         B     EXIT0000            AND EXIT
TAPE0220 DS    0H
         DITTRACE ID=PEOT          LOGICAL END OF TAPE
         MVI   PRTCMMD,$PRTDATA
         MVI   PRTCC,C'-'          TRIPLE SPACE
         MVC   PRTDATA(L'PEOTMSG),PEOTMSG
         LA    R1,PRTBLOK          PRINT INTERFACE BLOCK
         L     R15,APRT            PRINT MODULE ENTRY POINT
         BALR  R14,R15             LINK TO PRINT MODULE
         OI    COMMFLAG,$COMMEOF   SIGNAL 'END OF FILE'
         B     EXIT0000            EXIT
         EJECT
* ------------------------------------------------------------------- *
*                                                                     *
*                    ERROR ROUTINES                                   *
*                                                                     *
* ------------------------------------------------------------------- *
TAPE0400 DS    0H
         LA    R3,DYNIOB           IOB USED FOR I/O
         DITTRACE ID=IOERR         TRACE I/O ERRORS
         ABEND ABEND017,DUMP,,USER
TAPE0410 DS    0H
         DITTRACE ID=ZERORECL      TRACE LRECL = ZERO
         ABEND ABEND018,DUMP,,USER
* ------------------------------------------------------------------- *
*                                                                     *
*              PRINT HEADING FOR NEW PHYSICAL FILE                    *
*                                                                     *
* ------------------------------------------------------------------- *
HEAD0000 DS    0H
         AP    FILECT,COMMP1       ADD 1 TO FILE COUNT
         MVC   PRFILE,COMMED2      INITIALIZE WITH EDIT WORD
         ED    PRFILE,FILECT       EDIT FILE NUMBER
         MVC   PRTDATA(HEADMSGL),HEADMSG
         MVI   PRTCMMD,$PRTDATA    REQUEST PRINT
         MVI   PRTCC,C'0'          DOUBLE SPACE
         LA    R1,PRTBLOK          PRINT INTERFACE BLOCK
         L     R15,APRT            PRINT MODULE ENTRY POINT
         BALR  R14,R15             LINK TO PRINT MODULE
         BR    R9                  RETURN
* ------------------------------------------------------------------- *
*                                                                     *
*              VOL1 RECORD PROCESSING                                 *
*                                                                     *
* ------------------------------------------------------------------- *
VOL10000 DS    0H
         USING VOL1,R1             DEFINE DSECT BASE
         DITTRACE ID=VOL1          TRACE VOL1
         MVC   PRVOL,VOL1VOL       MOVE VOLUME SERIAL NUMBER
         MVC   PRTDATA(VOL1MSGL),VOL1MSG
         B     LBL0000             PRINT LABEL DATA
* ------------------------------------------------------------------- *
*                                                                     *
*              HDR1 RECORD PROCESSING                                 *
*                                                                     *
* ------------------------------------------------------------------- *
HDR10000 DS    0H
         USING HDR1,R1             DEFINE DSECT BASE
         DITTRACE ID=HDR1          TRACE HDR1
         MVC   PRDSN,HDR1DSN       DATASET NAME
         MVC   PRVSEQ,HDR1VSEQ     VOLUME SEQUENCE
         MVC   PRDSEQ,HDR1DSEQ     DATASET SEQUENCE
         MVC   PRCDTE,HDR1CDTE     CREATE DATE
         MVC   PREXPD,HDR1EXPD     EXPIRATION DATE
         MVC   PRTDATA(HDR1MSGL),HDR1MSG
         B     LBL0000             PRINT LABEL DATA
* ------------------------------------------------------------------- *
*                                                                     *
*              EOF1 RECORD PROCESSING                                 *
*                                                                     *
* ------------------------------------------------------------------- *
EOF10000 DS    0H
         DITTRACE ID=EOF1          TRACE EOF1
         MVC   PRBLKC,HDR1BLKC     BLOCK COUNT
         MVC   PRTDATA(EOF1MSGL),EOF1MSG
         B     LBL0000             PRINT LABEL DATA
* ------------------------------------------------------------------- *
*                                                                     *
*              EOV1 RECORD PROCESSING                                 *
*                                                                     *
* ------------------------------------------------------------------- *
EOV10000 DS    0H
         DITTRACE ID=EOV1          TRACE EOV1
         MVC   PRTDATA(L'EOV1MSG),EOV1MSG
         B     LBL0000             PRINT LABEL DATA
* ------------------------------------------------------------------- *
*                                                                     *
*              HDR2 RECORD PROCESSING                                 *
*                                                                     *
* ------------------------------------------------------------------- *
HDR20000 DS    0H
         USING HDR2,R1             DEFINE DSECT BASE
         DITTRACE ID=HDR2          TRACE HDR2
         MVC   PRRF,HDR2RF         RECORD FORMAT
         MVC   PRBLSZ,HDR2BLSZ     BLOCK SIZE
         MVC   PRRSZ,HDR2RSZ       RECORD SIZE
         MVC   PRDEN,HDR2DEN       TAPE DENSITY
         MVC   PRJOB,HDR2JOB       JOB/JOB STEP
         MVC   PRTDATA(HDR2MSGL),HDR2MSG
         B     LBL0000             PRINT LABEL DATA
* ------------------------------------------------------------------- *
*                                                                     *
*              EOF2 RECORD PROCESSING                                 *
*                                                                     *
* ------------------------------------------------------------------- *
EOF20000 DS    0H
         DITTRACE ID=EOF2          TRACE EOF2
         MVC   PRTDATA(L'EOF2MSG),EOF2MSG
         B     LBL0000             PRINT LABEL DATA
* ------------------------------------------------------------------- *
*                                                                     *
*              EOV2 RECORD PROCESSING                                 *
*                                                                     *
* ------------------------------------------------------------------- *
EOV20000 DS    0H
         DITTRACE ID=EOV2          TRACE EOV2
         MVC   PRTDATA(L'EOV2MSG),EOV2MSG
         B     LBL0000             PRINT LABEL DATA
LBL0000  DS    0H
         MVI   PRTCMMD,$PRTDATA    REQUEST PRINT
         MVI   PRTCC,C' '          SINGLE SPACE
         LA    R1,PRTBLOK          PRINT INTERFACE BLOCK
         L     R15,APRT            PRINT MODULE ENTRY POINT
         BALR  R14,R15             LINK TO PRINT MODULE
         B     TAPE0020            READ ANOTHER BLOCK
* ------------------------------------------------------------------- *
*                                                                     *
*              MODULE EXIT POINT                                      *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         DITTRACE ID=EXIT          TRACE EXIT
         L     R13,4(R13)          RESTORE REGISTER 13                  ASE01670
         LM    R14,R12,12(R13)     RESTORE ALL OTHER REGISTERS          ASE01680
         SR    R15,R15             GIVE GOOD RETURN CODE                ASE01690
         BR    R14                 RETURN TO CALLER                     ASE01700
         SPACE 2
* ------------------------------------------------------------------- *
*                                                                     *
*              WORKAREAS/CONSTANTS                                    *
*                                                                     *
* ------------------------------------------------------------------- *
REWCCW   CCW   X'07',*,SILI,1      REWIND CCW
READCCW  CCW   X'02',*,SILI,65535  READ FORWARD CCW
TAPESAVE DC    18F'0'              REGISTER SAVE AREA
H80      DC    H'80'               CONSTANT
TAPEFLAG DC    X'00'               SWITCHES/FLAGS
$TAPETM  EQU   X'80'               .. TAPE MARK JUST READ
$TAPEEOT EQU   X'40'               .. LOGICAL EOT REACHED
$TAPEDTA EQU   X'20'               .. DATA FILE FOUND
$TAPEFHD EQU   X'10'               .. FILE HEADING PRINTED
FILECT   DC    PL3'0'              FILES COUNTER
FULLTAPE DC    CL8'FULLTAPE'
FULL     DC    CL8'FULL'
ALL      DC    CL8'ALL'
TAPEVOL1 DC    C'VOL1'             CONSTANT
TAPEHDR1 DC    C'HDR1'             CONSTANT
TAPEHDR2 DC    C'HDR2'             CONSTANT
TAPEEOF1 DC    C'EOF1'             CONSTANT
TAPEEOF2 DC    C'EOF2'             CONSTANT
TAPEEOV1 DC    C'EOV1'             CONSTANT
TAPEEOV2 DC    C'EOV2'             CONSTANT
HEADMSG  DS    0C
         DC    C'* * * * * * * * * * * * * * * * * PHYSICAL FILE #'
PRFILE   DC    CL6' '
         DC    C' * * * * * * * * * * * * * * * * *'
HEADMSGL EQU   *-HEADMSG
LEOTMSG  DC    C'=============================== LOGICAL END OF TAPE ==+
               ====================='
PEOTMSG  DC    C'=============================== PHYSICAL END OF TAPE =+
               ====================='
DATAMSG  DC    C'DATA'
VOL1MSG  DS    0C
         DC    C'VOL1 VOLSER '
PRVOL    DC    CL6' '
VOL1MSGL EQU   *-VOL1MSG
HDR1MSG  DS    0C
         DC    C'HDR1 DSN='
PRDSN    DC    CL17' '
         DC    C' VOLSEQ='
PRVSEQ   DC    CL4' '
         DC    C' DATASET SEQ='
PRDSEQ   DC    CL4' '
         DC    C' CREATE DATE='
PRCDTE   DC    CL6' '
         DC    C' EXPIRE DATE='
PREXPD   DC    CL6' '
HDR1MSGL EQU   *-HDR1MSG
HDR2MSG  DS    0C
         DC    C'HDR2 RECORD FORMAT='
PRRF     DC    C' '
         DC    C' BLOCK SIZE='
PRBLSZ   DC    CL5' '
         DC    C' RECORD SIZE='
PRRSZ    DC    CL5' '
         DC    C' DENSITY='
PRDEN    DC    C' '
         DC    C' JOB/JOBSTEP='
PRJOB    DC    CL17' '
HDR2MSGL EQU   *-HDR2MSG
EOF1MSG  DS    0C
         DC    C'EOF1 BLOCK COUNT='
PRBLKC   DC    CL6' '
EOF1MSGL EQU   *-EOF1MSG
EOF2MSG  DC    C'EOF2'
EOV1MSG  DC    C'EOV1'
EOV2MSG  DC    C'EOV2'
* ------------------------------------------------------------------- *
*                                                                     *
*              PRINT REQUEST BLOCK                                    *
*                                                                     *
* ------------------------------------------------------------------- *
PRTBLOK  PRTBLOK
* ------------------------------------------------------------------- *
*                                                                     *
*              EXCP INTEFACE BLOCK                                    *
*                                                                     *
* ------------------------------------------------------------------- *
EXCPBLOK EXCPBLOK
         EJECT
* ------------------------------------------------------------------- *
*                                                                     *
*              TAPE LABEL DSECTS                                      *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY  DITTTLBL
         EJECT
* ------------------------------------------------------------------- *
*                                                                     *
*              REGISTER EQUATES                                       *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY REGEQU
SILI     EQU   X'20'
         ABCODES
         EJECT
* ------------------------------------------------------------------- *
*                                                                     *
*              COMMON MODULE DSECT                                    *
*                                                                     *
* ------------------------------------------------------------------- *
DITTCOMM DITTCOMM TYPE=DSECT
         EJECT
* ------------------------------------------------------------------- *
*                                                                     *
*              DYNAMIC BLOCK DSECT                                    *
*                                                                     *
* ------------------------------------------------------------------- *
DYNBLOK  DYNBLOK TYPE=DSECT
         EJECT
* ------------------------------------------------------------------- *
*                                                                     *
*              MVS IOB DSECT                                          *
*                                                                     *
* ------------------------------------------------------------------- *
         IEZIOB
IOBLEN   EQU  *-IOB
* ------------------------------------------------------------------- *
*                                                                     *
*              MVS DCB DSECT                                          *
*                                                                     *
* ------------------------------------------------------------------- *
         DCBD DSORG=PS
DYNDCBL  EQU  *-IHADCB
         END  DITTTMAP
