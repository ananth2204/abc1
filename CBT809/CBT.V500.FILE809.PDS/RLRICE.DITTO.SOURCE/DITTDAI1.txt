*--------------------------------------------------------------------*
*                                                                    *
*              DASD INPUT MODULE                                     *
*                                                                    *
*--------------------------------------------------------------------*
DITTDAI1 DITTPRFX DASDSAVE,'DASD READER MODULE'
         USING DITTCOMM,R11        SPECIFY BASE
         DITTRACE ID=ENTRY         TRACE ENTRY
         TM    COMMFLAG,$COMMCLS   CLOSE FILE INDICATOR??
         BO    DASD9900            YES
         L     R10,COMMIND         DYNAMIC BLOCK ADDRESS
         USING DYNBLOK,R10         SPECIFY BASE
         LA    R9,DYNIOB           IOB WITHIN DYNBLOCK
         USING IOB,R9              DEFINE BASE
         TM    COMMFLAG,$COMM1ST   FIRST PASS??
         BNO   DASD0010            NO
         ST    R10,EXCPDYN         PASS DYNAMIC BLOCK ADRESS TO EXCP
         ZAP   DASDRCDS,COMMP0     ZERO RECORD COUNTER
         XC    DASDCRCD,DASDCRCD   CLEAR CURRENT RECORD ADDRESS
         XC    DASDREM,DASDREM     CLEAR REMAINING BLOCK AMOUNT
         DITTRACE ID=CCWINIT       TRACE INTIALIZE ROUTINE
         MVC   DYNCCW(CCWL),SRCHCCW INITIALIZE CCW STRING
         LA    R1,IOBCC            SEARCH ADDRESS
         STCM  R1,7,DYNCCW1+1      SET SEARCH DATA ADDRESS
         LA    R1,DYNCCW1          SEARCH CCW ADDRESS
         STCM  R1,7,DYNCCW2+1      SET TRANSFER ADDRESS IN TIC CCW
         L     R1,ACOMMIOI         INPUT BUFFER
         STCM  R1,7,DYNCCW3+1      SET DATA BUFFER ADDRESS
         OC    IOBCC(5),IOBCC      HIGH ADDRESS DETERMINED?
         BNZ   DASD0010            YES
*--------------------------------------------------------------------*
*                                                                    *
*        DYNBLOK INITIALIZATION                                      *
*                                                                    *
*     THIS MODULE IS CALLED BY 'DITTDAIR' DURING ALLOCATION OF A     *
*     DASD DEVICE.  THIS PORTION OF PROCESSING DETERMINES THE DASD'S *
*     DEVICE'S MAX CYLINDER AND HEAD NUMBERS.                        *
*                                                                    *
*--------------------------------------------------------------------*
         MVC   IOBCC(5),VTOCPTRA   SET DISK ADDRESS FOR VTOC POINTER
         BAL   R8,DASD0250         READ VTOC POINTER RECORD
         CLI   EXCPSTAT,$EXCPOK    READ SUCCESSFULLY?
         BNE   DASD0400            NO
         L     R1,DASDCRCD         INPUT BUFFER ADDRESS
         USING VTOCPTR,R1          DEFINE VTOC POINTER BASE
         MVC   IOBCC(5),VTOCADDR   SET DISK ADDRESS TO VTOC'S ADDRESS
         DITTRACE ID=READFMT4,     READING FORMAT 4 RECORD             +
               DATA1=IOBCC         .. INCLUDE DISK ADDRESS USED
         BAL   R8,DASD0250         READ FORMAT 4 RECORD
         CLI   EXCPSTAT,$EXCPOK    READ SUCCESSFULLY?
         BNE   DASD0400            NO
         L     R1,DASDCRCD         INPUT BUFFER ADDRESS
         USING FMT4,R1             DEFINE FORMAT 4 RECORD BASE
         MVC   DYNHDADR,DS4DEVSZ   SAVE MAX CYLINDER AND HEAD NUMBERS
         B     DASD9900            'INITIALIZATION' COMPLETE
DASD0010 DS    0H
         LA    R2,DYNDCB           DCB WITHIN DYNBLOK
         USING IHADCB,R2           DEFINE DSECT BASE
         NI    COMMIFLG,255-$NEWBLOK TURN OFF NEW BLOCK INDICATOR
DASD0020 DS    0H
         TM    COMMPFLG,$COMMRCS   RECORD COUNT GIVEN??
         BNO   DASD0030            NO, BYPASS RECORD COUNT CHECK
         CP    DASDRCDS,COMMRCDS   READ ENOUGH RECORDS??
         BE    DASD0530            YES, SIGNAL END OF FILE
DASD0030 DS    0H
         TM    COMMIFLG,$DEBLOCK   DEBLOCKING RECORDS??
         BNO   DASD0100            NO
         DITTRACE ID=NEXTRECD,     CALLED FOR NEXT RECORD,             +
               DATA1=DASDCRCD,     .. CURRENT RECORD'S ADDRESS         +
               DATA2=DASDREM       .. LENGTH REMAINING
         OC    DASDREM,DASDREM     ANY LENGTH REMAINING??
         BZ    DASD0100            NO.. NEED ANOTHER RECORD
         DITTRACE ID=DEBLOCK       LENGTH REMAINING, DEBLOCK RECORD
DASD0040 DS    0H
         ICM   R3,15,DASDCRCD      CURRENT RECORD ADDRESS
         BZ    DASD0460            ERROR
         SR    R2,R2               CLEAR REGISTER
         TM    COMMIFLG,$VREC      VARIABLE LENGTH RECORDS??
         BO    DASD0050            YES
         ICM   R2,3,COMMLRCL       LOGICAL RECORD SIZE
         B     DASD0060            CONTINUE
DASD0050 DS    0H
         ICM   R2,3,0(R3)          RECORD SIZE
DASD0060 DS    0H
         TM    COMMIFLG,$NEWBLOK   NEW BLOCK JUST READ??
         BO    DASD0070            YES, LEAVE CURRENT ADDRESS
         AR    R3,R2               BRING INPUT ADDRESS FORWARD
DASD0070 DS    0H
         TM    COMMIFLG,$VREC      VARIABLE LENGTH RECORDS??
         BNO   DASD0080            NO, RECORD LENGTH WILL BE THE SAME
         ICM   R2,3,0(R3)          LENGTH OF NEW RECORD
         BZ    DASD0420         ...RECORD LENGTH ZERO.. INVALID
DASD0080 DS    0H
         CLM   R2,3,DASDREM        LONGER THAN REMAINING LENGTH??
         BNH   DASD0090            NO
         ICM   R2,3,DASDREM        OTHERWISE LIMIT LENGTH
DASD0090 DS    0H
         ST    R3,DASDCRCD         SAVE CURRENT RECORD ADDRESS
         ST    R3,COMMCRCD         PASS CURRENT RECORD ADDRESS
         STH   R2,COMMCRCL         PASS CURRENT RECORD LENGTH
         DITTRACE ID=LREC,         RECORD ADDRESS AND LENGTH           +
               RDATA1=R3,          .. LOGICAL RECORD'S ADDRESS         +
               RDATA2=R2           .. LOGICAL RECORD'S LENGTH
         SR    R3,R3               CLEAR REGISTER
         ICM   R3,3,DASDREM        REMAINING SIZE
         SR    R3,R2               COMPUTE REMAINING SIZE
         STCM  R3,3,DASDREM        SAVE REMAINING SIZE
         AP    DASDRCDS,COMMP1     ADD 1 TO RECORD COUNT
         B     DASD9900            EXIT
DASD0100 DS    0H
         DITTRACE ID=NEXTBLOK      READ NEXT PHYSICAL RECORD
         BAL   R8,DASD0200         LINK TO I/O
         TM    COMMIFLG,$DEBLOCK   DEBLOCKING RECORDS??
         BNO   DASD0130            NO
         TM    COMMIFLG,$VREC      VARIABLE LENGTH RECORDS?
         BNO   DASD0040            NO
         SR    R0,R0               CLEAR REGISTER
         L     R3,DASDCRCD         BLOCK ADDRESS
         ICM   R0,3,0(R3)          LENGTH FROM BDW (MAYBE)
         SH    R0,H4               MINUS BDW LENGTH
         BNH   DASD0040            INSUFFICIENT LENGTH
         LA    R3,4(R3)            FIRST RDW IN THE BLOCK
         SR    R2,R2               CLEAR REGISTER
DASD0110 DS    0H
         ICM   R2,3,0(R3)          RECORD LENGTH FROM RDW
         BZ    DASD0420            RECORD WITH LENGTH = ZERO!
         SR    R0,R2               MINUS LENGTH OF THIS RECORD
         BM    DASD0040            NEGATIVE... LENGTHS DON'T ADD UP
         BZ    DASD0120            ZERO.. LENGTHS MATCH
         AR    R3,R2               NEXT RDW ADDRESS
         B     DASD0110            LOOP
DASD0120 DS    0H
         L     R3,DASDCRCD         BLOCK ADDRESS
         ICM   R2,3,0(R3)          BLOCK LENGTH
         SH    R2,H4               MINUS BDW LENGTH
         STCM  R2,3,DASDREM        SET LENGTH REMAINING
         LA    R3,4(R3)            FIRST LOGICAL RECORD'S ADDRESS
         ST    R3,DASDCRCD         SET RECORD ADDRESS
         B     DASD0040            DEBLOCK FIRST RECORD
DASD0130 DS    0H
         DITTRACE ID=PHYRECD,      PHYSICAL BLOCK                      +
               DATA1=DASDCRCD,     .. PHYSICAL BLOCK ADDRESS           +
               DATA2=DASDREM       .. PHYSICAL BLOCK SIZE
         MVC   COMMCRCD,DASDCRCD   PASS RECORD ADDRESS
         MVC   COMMCRCL,DASDREM    PASS RECORD LENGTH
         AP    DASDRCDS,COMMP1     ADD 1 TO RECORD COUNT
         B     DASD9900            EXIT
*---------------------------------------------------------------------*
*                                                                     *
*                    LINK TO EXCP FOR ACTUAL I/O                      *
*                                                                     *
*---------------------------------------------------------------------*
DASD0200 DS    0H
         TM    COMMFLAG,$COMM1ST   FIRST PASS?
         BNO   DASD0210            NO
         MVC   IOBCC(5),COMMDADR   SET DISK ADDRESS
         B     DASD0240            SKIP DISK ADDRESS UPDATE
DASD0210 DS    0H
         SR    R1,R1               CLEAR REGISTER
         IC    R1,IOBR             RECORD NUMBER
         LA    R1,1(R1)            NEXT RECORD
         STC   R1,IOBR             SAVE RECORD NUMBER
         DITTRACE ID=NEWREC,DATA1=IOBCC
         MVC   COMMDADR,IOBCC      SAVE DISK ADDRESS
         B     DASD0240            READ WITH NEW RECORD NUMBER
DASD0220 DS    0H
         SR    R1,R1               CLEAR REGISTER
         ICM   R1,3,IOBHH          CURRENT HEAD NUMBER
         LA    R1,1(R1)            NEXT HEAD
         STCM  R1,3,IOBHH          SAVE HEAD NUMBER
         MVI   IOBR,1              START OVER AT RECORD 1
         DITTRACE ID=NEWHEAD,DATA1=IOBCC
         MVC   COMMDADR,IOBCC      SAVE DISK ADDRESS
         CLM   R1,3,DYNHHEAD       TIME FOR NEW CYLINDER?
         BL    DASD0240            NO
DASD0230 DS    0H
         SR    R1,R1               CLEAR REGISTER
         ICM   R1,3,IOBCC          CURRENT CYLINDER NUMBER
         LA    R1,1(R1)            NEXT CYLINDER
         STCM  R1,3,IOBCC          SAVE CYLINDER NUMBER
         XC    IOBHH,IOBHH         SET HEAD TO ZERO
         MVI   IOBR,1              START OVER AT RECORD 1
         DITTRACE ID=NEWCYL,DATA1=IOBCC
         MVC   COMMDADR,IOBCC      SAVE DISK ADDRESS
         CLM   R1,3,DYNHCYL        BEYOND MAX CYLINDER?
         BNL   DASD0500            YES.. WHOA!
DASD0240 DS    0H
         CLC   DYNHCYL,IOBCC       INVALID CYLINDER
         BL    DASD0430            YES
         CLC   DYNHHEAD,IOBHH      INVALID DISK ADDRESS?
         BL    DASD0430            YES
DASD0250 DS    0H
         SR    R1,R1               CLEAR REGISTER
         IC    R1,IOBR             .. SUBTRACT 1 FROM RECORD NUMBER
         BCTR  R1,0                   BECAUSE WE ALWAYS DO A READ
         STC   R1,IOBR                COUNT, KEY, AND DATA
         DITTRACE ID=DASDIO,       TRACE I/O'S                         +
               DATA1=IOBCC         .. DISK ADDRESS USED FOR I/O
         XC    COMMKEY,COMMKEY     CLEAR KEY'S ADDRESS
         LA    R1,EXCPBLOK         EXCP INTERFACE PARAMETER LIST
         L     R15,AEXCP           EXCP MODULE ENTRY POINT
         BALR  R14,R15             LINK TO EXCP INTERFACE
         SR    R1,R1               CLEAR REGISTER
         IC    R1,IOBR             .. ADD 1 BACK TO RECORD NUMBER
         LA    R1,1(R1)               BECAUSE WE ALWAYS DO A READ
         STC   R1,IOBR                COUNT, KEY, AND DATA
         CLI   EXCPSTAT,$EXCPRNF   RECORD NOT FOUND?
         BE    DASD0220            YES, TRY NEXT HEAD
         L     R1,ACOMMIOI         I/O AREA ADDRESS
         MVC   COMMCNT,0(R1)       MOVE CKD COUNT INFO TO COMM
         CLI   EXCPSTAT,$EXCPOK    I/O SUCCESSFUL?
         BNE   DASD0400            NO..
         CLC   CNTADDR,IOBCC       DID I READ THE RECORD I WANTED?
         BL    DASD0220            NO.. TRACK WRAPPED
         OI    COMMIFLG,$NEWBLOK   TURN ON NEW BLOCK INDICATOR
         MVC   DASDREM,CNTDATAL    SET DATA LENGTH
         LA    R1,COMMCNTL(R1)     COUNT IS NOT PART OF RECORD'S DATA
         SR    R0,R0               CLEAR REG 0
         ICM   R0,1,CNTKEYL        KEY LENGTH
         BZ    DASD0260            NO KEY
         DITTRACE ID=KEYADDR,      KEYED RECORD                        +
               RDATA1=R1
         ST    R1,COMMKEY          SET KEY'S ADDRESS
         AR    R1,R0               PLUS KEY'S LENGTH
DASD0260 DS    0H
         DITTRACE ID=BLOKADDR,     PHYSICAL DATA'S ADDRESS             +
               RDATA1=R1,          .. DATA ADDRESS                     +
               DATA2=DASDREM       .. REMAINING LENGTH
         ST    R1,DASDCRCD         SET DATA'S ADDRESS
         BR    R8                  RETURN
*---------------------------------------------------------------------*
*                                                                     *
*                    ERROR ROUTINES                                   *
*                                                                     *
*---------------------------------------------------------------------*
DASD0400 DS    0H
         CLC   CNTADDR,IOBCC       RECORD ADDRESS PRESENT IN I/O AREA?
         BNE   DASD0410            NO
         OC    CNTL,CNTL           KEY LENGTH AND DATA LENGTH = ZERO?
         BZ    DASD0520            YES.. EOF RECORD
DASD0410 DS    0H
         LA    R3,DYNIOB           IOB USED FOR I/O
         DITTRACE ID=READERR       TRACE READ ERRORS
         ABEND ABEND018,DUMP,,USER
DASD0420 DS    0H
         DITTRACE ID=ZERORECL      TRACE LRECL = ZERO
         ABEND ABEND019,DUMP,,USER
DASD0430 DS    0H
         DITTRACE ID=BADADDR       BAD DISK ADDRESS
         TM    COMMFLAG,$COMM1ST   FIRST PASS?
         BO    DASD0440            YES... USER ENTERED A BAD ADDRESS
         ABEND ABEND020,DUMP,,USER
DASD0440 DS    0H
         DITTRACE ID=USERBAD       BAD DISK ADDRESS
         OI    COMMFLAG,$ABORT     SET ABORT FLAG
         CLI   COMMENV,$ENVTSO     TSO ENVIRONMENT?
         BE    DASD0450            YES
         MVC   PRTDATA(DASDM02L),DASDM02
         MVI   PRTCMMD,$PRTCMD     PRINT COMMAND
         LA    R1,PRTBLOK          PRINT PARAMETER BLOCK
         L     R15,APRT            PRINT ENTRY POINT
         BALR  R14,R15             PRINT BAD ADDRESS MESSAGE
         CLI   COMMENV,$ENVJOB     BATCH ENVIRONMENT?
         BE    DASD9900            YES EXIT
         WTO   'INVALID DISK ADDRESS (CYLINDER OR HEAD NUMBER TOO HIGH)+
               ',CONSNAME=COMMCNAM
         B     DASD9900            AND EXIT
DASD0450 DS    0H
         MVC   MSG01(DASDM02L),DASDM02
         B     DASD9900            AND EXIT
DASD0460 DS    0H
         DITTRACE ID=ZEROCRCD      CURRENT RECORD ADDRESS WAS ZERO
         ABEND ABEND024,DUMP,,USER
*---------------------------------------------------------------------*
*                                                                     *
*                                                                     *
*                                                                     *
*---------------------------------------------------------------------*
DASD0500 DS    0H
         DITTRACE ID=EOV           END OF VOLUME
         CLI   COMMENV,$ENVTSO     TSO ENVIRONMENT?
         BE    DASD0510            YES
         MVC   PRTDATA(DASDM01L),DASDM01
         MVI   PRTCMMD,$PRTDATA    PRINT COMMAND
         LA    R1,PRTBLOK          PRINT PARAMETER BLOCK
         L     R15,APRT            PRINT ENTRY POINT
         BALR  R14,R15             PRINT EOV MESSAGE
         CLI   COMMENV,$ENVSTC     STC ENVIRONMENT?
         BNE   DASD0530            NO
         WTO   'END OF VOLUME REACHED, COMMAND HALTED',                +
               CONSNAME=COMMCNAM   .. CONSOLE ID
         B     DASD0530            SET EOF AND EXIT
DASD0510 DS    0H
         MVC   MSG01(DASDM01L),DASDM01
         B     DASD0530
DASD0520 DS    0H
         DITTRACE ID=EOFRECD
         MVI   DYNSTAT,$DYNEOF     SET EOF RECORD READ
         TM    COMMPFLG,$COMMRCS   'RECORDS' IN EFFECT?
         BNO   DASD0530            NO
         DITTRACE ID=FORCEOF       EOF FORCED BY EOF RECORD
         OI    COMMFLAG,$COMMFE    EOF FORCED BY EOF RECORD
DASD0530 DS    0H
         DITTRACE ID=EOF
         OI    COMMFLAG,$COMMEOF   SIGNAL EOF
*--------------------------------------------------------------------*
*                                                                    *
*              MODULE EXIT POINT                                     *
*                                                                    *
*--------------------------------------------------------------------*
DASD9900 DS    0H
         DITTRACE ID=EXIT          TRACE EXIT
         L     R13,4(R13)          RESTORE REGISTER 13                  ASE01670
         LM    R14,R12,12(R13)     RESTORE ALL OTHER REGISTERS          ASE01680
         SR    R15,R15             GIVE GOOD RETURN CODE                ASE01690
         BR    R14                 RETURN TO CALLER                     ASE01700
         SPACE 2
*--------------------------------------------------------------------*
*                                                                    *
*              WORK AREAS/CONSTANTS                                  *
*                                                                    *
*--------------------------------------------------------------------*
SRCHCCW  CCW   X'31',*,CC,5        SEARCH FOR RECORD BY RECORD ID
TICCCW   CCW   X'08',SRCHCCW,0,8   TRANSFER IN CHANNEL
READCCW  CCW   X'1E',*,SILI,65535  READ COUNT, KEY, AND DATA
CCWL     EQU   *-SRCHCCW           CCW STRING LENGTH
DASDSAVE DC    18F'0'              REGISTER SAVE AREA
DASDCRCD DC    A(0)                CURRENT RECORD ADDRESS
H4       DC    H'4'                CONSTANT
DASDREM  DC    H'0'                LENGTH REMAINING IN RECORD
DASDRCDS DC    PL5'0'              RECORD COUNTER
VTOCPTRA DC    X'0000000003'       VTOC POINTER DISK ADDRESS
DASDM01  DC    C'END OF VOLUME REACHED, COMMAND HALTED'
DASDM01L EQU   *-DASDM01
DASDM02  DC    C'DISK ADDRESS INVALID (CYLINDER OR HEAD NUMBER TOO HIGH+
               )'
DASDM02L EQU   *-DASDM02
         LTORG
*--------------------------------------------------------------------*
*                                                                    *
*              EXCP INTERFACE BLOCK                                  *
*                                                                    *
*--------------------------------------------------------------------*
EXCPBLOK EXCPBLOK
*--------------------------------------------------------------------*
*                                                                    *
*              PRINT MODULE INTERFACE                                *
*                                                                    *
*--------------------------------------------------------------------*
PRTBLOK  PRTBLOK
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              REGISTER EQUATES                                      *
*                                                                    *
*--------------------------------------------------------------------*
         COPY  REGEQU
SILI     EQU   X'20'
CC       EQU   X'40'
         ABCODES
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              COMMON MODULE DSECT                                   *
*                                                                    *
*--------------------------------------------------------------------*
DITTCOMM DITTCOMM TYPE=DSECT
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              DYNAMIC BLOCK DSECT                                   *
*                                                                    *
*--------------------------------------------------------------------*
DYNBLOK  DYNBLOK TYPE=DSECT
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              MVS IOB DSECT                                         *
*                                                                    *
*--------------------------------------------------------------------*
         IEZIOB
IOBLEN   EQU  *-IOB
*--------------------------------------------------------------------*
*                                                                    *
*              MVS DCB DSECT                                         *
*                                                                    *
*--------------------------------------------------------------------*
         DCBD DSORG=PS
DYNDCBL  EQU   *-IHADCB
*--------------------------------------------------------------------*
*                                                                    *
*              VTOC POINTER RECORD                                   *
*                                                                    *
*--------------------------------------------------------------------*
VTOCPTR  DSECT
VTOCVOL1 DS   CL4                VOL1 RECORD IDENTIFIER 'VOL1'
VTOCSER  DS   CL6                VOLSER
         DS   X
VTOCADDR DS   XL5                VTOC'S DISK ADDRESS
         DS   CL64
*--------------------------------------------------------------------*
*                                                                    *
*              FORMAT 4 LABEL                                        *
*                                                                    *
*--------------------------------------------------------------------*
FMT4     DSECT
         IECSDSL1  4             GENERATE FORMAT 4 RECORD MAP
         END  DITTDAI1
