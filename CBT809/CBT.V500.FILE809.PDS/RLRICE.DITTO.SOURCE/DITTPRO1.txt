* ------------------------------------------------------------------- *
*                                                                     *
*            DATA PRINT                                               *
*                                                                     *
*       THIS IS MODULE PRODUCES THE STANDARD OR VERTICAL HEX PRINT    *
*       OUTPUT.  IT IS ALWAYS LINKED TO FROM 'DITTMAIN' AS AN OUTPUT  *
*       MODULE.  THE PRINT LINES ARE BUILT AND PASSED TO 'DITTPRT'    *
*       TO DO THE ACTUAL PRINTING.                                    *
*                                                                     *
* ------------------------------------------------------------------- *
DITTPRO1 DITTPRFX PRTSAVE,'DATA PRINT'
         USING DITTCOMM,R11        ADDRESS IT
         DITTRACE ID=ENTRY         TRACE ENTRY
PRT0010  DS    0H
         TM    COMMFLAG,$COMMEOF   EOF FLAG ON?
         BO    PRT0070             YES
         DITTRACE ID=RECORD        TRACE STARTING RECORD PRINT
         ICM   R10,15,COMMIND      INPUT DYNAMIC BLOCK ADDRESS
         BZ    PRT0030             NO DYNAMIC BLOCK...
         USING DYNBLOK,R10         DEFINE BASE
         CLC   PRTDASD,DYNDDNAM    INPUT FROM DASD?
         BNE   PRT0030             NO
         TM    COMMIFLG,$NEWBLOK   NEW BLOCK JUST READ?
         BNO   PRT0030             NO
         MVC   DASDVOL,DYNVOL      COPY VOLSER
         SR    R1,R1               CLEAR REGISTER
         IC    R1,CNTREC           RECORD NUMBER FROM 'COUNT'
         CVD   R1,COMMDWRD         CONVERT TO DECIMAL
         MVC   EDWORK,EDWORD2      INTIALIZE WITH EDIT WORD
         ED    EDWORK,COMMDWRD+5   EDIT RECORD NUMBER
         MVC   DASDREC,EDWORK+3    COPY RECORD NUMBER
         IC    R1,CNTKEYL          KEY LENGTH FROM 'COUNT'
         CVD   R1,COMMDWRD         CONVERT TO DECIMAL
         MVC   EDWORK,EDWORD3      INITIALIZE WITH EDIT WORD
         ED    EDWORK,COMMDWRD+5   EDIT KEY LENGTH
         MVC   DASDKL,EDWORK+3     COPY KEY LENGTH
         ICM   R1,3,CNTHEAD        HEAD NUMBER FROM 'COUNT'
         CVD   R1,COMMDWRD         CONVERT TO DECIMAL
         MVC   EDWORK,EDWORD2      INTIALIZE WITH EDIT WORD
         ED    EDWORK,COMMDWRD+5   EDIT HEAD NUMBER
         MVC   DASDHEAD,EDWORK+4   COPY RECORD NUMBER
         ICM   R1,3,CNTCYL         CYLINDER NUMBER FROM 'COUNT'
         CVD   R1,COMMDWRD         CONVERT TO DECIMAL
         MVC   EDWORK,EDWORD2      INTIALIZE WITH EDIT WORD
         ED    EDWORK,COMMDWRD+5   EDIT CYLINDER NUMBER
         MVC   DASDCYL,EDWORK+2    COPY CYLINDER NUMBER
         ICM   R1,3,CNTDATAL       DATA LENGTH FROM 'COUNT'
         CVD   R1,COMMDWRD         CONVERT TO DECIMAL
         MVC   EDWORK,EDWORD3      INTIALIZE WITH EDIT WORD
         ED    EDWORK,COMMDWRD+5   EDIT DATA LENGTH
         MVC   DASDDL,EDWORK+1     COPY DATA LENGTH
         MVI   PRTCC,C'-'          TRIPLE SPACE
         MVC   PRTAREA(DASDMSGL),DASDMSG
         BAL   R8,PRT0100          PRINT DASD ADDRESS MESSAGE
         BAL   R8,PRT0100          PRINT BLANK LINE
         SR    R4,R4               CLEAR REGISTER
         ICM   R4,1,CNTKEYL        PICK UP KEY LENGTH
         BZ    PRT0020             NO KEY
         L     R3,COMMKEY          KEY'S ADDRESS
         MVC   PRTAREA1,KEYHEAD    MOVE 'KEY' TO LINE HEADING1 AREA
         MVC   PRTAREA2,COMMBLKS   CLEAR LINE HEADING 2
         ZAP   PRT100,P0           RESET SCALE OFFSET COUNTER
         BAL   R9,PRT0040          PRINT KEY
         BAL   R8,PRT0100          PRINT BLANK LINE
PRT0020  DS    0H
         TM    COMMIFLG,$DEBLOCK   IS INPUT DEBLOCKING RECORDS?
         BO    PRT0030             YES.. PRINT LOGICAL RECORD
         L     R3,COMMCRCD         DATA'S ADDRESS
         SR    R4,R4               CLEAR REGISTER
         ICM   R4,3,CNTDATAL       PICK UP DATA LENGTH
         BZ    PRT9900             ALL DONE
         MVC   PRTAREA1,DATAHEAD   MOVE 'DATA' TO LINE HEADING 1 AREA
         MVC   PRTAREA2,COMMBLKS   CLEAR LINE HEADING 2
         ZAP   PRT100,P0           RESET SCALE OFFSET COUNTER
         BAL   R9,PRT0040          PRINT DATA
         B     PRT9900             AND EXIT
PRT0030  DS    0H
         L     R3,COMMCRCD         DATA'S ADDRESS
         SR    R4,R4               CLEAR REGISTER
         ICM   R4,3,COMMCRCL       CURRENT RECORD LENGTH
         MVC   PRTAREA,PRTAREA-1   INITIALIZE PRINT LINE
         MVC   PRTAREA1,=C'RECORD SIZE '
         CVD   R4,COMMDWRD         CONVERT RECORD LENGTH TO DECIMAL
         MVC   PRTAREA2,EDWORD1    INITIALIZE WITH EDIT WORD
         ED    PRTAREA2,COMMDWRD+5 EDIT RECORD LENGTH
         ZAP   PRT100,P0           RESET SCALE OFFSET COUNTER
         BAL   R9,PRT0040          PRINT DATA
         B     PRT9900             AND EXIT
* ------------------------------------------------------------------- *
*                                                                     *
*        DATA PRINTING.  AT ENTRY THIS CODE EXPECTS R3 TO BE THE      *
*        DATA ADDRESS, R4 TO BE THE DATA LENGTH, AND R9 TO BE THE     *
*        RETURN ADDRESS.                                              *
*                                                                     *
*        R1, R2, R3, R4, R14, AND R15 WILL BE MODIFIED                *
*                                                                     *
* ------------------------------------------------------------------- *
PRT0040  DS    0H
         DITTRACE ID=PRT0040,      TRACE POINT                         +
               RDATA1=R3,          .. DATA ADDRESS                     +
               RDATA2=R4           .. DATA LENGTH
         LR    R2,R4               COPY RECORD LENGTH
         CH    R2,H100             TOO MUCH FOR 1 LINE?
         BNH   PRT0050             NO, CONTINUE
         LH    R2,H100             LIMIT LENGTH TO 100
PRT0050  DS    0H
         DITTRACE ID=PRT0050,      TRACE POINT                         +
               RDATA1=R11,         .. COMM AREA ADDRESS                +
               RDATA2=R12          .. BASE ADDRESS
         BCTR  R2,0                ADJUST FOR EXECUTES
         EX    R2,CHARMVC          MOVE DATA TO PRINT AREA 3
         EX    R2,PRTTR            TRANSLATE OUT UNPRINTABLES
         BAL   R8,PRT0100          PRINT
         TM    COMMOFLG,$HEX       PRINTING IN HEX?
         BNO   PRT0060             NO
         EX    R2,CHARMVC          MOVE DATA TO PRINT AREA 3
         EX    R2,ZONEMVC          PREPARE FOR ZONES
         EX    R2,HEXCHAR          TRANSLATE HEX TO CHARACTER
         BAL   R8,PRT0100          PRINT
         EX    R2,CHARMVC          MOVE DATA TO PRINT AREA 3
         EX    R2,NUMRMVC          PREPARE FOR NUMERICS
         EX    R2,HEXCHAR          TRANSLATE HEX TO CHARACTER
         BAL   R8,PRT0100          PRINT
         EX    R2,SCALEMVC         MOVE SCALE
         MVC   PRTPR100,EDWORD4    INITIALIZE WITH EDIT WORD
         ED    PRTPR100,PRT100     EDIT SCALE OFFSET
         BAL   R8,PRT0100          PRINT
         BAL   R8,PRT0100          PRINT BLANK LINE
         AP    PRT100,P10          ADD TO SCALE OFFSET
PRT0060  DS    0H
         LA    R2,1(R2)            RESET LENGTH
         AR    R3,R2               BRING INPUT ADDRESS FORWARD
         SR    R4,R2               COMPUTE LENGTH REMAINING
         BNZ   PRT0040             REPEAT 'TILL ALL LENGTH DONE
         BR    R9                  RETURN
         EJECT
* ------------------------------------------------------------------- *
*                                                                     *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
PRT0070  DS    0H
         DITTRACE ID=EOF
         B     PRT9900             AND EXIT
* ------------------------------------------------------------------- *
*                                                                     *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
PRT0100  DS    0H
         MVI   PRTCMMD,$PRTDATA    INSERT PRINT COMMAND
         MVC   PRTDATA,PRTAREA     MOVE DATA TO PRINT
         LA    R1,PRTBLOK          PRINT INTERFACE BLOCK
         L     R15,APRT            PRINT MODULE ENTRY POINT
         BALR  R14,R15             LINK TO PRINT MODULE
         MVC   PRTAREA,PRTAREA-1   INITIALIZE PRINT LINE
         BR    R8                  RETURN
* ------------------------------------------------------------------- *
*                                                                     *
*              MODULE EXIT POINT                                      *
*                                                                     *
* ------------------------------------------------------------------- *
PRT9900  DS    0H
         DITTRACE ID=EXIT          TRACE EXIT
         L     R13,4(R13)          RESTORE REGISTER 13                  ASE01670
         LM    R14,R12,12(R13)     RESTORE ALL OTHER REGISTERS          ASE01680
         SR    R15,R15             GIVE GOOD RETURN CODE                ASE01690
         BR    R14                 RETURN TO CALLER                     ASE01700
         EJECT
* ------------------------------------------------------------------- *
*                                                                     *
*              EXECUTED INSTRUCTIONS                                  *
*                                                                     *
* ------------------------------------------------------------------- *
CHARMVC  MVC   PRTAREA3(0),0(R3)   MOVE DATA TO PRINT AREA
PRTTR    TR    PRTAREA3(0),CHARTBL TRANSLATE OUT UNPRINTABLES
ZONEMVC  MVN   PRTAREA3(0),HEX0000 PREPARE FOR ZONE TRANSLATE
NUMRMVC  MVZ   PRTAREA3(0),HEX0000 PREPARE FOR NUMERIC TRANSLATE
HEXCHAR  TR    PRTAREA3(0),HEXTBL  TRANSLATE HEX TO CHARACTER
SCALEMVC MVC   PRTAREA3(0),SCALE   MOVE SCALE
         SPACE 2
* ------------------------------------------------------------------- *
*                                                                     *
*              PRINTABLE CHARACTERS TABLE                             *
*                                                                     *
* ------------------------------------------------------------------- *
*                   0 1 2 3 4 5 6 7 8 9 A B C D E F                     DUT09680
CHARTBL  DC    XL16'40404040404040404040404040404040'     X'00' - X'0F' DUT09690
         DC    XL16'40404040404040404040404040404040'     X'10' - X'1F' DUT09700
         DC    XL16'40404040404040404040404040404040'     X'20' - X'2F' DUT09710
         DC    XL16'40404040404040404040404040404040'     X'30' - X'3F' DUT09720
         DC    XL16'404040404040404040404A4B4C4D4E4F'     X'40' - X'4F' DUT09730
         DC    XL16'504040404040404040405A5B5C5D5E5F'     X'50' - X'5F' DUT09740
         DC    XL16'60614040404040404040406B6C6D6E6F'     X'60' - X'6F' DUT09750
         DC    XL16'404040404040404040407A7B7C7D7E7F'     X'70' - X'7F' DUT09760
         DC    XL16'40404040404040404040408B8C8D8E8F'     X'80' - X'8F' DUT09770
         DC    XL16'40404040404040404040409B9C9D9E9F'     X'90' - X'9F' DUT09780
         DC    XL16'4040404040404040404040ABACADAEAF'     X'A0' - X'AF' DUT09790
         DC    XL16'4040404040404040404040BBBCBDBEBF'     X'B0' - X'BF' DUT09800
         DC    XL16'40C1C2C3C4C5C6C7C8C9404040404040'     X'C0' - X'CF' DUT09810
         DC    XL16'40D1D2D3D4D5D6D7D8D9404040404040'     X'D0' - X'DF' DUT09820
         DC    XL16'4040E2E3E4E5E6E7E8E9404040404040'     X'E0' - X'EF' DUT09830
         DC    XL16'F0F1F2F3F4F5F6F7F8F9404040404040'     X'F0' - X'FF' DUT09840
*                                                                       DUT09680
HEXTBL   DC    CL16'0123456789ABCDEF'                    X'00' - X'0F'  DUT09690
         DC    CL16'1               '                    X'10' - X'1F'  DUT09690
         DC    CL16'2               '                    X'20' - X'2F'  DUT09690
         DC    CL16'3               '                    X'30' - X'3F'  DUT09690
         DC    CL16'4               '                    X'40' - X'4F'  DUT09690
         DC    CL16'5               '                    X'50' - X'5F'  DUT09690
         DC    CL16'6               '                    X'60' - X'6F'  DUT09690
         DC    CL16'7               '                    X'70' - X'7F'  DUT09690
         DC    CL16'8               '                    X'80' - X'8F'  DUT09690
         DC    CL16'9               '                    X'90' - X'9F'  DUT09690
         DC    CL16'A               '                    X'A0' - X'AF'  DUT09690
         DC    CL16'B               '                    X'B0' - X'BF'  DUT09690
         DC    CL16'C               '                    X'C0' - X'CF'  DUT09690
         DC    CL16'D               '                    X'D0' - X'DF'  DUT09690
         DC    CL16'E               '                    X'E0' - X'EF'  DUT09690
         DC    CL16'F               '                    X'F0' - X'FF'  DUT09690
         SPACE 2
* ------------------------------------------------------------------- *
*                                                                     *
*              PRINT MODULE INTERCOMMUNICATION                        *
*                                                                     *
* ------------------------------------------------------------------- *
PRTBLOK  PRTBLOK
         SPACE 2
* ------------------------------------------------------------------- *
*                                                                     *
*              PRINT MODULE WORK AREAS                                *
*                                                                     *
* ------------------------------------------------------------------- *
PRTSAVE  DC    18F'0'              REGISTER SAVE AREA
H100     DC    H'100'              CONSTANT
PRTFLAG  DC    X'00'               FLAGS/SWITCHES
HEX0000  DC    100X'00'
EDWORD1  DC    X'4020206B202120'   CONSTANT
EDWORD2  DC    X'F02020202020'     CONSTANT
EDWORD3  DC    X'402020202120'     CONSTANT
EDWORD4  DC    X'402020202020'     CONSTANT
PRT100   DC    PL3'0'              SCALE OFFSET COUNTER
P0       DC    P'0'                CONSTANT
P10      DC    P'10'               CONSTANT
PRTDASD  DC    C'DASD'             CONSTANT
KEYHEAD  DC    CL12'KEY'
DATAHEAD DC    CL12'DATA'
EDWORK   DC    CL6' '
         DC    C' '
PRTAREA  DS    0CL121              PRINT LINE
PRTAREA1 DC    CL12' '             COMMENT AREA
PRTAREA2 DC    CL7' '              RECORD SIZE AREA
         DC    C' '                SPACE
         ORG   *-6
PRTPR100 DC    CL6' '
PRTAREA3 DC    100C' '
DASDMSG  DS    0C
         DC    C' VOLSER '
DASDVOL  DC    CL6' '
         DC    C'   DASD ADDRESS:'
DASDCYL  DC    CL4' '
         DC    C'-'
DASDHEAD DC    CL2' '
         DC    C'-'
DASDREC  DC    CL3' '
         DC    C'   KEY LENGTH='
DASDKL   DC    CL4' '
         DC    C'   DATA LENGTH='
DASDDL   DC    CL5' '
DASDMSGL EQU   *-DASDMSG
SCALE    DC    C'1...+...10....+...20....+...30....+...40....+...50....+
               +...60....+...70....+...80....+...90....+..100'
         SPACE 2
* ------------------------------------------------------------------- *
*                                                                     *
*              REGISTER EQUATES                                       *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY  REGEQU
         EJECT
* ------------------------------------------------------------------- *
*                                                                     *
*              COMMON MODULE DSECT                                    *
*                                                                     *
* ------------------------------------------------------------------- *
DITTCOMM DITTCOMM TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
*              DYNAMIC BLOCK DSECT                                    *
*                                                                     *
* ------------------------------------------------------------------- *
DYNBLOK  DYNBLOK TYPE=SHORT
         END  DITTPRO1
