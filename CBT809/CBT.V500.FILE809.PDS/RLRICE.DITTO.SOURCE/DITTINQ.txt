* ------------------------------------------------------------------- *
*                                                                     *
*              STC MODE INQUIRY                                       *
*                                                                     *
*    WHEN RUNNING IN STC MODE, THIS MODULE PROVIDES THE OPERATOR      *
*    WITH A WAY TO DISPLAY ALLOCATED TAPES, DASD'S, AND THE EXTERNAL  *
*    TRACE STATUS.                                                    *
*                                                                     *
* ------------------------------------------------------------------- *
DITTINQ  DITTPRFX INQSAVE,'STC MODE INQUIRE FUNCTION'
         USING DITTCOMM,R11        SPECIFY BASE
         DITTRACE ID=ENTRY,        TRACE ENTRY                         +
               DATA1=COMMINQ       .. INQUIRE PARAMETER
         TM    COMMFLAG,$COMMCLS   CLOSE-UP CALL?
         BO    INQ9910             YES.. JUST EXIT
         CLC   INQTAPE,COMMINQ     DISPLAY TAPES?
         BE    INQ0010             YES
         CLC   INQDASD,COMMINQ     DISPLAY DASD'S?
         BE    INQ0100             YES
         CLC   INQTRACE,COMMINQ    DISPLAY TRACE STATUS?
         BE    INQ0200             YES
         LA    R8,INQM01           INVALID INQUIRE REQUEST
         BAL   R9,WTO0000          DISPLAY MESSAGE AND PRINT IT
         B     INQ9900             AND EXIT
* ------------------------------------------------------------------- *
*                                                                     *
*              LIST ALLOCATED TAPE DRIVES                             *
*                                                                     *
* ------------------------------------------------------------------- *
INQ0010  DS    0H
         DITTRACE ID=TAPEINQ
         NI    INQFLAG,255-$INQFND RESET 'FOUND' FLAG
         ICM   R2,15,COMMDYHD      FIRST DYNAMIC BLOCK
         USING DYNBLOK,R2          DEFINE BASE
         BZ    INQ0300             NOTHING ON THE CHAIN
INQ0020  DS    0H
         CLC   INQTAPE,DYNDDNAM    IS THIS A TAPE?
         BNE   INQ0070             NO
         OI    INQFLAG,$INQFND     SET FOUND FLAG
         MVC   INQM02UNT,DYNDDNAM+4 COPY DEVICE NUMBER
         L     R1,DYNDEBA          DEB ADDRESS FROM DYNAMIC BLOCK
         USING DEB,R1              DEFINE BASE
         CLI   DEBSDVM,$DEN1600    1600 BPI?
         BE    INQ0030             YES
         CLI   DEBSDVM,$DEN6250    6250 BPI?
         BE    INQ0040             YES
         CLI   DEBSDVM,$DEN3480    3480 DRIVE?
         BE    INQ0050             YES
         MVC   INQM02TYP,INQUNKN   UNKNOWN DENSITY OR TYPE
         MVC   INQM02BPI,COMMBLKS  SET 'BPI' TO BLANKS
         B     INQ0060
INQ0030  DS    0H
         MVC   INQM02TYP,INQ1600   SET TYPE TO 1600
         MVC   INQM02BPI,INQBPI    SET 'BPI' IN MESSAGE
         B     INQ0060
INQ0040  DS    0H
         MVC   INQM02TYP,INQ6250   SET TYPE TO 6250
         MVC   INQM02BPI,INQBPI    SET 'BPI' IN MESSAGE
         B     INQ0060
INQ0050  DS    0H
         MVC   INQM02TYP,INQ3480   SET TYPE TO 3480
         MVC   INQM02BPI,COMMBLKS  SET 'BPI' TO BLANKS
INQ0060  DS    0H
         LA    R8,INQM02           MESSAGE'S ADDRESS
         BAL   R9,WTO0000          ISSUE WTO AND PRINT MESSAGE
INQ0070  DS    0H
         ICM   R2,15,DYNNEXT       NEXT BLOCK ON CHAIN
         BNZ   INQ0020             LOOP
         TM    INQFLAG,$INQFND     ANY TAPES FOUND?
         BNO   INQ0300             NO
         B     INQ9900             EXIT
* ------------------------------------------------------------------- *
*                                                                     *
*              LIST ALLOCATED DASD                                    *
*                                                                     *
* ------------------------------------------------------------------- *
INQ0100  DS    0H
         DITTRACE ID=DASDINQ
         NI    INQFLAG,255-$INQFND RESET 'FOUND' FLAG
         ICM   R2,15,COMMDYHD      FIRST DYNAMIC BLOCK
         BZ    INQ0300             NOTHING ON THE CHAIN
INQ0110  DS    0H
         CLC   INQDASD,DYNDDNAM    IS THIS A DASD?
         BNE   INQ0120             NO
         OI    INQFLAG,$INQFND     SET FOUND FLAG
         MVC   INQM03UNT,DYNDDNAM+4 COPY DEVICE NUMBER
         MVC   INQM03VOL,DYNVOL    COPY VOLSER
         LA    R8,INQM03           MESSAGE'S ADDRESS
         BAL   R9,WTO0000          ISSUE WTO AND PRINT MESSAGE
INQ0120  DS    0H
         ICM   R2,15,DYNNEXT       NEXT BLOCK ON CHAIN
         BNZ   INQ0110             LOOP
         TM    INQFLAG,$INQFND     ANY TAPES FOUND?
         BNO   INQ0300             NO
         B     INQ9900             EXIT
* ------------------------------------------------------------------- *
*                                                                     *
*              DISPLAY EXTERNAL TRACE STATUS                          *
*                                                                     *
* ------------------------------------------------------------------- *
INQ0200  DS    0H
         DITTRACE ID=INQTRACE
         TM    XTRFLAG,$XTRACE     EXTERNAL TRACE ACTIVE?
         BNO   INQ0210             ON
         LA    R8,INQM04           MESSAGE'S ADDRESS
         BAL   R9,WTO0000          ISSUE WTO AND PRINT
         B     INQ9900             AND EXIT
INQ0210  DS    0H
         LA    R8,INQM05           MESSAGE'S ADDRESS
         BAL   R9,WTO0000          ISSUE WTO AND PRINT
         B     INQ9900             AND EXIT
* ------------------------------------------------------------------- *
*                                                                     *
*              NO UNITS OF REQUESTED TYPE ALLOCATED                   *
*                                                                     *
* ------------------------------------------------------------------- *
INQ0300  DS    0H
         LA    R8,INQM06           MESSAGE'S ADDRESS
         BAL   R9,WTO0000          ISSUE WTO AND PRINT MESSAGE
         B     INQ9900             AND EXIT
* ------------------------------------------------------------------- *
*                                                                     *
*              ISSUE WTO'S AND PRINT MESSAGE TEXT                     *
*                                                                     *
* ------------------------------------------------------------------- *
WTO0000  DS    0H
         SR    R1,R1               CLEAR REGISTER
         ICM   R1,3,0(R8)          MESSAGE TEXT'S LENGTH
         BCTR  R1,0                ADJUST FOR EXECUTE'S
         EX    R1,PRTMVC           MOVE DATA TO PRINT AREA
         LA    R14,WTOL            LIST FORM WTO'S ADDRESS
         USING WPL,R14             DEFINE WPL BASE
         MVC   WPLTXT(50),COMMBLKS CLEAR MESSAGE IN WTO LIST FORM
         EX    R1,WTOMVC           MOVE TEXT TO WTO MESSAGE
         MVI   PRTCMMD,$PRTCMD     SET COMMAND
         LA    R1,PRTBLOK          PRINT INTERFACE BLOCK
         L     R15,APRT            PRINT MODULE ENTRY POINT
         BALR  R14,R15             PRINT MESSAGE
         WTO   CONSNAME=COMMCNAM,  ISSUE WTO                           +
               MF=(E,WTOL)
         BR    R9                  RETURN
PRTMVC   MVC   PRTDATA(0),2(R8)    MOVE MESSAGE TEXT
WTOMVC   MVC   WPLTXT(0),2(R8)     MOVE MESSAGE TEXT
* ------------------------------------------------------------------- *
*                                                                     *
*              MODULE EXIT POINT                                      *
*                                                                     *
* ------------------------------------------------------------------- *
INQ9900  DS    0H
         OI    COMMFLAG,$COMMEOF   SET EOF FLAG
INQ9910  DS    0H
         DITTRACE ID=EXIT          TRACE EXIT
         L     R13,4(R13)          RESTORE REGISTER 13                  ASE01670
         LM    R14,R12,12(R13)     RESTORE ALL OTHER REGISTERS          ASE01680
         SR    R15,R15             GIVE GOOD RETURN CODE                ASE01690
         BR    R14                 RETURN TO CALLER                     ASE01700
         SPACE 2
* ------------------------------------------------------------------- *
*                                                                     *
*              WORK AREAS/CONSTANTS                                   *
*                                                                     *
* ------------------------------------------------------------------- *
INQSAVE  DC    18F'0'              REGISTER SAVE AREA
WTOL     WTO   '....5...10....5...20....5...30....5...40....5...50',   +
               CONSNAME=,                                              +
               MF=L
INQFLAG  DC    X'00'               FLAGS/SWITCHES
$INQFND  EQU   X'80'               .. UNIT OF REQUEST TYPE FOUND
INQTAPE  DC    C'TAPE'
INQDASD  DC    C'DASD'
INQTRACE DC    C'TRACE'
INQ1600  DC    C'1600'
INQ6250  DC    C'6250'
INQ3480  DC    C'3480'
INQUNKN  DC    C'UNKN'
INQBPI   DC    C'BPI'
INQM01   DC    AL2(INQM01L)
INQM01M  DC    C'INVALID INQUIRE PARAMETER'
INQM01L  EQU   *-INQM01M
INQM02   DC    AL2(INQM02L)
INQM02M  DS    0C
         DC    C'TAPE UNIT '
INQM02UNT DC   CL04' '
         DC    C' IS ALLOCATED AS '
INQM02TYP DC   CL04' '
INQM02BPI DC   CL03' '
INQM02L  EQU   *-INQM02M
INQM03   DC    AL2(INQM03L)
INQM03M  DS    0C
         DC    C'DASD VOLUME '
INQM03VOL DC   CL06' '
         DC    C' IS ALLOCATED ON UNIT '
INQM03UNT DS   CL04' '
INQM03L  EQU   *-INQM03M
INQM04   DC    AL2(INQM04L)
INQM04M  DC    C'EXTERNAL TRACE IS ACTIVE'
INQM04L  EQU   *-INQM04M
INQM05   DC    AL2(INQM05L)
INQM05M  DC    C'EXTERNAL TRACE IS NOT ACTIVE'
INQM05L  EQU   *-INQM05M
INQM06   DC    AL2(INQM06L)
INQM06M  DC    C'NONE ALLOCATED'
INQM06L  EQU   *-INQM06M
         SPACE 2
* ------------------------------------------------------------------- *
*                                                                     *
*              PRINT MODULE INTERFACE BLOCK                           *
*                                                                     *
* ------------------------------------------------------------------- *
PRTBLOK  PRTBLOK
         SPACE 2
* ------------------------------------------------------------------- *
*                                                                     *
*              REGISTER EQUATES                                       *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY  REGEQU
         ABCODES
         EJECT
* ------------------------------------------------------------------- *
*                                                                     *
*              COMMON MODULE DSECT                                    *
*                                                                     *
* ------------------------------------------------------------------- *
DITTCOMM DITTCOMM TYPE=DSECT
         EJECT
* ------------------------------------------------------------------- *
*                                                                     *
*              DYNAMIC UNITS BLOCK DSECT                              *
*                                                                     *
* ------------------------------------------------------------------- *
DYNBLOK  DYNBLOK  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
*              MVS IOB DSECT                                          *
*                                                                     *
* ------------------------------------------------------------------- *
         IEZIOB
IOBLEN   EQU  *-IOB
* ------------------------------------------------------------------- *
*                                                                     *
*              MVS DCB DSECT                                          *
*                                                                     *
* ------------------------------------------------------------------- *
         DCBD  DSORG=PS
DYNDCBL  EQU   *-IHADCB
* ------------------------------------------------------------------- *
*                                                                     *
*              MVS DEB DSECT                                          *
*                                                                     *
* ------------------------------------------------------------------- *
         IEZDEB LIST=NO
         EJECT
* ------------------------------------------------------------------- *
*                                                                     *
*              WTO (WPL) DSECT                                        *
*                                                                     *
* ------------------------------------------------------------------- *
         IEZWPL DSECT=YES
         END  DITTINQ
