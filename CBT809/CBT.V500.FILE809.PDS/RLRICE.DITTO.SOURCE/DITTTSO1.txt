* ------------------------------------------------------------------- *
*                                                                     *
*                  CHARACTER ONLY DATA DISPLAY                        *
*                                                                     *
* ------------------------------------------------------------------- *
DITTTSO1 DITTPRFX TSO1SAVE,'CHARACTER ONLY DATA DISPLAY'
         USING DITTCOMM,R11        DEFINE COMM AREA BASE
         DITTRACE ID=ENTRY         TRACE ENTRY
         TM    COMMFLAG,$COMMCLS   CLOSING TIME?
         BO    TSO0500             YES...
         DITTRACE ID=DEFTABLE      TRACE TABLE DEFINITION
         LA    R1,TBLEPARM         'DITTDATA' TABLE PARMS
         L     R15,AISP            SPF INTERFACE ADDRESS
         BALR  R14,R15             LINK TO SPF TO DEFINE TABLE
         CH    R15,H4              SUCCESSFUL?
         BH    ERR0010             NO...
         L     R10,COMMIND         INPUT DYNAMIC BLOCK ADDRESS
         USING DYNBLOK,R10         DEFINE BASE
         CLC   TSOTAPE,DYNDDNAM    WAS INPUT FROM TAPE?
         BE    TSO0020             YES...
         SR    R1,R1               CLEAR REGISTER
         IC    R1,COMMDREC         RECORD NUMBER
         CVD   R1,COMMDWRD         CONVERT TO DECIMAL
         MVC   EDWORK,EDWORD1      INITIALIZE WITH EDIT WORD
         ED    EDWORK,COMMDWRD+5   EDIT RECORD NUMBER
         MVC   DISKREC,EDWORK+3    COPY RECORD NUMBER
         IC    R1,CNTKEYL          KEY LENGTH
         CVD   R1,COMMDWRD         CONVERT TO DECIMAL
         MVC   KEYLEN,EDWORD2      INITIALIZE WITH EDIT WORD
         ED    KEYLEN,COMMDWRD+5   EDIT KEY LENGTH
         ICM   R1,3,COMMDCYL       CYLINDER NUMBER
         CVD   R1,COMMDWRD         CONVERT TO DECIMAL
         MVC   EDWORK,EDWORD1      INITIALIZE WITH EDIT WORD
         ED    EDWORK,COMMDWRD+5   EDIT CYLINDER NUMBER
         MVC   DISKCYL,EDWORK+2    COPY CYLINDER NUMBER
         ICM   R1,3,COMMDHD        HEAD NUMBER
         CVD   R1,COMMDWRD         CONVERT TO DECIMAL
         MVC   EDWORK,EDWORD1      INITIALIZE WITH EDIT WORD
         ED    EDWORK,COMMDWRD+5   EDIT HEAD NUMBER
         MVC   DISKHEAD,EDWORK+4   COPY HEAD NUMBER
         ICM   R1,3,CNTDATAL       DATA LENGTH FROM COUNT
         CVD   R1,COMMDWRD         CONVERT TO DECIMAL
         MVC   DATALEN,EDWORD2     INITIALIZE WITH EDIT WORD
         ED    DATALEN,COMMDWRD+5  EDIT DATA LENGTH
* ------------------------------------------------------------------- *
*                                                                     *
*              DISPLAY 'KEY' IF PRESENT                               *
*                                                                     *
* ------------------------------------------------------------------- *
         SR    R3,R3               CLEAR REGISTER
         ICM   R3,1,CNTKEYL        KEY LENGTH
         BZ    TSO0010             NO 'KEY' TO DISPLAY
         ICM   R4,15,COMMKEY       KEY'S ADDRESS
         BZ    ERR0030             KEY LENGTH WITH NO KEY ADDRESS?
         DITTRACE ID=DISPKEY,      TRACE                               +
               RDATA1=R3,          .. KEY'S LENGTH                     +
               RDATA2=R4           .. KEY'S ADDRESS
         MVC   DATAAREA,COMMBLKS   INITIALIZE WORK AREA
         MVC   KEYHEAD,=C'KEY'     SET HEADING
         BAL   R9,TSO0100          BUILD DETAIL FOR KEY
* ------------------------------------------------------------------- *
*                                                                     *
*              DISPLAY DATA                                           *
*                                                                     *
* ------------------------------------------------------------------- *
TSO0010  DS    0H
         DITTRACE ID=DISPDATA
         SR    R3,R3               CLEAR REGISTER
         ICM   R3,3,CNTDATAL       DATA LENGTH
         BZ    TSO0300             NO DATA... DISPLAY THE TABLE
         ICM   R4,15,COMMCRCD      DATA'S ADDRESS
         BZ    ERR0040             DATA LENGTH WITH NO DATA ADDRESS?
         TM    COMMOFLG,$DEBLOCK   DEBLOCKING RECORDS?
         BO    TSO0030             YES..
         MVC   DATAHEAD,=C'DATA'   SET HEADING
         BAL   R9,TSO0100          BUILD DETAIL FOR DATA
         B     TSO0300             DISPLAY TABLE
* ------------------------------------------------------------------- *
*                                                                     *
*              TAPE DATA                                              *
*                                                                     *
* ------------------------------------------------------------------- *
TSO0020  DS    0H
         DITTRACE ID=TAPEDATA
         SR    R3,R3               CLEAR REGISTER
         ICM   R3,3,COMMCRCL       DATA LENGTH
         BZ    ERR0040             NO DATA...
         ICM   R4,15,COMMCRCD      DATA'S ADDRESS
         BZ    ERR0040             DATA LENGTH WITH NO DATA ADDRESS?
         CVD   R3,COMMDWRD         CONVERT TO DECIMAL
         MVC   DATALEN,EDWORD2     INITIALIZE WITH EDIT WORD
         ED    DATALEN,COMMDWRD+5  EDIT BLOCK SIZE
         TM    COMMOFLG,$DEBLOCK   DEBLOCKING RECORDS?
         BO    TSO0030             YES..
         MVC   DATAHEAD,=C'DATA'   SET HEADING
         BAL   R9,TSO0100          BUILD DETAIL FOR DATA
         B     TSO0300             DISPLAY TABLE
* ------------------------------------------------------------------- *
*                                                                     *
*              DISPLAY DATA 1 LOGICAL RECORD AT A TIME                *
*                                                                     *
*         R3 IS FULL LENGTH OF DATA                                   *
*         R4 IS ADDRESS OF FIRST LOGICAL RECORD                       *
*                                                                     *
* ------------------------------------------------------------------- *
TSO0030  DS    0H
         DITTRACE ID=DEBLOCK
         TM    COMMOFLG,$VREC      VARIABLE LENGTH RECORDS?
         BNO   TSO0060             NOPE
         LR    R5,R3               COPY DATA LENGTH
         LR    R6,R4               COPY DATA ADDRESS
TSO0040  DS    0H
         SH    R5,H4               MINUS LENGTH OF BDW
         BNH   TSO0060             TOO SHORT... WELL TRY ANYWAY
         LA    R6,4(R6)            FIRST RDW (MAYBE)
         SR    R1,R1               CLEAR REGISTER
TSO0050  DS    0H
         ICM   R1,3,0(R6)          RECORD LENGTH FROM RDW
         CR    R5,R1               RDW WITHIN REASON?
         BL    TSO0060             NO.. ASSUME RECFM=V (NOT VB)
         AR    R6,R1               NEXT RECORD'S ADDRESS
         SR    R5,R1               CALCULATE LENGTH REMAINING IN BLOCK
         BH    TSO0050             CHECK OUT THIS LOGICAL RECORD
         BNZ   TSO0060             RDW'S DON'T ADD UP TO BDW'S
         SH    R3,H4               MINUS BDW LENGTH
         LA    R4,4(R4)            SKIP BDW
TSO0060  DS    0H
         LR    R5,R3               SAVE DATA LENGTH
         LR    R6,R4               SAVE DATA ADDRESS
TSO0070  DS    0H
         ICM   R3,3,COMMLRCL       LOGICAL RECORD LENGTH
         TM    COMMOFLG,$VREC      VARIABLE LENGTH RECORDS?
         BNO   TSO0080             NO
         ICM   R3,3,0(R6)          LENGTH FROM RECORD
TSO0080  DS    0H
         CR    R3,R5               LONGER THAN LENGTH LEFT?
         BNH   TSO0090             NO
         LR    R3,R5               LIMIT LENGTH TO LENGTH LEFT
TSO0090  DS    0H
         LR    R4,R6               COPY RECORD'S ADDRESS
         LR    R7,R3               SAVE LENGTH USED FOR THIS RECORD
         DITTRACE ID=LREC,         TRACE EACH LOGICAL RECORD           +
               RDATA1=R3,          .. LOGICAL RECORD'S LENGTH          +
               RDATA2=R4           .. LOGICAL RECORD'S ADDRESS
         MVC   RECHEAD,=C'RECORD SIZE'
         CVD   R3,COMMDWRD         CONVERT TO DECIMAL
         MVC   RECSIZE,EDWORD2     INITIALIZE WITH EDIT WORD
         ED    RECSIZE,COMMDWRD+5  EDIT LOGICAL RECORD SIZE
         BAL   R9,TSO0100          BUILD DETAIL LINES FOR THIS RECORD
         AR    R6,R7               NEXT LOGICAL RECORD'S ADDRESS
         SR    R5,R7               MINUS LENGTH OF LAST RECORD
         BH    TSO0070             BUILD DATA FOR EACH LOGICAL RECORD
         B     TSO0300             DISPLAY THE TABLE
* ------------------------------------------------------------------- *
*                                                                     *
*              BUILD DETAIL LINES AND ADD THEM TO THE TABLE           *
*                                                                     *
*         AT ENTRY:  R3 LENGTH OF DATA                                *
*                    R4 ADDRESS OF DATA                               *
*                    R9 RETURN ADDRESS                                *
*                                                                     *
*         REGISTERS 1, 2, 3, 4, 14, AND 15 WILL BE ALTERED            *
*                                                                     *
* ------------------------------------------------------------------- *
TSO0100  DS    0H
         DITTRACE ID=STRTBLD,      TRACE POINT                         +
               RDATA1=R3,          .. DATA ADDRESS                     +
               RDATA2=R4           .. DATA LENGTH
         ZAP   TSOOFF,P0           RESET OFFSET COUNTER
TSO0110  DS    0H
         LR    R2,R3               COPY RECORD LENGTH
         CH    R2,H50              TOO MUCH FOR 1 LINE??
         BNH   TSO0120             NO, CONTINUE
         LH    R2,H50              LIMIT LENGTH TO 50
TSO0120  DS    0H
         BCTR  R2,0                ADJUST FOR EXECUTES
         DITTRACE ID=DETAIL,       TRACE POINT                         +
               RDATA1=R2           .. LENGTH USED FOR DETAIL
         EX    R2,DATAMVC          MOVE TO 'DATAAREA'
         EX    R2,DATATR           TRANSLATE OUT UNPRINTABLES
         TM    COMMOFLG,$HEX       DISPLAY IN HEX?
         BO    TSO0130             YES
         CP    TSOOFF,P0           FIRST LINE OF RECORD?
         BE    TSO0130             YES... SKIP OFFSET
         MVC   DATAOFF,EDWORD2     INITIALIZE WITH EDIT WORD
         ED    DATAOFF,TSOOFF      EDIT OFFSET
         MVI   DATAPLUS,C'+'       MOVE PLUS SIGN
TSO0130  DS    0H
         BAL   R8,TSO0400          ADD ROW TO TABLE
         TM    COMMOFLG,$HEX       DISPLAY IN HEX?
         BNO   TSO0150             NO
         EX    R2,DATAMVC          MOVE TO 'DATAAREA'
         EX    R2,ZONEMVC          PREPARE FOR ZONES TRANSLATE
         EX    R2,HEXTR            TRANSLATE ZONES TO DISPLAYABLE
         BAL   R8,TSO0400          ADD ROW TO TABLE
         EX    R2,DATAMVC          MOVE TO 'DATAAREA'
         EX    R2,NUMRMVC          PREPARE FOR NUMERICS TRANSLATE
         EX    R2,HEXTR            TRANSLATE NUMERICS TO DISPLAYABLE
         BAL   R8,TSO0400          ADD ROW TO TABLE
         EX    R2,SCALEMVC         MOVE TO SCALE
         CP    TSOOFF,P0           FIRST LINE OF RECORD?
         BE    TSO0140             YES... SKIP OFFSET
         MVC   DATAOFF,EDWORD2     INITIALIZE WITH EDIT WORD
         ED    DATAOFF,TSOOFF      EDIT OFFSET
         MVI   DATAPLUS,C'+'       MOVE PLUS SIGN
TSO0140  DS    0H
         BAL   R8,TSO0400          ADD ROW TO TABLE
         BAL   R8,TSO0400          ADD ROW TO TABLE
TSO0150  DS    0H
         LA    R2,1(R2)            RESET LENGTH
         AR    R4,R2               BRING INPUT ADDRESS FORWARD
         AP    TSOOFF,P50          ADD TO DATA OFFSET
         SR    R3,R2               COMPUTE LENGTH REMAINING
         BNZ   TSO0110             REPEAT 'TILL ALL LENGTH DONE
         TM    COMMOFLG,$HEX       DISPLAYING IN HEX?
         BO    TSO0160             YES
         BAL   R8,TSO0400          ADD BLANK LINE TO TABLE
TSO0160  DS    0H
         DITTRACE ID=BLDDONE
         BR    R9                  BUILD DONE
         SPACE 2
* ------------------------------------------------------------------- *
*                                                                     *
*              DISPLAY TABLE                                          *
*                                                                     *
* ------------------------------------------------------------------- *
TSO0300  DS    0H
         DITTRACE ID=DISPLAY
         LA    R1,TOPPARM          'TBTOP' PARAMETERS
         L     R15,AISP            SPF INTERFACE ADDRESS
         BALR  R14,R15             LINK TO SPF FOR 'TBTOP'
         LA    R1,COMMPANL         PANEL NAME ADDRESS
         ST    R1,DISPPANL         SET ADDRESS IN PARMLIST
         OI    DISPPANL,X'80'      SET END OF LIST
         LA    R1,DISPPARM         DISPLAY PARAMETERS
         L     R15,AISP            SPF INTERFACE ADDRESS
         BALR  R14,R15             LINK TO SPF TO DISPLAY
         B     TSO9900             AND EXIT
* ------------------------------------------------------------------- *
*                                                                     *
*              ADD ROWS TO SPF TABLE                                  *
*                                                                     *
* ------------------------------------------------------------------- *
TSO0400  DS    0H
         DITTRACE ID=ADDROW
         MVC   DATA,DATAAREA       COPY DATA
         LA    R1,ADDPARM          SPF TBADD PARMS
         L     R15,AISP            SPF INTERFACE ENTRY POINT
         BALR  R14,R15             LINK TO SPF
         LTR   R15,R15             TBADD SUCCESSFUL?
         BNZ   ERR0020             NO
         MVC   DATAAREA,COMMBLKS   RE-INITIALIZE WORK AREA
         BR    R8                  RETURN
* ------------------------------------------------------------------- *
*                                                                     *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
TSO0500  DS    0H
         DITTRACE ID=EOF
         B     TSO9900             EXIT
* ------------------------------------------------------------------- *
*                                                                     *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0010  DS    0H
         DITTRACE ID=CREFAIL,      TBCREATE FAILED                     +
               RDATA1=R15          .. CAPTURE RETURN CODE
         CVD   R15,COMMDWRD        CONVERT RETURN CODE TO DECIMAL
         MVC   MSG001RC,EDWORD3    INITIALIZE WITH EDIT WORD
         ED    MSG001RC,COMMDWRD+6 EDIT RETURN CODE
         LA    R1,MSG001           MESSAGE TEXT ADDRESS
         LA    R0,MSG001L          MESSAGE TEXT LENGTH
         B     TPUT0000            DISPLAY MESSAGE
ERR0020  DS    0H
         DITTRACE ID=ADDFAIL,      TBADD FAILED                        +
               RDATA1=R15          .. CAPTURE RETURN CODE
         CVD   R15,COMMDWRD        CONVERT RETURN CODE TO DECIMAL
         MVC   MSG002RC,EDWORD3    INITIALIZE WITH EDIT WORD
         ED    MSG002RC,COMMDWRD+6 EDIT RETURN CODE
         LA    R1,MSG002           MESSAGE TEXT ADDRESS
         LA    R0,MSG002L          MESSAGE TEXT LENGTH
         B     TPUT0000            DISPLAY MESSAGE
ERR0030  DS    0H
         ABEND ABEND022,DUMP,,USER ABEND
ERR0040  DS    0H
         ABEND ABEND023,DUMP,,USER ABEND
* ------------------------------------------------------------------- *
*                                                                     *
*              DISPLAY TPUT MESSAGES                                  *
*                                                                     *
* ------------------------------------------------------------------- *
TPUT0000 DS    0H
         TPUT  (R1),(R0)           DISPLAY TPUT MESSAGE
* ------------------------------------------------------------------- *
*                                                                     *
*              EXIT POINT                                             *
*                                                                     *
* ------------------------------------------------------------------- *
TSO9900  DS    0H
         DITTRACE ID=EXIT          TRACE EXIT
         L     R13,4(R13)          RESTORE REGISTER 13                  ASE01670
         LM    R14,R12,12(R13)     RESTORE ALL OTHER REGISTERS          ASE01680
         SR    R15,R15             GIVE GOOD RETURN CODE                ASE01690
         BR    R14                 RETURN TO CALLER                     ASE01700
         EJECT
* ------------------------------------------------------------------- *
*                                                                     *
*              EXECUTED INSTRUCTIONS                                  *
*                                                                     *
* ------------------------------------------------------------------- *
DATAMVC  MVC   DATADATA(0),0(R4)   MOVE DATA TO DETAIL AREA
DATATR   TR    DATADATA(0),CHARTBL TRANSLATE OUT UNDISPLAYABLES
ZONEMVC  MVN   DATADATA(0),HEX0000 NULLIFY NUMERICS
NUMRMVC  MVZ   DATADATA(0),HEX0000 NULLIFY ZONES
HEXTR    TR    DATADATA(0),HEXCHAR TRANSLATE TO DISPLAYABLE
SCALEMVC MVC   DATADATA(0),SCALE   MOVE SCALE
         SPACE 2
* ------------------------------------------------------------------- *
*                                                                     *
*              WORK AREAS/CONSTANTS                                   *
*                                                                     *
* ------------------------------------------------------------------- *
TSO1SAVE DC    18F'0'              REGISTER SAVE AREA
TBLEPARM DC    A(TBCREATE)         VERB ADDRESS
         DC    A(NTABLE)           TABLE NAME ADDRESS
         DC    A(0)                NO 'KEY' NAMES
         DC    A(NDATA)            VARIABLES LIST
         DC    A(NOWRITE)          IN CORE ONLY
         DC    A(REPLACE+X'80000000')
ADDPARM  DC    A(TBADD)            VERB ADDRESS
         DC    A(NTABLE+X'80000000')
TOPPARM  DC    A(TBTOP)            VERB ADDRESS
         DC    A(NTABLE+X'80000000')
DISPPARM DC    A(TBDISPL)          VERB ADDRESS
         DC    A(NTABLE)           TABLE NAME
DISPPANL DC    A(0)                PANEL NAME ADDRESS
H4       DC    H'4'                CONSTANT
H50      DC    H'50'               CONSTANT
HEX0000  DC    50X'00'             CONSTANT
TSOOFF   DC    PL3'0'              DATA OFFSET
P0       DC    P'0'                CONSTANT
P50      DC    P'50'               CONSTANT
TBCREATE DC    CL8'TBCREATE'       TABLE CREATE VERB
TBADD    DC    CL8'TBADD'          TABLE ADD VERB
TBTOP    DC    CL8'TBTOP'          TABLE TOP VERB
TBDISPL  DC    CL8'TBDISPL'        TABLE DISPLAY VERB
NTABLE   DC    CL8'TSO1DATA'       TABLE NAME
NDATA    DC    C'(DATA)'           VARIABLE NAMES LIST
NOWRITE  DC    CL8'NOWRITE'        CONSTANT
REPLACE  DC    CL8'REPLACE'        CONSTANT
TSOTAPE  DC    C'TAPE'             CONSTANT
         SPACE
DATAAREA DS    0CL79
         DC    CL18' '
         ORG   DATAAREA
KEYHEAD  DS    CL3                 'KEY'
         ORG   DATAAREA
DATAHEAD DS    CL4                 'DATA'
         ORG   DATAAREA
RECHEAD  DS    CL11                'RECORD SIZE'
         DS    C
RECSIZE  DS    CL6                 LOGICAL RECORD SIZE
         ORG   DATAAREA+18
         DC    CL02' '
DATAPLUS DC    C' '
DATAOFF  DC    CL6' '              LINE'S OFFSET FROM FRONT OF RECORD
         DC    CL02' '
DATADATA DC    CL50' '             DATA AREA
         SPACE
SCALE    DC    C'....+...10....+...20....+...30....+...40....+...50'
EDWORK   DC    CL6' '
EDWORD1  DC    X'F02120202020'
EDWORD2  DC    X'402020202120'
EDWORD3  DC    X'40202120'         RETURN CODE EDIT WORD
MSG001   DS    0C
         DC    C'TBCREATE FAILED, RETURN CODE='
MSG001RC DC    CL4' '
         DC    C' (DECIMAL)'
MSG001L  EQU   *-MSG001
MSG002   DS    0C
         DC    C'TBADD FAILED, RETURN CODE='
MSG002RC DC    CL4' '
         DC    C' (DECIMAL)'
MSG002L  EQU   *-MSG002
* ------------------------------------------------------------------- *
*                                                                     *
*              DISPLAYABLE CHARACTERS TABLE                           *
*                                                                     *
* ------------------------------------------------------------------- *
*                   0 1 2 3 4 5 6 7 8 9 A B C D E F                     DUT09680
CHARTBL  DC    XL16'40404040404040404040404040404040'     X'00' - X'0F' DUT09690
         DC    XL16'40404040404040404040404040404040'     X'10' - X'1F' DUT09700
         DC    XL16'40404040404040404040404040404040'     X'20' - X'2F' DUT09710
         DC    XL16'40404040404040404040404040404040'     X'30' - X'3F' DUT09720
         DC    XL16'404040404040404040404A4B4C4D4E4F'     X'40' - X'4F' DUT09730
         DC    XL16'504040404040404040405A5B5C5D5E5F'     X'50' - X'5F' DUT09740
         DC    XL16'60614040404040404040406B6C6D6E6F'     X'60' - X'6F' DUT09750
         DC    XL16'404040404040404040407A7B7C7D7E7F'     X'70' - X'7F' DUT09760
         DC    XL16'40404040404040404040408B8C8D8E8F'     X'80' - X'8F' DUT09770
         DC    XL16'40404040404040404040409B9C9D9E9F'     X'90' - X'9F' DUT09780
         DC    XL16'4040404040404040404040ABACADAEAF'     X'A0' - X'AF' DUT09790
         DC    XL16'4040404040404040404040BBBCBDBEBF'     X'B0' - X'BF' DUT09800
         DC    XL16'40C1C2C3C4C5C6C7C8C9404040404040'     X'C0' - X'CF' DUT09810
         DC    XL16'40D1D2D3D4D5D6D7D8D9404040404040'     X'D0' - X'DF' DUT09820
         DC    XL16'4040E2E3E4E5E6E7E8E9404040404040'     X'E0' - X'EF' DUT09830
         DC    XL16'F0F1F2F3F4F5F6F7F8F9404040404040'     X'F0' - X'FF' DUT09840
*                   0 1 2 3 4 5 6 7 8 9 A B C D E F                     DUT09680
HEXCHAR  DC    CL16'0123456789ABCDEF'                     X'00' - X'0F' DUT09690
         DC    CL16'1               '                     X'10' - X'1F' DUT09700
         DC    CL16'2               '                     X'20' - X'2F' DUT09700
         DC    CL16'3               '                     X'30' - X'3F' DUT09700
         DC    CL16'4               '                     X'40' - X'4F' DUT09700
         DC    CL16'5               '                     X'50' - X'5F' DUT09700
         DC    CL16'6               '                     X'60' - X'6F' DUT09700
         DC    CL16'7               '                     X'70' - X'7F' DUT09700
         DC    CL16'8               '                     X'80' - X'8F' DUT09700
         DC    CL16'9               '                     X'90' - X'9F' DUT09700
         DC    CL16'A               '                     X'A0' - X'AF' DUT09700
         DC    CL16'B               '                     X'B0' - X'BF' DUT09700
         DC    CL16'C               '                     X'C0' - X'CF' DUT09700
         DC    CL16'D               '                     X'D0' - X'DF' DUT09700
         DC    CL16'E               '                     X'E0' - X'EF' DUT09700
         DC    CL16'F               '                     X'F0' - X'FF' DUT09700
         SPACE 2
* ------------------------------------------------------------------- *
*                                                                     *
*              REGISTER EQUATES                                       *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY  REGEQU
* ------------------------------------------------------------------- *
*                                                                     *
*              DYNAMIC BLOCK DSECT                                    *
*                                                                     *
* ------------------------------------------------------------------- *
DYNBLOK  DYNBLOK  TYPE=SHORT
         EJECT
* ------------------------------------------------------------------- *
*                                                                     *
*              COMMON MODULE DSECT                                    *
*                                                                     *
* ------------------------------------------------------------------- *
DITTCOMM DITTCOMM TYPE=DSECT
         ABCODES
         END  DITTTSO1
