* ------------------------------------------------------------------- *
*                                                                     *
*              TAPE INITIALIZATION                                    *
*                                                                     *
*       THIS MODULE PERFORMS THE 'INT' FUNCTION                       *
*                                                                     *
* ------------------------------------------------------------------- *
DITTTINT DITTPRFX TINTSAVE,'TAPE INITIALIZE'
         USING DITTCOMM,R11        SPECIFY BASE
         DITTRACE ID=ENTRY         TRACE ENTRY
         TM    COMMFLAG,$COMMCLS   CLOSE FILE INDICATOR??
         BO    TINT9900            YES
         TM    COMMFLAG,$COMM1ST   FIRST PASS??
         BNO   TINT0010            NO
         MVC   VOLSER,COMMLOV      INITIALIZE VOLSER
TINT0010 DS    0H
         L     R10,COMMIND         DYNAMIC BLOCK ADDRESS
         USING DYNBLOK,R10         SPECIFY BASE
         ST    R10,EXCPDYN         PASS DYNAMIC BLOCK ADDRESS TO EXCP
         LA    R2,DYNDCB           POINT TO DCB
         USING IHADCB,R2           ADDRESS DCB
TINT0020 DS    0H
         SR    R1,R1               CLEAR REGISTER
         ICM   R1,3,MOUNTWTO       LENGTH IN WTO
         SH    R1,VOLSERL          MINUS LENGTH OF VOLSER
         LA    R1,MOUNTWTO(R1)     FIRST CHARACTER OF VOLSER
         MVC   0(L'VOLSER,R1),VOLSER   INSERT VOLSER INTO WTO
         WTO   MF=(E,MOUNTWTO),    ISSUE MOUNT WTO                     +
               CONSNAME=COMMCNAM   .. CONSOLE ID
         DITTRACE ID=IO            TRACE I/O INITIATION
         MVC   DYNCCW(INITCCWL),REWCCW  INITIALIZE CCW'S
         LA    R1,EXCPBLOK         EXCP PARAMETER BLOCK
         L     R15,AEXCP           EXCP INTERFACE ENTRY POINT
         BALR  R14,R15             LINK TO EXCP MODULE
         TM    DYNSTAT,$DYNPROT    FILE PROTECTED?
         BO    TINT0080            YES
         CLI   EXCPSTAT,$EXCPOK    I/O SUCESSFUL?
         BNE   TINT0400            NO
         CLC   COMMHIV,COMMBLKS    HIGH VOLSER ALL BLANK?
         BE    TINT0070            YES.. EXIT
         CLC   VOLSER,COMMHIV      REACHED UPPER LIMIT?
         BNL   TINT0070            YES.. EXIT
         MVC   PACK1,PACK1-1       RESET TO ALL ZEROS
         LA    R1,PACK1+6          LAST DIGIT IN WORK AREA
         LA    R2,VOLSER+5         LAST DIGIT IN VOLSER
         LA    R3,6                MAX DIGITS
TINT0030 DS    0H
         CLI   0(R2),C'0'          VALID DIGIT?
         BL    TINT0040            NO
         MVC   0(1,R1),0(R2)       COPY THE DIGIT
         BCTR  R1,0                PREVIOUS DIGIT
         BCTR  R2,0                PREVIOUS DIGIT
         BCT   R3,TINT0030         LOOP
TINT0040 DS    0H
         PACK  PACK2,PACK1         PACK NUMERIC PORTION
         AP    PACK2,COMMP1        ADD 1 TO VOLSER
         UNPK  PACK1,PACK2         UNPACK NEXT VOLSER OR VOLSER SUFFIX
         OI    PACK1+6,X'F0'       CHANGE SIGN
         LA    R1,PACK1+6          LAST DIGIT IN WORK AREA
         LA    R2,VOLSER+5         LAST DIGIT IN VOLSER
         LA    R3,6                MAX DIGITS
TINT0050 DS    0H
         CLI   0(R2),C'0'          VALID DIGIT?
         BL    TINT0060            NO.. INITIALIZE THE TAPE
         MVC   0(1,R2),0(R1)       COPY THE DIGIT
         BCTR  R1,0                PREVIOUS DIGIT
         BCTR  R2,0                PREVIOUS DIGIT
         BCT   R3,TINT0050         LOOP
* ------------------------------------------------------------------- *
*                                                                     *
*                    UNLOAD CURRENT TAPE                              *
*                                                                     *
* ------------------------------------------------------------------- *
TINT0060 DS    0H
         DITTRACE ID=UNLOAD
         MVC   DYNCCW(RUNCCWL),RUNCCW   INITIALIZE CCW
         LA    R1,EXCPBLOK         EXCP PARAMETER BLOCK
         L     R15,AEXCP           EXCP INTERFACE ENTRY POINT
         BALR  R14,R15             LINK TO EXCP MODULE
         CLI   EXCPSTAT,$EXCPOK    I/O SUCESSFUL?
         BNE   TINT0400            NO
         B     TINT0020            INITIALIZE THE NEXT TAPE
TINT0070 DS    0H
         DITTRACE ID=INITDONE      TAPE OR RANGE INITIALIZED
         OI    COMMFLAG,$COMMEOF   SET END-OF-FILE FLAG
         B     TINT9900            EXIT
TINT0080 DS    0H
         DITTRACE ID=FILEPROT      TAPE IS FILE PROTECTED
         WTO   'OUTPUT TAPE IS FILE PROTECTED (NO WRITE RING)',        +
               CONSNAME=COMMCNAM
         B     TINT0060            UNLOAD THE TAPE
* ------------------------------------------------------------------- *
*                                                                     *
*                    ERROR ROUTINES                                   *
*                                                                     *
* ------------------------------------------------------------------- *
TINT0400 DS    0H
         LA    R2,DYNBLOK          POINT TO DYNAMIC BLOCK
         LA    R3,DYNDCB           POINT TO DCB
         LA    R4,DYNIOB           POINT TO IOB
         LA    R5,DYNECB           POINT TO ECB
         DITTRACE ID=BADIO         TRACE I/O FAILURES
         DC    H'0'                ABEND
         EJECT
* ------------------------------------------------------------------- *
*                                                                     *
*              MODULE EXIT POINT                                      *
*                                                                     *
* ------------------------------------------------------------------- *
TINT9900 DS    0H
         DITTRACE ID=EXIT          TRACE EXIT
         L     R13,4(R13)          RESTORE REGISTER 13                  ASE01670
         LM    R14,R12,12(R13)     RESTORE ALL OTHER REGISTERS          ASE01680
         SR    R15,R15             GIVE GOOD RETURN CODE                ASE01690
         BR    R14                 RETURN TO OPERATING SYSTEM           ASE01700
         SPACE 2
* ------------------------------------------------------------------- *
*                                                                     *
*              WORKAREAS/CONSTANTS                                    *
*                                                                     *
* ------------------------------------------------------------------- *
REWCCW   CCW   X'07',*,CHAINCMD+SILI,1      REWIND CCW
WRITECCW CCW   X'01',VOL1,CHAINCMD+SILI,80  WRITE CCW
WTMCCW1  CCW   X'1F',*,CHAINCMD+SILI,1      WRITE TAPE MARK CCW
WTMCCW2  CCW   X'1F',*,SILI,1               WRITE TAPE MARK CCW
INITCCWL EQU   *-REWCCW
RUNCCW   CCW   X'0F',*,SILI,1               REWIND/UNLOAD CCW
RUNCCWL  EQU   *-RUNCCW
TINTSAVE DC    18F'0'              REGISTER SAVE AREA
CHAINCMD EQU   X'40'              CHAIN CCW COMMANDS
SILI     EQU   X'20'              SUPPRESS INCORRECT LENGTH INDICATION
VOLSERL  DC    AL2(L'VOLSER)      LENGTH OF VOLSER
         DC    C'0'
PACK1    DC    CL7'0'             VOLSER WORK AREA
PACK2    DC    PL4'0'             VOLSER WORK AREA
VOL1     DS    0C                 <--+
         DC    CL04'VOL1'            |VOLUME SERIAL RECORD
VOLSER   DC    CL06' '               |
         DC    CL70' '            <--+
MOUNTWTO WTO   'MOUNT TAPE TO BE INITIALIZED WITH VOLSER XXXXXX',      +
               CONSNAME=,                                              +
               MF=L
         EJECT
* ------------------------------------------------------------------- *
*                                                                     *
*              EXCP INTERFACE PARAMETERS                              *
*                                                                     *
* ------------------------------------------------------------------- *
EXCPBLOK EXCPBLOK
         EJECT
* ------------------------------------------------------------------- *
*                                                                     *
*              REGISTER EQUATES                                       *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY  REGEQU
         ABCODES
         EJECT
* ------------------------------------------------------------------- *
*                                                                     *
*              COMMON MODULE DSECT                                    *
*                                                                     *
* ------------------------------------------------------------------- *
DITTCOMM DITTCOMM TYPE=DSECT
         EJECT
* ------------------------------------------------------------------- *
*                                                                     *
*              DYNAMIC BLOCK DSECT                                    *
*                                                                     *
* ------------------------------------------------------------------- *
DYNBLOK  DYNBLOK TYPE=DSECT
         EJECT
* ------------------------------------------------------------------- *
*                                                                     *
*              MVS IOB DSECT                                          *
*                                                                     *
* ------------------------------------------------------------------- *
         IEZIOB
IOBLEN   EQU  *-IOB
* ------------------------------------------------------------------- *
*                                                                     *
*              MVS DCB DSECT                                          *
*                                                                     *
* ------------------------------------------------------------------- *
         DCBD DSORG=PS
DYNDCBL  EQU   *-IHADCB
         END  DITTTINT
