* ------------------------------------------------------------------- *
*                                                                     *
*           CONSOLE COMMAND INPUT MODULE                              *
*                                                                     *
*      THIS MODULE PROCESSES CONSOLE-INPUT COMMANDS.  THIS MODULE     *
*      IS CALLED BY 'DITTMAIN' WHEN DITTO IS RUNNING AS A STARTED     *
*      TASK (NOT A JOB).                                              *
*                                                                     *
*      SINCE THE SAME PARAMETERS WILL BE REQUIRED BY CARD-INPUT       *
*      OR CONSOLE-INPUT, THE PARAMETER CONVERSION IS PERFORMED        *
*      BY A SEPARATE MODULE THAT IS CALLED BY BOTH INPUT COMMAND      *
*      MODULES.  THIS WAS TO ELIMINATE AS MUCH DUPLICATION OF         *
*      CODE AS POSSIBLE.  ALSO IF A NEW PARAMETER IS REQUIRED,        *
*      ONLY ONE MODULE USUALLY NEEDS TO BE CHANGED. THE PARAMETER     *
*      CONVERSION MODULE IS 'DITTPARM'.                               *
*                                                                     *
*      IF THE COMMAND IS UNDEFINED OR ALL PARAMETERS ARE NOT          *
*      ENTERED OR ARE IN ERROR, WTO'S WILL BE ISSUED IDENTIFYING      *
*      THE INVALID COMMAND OR PARAMETER, AND THE OPERATOR WILL        *
*      BE PROMPTED FOR MISSING OR INCORRECT INFORMATION.              *
*                                                                     *
* ------------------------------------------------------------------- *
DITTCONS DITTPRFX CONSSAVE,'CONSOLE COMMAND MODULE'
         USING DITTCOMM,R11        SPECIFY BASE
         DITTRACE ID=ENTRY         TRACE ENTRY
CONS0010 DS    0H
         DITTRACE ID=NEWCMMD       TRACE NEW COMMAND READS
         XC    COMMCECB,COMMCECB   CLEAR CONSOLE ECB
         MVC   CONSAREA,COMMBLKS   INITIALIZE REPLY AREA
         LA    R3,COMMCECB         ECB ADDRESS
         WTOR  'ENTER A DITTO COMMAND',  MESSAGE TEXT FOR CONSOLE      +
               CONSAREA,           .. REPLY AREA                       +
               80,                 .. LENGTH OF REPLY AREA             +
               (R3),               .. CONSOLE ECB                      +
               CONSNAME=COMMCNAM   .. CONSOLE NAME
         WAIT  1,ECB=(R3)          WAIT FOR OPERATOR
         SR    R1,R1               CLEAR REGISTER
         ICM   R1,7,CONSMID        MESSAGE ID FROM LAST WTO
         BZ    CONS0020            NO OUTSTANDING MESSAGE
         DOM   MSG=(R1)            DELETE THE MESSAGE
         XC    CONSMID,CONSMID     CLEAR THE ID
CONS0020 DS    0H
         CLC   CONSEOJ,CONSAREA    'EOJ' COMMAMND?
         BNE   CONS0030            NO
         OI    COMMFLAG,$COMMEOF   SIGNAL END OF FILE
         B     CONS9900            EXIT
CONS0030 DS    0H
         LA    R3,CONSAREA         WTOR REPLY AREA
         LA    R4,80               MAX LENGTH OF REPLY
         LA    R1,0(R3,R4)         END OF I/O AREA +1
         BCTR  R1,0                END OF I/O AREA
CONS0050 DS    0H
         CLI   0(R3),C' '          AT LEAST A BLANK?
         BNE   CONS0060            YES
         LA    R3,1(R3)            TRY NEXT ONE
         BCT   R4,CONS0050         LOOP
         B     CONS0010            NOTHING FOUND ISSUE MSG AGAIN
CONS0060 DS    0H
         CLI   0(R1),C' '          AT LEAST A BLANK?
         BH    CONS0070            YES
         BCTR  R1,0                BACK UP
         BCT   R4,CONS0060         LOOP BACK
         B     CONS0010            NOTHING FOUND ISSUE MSG AGAIN
CONS0070 DS    0H
         MVC   PRTDATA(CONSM01L),CONSM01
         MVC   PRTDATA+8(80),CONSAREA MOVE CONSOLE REPLY
         MVI   PRTCMMD,$PRTCMD     MOVE PRINT COMMAND
         BAL   R9,PRT0000          PRINT
         CLC   CONSXTR,CONSAREA    'TRACE' COMMAND?
         BE    CONS2000            YES
         MVC   CONSWORK,COMMBLKS   INITIALIZE WORK AREA
         BAL   R14,CONS7000        FIND COMMAND ON INPUT RECORD
         L     R5,ACMD             COMMAND TABLE ADDRESS
         USING FUNCBLOK,R5         DEFINE ADDRESSIBLITY
         DITTRACE ID=SCANSTRT,     TRACE COMMAND TABLE SCAN START      +
               DATA1=CONSWORK      .. CAPTURE THE COMMAND
CONS0090 DS    0H
         DITTRACE ID=SCANCMD       TRACE COMMAND TABLE SCAN
         CLI   FUNCELEN,X'FF'      END OF TABLE?
         BE    CONSERR1            YES, INVALID COMMAND
         TM    FUNCENV,$FUNCSTC    DOES THIS COMMAND APPLY?
         BNO   CONS0100            NO
         LH    R1,CONSWLEN         GET LENGTH
         CH    R1,FUNCFLEN         FUNCTION LENGTH MATCH?
         BNE   CONS0100            NO
         BCTR  R1,0                ADJUST FOR EXECUTE
         EX    R1,CONSFCLC         CHECK FUNCTION CODE
         BE    CONS0110            COMMAND LOCATED
CONS0100 DS    0H
         AH    R5,FUNCELEN         ADD ENTRY LENGTH
         B     CONS0090            LOOP
CONS0110 DS    0H
         DITTRACE ID=CMMDFND       TRACE COMMAND FOUND
         CLI   0(R3),C'?'          COMMAND EXAMPLE WANTED?
         BE    CONS0500            YES
         CLC   FUNCCMD,LISTFNC     XXX FUNCTION?
         BE    CONS1000            YES
         SR    R6,R6               CLEAR FOR NUMBER OF REQ PARMS
         ICM   R6,3,FUNCNREQ       NUMBER OF REQUIRED PARAMETERS
         BZ    CONS0150            NONE ... TRY FOR OPTIONAL PARMS
         LA    R7,FUNCREQ          FIRST REQUIRED PARAMETER
CONS0120 DS    0H
         DITTRACE ID=PARMSCAN      TRACE PARAMETER SEARCH START
         LTR   R4,R4               ANY DATA LEFT IN REPLY?
         BZ    CONS0300            START PROMPTING OPERATOR FOR THEM
         MVC   CONSWORK,COMMBLKS   INITIALIZE WORK AREA
         BAL   R14,CONS7000        GET PARAMETER
         DITTRACE ID=REQPARM       TRACE REQUIRED PARM CONVERSION
         MVI   PARMCMMD,$PARMLOC   'LOCATE' COMMAND
CONS0130 DS    0H
         MVC   PARMPARM,0(R7)      PASS FIELD ID
         MVC   PARMWORK,CONSWORK   PASS DATA
         MVC   PARMWLEN,CONSWLEN   PASS DATA LENGTH
         LA    R1,FUNCOPV          FIRST VALID VALUE FOR 'OPT'
         ST    R1,PARMOPV          PASS ADDRESS TO DITTPARM
         MVC   PARMOPN,FUNCOPN     NUMBER OF VALID VALUES
         LA    R1,PARMBLOK         PARAMETER CONVERTER PARMS
         L     R15,APARM           PARAMETER CONVERTER ENTRY PONT
         BALR  R14,R15             LINK TO PARAMETER CONVERTER
         TM    PARMFLAG,$PARMCAN   OPERATOR REQUEST 'CANCEL'?
         BO    CONS0600            YES... ABORT THIS COMMAND
         TM    PARMFLAG,$PARMOK    PARAMETER OKEE AND DOKEE?
         BO    CONS0140            YES
         L     R1,PARMMSGA         RETURNED MESSAGE ADDRESS
         SR    R15,R15             CLEAR REGISTER
         ICM   R15,3,0(R1)         TEXT LENGTH
         BCTR  R15,0               FOR EXECUTE
         EX    R15,PRTMMVC         MOVE TEXT TO PRINT I/O AREA
         MVC   CONSWTO+4(80),COMMBLKS
         EX    R15,WTOMMVC         MOVE MESSAGE
         MVI   PRTCMMD,$PRTCMD
         BAL   R9,PRT0000          PRINT MESSAGE
         CLC   PARMPARM,HIVOLID    WAS THIS 'HIGH VOLSER' OF A RANGE?
         BE    CONS0610            YES.. FORCE OPERATOR TO RE-ENTER
         LA    R1,CONSWTO          POINT TO OUR WTO
         ST    R1,PARMWTO          PASS ADDRESS BACK TO DITTPARM
         MVI   PARMCMMD,$PARMEWTO  INDICATE ERROR MSG WTO AND PROMPT
         B     CONS0130            RE-DRIVE PARAMETER MODULE
CONS0140 DS    0H
         LA    R7,1(R7)            NEXT REQUIRED PARAMETER
         BCT   R6,CONS0120         PROCESS NEXT PARAMETER
* ------------------------------------------------------------------- *
*        ALL REQUIRED PARAMETERS HAVE BEEN PROCESSED.                 *
*        NOW PROCESS ANY OPTIONAL PARAMETERS.                         *
* ------------------------------------------------------------------- *
CONS0150 DS    0H
         SR    R6,R6               CLEAR FOR NUMBER OF OPTIONAL PARMS
         ICM   R6,3,FUNCNOPT       NUMBER OF OPTIONAL PARAMETERS
         BZ    CONS0400            NONE ... ALL DONE
         LA    R7,FUNCOPT          FIRST OPTIONAL PARAMETER
CONS0160 DS    0H
         LTR   R4,R4               ANY DATA LEFT IN REPLY?
         BZ    CONS0400            NO
         MVC   CONSWORK,COMMBLKS   INITIALIZE WORK AREA
         BAL   R14,CONS7000        GET PARAMETER
         DITTRACE ID=OPTPARM       TRACE OPTIONAL PARM CONVERSION
         MVI   PARMCMMD,$PARMLOC   'LOCATE' COMMAND
CONS0170 DS    0H
         MVC   PARMPARM,0(R7)      FIELD ID
         MVC   PARMWORK,CONSWORK   DATA
         MVC   PARMWLEN,CONSWLEN   DATA LENGTH
         LA    R1,FUNCOPV          FIRST VALID VALUE FOR 'OPT'
         ST    R1,PARMOPV          PASS ADDRESS TO DITTPARM
         MVC   PARMOPN,FUNCOPN     NUMBER OF VALID VALUES
         LA    R1,PARMBLOK         PARAMETER CONVERTER PARMS
         L     R15,APARM           PARAMETER CONVERTER ENTRY POINT
         BALR  R14,R15             LINK TO PARAMETER CONVERTER
         TM    PARMFLAG,$PARMCAN   OPERATOR REQUEST 'CANCEL'?
         BO    CONS0190            YES... OPTIONAL PARAMETER NOT USED
         TM    PARMFLAG,$PARMOK    PARAMETER OKEE AND DOKEE?
         BO    CONS0180            YES
         L     R1,PARMMSGA         RETURNED MESSAGE ADDRESS
         SR    R15,R15             CLEAR REGISTER
         ICM   R15,3,0(R1)         TEXT LENGTH
         BCTR  R15,0               FOR EXECUTE
         EX    R15,PRTMMVC         MOVE MESSAGE
         MVC   CONSWTO+4(80),COMMBLKS
         EX    R15,WTOMMVC         MOVE MESSAGE
         MVI   PRTCMMD,$PRTCMD
         BAL   R9,PRT0000          PRINT MESSAGE
         CLC   PARMPARM,HIVOLID    WAS THIS 'HIGH VOLSER' OF A RANGE?
         BE    CONS0610            YES.. FORCE OPERATOR TO RE-ENTER
         LA    R1,CONSWTO          POINT TO OUR WTO
         ST    R1,PARMWTO          PASS ADDRESS BACK TO DITTPARM
         MVI   PARMCMMD,$PARMEWTO  INDICATE ERROR MSG WTO AND PROMPT
         B     CONS0170            RE-DRIVE PARAMETER MODULE
CONS0180 DS    0H
         LA    R7,1(R7)            NEXT OPTIONAL PARAMETER
         BCT   R6,CONS0160         PROCESS NEXT PARAMETER
         B     CONS0400            ALL OPTIONAL PARAMETERS DONE
CONS0190 DS    0H
         WTO   'OPTIONAL PARAMETER REMOVED FROM CURRENT COMMAND',      +
               CONSNAME=COMMCNAM   CONSOLE NAME
         B     CONS0180            CONTINUE WITH NEXT OPTIONAL PARM
         SPACE 2
* ------------------------------------------------------------------- *
*        OPERATOR COMMAND EXHAUSTED.  WE STILL NEED SOME REQUIRED     *
*        PARAMETERS.  START PROMPTING OPERATOR FOR THEM.              *
* ------------------------------------------------------------------- *
CONS0300 DS    0H
         DITTRACE ID=MISSING       ENTRY INTO MISSING PARM PROMPTS
CONS0310 DS    0H
         DITTRACE ID=PROMPT3       TRACE EACH ITERATION
         MVI   PARMCMMD,$PARMWTO   INDICATE PROMPT FOR PARAMETER
CONS0320 DS    0H
         MVC   PARMPARM,0(R7)      FIELD ID
         MVC   PARMWORK,CONSWORK   DATA
         MVC   PARMWLEN,CONSWLEN   DATA LENGTH
         LA    R1,PARMBLOK         PARAMETER CONVERTER PARMS
         L     R15,APARM           PARAMETER CONVERTER ENTRY POINT
         BALR  R14,R15             LINK TO PARAMETER CONVERTER
         TM    PARMFLAG,$PARMCAN   OPERATOR REQUET 'CANCEL'?
         BO    CONS0600            YES... ABORT THIS COMMAND
         TM    PARMFLAG,$PARMOK    PARAMETER OK?
         BO    CONS0330            YES
         DITTRACE ID=BADPARM3      TRACE INVALID PARAMETER
         MVI   PARMCMMD,$PARMEWTO  REQUEST ERROR MSG WTO AND PROMPT
         B     CONS0320            TRY AGAIN
CONS0330 DS    0H
         DITTRACE ID=GOODPRM3      TRACE GOOD PARAMETER
         LA    R7,1(R7)            NEXT REQUIRED PARAMETER
         BCT   R6,CONS0310         PROCESS NEXT PARAMETER
* ------------------------------------------------------------------- *
*        ALL PARAMETERS PROCESSED.  PASS INPUT AND OUTPUT MODULE      *
*        ADDRESSES AND FLAGS.                                         *
* ------------------------------------------------------------------- *
CONS0400 DS    0H
         SR    R1,R1               CLEAR REGISTER
         ICM   R1,3,FUNCIN1        INPUT MODULE ADDRESS DISPLACEMENT
         BZ    CONS0410            NO INPUT MODULE
         LA    R2,DITTCOMM(R1)     INPUT MODULE ADDRESS ADDRESS
         MVC   COMMINM,0(R2)       INPUT MODULE ADDRESS
         MVC   COMMIFLG,FUNCIFLG   INPUT MODULE FLAGS
CONS0410 DS    0H
         ICM   R1,3,FUNCOUT1       OUTPUT MODULE ADDRESS DISPLACEMENT
         BZ    CONS9900            NO OUTPUT MODULE
         LA    R2,DITTCOMM(R1)     OUTPUT MODULE ADDRESS ADDRESS
         MVC   COMMOUTM,0(R2)      OUTPUT MODULE ADDRESS
         MVC   COMMOFLG,FUNCOFLG   OUTPUT MODULE FLAGS
         B     CONS9900            EXIT
* ------------------------------------------------------------------- *
*        ISSUE EXAMPLE WTO FOR OPERATOR.                              *
* ------------------------------------------------------------------- *
CONS0500 DS    0H
         DITTRACE ID=EXAMPLE       TRACE POINT
         MVC   CONSWTO+4(80),FUNCEX2
         WTO   MF=(E,CONSWTO),     ISSUE WTO OF COMMAND EXAMPLE        +
               CONSNAME=COMMCNAM   CONSOLE NAME
         STCM  R1,7,CONSMID        SAVE WTO MESSAGE ID FOR DOM
         B     CONS0010            REQUEST ANOTHER COMMAND
* ------------------------------------------------------------------- *
*        COMMAND CANCELLED BY OPERATOR                                *
* ------------------------------------------------------------------- *
CONS0600 DS    0H
         DITTRACE ID=CANCEL        TRACE POINT
         MVI   PRTCC,C' '          SINGLE SPACE
         MVC   PRTDATA(CONSM04L),CONSM04
         BAL   R9,PRT0000          LINK TO PRINT MODULE
         B     CONS0620            INFORM OPERATOR
CONS0610 DS    0H
         DITTRACE ID=RANGERR       TRACE POINT
         MVI   PRTCC,C' '          SINGLE SPACE
         MVC   PRTDATA(CONSM05L),CONSM05
         BAL   R9,PRT0000          LINK TO PRINT MODULE
         WTO   CONSNAME=COMMCNAM,  ISSUE WTO FOR REASON                +
               MF=(E,CONSWTO)
CONS0620 DS    0H
         WTO   'CURRENT COMMAND CANCELLED',                            +
               CONSNAME=COMMCNAM   CONSOLE NAME
         B     CONS0010            REQUEST ANOTHER COMMAND
         EJECT
* ------------------------------------------------------------------- *
*                                                                     *
*              LIST AVAILABLE COMMANDS                                *
*                                                                     *
* ------------------------------------------------------------------- *
CONS1000 DS    0H
         DITTRACE ID=LISTCMDS      TRACE START OF LIST
         L     R5,ACMD             FUNCTIONS TABLE ADDRESS
         BAL   R9,PRT0000          LINK TO PRINT MODULE
CONS1010 DS    0H
         DITTRACE ID=LISTFUNC      TRACE EACH FUNCTION
         CLI   FUNCELEN,X'FF'      END OF TABLE?
         BE    CONS0010            YES
         TM    FUNCENV,$FUNCSTC    DOES THIS COMMAND APPLY?
         BNO   CONS1020            NO
         MVC   CONSM01C,FUNCCMD    MOVE COMMAND
         MVC   CONSM01D,FUNCDESC   MOVE DESCRIPTION
         MVI   PRTCMMD,$PRTDATA    SET COMMAND
         MVC   PRTDATA(CONSM01L),CONSM01
         MVI   PRTCC,C'0'          DOUBLE SPACE CARRIAGE CONTROL
         MVI   PRTCMMD,$PRTDATA    SET COMMAND
         BAL   R9,PRT0000          LINK TO PRINT MODULE
         MVC   CONSM02C,FUNCEX2    MOVE EXAMPLE OF COMMAND
         MVC   PRTDATA(CONSM02L),CONSM02
         MVI   PRTCC,C' '          SINGLE SPACE CARRIAGE CONTROL
         BAL   R9,PRT0000          LINK TO PRINT MODULE
CONS1020 DS    0H
         AH    R5,FUNCELEN         NEXT FUNCTION
         B     CONS1010            LIST NEXT FUNCTION
* ------------------------------------------------------------------- *
*                                                                     *
*              EXTERNAL TRACE CONTROL                                 *
*                                                                     *
* ------------------------------------------------------------------- *
CONS2000 DS    0H
         DITTRACE ID=XTRACE
         CLC   CONSON,CONSAREA+6   TRACE ON?
         BE    CONS2010            YES
         CLC   CONSOFF,CONSAREA+6  TRACE OFF?
         BE    CONS2030            YES
         WTO   'INVALID EXTERNAL TRACE COMMAND (USE ''TRACE ON'' OR ''T+
               RACE OFF'')',                                           +
               CONSNAME=COMMCNAM
         MVC   PRTDATA(CONSM06L),CONSM06
         BAL   R9,PRT0000          PRINT MESSAGE
         B     CONS0010            REQUEST ANOTHER COMMAND
CONS2010 DS    0H
         DITTRACE ID=TRACEON
         TM    XTRFLAG,$XTRACE     TRACE ALREADY ON?
         BO    CONS2020            YES
         OI    XTRFLAG,$XTRACE     SET EXTERNAL TRACE FLAG
         WTO   'EXTERNAL TRACE ACTIVATED',                             +
               CONSNAME=COMMCNAM
         MVC   PRTDATA(CONSM03L),CONSM03
         BAL   R9,PRT0000          PRINT THE MESSAGE
         B     CONS0010            REQUEST ANOTHER COMMAND
CONS2020 DS    0H
         DITTRACE ID=DUPON
         WTO   'EXTERNAL TRACE ALREADY ACTIVE',                        +
               CONSNAME=COMMCNAM
         MVC   PRTDATA(CONSM07L),CONSM07
         BAL   R9,PRT0000          PRINT THE MESSAGE
         B     CONS0010            REQUEST ANOTHER COMMAND
CONS2030 DS    0H
         TM    XTRFLAG,$XTRACE     EXTERNAL TRACE FLAG ON?
         BNO   CONS2040            NO
         DITTRACE ID=TRACEOFF
         NI    XTRFLAG,255-$XTRACE TURN OFF FLAG
         WTO   'EXTERNAL TRACE DE-ACTIVATED',                          +
               CONSNAME=COMMCNAM
         MVC   PRTDATA(CONSM08L),CONSM08
         BAL   R9,PRT0000          PRINT THE MESSAGE
         B     CONS0010            REQUEST ANOTHER COMMAND
CONS2040 DS    0H
         DITTRACE ID=DUPOFF
         WTO   'EXTERNAL TRACE NOT ACTIVE',                            +
               CONSNAME=COMMCNAM
         MVC   PRTDATA(CONSM09L),CONSM09
         BAL   R9,PRT0000          PRINT THE MESSAGE
         B     CONS0010            REQUEST ANOTHER COMMAND
* ------------------------------------------------------------------- *
*                                                                     *
*              INPUT PARSER                                           *
*         'CONSWORK' SHOULD BE INITIALIZED BEFORE INVOKING            *
*         R1, R2, R15 ARE WORK REGISTERS                              *
*         R3 HAS CURRENT INPUT ADDRESS                                *
*         R4 LENGTH REMAINING ON INPUT                                *
*         R14 IS RETURN ADDRESS                                       *
*                                                                     *
*         ON EXIT 'CONSWORK' CONTAINS DATA FOUND TO NEXT DELIMITER,   *
*         UPTO 8 BYTES.  'CONSWLEN' CONTAINS ITS LENGTH.              *
*         R3 WILL BE UPDATED PAST THE DELIMITER AND R4 WILL           *
*         REFLECT LENGTH LEFT FOLLOWING THE DELIMITER.                *
*                                                                     *
* ------------------------------------------------------------------- *
CONS7000 DS    0H
         LTR   R4,R4               ANY DATA LEFT IN RECORD?
         BZR   R14                 NO.. EXIT
         XC    CONSWORK,CONSWORK   CLEAR WORK AREA
         LA    R1,CONSWORK         POINT TO WORK AREA
         SR    R2,R2               CLEAR LENGTH COUNTER
         LA    R15,44              MAXIMUM LENGTH
CONS7010 DS    0H
         CLI   0(R3),C' '          BLANK?
         BE    CONS7030            YES
         CLI   0(R3),C','          COMMA?
         BE    CONS7030            YES
         MVC   0(1,R1),0(R3)       MOVE IT
         LA    R2,1(R2)            COUNT IT
         LA    R3,1(R3)            NEXT
         LA    R1,1(R1)            NEXT
         BCT   R4,CONS7020         DECREMENT COUNT
         STH   R2,CONSWLEN         SAVE CURRENT LENGTH
         BR    R14                 LENGTH EXHAUSTED, EXIT HERE
CONS7020 DS    0H
         BCT   R15,CONS7010        LOOP
CONS7030 DS    0H
         STH   R2,CONSWLEN         SAVE WORK AREA ACTIVE LENGTH
         LA    R3,1(R3)            SKIP DELIMITER
         BCTR  R4,0                REMOVE DELIMITER LENGTH
         BR    R14                 EXIT
         SPACE 2
* ------------------------------------------------------------------- *
*                                                                     *
*              PRINT ROUTINE                                          *
*                                                                     *
* ------------------------------------------------------------------- *
PRT0000  DS    0H
         LA    R1,PRTBLOK          PRINT INTERFACE PARAMETERS
         L     R15,APRT            PRINT MODULE ENTRY POINT
         BALR  R14,R15             LINK TO PRINT
         BR    R9                  RETURN
* ------------------------------------------------------------------- *
*                                                                     *
*              ERROR ROUTINES                                         *
*                                                                     *
* ------------------------------------------------------------------- *
CONSERR1 DS    0H
         OI    COMMFLAG,$COMMCER   SIGNAL INVALID COMMAND
         WTO   'INVALID CONSOLE COMMAND',                              +
               CONSNAME=COMMCNAM   .. CONSOLE NAME
         STCM  R1,7,CONSMID        SAVE MESSAGE ID FOR DOM
         B     CONS9900            EXIT
* ------------------------------------------------------------------- *
*                                                                     *
*              MODULE EXIT POINT                                      *
*                                                                     *
* ------------------------------------------------------------------- *
CONS9900 DS    0H
         DITTRACE ID=EXIT,         TRACE EXITS                         +
               DATA1=COMMINM,      .. INPUT MODULE ADDRESS             +
               DATA2=COMMOUTM      .. OUTPUT MODULE ADDRESS
         L     R13,4(R13)          RESTORE REGISTER 13                  ASE01670
         LM    R14,R12,12(R13)     RESTORE ALL OTHER REGISTERS          ASE01680
         SR    R15,R15             GIVE GOOD RETURN CODE                ASE01690
         BR    R14                 RETURN TO CALLER                     ASE01700
* ------------------------------------------------------------------- *
*                                                                     *
*              CONS-INPUT FUNCTION EXECUTED INSTRUCTIONS              *
*                                                                     *
* ------------------------------------------------------------------- *
CONSFCLC CLC   FUNCCMD(0),CONSWORK  CORRECT COMMAND?
CONSCMVC MVC   PRTDATA(0),0(R15)    MOVE COMMAND(S) TO PRINT
WTOMMVC  MVC   CONSWTO+4(0),2(R1)   MOVE MESSAGE TEXT TO WTO
PRTMMVC  MVC   PRTDATA(0),2(R1)     MOVE MESSAGE TEXT TO PRINT I/O
         EJECT
* ------------------------------------------------------------------- *
*                                                                     *
*              CONS-INPUT FUNCTION WORK AREAS                         *
*                                                                     *
* ------------------------------------------------------------------- *
CONSSAVE DC    18F'0'              REGISTER SAVE AREA
CONSWORK DC    CL44' '             PARSER OUTPUT WORK AREA
CONSWLEN DC    H'0'                CURRENT PARSER OUTPUT LENGTH
CONSMID  DC    XL3'000000'         MESSAGE ID FROM LAST WTO
CONSEOJ  DC    C'EOJ '             'END OF JOB' COMMAND
CONSCAN  DC    C'CANCEL '          'CANCEL' COMMAND
CONSXTR  DC    C'TRACE '           'TRACE' COMMAND
CONSON   DC    C'ON '              CONSTANT
CONSOFF  DC    C'OFF '             CONSTANT
CONSAREA DC    CL80' '             CONSOLE REPLY AREA
CONSWTO  WTO   '                                                       +
                                        ',                             +
               CONSNAME=,                                              +
               MF=L
         SPACE 2
* ------------------------------------------------------------------- *
*                                                                     *
*              PRINT MODULE INTERFACE BLOCK                           *
*                                                                     *
* ------------------------------------------------------------------- *
PRTBLOK  PRTBLOK
         SPACE 2
* ------------------------------------------------------------------- *
*                                                                     *
*              PARAMETER CONVERTER INTERFACE BLOCK                    *
*                                                                     *
* ------------------------------------------------------------------- *
PARMBLOK PARMBLOK
         SPACE 2
* ------------------------------------------------------------------- *
*                                                                     *
*              CONSTANTS                                              *
*                                                                     *
* ------------------------------------------------------------------- *
LISTFNC  DC    C'XXX '
CONSM01  DC    C'COMMAND: '
CONSM01C DC    CL4' '
         DC    C'   DESCRIPTION:'
CONSM01D DC    CL80' '
CONSM01L EQU   *-CONSM01
CONSM02  DC    C'EXAMPLE OF COMMAND:'
         DC    CL12' '
CONSM02C DC    CL80' '
CONSM02L EQU   *-CONSM02
CONSM03  DC    C'EXTERNAL TRACE ACTIVATED'
CONSM03L EQU   *-CONSM03
CONSM04  DC    C'COMMAND CANCELLED BY OPERATOR'
CONSM04L EQU   *-CONSM04
CONSM05  DC    C'INCONSISTENT PARAMETERS, COMMAND ABORTED'
CONSM05L EQU   *-CONSM05
CONSM06  DC    C'INVALID TRACE COMMAND (USE ''TRACE ON'' OR ''TRACE OFF+
               '')'
CONSM06L EQU   *-CONSM06
CONSM07  DC    C'EXTERNAL TRACE ALREADY ACTIVE'
CONSM07L EQU   *-CONSM07
CONSM08  DC    C'EXTERNAL TRACE DE-ACTIVATED'
CONSM08L EQU   *-CONSM08
CONSM09  DC    C'EXTERNAL TRACE NOT ACTIVE'
CONSM09L EQU   *-CONSM09
         SPACE 2
* ------------------------------------------------------------------- *
*                                                                     *
*              DEFINE PARAMETERS                                      *
*                                                                     *
* ------------------------------------------------------------------- *
         PARMGEN GEN=NO
HIVOLID  PARMID HIGHVOL
* ------------------------------------------------------------------- *
*                                                                     *
*              REGISTER EQUATES                                       *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY REGEQU
         EJECT
* ------------------------------------------------------------------- *
*                                                                     *
*              FUNCTION TABLE DSECT                                   *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY  FUNCBLOK
* ------------------------------------------------------------------- *
*                                                                     *
*              COMMON MODULE DSECT                                    *
*                                                                     *
* ------------------------------------------------------------------- *
DITTCOMM DITTCOMM TYPE=DSECT
         END  DITTCONS
