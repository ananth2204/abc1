         TITLE 'IFMTFILL - ENSURE ALL CHARACTERS FILLED ON INPUT'
* ******************************************************************* *
*                                                                     *
*    IIII   FFFFFFF M     M TTTTTTTT FFFFFFF  IIII   LL      LL       *
*     II    FF      MM   MM    TT    FF        II    LL      LL       *
*     II    FF      MMM MMM    TT    FF        II    LL      LL       *
*     II    FFFFF   MMMMMMM    TT    FFFFF     II    LL      LL       *
*     II    FF      MM M MM    TT    FF        II    LL      LL       *
*     II    FF      MM   MM    TT    FF        II    LL      LL       *
*     II    FF      MM   MM    TT    FF        II    LL      LL       *
*    IIII   FF      MM   MM    TT    FF       IIII   LLLLLLL LLLLLLL  *
*                                                                     *
* ******************************************************************* *
* THIS SUBROUTINE SCANS AN INPUT FIELD (DISPLAY FORMAT) TO VERIFY     *
* THAT ALL CHARACTERS ARE FILLED (NO LEADING, TRAILING, OR EMBEDDED   *
* BLANKS).                                                            *
*                                                                     *
* NONE OF THE INITIALIZED VARIABLES ARE CHANGED DURING THE ROUTINE'S  *
* EXECUTION AND THERE ARE NO INTERNAL DATA FIELDS. ALL MANIPULATION   *
* IS DONE THROUGH REGISTERS. THE ROUTINE IS COMPLETELY REENTRANT.     *
*                                                                     *
* TWO FIELDS ARE PASSED AS PARAMETERS TO THE ROUTINE:                 *
*                                                                     *
*   1. A FIELD OF VARYING LENGTH CONTAINING THE INPUT DATA, AND       *
*   2. A 2 BYTE FIELD (IN BINARY FORMAT) CONTAINING THE LENGTH OF     *
*      THE FIRST FIELD PASSED.                                        *
*                                                                     *
* SUGGESTED CALLING SYNTAX FOR ALC CALLERS:                           *
*                                                                     *
*              CALL IFMTFILL,(INFIELD,INLENGTH)                       *
*                 . . .                                               *
*     INFIELD  DS   CL12'ABCDEF-12345'                                *
*     INLENGTH DS   H'12'                                             *
*                                                                     *
* SUGGESTED CALLING SYNTAX FOR COBOL CALLERS:                         *
*                                                                     *
*     01  INPUT-FIELD           PIC X(12) VALUE 'ABCDEF-12345'.       *
*     01  INPUT-FIELD-LENGTH    PIC S9(4) COMP VALUE 12.              *
*                                                                     *
*     CALL 'IFMTFILL' USING INPUT FIELD, INPUT-FIELD-LENGTH.          *
*                                                                     *
* POSSIBLE RETURN CODE VALUES:                                        *
*     0 - NO BLANK CHARACTERS FOUND WITHIN FIELD                      *
*     8 - BLANK CHARACTER(S) FOUND WITHIN FIELD                       *
* ******************************************************************* *
* ******************************************************************* *
         EJECT
         PRINT NOGEN
IFMTFILL CSECT
         EYEC  'VERIFY INPUT FIELD IS FILLED'
         SAVE  (14,12)                 SAVE CALLER'S REGISTERS
*
         BALR  BASE,0                  ESTABLISH BASE REGISTER
         USING *,BASE                  ASSUME BASE REGISTER
*
         LM    INREG,LGTHREG,0(R1)     LOAD ADDRESSES OF PARAMETERS
*
         SR    R15,R15                 ASSUME RETURN CODE OF ZERO
*
         LH    LGTHREG,0(,LGTHREG)     GET LENGTH OF INPUT FIELD
         LTR   LGTHREG,LGTHREG         IS INPUT FIELD LENGTH ZERO?
         BZ    RETURN                     IF YES, RETURN TO CALLER
*
         BCTR  LGTHREG,0               DECREASE BY 1 FOR EXECUTE
         EX    LGTHREG,FINDBL          SCAN FOR BLANK
         BZ    RETURN                  IF NO BLANKS FOUND
*
*    IF ANY BLANKS WERE FOUND, RETURN CODE IS SET TO "8"
*
ERROR    LA    R15,8                    RETURN CODE OF 8
*
*    RETURN TO CALLING PROGRAM WITH RETURN CODE IN REGISTER 15
*
RETURN   RETURN (14,12),RC=(15)
*
*    TRT PERFORMED VIA EXECUTE WITH FIELD LENGTH MODIFIER
*
FINDBL   TRT   0(0,INREG),TABLEBL       SCAN FOR BLANK CHARACTER
*
* ******************************************************************* *
* *************************** CONSTANTS ***************************** *
* ******************************************************************* *
TABLEBL  DC    64X'00'
         DC    X'01'
         DC    191X'00'
         LTORG
         EJECT
* ******************************************************************* *
* ***************************** EQUATES ***************************** *
* ******************************************************************* *
R0       EQU   0                       REGISTER 0
R1       EQU   1                       REGISTER 1
R2       EQU   2                       REGISTER 2
R3       EQU   3                       REGISTER 3
R4       EQU   4                       REGISTER 4
INREG    EQU   5                       INPUT FIELD
LGTHREG  EQU   6                       INPUT FIELD LENGTH
R7       EQU   7                       REGISTER 7
R8       EQU   8                       REGISTER 8
R9       EQU   9                       REGISTER 9
R10      EQU   10                      REGISTER 10
R11      EQU   11                      REGISTER 11
BASE     EQU   12                      BASE REGISTER
R13      EQU   13                      REGISTER 13
R14      EQU   14                      REGISTER 14
R15      EQU   15                      REGISTER 15
         END
