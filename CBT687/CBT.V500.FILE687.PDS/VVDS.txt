/*********************************************************************/
/*****                  V V D S             ** AUTHOR: CHEMA ALVAREZ */
/*********************************************************************/
CONTROL NOFLUSH NOMSG /*LIST SYMLIST CONLIST
 /*
 /* CLIST TAILORING THE JOB TO DIAGNOSE ICFCATALOGS AND VVDS'S
 /*
ERROR -
  DO
   IF &LASTCC = 400 THEN GOTO &FINFIC    /* EODAD IN THE GETFILE     */
   /*WRITE WARNING &LASTCC IN PROCEDURE LAST COMMAND &SYSPCMD
   SET CC = &LASTCC
   IF &LASTCC < 16 THEN RETURN           /* WARNING MESSAGE          */
   WRITE ERROR &LASTCC IN PROCEDURE LAST COMMAND &SYSPCMD
   EXIT QUIT                             /* SI EL ERROR ES MAYOR DE 16-
                                            TERMINAMOS SIN CERRAR NI  -
                                            LIBERAR NINGUN FICHERO NI -
                                            TABLA                    */
  END
 SET SW1 = 0                              /* SWICH PARA TRATAR SOLO UN-
                                             MASTERCATALOG           */
 SET CONT1 = 0                            /* CONTADOR DE CATALOGOS   */
 SET CONT2 = 0                            /* CONTADOR DE VVDS        */
 ALLOC FI(SYSPRINT)  NEW SPACE(1 1) TRACKS REUSE
 ALLOC FI(SYSIN)  NEW SPACE(1 1) TRACKS REUSE
 OPENFILE SYSIN OUTPUT
 SET &SYSIN = &STR( LISTC UCAT)
 PUTFILE SYSIN                            /* ENTRADA PARA IDCAMS     */
 CLOSFILE SYSIN
 CALL 'SYS1.LINKLIB(IDCAMS)'
 ISPEXEC TBCREATE TBUCAT NAMES(CONT1 VUCAT) NOWRITE REPLACE
 OPENFILE SYSPRINT
 SET &FINFIC = FSYSPR1
INICIO: -
 GETFILE SYSPRINT
 IF &LENGTH(&SYSPRINT) < 19 THEN GOTO INICIO
       /* RECHAZAMOS LAS LINEAS DE LISTADO DE MENOS DE 19  CARACTERES*/
 IF &SUBSTR(1:8,&STR(&SYSPRINT)) NE &STR(0USERCAT) THEN -
 DO                          /* BUSCAMOS EL NOMBRE DEL MASTER CATALOG*/
   IF &LENGTH(&SYSPRINT) < 37 THEN GOTO INICIO
                             /* SI ES MENOR DE 37 NO ES MASTERCATALOG*/
   IF &SW1 = 1 THEN GOTO INICIO
       /* SI YA TENEMOS EL NOMBRE DEL MASTER CATALOG RECHAZAMOS      */
   IF &SUBSTR(31:37,&STR(&SYSPRINT)) NE &STR(LISTING) THEN GOTO INICIO
                     /* SI NO ES MASTERCATALOG LEEMOS NUEVO REGISTRO */
   SET SW1 = 1                          /* MASTER CATALOG ENCONTRADO */
   SET LL1 = &LENGTH(&SYSPRINT)           /* PONEMOS EL NOMBRE DEL   */
   SET VUCAT = &SUBSTR(55:&LL1,&SYSPRINT) /* MCAT EN VARIABLE        */
   GOTO S0001
 END
 SET LL1 = &LENGTH(&SYSPRINT)             /* PONEMOS EL NOMBRE DEL   */
 SET VUCAT = &SUBSTR(18:&LL1,&SYSPRINT)   /* UCAT EN VARIABLE        */
S0001: -
 SET &CONT1 = &CONT1 + 1
 ISPEXEC TBADD TBUCAT                     /* INSERTAMOS MCAT O UCAT */
 GOTO INICIO
FSYSPR1:                              /* FINAL LISTC DE IDCAMS   */ -
 CLOSFILE SYSPRINT
 SET &MAXUCAT = &CONT1                    /* NUMERO DE CATALOGOS     */
       /* EN ESTE MOMENTO TENEMOS LA TABLA TBUCAT CON TODOS LOS CATA  -
          LOGOS EXISTENTES, SIEMPRE QUE ESTEN EN EL MASTERCATALOG    */
 ISPEXEC TBTOP TBUCAT
 ISPEXEC TBCREATE TVDS NAMES(CONT1 VUCAT CONT2 VOL VVVDS) NOWRITE -
  REPLACE
PROCTB: -
 ISPEXEC TBSKIP TBUCAT
 ISPEXEC TBGET TBUCAT
 OPENFILE SYSIN OUTPUT
 SET &SYSIN = &STR( LISTC LVL(SYS1.VVDS) CAT(&VUCAT))
 PUTFILE SYSIN                            /* ENTRADA PARA IDCAMS     */
 CLOSFILE SYSIN
 SET CC = 0
 CALL 'SYS1.LINKLIB(IDCAMS)'
 IF &CC > 0 THEN DO
                 ISPEXEC TBDELETE TBUCAT
   WRITE PROBLEM WITH IDCAMS: LISTC LVL(SYS1.VVDS) CAT(&VUCAT)
                 GOTO PROCTB
                 END
 ISPEXEC TBCREATE TVDS&CONT1 NAMES(CONT2) NOWRITE REPLACE
 OPENFILE SYSPRINT
 SET &FINFIC = FSYSPR2
PROCVVDS:                       /* TRATAMIENTO DEL LISTADO DE VVDS */ -
 GETFILE SYSPRINT
 IF &LENGTH(&SYSPRINT) < 19 THEN GOTO PROCVVDS
       /* RECHAZAMOS LAS LINEAS DE LISTADO DE MENOS DE 19  CARACTERES*/
 IF &SUBSTR(1:8,&STR(&SYSPRINT)) NE &STR(0CLUSTER) THEN GOTO PROCVVDS
 SET LL1 = &LENGTH(&SYSPRINT)             /* PONEMOS EL NOMBRE DEL   */
 SET VVVDS = &SUBSTR(18:&LL1,&SYSPRINT)   /* VVDS EN VARIABLE        */
S0002: -
 SET CONT2 = &CONT2 + 1
 SET LL2 = &LENGTH(&VVVDS)
 SET VOL = &SUBSTR(12:&LL2,&VVVDS)
 ISPEXEC TBPUT TBUCAT               /* INSERTAMOS UCAT A TABLA   */
 ISPEXEC TBADD TVDS&CONT1           /* INSERTAMOS VVDS A TABLA   */
 ISPEXEC TBADD TVDS                 /* INSERTAMOS VVDS A TABLA   */
 GOTO PROCVVDS
FSYSPR2:                                /* FINAL LISTC DE IDCAMS   */ -
 CLOSFILE SYSPRINT
 IF &CONT1 NE &MAXUCAT THEN GOTO PROCTB
       /* EN ESTE MOMENTO TENEMOS TANTAS TABLAS COMO CATALOGOS EXISTAN-
          CADA UNA DE ELLAS CON LOS VVDS DE CADA CATALOGO, EL NOMBRE  -
          DE CADA TABLA EN TVDSXX SIENDO XX UN NUMERO DE CATALOGO    */
 /* VER SI VOLUMEN OFFLINE */
 SET &MAXVVDS = &CONT2                    /* NUMERO DE VVDS'S        */
 ISPEXEC TBTOP TVDS
 SET VOLOFF = NO
 ISPEXEC TBCREATE TOFF NAMES(OUCAT OVOL OVVDS) NOWRITE -
  REPLACE
NEXTVVDS:                               /* VVDS PROCESS            */ -
 ISPEXEC TBSKIP TVDS
 ISPEXEC TBGET TVDS
 LISTDSI &VVVDS VOLUME(&VOL)
 IF LASTCC = 16 THEN DO
     IF &SYSREASON = 22 THEN DO
     SET VOLOFF = YES
     SET OUCAT = &VUCAT
     SET OVOL  = &VOL
     SET OVVDS = &VVVDS
     ISPEXEC TBADD TOFF                 /* INSERTAMOS VOL A TABLA   */
                             END
                     END
 IF &CONT2 NE &MAXVVDS THEN GOTO NEXTVVDS
 /* FIN DE VER SI VOLUMEN OFFLINE */
 /* ISPEXEC VGET (ZSCREEN)
 ALLOC FI(ISPWRK&ZSCREEN) NEW SPACE(5 5) CYL  REUSE -
  LRECL(80) RECFM(F,B) RELEASE */
 ALLOC FI(ISPFILE) NEW SPACE(5 5) CYL  REUSE -
  LRECL(80) RECFM(F,B) RELEASE
 ISPEXEC FTOPEN
 ISPEXEC FTINCL VVDS
 ISPEXEC FTCLOSE
 ISPEXEC LMINIT DATAID(DTAID) DDNAME(ISPFILE)
 ISPEXEC EDIT DATAID(&DTAID)
 FREE FI(ISPFILE)
 ALLOC DA(*) FI(SYSPRINT) REUSE
 ALLOC DA(*) FI(SYSIN) REUSE
/************************** END CLIST VVDS ***************************/
