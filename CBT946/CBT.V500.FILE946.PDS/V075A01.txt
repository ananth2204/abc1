Purging entries from your reader queue

Use this EXEC to purge entries from your reader queue by
jobname (partial or full), class, date, or number of records. If
you choose to purge entries by class, date, or records you can
use the comparator '< >' or '='. This EXEC also purges entries
by spool-id.

Examples of its usage are shown in Figure 1.

This EXEC can be used more conveniently if you have a single
character synonym for it. You can issue this EXEC from CMS
or from the RDRLIST command area.


PURGERDR EXEC

/*  REXX to purge entries in the     */
/*  reader queue                     */
parse source . . fn ft fm syn .
arg name
hil='1de8'x;nor='1d60'x
if name='?' | name='' then signal help
name.save=name
'EXECIO * CP ( STRING QUERY SET'
do until hit='yes'
 pull line
 if index(line,'IMSG')>0 then do
  do i=1 by 1 until words(line)
  if word(line,i)='IMSG' then do
   imsgset=word(line,i+1)
   leave
   end
  end
  desbuf
  hit='yes'
  end
end
'cp set imsg off'
xit='yes'
select
 when name='RDR ALL'               then 'CP PURGE RDR ALL'
 when datatype(name,'w')           then 'CP PURGE RDR' name
 when verify(name,'0123456789 ')=0 then 'CP PURGE RDR' name
 otherwise xit='no'
end
if xit='yes' then do
 'cp set imsg ' imsgset
 exit
 end
purge_ind='no'
symbol=0
if verify(name,'><=','m')>0 then call nojob_purge
  else call jobname_purge
len=length(name)
EXECIO '*' CP '(STRING Q RDR ALL'
pull header
if header='NO RDR FILES' then do
  say 'Your reader queue is empty'
  'cp set imsg ' imsgset
  exit 999
  end
do while queued() ¬= 0
 pull . 9 spoolid class . records . . date . 53 rdrentry 62 .
 jobname=strip(rdrentry)
 hit=0
 if symbol>0 then do
    if ide='DATE' then  date=translate(date,' ','/')
    interpret 'if' value(ide) com val 'then hit=1'
    end
  else do
   select
    when length(jobname)<len then iterate
    when ind='end' then key=substr(jobname,1,len)
    when ind='begn' then key=substr(jobname,length(jobname)-len+1,len)
    when ind='mid' then hit=index(jobname,name)
    otherwise key=jobname
    end
   end
 if key=name | hit>0 then do
   set cmstype ht
   purge rdr spoolid
   set cmstype rt
   if symbol=0 then mess=jobname spoolid
    else mess=jobname spoolid ide'='value(ide)
   say mess 'purged from reader queue'
   purge_ind='yes'
   end
end
if purge_ind='no' then do
 if symbol=0 then say 'There is no entry for 'name.save' in RDR queue'
  else say 'No hits in the RDR queue for your selection ' name.save
  end
'cp set imsg ' imsgset
if fn=translate(syn) then queue 'refresh'
                                 /* if you have not set a synonym  */
                                 /* for this EXEC, delete this line*/
exit
/* -----------------------------------------------------------------*/
jobname_purge:
if length(name) > 8 then name=left(name,8)
len=length(strip(name))
select
 when right(name,1)='*' & left(name,1)='*' then do
  ind='mid'
  name=substr(name,2,len-2)
  end
 when right(name,1)='*' then do
  ind='end'
  name=substr(name,1,len-1)
  end
 when left(name,1)='*' then do
  ind='begn'
  name=substr(name,2,len-1)
  end
 otherwise ind='full'
end
return
nojob_purge:
symbol=verify(name,'><=','m')
ide=strip(substr(name,1,symbol-1))
com=substr(name,symbol,1)
val=strip(substr(name,symbol+1))
errmsg=''
select
 when ide='CLASS' & length(val)¬=1 then errmsg='Invalid Class'
 when ide='RECORDS' & ¬datatype(val,'W') then errmsg='Invalid no of records'
 when ide='DATE' & val='TODAY' then do
   if com='>' then errmsg='Date Cannot be > Today'
  end
 when ide='DATE' then call datechk
 when ,
pos('*'ide'*','*CLASS*DATE*RECORDS*')=0 then errmsg='Invalid Parameters Present'
 otherwise nop
end
if errmsg¬='' then do
  say errmsg
  'cp set imsg ' imsgset
  exit 999
  end
if val='TODAY' then val=substr(date(e),4,2)' 'substr(date(e),1,2)
if ide='DATE' then val=translate(val,' ','/')
return
datechk:
residue=left(date(s),4)//4
if residue=0 & left(date(j),2)>0 then leap='y'
 else leap='n'
mm=strip(substr(val,1,2),'L','0')
dd=strip(substr(val,4,2),'L','0')
days.1=31; days.3=31; days.5=31; days.7=31; days.8=31; days.10=31;
days.12=31; days.4=30; days.6=30; days.9=30; days.11=30
days.2=28
select
 when mm<1  then errmsg='Month cannot be < 1'
 when mm>12 then errmsg='Month cannot be > 12'
 when dd<1  then errmsg='Date cannot be < 1'
 when leap='n' & dd>days.mm then errmsg='Date cannot be >' days.mm
 when leap='y' & dd>days.mm+1 then errmsg='Date cannot be >' days.mm+1
 otherwise nop
end
return
help:
queue 'This EXEC purges entries from your reader queue.'
queue 'It can purge entries with jobname (full or partial), date'
queue 'class or no. of records.'
queue 'For class, records, and date, the comparator can be < > or ='
queue 'It can also purge single entry / multiple entries with spool-id.'
queue 'Set a synonym of P in your USER SYNONYM file for this EXEC'
queue ''
queue hil'P spoolid   'nor'------> purge a single entry                '
queue hil'P sp1 sp2   'nor'------> purge more than one entry -spoolids '
queue hil'P TESTCICS  'nor'------> purge all entries for TESTCICS      '
queue hil'P TEST*     'nor'------> purge all entries starting with TEST'
queue hil'P *TEST     'nor'------> purge all entries   ending with TEST'
queue hil'P *FIRD*    'nor'------> purge all entries which have FIRD   '
queue hil'P RDR ALL   'nor'------> Purge all the RDR queue entries     '
queue hil'P CLASS=S   'nor'------> Purge all entries for CLASS=S       '
queue hil'P DATE<TODAY'nor'------> Purge all entries if  DATE < TODAY  '
queue hil'P DATE>mm/dd'nor'------> Purge all entries if  DATE > mm/dd  '
queue hil'P RECORDS>n 'nor'------> Purge all entries if records > n    '
queue '----------------------------------------------------------------'
do until queued()=0
 parse pull line
 say center(line,72)
end
exit

Moyeen Ahmed Khan
Systems Analyst
Qatar General Petroleum Corporation (Qatar)                    c QGPC 1992

