 Extracting SPM source
In our installation we use SPM as the editor under VSE.  Many of our programmers
discovered the ease with which files can be edited using the editor under CMS.
This led to the moving of files from SPM to CMS.  To do this a CMS DITTO had to
be done.  Before this could be done, all '* $$' statements had to be changed to
'* SS' so that it would not run.
One of the utility packages under SPM is 'SPMBATCH'.  This creates a listing of
job control or program listings without having to change anything.  The
following program runs under CMS and generates a screen giving the options for
the library (such as 'JPIN' or 'TPIN').  There is a space for name, password,
version, and level.  All one does is enter the information and press ENTER.  The
proper job control statements are spooled to VSE.  The listing comes back in
about three seconds average.  Press PF12 to see if it has been returned.  If it
has you will enter the RDRLIST.  Here you must enter the command 'CONVERT' to
put this file in your CMS FILELIST.  This will give a filename of whatever the
SPM name was, and a filetype of 'JCL' with the version and level numbers tagged
onto the end.  When you exit the RDRLIST you will be brought back to the
original screen.  From here filelist can be entered by pressing PF11.
The format of the command is 'SPM name PSWD version level SPMLIB'.  If the
correct number of arguments are not entered, a screen will prompt these things.
The use of the argument function capabilities is useful when extracting jobs
from SPM using a batch method.  The EXEC at the very bottom called 'GETSPM EXEC'
utilises this capability.  To use this, key 'GETSPM'; everything else is
explained.
Figure 1 shows what the screen looks like for the 'SPM' command without any
arguments.
SPM EXEC
&TRACE
&A = &N
&IF &A = 5 &STACK &1 &2 &3 &4 &5 YES
&IF &A Â¬= 5 &STACK * * * * * NO
XEDIT TEST TEMP A1 (PROFILE SPM
&EXIT

SPM XEDIT
&TRACE
&READ VARS &NAME &PSWD &VERSION &LEVEL &SPMLIB &STOP
MAKEBUF
IDENTIFY (LIFO
&READ VARS &OWNER
DROPBUF
&IF &NAME Â¬= * &GOTO -SUBMIT
&EMSG = &BLANK
-TOP
&LIN = 10
&NAME = &BLANK
&PSWD = &BLANK
&VERSION = 01
&IF &OWNER = OP1 &SPMLIB = J
&IF &OWNER Â¬= OP1 &SPMLIB = S
&LEVEL = 01
CP SET EMSG OFF
COMMAND TRANSFER TERMINAL
&READ VARS &TERM
&IF &TERM = TYPEWRITER &CALL -ENDMACRO
&PANEL = 0
&CASE U
&COMMAND MAKEBUF
COMMAND SET CMDL OFF
COMMAND SET TOFEOF OFF
COMMAND SET LRECL 100
COMMAND SET TRUNC 100
COMMAND SET WRAP ON
COMMAND SET COLOR MSG WHI BLI NOHIGH
COMMAND SET STAY ON
COMMAND SET SCALE OFF
 COMMAND SET LINEND OFF
COMMAND SET PREFIX OFF
COMMAND SET CASE MIXED IGNORE
COMMAND SET CTLCHAR ! ESCAPE
COMMAND SET CTLCHAR " PROTECT NOHIGH
COMMAND SET CTLCHAR  NOPROTECT TUR UND NOHIGH
COMMAND SET CTLCHAR $ NOPROTECT RED UND NOHIGH
&I = 1
&LOOK 2 UNTIL &I > 24
COMMAND SET PF&I *
&I = &I + 1
SET PA2 NULLKEY
COMMAND TRANSFER LSCREEN
&READ VARS &SCREEN
&SCREENM2 = &SCREEN - 4
&SCREENM4 = &SCREEN - 4
-BEGIN
&CALL -PANEL
&REV = NON
&HELP1 = &BLANK
&HELP2 = &BLANK
&HELP3 = &BLANK
&HELP4 = &BLANK
&HELP5 = &BLANK
&EMSG = &BLANK
COMMAND READ ALL TAG
&COMMAND SENTRIES
&NUMLINE = &RC
&LOOP -END &NUMLINE
&READ ARGS
&GOTO -&1
-PFK
&PFKEY = &2
&IF &PFKEY = 1 &GOTO -HELP
&IF &PFKEY = 2 &GOTO -TOP
&IF &PFKEY = 3 &GOTO -ENDMACRO
&IF &PFKEY = 4 &GOTO -BEGIN
&IF &PFKEY = 11 CMS FILELIST * JCL * A
&IF &PFKEY = 11 &IF &RC Â¬= 0 &EMSG = &STRING OF NO FILES IN YOUR FILELIST
&IF &PFKEY = 11 &GOTO -TOP
&IF &PFKEY = 12 CMS RDRLIST
&IF &PFKEY = 12 &IF &RC Â¬= 0 &EMSG = &STRING OF NO FILES IN YOUR READER
&IF &PFKEY = 12 &GOTO -TOP
&EMSG = &STRING OF PF &2 IS INVALID
&GOTO -BEGIN
-ETK &GOTO -END
-RES
&IF &2 = 08 &GOTO -SPMLIB
&IF &2 = 10 &GOTO -NAME
&IF &2 = 12 &GOTO -PSWD
&IF &2 = 14 &GOTO -VERSION
&IF &2 = 16 &GOTO -LEVEL
&GOTO -END
-SPMLIB
&IF .&4 = . &SPMLIB = &BLANK
&IF .&4 Â¬= . &SPMLIB = &4
&GOTO -END
-NAME
&IF .&4 = . &NAME = &BLANK
&IF .&4 Â¬= . &NAME = &4
&GOTO -END
-PSWD
&IF .&4 = . &PSWD = &BLANK
&IF .&4 Â¬= . &PSWD = &4
&GOTO -END
-VERSION
 &IF .&4 = . &VERSION = &BLANK
&IF .&4 Â¬= . &VERSION = &4
&GOTO -END
-LEVEL
&IF .&4 = . &LEVEL = &BLANK
&IF .&4 Â¬= . &LEVEL = &4
-END
&IF &NAME = &BLANK &EMSG = &STRING OF PLEASE ENTER THE NAME
&IF &NAME = &BLANK &LIN = 10
&IF &NAME = &BLANK &GOTO -BEGIN
&IF &NAME = ******** &NAME = &BLANK
&IF &PSWD = &BLANK &PSWD = &NAME
&IF &PSWD = ******** &PSWD = &BLANK
&IF &SPMLIB = &BLANK &EMSG = &STRING OF PLEASE ENTER SPMLIB
&IF &SPMLIB = &BLANK &LIN = 08
&IF &SPMLIB = &BLANK &GOTO -BEGIN
&IF &VERSION = &BLANK &EMSG = &STRING OF ENTER VERSION AND/OR LEVEL NO
&IF &VERSION = &BLANK &LIN = 14
&IF &VERSION = &BLANK &GOTO -BEGIN
&IF &LEVEL = &BLANK &EMSG = &STRING OF ENTER VERSION AND/OR LEVEL NO
&IF &LEVEL = &BLANK &LIN = 16
&IF &LEVEL = &BLANK &GOTO -BEGIN
&DTV = &DATATYPE OF &VERSION
&DTL = &DATATYPE OF &LEVEL
&IF &DTV Â¬= NUM &EMSG = &STRING OF VERSION MUST BE NUMERIC
&IF &DTV Â¬= NUM &LIN = 14
&IF &DTV Â¬= NUM &GOTO -BEGIN
&IF &DTL Â¬= NUM &EMSG = &STRING OF LEVEL MUST BE NUMERIC
&IF &DTL Â¬= NUM &LIN = 16
&IF &DTL Â¬= NUM &GOTO -BEGIN
&IF &SPMLIB = J &GOTO -SUBMIT
&IF &SPMLIB = S &GOTO -SUBMIT
&IF &SPMLIB = F &GOTO -SUBMIT
&IF &SPMLIB = T &GOTO -SUBMIT
&IF &SPMLIB = N &GOTO -SUBMIT
&EMSG = &STRING OF &SPMLIB IS NOT A VALID SPM LIBRARY
&LIN = 08
&GOTO -BEGIN
-PANEL
&COMMAND CONWAIT
&COMMAND DESBUF
MAKEBUF
EXECIO * CP (STRING Q TIME
&READ VARS * * &TIM * * &DAT
&READ VARS * * * * * *
DROPBUF
&IF &EMSG Â¬= &BLANK COMMAND EMSG &EMSG
&LINE = &LINE + 1
&X = &STRING OF                               SPM SOURCE LIBS
COMMAND SET RESERVED 1 GRE REV HIGH &X
COMMAND SET RESERVED 2 GRE NON HIGH &BLANK
&X = &STRING OF                         TIME &TIM   DATE &DAT
COMMAND SET RESERVED 3 BLU NON HIGH &X
COMMAND SET RESERVED 4 GRE NON HIGH &BLANK
&LINE = 5
&LOOP 2 UNTIL &LINE > 9
COMMAND SET RESERVED &LINE RED NON HIGH &BLANK
&LINE = &LINE + 1
&A = &LEFT OF &SPMLIB 1
&A = &CONCAT OF ! &A !"
&X = &STRING OF                                  SPM LIB - &A
COMMAND SET RESERVED 08 RED NON HIGH &X
COMMAND SET RESERVED 09 FRE NON HIGH &BLANK
&A = &LEFT OF &NAME 8
&A = &CONCAT OF !$ &A !"
&X = &STRING OF                                     NAME - &A
 COMMAND SET RESERVED 10 BLU NON HIGH &X
COMMAND SET RESERVED 11 GRE NON HIGH &BLANK
&A = &LEFT OF &PSWD 8
&A = &CONCAT OF !$ &A !"
&X = &STRING OF                          PASSWORD - &A (NAME IS DEFAULT)
COMMAND SET RESERVED 12 BLU NON HIGH &X
COMMAND SET RESERVED 13 &BLANK
&A = &LEFT OF &VERSION 2
&A = &CONCAT OF ! &A !"
&X = &STRING OF                                  VERSION - &A
COMMAND SET RESERVED 14 YEL NON HIGH &X
COMMAND SET RESERVED 18 &BLANK
&A = &LEFT OF &LEVEL 2
&A = &CONCAT OF ! &A !"
&X = &STRING OF                                    LEVEL - &A
COMMAND SET RESERVED 16 YEL NON HIGH &X
COMMAND SET RESERVED 17 &BLANK
COMMAND SET RESERVED 18 GRE &REC HIGH &HELP1
COMMAND SET RESERVED 19 GRE &REV HIGH &HELP2
COMMAND SET RESERVED 20 GRE &REV HIGH &HELP3
COMMAND SET RESERVED 21 GRE &REV HIGH &HELP4
COMMAND SET RESERVED 22 GRE &REV HIGH &HELP5
COMMAND SET RESERVED 23 PIN UND HIGH &BLANK
&X = &STRING OF ENTER = SUBMIT  |  PF: 1-HELP  2-RESET  3-QUIT
        11-FILELIST   12-RDRLIST
COMMAND SET RESERVED 24 PIN UND HIGH &X
COMMAND CURSOR SCREEN &LIN 45
&RETURN
-SUBMIT
CP CLOSE PUNCH PURGE
&NAME = &LEFT OF &NAME 8
&PSWD = &LEFT OF &PSWD 8
&LIBMEM = &CONCAT OF <LIST>D &PSWD &NAME &VERSION &LEVEL
&LSTCARD = &CONCAT OF DISP=D,CLASS=R,DEST=(, &OWNER )
MAKEBUF
&STACK * $$ JOB JNM=LISTSPM,DISP=D,CLASS=W
&STACK * $$ LST &LSTCARD
&STACK // JOB EXTRACT &NAME FROM PROGD &SPMLIB FOR USER &OWNER
&STACK // LIBDEF PHASE,SEARCH=SP2LIB.SPM
&IF &SPMLIB = J &CALL -PROGDJ
&IF &SPMLIB = S &CALL -PROGDS
&IF &SPMLIB = T &CALL -PROGDT
&IF &SPMLIB = F &CALL -PROGDF
&IF &SPMLIB = N &CALL -PROGDN
&STACK &DLBL
&STACK &EXTENT
&STACK &ASSGN
&STACK // EXEC SPMBATCH
&STACK &LIBMEM
&STACK /*
&STACK /&
&STACK * $$ EOJ
SENTRIES
&LINES = &RC
CP SPOOL PUN TO VSEIPO
EXECIO &LINES PUNCH
CP SET MSG OFF
CP CLOSE PUNCH
CP SET MSG ON
CP SPOOL PUN FOR *
&IF &STOP = YES &GOTO -ENDMACRO
&HELP3 = &STRING OF        &NAME &PSWD &LEVEL &VERSION IS BEING
          EXTRACTED FROM SPM &SPMLIB
&REV = REV
EXECIO * CP (STRING Q TIME
&READ VARS * * &T * * &D
 &READ VARS * * * * * *
&EXSPM = &STRING OF &NAME &PSWD &LEVEL &VERSION &SPMLIB &T &D SPM
&IF &STOP = NO CMS EXECIO 1 DISKW SPM HISTORY A1 (FINIS STRING &EXSPM
&GOTO -TOP
* =========================================== *
*   ALL SPM LIBRARY PARAMETERS ARE HERE       *
* =========================================== *
-PROGDS
&DLBL = &STRING OF // DLBL PROGDS,'PROGRAM DATA SET',99/365,DA
&EXTENT = &STRING OF // EXTENT SYS013,SYSWKS,1,0 5250,3225
&ASSGN = &STRING OF // ASSGN SYS013,DISK,VOL=SYSWKS,SHR
&RETURN
-PROGDJ
&DLBL = &STRING OF // DLBL PROGDS,'PROGDJ DATA SET',99/365,DA
&EXTENT = &STRING OF // EXTENT SYS013,SYSWKD,1,0,465,540
&ASSGN = &STRING OF // ASSGN SYS013,DISK,VOL=SYSWKF,SHR
&RETURN
-PROGDF
&DLBL = &STRING OF // DLBL PROGDS,'PROGDF DATA SET',99/365,DA
&EXTENT = &STRING OF // EXTENT SYS013,SYSWKC,1,0,11820,180
&ASSGN = &STRING OF // ASSGN SYS013,DISK,VOL=SYSWKC,SHR
&RETURN
-PROGDT
&DLBL = &STRING OF // DLBL PROGDS,'PROGDT DATA SET',99/365,DA
&EXTENT = &STRING OF // EXTENT SYS013,SYSWKD,1,0,15,540
&ASSGN = &STRING OF // ASSGN SYS013,DISK,VOL=SYSWKD,SHR
&RETURN
-PROGDN
&DLBL = &STRING OF // DLBL PROGDS,'PROGDN DATA SET',99/365,DA
&EXTENT = &STRING OF // EXTENT SYS013,SYSWKC,1,0,12000,75
&ASSGN = &STRING OF // ASSGN SYS013,DISK,VOL=SYSWKC,SHR
&RETURN
-HELP
&HELP1 = &STRING OF IF NAME OF PASSWORD MUST BE BLANK,
           ENTER 8 '*' IN EITHER OR BOTH ENTRIES.
&HELP2 = &STRING OF VERSION AND LEVEL MUST BE NUMERIC.
           PASSWORD DEFAULT IS WHATEVER NAME IS.
&HELP3 = &STRING OF PRESS PF12 TO ENTER YOUR RDRLIST
           AND ENTER CONVERT NEXT TO THE LISTSPM FILES.
&HELP4 = &STRING OF TO CONVERT MORE THAN ONE FILE AT A TIME,
           ENTER '=' NEXT TO EACH ONE.
&HELP5 = &STRING OF            ====>  PRESS PF4 TO RESUME
           NORMAL DISPLAY PANEL  <====
&REV = REV
&GOTO -BEGIN
-ENDMACRO
DROPBUF
QQUIT
&EXIT

CONVERT EXEC
/* */
ARG NO
PARSE SOURCE PULL ENV EXNAME ENV
IF NO='' THEN SIGNAL NOTRDRL
'SET ISMG OFF'
'CP CH RDR 'NO 'NOHOLD'
'ORDER RDR 'NO
'SET IMSG ON'
'DROPBUF'
'MAKEBUF'
'EXECIO * CP (STRING Q RDR 'NO 'ALL'
PULL JUNK
PULL . . . . RECS . . . . RLNAME . .
RECS=RECS-5
'DROPBUF'
 'MAKEBUF'
DO 4
'EXECIO 1 CARD'
PULL A
IF SUBSTR(A,1,14)='***SPMBATCH***' THEN CALL LISTSPM
END
IF LISTÂ¬='YES' THEN SIGNAL ERROR
DO RECS
'EXECIO 1 CARD'
PULL A
LIN=SUBSTR(A,19,80)
'EXECIO 1 DISKW' FNAME FTYPE FMODE '0 F 80 (STRING 'LIN
END
'FINIS 'FNAME FTYPE FMODE
CALL CLOSERDR
SIGNAL QUIT
LISTSPM:
LIST='YES'
FNAME=SUBSTR(A,34,8)
FTYPE=SUBSTR(A,42,4)
FTYPE='JCL'||FTYPE
FMODE='A1'
'DROPBUF'
'MAKEBUF'
'LISTFILE * * A (STACK)'
DO QUEUED()
PULL FN FT FM
IF FN=FNAME & FT=FTYPE THEN SIGNAL EXISTS
END
RETURN
CLOSERDR:
'SET IMSG OFF'
'CP SPOOL RDR NOCONT NOHOLD'
'CP CLOSE RDR'
'CP SPOOL RDR NOCONT NOHOLD'
'SET IMSG ON'
RETURN
NOTRDRL:
MSG='PLEASE EXECUTE THIS FROM RDRLIST OR ENTER ID NUMBER AFTER COMMAND'
SAY MSG
SIGNAL QUIT
ERROR:
MSG='THIS IS NOT A LISTSPM FILE'
SAY MSG
SIGNAL SAVERDR
EXISTS:
MSG= FILE FNAME FTYPE 'ALREADY EXISTS'
SAY MSG
SAVERDR:
'SET IMSG OFF'
'CP SPOOL RDR CONT HOLD'
'CP CLOSE RDR'
'CP CH RDR 'NO 'NOHOLD'
'CP SPOOL RDR NOCONT NOHOLD'
'SET IMSG ON'
QUIT:
'DROPBUF'
EXIT

GETSPM EXEC
&TRACE
-TOP
CLRSCRN
&BEGTYPE -ENDTYPE
*======================================================================*
THIS WILL READ A FILE CALLED SPM FILES AND RETURN THE FILES FROM SPM
 ACCORDING TO THE NAMES IN 'SPM FILES'.  THE FILE SHOULD CONTAIN THE
FOLLOWING INFORMATION:  'NAME PASSWORD LEVEL VERSION SPMLIB' ON EACH
LINE OF THE FILE, WITH NO BLANK LINES.  IF THE FILE CONTAINS ONLY THE
NAME, THEN THE PASSWORD IS WHATEVER THE NAME IS, THE VERSION IS '01',
THE LEVEL IS '01', AND THE SPMLIB IS 'J'.  IF YOU WISH TO CHANGE THE
DEFAULTS FOR VERSION, LEVEL, AND SPMLIB, THEN ENTER THE DEFAULTS IN THAT
ORDER.  IF YOU WISH FOR THE DEFAULTS TO BE 01 01 J THEN PRESS ENTER KEY.
TO MODIFY OR CREATE 'SPM FILES', ENTER 'X'.
*======================================================================*
TO EXIT THIS ENTIRELY, ENTER 'Q'.
-ENDTYPE
&READ VARS &D1 &D2 &D3
&IF .&D1 = . &D1 = 01
&IF &D1 = Q &GOTO -EXIT
&IF &D1 = X &GOTO =XEDIT
&IF .&D2 = . &D2 = 01
&IF .&D3 = . &D3 = J
&X = 0
MAKEBUF
EXECIO * DISKR SPM FILES A1 (FINIS
&IF &RC Â¬= 0 &GOTO -EXIT
SENTRIES
&LINES = &RC
&LOOP -ENDASIGN &LINES
&X = &X + 1
&N = &CONCAT OF N &X
&P = &CONCAT OF P &X
&V = &CONCAT OF V &X
&L = &CONCAT OF L &X
&S = &CONCAT OF S &X
&READ VARS &&N &&P &&V &&L &&S
&IF .&&P = . &&P = &&N
&IF .&&V = . &&V = &D1
&IF .&&L = . &&L = &D2
&IF .&&S = . &&S = &D3
-ENDASIGN
&X = 0
CLRSCRN
&LOOP -ENDEXEC &LINES
&X = &X + 1
&N = &CONCAT OF N &X
&P = &CONCAT OF P &X
&V = &CONCAT OF V &X
&L = &CONCAT OF L &X
&S = &CONCAT OF S &X
EXEC SPM &&N &&P &&V &&L &&S
&TYPE &&N &&P &&V &&L BEING EXTRACTED FROM SPM &&S
EXECIO * CP (STRING Q TIME
&READ VARS * * &T * * &D
&READ VARS * * * * * *
EXECIO 1 DISKW SPM HISTORY A1 (STRING &&N &&P &&V &&L &&S &T &D GETSPM
-ENDEXEC
DROPBUF
-EXIT
&EXIT
-XEDIT
XEDIT SPM FILES A1
&GOTO -TOP
This 'JOBCON EXEC' is useful for 'jobbing' from CMS.  This EXEC will read the
JCL file from the FILELIST and spool it to VSE with conditional JCL inserted in
the proper places.

JOBCON EXEC
&TRACE
CLRSCRN
&IF &N Â¬= 3 &TYPE PLEASE ENTER FILENAME FILETYPE FILEMODE BESIDE COMMAND
 &IF &N Â¬= 3 &GOTO -EXIT
&F = &1
&T = &2
&M = &3
DROPBUF
MAKEBUF
EXECIO * DISKR &F &T &M (FINIS
SENTRIES
&LINES = &RC
CP CLOSE PUNCH PURGE
CP SPOOL PUN TO VSEIPO
-CONDIT
&FIRST = &STRING OF // EXEC
&SECOND = &STRING OF /*
&LOOP -ENDCLN &LINES
&READ STRING &JCL
&TEST1 = &LEFT OF &JCL 7
&TEST2 = &LEFT OF &JCL 2
&IF &TEST1 = &FIRST &CALL -WRITABEN
&IF &TEST2 = &SECOND &CALL -WRITLABL
&IF &TEST1 = &FIRST EXECIO 1 PUNCH (STRING &JCL
&IF &TEST1 Â¬= &FIRST &IF &TEST2 Â¬= &SECOND EXECIO 1 PUNCH (STRING &JCL
-ENDCLN
&GOTO -FINISH
-WRITABEN
&X = &STRING OF // ON $ABEND GOTO ABERROR
EXECIO 1 PUNCH (STRING &X
&X = &STRING OF // ON $CANCEL GOTO CANERROR
EXECIO 1 PUNCH (STRING &X
&X = &STRING OF // ON $RC Â¬= 0 GOTO RCERROR
EXECIO 1 PUNCH (STRING &X
&RETURN
-WRITLABL
EXECIO 1 PUNCH (STRING &JCL
&X = &STRING OF // GOTO END
EXECIO 1 PUNCH (STRING &X
&X = &STRING OF /. ABERROR
EXECIO 1 PUNCH (STRING &X
&X = &STRING OF // PAUSE **** THERE WAS AN ABEND IN THIS PROGRAM ****
EXECIO 1 PUNCH (STRING &X
&X = &STRING OF // GOTO END
EXECIO 1 PUNCH (STRING &X
&X = &STRING OF /. CANERROR
EXECIO 1 PUNCH (STRING &X
&X = &STRING OF // PAUSE **** THERE WAS A CANCEL IN THIS PROGRAM ****
EXECIO 1 PUNCH (STRING &X
&X = &STRING OF // GOTO END
EXECIO 1 PUNCH (STRING &X
&X = &STRING OF /. RCERROR
EXECIO 1 PUNCH (STRING &X
&X = &STRING OF // PAUSE **** THERE WAS A RETURN CODE OTHER THAN 0 ****
EXECIO 1 PUNCH (STRING &X
&X = &STRING OF // GOTO END
EXECIO 1 PUNCH (STRING &X
&X = &STRING OF /. END
EXECIO 1 PUNCH (STRING &X
&RETURN
-HEADER
MAKEBUF
&HEADER = &STRING OF FILENAME FILETYPE FM   DATE     TIME
&STACK &HEADER
&HEADER = &STRING OF ======== ======== == ======== ========
&STACK &HEADER
EXECIO 2 DISKW JOBS SUBMITED A1 (FINIS
DROPBUF
&RETURN
 -FINISH
CP CLOSE PUNCH
CP SPOOL PUNCH FOR *
DROPBUF
&F = &LEFT  OF &F 8
&T = &LEFT  OF &T 8
MAKEBUF
EXECIO * CP (STRING Q TIME
&READ ARGS
&TIM = &3
&DAT = &6
LISTFILE JOBS SUBMITED A1
&IF &RC Â¬= 0 &CALL -HEADER
EXECIO 1 DISKW JOBS SUBMITED A1 (FINIS STRING &F &T &M &DAT &TIM (CONDITIONAL)
DROPBUF
-EXIT
&EXIT
This  'CONDIT EXEC' can be used to take normal JCL files from your FILELIST in
CMS and create another file with conditional JCL inserted.  Both this and
'JOBCON' were designed to be used from a FILELIST screen.  However, if you wish,
the arguments can be keyed next to the command.

CONDIT EXEC
&TRACE
&IF &N Â¬= 3 &GOTO -TOP
&FILENAME = &1
&FILETYPE = &2
&FILEMODE = &3
&GOTO -CONDIT
-TOP
CLRSCRN
&TYPE ENTER FILENAME FILETYPE FILEMODE
&TYPE ================================
&TYPE DEFAULT FILEMODE IS A1
&TYPE ENTER 'Q Q Q' TO QUIT
&READ VARS &FILENAME &FILETYPE &FILEMODE
&IF .&FILENAME = . &GOTO -TOP
&IF .&FILETYPE = . &GOTO -TOP
&IF .&FILEMODE = . &FILEMODE = A1
&IF &FILENAME = Q &IF &FILETYPE = Q &IF &FILEMODE = Q &GOTO -EXIT
-CONDIT
&WRIT = NO
&BUFFER *
MAKEBUF
EXECIO * DISKR &FILENAME &FILETYPE &FILEMODE (FINIS
SENTRIES
&LINES = &RC
&FIRST = &STRING OF // EXEC
&SECOND = &STRING OF /*
&LOOP -ENDCLN &LINES
&READ STRING &JCL
&TEST1 = &LEFT OF &JCL 7
&TEST2 = &LEFT OF &JCL 2
&IF &TEST1 = &FIRST &CALL -WRITABEN
&IF &TEST2 = &SECOND &CALL -WRITLABL
&IF &TEST1 = &FIRST EXECIO 1 DISKW &FILENAME JCL1 A1 (STRING &JCL
&IF &TEST1 Â¬= &FIRST &IF &TEST2 Â¬= &SECOND EXECIO 1 DISKW &FILENAME JCL1 A1
(STRING &JCL
-ENDCLN
-EXIT
FINIS &FILENAME JCL1 A1
DROPBUF
&EXIT
-WRITABEN
&X = &STRING OF // ON $ABEND GOTO ABERROR
EXECIO 1 DISKW &FILENAME JCL1 A1 (STRING &X
 &X = &STRING OF // ON $CANCEL GOTO CANERROR
EXECIO 1 DISKW &FILENAME JCL1 A1 (STRING &X
&X = &STRING OF // ON $RC Â¬= 0 GOTO RCERROR
EXECIO 1 DISKW &FILENAME JCL1 A1 (STRING &X
&RETURN
-WRITLABL
EXECIO 1 DISKW &FILENAME JCL1 A1 (STRING &JCL
&X = &STRING OF // GOTO END
EXECIO 1 DISKW &FILENAME JCL1 A1 (STRING &X
&X = &STRING OF /. ABERROR
EXECIO 1 DISKW &FILENAME JCL1 A1 (STRING &X
&X = &STRING OF // PAUSE **** THERE WAS AN ABEND IN THIS PROGRAM ****
EXECIO 1 DISKW &FILENAME JCL1 A1 (STRING &X
&X = &STRING OF // GOTO END
EXECIO 1 DISKW &FILENAME JCL1 A1 (STRING &X
&X = &STRING OF /. CANERROR
EXECIO 1 DISKW &FILENAME JCL1 A1 (STRING &X
&X = &STRING OF // PAUSE **** THERE WAS A CANCEL IN THIS PROGRAM ****
EXECIO 1 DISKW &FILENAME JCL1 A1 (STRING &X
&X = &STRING OF // GOTO END
EXECIO 1 DISKW &FILENAME JCL1 A1 (STRING &X
&X = &STRING OF /. RCERROR
EXECIO 1 DISKW &FILENAME JCL1 A1 (STRING &X
&X = &STRING OF // PAUSE **** THERE WAS A RETURN CODE OTHER THAN 0 ****
EXECIO 1 DISKW &FILENAME JCL1 A1 (STRING &X
&X = &STRING OF // GOTO END
EXECIO 1 DISKW &FILENAME JCL1 A1 (STRING &X
&X = &STRING OF /. END
EXECIO 1 DISKW &FILENAME JCL1 A1 (STRING &X
&RETURN
The 'JOB EXEC' simply reads the JCL from the FILELIST screen and spools it to
VSE.

JOB EXEC
&TRACE
CLRSCRN
&IF &N Â¬= 3 &TYPE PLEASE ENTER FILENAME FILETYPE FILEMODE BESIDE COMMAND
&IF &N Â¬= 3 &GOTO -EXIT
&F = &1
&T = &2
&M = &3
DROPBUF
MAKEBUF
EXECIO * DISKR &F &T &M (FINIS
SENTRIES
&LINES = &RC
CP CLOSE PUNCH PURGE
CP SPOOL PUN TO VSEIPO
EXECIO &LINES PUNCH
CP CLOSE PUNCH
CP SPOOL PUNCH FOR *
DROPBUF
&F = &LEFT  OF &F 8
&T = &LEFT  OF &T 8
MAKEBUF
EXECIO * CP (STRING Q TIME
&READ ARGS
&TIM = &3
&DAT = &6
LISTFILE JOBS SUBMITED A1
&IF &RC Â¬= 0 &CALL -HEADER
EXECIO 1 DISKW JOBS SUBMITED A1 (FINIS STRING &F &T &M &DAT &TIM
DROPBUF
-EXIT
&EXIT
-HEADER
 MAKEBUF
&HEADER = &STRING OF FILENAME FILETYPE FM   DATE     TIME
&STACK &HEADER
&HEADER = &STRING OF ======== ======== == ======== ========
&STACK &HEADER
EXECIO 2 DISKW JOBS SUBMITED A1 (FINIS
DROPBUF
&RETURN
Ray Dubcak
Computer Operator
Ralph Wilson Plastics (USA)   ) Ralph Wilson Plastics 1988























































