Scan a list of files for a character string

We have found the following EXEC and XEDIT routine quite
useful for scanning a list of CMS files for a certain string of data.
The files to be scanned are determined in the same manner as
listfile or filelist, thus the user may use * or % wildcard
characters.

It will query the user for the following options:
1   Scan files for a mixed-case character string
2   Display lines found on your CMS console
3   Scan only specific zones on each record.

If more than a certain number of records are to be scanned, it will
pause and ask if the user still wishes to proceed with what may be
a lengthy scan.  The limit is set to 20 000 records but this
parameter could be changed in the EXEC.

It was originally written as we were installing VM/SP 3, and we
still have not got around to converting it to REXX.


&TRACE OFF
*  NAME -      SCANALL
*  PURPOSE -   TO SCAN A LIST OF FILES FOR OCCURRENCE OF A CERTAIN
*              STRING OF CHARACTERS. PRODUCES A FILE CONTAINING
*              FILENAME,FILETYPE OF EACH FILE CONTAINING STRING, AND
*              RECORD CONTAINING STRING
     CLRSCRN
* .....FOLLOWING LINE SETS LIMIT OF RECORDS TO BE SCANNED WITHOUT
*                   QUERYING THE USER TO CONTINUE.
     &RECLIM  =  20000
     &FOUNDX = 0
     &IF  &N <  2  &GOTO -DSPLY1
     &IF  &N >  3  &GOTO -DSPLY1
     &LLN  = &1
     &LLT  = &2
     &FM   = &3
     &GOTO  -CHKFM
-DSPLY1
     &BEGTYPE -END1
                         SCANALL 1.1
   Please enter filename and filetype of list of files to be scanned.
 Enter asterisks where appropriate to produce desired list of files.
     -END1
     &READ VARS &LLN &LLT &FM
     &IF .&LLT = .  &EXIT
     &IF .&FM   = .  &FM = A
-CHKFM
     &IF  .&FM  ¬= .* &GOTO -ASK2
     &TYPE   CANNOT PROCESS MORE THAN ONE FILE MODE. PLEASE RE-ENTER
     &EXIT
-ASK2 &BEGTYPE -END2
   Please enter string of characters to be scanned for in above
 list of files-
   If lower-case characters are to be retained, prefix string with
quote- eg 'Queen Mary', otherwise everything is converted to uppercase.
    -END2
    &CASE M
    &READ  STRING &SSSS
    &IF  .&SSSS = .  &EXIT
    &QQ  =  &PIECE OF  &SSSS   1 1
    &IF  &QQ   = '    &GOTO   -MIXED
             &UPPER  VARS &SSSS
             &GOTO  -ASKDET
-MIXED    &TYPE mIxEd cAsE sEt  !
          &QQL = &LENGTH OF &SSSS
          &QQ2 = &PIECE OF &SSSS &QQL 1
          &IF  &QQ2  = '    &QQL = &QQL - 1
          &QQL  = &QQL - 1
          &SSSS = &PIECE OF &SSSS 2 &QQL
-ASKDET
          &CASE U
          &TYPE Scanning for: &SSSS
          &TYPE
&TYPE Do you wish each line where char-string is found displayed?Y|N
     &READ  VARS &DETLSW
     &ZZ1 =   1
     &ZZ2 =   *
&TYPE
&TYPE   All columns will be scanned. Enter alternate values if desired,
&TYPE   otherwise hit ENTER key-
     &READ VARS &ZZA  &ZZB
     &IF  .&ZZA  = .  &SKIP 3
                  &ZZ1 =  &ZZA
                  &IF .&ZZB  = .  &SKIP 1
                             &ZZ2 =   &ZZB
     CP SET EMSG OFF
     &RECNO = 1
* RACE ALL
     LISTFILE &LLN &LLT &FM   (L  STACK
     &IF &RETCODE = 28  &GOTO  -NORECS
     &IF &RETCODE = 36  &GOTO  -NORECS
     SENTRIES
     &IF &RETCODE = 0   &GOTO  -NORECS
     &STAKN =  &RETCODE
     &TYPE  PROCESSING &STAKN FILES...
     &TOTRECS = 0
     &X = 1
-RDLOOP
     &READ VARS  &FN&X &FT&X  *   *  *   &SZ
     &TOTRECS = &TOTRECS + &SZ
     &X = &X + 1
     &IF &X ¬>  &STAKN   &GOTO -RDLOOP
     &IF   &TOTRECS <  &RECLIM   &SKIP 3
     &TYPE Examining &TOTRECS records! Do you wish to continue? y|n
     &READ VARS &PROCEED
     &IF   .&PROCEED ¬= .Y &EXIT
     ERASE &0 $$FOUND
     CLRSCRN
     &X = 1
-XED
     &IF &FN&X = &0  &GOTO -XEDNEXT
     &TYPE  Processing file &X of &STAKN : &FN&X &FT&X
     &STACK &ZZ1  &ZZ2 &DETLSW
     &STACK &SSSS
     &STACK LIFO &0
     XEDIT &FN&X &FT&X &FM ( NOPROFIL
     &IF  &RETCODE = 0   &GOTO -XEDRET
     DESBUF
     &TYPE  /////COULDNT PROCESS &FN&X &FT&X &FM - ERR &RETCODE
     &GOTO -XEDNEXT
-XEDRET
     &READ VARS &FOUNDN
     &IF  &FOUNDN > 0 &FOUNDX = &FOUNDX + 1
-XEDNEXT
     &X =  &X + 1
     &IF &X  ¬>  &STAKN   &GOTO -XED
     CP SET EMSG ON
     &IF &FOUNDX = 0  &GOTO -NOFIND
     &TYPE  *=====* We found the data in &FOUNDX files *======*
     &TYPE  *===  A summary may be found in file &0 $$FOUND ==*
     &IF .&DETLSW = .Y   &STACK BROWSE &0 $$FOUND
     &EXIT
-NOFIND
     &TYPE  //////////// NO OCCURENCES WERE FOUND //////////////
     &EXIT
-NORECS   &TYPE NO RECS FOUND IN CMS EXEC A
     &EXIT
SCANALL XEDIT
&TRACE OFF
     &CASE M
     PRESERVE
     SET LINEND ON '
     SET VERIFY OFF
**   SET MSGM OFF
     &READ VARS &ZON1 &ZON2 &DETLSW
     &READ STRING  &SCANARG
     SET ZONE &ZON1 &ZON2
     &CNT = 0
     TRANSFER FN FT FM
     &READ VARS &EFN &EFT &EFM
-SCAN
     LOC:&SCANARG
     &IF  &RETCODE ¬= 0     &GOTO -NOTFND
     &CNT  =  &CNT + 1
     TRANSFER LINE
     &READ VARS &LINX
     &LINX = &CONCAT OF &LINX :
     &LINNO&CNT =  &RIGHT OF 000&LINX 5
     STACK
     &READ STRING &DTL&CNT
     &GOTO  -SCAN
-NOTFND
    &IF   &CNT = 0   &GOTO -EXIT
*  ..............NOW EDIT OUR $$FOUND FILE-
    XEDIT &0   $$FOUND A ( NOPROFIL
                SET LINEND ON '
                BOT
                I &EFN &EFT &EFM ---> We found &SCANARG in &CNT lines-
                &IF  .&DETLSW  ¬= .Y  &GOTO -FILEIT
                &X = 1
          -DETAIL
                I &BLANK &LINNO&X &DTL&X
                &X = &X + 1
                &IF &X ¬>  &CNT   &GOTO -DETAIL
          -FILEIT
                FILE
    &FNDMSG = &STRING OF          ...found &SCANARG  in &CNT lines.
    &TYPE &FNDMSG
-EXIT
    &STACK QQUIT
    &STACK &CNT
    RESTORE
    &EXIT


Tony King
Manager, Technical Services,
SYSCOR (Canada) ) Tony King 1989

