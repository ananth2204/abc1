WHO module revisited

A program I submitted was published in issue 10 of VM Update.
It displayed logged-on users sorted alphabetically.  Later, it was
pointed out to me that a bug in the code occurred when duplicate
user-ids were logged on.  Since then I have found a few of my
own!

Essentially, the original attempted (quite badly!) to sort out the
situation when SNA users via VSCS were logged on.  In some
situations, the program was unsuccessful at displaying users when
users with ids of 8 characters were logged on with VTAM-id
terminals also of 8 characters.  I have now managed to overcome
this by tackling it in a somewhat different (and more efficient)
way.  The result is as shown below.  The program runs as before
and the output is pretty much the same, but slightly wider than
before.  I've run this under VM/SP3, VM/SP4 and VM/SP5.

In order to run under VM/XA SF2, it needs two changes: because
VM/XA describes disconnected users with DISC (instead of DSC
for VM/SP), the constant, DISC, needs to be changed to reflect
this.  This also means that the compare at label ISITD has to be
changed.

I have not been able yet to try this under VM/XA SP, so if
anybody out there can I would be interested to hear from you.


WHO LIST

WHO      CSECT
         USING WHO,12
         LR    12,15
         ST    14,SAVE14
         CLI   8(1),C'L'                ONLY DISPLAY LOGGED ON USERS?
         BNE   DOALL
         OI    OPTION,LOGGED            SET WE DISPLAY ONLY LOGGED (NOT DISC
DOALL    LA    2,CPQNAME                ADDRESS OF COMMAND
         LA    3,BUFFER                 WHERE WE WANT THE ANSWER
         ICM   4,15,LCPQNAME            LENGTH OF COMMAND (WITH FLAG)
         L     5,LBUFFER                LENGTH OF REPLY BUFFER
         DC    X'83240008'
         L     2,ABUF                   WHERE WE WILL SORT
         LA    3,BUFFER                 POINT TO REPLY
         L     4,ABUF                   COPY OF CURRENT POINTER
MOVE     CLI   0(3),X'15'               LF?
         BE    ENDENT
         CLI   0(3),C','                END OF 'ENTRY'?
         BE    ENDENT
         MVC   0(1,2),0(3)              MOVE BYTE TO WHERE WE SORT
         LA    2,1(2)                   UPDATE POINTERS
         LA    3,1(3)
         B     MOVE                     KEEP GOING
* WHEN WE COME HERE WE HAVE MOVED ALL THE DETAIL (USER ID TERM)
* TO WHERE WE SORT.
ENDENT   LA    4,20(4)                  UPDATE THE COPIED POINTER TO
         LR    2,4                      NEXT O/P SLOT
         LA    3,1(3)                   POINT PAST COMMA
         CLI   0(3),X'00'               ALL DONE?
         BE    SORT
         CLI   0(3),C' '                IS THE NEXT CHARACTER A BLANK?
         BNE   MOVE                     NO, POINTER IS OK
         LA    3,1(3)                   YES, POINT PAST IT
         B     MOVE
SORT     L     3,ABUF                   POINT TO REPLY
         CLI   SORTSW,X'FF'             DID WE SWOP?
         MVI   SORTSW,X'00'             RESET SWITCH ANYWAY
         BNE   ENDSORT                  NO, IT'S ALL DONE
SORT1    CLC   0(8,3),20(3)             COMPARE CURRENT WITH NEXT SLOT
         BH    SWOP
NEXT     LA    3,20(3)                  NEXT SLOT
         CLI   0(3),X'00'               NO MORE?
         BE    SORT                     YES, GO BACK AND SEE IF SORT ENDED
         CLI   20(3),X'00'              CHECK NEXT SLOT JUST IN CASE
         BE    SORT
         B     SORT1                    CONTINUE SORT
SWOP     XC    0(20,3),20(3)            SWOP SLOTS
         XC    20(20,3),0(3)
         XC    0(20,3),20(3)            SWOP SLOTS
         MVI   SORTSW,X'FF'             SHOW WE SWOPPED AT LEAST ONCE
         B     NEXT
ENDSORT  L     3,ABUF
         LA    4,LINE                   WHERE WE DISPLAY
         LA    5,4                      NO. PER LINE
         WRTERM CON3,80
         WRTERM CON4,80
ISITD    CLC   11(L'DISC,3),DISC        THIS USER DISCONNECTED?
*        CLC   12(L'DISC,3),DISC        USED FOR XA SF
         BE    DSCUSER                  YES, DON'T MOVE
         AP    LOGGEDON,PONE
DOHIM    MVC   0(20,4),0(3)             NO, MOVE TO OUTPUT LINE
         BCT   5,NXTPOS
         WRTERM LINE,80
         MVC   LINE,LINE-1
         LA    5,4                      NO. PER LINE
         LA    4,LINE                   WHERE WE DISPLAY
         B     NXTUSER
DSCUSER  AP    DSCCNT,PONE
         TM    OPTION,LOGGED            ONLY LOGGED (NOT DISC) USERS?
         BZ    DOHIM                    NO, GO AND DO THE LOT
         B     NXTUSER                  YES, DON'T MOVE
NXTPOS   LA    4,20(4)                  NEXT O/P SLOT
NXTUSER  LA    3,20(3)                  NEXT USER
         CLI   0(3),X'00'               ALL DONE?
         BNE   ISITD
         CH    5,FOUR                   SOME TO DISPLAY?
         BE    DSPCTS                   NO, DO COUNTS LINE
         WRTERM LINE,80
DSPCTS   MVC   LINE,LINE-1
         MVC   LINE(L'CON1),CON1        MOVE IN TITLES
         UNPK  LINE+1'CON1(3),LOGGEDON
         OI    LINE+L'CON1+2,X'F0'
         MVC   LINE+L'CON1+4(L'CON2),CON2   MOVE IN TITLES
         UNPK  LINE+L'CON+L'CON2+4(3),DSCCNT
         OI    LINE+L'CON1+6+L'CON2,X'F0'
         WRTERM LINE,64
EXIT     SR    15,15
         L     14,SAVE14
         BR    14
CON1     DC    C'LOGGED ON USERS = '
CON2     DC    C'DISCONNECTED USERS - '
CON3     DC    CL80'USER     TERM.     USER     TERM.     USER     TERMC
               .     USER     TERM.'
CON4     DC    CL80'----     -----     ----     -----     ----     ----C
               -     ----     -----'
LOGGEDON DC    PL2'0'
DSCCNT   DC    PL2'0'
DISC     DC    C'DSC'
* DISC    DC    C'DISC'                    USED FOR XA SF
PONE     DC    P'1'
SORTSW   DC    X'FF'
OPTION   DC    X'00'
LOGGED   EQU   X'80'
         DC    C' '
LINE     DC    CL80' '
FOUR     DC    H'4'
         DC    0D'0'
CPQNAME  DC    C'Q N'
LCPQNAME DC    AL1(64),AL3(L'CPQNAME)
SAVE14   DC    F'0'
ABUF     DC    A(SORTBUF)
LBUFFER  DC    A(8192)
BUFFER   DC    32XL256'00'
SORTBUF  DC    32XL256'00'
         END


A Bodill
Software Support Manager
Memorex Telex International (UK)  ) A Bodill 1989

