Adding synonyms revisited

Since I wrote the ADDSYN EXEC which was published in the
February issue of VM Update, I have streamlined the code,
improved error handling and provided a command option
whereby a user can request that the new synonym be made a
permanent addition to his personal synonym file.

In the EXEC as printed, the HELP screen generated by
invoking the program with invalid parameters will not function
properly due to the removal of my name (which was being
sought by the HELP routine as an 'end of HELP' flag).  The
entire file will be displayed.  I have restructured my
methodology for displaying and delimiting HELP information,
and the methodology which resulted is cleaner, more efficient
and powerful, and is delimited by:

/*                    - Comments for HELPSCREEN -                     */

instead of my name, and now appears at the bottom of the
program.

The ADDSYN EXEC permits the addition of CMS synonyms
'on the fly'.  By default, the synonym is added only for the
duration of the current log-on session: however, by following
the definition string with '(P' the addition becomes a permanent
one and is added to the user's synonym file.

A HELP screen is provided if the EXEC is invoked without
arguments.

ADDSYN  EXEC

/* FUNCTION : ADDs a SYNonym to those currently defined. May be added */
/*            permanently or only for current logon session.          */
/* ARGUMENTS: Any of the following four formats is permissible:       */
/*              (1)      (2)            (3)                           */
/*            COMMAND  SYNONYM  SHORTEST-ABBREVIATION                 */
/*            COMMAND  SYNONYM  (synonym considered shortest abbrev.) */
/*                                - or -                              */
/*            COMMAND  SYN*ONYM                                       */
/*            COM*MAND                                                */
/*     (where letters LEFT of the * indicate the Shortest ABBREV.)    */
/* OPTIONS  : (P  as the final argument indicates PERMANENT addition. */
/* RETURNS  : Newly defined and currently active synonym              */
parse upper arg newreal newsyn extra '(' option
if newsyn = '' then
 do
   if newreal = '' then call helper 9' No COMMAND or SYNONYM Specified.'
   star = pos('*',newreal)
   if star > 1 then
    do
      extra = star -1
      newsyn = substr(newreal,1,star-1)||substr(newreal,star+1)
      newreal = newsyn
    end
   else call helper 8' Improper format for arguments: 'newreal
 end
else
 do
   if extra = '' then extra = length(strip(newsyn))
   star = pos('*',newsyn)
   if star > 1 then
    do
      extra = star -1
      newsyn = substr(newsyn,1,star-1)||substr(newsyn,star+1)
    end
   if datatype(extra,n) ¬= 1 then extra = length(strip(extra))
 end
'q synonym user (STACK'
if queued() < 4 then
 do
   'dropbuf'
   y = 1
 end
else
 do
   do 3; pull junk; end
   do y = 1 to queued()
     parse pull real synm short
     if short ¬= '' then syn.y = real synm length(strip(short))
     else syn.y = real synm length(strip(synm))
   end
 end
syn.0 = newreal newsyn extra
'dropbuf'
do z = 0 to y-1
  queue syn.z
end
 'execio 'queued()' diskw $TMPSYN$ SYNONYM A3 1 F (FINIS'
'synonym $TMPSYN$'
option = strip(option)
if option ¬= '' then
 do
   if substr(option,1,1) = 'P' then
    do
      queue 'input 'syn.0
      queue 'COMMAND FILE'
      'XEDIT 'userid()' SYNONYM A (NOPROF'
    end
 end
exit
helper:
parse arg outrc sayline
parse source . . proc .
'execio 1 cp (STRING Q SCREEN'
pull . . . . clr hlght
'vmfclear'
if sayline ¬= '' then
 do
   'execio 0 cp (STRING SCREEN VMOUT WHITE NONE'
   say center(sayline,79)
 end
'execio 0 cp (STRING SCREEN VMOUT RED NONE'
say center(proc,79)
say
'execio 0 cp (STRING SCREEN VMOUT YELLOW NONE'
do y = sourceline() to 30 by -1
  if pos('- Comments for HELPSCREEN -',sourceline(y)) > 0 then leave
end
do y = y+2 to sourceline()
 say center(strip(substr(sourceline(y),3,68)),79)
end
'execio 0 cp (STRING SCREEN VMOUT 'clr hlght
exit ourtc
/*                    - Comments for HELPSCREEN -                     */
/*  Adds a CMS SYNONYM to those already in effect for this session.   */
/*   The ARGUMENT(s) may be passed in any of the following 4 forms:   */
/*    (1)           (2)            (3)                                */
/*  COMMAND       SYNONYM       shortest-abbreviation                 */
/*  COMMAND       SYNONYM       (synonym considered shortest abbrev.) */
/*  COMMAND       SYN*ONYM                                            */
/*  COM*MAND                                                          */
/*     (where letters LEFT of the * indicate the Shortest ABBREV.)    */
/* The new synonym is added PERMANENTLY if the final argument is:  (P */
/*    To create a synonym: DELETE to invoke ERASE and be able to      */
/*             abbreviate it to the DEL, you could enter:             */
/*     ADDSYN ERASE DEL*ETE     - or -     ADDSYN ERASE DELETE DEL    */
/* To be able to abbreviate VMFPLC2 to just VMF, you could enter:     */
/*    ADDSYN VMFPLC2 VMF        - or -         ADDSYN VMF*PLC2        */
/*                                                                    */

Rick Frye
Systems Programming Consultant
DeLeuw Cather & Company (USA)                               c Rick Frye 1990


