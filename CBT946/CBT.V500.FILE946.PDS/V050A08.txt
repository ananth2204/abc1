PUT and GET macros

The PUTBLOCK macro allows you to copy a block of lines
into a CMS file in another VM session or in the same session.
The command P will copy only one line, PP will copy several
lines.

The RECBLOCK macro allows you to receive a block of lines
created by the PUTBLOCK macro.  The command R will place
the copied block after the current line.


PUTBLOCK XEDIT

/*   Note : in order to use PREFIX COMMANDS, change your PROFILE XEDIT*/
/*          'COMMAND SET PREFIX SYNONYM P  PUTBLOCK'                  */
/*          'COMMAND SET PREFIX SYNONYM PP PUTBLOCK'                  */
Trace o
 Address 'XEDIT'
 Arg prefix operand pline
'COMMAND EXTRACT/LINE/'
 curline = line.1
 Parse source . . . . . synonym  .
 Select
   When Length(synonym)=1 Then
     Do
      'COMMAND : 'pline
      'COMMAND PUT'
       Call Send_file
     End
   When Length(synonym)=2 Then
     Do
      'COMMAND EXTRACT /PENDING BLOCK' synonym ':0 :'pline '/'
       If pending.0 Â¬= 0 Then
          Do
           'COMMAND :'pending.1 'COMMAND SET PENDING OFF'
            lines_to_put = pline - pending.1 + 1
           'COMMAND :'pending.1
           'COMMAND PUT' lines_to_put
            Call Send_file
          End
        Else
           'COMMAND :'pline 'COMMAND SET PENDING BLOCK' synonym
     End
   Otherwise
     Do
      'COMMAND SET SCOPE ALL'
      'COMMAND :'pline 'COMMAND SET PENDING ERROR' Left(synonym,5)
     End
 End
Exit
Send_file:
 Address 'CMS'
 Say 'Enter the receiver userid.  (Just press ENTER if receiver=sender)'
 Pull receiver
 If receiver='' Then receiver=Userid()
 ft=word(receiver,1)
 'SET CMSTYPE HT'
 'RENAME XEDTEMP0 CMSUT2 A $PBLOCK$' ft 'A3'
 'SENDFILE $PBLOCK$' ft 'A3 TO' receiver
 'VMFCLEAR'
  Address 'XEDIT'
 'COMMAND :'curline
Return


RECBLOCK XEDIT

/*   Note : in order to use PREFIX COMMANDS, change your PROFILE XEDIT*/
/*          'COMMAND SET PREFIX SYNONYM R  RECBLOCK'                  */
Trace o
 Address 'XEDIT'
 Arg prefix operand pline
'EXTRACT/LINE/'
 curline = line.1
 Parse source . . . . . synonym .
 Select
   When Length(synonym)=1 Then
     Do
       call Receive_File
      'COMMAND : 'pline
      'COMMAND GET $PBLOCK$' userid() 'A'
     End
   Otherwise
     Do
      'COMMAND SET SCOPE ALL'
      'COMMAND :'pline 'COMMAND SET PENDING ERROR' Left(synonym,5)
     End
 end
'COMMAND :'curline
Exit
Receive_File:
  Address 'CMS'
 'SET CMSTYPE HT'
 'MAKEBUF'
  numbuf = rc
 'EXECIO * CP (STRING Q R * ALL'
  Pull header
  Do Until Queued() = 0
     Parse Pull . spid . . . . . . . rdr_name receiver dummy
     If (rdr_name = '$PBLOCK$' & receiver=userid()) Then
        Do
         'RECEIVE' spid rdr_name receiver 'A3'
          Leave
        End
  End
 'DROPBUF' numbuf
  Address 'XEDIT'
return


Fuso Antonio
Systems Engineer
Hoechst Belgium SA (Belgium)


