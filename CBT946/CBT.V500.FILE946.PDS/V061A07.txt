Display spool utilization under VM/XA

When I worked on VM/SP, I wrote a program to display spool
utilization.  With VM/XA SP, IBM provides this function with
the CP command QUERY ALLOC.  Unfortunately, the output
display is rather unreadable.  For example:

DASD 0252 VMSERV 3380 TDSK  TOTAL=0150 INUSE=0130 AVAIL=0020

                      PAGE  TOTAL=0442 INUSE=0116 AVAIL=0326

                      SPOOL TOTAL=0292 INUSE=0111 AVAIL=0181

                      DRCT  TOTAL=0008 INUSE=0002 AVAIL=0006, ACTIVE

If you have several disks allocated for page/spool, this kind of
display is repeated for each volume.  Once you get more than
two or three disks, it is difficult to get an instant impression of
resource usage.

QALLOC is an extension to QUERY ALLOC, which takes the
output of the command and produces a single value showing
the percentage allocation of the system spool, page, and TDSK
space.  I use this command, in conjunction with an automatic
'spool purger', to monitor spool usage.

The format of the command is:

      QALLOC  <ALL>

If the optional ALL parameter is given, QALLOC will display
space usage by individual volume as well as for the system as a
whole.  If ALL is not specified, a single 'system wide'
utilization will be displayed.  For example:

     Space utilization (system wide) :-


                 Spool :-   28%  of nnn cylinders allocated

                 Page  :-   47%  of nnn cylinders allocated

                 Tdsk  :-   10%  of nnn cylinders allocated

Note: in order to run the EXEC, class D is required.

/*    Display amount of spool/page/tdsk in use as a percentage of
      the allocated space.                                           */
arg all .
cdasd = ""
vmfclear
totspoolt = 0 ;totspoola = 0 ;totspoolu = 0
totpaget  = 0 ;totpagea  = 0 ;totpageu  = 0
tottdiskt = 0 ;tottdiska = 0 ;tottdisku = 0
"q alloc ( stack"
queue "DASD"
do while queued() ¬= 0
   pull dasd cuu vol disk sptype total inuse avail
   if "DASD" == dasd then do
      if cdasd ¬= "" then do
           if all = "ALL" then
              say "Space utilization on "cdasd
           if spoolt = 0 then spoolper = "None-allocated"
                        else spoolper = format( ((spoolu * 100)/spoolt), 3, 2 ),
           ||"%  of "format(spoolt, 4)" cylinders allocated"
           if paget = 0  then pageper  = "None-allocated"
                         else pageper  = format( ((pageu * 100)/paget), 3, 2)||,
           "%  of "format(paget, 4)" cylinders allocated"
           if tdiskt = 0 then tdiskper = "None-allocated"
                         else tdiskper = format( ((tdisku * 100)/tdiskt), 3, 2),
           ||"%  of "format(tdiskt, 4)" cylinders allocated"
           if all = "ALL" then do
              say "            Spool :-   "spoolper
              say "            Page  :-   "pageper
              say "            Tdsk  :-   "tdiskper
              end
           totspoolt = totspoolt + spoolt
           totspoolu = totspoolu + spoolu
           totspoola = totspoola + spoola
           totpaget  = totpaget  + paget
           totpageu  = totpageu  + pageu
           totpagea  = totpagea  + pagea
           tottdiskt = tottdiskt + tdiskt
           tottdisku = tottdisku + tdisku
           tottdiska = tottdiska + tdiska
      end
   cdasd = vol
   spoolt = 0 ;spoolu = 0 ;spoola = 0
   paget = 0  ;pageu = 0  ;pagea = 0
   tdiskt = 0 ;tdisku = 0 ;tdiska = 0
   end
   else do
      sptype= dasd
      total = cuu
      inuse = vol
      avail = disk
   end
   parse var total dummy '=' totcyls
   parse var inuse dummy '=' usecyls
   parse var avail dummy '=' avacyls
   select
      when sptype = 'SPOOL' then do
         spoolt = spoolt + totcyls
         spoolu = spoolu + usecyls
         spoola = spoola + avacyls
      end
      when sptype = 'PAGE' then do
         paget  = paget  + totcyls
         pageu  = pageu  + usecyls
         pagea  = pagea  + avacyls
      end
      when sptype = 'TDSK' then do
         tdiskt = tdiskt + totcyls
         tdisku = tdisku + usecyls
         tdiska = tdiska + avacyls
      end
      otherwise do
      end
   end
end
say " "
say "Space utilization (system wide) :-"
if totspoolt = 0 then spoolper = "None-allocated"
                 else spoolper = format( ((totspoolu * 100)/totspoolt), 3, 2 ),
||"%  of "format(totspoolt, 4)" cylinders allocated"
if totpaget = 0  then pageper  = "None-allocated"
                 else pageper  = format( ((totpageu * 100)/totpaget), 3, 2),
||"%  of "format(totpaget, 4)" cylinders allocated"
if tottdiskt = 0 then tdiskper = "None-allocated"
                 else tdiskper = format( ((tottdisku * 100)/tottdiskt), 3, 2),
||"%  of "format(tottdiskt, 4)" cylinders allocated"
say "            Spool :-   "spoolper
say "            Page  :-   "pageper
say "            Tdsk  :-   "tdiskper
say " "

S A Barnett
Senior Technical Consultant
IMI Computing Ltd (UK)

