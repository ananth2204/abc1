 Delete a file on equal match

Working on several files (programs, EXECs, etc) on my A disk I sometimes forget
to delete the file once it has been copied back to the production disk.  The
MATCHDEL EXEC will delete a file from my primary disk if it finds an exact match
on a specified target disk.  The EXPLAIN section of the EXEC describes how to
use the EXEC.

 /***   Delete a matching file from the primary disk if a match       */
 /***   is found on the secondary disk.                               */
 PARSE UPPER ARG fm2 fn1 ft1 fm1 .
 PARSE SOURCE . . me .
 IF fm2 = '?' THEN SIGNAL EXPLAIN
 IF fm2 = ' ' THEN SIGNAL EXPLAIN
 /** get the FILE TIME STAMPS and other attributes if the files exist */
 'LISTFILE' fn1 ft1 fm1 '(LABEL STACK LIFO'
 IF RC ¬= 0 THEN SIGNAL NODEL2
 PULL . . . ff1 rl1 nr1 nb1 dt1 tm1 .
 'LISTFILE' fn1 ft1 fm2 '(LABEL STACK LIFO'
 IF RC ¬= 0 THEN SIGNAL NODEL1
 PULL . . . ff2 rl2 nr2 nb2 dt2 tm2 .
 'DESBUF'
 /** Now check file attributes rather than comparing files            */
 IF ff2 ¬= ff1 | rl2 ¬= rl1 | nr2 ¬= nr1 THEN SIGNAL NODEL3
 /**If date/time are different, we need to actually compare the files */
 IF dt2 = dt1 & tm2 = tm1 THEN SIGNAL MATCHOK
 'SET CMSTYPE HT'
 'COMPARE' fn1 ft1 fm1 fn1 ft1 fm2
 IF RC ¬= 0 THEN SIGNAL NODEL1
 'SET CMSTYPE RT'
MATCHOK:
 /**  It now seems ok, so delete original                             */
 'ERASE'   fn1 ft1 fm1
 EXIT RC
NODEL1:
 'SET CMSTYPE RT'
 SAY me': Non matching file on SECONDARY disk, "'fn1 ft1 fm1'" kept'
 EXIT 1
NODEL2:
 SAY me': File "'fn1 ft1 fm1'" cannot be found, nothing deleted.'
 EXIT 2
NODEL3:
 SAY me': Non matching file attributes, "'fn1 ft1 fm1'" kept.'
 EXIT 3
EXPLAIN:
'VMFCLEAR'
say ' '
say 'MATCHDEL f-mode2 f-name1 f-type1 f-mode1'
say ' '
say 'Parameters :'
say ' '
say 'f-mode2     file-mode of       2nd file'
say 'f-name1     file-name of 1st & 2nd file'
say 'f-type1     file-type of 1st & 2nd file'
say 'f-mode1     file-mode of 1st       file'
say' '
say 'Function :'
say ' '
say 'This (CMS) procedure deletes a file from the PRIMARY disk,'
say 'but only if it matches with a copy on the SECONDARY disk,'
say ' '
say 'Files are considered to match if they carry the same date/time '
say 'stamp.  If they do not, they are physically compared for equality.'
say ' '
say 'This exec can also be entered on the FILELIST or FLIST menu'
say '     MATCHDEL fm2    against the file to be tested'
 say ' '
 EXIT5

A Astle
Technical Consultant (Australia)   ) Xephon 1987





























































