System TEMP space utilisation

The approach to measuring TEMP space utilisation offered in the
article on page 47 of VM Update issue 33, has a shortcoming with
VM/SP HPO release 5, since the bit string '08040201' no longer
appears on a half-word word boundary.

I suggest instead the REXX EXEC below is used, since it is
release independent and does not need to scan memory to locate
DMKPGT.

Since DMKPGT is a nucleus-resident module, the memory
location of the two entry points DMKPGTTM (number of pages
available) and DMKPGTTU (number of pages used) can be
obtained from the nucleus map.

The nucleus map is, by default on our system, located on the
MAINT 194 mini-disk and has the name CPHPONUC MAP.  If
this is not the case for your system, the EXEC needs only to be
altered for the mini-disk you use, as it will allow the entry of
another filename.

SUSE EXEC

/* DESCRIPTION:      : REXX EXEC TOP DETERMINE THE AMOUNT  */
/*                     OF TEMP SPACE UTILISED BY CP.       */
/* CP COMMANDS USED  : DCP (CLASS C;E)                     */
/*                     LINK(CLASS G)                       */
/* PREREQUISITES     : ACCESS TO MAINT 194 MDISK           */
/*                     FOR CPHPONUC MAP                    */
/* OPERATION         : SUSE EXEC USES EXECIO TO SCAN THE   */
/*                     CP NUCLEUS MAP FOR ENTRY POINTS     */
/*                     DMKPGTTM AND DMKPGTTU. ONCE FOUND   */
/*                     THESE ADDRESSES ARE USED BY DCP     */
/*                     TO DISPLAY THE NUMBER OF AVAILABLE  */
/*                     AND USED PAGES OF TEMP SPACE.       */
/* ASSUMPTIONS       : 1. THE CP NUCLEUS MAP IS CURRENT.   */
/*                     2. MODULE DMKPGT IS NUCLEUS RESIDENT*/
/*                     3. THE CP NUCLEUS MAP IS AVAILABLE  */
TRACE O
SET CMSTYPE HT
/* LINK TO AND ACCESS MAINT 194 FOR NUCLEUS MAP            */
/* (IF NOT ON USERID MAINT, PASSWORD WILL BE PROMPTED FOR) */
CP LINK MAINT 194 194 RR
'ACC 194 B/A'
/* SCAN NUCLEUS MAP FOR ENTRY POINTS DMKPGTTM AND DMKPGTTU */
/* IF MAP IS NOT NAMED CPHPONUC MAP, ACTUAL NAME WILL BE   */
/* REQUESTED                                               */
STATE CPHPONUC MAP B
IF RC ¬= 0 THEN DO
          DO UNTIL FN ¬= '' & FT ¬= '' & FM ¬= ''
               SET CMSTYPE RT
               SAY 'ENTER FN FT FM OF CP NUCLEUS MAP'
               PULL FN FT FM .
               SET CMSTYPE HT
               STATE FN FT FM
               IF RC = 28 THEN DO
                       SET CMSTYPE RT
                       SAY FN FT FM 'DOES NOT EXIST'
                       SET CMSTYPE HT
                       FN = ''
                       FT = ''
                       FM = ''
                       END
                   END
END
ELSE DO
        FN = 'CPHPONUC'
        FT = 'MAP'
        FM = 'B'
END
/* EXECIO TWICE TO LOCATE ENTRY POINTS AND PULL ADDRESSES  */
'EXECIO * DISKR 'FN FT FM '(LOCATE /DMKPGTTM/'
IF RC ¬= 0 THEN SIGNAL NOT_FOUND
PULL . RECNO
PULL NAME . ADDRTM .
FINIS FN FT FM
'EXECIO * DISKR 'FN FT FM '(LOCATE /DMKPGTTU/'
IF RC ¬= 0 THEN SIGNAL NOT_FOUND
PULL . RECNO
PULL NAME . ADDRTU .
/* USE THESE ADDRESSES TO OBTAIN NUMBER OF PAGES AVAILABLE */
/* AND NUMBER USED WITH DCP COMMAND                        */
AVAIL = DIAG(8,'DCP 'ADDRTM)
AVAIL = SUBSTR(AVAIL,11,8)
AVAIL = X2D(AVAIL)
USED  = DIAG(8,'DCP 'ADDRTU)
USED  = SUBSTR(USED,11,8)
USED = X2D(USED)
/* DISPLAY RESULTS                                         */
SET CMSTYPE RT
SAY 'SYSTEM TEMP SPACE UTILISATION'
SAY 'PAGES AVAILABLE    PAGES USED    % UTILISATION'
SAY '---------------    ----------    -------------'
SAY LEFT(AVAIL,15)'    'LEFT(USED,10)'    'LEFT(USED/AVAIL*100,13)
SET CMSTYPE HT
EXIT
/* DISPLAY RESULTS                                         */
NOT_FOUND
SET CMSTYPE RT
SAY 'ENTRY POINTS DMKPGTTM/DMKPGTTU NOT FOUND IN 'FN FT FM
SAY 'PLEASE VERIFY THAT YOUR NUCLEUS MAP IS CURRENT AND RETRY'
EXIT

D S Denyssen
Software Department
Genmin Group Information Services (S Africa) ) D S Denyssen 1989


