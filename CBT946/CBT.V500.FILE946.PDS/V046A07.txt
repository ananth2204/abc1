LEAVE option for VM/XA DETACH command

The following fixes put back the LEAVE option of the CP
DETACH command which IBM failed to include in VM/XA
SP 2.0.  Our service level is 0204 but it looks as if the fixes
will be compatible at lower levels.

The DETACH command is processed by several modules.
However we are only interested in HCPDTC, which detects the
LEAVE option in the command line and issues the message
'LEAVE OPTION NOT SUPPORTED BY VM/XA'; and
HCPDTD which does the actual rewind and unload.

In order to communicate from HCPDTC to HCPDTD that the
LEAVE option has been issued, the byte at offset X'1F' in the
RDEV block, which isn't used for tapes, is set to X'FF',
otherwise it is set to X'00'. (Please check in the macro library
and not the manuals that this byte is not used at your site.)

Only the first tape is affected by the LEAVE option if a device
range instead of a single address is entered.

The fixes for HCPDTC are as follows:

%     Locate the line:
     EXTRN HCPSCCFR - SCAN FOR RANGE DELIMITER
     and insert after it:
     EXTRN HCPSCNRU - LOCATE RDEV BLOCK

%     Locate the line:

HCPDTCMD HCPENTER CALL,SAVE=DYNAMIC

     and insert after it:

         MVI   JEZFLG,X'00'            * clear internal flag (not RDEV)

%     Locate the lines:

DETACHNX DS    0H
         LTR   R4,R4              DO WE HAVE ANOTHER DTCBK ?
         BZ    DETACHED           NO. RETURN TO CALLER NOW

     and insert after them:

         TM    JEZFLG,JEZLEAVE         * was the LEAVE option entered ?
         BZ    ENDMYBIT                * branch if no
         SLR   R1,R1                   * clear R1 for ICM
         ICM   R1,B'0011',DTCDEVS      * get first device addr
         ST    R8,JEZSAVR8             * save R8 just to be safe
         HCPCALL HCPSCNRU              * get the RDEV block
         BNZ   ENDMYBI0                * branch if doesn't exist
         CLI   0(R8),X'08'             * is it a tape (CLASTAPE=X'08')
         BNZ   ENDMYBI0                * branch if no
         MVI   X'1F'(R8),X'FF'         * otherwise set byte in RDEV
ENDMYBI0 DS    0H
         L     R8,JEZSAVR8             * restore R8 to be safe
ENDMYBIT DS    0H

%     Locate the line:

DTCSETLE DS    0H

     and insert after it:

         OI    JEZFLG,JEZLEAVE         * set internal flag (not RDEV)
         B     DTCREQS                 * continue as normal

%     Locate the line:

C$SYSTEM DC    CL8'SYSTEM'        KEYWORD

     and insert after it:

JEZFLG   DC    XL1'00'
JEZSAVR8 DC    F'0'
JEZLEAVE EQU   X'80'

The fixes for HCPDTD are as follows:
%     Locate the lines:

         CLI   RDEVTYPE,TYP3480   SEE IF 3480 TAPE SUBSYSTEM
         BNE   DTDREWUL           IF NOT, GO TO REWIND UNLOAD

     replace the

         BNE   DTDREWUL

     line with:

         BE    DO43480                 * if yes branch to 3480 code

     and insert the following lines after it:

         CLI   X'1F'(R8),X'FF'         * was LEAVE option entered
         BNE   DTDREWUL                * branch if no to rewind/unload
         MVI   X'1F'(R8),X'00'         * reset the RDEV flag
         B     BYPASSUL                * bypass the rewind/unload
DO43480  DS    0H                      * my label to mark 3480 code

%     Locate the lines:

         CLI   RDEVTYPE,TYP3480   SEE IF 3480 TAPE SUBSYSTEM
         BNE   DTDACODV           IF NOT A 3480, CONTINUE
DTDUNASN DS    0H

     and insert the following line before them:

BYPASSUL DS    0H

Jeremy Carter
Systems Administrator
MGM Assurance (UK)                              ) Xephon 1990


