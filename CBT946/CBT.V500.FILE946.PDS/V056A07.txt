Prefix commands without a prefix area

When I edit an 80-column file on an 80-column wide screen I
can either issue SET VERIFY 1 72 to work only with the first
72 columns or set the prefix area off by issuing SET PREFIX
OFF.  If I do not issue one of these commands then the lines
wrap, which can be very confusing.  I have SET VERIFY 1 72
in my XEDIT profile as the default setting; however, there are
times when I want to work on all 80 columns.  Setting the
prefix area off allows me to work with 80 columns without
having the lines wrap.  Unfortunately I cannot then use any
prefix commands.

The XPREFIX XEDIT macro provides a solution by allowing
prefix commands to be executed without a prefix area.  It
works with all prefix commands (in VM Release 4) except
SCALE (to set the scale line at the cursor line) and S (to re-
show excluded lines).  The macro can be invoked from the
command line or by using a PF key.

To issue a prefix command from the command line, type
XPREFIX cmd on the command line and then move the cursor
to the line where the command is to be executed and press the
ENTER key.

To assign a prefix command to a PF key, type XPREFIX pfn
cmd on the command line and press ENTER.  This assigns the
command cmd to the PF key pfn.  The assignment can also be
made by issuing the XEDIT SET command: SET pfn
XPREFIX cmd.  To use XPREFIX in this manner, the cursor is
first moved to the line where the prefix command is to be
executed, and then the PF key is pressed.

The XPREFIX macro's comments are used as its help screen if
the program is invoked with HELP, ?, or no parameter at all.

/* The XPREFIX macro allows one to execute a prefix command without   */
/* requiring a prefix area.  The prefix command can be assigned to a  */
/* PF key or can be issued from the command line. The prefix command  */
/* is executed on the line where the cursor resides.  For example:    */
/*                                                                    */
/* ====> xprefix a3                                                   */
/*                                                                    */
/* will cause the prefix command "a3" to be executed immediately on   */
/* the line where the cursor resides.  In this case it would add 3    */
/* lines after the cursor line.  Another form of the command is:      */
/*                                                                    */
/* ====> xprefix pf5 a3                                               */
/*                                                                    */
/* which assigns the prefix command "a3" to the PF5 key.  One can     */
/* also issue  "SET PF5 XPREFIX A3"  on the command line or put the   */
/* command in one's PROFILE XEDIT file for a permanent assignment.    */
/*                                                                    */
/* You cannot use XPREFIX for the commands SCALE or S (show).         */
  parse upper ARG a1 a2
  if a1 = '' | a1 = '?' | a1 = 'HELP'
     then signal HELP
  if SUBSTR(a1,1,2) = 'PF'
     then cmd = a2
     else cmd = a1
  if FIND('SCALE S SHOW',cmd) > 0
     then do
          'COMMAND MSG The' cmd 'command cannot be used with XPREFIX.'
          exit
          end
  if SUBSTR(a1,1,2) = 'PF'
     then do
          'COMMAND SET' a1 'XPREFIX' a2
          'COMMAND MSG' a2 'has been assigned to' a1
          end
     else do
          'COMMAND EXTRACT /LINE/SIZE/CURSOR'
          if CURSOR.3 <= 0 | CURSOR.3 > SIZE.1
             then do
                  'COMMAND MSG The cursor is not on a valid line.'
                  exit
                  end
          'COMMAND SAVE'
          'COMMAND :' CURSOR.3
          'COMMAND LPREFIX' a1
          if FIND('/ PARA',a1) > 0 then exit
          'COMMAND :' LINE.1
          end
  exit
HELP:
  'EXTRACT /RESERVED *'                     /* Save reserved lines    */
  do i = 2 to 21                            /* Display help screen    */
     xxx  = CENTER(SOURCELINE(i),80)
     j = i + 1
     'SET RESERVED' j 'BLUE NONE H' xxx
  end
  msg "Press ENTER or any PF key to remove this help screen."
  'MAKEBUF'
  'READ ALL TAG'                            /* Intercept term. input  */
  'DROPBUF'
  do ix = 3 to 22                           /* Remove help screen     */
     'SET RESERVED' ix 'off'
  end
  do ix = 1 to RESERVED.0                   /* Restore reserved lines */
     'SET RESERVED' RESERVED.ix
  end
  exit

P C Shumway
Project Leader
Masonite Corporation (USA)                   c P C Shumway 1991
