 Controlling the VM/VTAM service machine
After installing VM/VTAM to control our VM network we discovered a problem which
I'm sure most people with VM/VTAM installed have encountered.  The only VM user
allowed to send commands to the VTAM service machine for execution is the user
specified as the SECONDARY USERID in the VM Directory for the VTAM service
machine.  For most shops, this will be the VM OPERATOR.  This is fine until the
need or desire arises for some other VM machine to be able to send commands to
VTAM.  This most often occurred at our shop when we needed to resolve a problem
from a dial-up line, say from home, to resolve an after-hours problem, but we
could not execute any VTAM commands because the OPERATOR machine was always left
logged on.  This would necessitate a drive to work to resolve a problem which
could have been taken care of at home.  It also became necessary to be able to
enter VTAM commands when away from the console.
To circumvent this problem, I wrote the following EXEC.  It is used to change
the SECONDARY USERID field in the VM miniblok for the VTAM service machine to
another user.  The EXEC does a CP LOCATE VTAM to find the VM miniblok, and then
does a CP STCP at X'218' into the miniblok to store a new VM USERID.  After the
STCP is done, the EXEC relocates the VM miniblok and verifies that the STORE
command stored the correct userid.  If the userid to become the new SECONDARY
USERID is the userid of the machine running this EXEC, then PF24 is set to 'SEND
VTAM VTAM', which is the command format needed to send commands to VTAM.
Whenever the SECONDARY USERID is changed, a message is sent to the VM OPERATOR
indicating this change to provide for an audit trail.
The EXEC may be invoked with an optional name of the VM user to become the new
SECONDARY USERID, but the default if no name is entered is the VM user invoking
the EXEC.
Because of the CP commands used in the EXEC, it must be invoked from a VM
machine with CP CLASS C privileges.  A Help screen is also available by entering
a '?' as the USERID parameter.
This EXEC is currently working on a VM Release 4 system, and should only need
the displacement into the miniblok for the SECONDARY USERID field be changed to
allow it to work on other releases of VM.
VTAMSWAP EXEC
/*   Syntax  :  VTAMSWAP (userid)                                */
/*   Calls   :  CP LOCATE, CP DCP, CP STCP                       */
/*   Purpose :  This EXEC is used to change the SECONDARY USERID */
/*              field in the VM block for VTAM to another user.  */
/*              The SECONDARY USERID is initially specified in   */
/*              the VM Directory and is the only user allowed    */
/*              to send commands to the VM/VTAM service machine. */
/*   Remarks :  This EXEC uses the CP LOCATE command to find the */
/*              miniblok in storage for the VM/VTAM service      */
/*              machine. The EXEC then goes X'218' into the      */
/*              miniblok where the SECONDARY USERID is located,  */
/*              and issues a CP STCP command to store a new VM   */
/*              USERID as passed to this EXEC.                   */
/*              If the USERID to be the new SECONDARY USERID is  */
/*              the userid of the machine running this EXEC, PF24*/
/*              is set to "SEND VTAM VTAM", which is the command */
/*              format needed to send commands to VTAM.          */
/*              A message is sent to the VM OPERATOR whenever    */
/*              the SECONDARY USERID is changed to provide an    */
/*              audit trail.                                     */
/*   Security:  Since this EXEC issues the privileged CP         */
/*              commands LOCATE, DCP, and STCP, CP Class C is    */
/*              required to run the EXEC.                        */
 Trace 0
 Address CMS
 'Clrscrn'
/*  Check to see if the user needs HELP                          */
CHECK_FOR_HELP:
 Parse Upper Arg SECUSER .
 If SECUSER = '?' Then Do
    LINE = 1
    Do LOOPCNT = 1 to Sourceline()
       If Sourceline(LOOPCNT) = 'HELP_SCREEN:' Then Do
           LINE = LOOPCNT + 1
          Leave
       End
    End
    'Clrsrn'
    Do Until Left(Sourceline(LINE),2) ¬= '/*'
       INFO = Strip(Strip(Sourceline(LINE),B,' '),B,'/')
       INFO = Center(Strip(INFO,B,'*'),80)
       Say INFO
       LINE = LINE + 1
    End
    Signal Exit
 End
/*  Set up who is to be the VTAM Secondary User ID                  */
SET_UP_USERID:
 'Desbuf'
 'USERID'
 Parse Upper Pull . VMID .
 If SECUSER = '' Then SECUSER = VMID
 SECUSER = Left(SECUSER,8)
 Say SECUSER ' will now be the SECONDARY user for VTAM'
/*  Locate the VTAM VM BLOCK, and Display who is currenty SECONDARY */
LOCATE_MINIBLOK:
 CP = 'Execio * CP (STEM CP. STRING '
 CP 'LOCATE VTAM'
 If RC ¬= 0 Then Do
    Say '      ERROR during CP LOCATE VTAM      '
    SAY '              - EITHER -               '
    Say '         VTAM is not logged on,        '
    Say 'You are NOT authorized to run this EXEC'
    Say ' '
    Exit
 End
 Parse Var CP.1 LABEL '=' VMBLOK
 VMBLOK = STRIP(VMBLOK)
 ID_ADDRESS = D2X(X2D(VMBLOK) + X2D(218))
 CP 'DCP 'ID_ADDRESS'.8'
 Parse Var CP.1 ADDRESS WORD1 WORD2 .
 Say ' '
 Say 'Current Secondary User is 'X2C( WORD1 || WORD2 ),
                        ' at address ' ID_ADDRESS
/*  Loop until user either Quits or instructs us to Continue        */
PROMPT:
 Say 'Should we continue (Y/N) ?'
 Parse Upper Pull ANSWER
 If ANSWER = 'N' Then Exit
 If ANSWER = 'NO' Then Exit
 If ANSWER = 'HT' Then Exit
 If ANSWER = 'QUIT' Then Exit
 If ANSWER = 'STOP' Then Exit
 If ANSWER ¬= 'Y' & ANSWER ¬= 'YES' Then Signal PROMPT
/*  Here we go, ZAP the SECUSER field to be the specified SECUSER   */
ISSUE_THE_STORE:
 NEW_SECUSER = C2X(SECUSER)
 NEW_SECUSER = NEW_SECUSER || '4040404040404040'
 NEW_SECUSER = Left(NEW_SECUSER,16)
 NEW_SECUSER1 = Substr(NEW_SECUSER,1,8)
 NEW_SECUSER2 = Substr(NEW_SECUSER,9,8)
 CP 'STCP 'ID_ADDRESS NEW_SECUSER1 NEW_SECUSER2
 If RC ¬= 0 Then Do
    Say 'An ERROR has occurred while trying to UPDATE the VMBLOCK'
    Say 'STCP Return Code = 'RC
    Say 'Issue DCP T'ID_ADDRESS'.8  to see who is Secondary User ID.'
    Say 'Depending on the error, results may be unpredictable'
    Say ' '
    Exit
  End
/*  VALIDATE - Relocate VM BLOCK and compare SEC. USER with SECUSER */
VALIDATE_THE_CHANGE:
 Say ' '
 Say ' '
 Say 'I am now validating the update........'
 CP 'LOCATE VTAM'
 If RC ¬= 0 Then Do
    Say 'VTAM VM block cannot be found ...'
    SAY '          - EITHER -              '
    Say 'VTAM is not logged on, or PROBLEM !'
    Exit
 End
 Parse Var CP.1 LABEL '=' VMBLOK
 VMBLOK = STRIP(VMBLOK)
 ID_ADDRESS = D2X(X2D(VMBLOK) + X2D(218))
 CP 'DCP 'ID_ADDRESS'.8'
 Parse Var CP.1 ADDRESS WORD1 WORD2 .
 VALIDATE_ID = WORD1 || WORD2
 IF VALIDATE_ID ¬= NEW_SECUSER Then Do
   Say 'VALIDATION ERROR   !!!!'
   Say 'Hex 218 into VM BLOCK for VTAM was not zapped to 'SECUSER
   Say 'Use CP LOCATE VTAM to locate VM BLOCK address'
   Say 'Then use CP DCP Txxxxxxx.310  to display the VM BLOCK'
   Say ' '
 End
 Else Do
   Say 'Validation Completed SUCCESSFULLY, 'SECUSER' is now Secondary User'
   Say ' '
   If VMID = SECUSER Then Do
       Say 'PF24 is now set to SEND commands to VTAM'
       'CP Set PF24 DELAY SEND VTAM VTAM "'
   End
   Else Do
       Say 'PF24 is now set to #CP LOGOFF'
       'CP Set PF24 IMMED #CP LOGOFF'
   End
       CP 'MSG OP 'SECUSER 'is now SECONDARY USERID'
            ' for VM/VTAM service machine'
    Say ' '
    Say ' '
    Say ' '
 End
 Exit
/*  Exit this EXEC                                               */
EXIT:
 'DESBUF'
 Exit
/*  This is the HELP screen.                                     */
 HELP_SCREEN:
/*                          HELP SCREEN                          */
/*       *************************************************       */
/*       *                                               *       */
/*       *   VTAMSWAP  (userid)                          *       */
/*       *                                               *       */
/*       *************************************************       */
/*       VTAMSWAP   is used to change the SECONDARY USERID       */
/*                  specified in the VM/VTAM mini-blok to        */
/*                  another userid.                              */
/*                                                               */
/*                  This EXEC allows the specified user to be    */
/*                  the only user allowed to send commands to    */
/*                  the VM/VTAM service machine.                 */
/*       USERID     is an optional parameter which specifies     */
/*                  the NEW userid to be designated as the       */
/*                  SECONDARY USERID.  If no USERID is entered,  */
 /*                  the default is the USERID of the VM machine  */
/*                  running this EXEC.                           */
/*       NOTE       If you use this EXEC to change the SECONDARY */
/*                  USERID, please be sure to change it back to  */
/*                  the official SECONDARY USERID (usually the   */
/*                  VM OPERATOR) when you are through and before */
/*                  LOGGING OFF.                                 */
/*       SECURITY   Since this EXEC issues privileged CP         */
/*                  commands, you must be on a CP CLASS C        */
/*                  privileged machine to run this EXEC.         */
/*                                                               */

Kevin Potter
Systems Programmer
Douglas County (USA)     ) Xephon 1988



















































