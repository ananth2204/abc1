 MVS/VTAM to VM

We run MVS under VM with three CICS regions, and TSO.  We wanted to re-set back
to VM without having to sign on to CICS first.

The solution was the following VTAM application program.  All the user needs to
do is to log on to this application and he is re-set to VM.  This program runs
as a started task but is swapped out by MVS unless it is being used, so there is
little storage overhead.  We also modified the USSTAB for VTAM to make a menu of
log-ons.  This made logging on to CICS and TSO, and dropping to VM, easier.


MVS TO VM

//CPRESET  JOB  MSGCLASS=X,REGION=2048K
//ASM    EXEC PGM=IFOX00,PARM='DECK,NOOBJECT'
//SYSLIB   DD DSN=SYS1.MACLIB,DISP=SHR
//         DD DSN=SYS1.AMACLIB,DISP=SHR
//SYSUT1   DD UNIT=DISK,SPACE=(1700,(400,400))
//SYSUT2   DD UNIT=DISK,SPACE=(1700,(400,400))
//SYSUT3   DD UNIT=DISK,SPACE=(1700,(400,400))
//SYSPRINT DD SYSOUT=A
//SYSPUNCH DD DSN=&&OBJMOD,DISP=(,PASS),UNIT=DISK,
//            DCB=(RECFM=FB,LRECL=80,BLKSIZE=400),
//            SPACE=(400,(100,100))
//SYSIN    DD *
         TITLE 'MVS/SP ACF/VTAM VM CP RESET PROGRAM'
         PRINT NOGEN
CPRESET  CSECT
         STM   R14,R12,12(R13)
         BALR  R3,R0
         USING *,R3
         ST    R13,SAVE1+4
         LA    R15,SAVE1
         ST    R15,8(R13)
         LR    R13,R15
CPENACB  EQU *
         OPEN  ACB1
         LA    R5,RPL1
         USING IFGRPL,R5
         SETLOGON RPL=RPL1,OPTCD=START
WAIT     EQU *
         WAIT  ECBLIST=ECB1
CLOSACB  EQU *
         MVI   TPENDFLG,X'FF'
         CLOSE ACB1
         L     R13,SAVE1+4
         LM    R14,R12,12(R13)
         BR    R14
ACB1     ACB   AM=VTAM,APPLID=APPL1,EXLST=EXLST1,MACRF=LOGON
EXLST1   EXLST AM=VTAM,LOGON=LOGON1,TPEND=TPEND1
RPL1     RPL   AM=VTAM,ACB=ACB1
         DS    0F
ECB1     DC    X'80',AL3(TPENDECB)
TPENDECB DC    F'0'
TPENDFLG DC    X'00'
APPL1    DC    X'08'
         DC    CL8'CPRESET'
SAVE1    DS    18F
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
 R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         LTORG
         IFGRPL AM=VTAM
         ISTUSFBC
         IFGACB AM=VTAM
         IFGEXLST AM=VTAM
CPRESET  CSECT
LOGON1   BALR  R3,R0
         USING *,R3
         LA    R13,SAVE2
         ST    R14,SAVE3
         L     R6,=A(TPENDFLG)
         TM    0(R6),X'FF'
         BO    RETURN
         LR    R6,R1
         L     R4,4(R6)
         LA    R5,RPLICONN
         USING IFGRPL,R5
         USING ISTDNIB,R7
         LA    R7,NIB1
         MVC   NIBSYM,0(R4)
         OPNDST RPL=RPLICONN,OPTCD=(SYN,ACCEPT),NIB=NIB1
         LTR   R15,R15
         BZ    SESSOK
         B     RETURN
SESSOK   EQU *
         MVC   CPCMD+9(3),NIBSYM+1
         MODESET MODE=SUP
         LRA   R0,CPCMD
         LA    R1,12
         DC    X'83010008'
         MODESET MODE=PROB
         CLSDST RPL=RPLICONN,OPTCD=SYN
RETURN   EQU *
         L     R14,SAVE3
         BR    R14
         DS    0F
SAVE2    DS    18F
SAVE3    DS    F
CPCMD    DC    C'CP RESET 000'
NIB1     NIB   MODE=RECORD
RPLICONN RPL   AM=VTAM,ACB=ACB1
         LTORG
         ISTDNIB
CPRESET  CSECT
TPEND1   BALR  R3,R0
         USING *,R3
         ST    R14,TPENDS14
         L     R4,=A(TPENDEC8)
         POST  (R4)
         L     R14,TPENDS14
         BR    R14
TPENDS14 DS    F
         LTORG
         END   CPRESET
/*
//LNKEDT EXEC PGM=IEWL,PARM='LIST,XREF,LET,NCAL,AC=1'
//SYSUT1   DD UNIT=DISK,SPACE=(1024,(100,50))
 //SYSPRINT DD SYSOUT=A
//SYSLMOD  DD DSN=SYS1.VTAMLIB,DISP=SHR
//SYSIN    DD DSN=&&OBJMOD,DISP=(OLD,PASS)
//SYSLIN   DD *
 INCLUDE SYSIN
 NAME CPRESET(R)
/*

Paul Jespersen
Systems Programmer
Gulf States Toyota (USA)      ) Gulf States Toyota 1987























































