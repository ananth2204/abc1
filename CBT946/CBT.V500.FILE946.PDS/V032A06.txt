DMKSNT maintenance

The VM System Name Table (DMKSNT) is one of the most
difficult maintenance items in most VM shops.  Part of this
difficulty is due to the fact that there is much manual work
involved in checking and setting various options.  There are few
safeguards against overlapping the disk area in which the saved
segments are stored or against overlapping virtual addresses at
which the segments will be attached.

The SNTMAP EXEC provided by IBM helps somewhat in
checking parameters.  As an adjunct, I wrote this EXEC, called
PAGECALC, which takes in a starting cylinder/page address and
a page count, then calculates and displays the ending
cylinder/page  address.  By using PAGECALC, it is relatively
easy to compute where a shared segment will be placed on disk
and where the next available area will be.


/*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*/
/* PageCalc - Calculates ending Cyl/Page# for DMKSNT entries.        */
/*            Command line inputs are starting cylinder/page address */
/*            and the number of pages.  The ending cylinder/page     */
/*            address is displayed after calculation.                */
/*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*/
   Signal On Syntax
   Page_per_cyl=150                 /* Number of 4K pages on 3380    */
   Max_cyl=884                      /* Highest cylinder on 3380 A/D  */
   Arg Beg_cyl ',' Beg_pag '+' N_pages .
   If Beg_pag<1 | Beg_pag>Page_per_cyl | (Beg_pag//1)¬=0 |,
      Beg_cyl<0 | Beg_cyl>Max_cyl      | (Beg_cyl//1)¬=0 |,
      N_pages<1                        | (N_pages//1)¬=0 Then
      Signal Syntax
   Beg_rel_page=((Beg_cyl*Page_per_cyl)+Beg_pag)-1
   End_rel_page=Beg_rel_page+N_pages
   End_cyl=End_rel_page % Page_per_cyl
   End_pag=End_rel_page // Page_per_cyl
   Say 'Location:' Format(Beg_cyl)','Format(Beg_pag) 'Thru',
       Format(End_cyl)','Format(End_pag) '-' N_pages 'pages.'
   Exit
Syntax:
   Say 'Syntax error'
   Say 'Correct syntax: PAGECALC strtcyl,strtpage+pagecount'
   Say '   strtcyl must be from 0 to' Max_cyl
   Say '   strtpage must be from 1 to' Page_per_cyl
   Say '   pagecount must be greater than 1'
   Exit 88


T Donnelly
Systems Programmer
Loral Electro-Optical Systems (USA) ) Loral EOS 1989

