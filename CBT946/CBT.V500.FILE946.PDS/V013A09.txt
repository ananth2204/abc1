 A VM news bulletin utility program

VM/SP provides broadcast commands such as 'MESSAGE ALL' or 'WARN ALL' for
sending messages to all signed-on users;  and command 'SET LOGMSG' for sending
messages to logging-on users.  However, there is no facility for informing users
at the time they attempt to sign-on to the system of the state of the system or
installation news relating to them.  The following routine was therefore
designed and an example of the result is shown in Figure 1.

A news item of maximum 136 characters can be entered from the VM operator's
console;  or 126 characters maximum from a VM user with the class A, B or C
authorisation.  The news item is entered via a user-created CP command call
'APB' (eg APB message text) and the message text is then stored in a user-
written (nucleus-resident) program called 'CPAPB'.

This nucleus-resident program performs two functions, hence two entry points.
Entry point 'CPAPB' is the 'APB' command handler.  Depending on whether or not
the command is followed by message text the program clears or saves the news
item.  The second entry point 'IMSLAPB' is for displaying the stored message
text if there is one.  This entry point is referenced by two routines, DMKCFM
and DMKLOG, both of which we have to modify.

Modification steps under VM/SP 4.0 are as follows:

1    Code and assemble the nucleus-resident routine 'CPAPB'.

2    Create UPDATE files DMKCFC AUXLCL and DMKCFC IMSL03 using XEDIT, then re-
assemble DMKCFC with VMFASM DMKCFC DMKSP.

3    Create UPDATE files DMKCFM AUXLCL and DMKCFM IMSL03 using XEDIT, then re-
assemble DMKCFM with VMFASM DMKCFM DMKSP.

4    Create UPDATE files DMKLOG AUXLCL and DMKLOG IMSL03 using XEDIT, then re-
assemble DMKLOG with VMFASM DMKLOG DMKSP.

5    Copy DMKSP CNTRL and insert 'LCL AUXLCL' card.

6    Copy CPLOAD EXEC and insert '&1 &2 &3 CPAPB' card.

7    Regenerate CP nucleus with VMFLOAD CPLOAD DMKSP.


CPAPB ASSEMBLE

         ISEQ  73,80
         COPY  OPTIONS
         COPY  LOCAL
         EJECT
         PRINT NOGEN
*                       PROGRAM STARTS
CPAPBEN  CSECT
MODID    DC    CL8'CPAPB'
         USING PSA,R0
         USING IOBLOK,R10
         USING VMBLOK,R11
         EXTRN DMKSCNFD            SCAN CONSOLE INPUT ROUTINE
         ENTRY CPAPB
         ENTRY IMSLAPB
CPAPB    EQU   *
         USING CPAPB,R12           THIS PROGRAM BASE ADDR
         STM   R0,R15,BALRSAVE     SAVE ALL REGS
*                 GET THE INPUT BROADCAST MESSAGE
         LA    R6,APBDATA          POINT TO SAVE APB MESSAGE AREA
         CALL  DMKSCNFD            CHECK ANY SPECIFIC DOS WANTED DIAL
         BNZ   CLEARMSG            NO INPUT MSG - DEFAULT CLEAR MSG
         MVI   0(R6),C' '          CLEAR THE SAVE MSG AREA
          MVC   1(135,R6),0(R6)
         MVC   APBCNT,=F'136'      FLAG THERE IS APB MSG (DEFAULT)
         SR    R5,R5               CLEAR REG5 & USE AS COUNTER
MSGLOOP  EQU   *
         LR    R2,R1               STARTING FIELD ADDR
         LR    R3,R0               LENGTH OF FIELD
         LR    R7,R0               LENGTH OF FIELD
         ALR   R5,R0               TOTAL THE INPUT MSG LENGTH
         LA    R5,1(R5)            DON'T FORGET THE SPACE DELIMITER
         CL    R5,=F'136'          CHECK STILL ROOM IN MSG SAVE AREA
         BH    DONE                ..NO MORE ROOM, CONSIDER IT DONE
         MVCL  R6,R2               SAVE THE MESSAGE
         LA    R6,1(R6)            NXT STORE LOCATION IN DATA AREA
         CALL  DMKSCNFD            GET NEXT INPUT WORD
         BNZ   DONE                ...NO MORE MSG TEXT TO BE SAVED
         B     MSGLOOP             ...YES, MORE GO LOOP
DONE     EQU   *
         ST    R5,APBCNT           SAVE THE MSG LENGTH COUNT
         B     FINISH              LETS GET OUT OF HERE
CLEARMSG EQU   *
         MVI   APBDATA,X'40'       CLEAR THE APB MSG AREA
         MVC   APBDATA+1(135),APBDATA
         MVC   APBCNT,=F'0'        CLEAR THE INPUT CHAR COUNTER
         B     FINISH              GO TO EXIT
IMSLAPB  EQU   *
         USING IMSLAPB,R12         THIS PROGRAM BASE ADDR
         STM   R0,R15,BALRSAVE     SAVE ALL REGS
         CLC   APBCNT,=F'0'        IS THERE AN APB ISSUED ?
         BZ    FINISH              ...NO, FORGET IT
         LA    R1,APBBUF           POINT TO THE BULLETTIN BUFFER
         L     R0,=F'234'          SET THE MAX APB MESSAGE LENGTH
         L     R2,=A(HILIGHT)      SET HILIGHT OF MSG
         CALL  DMKQCNWT,PARM=NORET+ALARM(,R2)   SEND THE APB
*                   RETURN CONTROL BACK TO VM
FINISH   EQU   *
         LM    R0,R11,BALRSAVE     RESTORES REGS
         SVC   12                  GO HOME
         EJECT
*            DISPLAY MESSAGE AREA / CHARACTER STRING AREA
         SPACE 3
         DS    0F
APBCNT   DC    X'00000000'         INPUT MSG CHAR COUNT
APBBUF   DS    0CL234
APBHDR   DC    X'15'               CARRIAGE RETURN
         DC    CL29' '
         DC    CL18'IMSL NEWS BULLETIN'
         DC    X'15'               CARRIAGE RETURN
         DC    CL29' '
         DC    CL18'******************'
         DC    X'15'               CARRIAGE RETURN
         DC    X'15'               CARRIAGE RETURN
APBDATA  DC    CL136' '            INPUT APB MSG AREA
         LTORG
         PSA
         COPY  EQU
         COPY  CONBUF
         COPY  IOBLOKS
         COPY  VMBLOK
         END


DMKCFC AUXLCL

IMSL03        INCLUDE 'APB' IN DMKCFC COMMAND LIST


 DMKCFC IMSL03

./ I 13330000 $
         COMMD COMMAND=(APB,3),EP=CPAPB,CLASS=A+B+C


DMKCFM AUXLCL

IMSL03       INSERT CALL 'IMSLAPB' ROUTINE CODINGS


DMKCFM IMSL03

./ I 02000000 $ 02001000 1000
         EXTRN IMSLAPB        IMSL NEWS BULLETIN ROUTINE
./02656660 $ 02656800 100
         CALL  IMSLAPB        CHECK ANY NEWS TO BROADCAST


DMKLOG AUXLCL

IMSL03        INSERT CALL 'IMSLAPB' ROUTINE CODINGS


DMKLOG IMSL03

./ I 00930000 $ 00930900 900
         EXTRN IMSLAPB        IMSL NEWS BULLETIN ROUTINE
./ I 09890000 $ 09891000 1000
         CALL  IMSLAPB        CHECK ANY NEWS TO BROADCAST


DMKSP CNTRL

TEXT MACS DMKTMP DMKSP DMKMAC DMSSP CMSLIB OSMACRO
LCL  AUXLCL                       <---- Insert this card
TMP  AUXTMP
TEXT AUXSP


CPLOAD EXEC

&CONTROL OFF
&1 &2 &3 DMKLD00E LOADER
&1 &2 &3 DMKPSA
    ""
    ""
&1 &2 &3 CPAPB                    <---- Insert this card
&1 &2 &3 DMKCPE
*    END OF THE RESIDENT
*    VM/370 NUCLEUS
&1 &2 &3 DMKTAP
    ""
    ""
&1 &2 &3 DMKVMG
*  THE END OF THE PAGEABLE VM/370 NUCLEUS.  DMKCKP, DMKCKD,DMKCKF
*  DMKCKH, CMKCKM, AND DMKCKN MUST BE THE LAST MODULES IN THE LOAD
*  LIST AND MUST REMAIN IN THEIR CURRENT ORDER.
&1 &2 &3 DMKCKP
&1 &2 &3 DMKCKD
&1 &2 &3 DMKCKF
&1 &2 &3 DMKCKH
&1 &2 &3 DMKCKM
&1 &2 &3 DMKCKN
&1 &2 &3 LDT      DMKSAVNC


Robert K F Ko
Senior Systems Programmer
Inchcape Management Services Limited (Hong Kong)  ) Xephon 1987






























































