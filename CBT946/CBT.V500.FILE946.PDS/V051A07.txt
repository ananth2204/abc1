Profiles for FILELIST and RDRLIST

Very often users would like to tailor their RDRLIST or
FILELIST profiles.  Certain changes, like colours, are
dependent on the individual tastes of each user, or dependent
on the hardware (for example, blue is almost invisible on some
PCs under certain light conditions).  On the other hand, certain
changes may be made for all users as company specified
defaults.  In any case a solution must be developed to allow the
users to make changes.

Changing the default profile to a different profile procedure can
be done with the CMS command DEFAULTS, but requires
copying the standard system profile to a new name, modifying
it, and making it available for the user to change as he wishes.
The next release may call for it all to be changed again.  Also,
the user must have detailed knowledge of how to change the
profile.

Another way to solve the problem is to change the default
profile using DEFAULTS to a procedure that first calls the
standard profile (PROFRLST) and then resets any values that
should be changed.  The problem with this solution is that you
cannot change the colours of the PF description without a lot of
extra coding to get the values that are already in the standard
profile.

We have chosen to make a minor modification to the standard
profiles PROFRLST and PROFFLST to call our own company
changes to the profiles. Because it is a very small change it is
easy to recreate and check to see if all the variables are still
valid at the next release change. All we do is give each
message line and its position to our company profile.  The
company file has lines of coding that have been extracted out
of the standard profiles and have been changed to our
requirements. The company file checks if the user also has a
procedure PROFFARB, and if found it will be executed to set
the colours the way the users wishes.

Below you will see the changes for the RDRLIST and
FILELIST that we have made.

Changes that need to be made to the PROFRLST are shown
below.

&C SET TOFEOF OFF
&C CURSOR FILE 1 1
*    ----  Company changes START  ----
&ARGLIST = &CONCAT OF &PL ; &MESSAGE.1 ; &PL1 ; &MESSAGE.2 ; &TL ; &MESSAGE.3
PROFRLFA PROFRLFA &ARGLIST
*    ----  Company changes END    ----
&EXIT
-TITLE &TYPE &MESSAGE.3
&EXIT

Company PF settings and colours are shown below.

/* profrlfa */
parse arg profrlfa arglist
if profrlfa ¬= 'PROFRLFA' then exit     /* prohibit usage as macro */
pin = pos('1=',arglist)                 /* change the pf-text */
arglist = overlay('1= Discard',arglist,pin)
pin = pos('10=',arglist)
arglist = overlay('10= Change    ',arglist,pin)
pin = pos('11=',arglist)
arglist = overlay('11= RDRSCAN   ',arglist,pin)
pin = pos('12=',message2)
arglist = overlay('12= Print ',arglist,pin)
parse value arglist with pl';'message1';'pl1';'message2';'tl';'message3
'COMMAND SET SYN LINEND / SNAME CMS DMSXMS A 7 14 16 23/
                                           COMMAND TOP/+1 COMMAND CUR C'
'COMMAND SET SYN LINEND / STYPE CMS DMSXMS A 16 23 7 14/
                                           COMMAND TOP/+1 COMMAND CUR C'
'COMMAND SET SYN LINEND / SCLAS CMS DMSXMS A 25 27 29 29 49 52/
                                           COMMAND TOP/+1 COMMAND CUR C'
'COMMAND SET SYN LINEND / SHOLD CMS DMSXMS A 49 52 25 27 29 29/
                                           COMMAND TOP/+1 COMMAND CUR C'
'COMMAND SET SYN LINEND / SSIZE CMS DMSXMS D 54 61/
                                           COMMAND TOP/+1 COMMAND CUR C'
'COMMAND SET SYN LINEND / SDATE CMS DMSXMS A 63 64 66 67 72 79/
                                           COMMAND TOP/+1 COMMAND CUR C'
'COMMAND SET SYN LINEND / SUSER CMS DMSXMS A 31 38 40 47 63 64
                                    66 67 72 79/COMMAND TOP/+1 &C CUR C'
'COMMAND SET PF01 MACRO EXECUTE CURSOR EXECIO 0 CP (STRING PURGE RDR /'
'COMMAND SET PF02 REFRESH#SDATE'
'COMMAND SET PF04 STYPE'
'COMMAND SET PF05 SDATE'
'COMMAND SET PF06 SUSER'
'COMMAND SET PF09 MACRO EXECUTE CURSOR EXEC RECEIVE / (NOLOG'
'COMMAND SET PF10 MACRO EXECUTE CURSOR RDRCHNG / R'
'COMMAND SET PF11 MACRO EXECUTE CURSOR RDRSCAN /'
'COMMAND SET PF12 MACRO EXECUTE CURSOR RDRTRAN /'
'COMMAND SET PF14 REFRESH#SDATE'
'COMMAND SET PF16 STYPE'
'COMMAND SET PF17 SDATE'
'COMMAND SET PF18 SUSER'
'COMMAND SET PF21 MACRO EXECUTE CURSOR EXEC RECEIVE / (NOLOG'
'CMS GLOBALV SELECT QSET GET' setemsg setimsg
'CP SET EMSG OFF'
'CP SET IMSG OFF'
'COMMAND SET RESERVED' pl  'BLUE NONE NOHIGH' message1
'COMMAND SET RESERVED' pl1 'BLUE NONE NOHIGH' message2
'STATE PROFFARB XEDIT *'
if rc = 0 then 'PROFFARB PROFFARB'
                          arglist                              'message3
'CP SET' setemsg
'CP SET' setimsg
/* Basler Changes    PF01 DISCARD PF10 RDRCHNG PF11 RDRSCAN PF12 RDRTRAN
   'COMMAND CUR C BEI SET SYN' */

The company default colours can be changed by the user if he
wishes.  He must copy the following file to his A disk and
change it.

/* Set colours for FILELIST and RDRLIST */
parse arg proffarb arglist
if proffarb ¬= 'PROFFARB' then exit
parse value arglist with pl';'t1';'pl1';'t2';'tl';'t3
'COMMAND SET RESERVED' pl    'GREEN   NONE NOHIGH' t1
'COMMAND SET RESERVED' pl1   'GREEN   NONE NOHIGH' t2
'COMMAND SET RESERVED' tl    'GREEN   NONE NOHIGH' t3
'COMMAND SET COLOR   *        WHITE   NONE NOHIGH'
'COMMAND SET COLOR   CURLINE  TURQ    NONE   HIGH'
'COMMAND SET COLOR   FILEAREA TURQ    NONE   HIGH'
'COMMAND SET COLOR   CMDLINE  GREEN   NONE NOHIGH'
'COMMAND SET COLOR   MSGLINE  RED     NONE   HIGH'
'COMMAND SET COLOR   IDLINE   YEL     REV    HIGH'
exit

Changes that need to be made to the PROFFLST are shown
below.

&C SET TOFEOF OFF
&C CURSOR FILE 1 1
*    ----  Company changes START  ----
&ARGLIST = &CONCAT OF &PL ; &MESSAGE.2 ; &PL1 ; &MESSAGE.3 ; &TL ; &MESSAGE.1
PROFFLFA PROFFLFA &ARGLIST
*    ----  Company changes END    ----
&EXIT
-TITLE &TYPE &MESSAGE.1
&EXIT

Company PF settings and colours are shown below.

/* profflfa Baseler changes for PROFFLST */
parse arg profflfa arglist
if profflfa ¬= 'PROFFLFA' then exit
p10 = pos('10=',arglist)
arglist = overlay('10= Sort(name)',arglist,p10)
parse value arglist with pl';'message2';'pl1';'message3';'tl';'message1
'CMS GLOBALV SELECT QSET GET' setemsg setimsg
'CP SET EMSG OFF'
'CP SET IMSG OFF'
'SET LINEND OFF'
'COMMAND SET PF10 SNAME#COMMAND CURSOR COLUMN'
'COMMAND SET PF13 TOP'
'COMMAND SET PF14 BOT#-17'
'COMMAND SET PF17 U'
'COMMAND SET PF18 N'
'COMMAND SET PF21 ='
'COMMAND SET PF22 U 10'
'COMMAND SET PF23 N 10'
'COMMAND SET PF24 ?'
'SET LINEND ON'
'COMMAND SET COLOR   FILEAREA GREEN   NONE NOHIGH'
'COMMAND SET RESERVED' pl  'BLUE NONE NOHIGH' message2
'COMMAND SET RESERVED' pl1 'BLUE NONE NOHIGH' message3
'COMMAND SET RESERVED' tl  'BLUE NONE NOHIGH' message1
'STATE PROFFARB XEDIT *'
if rc = 0 then 'PROFFARB PROFFARB' arglist
'CP SET' setemsg
'CP SET' setimsg
exit

William K Mongan
Systems Programmer
Basler Versicherung (West Germany)             c William K Mongan 1990


