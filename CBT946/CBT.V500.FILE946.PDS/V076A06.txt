An SAA/CUA menu system for VM - continued

This month we conclude the article describing an SAA/CUA
interface used to provide a multi-faceted menuing system.

Header_2:
   screen_line.2 = left('-',70,'-')||left(strip(port_id),9,'-')
   'COMMAND SET RESERVE 2 B N ' left('-',70,'-'),
                                           ||left(strip(port_id),9,'-')
   'COMMAND SET RESERVE 3 B N' title_line
return
Header_1:
   'COMMAND SET RESERVE 1 W H' ,
                        left('¬A ¬0Services ¬A ¬0Options ¬A ¬0Help',79)
return
Toggle_folders:
   current_front = front_folder
   front_folder   = substr(next_folder.num_folders,current_front+1,1)
   if num_folders > 1 then
      middle_folder  = substr(next_folder.num_folders,current_front+2,1)
   if num_folders > 2 then
      back_folder    = substr(next_folder.num_folders,current_front+3,1)
   if num_folders > 3 then
      pers_folder    = substr(next_folder.num_folders,current_front+4,1)
   if Level = 1 then
      address COMMAND 'GLOBALV SELECT WITMENU SETP FOLDER' front_folder
   call create_folders
   call display_screen
return
Create_folders:
   if num_folders > 3 then do
      screen_line.5  = "        .------------------------------------."
      screen_line.6  = "          |"left(menu.pers_folder.0,38,' ') "|"
   end
   else do
      screen_line.5  = " "
      screen_line.6  = " "
   end
   if num_folders > 2 then do
      screen_line.7  = "       .------------------------------------",
                                         "."copies("  |",num_folders-3)
      screen_line.8  = "       |"left(menu.back_folder.0,38,' '),
                                         "|"copies("  |",num_folders-3)
   end
   else do
      screen_line.7  = " "
      screen_line.8  = " "
   end
   if num_folders > 1 then do
      screen_line.9  = "    .------------------------------------",
                                         "."copies("  |",num_folders-2)
      screen_line.10 = "    |"left(menu.middle_folder.0,38,' ') ,
                                         "|"copies("  |",num_folders-2)
   end
   else do
      screen_line.9  = " "
      screen_line.10  = " "
   end
   screen_line.11 = " .------------------------------------.",
                                            copies("  |",num_folders-1)
   screen_line.12 = " |"left(menu.front_folder.0,38,' ') "|",
                                            copies("  |",num_folders-1)
   screen_line.13 = " |¬5  1."left(descr.front_folder.1,30,' ') ,
                                          "|"copies("  |",num_folders-1)
   screen_line.14 = " |¬5  2."left(descr.front_folder.2,30,' ') ,
                                         "|"copies("  |",num_folders-1)
   screen_line.15 = " |¬5  3."left(descr.front_folder.3,30,' ') ,
                                         "|"copies("  |",num_folders-1)
   screen_line.16 = " |¬5  4."left(descr.front_folder.4,30,' ') "|"
   screen_line.17 = " |¬5  5."left(descr.front_folder.5,30,' ') "|"
   screen_line.18 = " |¬5  6."left(descr.front_folder.6,30,' ') "|"
   if num_folders > 1 then do
      screen_line.16 = screen_line.16||"  |"
      screen_line.17 = screen_line.17||"  |"
      screen_line.18 = screen_line.18||"  |"
   end
   if num_folders > 2 then do
      screen_line.16 = screen_line.16||"  |"
      screen_line.17 = screen_line.17||"  |"
      screen_line.18 = screen_line.18||"--'"
   end
   if num_folders > 3 then
      screen_line.16 = screen_line.16||"--'"
   screen_line.19 = " "LS"¬5  7."left(descr.front_folder.7,30,' ') "|"
   screen_line.19 = " |¬5  7."left(descr.front_folder.7,30,' ') "|"
   screen_line.20 = " |¬5  8."left(descr.front_folder.8,30,' ') "|"
   if num_folders > 1 then do
      screen_line.19 = screen_line.19||"  |"
      screen_line.20 = screen_line.20||"--'"
   end
   screen_line.21 = " |¬5  9."left(descr.front_folder.9,30,' ') "|"
   screen_line.22 = " '------------------------------------'"
   do i = 6 to 14
      k = i - 5
      if pos('¬',screen_line.i) ¬= 0 then do
         if k = 8 then
            screen_line.i = overlay,
                          (msg_window.k,left(screen_line.i,55,' '),54)
         else
            screen_line.i = overlay,
                          (msg_window.k,left(screen_line.i,55,' '),56)
      end
      else
         screen_line.i = overlay,
                          (msg_window.k,left(screen_line.i,52,' '),53)
   end
return
Display_screen:
   do i = 4 to lscreen.1-2
     'COMMAND SET RESERVE' i 'GREEN N' screen_line.i
   end
return
Calendar:
   current_year    = substr(date(o),1,2)
   current_month   = substr(date(o),4,2)
   current_date    = substr(date(o),7,2)
   select
      when current_month = 1 then days= 31
      when current_month = 2 then do
         if current_year//4 = 0 then days = 29
                                else days = 28
      end
      when current_month = 3 then days= 31
      when current_month = 5 then days= 31
      when current_month = 7 then days= 31
      when current_month = 8 then days= 31
      when current_month = 10 then days= 31
      when current_month = 12 then days= 31
      otherwise days = 30
   end
   do i = 1 to days
      day.i = right(i,2,' ')
   end
   leap_years = trunc(current_year/4)+1
   years =  current_year - leap_years - 1
   total_days = (years*365) + (leap_years*366) + date(d)
   first_of_month = ((total_days - current_date)//7)+1
   pos. = "    "
   p = first_of_month + 1
   if p = 8 then p = 1
   z = 1
   if days ¬= 0 then do days
      pos.p = ' 'day.z' '
      if strip(day.z,l,' ') = strip(current_date,l,'0') then
                                                 pos.p= '¬0'day.z'¬6'
      p = p + 1
      z = z + 1
   end
return
Cal_window:
   msg_window. = ''
   set_calendar = 'ON'
   year = substr(date(s),1,4)
   msg_window.1 = ' 'year || centre(date(m),18) || year
   msg_window.2 = '  S   M   T   W   T   F   S'
   msg_window.3 = left(pos.1||pos.2||pos.3||pos.4,
                                             ||pos.5||pos.6||pos.7,30)
   msg_window.4 = left(pos.8||pos.9||pos.10||pos.11,
                                         ||pos.12||pos.13||pos.14,30)
   msg_window.5 = left(pos.15||pos.16||pos.17||pos.18||pos.19,
                                                 ||pos.20||pos.21,30)
   msg_window.6 = left(pos.22||pos.23||pos.24||pos.25||pos.26,
                                                 ||pos.27||pos.28,30)
   msg_window.7 = left(pos.29||pos.30||pos.31||pos.32||pos.33,
                                                 ||pos.34||pos.35,30)
   msg_window.8 = left(pos.36||pos.37||pos.38||pos.39||pos.40||pos.41,
                                                         ||pos.42,30)
   msg_window.9 = left(' Day of year: 'substr(date(J),3,3),20)
return
Urgent_msg:
   msg_window. = ''
   set_calendar = 'OFF'
   address COMMAND 'STATE $URGENT $MESSAGE A'
   if rc = 0 then
      'EXECIO * DISKR $URGENT $MESSAGE * (FINIS STEM MSG_WINDOW.'
return

Steve Whan                  c Western Integrated Technologies Inc 1992
System Programmer
Western Integrated Technologies Inc (Canada)
