Establish a secondary user-id

This EXEC is a variation on other programs that I've seen in
VM Update which establish a secondary user-id for a
disconnected service virtual machine.  The others that have
been published either required a mod to CP, or were in BAL.  I
like BAL, but isn't REXX much easier?  The only drawback to
this EXEC is that it requires CP class C to run.

Please note that some of the characters shown in this EXEC are
simply for highlighting the text on the terminal.  It shows up as
'-9' in the code.  Please use whatever you wish in this case.


SECUSER  EXEC

/* Doc: Establish a secondary user-id for another user-id (SCIF)
   Note:  You must have CP class C authority to use this EXEC.
          This has been tested under VM/SP HPO 5.
          It also appears to function under VM/SP 6 although it
          doesn't always display the user-id with the 'QUERY' function
          because of alignment.                                     */
secuser_offset = 536;  /* decimal offset of SECUSER in VMBLOK       */
                       /*        this is x'218'                     */
PARSE UPPER ARG disc_user my_id off .
if disc_user = '?' then do
   say 'Format is: SECUSER disconnected_user_id secondary_user_id'
   say '   -or-  : SECUSER disconnected_user_id OFF'
   say '   -or-  : SECUSER disconnected_user_id QUERY'
   say 'Example:   SECUSER PROCAL MAINT'
   exit 0
end
query = 'NO'
'EXECIO * CP (STRING Q' disc_user
if rc ¬= 0 then do
   'DESBUF'
   say 'SECUSER: User:' disc_user 'isn''t logged on.'
   exit 4
end
PARSE PULL . . terminal
if terminal ¬= 'DSC' then do
   say 'SECUSER: Warning!' disc_user 'is logged onto a terminal.'
end
if my_id = 'QUERY' then do
   CALL query_secuser;
   'desbuf';
   exit 0
end
if off ¬= '' & off ¬= 'OFF' then do
   say 'Extraneous parmater ('off') ignored.'
end
if my_id = 'OFF' | off = 'OFF' then do
   off = 'OFF';
   my_id = left('',8,' ');
   CALL new_secoper;            /* set secoper to blanks */
   exit
end
if my_id = '' then,
  my_id = userid();
/* ------------ Now, set the secoper ----------------*/
CALL new_secoper;
/* ------------------------------- */
exit 0
/* ------------------------------- */
NEW_SECOPER:
  'EXECIO * CP (STRING LOC' disc_user
  PARSE PULL . . vmblok_addr
  dec = x2d(vmblok_addr) + secuser_offset; /* x'218' = vmblok offset */
  addr_secuser = d2x(dec,6);               /* change back to hex     */
  'EXECIO * CP (STRING DCP T'addr_secuser'.8'
  if rc ¬= 0 then do
     say 'SECUSER: DCP failed. rc='rc
     'desbuf';
     exit 16
  end
  PARSE PULL . . . . . stuff .
  current_secuser = substr(stuff,10,8);
  if current_secuser = my_id then do;
     say 'SECUSER: It''s already set to your requested id.'
     exit 4
  end
  my_id = left(my_id,8);
  my_id_hex = c2x(my_id);
  my_id_hex = substr(my_id_hex,1,8)||' '||substr(my_id_hex,9,8);
  if my_id ¬= ' ' then do
     'EXECIO 0 CP (STRING SP PUN' my_id
     if rc ¬= 0 then do
        say 'SECUSER:' my_id 'does not exist - request ignored.'
        exit 8
     end
     'EXECIO 0 CP (STRING SP PUN SYSTEM'
  end
  'EXECIO 0 CP (STRING STCP' addr_secuser my_id_hex;
  if rc ¬= 0 then do
     say 'SECUSER: STCP failed with rc='rc
     exit rc
  end
  if my_id ¬= ' ' then,
     say 'SECUSER: Set SECOPER for' disc_user 'to' my_id;
  else say 'SECUSER: Set SECOPER for' disc_user 'to blanks';
return
/* ------------------------------- */
QUERY_SECUSER:
  'EXECIO * CP (STRING LOC' disc_user
  PARSE PULL . . vmblok_addr
  dec = x2d(vmblok_addr) + secuser_offset; /* x'218' = vmblok offset */
  addr_secuser = d2x(dec,6);               /* change back to hex     */
  'EXECIO * CP (STRING DCP T'addr_secuser'.8'
  if rc ¬= 0 then do
     say 'SECUSER: DCP failed. rc='rc
     'desbuf';
   exit 16
  end
  PARSE PULL . . . . . stuff .
  current_secuser = substr(stuff,10,8);
  temp = strip(current_secuser);
  if temp ¬= '' then,
  say 'SECUSER: The current secondary user for9'disc_user'-is9',
                          current_secuser '-';
  else say 'SECUSER: The is no secondary console for:' disc_user'.'
return

Pat Tierce
Systems Software Specialist
State of Kansas (USA)

