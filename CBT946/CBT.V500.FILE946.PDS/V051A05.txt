Improving SQL performance

If you have SQL/DS running under VM/SP, you can improve
its performance by making those CP modules that are heavily
used by SQL/DS part of the CP resident nucleus.  CPLOAD
EXEC, which is distributed on VM PUT tapes or on Product
tapes for initial installation of VM/SP, contains modules
DMKIUA, DMKIUE, DMKIUL and DMKBIO but they are
not in the resident part of CP nucleus.  Therefore, these
modules have to be moved to the resident part of the nucleus
every time a new service level of VM/SP is installed.  Because
it is very easy to forget, I wrote SQLCP EXEC to do this for
me.  I have changed CP setup in SPGEN PROFILE (Figure 1)
so that all processing is done automatically whenever a new CP
nucleus is built. SQLCP will save the existing version of
CPLOAD EXEC, before creating the new one, by copying it to
your A disk.

This program is running on VM/SP Release 5 (SL 514).


SQLCP EXEC

/* ************************************ */
/* .... SQL - Improve Perfomances ....  */
/* CP modules ---> CP Resident Nucleus  */
'LISTFILE CPLOAD EXEC * (STACK'
If rc > 0 Then Exit rc
Parse Pull . . fm .
'DESBUF'
'COPY CPLOAD EXEC' fm 'CPLOAD OLDEXEC A (REP OLDD'
'SET CMSTYPE HT'
'STATE CPLOADX EXEC A'
If rc = 0 then 'ERASE CPLOADX EXEC A'
'SET CMSTYPE RT'
i = 0
sql_mod = 'DMKIUA DMKIUE DMKIUL DMKBIO'
num_mod = Words(sql_mod)
Say 'SQLCP in progress...'
'EXECIO * DISKR CPLOAD EXEC' fm '(FINIS'
Do Queued()
   i = i + 1
   Pull line.i
   If Word(line.i,4) = 'DMKCPE' Then do   /* Last Resident module */
      eline  = line.i
      Do m = 1 to num_mod
         line.i = '&1 &2 &3' Word(sql_mod,m)
         i = i + 1
         End
      line.i = eline
      End
      Else do
             Do m = 1 to num_mod
                If Word(line.i,4) = Word(sql_mod,m) Then do
                   i = i - 1
                   Leave
                   End
                End
        End
   End
Say 'Creating a new CPLOAD...'
'EXECIO' i 'DISKW CPLOADX EXEC A (FINI STEM LINE.'
If rc > 0 Then Exit rc
'COPY CPLOADX EXEC A CPLOAD EXEC' fm '(REP RECFM F LRECL 80'
If rc > 0 Then Exit rc
'ERASE CPLOADX EXEC A'
Say 'SQLCP EOJ'
Exit
Teodor Jurak
Systems Programmer
Jadrolinija Rijeka (Yugoslavia)                    c  Jadrolinija 1990


