 Auto-dialling to a virtual machine
In our shop we have several CICS partitions running on several VSE guest virtual
machines.  However, there is only one VSE machine which the users are able to
'DIAL' into.  Although they have no other virtual machine to dial into, users
must still type the entire DIAL command and parameters (eg DIAL ATESTVSE T484 ).
I created the following CP mod to relieve users of unnecessary keystrokes and
having to remember the virtual address to specify in order to get the proper
terminal characteristics when in CICS.  When the mod is applied, the user need
only type the letter D and then press enter.  The user will be dialled to the
appropriate virtual machine at the proper virtual address.  A user can still
dial into any other virtual machine simply by typing the entire DIAL command and
parameters as before.  This mod was developed and used under VM/HPO Release 3.
It is currently being used under VM/HPO Release 4.2.  The mod has also been
successfully tested on VM/HPO Release 5.  The line numbers given are for release
4.2.  For other releases, the mod must be inserted after the call to DMKSCNFD,
which is three lines after the label NOTRQ in the module DMKDIA.  The COPY
statement at the end goes with all the rest of the COPY lines in DMKDIA which
are towards the end of the source code.
./ I 01870000          $ 1870300 300                  02/05/88 10:21:48
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         BZ    MOREDIAL       IF USERID SPECIFIED, DO NORMAL   PROGRESS
         USING BUFFER,R9                                       PROGRESS
         STM   R14,R12,MYDSAV  SAVE ALL USEFUL REGISTERS       PROGRESS
         MVC   BUFIN(CMDLEN),CMDLINE   SET NEW INPUT BUFFER    PROGRESS
         L     R8,VMTERM    GET ADDR OF TERMINAL FOR DMKSCNRD  PROGRESS
         CALL  DMKSCNRD     TO GET REAL DEVICE ADDRESS         PROGRESS
         CALL  DMKCVTBH CONVERT HEX IN R1 TO PRINTABLE CHARS   PROGRESS
         STCM  R1,B'0111',BUFIN+CSTLEN  MOVE CHARS TO BUFFER   PROGRESS
         LA    R4,CMDLEN-1    LENGTH OF NEW COMMAND BUFFER     PROGRESS
         ST    R4,BUFCNT      SAVE IN CORRECT SPOT IN BUFF     PROGRESS
         LA    R4,1(,R9)      GET PAST 'D'  IN COMMAND BUFFER  PROGRESS
         ST    R4,BUFNXT      ADDR OF NEXT CHAR TO SCAN        PROGRESS
         B     DONEMMOD       JUMP AROUND SAVE AREA            PROGRESS
         DS    0D             DEMAND DOUBLEWORD ALIGNMENT      PROGRESS
MYDSAV   DS    9D                      LOCAL SAVE AREA         PROGRESS
CMDLINE  DC    C'D ATESTVSE '                                  PROGRESS
CSTLEN   EQU   *-CMDLINE                                       PROGRESS
CMDADDR  DC    C'    '  4 CHARS IN CASE MACHINE GOES TO XA     PROGRESS
CMDLEN   EQU   *-CMDLINE                                       PROGRESS
         DS    0H                                              PROGRESS
DONEMMOD EQU   *                                               PROGRESS
         LM    R14,R12,MYDSAV GET BACK ALL USEFUL REGISTERS    PROGRESS
         CALL  DMKSCNFD       GET USERID FROM COMMAND LINE     PROGRESS
MOREDIAL EQU   *                                               PROGRESS
         DROP  R9                                              PROGRESS
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
./ I 12900000          $ 12905000 5000                02/05/88 10:21:48
         COPY  CONBUF                                          PROGRESS
Bernard McIlroy
Systems Programmer
Progress Lighting (USA)  ) B McIlroy 1989















