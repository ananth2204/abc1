'Answerphone' for terminals - part 2

Editor's note: last month we published the code to respond to
VMCF messages automatically.

Program READINT responds automatically to notes sent by
other users to your terminal during your absence.  Your
terminal must be in connected or disconnected state.  In order
to start the note responder you start the BUSYNOTE EXEC.
You will be prompted to enter the text you want the other user
to receive during your absence.  The message can explain what
you are doing or that you are busy.  The length of the message
cannot exceed 100 characters.  After the text of the message is
entered, you press the enter key and you see the VM READ
message.  Press the enter key once again.  The message
RUNNING appears.  Now you can disconnect your virtual
machine (#CP DISC).  The program will send your stored
message to each user who sends you a note.  The messages
sent by other users will be saved in the console log of your
virtual machine.  After coming back to your terminal and
connecting to your virtual machine you can stop the responder
with the HX command and close the console.  You obtain a
report about what has been received during your absence.


BUSYNOTE EXEC

/*    */
'CP SPOOL CONSOLE *'
'CP SPOOL CONSOLE START'
'READINT'


READINT ASSEMBLE PROGRAM

READINT  CSECT
         SPACE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
CC1      EQU   1
CC2      EQU   2
CC3      EQU   3
         STM   14,12,12(13)        STORE REGISTERS IN SAVE
         BALR  12,0                ENTRY ADDRESS TO R12
         USING *,12                ESTABLISH ADDRESS
         ST    13,SAVEMAIN+4       STORE ADDR OF OLD IN NEW SAVE
         LA    15,SAVEMAIN         LOAD ADDRESS OF NEW SAVE
         ST    15,8(13)            STORE ADDR OF NEW IN OLD
         LR    13,15               LOAD ADDR OF NEW IN R13
         WRTERM 'ENTER TEXT(100 CHARACTERS)',100    MESSAGE
         RDTERM DATA,100          READ YOUR MESSAGE
         LA    10,MESSAGE          LOAD ADDR OF MESSAGE BUFFER
         MVC   13(80,10),DATA     MOVE YOUR MESSAGE IN BUFFER
****   SET READER INTERRUPT ROUTINE :HANDLER
         HNDINT SET,(READ,HANDLER,00C,ASAP),ERROR=INTERR
WAIT     WAITD READ                WAIT FOR INTERRUPT
         LA    5,BUFFER0           LOAD ADDRESS OF USER BUFFER
         LA    6,X'00000000'       SET R6 AS 0(SELECTS NEXT FILE)
         LA    7,X'00000FFE'       LOAD DIAGNOSE SUBCODE
**     DIAGNOSE READS SFBLOK FOR THE NEXT READER FILE
         DC    X'83560014'         READ SFBLOK FOR THE NEXT FILE
         SR    10,10               CLEAR R10
         LA    10,MESSAGE          LOAD ADDRESS OF MESSAGE BUFFER
         MVC   4(8,10),BUFFER0+16  MOVE SENDERID INTO BUFFER
         MVC   0(3,10),MSG         MOVE COMMAND NAME INTO BUFFER
         WRTERM MESSAGE,100        DISPLAY RESPONSE AT CONSOLE
         XC    CHECK,CHECK         CLEAR CHECK
         MVC   CHECK(4),BUFFER0+52 MOVE READER FILETYPE INTO CHECK
         SR    9,9                 CLEAR R9
         LA    9,CHECK             LOAD ADDRESS OF CHECK IN R9
         CLC   0(4,9),NOTE         CHECK IF FILETYPE=NOTE
         BC    8,SEND              IF YES THEN BRANCH TO RESPONSE
         B     WAIT                OTHERWISE WAIT FOR NEXT INTERRUPT
SEND     EQU   *
         SR    0,0                 CLEAR R0
         SR    1,1                 CLEAR R1
         LA    0,EXPLIST1          LOAD ADDRESS OF EXTENDED PLIST
         LA    1,PLIST             LOAD ADDRESS OF SVC PLIST
         ICM   1,B'1000',=X'02'    SET HIGHER BYTE OF R1 TO X'02'
         SVC   202                 CALL SVC
         DC    AL4(1)              DEFINE CONSTSNT
         B     WAIT                BRANCH TO WAITING ROUTINE
         HNDINT CLR,(READ)         CLEAR READER INTERRUPT
         L     R13,SAVEMAIN+4      LOAD OLD SAVE
         LM    R14,R12,12(R13)     RESTORE REGISTERS
         SR    R15,15              CLEAR R15
         BR    R14                 BRANCH TO RETURN ADDRESS
         DS    0H                  SET BOUNDARY
HANDLER  EQU   *                  BEGIN OF INTERRUPT HANDLER
         LR    5,15                LOAD ENTRY IN R5
         USING *,5                 ESTABLISH R5 AS BASIS
         SR    15,15               CLEAR R15
         BR    14                  BRANCH TO RETURN ADDRESS
INTERR   EQU   *                   ERROR ROUTINE
         WRTERM ERROR,20           MESSAGE
EXIT     EQU   *                   EXIT
         HNDINT CLR,(READ)         CLEAR INTERRUPT
         L     R13,SAVEMAIN+4      LOAD OLD SAVE IN R13
         LM    R14,R12,12(R13)     RESTORE REGISTERS
         SR    R15,15              CLEAR R15
         BR    R14                 BRANCH TO RETURN ADDRESS
SAVEMAIN DS    18F                 OWN NEW SAVEAREA
CHECK    DC    CL4' '              CHECK CONSTANT
PLIST    DS    0F                  SET BOUNDARY TO FULLWORD FOR PLIST
         DC    CL8'CMS     '       SET ENVIRONMENT
EXPLIST1 DS    0F                  SET BOUNDARY FOR EXTENDED PLIST
         DC    A(PLIST)            ADDRESS OF PLIST
         DC    A(BEGARG1)          ADDR OF BEGIN OF ARG OF THE COMMAND
         DC    A(ENDARG1)          ADDR OF END OF ARG
         DC    A(0)                DEFINE CONSTANT
BEGARG1  DS    0F                  SET BOUNDARY FOR BEGIN OF ARG
MESSAGE  DC    C'MSG                                          +++++++++*
               ++++++++++++++++++++++++++++++++++++++++++++++++++++++'
ENDARG1  EQU   *                   END OF ARG
NOTE     DC    CL4'NOTE'           FILETYPE
MSG      DC    CL3'MSG'            MESSAGE COMMAND NAME
CODE     DS    F                   CONSTANT
READER   DC    XL4'0000000C'       VIRTUAL ADDRESS OF READER
ERROR    DC    CL20'INTERRUPT ERROR'   ERROR MESSAGE
READECB  DS    F                   ECB
DATA     DC    CL100'DO NOT WORRY BE HAPPY'    BUFFER FOR YOUR MESSAGE
CODE1    DC    XL2'0FFE'          DIAGNOSE SUBCODE
BUFFER0  DC    CL256'   '         USER BUFFER FOR SFBLOK
         DC    CL256'   '
         END   READINT

Andrzej Popielewicz
System Analyst
Danet GmbH (Germany)                              c Danet GmbH 1991

