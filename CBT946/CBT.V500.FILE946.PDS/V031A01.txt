Display FAVORed users

One minor problem with the CP QUERY command is that there is
no option to display FAVORed users.  We run four VSE guests
and change FAVORed percentages depending on the time-of-day
and the particular workload expected each day.  Unfortunately,
there was no way to check current FAVORed percentages; one
could only reset them in order to ensure they were set properly.

This EXEC rectifies this problem by calling a program that scans
the VMBLOKs, looking for users that are FAVORed (with or
without a FAVORed percentage).  When such a user is found, the
VM name is listed along with a message that shows whether the
user is FAVORed, and by what (if any) percentage.

Initially, the EXEC queries the screen colour for CP and VMOUT
so that the colours can be changed and the program output will be
displayed in the CP colour.  After the FAVOR program completes
execution, the colours are set back.


QFAVOR EXEC

/* Display FAVORED execution virtual machines  */
trace n
'MAKEBUF'
'EXECIO 1 CP ( FIFO STRING QUERY SCREEN'
pull . cpout . . vmout .
'EXECIO 0 CP ( STRING SCREEN VMOUT' cpout
'EXECIO * CP ( FIFO STRING Q R ALL'
'FAVOR'     /* call program to list favored users */
EXIT:
'DROPBUF'
'EXECIO 0 CP ( STRING SCREEN VMOUT' vmout
exit


FAVOR ASSEMBLE

* FAVOR - DISPLAY USERS WITH FAVORED EXECUTION.
*   THIS PROGRAM USES THE DIAG4 MACRO THAT APPEARED IN AN EARLIER ISSUE
*   IT WAS WRITTEN FOR AN HPO 4.2 SYSTEM.
*   YOU MUST HAVE DIAGNOSE 4 PRIVELEGES TO USE THIS PROGRAM.
         REGEQU
FAVOR    CSECT
         PRINT GEN
         BALR  R2,0                    LOAD BASE #1
         USING *,R2                    TELL ASSEMBLER
         ST    R14,SAV14               SAVE REGISTER 14
         LA    R13,SAVEAREA            LOAD SAVEAREA FOR DIAG4 MACRO
*    CLEAR SCREEN
         LA    R4,*+20                 LOAD ADDRESS OF CCW
         LA    R5,9                    CONSOLE ADDRESS
         DC    X'83450058'             FULL SCREEN DIAGNOSE
         B     *+16                    BRANCH AROUND CCW
         DS    0D
         DC    X'19',AL3(0),X'20FF0001'
*              GET POINTER TO SYSTEM VMBLOK
GETSYS   DS    0H
         DIAG4 CP=X'37C',OUT=ASYSVM,LEN=4
         DIAG4 CP=ASYSVM,OUT=FPTR,LEN=4,OFFSET=X'8'   GET FWRD POINTER
         MVC   FIRSTPTR,FPTR           SAVE IT
         L     R3,FPTR                 LOAD TO REG 3
*              CHASE THE VMBLOKS
CHASER   DS    0H
         DIAG4 CP=(3),OUT=FPTR,LEN=4,OFFSET=X'8'   GET FWRD POINTER
         DIAG4 CP=(3),OUT=VMUSER,LEN=8,OFFSET=X'110'    GET VM NAME
         DIAG4 CP=(3),OUT=VMQLEVEL,LEN=1,OFFSET=X'60'   GET QLEVEL
         TM    VMQLEVEL,B'00001100'    FAVORED ?
         BZ    NOTFAVOR                ...NO
         TM    FOUNDBYT,X'80'          ALREADY FOUND ONE ?
         BO    BINFOUND                YES - DON'T DISPLAY HEADING LINE
         OI    FOUNDBYT,X'80'          NO - SET BIT AND SHOW HEADING
         LINEDIT TEXT='FAVOR SET FOR:',                                X
               RENT=NO,DOT=NO,COMP=NO
BINFOUND DS    0H
         DIAG4 CP=(3),OUT=VMFVRF,LEN=1,OFFSET=X'1D4'  GET %
         CLI   VMFVRF,X'00'            ANY PERCENTAGE ?
         BNE   SHOWPCT                 ...YES
         MVI   FAVORED,C'Y'            SET DISPLAY
         LINEDIT TEXT='   USER = ........   FAVORED? ..',              X
               SUB=(CHARA,VMUSER,CHARA,FAVORED),                       X
               RENT=NO,DOT=NO,COMP=NO
         B     NOTFAVOR
SHOWPCT  DS    0H                      CALCULATE FAVORED PERCENTAGE
         SR    R4,R4                   CLEAR REG 4
         SR    R5,R5                   CLEAR REG 5
         IC    R5,VMFVRF               LOAD VMBLOK SETTING
         MH    R5,=H'100'              MULTIPLY BY 100
         D     R4,=F'255'              AND DIVIDE BY 255
         C     R4,=F'125'              ROUNDING NEEDED ?
         BL    NOROUND                 NO
         LA    R5,1(,R5)               YES - INCREMENT BY ONE
NOROUND  DS    0H
         CVD   R5,FAVORPCT             STORE IT AS PACKED
         MVC   EDFAVOR,EDPTRN          AND EDIT IT
         ED    EDFAVOR,FAVORPCT+6
         TM    VMQLEVEL,B'00001000'    FAVORED ?
         BZ    NOFAV
         MVI   FAVORED,C'Y'
         B     SHOWIT
NOFAV    DS    0H
         MVI   FAVORED,C'N'
SHOWIT   DS    0H
         LINEDIT TEXT='   USER = ........   FAVORED? ..  PERCENTAGE = .X
               ..',SUB=(CHARA,VMUSER,CHARA,FAVORED,CHARA,EDFAVOR),     X
               RENT=NO,DOT=NO,COMP=NO
NOTFAVOR DS    0H
         CLC   FIRSTPTR,FPTR           HAVE WE'VE BEEN HERE ALREADY ?
         BE    FINISH
         L     R3,FPTR                 ...NO - GO CHECK ANOTHER
         B     CHASER
*              END OF JOB
FINISH   DS    0H
         TM    FOUNDBYT,X'80'          ANY FOUND ?
         BO    ZERO15                  YES
         LINEDIT TEXT='NO FAVORED USERS FOUND.',RENT=NO,COMP=NO,DOT=NO
         LA    R15,28
         B     ERROR
ZERO15   DS    0H
         SR    R15,R15                 LOAD ZERO RC
ERROR    DS    0H
         L     R14,SAV14               LOAD R14
         BR    R14                     GOBACK TO CMS
*              W O R K I N G   S T O R A G E
SAVEAREA DS    9D
FAVORPCT DS    D
SAV14    DS    F
ASYSVM   DC    F'0'
FIRSTPTR DC    F'0'
FPTR     DC    F'0'
VMUSER   DC    CL8' '
VMQLEVEL DC    X'00'
FOUNDBYT DC    X'00'
VMFVRF   DC    X'00'
FAVORED  DC    C'  '
EDFAVOR  DC    X'000000'
EDPTRN   DC    X'212020'
         LTORG
         END   FAVOR


Michael P Kilroy
Systems Programming Supervisor
Progress (USA) ) Xephon 1989

