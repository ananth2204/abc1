 DIAL and VTAM in a GCS environment
At our site end-user terminals are dedicated to VTAM.  Most of them are BSC
terminals and all of them are local.
Users have the possibility to DIAL into several systems.  The DIAL command is
coded in VTAM's USSTAB; at run-time the application-group-id (entered by the
user) invokes a VTAM LOGON which has in its DATA part the CP DIAL command.
Many users select the same application-group (= virtual machine) so the DIAL
command cannot be coded with a virtual device address.
In all our systems, however, we require a fixed relation between a physical
terminal and its virtual address because of the terminal/printer relationships:
end users (non-VM people) want printing done at the nearest printer.
A simple modification to the CP DIAL command gives us a solution, as follows.
When a DIAL command with virtual device address zero is coded, at run-time the
actual address overwrites the zero address.  This solution works for both non-
VTAM and VTAM terminals.  To keep things simple, I extract the DIAL address for
VTAM terminals from VTAM's minor node name, which has, following our naming
conventions, this address imbedded.  This has the advantage that the virtual
address in the DIALed machine is a free choice and depends only on VTAM's naming
convention.
SAMPLE ENTRY IN VTAM'S USS TABLE
APT      USSCMD   CMD=APT,REP=LOGON,FORMAT=BAL
         USSPARM  PARM=P1,REP=DATA,DEFAULT='DIAL VSE5 000'
         USSPARM  PARM=P2,REP=LOGMODE
         USSPARM  PARM=APPLID,DEFAULT=VM10
SAMPLE ENTRY IN VTAM'S MAJOR NODE TABLE
V20L4E0  LU    LOCADDR=2,         ** 4E0 IS THE DEFAULT DIAL ADDR   ** X
               SSCPFM=USSSCS,     ** CHARACTER-CODED MESSAGES       ** X
               USSTAB=VUSSTAB,    ** USS DEFINITION TABLE NAME      ** X
               ISTATUS=ACTIVE,    ** DEFAULT                        ** X
               DLOGMOD=SNA3278
If an end user hits APT on his screen for this LU the DIAL command that is
executed is:  DIAL VSE5 4E0
LOCAL MODIFICATION SOURCE FOR DMKDIA
./ R 11500000          $ 11500290 290
         LTR   R1,R1          DUMMY ADDRESS SPECIFIED ?
         BNZ   SCANVW1        SUBSTITUTE 000 BY REAL ADDRESS
         LR    R6,R8          SAVE R8
         L     R8,SAVEWRK8    DIALING RDEVBLOK (FROM VMTERM)
         USING RDEVBLOK,R8
         CLC   RDEVADD,SNAFLAG SNA CCS RDEVBLOK RDEVADD ?
         BE    SCANVW2
         CALL  DMKSCNRD       GET REAL ADDRESS IN 'CUU' FORM
         LR    R8,R6          RESTORE R8
         B     SCANVW1
SCANVW2  EQU   *
         L     R8,RDEVSNRB    ADDRESS OF THE SNA RESOURCE BLOCK
         DROP  R8
         USING SNARBLOK,R8
         LA    R0,3           VADDR LENGTH
         LA    R1,SNARLUN+4   VADDR FOUND IN LOGICAL UNIT NAME
         DROP  R8                   FOR THE SNA CONSOLE
         LR    R8,R6          RESTORE R8
         CALL  DMKCVTHB       CONVERT ADDRESS TO BINARY
         BNZ   INVVADD        CONVERT FAILED - INVALID
         MAXDV R15            GET MAXIMUM VALID ADDRESS IN GR15
         CLR   R1,R15         IS THE DEVICE ADDRESS POSSIBLE ?
         BH    INVVADD        NO -- KICK IT OUT
SCANVW1  EQU   *
         LR    R6,R1          REMEMBER ADDRESS FOR DMKSCNVU
Victor Willems
Lead Systems Programmer
Academic Hospital - University of Brussels (Belgium)   ) AZ-VUB 1988




