 Printing REXX programs using two fonts

The following REXX EXEC prints REXX programs with the comments in a different
font from the code.  It is particularly useful in drawing attention to code
which has been commented on.  The TAG and SPOOL commands will have to be updated
to meet your installation's requirements.  The printer requested in the code is
a 3820.  The first font listed in SPOOL command is used for printing the code
and the second font is used for printing the comments.  These can be changed to
any two compatible font types.

/*  THE PURPOSE OF THIS EXEC IS TO REFORMAT A REXX PROGRAM SO THAT IT
    MAY BE PRINTED WITH THE COMMENTS IN A DIFFERENT FONT TO THE
    CODE. TWO LINES OF OUTPUT ARE CREATED FOR EACH LINE OF INPUT. THE
    FIRST LINE CONTAINS THE REXX CODE AND USES FONT 0. THE SECOND
    LINE CONTAINS THE COMMENTS AND USES FONT 1 AND A CARRIAGE CONTROL
    VALUE OF '+' SO THAT IT IS PRINTED ON THE SAME LINE.
*/
START_COMM = 0
END_COMM   = 0
RCNT       = 1
EOF        = 0
MAKEBUF
SIGNAL ON ERROR
SAY "ENTER REXX PROGRAM NAME: FILENAME, FILETYPE AND FILEMODE:"
PULL FN FT FM
STATE FN FT FM
CALL READ
DO WHILE Â¬EOF
  REC1 = ' 0            '             /* SET CC AND FONT          */
  REC2 = '+1            '
   DO I = 1 TO LENGTH(IREC)           /* LOOP THRU THE LINE       */
      IF START_COMM = 1               /* COMMENT FOUND PREVIOUSLY?*/
      THEN DO                         /* END OF COMMENT FOUND B4? */
         IF END_COMM = 1              /* (IT TAKES 2 TURNS TO END */
         THEN DO                      /* A COMMENT BECAUSE ITS 2  */
            REC1 = REC1||' '          /* CHARS LONG)              */
            REC2 = REC2||'/'          /* FINISH UP COMMENT        */
            END_COMM = 0              /* RESET END COMMENT FLAG   */
            START_COMM = 0            /* RESET START COMMENT FLAG */
         END
         ELSE DO                      /* NOT THE END OF A COMMENT */
            IF SUBSTR(IREC,I,2) = '*/' /* START OF COMMENT?       */
            THEN DO
               END_COMM = 1           /* SET FLAG                 */
               REC1 = REC1||' '       /* BLANK OUT THE CODE LINE  */
               REC2 = REC2||'*'       /* MOVE '*' TO COMMENT LINE */
            END
            ELSE DO                   /* WITHIN THE COMMENT TEXT  */
               REC1 = REC1||' '       /* BLANK OUT CODE LINE      */
               REC2 = REC2||SUBSTR(IREC,I,1) /* MOVE COMMENT TEXT */
            END
         END
      END
      ELSE DO                   /* COMMENT WAS NOT PREVIOUSLY FOUND*/
         IF SUBSTR(IREC,I,2) = '/*'  /* COMMENT OCCURRING CURRENTLY?*/
         THEN DO
            START_COMM = 1
            REC1 = REC1||' '
            REC2 = REC2||SUBSTR(IREC,I,1)
         END
         ELSE DO                     /* CODE OCCURRING CURRENTLY    */
            REC1 = REC1||SUBSTR(IREC,I,1)
            REC2 = REC2||' '
         END
      END
   END
 'EXECIO 1 DISKW  PFORM TEXT A  (VAR REC1'
'EXECIO 1 DISKW  PFORM TEXT A  (VAR REC2'
CALL READ
END
DROPBUF
FINIS FN FT FM
FINIS 'PFORM  TEXT A'
'CP TAG DEV PRT BOSTON R48 * FCB=VM8P OPTCD=J MODIFY=2 SYSOUT=M'
'CP SPOOL PRT RSCS COPY 1 CHARS GT12 SR12'
'PRINT PFORM TEXT A (CC'
SIGNAL OFF ERROR
ERASE 'PFORM TEXT A'
SAY 'YOUR PROGRAM IS PRINTING'
EXIT
READ:
     SIGNAL OFF ERROR
    'EXECIO 1 DISKR' FN FT FM  RCNT '(VAR IREC'
     SELECT
        WHEN  RC = 2
             THEN EOF  = 1
        WHEN  RC = 0
             THEN NOP
        OTHERWISE SIGNAL ERROR
     END
     SIGNAL ON ERROR
     RCNT = RCNT + 1
RETURN
ERROR:
     SAY 'AN UNEXPECTED ERROR HAS OCCURRED:'
     SAY 'RETURN CODE: ' RC
     SAY 'STATEMENT NUMBER: ' SIGL
     SAY 'LINE OF CODE EXECUTING:'
     SAY SOURCELINE(SGL)
     EXIT
RETURN

Tara Feraco
Systems Programmer
John Hancock Insurance Company (USA)

) Tara Feraco 1987

























