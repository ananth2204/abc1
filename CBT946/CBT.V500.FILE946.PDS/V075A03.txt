Justifying text

The JUST XEDIT macro will allow you to right and left justify
part of a text file. The macro requires certain arguments. These
are the width of the formatted paragraph, the start line, the end
line, and the actual indentation within the file. To work on a
part of the text use the ZONE command prior to executing the
macro. The command format is:

      JUST       w        sl       el        i

where w is the width of the justified output paragraph; sl is the
start line; el is the end line; and i is the indentation required.


JUST XEDIT

/*-------------------------------------------------------------------*/
"set emsg off"
"top"
Arg len start to_end indent
If len = "?" then signal helppanel
If len = "" then do
   call emsg_on
   emsg "Width of output paragraph has to be entered...Type ? for help"
   call emsg_off
   Exit
End
If start = "" then do
   call emsg_on
   emsg "Start line needs to be entered...Type just ? for help"
   call emsg_off
   Exit
End
If to_end = "" then do
   call emsg_on
   emsg "End line needs to be entered...Type just ? for help"
   call emsg_off
   Exit
End
If indent = "" then do
   call emsg_on
   emsg "Indentation needs to be entered...Type just ? for help"
   call emsg_off
   Exit
End
"extract/zone"
if len + indent > zone.2 then do
   emsg "Width and indentation greater than file width. ",
                                                 "Data will be lost..."
   Exit
End
":" start
to_end = (to_end-start) + 1
whole_line = "   "
do to_end
   "extract/curline"
   curline.3 = substr(curline.3,zone.1,zone.2-zone.1)
   curline.3 = strip(curline.3,,' ')
   word_count = words(curline.3)
   do i = 1 to word_count
      whole_line = whole_line''" "''subword(curline.3,i,1)
   End
   "down 1"
End
"zone 1 *"
"up 1"
Whole_count = words(whole_line)
Counter = "1"
Newcounter = "1"
Format_text:
Form_line = ""
do forever
   form_line = form_line''" "''subword(whole_line,counter,1)
   If counter > whole_count then do
      "input" strip(form_line)
      "shift right" indent "1"
      ":" start
      "del" to_end
      "zone" zone.1 zone.2
      call emsg_on
      Exit
   End
   if length(form_line) > len then do
      form_line = subword(whole_line,newcounter,(counter-newcounter))
      newcounter = counter
      "input" justify(form_line,len)
      "shift right" indent "1"
      form_line = ""
      counter = counter - 1
   End
   Counter = counter + 1
End
helppanel:
Say "The JUST XEDIT macro will permit  you to right and left justify a"
Say "part of the text in a file."
Say ""
Say "The macro is executed with some arguments that you must provide, "
Say "ie the width of the formatted  paragraph, the start line, the end"
Say "line,  and  the  actual  indentation within  the  file  that  you"
Say "require.  To work  on a  part of  the text,  please use  the ZONE"
Say "command prior to executing the macro,"
Say ""
Say " Ex : just       40        1       10        25   "
Say "                 !         !       !         !    "
Say "            width of     start    end       inden-"
Say "            justified    line     line      tion  "
Say "            output pa-"
Say "            ragraph"
Say ""
exit
emsg_on:
"set emsg on"
Return
emsg_off:
"set emsg off"
Return

Pierre Laurin
Customer Assistance Manager
GSI (France)

