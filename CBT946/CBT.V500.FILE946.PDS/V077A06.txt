FILELIST for OS disks

At our organization we use VM and MVS with shared disks.
Often we need to browse an MVS dataset from VM. Although
CMS can read OS datasets there is no facility like FILELIST
for OS disks.

The REXX program below can be used to obtain a list of
datasets in FILELIST-like format. From the list, PF11 can be
used to browse a dataset using XEDIT. In the case of
partitioned datasets, the member list is shown and single
members can be browsed via XEDIT.

The command format is:

      OSFL <dsname> mode

where dsname is the dataset name. If dsname is not used, or
coded as an asterisk, all datasets are listed. If the name ends
with an asterisk, all datasets whose names begin with the
characters preceding the asterisk are listed. Mode is the access
mode of the MVS disk.

The OSFL program uses CMS Pipeline in order to trap the
output of the LISTDS command so CMS Pipeline is a
prerequisite.


OSFL EXEC

/**** FILELIST on OS disk: OSFL <dsn> mode ****/
trace off
parse upper arg dsn mode .
if mode="" then do; mode=dsn; dsn=""; end
if mode="" then do
        say "Error, Format is:"
        say "OSFL <dsn> mode"
        exit 12
        end
address command
parse value diag(8c) with 3 numc 5 numl 7 .  /*get terminal data      */
numc=x2d(c2x(numc))                          /* # of columns on screen*/
numl=x2d(c2x(numl))                          /* # of lines on screen  */
if numl<24 | numc<80 then do     /*at least 80 columns and 24 lines?  */
        say "Command is only valid on display terminal"
        exit 8
        end
if dsn="*" then dsn=""
if dsn="" then ldsn=""                 /*no search for dsname, default*/
       else do
       l=length(dsn)
       ldsn="LOCATE 44."                /*prepare locate for pipeline */
       if substr(dsn,1,1)="*" then ldsn=ldsn||1 "/"substr(dsn,1,1-1)"/|"
                         else ldsn=ldsn||"44 /"left(dsn,44)"/|"
        end
rlist.1="   * * * Top of Dataset List * * *"
rlist.0=1
"PIPE COMMAND LISTDS" mode "(FORMAT|" , /* list dataset               */
              "drop 1|" ,               /* drop title line            */
              ldsn ,                    /* select dsnames if requested*/
              "spec 44.44 1 " ,         /* dsname                     */
                   "1.5 46 " ,          /* recfm                      */
                   "7.5 52.5 r",        /* lrecl                      */
                   "13.5 58.5 r",       /* blksize                    */
                   "19.24 64|" ,        /* dsorg date mode vol-id     */
              "sort 1-44|" ,            /* sort by dataset name       */
              "stem rlist. append"      /* put output in stem         */
if rc¬=0 then do                        /* if error                   */
           do i=2 to rlist.0-1
              say rlist.i               /* write error messages       */
           end
           exit rc                      /* and exit                   */
           end
if rlist.0=0 then do                    /* dataset found?             */
           say "Dataset not found on disk" mode
           exit 28
           end
k=rlist.0
k=k+1
rlist.k="   * * * End of Dataset List * * *"
rlist.0=k
volid=substr(rlist.1,79,6)              /* extract vol-id             */
msger=" "
title1="Filelist dataset on vol-id="volid"  access mode="mode
title2="   Dataset name                              Dsorg   "
title2=title2||"Lrecl  Blks Dsorg  Date"
pfline1="PF: 1=         2=          3=End      4=         5=         6="
pfline2="PF: 7=Backward 8=Forward   9=        10=        11=Browse  12="
"VSC DEF OSFL" rlist.0 numc 2 5 "(PROTECT"  /*define virtual screen   */
"WIN DEF OSFL" numl numc 1 1                /*define window           */
"WIN SHOW OSFL ON OSFL"
"VSC WRI OSFL 1 1 80 (RES PRO H FIELD" title1
"VSC WRI OSFL 2 1 80 (RES PRO H FIELD" title2
"VSC WRI OSFL -5 1 80 (RES PRO H FIELD" pfline1
"VSC WRI OSFL -4 1 80 (RES PRO H FIELD" pfline2
"VSC WRI OSFL -2 1 6  (RES PRO H FIELD ====>"
"VSC WRI OSFL -2 7 70 (RES NOPRO FIELD" copies(" ",68)
"VSC WRI OSFL 1 1" rlist.0*numc "(NOPRO FIELD  "
do i=1 to rlist.0
 "VSC WRI OSFL" i "1 80 (NOPRO DATA   " substr(rlist.i,1,77)
end
vsc="OSFL"                              /* name of virtual screen     */
call dispv                              /* display screen             */
finish:
"VSC DEL OSFL"
"WIN DEL OSFL"
"VSC DEL OSFLM"
"WIN DEL OSFLM"
exit
/************* display virtual screen and read **********/
dispv:
curpos="2 1"                            /* default cursor position    */
do forever
     if msger="MSGER" then msger=""
     "VSC WRI" vsc "-3 1 80 (RES PRO H FIELD" msger
     if msger¬="" then "VSC ALARM" vsc
     "VSC CUR" vsc curpos
     "VSC WAITREAD" vsc                 /* read from terminal         */
     msger=" "
     curpos="-2 8 (R"
     key=subword(waitread.1,1,1)
     select
        when key="ENTER" & waitread.0>2 then call enterk
        when key="PFKEY" then do
           pf=subword(waitread.1,2,1)
           select
              when pf=3 | pf=15 then return        /* pfk 3 end       */
              when pf=11 | pf=23 then call edita   /* pfk 11 browse   */
              when pf=8 | pf=20 then "WIN F ="     /* screen forward  */
              when pf=7 | pf=19 then "WIN BA ="    /* screen backward */
              otherwise iterate
              end
           end
        otherwise iterate
        end
end
/***************** enter key **************************************/
enterk:
 cmd=""
 do i=3 to waitread.0
    if subword(waitread.i,2,1)="-2" then do
                    parse upper value subword(waitread.i,4) with cmd
                    leave
                    end
 end
 if cm="" then return
 select
    when abbrev("TOP",cmd,3) then "WIN TOP ="
    when abbrev("BOTTOM",cmd,1) then "WIN B ="
    when substr(cmd,1,1)="/" & ,
         substr(cmd,2)¬=" " then call locate
    otherwise do
       if subword(cmd,1,1)='CMS' then cmd=subword(cmd,2)
       address cms cmd
       if rc<0 then msger="Unknown command"
       end
    end
 return
/************ locate *************/
locate:
 str=substr(cmd,2)
 if vsc="OSFL" then do
                    do i=2 to rlist.0-2
                       if index(rlist.i,str)¬=0 then leave
                       end
                    if i=rlist.0-1 then i=0
                    end
               else do
                    do i=2 to lm.0-2
                       if index(lm.i,str)¬=0 then leave
                       end
                    if i=lm.0-1 then i=0
                    end
 if i¬=0 then "WIN SHOW" vsc "ON" vsc i "1"
         else msger="Target not found"
 return
/***************** browse dataset or member list *****************/
edita:
 if subword(waitread.2,4,1)¬="DATA" then do /* cursor on data field? */
        msger="Cursor not in valid data position"
        return
        end
 j=subword(waitread.2,2,1)                  /* line                   */
 curpos=j "1"                               /* cursor position        */
 if vsc="OSFLM" then do                     /* from member list?      */
         member=lm.j                        /* save member name       */
         call editl                         /* browse member          */
         return
         end
 parse var rlist.j dsname 46 recfm 52 lrecl 58 blksize 64 dsorg 68 .
 member=""
 if dsorg="PO" then do                  /* if partition dataset       */
        call editp                      /* show member list           */
        return
        end
 if dsorg¬="PS" then do
        msger="Dsorg "'dsorg'" non supported"
        return
        end
 call editl
 return
/****************** show member list  ******************************/
editp:
 lm.1="   * * * Top of Member List * * *"
 lm.0=1
 "PIPE COMMAND LISTDS" dsname mode "(PDS|" ,
      "drop 3|" ,
      "fblock 9|" ,
      'STEM LM. APPEND"
 if rc¬=0 then do
          msger="Error in list pds members"
          return
          end
 if lm.0=0 then do
          msger="No member in directory"
          return
          end
 k=lm.0
 k=k+1
 lm.k="   * * * End of Member List * * *"
 lm.0=k
title3=' Member list; dataset="dsname
"WIN HIDE" vsc
"VSC DEF OSFLM" lm.0 numc 2 5 "(PROTECT"   /*define virtual screen    */
"WIN DEF OSFLM" numl numc 1 1              /*define window            */
"WIN SHOW OSFLM ON OSFLM"
"VSC WRI OSFLM 1 1 80 (RES PRO H FIELD" title3
"VSC WRI OSFLM 2 1 80 (RES PRO H FIELD  "
"VSC WRI OSFLM -5 1 80 (RES PRO H FIELD" pfline1
"VSC WRI OSFLM -4 1 80 (RES PRO H FIELD" pfline2
"VSC WRI OSFLM -2 1 6  (RES PRO H FIELD ====>"
"VSC WRI OSFLM -2 7 70 (RES NOPRO FIELD" copies(" ",68)
"VSC WRI OSFLM 1 1" lm.0*numc "(NOPRO FIELD  "
do i=1 t lm.0
 "VSC WRI OSFLM" i "1" numc "(PRO DATA " lm.i
end
svsc=vsc                                /* save virtual screen name   */
vsc="OSFLM"
scur=curpos
call dispv                              /* display screen             */
"VSC DEL OSFLM"
"WIN DEL OSFLM"
vsc=svsc
curpos=scur
"WIN SHOW" vsc
return
/***************** call xedit to browse dataset *************/
editl:
 fn="OS"
 ft="DATASET"
 if member¬="" then do
               fn=member
               ft="MEMBER"
               end
 if lrecl="" then xw= blksize
             else xw= lrecl
 if member¬="" then fip="(MEMBER" member
               else fip=""
 "FILEDEF $OSFLDD$ CLEAR"
 "FILEDEF $OSFLDD$" mode "DSN" dsname fip
 "XEDIT" fn ft "S (W" xw "PROFILE OSFL)" dsname
 "FILEDEF $OSFLDD$ CLEAR"
 return


OSFL XEDIT

/********  XEDIT profile for osfl                          ******/
address CMS 'PIPE VAR DSNAME 1|VAR DSNAME'
address CMS 'PIPE VAR RECFM  1|VAR RECFM '
address CMS 'PIPE VAR MEMBER 1|VAR MEMBER'
'EXTRACT /FN /FT /FM /W'
if member¬='' then mm='MEMBER='member
              else mm=''
'SET RESERVED 3 HIGH Browse dsname --->' dsname mm
rf=substr(strip(recfm),1,1)
if rf='F' | rf='V' then 'RECFM' rf
                   else 'RECFM V'
address cms 'PIPE (stagesep +) command PIPE QSAM $OSFLDD$|' ,
             'xedit' fname.1 ftype.1 fmode.1 '+var msger 1'
if rc¬=0 then 'QUIT'
'TOP'
'SET ALT 0'
'SET PF3 QQUIT'
'SET PF15 QQUIT'
exit

Pasquale Lazzareschi
CNUCE (Italy)

