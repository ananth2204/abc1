Rearranging columns in XEDIT

COLX is an XEDIT macro I have found useful in rearranging
the columns in data files. COLX (Column XEDIT) will allow
you to copy as well as move columns of data in an edited file.
The command format is:

      COLX  Copy|Move col1 col2 col3

Where: Copy|Move is the function to be performed; col1 is the
starting column of the data to be moved; col2 is the ending
column of the data to be moved; col3 is the destination column.
Note: once the COLX command has been issued, the REPEAT
command may be used to extend the range of the command
throughout the file.


COLX XEDIT

/* COLX XEDIT                                                    */
/* Function: COLX is used to perform column editing (MOVE, COPY) */
/*           on an edited file.                                  */
parse upper arg func col1 col2 col3 junk  /* Retrieve parameters */
/* Test for valid function */
if ABBREV('COPY',func,1) = 0 & ABBREV('MOVE',func,1) = 0
then
  do
   'EMSG Function invalid. Valid function Copy|Move'
   exit(2)
  end
if col3 = ''                     /* Sufficient parameters ?           */
then
  do
   'EMSG Insufficient parameters for COLX'
   exit(3)
  end
'EXTRACT /CURLINE/COLUMN/LRECL/' /* Retrieve Current line, Current    */
                                 /* Column pointer and Logical Rec Len*/
if col1 > col2                   /* Column numbers for move valid ?   */
then
  do
   'EMSG Column numbers invalid.  Column 2 < Column 1'
   exit(4)
  end
if col1 > lrecl.1 or col2 > lrecl.1 or col3 > lrecl.1 /*Column numbers*/
then                                        /* within range ?         */
  do
   'EMSG Column number greater than logical record length'
   exit(5)
  end
sav_col = column.1                /* Save current column pointer      */
mve_len = col2 - col1 + 1         /* Calculate length of data to move */
mve_data = substr(curline.3, col1, mve_len)  /* Extract data to move  */
if ABBREV('MOVE',func,1) = 1       /* Move requested ?                */
then
  do
   'CLOCATE :'col1                /* Go to starting column            */
   'CREPLACE 'left('',mve_len)    /* Clear Area                       */
  end
'CLOCATE :'col3                   /* Go to destination column         */
'CREPLACE 'mve_data               /* Move data                        */
'CLOCATE :'sav_col                /* Restore column pointer           */
exit                              /* return                           */

Dudley Johnson
Senior Systems Programmers
LOJ Ltd
