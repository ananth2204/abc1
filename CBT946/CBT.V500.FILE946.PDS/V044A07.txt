Scrolling XEDIT macro

I have found on numerous occasions, when editing any source
file, a great need to get the line being edited to become the
current line or the bottom line, by the usage of normal scroll
PF keys, without losing the cursor position on the line.

In order to facilitate this, I have written a small XEDIT macro
in EXEC2 language, which, when assigned to the scroll PF
keys, will do the trick.

Set up the scroll keys as follows in your PROFILE XEDIT
macro.

SET  PF7 CURLN  U    /* FOR SCROLL UP    */
SET  PF8 CURLN       /* FOR SCROLL DOWN  */

CURLN XEDIT

   &TRACE OFF
   SET IMSG OFF
   SET EMSG OFF
   EXTRACT/CURSOR/LSCREEN/CURLINE
   &TOP = &CURLINE.2 + 1
   &BOT = &LSCREEN.1
   &IF .&1 = .U &THEN &GOTO -UP
   &D = &CURSOR.1 - &TOP
   &IF &CURSOR.1 = &TOP &THEN &GOTO -BACK
   &IF &CURSOR.1 = &BOT &THEN &GOTO -BACK
   &IF &CURSOR.1 < &TOP &THEN &GOTO -BACK
   DOWN &D
   &GOTO -SETCUR
-BACK
   FORWARD
-SETCUR
   &IF &CURSOR.1 < &TOP &THEN &GOTO  -CMD
   CURSOR SCREEN &TOP &CURSOR.2
   &IF &RC = 0 &GOTO -EXIT
-CMD
   CURSOR CMDLINE
   &GOTO -EXIT
-UP
   &D = &BOT - &CURSOR.1
   &IF &CURSOR.1 = &TOP &THEN &GOTO -UPWARD
   &IF &CURSOR.1 = &BOT &THEN &GOTO -UPWARD
   &IF &CURSOR.1 < &TOP &THEN &GOTO -UPWARD
   UP &D
   &GOTO -SETCUR1
-UPWARD
   BACKWARD
-SETCUR1
   &IF &CURSOR.1 < &TOP &THEN &GOTO -CMD1
   CURSOR SCREEN &BOT &CURSOR.2
   &IF &RC = 0 &THEN &GOTO  -EXIT
-CMD1
   CURSOR CMDLINE
-EXIT
   SET IMSG ON
   SET EMSG ON
   &EXIT

C Screenath Chary
Consultant
Federation Insurance (Australia)  ) Xephon 1990


