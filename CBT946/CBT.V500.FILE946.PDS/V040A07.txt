EXEC to access T-disk

Temporary disk space (T-disk) is assigned from a pool of DASD
resources; therefore, you should always:

%   Define the T-disk

%   Format it

%   Find a free virtual address

%   Find a free access mode, in order to keep all mini-disks that
are already accessed.

The TEMP EXEC performs the functions described above.  It
defines a T-disk, formats it, and uses the module GETFMADR to
find a free virtual address and a free access mode.  It is composed
of two panels (DMS/CMS):

%   TMP01 : Menu (see Figure 1)

%   TMP02 : Help (see Figure 2)

The first panel is the menu.  It contains four fields that define the
virtual address, the access mode, the number of cylinders and the
blocksize of the T-disk.

The second panel is the 'HELP' panel.  It has information about
each field of TMP01 and how to change the default values.

The maximum size of a T-disk that can be defined by the TEMP
EXEC is 99 cylinders.  If you want to change this value you can
modify the appropriate line of the EXEC.  Do not forget to change
comment number 3 of the error messages and the 'HELP' panel
too.

       &TRACE OFF
***    This EXEC creates a T-disk. It has 2 (two) panels:    ***
***         - TMP01: Menu                                    ***
***         - TMP02: Help                                    ***
&COMMAND SET CMSTYPE HT
       GETFMADR
       &READ VARS &NADA &FFMODE1 &DDISCO1
       &COMMAND CP LINK MAINT 319 &DDISCO1 RR
       ACC &DDISCO1 &FFMODE1
       GETFMADR
       &READ VARS &NADA &FFMODE &DDISCO
***    The EXEC uses these defaults to create a T-disk       ***
-DEFAULT
    EUDEXEC2
       &PRESUME &SUBCOMMAND DISPLAY
       &DISK  = &DDISCO
       &MODO  = &FFMODE
       &CYL   = 2
       &BLOCK = 4
       &D1    = &DDISCO
       &D2    = &FFMODE
       &D3    = 2
       &D4    = 4
***    Displays the first panel (TMP01): MENU                ***
       &STACK LIF0 D1 D2 D3 D4
       &READ STRING &ALL
-VOLTA
    USE PANEL TMP01
-PAINEL
    DISPLAY
***    Tests the PFs of the first panel (TMP01)              ***
SIGNAL OFF
&IF &RSTATUS = PF1   &GOTO -HELP
&IF &RSTATUS = PF3   &GOTO -EXIT
&IF &RSTATUS = ENTER &GOTO -CONTINUE
    COMMENT        This PF is not defined
    SIGNAL ON
    &GOTO -PAINEL
***    Tests the data of the first panel (TMP01)             ***
-CONTINUE
       &DISK     = &D1
       &MODO     = &D2
       &CYL      = &D3
       &BLOCK    = &D4
       &IF &BLOCK  NE 4 &GOTO -COMENT5
       &IF .&BLOCK EQ . &GOTO -COMENT5
       &IF .&CYL   EQ . &GOTO -COMENT6
       &IF &CYL    EQ 0 &GOTO -COMENT6
       &IF .&MODO  EQ . &GOTO -COMENT7
       &DISK1    = &DATATYPE OF &DISK
       &MODO1    = &DATATYPE OF &MODO
       &CYL1     = &DATATYPE OF &CYL
       &BLOCK1   = &DATATYPE OF &BLOCK
&IF &DISK1  NE NUM  &GOTO -ERRO
&IF &MODO1  NE CHAR &GOTO -ERRO
&IF &MODO   EQ A    &GOTO -COMENT2
&IF &CYL1   NE NUM  &GOTO -ERRO
&IF &CYL1   GT 99   &GOTO -COMENT3
&IF &BLOCK1 NE NUM  &GOTO -ERRO
    &COMMAND CP DEF T3375 &DISK &CYL CYL
    &IF &RETCODE = 91 &GOTO -COMENT4
    &IF &RETCODE = 92 &GOTO -COMENT1
    &IF &RETCODE NE 0 &EXIT &RETCODE
    &BLK   = &CONCAT OF &BLOCK K
    &STACK YES
    &STACK WORK
    &COMMAND FORMAT &DISK &MODO ( BLKSIZE &BLK
    &IF &RETCODE = 24 &GOTO -COMENT5
-RELEASE
&COMMAND SET CMSTYPE RT
    &TYPE
    &TYPE      Format complete
    &TYPE
    &TYPE  MDISK =  &DISK
    &TYPE  FMODE =  &MODO
    &TYPE  LABEL =  WORK
    &TYPE
-EXIT
    &COMMAND REL &FFMODE1 (DET
    &EXIT &RETCODE
***    ERROR Messages                                        ***
-COMENT1
       COMMENT     This address is already being used
       SIGNAL ON
       &GOTO -PAINEL
-COMENT2
       COMMENT     Invalid access mode
       SIGNAL ON
       CURSOR D2
       &GOTO -PAINEL
-COMENT3
       COMMENT     You can allocate only 99 cyls
       SIGNAL ON
       CURSOR D3
       &GOTO -PAINEL
-COMENT4
       COMMENT     Try to allocate less space
       SIGNAL ON       CURSOR D3
       &GOTO -PAINEL
-COMENT5
       COMMENT     Invalid blksize
       CURSOR D4
       SIGNAL ON
       &GOTO -PAINEL
-COMENT6
       COMMENT     Invalid number of cyls
       CURSOR D3
       SIGNAL ON
       &GOTO -PAINEL
-COMENT7
       COMMENT     Invalid access mode
       CURSOR D2
       SIGNAL ON
       &GOTO -PAINEL
-ERRO
       COMMENT     Invalid data entered
       SIGNAL ON
       &GOTO -PAINEL
***    Displays the second panel (TMP02): HELP               ***
-HELP
    USE PANEL TMP02
-PAINEL2
    DISPLAY
***    Tests the PFs of the second panel (TMP02)             ***
&IF &RSTATUS = PF3   &GOTO -VOLTA
&IF &RSTATUS = PF4   &GOTO -EXIT
    COMMENT        This PF is not defined
    SIGNAL ON
    &GOTO -PAINEL2


Cesar Henrique de Lima Nobre (Brazil) ) Xephon 1989


