Improving Q NAMES usability

The purpose of this EXEC is to improve the usability of the
QUERY NAMES command on large VM systems.

At our site we have over 1200 users logged on and it can be
hard to spot the user required, and also a slow business to
scroll a remote screen over the full output from the raw
QUERY NAMES command.

This EXEC combines the QUERY NAMES command with the
flexibility of the XEDIT environment to allow support staff
quickly to find logged-on users by name, VTAM LUname etc.
The EXEC effectively adds another parameter to the QUERY
NAMES command, which is passed to XEDIT for use by the
ALL macro.  Thus UID DSC will display all lines having the
DSC character on them (normally disconnected users).

If preferred, the XEDIT commands could be held in an XEDIT
macro, which is invoked as a PROFILE at XEDIT
initialization.  The current method has the advantage, however,
of maintaining a single EXEC to control the function.

The basic idea of the EXEC can be extended to cover other
VM QUERY commands that may have large outputs.

The EXEC has been used on VM/XA SP 2 and 2.1 systems.


UID EXEC

/*   use XEDIT to find and display a user in a big list of users    */
address command
arg parm1
if parm1 = '?' then call EXECHELP
outfile = 'QNAMER CMSUT1 A'
'SET CMSTYPE HT'
'STATE ' outfile
if rc = 0 then 'ERASE ' outfile
'SET CMSTYPE RT'
'EXECIO * CP (STEM NAMES. STRING QUERY USERS '
'EXECIO 'names.0' DISKW ' outfile ' (STEM NAMES. '
'EXECIO * CP (BUFFER 20000 STEM NAMES. STRING QUERY NAMES '
'EXECIO 'names.0' DISKW ' outfile '(STEM NAMES. '
'FINIS ' outfile
/* stack the xedit environment commands                             */
pfk = ' PF1 for ALL    PF2 for 'parm1
if parm1 = '' then pfk  = '     '
if parm1 Â¬= '' then push 'ALL/'parm1
push 'set reserve 1 nohigh QUERY NAMES ' parm1'              'pfk
push 'set shadow off '
push 'set scale off '
push 'set curline on 3'
push 'set msgline on 1 overlay'
push 'set cmdline TOP'
push 'set TOFEOF off'
push 'set prefix off'
push 'set PF1 ALL  '
push 'set PF2 ALL/'parm1
push 'set PF3 qquit'
'XEDIT ' outfile ' ( NOPROF '
'ERASE ' outfile
exit
EXECHELP:
'VMFCLEAR'
say '                   UID EXEC - HELP       ICI CMS IDG TRS VM 'date()
say
say
say '    This EXEC allows you to use the QUERY NAMES (Q N) command   '
say '    in an XEDIT environment, thus allowing search commands to  '
say'     be used to find individual users.                            '
say
say '      Usage:                                                    '
say '             UID xxxxx                                          '
say
say '             UID                 displays ALL userids           '
say '             UID DSC             displays ALL DISCONNECTED users'
say '             UID uid             displays ALL occurrences of uid'
say '             UID vtamid          displays user using vtamid     '
say
say '             UID ?               displays this screen           '
say
exit
/*      last line of exec                                             */

D W Lawton
VM Systems Programmer
ICI IBM Development Group (UK)

