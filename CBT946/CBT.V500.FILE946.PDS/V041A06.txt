COBOL program sequencing

Apparently XEDIT does not provide a command for sequencing
COBOL programs in columns 1-6.  Even though sequence
 numbers are not necessary in source programs, since the
compiler generates new numbers, our application programmers
still wanted the ability to sequence their source programs.  To
sequence COBOL programs I wrote the following XEDIT macro,
called COBSEQ.

COBSEQ first saves your data and then performs the change.  If
you accidentally invoke this macro, you can issue QQUIT to back
out the changes (sequence numbers).  COBSEQ can be assigned
to a PF key in your PROFILE XEDIT macro or you can just type
COBSEQ on the XEDIT command line and press ENTER.  It
will always sequence the whole file and it will also keep your
place in the file.  It does not matter where you are in the file
when you issue COBSEQ.

COBSEQ XEDIT

/* This macro sequences CMS files in columns 1-6 incremented by 10    */
/* (for COBOL programs).  The file is first saved in case this macro  */
/* is invoked by mistake. (Issue QQUIT to backout changes). The whole */
/* program will be sequenced no matter where you are in the file when */
/* the macro is invoked.                                              */
  'COMMAND EXTRACT /LINE/SIZE/CURSOR/AUTOSAVE'
  'COMMAND SAVE'
  'COMMAND AUTOSAVE OFF'
  'COMMAND :1'
   seqnum   = 10
   seqnum   = RIGHT(seqnum,6,'0')
do SIZE.1
   'COMMAND CLOCATE :1'
   'COMMAND CREPLACE' seqnum
   'COMMAND NEXT 1'
   seqnum = seqnum + 10
   seqnum = RIGHT(seqnum,6,'0')
end
  if LINE.1   > 0
     then 'COMMAND :' LINE.1
     else 'COMMAND TOP'
  if CURSOR.3 > 0
     then 'COMMAND CURSOR FILE' CURSOR.3 CURSOR.4
     else 'COMMAND CURSOR CMDLINE'
  'COMMAND AUTOSAVE' AUTOSAVE.1
exit


P C Shumway
Senior Systems Analyst
Masonite Corp (USA) ) P C Shumway 1990


