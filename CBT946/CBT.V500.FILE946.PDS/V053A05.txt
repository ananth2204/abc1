SHOW macro - an additive ALL

The ALL subcommand in XEDIT is a tremendous feature.  If
you are not familiar with this subcommand I suggest that you
read the reference manual or use the help screens (if they are
available).

The ALL subcommand will present all the lines of the file you
are editing that contain a specified data string.  For instance, if
you type 'ALL error' on the command line of a file being
edited, all the lines that contain the word 'error' will be
displayed.  XEDIT will also shadow the lines not being
displayed unless you issue 'SET SHADOW OFF'.  You can
have the set command in your PROFILE XEDIT file, if you
want to suppress shadow lines as your default.

The ALL subcommand always uses the entire file being edited
when searching for the string.  I have written the following
macro called SHOW to work like the ALL subcommand except
that it is additive.  It uses the result of its execution as the input
to subsequent executions of the macro.  For example, if you
issue 'SHOW error' it will work like the above example and
show all the lines of the file which have the word 'error'.  If
you then issue 'SHOW warn' it will show all the lines which
contain both 'error' and 'warn'.

The SHOW macro issues the prefix command 'X' to exclude
the lines which do not meet the given criteria.  To view the
entire file again after executing SHOW one or more times,
issue 'SHOW ALL'  or just issue an ALL subcommand without
supplying a parameter.


SHOW XEDIT

/*                                                                    */
/* This XEDIT macro is an additive ALL subcommand.  It uses the       */
/* subset of lines selected from previous executions of SHOW as the   */
/* input set of lines for the current execution.  The entire file     */
/* can be displayed again by either executing 'ALL' without a         */
/* parameter string or by issuing 'SHOW ALL'.                         */
/*                                                                    */
  'PRESERVE'
  'SET MSGMODE OFF'
  'SET SHADOW OFF'
  'SET SCOPE DISPLAY'
  'SET CASE M I'
  parse arg string
  if SUBSTR(string,1,1) = '/'
     then string = SUBSTR(string,2,(LENGTH(string) - 1))
  if string = '' | string = 'all' | string = 'ALL'
     then do
          'ALL'
          signal EXIT
          end
  'TOP'
  'LOCATE|'string'|'
  if rc ¬= 0
     then do
          'SET MSGMODE ON'
          'MSG Note: "'string'" was not found... no action taken'
          signal EXIT
          end
  'TOP'
  'DOWN 1'
  do forever
     'EXTRACT /CURLINE/EOF'
     if rc ¬= 0
        then signal EXIT
     if EOF.1 = 'ON'
        then signal EXIT
     if INDEX(CURLINE.3,string) = 0
        then do
             'LPREFIX X'
             iterate
             end
        else do
             'DOWN 1'
             iterate
             end
  end
EXIT:
  'RESTORE'
  'CURSOR CMDLINE'
  'TOP'
  exit

P C Shumway
Project Leader
Masonite Corporation (USA)                               c P C Shumway 1991


