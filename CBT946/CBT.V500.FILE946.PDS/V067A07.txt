Paging techniques for IOS3270

This EXEC shows you a simple technique for paging functions,
using the module IOS3270. It's particularly useful when you
must show a large quantity of short fields, such as tables,
library directory entries, and so forth. In such cases your users
may want to go directly to the end of the data. This technique
permits the use of function keys PF7 and PF8 and, more
importantly, a page number. This EXEC creates and shows
2000 8-byte fields in pages of 120 entries. Enter, for example,
page number 20 and the screen will show the last page, 17. If
you enter zero or a non-numeric value as page number, the
EXEC will respond with an appropriate message and show the
first page. Note that there is only one IOS3270 screen defined,
showing exactly 120 fields. Every paging function results in
the preparation of the logical screen. The first page is
composed of fields ELEM0001 to ELEM0120, page 2 goes
from ELEM0121 to ELEM0240, and so on.

&TRACE OFF
* THIS EXEC SHOWS YOU HOW TO USE ADVANCED PAGING FUNCTIONS WITH IOS3270
&FIRST_PAGE = &LITERAL OF *** FIRST PAGE ***
&LAST_PAGE  = &LITERAL OF *** LAST PAGE ***
&PAGE_NNUM  = &LITERAL OF *** PAGE NOT NUMERIC; SHOWING FIRST PAGE ***
&PAGE_LESS1 = &LITERAL OF *** PAGE LESS THAN 1; SHOWING FIRST PAGE ***
&SCREEN = 1
&COUNT  = 0
&SCOUNT = 0
-LOOP
&COUNT = &COUNT  + 1
&IF &COUNT GT 2000 &GOTO -END_OF_LOOP
&COUNT = &CONCAT OF 0000 &COUNT
&COUNT = &RIGHT OF &COUNT 4
&ELEM&COUNT = &CONCAT OF ELEM &COUNT
&GOTO -LOOP
-END_OF_LOOP
* PREPARING A LOGICAL SCREEN
-PREP_SCREEN
&SCOUNT = 0
&COUNT = &MULT OF 120 &SCREEN
&COUNT = &COUNT - 120
-PREP_LOOP
&SCOUNT = &SCOUNT + 1
&COUNT = &COUNT + 1
&IF &SCOUNT GT 120 &GOTO -DISP_SCREEN
&COUNT = &CONCAT OF 0000 &COUNT
&COUNT = &RIGHT  OF &COUNT 4
&SCOUNT = &CONCAT OF 0000 &SCOUNT
&SCOUNT = &RIGHT  OF &SCOUNT 4
&IF .&ELEM&COUNT EQ . &SE&SCOUNT = &LEFT OF &BLANK     8
&IF .&ELEM&COUNT NE . &SE&SCOUNT = &LEFT OF &ELEM&COUNT 8
&GOTO -PREP_LOOP
* DISPLAYING THE LOGICAL SCREEN
-DISP_SCREEN
&IF .&SE0001 EQ . &ALARM = (ALARM
&IF .&SE0001 EQ . &MSG = &LAST_PAGE
&IF .&SE0001 EQ . &SCREEN = &SCREEN - 1
&IF .&SE0001 EQ . &GOTO -PREP_SCREEN
IOS3270 &0 EXEC ;SCREENB ;SCREENE &ALARM
&ALARM = &BLANK
&MSG   = &BLANK
&TIPO = &TYPE OF &SCREEN
&IF &TIPO NE NUM &ALARM = (ALARM
&IF &TIPO NE NUM &MSG   = &PAGE_NNUM
&IF &TIPO NE NUM &SCREEN = 1
&IF &SCREEN LT 1 &ALARM = (ALARM
&IF &SCREEN LT 1 &MSG   = &PAGE_LESS1
&IF &SCREEN LT 1 &SCREEN = 1
&IF &IOSK = PF03 &EXIT
&IF &IOSK = PF07 &GOTO -BACKWARD
&IF &IOSK = PF08 &GOTO -FORWARD
&GOTO -PREP_SCREEN
* PAGING BACKWARDS
-BACKWARD
&SCREEN = &SCREEN - 1
&IF &SCREEN NE 0 &GOTO -PREP_SCREEN
&SCREEN = &SCREEN + 1
&ALARM = (ALARM
&MSG   = &FIRST_PAGE
&GOTO -DISP_SCREEN
* PAGING FORWARDS
-FORWARD
&SCREEN = &SCREEN + 1
*GOTO -PREP_SCREEN
* IOS3270 SCREEN FORMAT
;SCREENB
.F % % PF03 % % % PF07 PF08 %
.C
WITH THIS TECHNIQUE, YOU CAN USE PF7 & PF8 TO PAGE
THROUGH THE SCREENS OR YOU CAN INFORM THE PAGE NUMBER... Â¬&SCREEN
.SSC
&SE0001 &SE0016 &SE0031 &SE0046 &SE0061 &SE0076 &SE0091 &SE0106
&SE0002 &SE0017 &SE0032 &SE0047 &SE0062 &SE0077 &SE0092 &SE0107
&SE0003 &SE0018 &SE0033 &SE0048 &SE0063 &SE0078 &SE0093 &SE0108
&SE0004 &SE0019 &SE0034 &SE0049 &SE0064 &SE0079 &SE0094 &SE0109
&SE0005 &SE0020 &SE0035 &SE0050 &SE0065 &SE0080 &SE0095 &SE0110
&SE0006 &SE0021 &SE0036 &SE0051 &SE0066 &SE0081 &SE0096 &SE0111
&SE0007 &SE0022 &SE0037 &SE0052 &SE0067 &SE0082 &SE0097 &SE0112
&SE0008 &SE0023 &SE0038 &SE0053 &SE0068 &SE0083 &SE0098 &SE0113
&SE0009 &SE0024 &SE0039 &SE0054 &SE0069 &SE0084 &SE0099 &SE0114
&SE0010 &SE0025 &SE0040 &SE0055 &SE0070 &SE0085 &SE0100 &SE0115
&SE0011 &SE0026 &SE0041 &SE0056 &SE0071 &SE0086 &SE0101 &SE0116
&SE0012 &SE0027 &SE0042 &SE0057 &SE0072 &SE0087 &SE0102 &SE0117
&SE0013 &SE0028 &SE0043 &SE0058 &SE0073 &SE0088 &SE0103 &SE0118
&SE0014 &SE0029 &SE0044 &SE0059 &SE0074 &SE0089 &SE0104 &SE0119
&SE0015 &SE0030 &SE0045 &SE0060 &SE0075 &SE0090 &SE0105 &SE0120
.SC
%&MSG
.SH
PF-KEYS; <3=QUIT> <7=BACKWARD> <8=FORWARD>
;SCREENE

Gene Earl Bearup
Informatics Consultant (Brazil)

