Correcting REXX errors

If an error occurs while programming in REXX and during
execution of the EXECs, REXX writes an error message
indicating the line where the error appeared.

The GETREXX program, installed as a nucleus extension, notes
the error written on the screen by the DMSRCN routine and starts
the REXXERR EXEC, which edits the faulty program, positions
the error line as the current line and writes the REXX error
messages in the editor message area.


The GETREXX EXEC is used to install the GETREXX program.
It has three options:

ON: Installs the GETREXX module as a nucleus extension
and activates it.
OFF: Deactivates the GETREXX module and drops it from the
list of nucleus extension programs.
Q:   Indicates the GETREXX module status.


GETREXX EXEC

/*              PROGRAM FOR INSTALLATION OF GETREXX MODULE           */
trace off
ARG WHAT                               /* NOTE OPTION                */
if what='' then do                     /* no option specified        */
  say 'options are : Q | ON | OFF'     /* show options               */
  exit
  end
sw=0
'nucxmap (stack'                       /* see if GETREXX is active   */
do i=1 to queued()
  pull xx
  parse var xx pgnam ost
  if pgnam='GETREXX' then do
    sw=1
    leave
    end
  end
'desbuf'
select
  when what='Q' then do               /* Q option -                  */
    IF SW=1 THEN SAY 'GETREXX IS ON' /*INDICATE GETREXX MODULE STATUS*/
    else say 'GETREXX is OFF'
    end
  when what='ON' then do              /* ON option -                 */
    'set emsg on'                     /* activate GETREXX module     */
    if sw=1 then say 'GETREXX is already ON'
    else do
      'nucxload getrexx (system'
      'getrexx'
      if rc<>1 then do
        say 'error01: GETREXX not activated.'
        exit
        end
      say 'GETREXX is now ON'
      end
    end
  when what='OFF' then do             /* OFF option -                */
    if sw=0 then say 'GETREXX is already OFF'
    else do                           /* deactivate GETREXX module   */
    'getrexx'
      if rc<>2 then do
        say 'error02: GETREXX not deactivated'
        exit
        end
      'nucxdrop getrexx'
      say 'GETREXX is now OFF'
      end
    end
  otherwise say 'options are :  Q | ON | OFF | DROP'
  end
exit
REXXERR EXEC
/*            PROGRAM FOR PROCESSING THE REXX ERROR STACKER FROM     */
/*                         GETREXX module                            */
trace off
PARSE PULL ERRLIN           /* GET REXX ERROR STACKED FROM GETREXX   */
'desbuf'
parse var errlin . error    /* get rid of DMSREX***                  */
prase var error . . . exenam . . linenum ost                         */
len=length(linenum)         /* parse exec name and number of bad line*/
len=len-1
linenum=substr(linenum,1,len)
push ':'linenum
push 'msg 'error
push 'xe 'exenam' EXEC'     /* go into exec that failed              */
exit


GETREXX  CSECT
         USING *,15
         STM   14,12,12(13)
         ST    13,*+12
         BAL   13,*+4+18*4+14
         USING *,13
         DS    18F
EXIT     L     13,4(13)
         L     14,12(13)
         LM    2,12,28(13)
         BR    14
*  THIS PROGRAM NOTES DMSREX ERROR MESSAGES AND PREPARES EXECUTION    *
*  OF A REXXERR ROUTINE, WHICH PROCESSES THE MESSAGE.                 *
*  IT MUST BE INSTALLED AS A NUCLEUS EXTENSION WITH SYSTEM            *
*  OPTION.                                                            *
*  THIS PROGRAM USES NUCCOPYR AREA IN NUCON TO SAVE :                 *
*              NUCCOPYR   --->   GETREXX BASE REG. (R13)              *
*              NUCCOPYR+4 --->   CMS OR OTHER PROGRAMS R13            *
*              NUCCOPYR+8 --->   CMS SVCNPSW                          *
         USING NUCON,R0
         CLC   MYPSW(8),SVCNPSW        HAVE I BEEN ALREADY INSTALLED?
         BNE   NO
         SPKA  0(R0)                   KEY 0
         MVC   SVCNPSW(8),NUCCOPYR+8   RESTORE CMS SVC
         XR    R15,R15                 I'VE BEEN INSTALLED SO EXIT
         LA    R15,2(R15)              WITH RET. CODE 2
         SPKA  3308(R0)                KEY E
         B     EXIT
NO       LA    R2,BACK                 GET NEW SVC ADDRES
         ST    R2,MYPSW+4              STORE IT IN MY NEW SVC PSW
         SPKA  0(R0)                   KEY 0
         ST    R13,NUCCOPYR            STORE MY BASE REG.
         MVC   NUCCOPYR+8(8),SVCNPSW   STORE CMS NEW SVC PSW
         MVC   SVCNPSW(8),MYPSW        REPLACE IT WITH MY NEW SVC PSW
         XR    R15,R15                 INSTALLATION COMPLETE, EXIT
         LA    R15,1(R15)              WITH RET. CODE 1
         SPKA  3308(R0)                KEY E
         B     EXIT
BACK     ST    R13,NUCCOPYR+4          STORE CMS OR OTHER PROGRAM R13
         L     R13,MUCCOPYR            LOAD MY BASE REG.
         CLI   SVCOPSW+3,X'CA'         IS IT SVC202 ?
         BNE   GOOUT
         CLC   0(8,R1),TYPLIN          IS IT THE TYPLIN ROUTINE ?
         BNE   GOOUT
         ST    R9,SAVR09               SAVE R9
         L     R9,8(R1)                GET BUFFER ADDRES
         STCM  R9,B'0111',ADBUFF1      PREPARE IT FOR THE ATTN ROUTINE
         CLC   ERROR(6),0(R9)          IS IT A REXX ERROR (DMSRCN) ?
         L     R9,SAVR09               RESTORE MY R9
         BNE   GOOUT
         MVC   SVCNPSW(8),NUCCOPYR+8   GOT IT !, RESTORE CMS SVC PSW
         ST    R1,SAVR09               SAVE MY R1
         STCM  R11,B'0001',LENBUF1     R11 FROM DMSRCN CONTAINS BUFFER
         LA    R1,PLIST1               LENGTH
         SVC   202                     STACK REXX ERROR MESSAGE
         DC    AL4(1)
         LA    R1,REXXERR
         STCM  R1,B'0111',ADBUFF2
         LA    R1,PLIST2
         SVC   202                     STACK 'EXEC REXXERR'
         DC    AL4(1)
         L     R1,SAVR09
         MVC   SVCNPSW(8),MYPSW        STORE AGAIN MY NEW SVC PSW
         B     GOOUT                   LET'S DO IT AGAIN !
GOOUT    ST    R13,NUCCOPYR            STORE MY BASE REG.
         L     R13,NUCCOPYR+4          LOAD CMS OR OTHER PROGRAM R13
         LPSW  NUCCOPYR+8              GO BACK WHERE I STOPPED
         DS    0D
MYPSW    DC    XL4'FF000000',XL4'00'
ERROR    DC    CL6'DMSREX'
TYPLIN   DC    CL8'TYPLIN'
         DS    0F
SAVR09   DS    1F
PLIST1   DS    0D
         DC    CL8'ATTN'
         DC    CL4'LIFO'
LENBUF1  DC    X'50'
ADBUFF1  DC    XL3'00'
FENCE1   DC    8X'FF'
PLIST2   DS    0D
         DC    CL8'ATTN'
         DC    CL4'LIFO'
         DC    X'C'
ADBUFF2  DC    XL3'00'
FENCE2   DC    8X'FF'
REXXERR  DC    CL12'EXEC REXXERR'
         LTORG
         NUCON
         REGEQU
         END


Sinisa Petric
Systems Programmer
Zerc (Yugoslavia) ) Zerc 1989

