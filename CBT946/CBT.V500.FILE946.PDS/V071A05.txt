Restoring user mini-disks

The following EXEC restores user mini-disks. In order for it to
execute there must be a mini-disk at X'192'. By and large this
is just a temporary disk, so that the user may select an
individual file, rather than having the whole disk restored, and
thus possibly losing any updates which may have been made
since the last back-up was taken.

The EXEC will restore an FB-512 mini-disk from a DDR dump
tape. A file RESTORE PARMS is punched, and read in, to be
the input to the DDR program. A 3420 must be attached to this
machine, and the disk to be restored must be accessed as
X'192'.

If X'192' is not the DASD address required, &4 must be
updated. This EXEC will restore to an FB-512 device. If
X'192' is a CKD, please make the necessary amendments.

&CONTROL OFF NOMSG
**********************************************************************
 *%OPTIMIZED AT 18:37:28 ON 91/09/25
 *%       N O T I C E:
 *% THIS STATEMENT IS NECESSARY FOR THE OPTIMIZATION PROGRAM - VMFOPT
 *% DE-OPTIMIZE THIS MACRO BEFORE MAKING ANY CHANGES USING - VMFDEOPT
 &IF &INDEX < 2 &GOTO 67 -TELL
 &IF &2 LT &1 &GOTO 63 -PARMERR
 &IF &2 EQ &1 &GOTO 63 -PARMERR
 * Declare the args
 &ARGS &1 &2 0 192
 CP QUERY VIRT &4
 &IF &RETCODE EQ 40 &GOTO 55 -NOUSER
 CP QUERY 181
 &IF &RETCODE EQ 40 &GOTO 59 -NOTAPE
 * Build parameter file.   &1 is start block.   &2 is endblock.
 CP SP CONS * START
 CP SP PUN * CONT CLA R
 &PUNCH :READ RESTORE PARMS A
 &PUNCH SYSPRINT 00E
 &PUNCH IN 181 3420
 &PUNCH OUT &4 FB-512
 &PUNCH RESTORE &1 TO &2 REORDER TO &3
 CP SP PUN CLOSE
 CP ORDER RDR CLA R
 READCARD * *
 * Skip over VOL1 header label.  If no VOL1 present, comment out the
 * following line.   DO NOT ERASE IT, as this will invalidate updates
 * made to this exec via the VMFOPT macro.
 TAPE FSF 1 ( TAP1
 * How to reply to msgs.
 &BEGTYPE
 RESTORE EXEC:
              REPLY 'YES' TO MSG DMKDDR717R
              REPLY 'YES' TO MSG DMKDDR711R
              REPLY 'YES' TO MSG DMKDDR725R
 &END
 DDR RESTORE PARMS A
 ERASE RESTORE PARMS A
 CP SP CONS CLOSE STOP
 &EXIT 0
 * Error routines commence here.   Note that an error condition in the
 * input parameters will cause job execution to terminate abnormally.
 -NOUSER
 CLRSCRN
 &TYPE RESTORE EXEC:  DISK TO BE RESTORED MUST BE ATTACHED AT X'192'
 &EXIT 40
 -NOTAPE
 CLRSCRN
 &TYPE RESTORE EXEC:  3420 MUST BE ATTACHED AS X'181'
 &EXIT 40
 -PARMERR
 CLRSCRN
 &TYPE RESTORE EXEC:  END BLOCK ( CYLINDER ) MUST EXCEED START BLOCK.
 &EXIT 24
 -TELL
 CLRSCRN
 &BEGTYPE
       RESTORE EXEC:
                      FORMAT IS RESTORE (START BLOCK) (END BLOCK)
                                    --- or ---
                        Restore (START cylinder) (END cylinder)
 &END
 &EXIT 24

M A Shears
Shift Leader (UK)

