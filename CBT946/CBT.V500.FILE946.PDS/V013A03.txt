 Performance threshold monitor

Last month we published the first part of this article which examined the way to
provide an on-line non-interventionist performance threshold monitor that
provided data about virtual machines exceeding specific installation-defined
performance threshold values.  A system was developed that was called XPERFORM,
which relied on performance data supplied by IBM's VM Real Time Monitor Program
(SMART).  XPERFORM operates under VM/SP Releases 3.1 and 4.0.

This month we publish the other EXECs and parameter files along with an example
of the output.



DRAW EXEC

ÝEditor's note:  this EXEC contains the XPERFORM logo and has not been
reproduced here.¨

QPERFORM EXEC

&TRACE OFF
*%       N O T I C E:
*% THIS STATEMENT IS NECESSARY FOR THE OPTIMISATION PROGRAM - VMFOPT
*% DE-OPTIMISE THIS MACRO BEFORE MAKING ANY CHANGES USING - VMFDEOPT
&B12 = &LEFT OF &BLANK 12
&MHDER = &STRING OF The following machines are NOT checked against limits
MAKEBUF
SET CMSTYPE HT
GETFMADR                         *NOTE* IBM'S IPF MODULE THAT STACKS THE
                                        FIRST AVAILABLE FILEMODE AND
                                        VIRTUAL DISK ADDRESS, THUS ALLOW-
                                        ING AN EXEC TO LINK AND ACCESS A
                                        MINI-DISK WITHOUT DISTURBING THE
                                        EXISTING LINKS OR ACCESSES.
&READ VARS * &MODE *
DROPBUF
SET CMSTYPE HT
ACCESS 4F1 &MODE                 *NOTE* THIS IS XPERFORM'S 191 MINI-DISK
SET CMSTYPE RT
&ALLINFO = 0
&IF &INDEX = 0 &ALLINFO = 1
&IF .&1 = .LIMITS &GOTO -DISPLAY_LIMITS
&IF .&1 = .MACHINES &GOTO -DISPLAY_SERVICE_MACHINES
&IF .&1 = .USERS &GOTO -DISPLAY_USERS_TO_GET_MESSAGES
&IF .&1 = .HELP &GOTO -DISPLAY_HELP_INFORMATION
&IF &ALLINFO = 1 &GOTO -DISPLAY_LIMITS
-DISPLAY_HELP_INFORMATION
EXECIO 1 CP (SKIP STRING SCREEN OUTAREA RED
CLRSCRN
&BEGTYPE -INFORMATION
The command QPERFORM allows the user to query the parameters used by
    the XPERFORM machine.
                                 +--                              --+
    Command format:     QPERFORM | LIMITS | MACHINES | USERS | HELP |
                                 +--                              --+
    where options are:

          LIMITS   - Displays the limit thresholds that are checked by
                     XPERFORM.
          MACHINES - Displays those machines that are NOT to be checked
                     by XPERFORM.
          USERS    - Displays a list of userids that are authorised to
                     receive messages from XPERFORM indicating threshold
                     excesses
          HELP     - Self explanatory

Entering QPERFORM without any parameters will display the LIMITS, MACHINE,
         and USERS information one after another.
-INFORMATION
&TYPE Please enter LIMITS | MACHINES | USERS | ALL (ALL INFORMATION)
&TYPE &B12         or press ENTER to exit
&READ VAR &WHICH
&IF .&WHICH = .&GOTO -RELEASE
&IF &WHICH = LIMITS &GOTO -DISPLAY_LIMITS
&IF &WHICH = MACHINES &GOTO -DISPLAY_SERVICE_MACHINES
&IF &WHICH = USERS &GOTO -DISPLAY_USERS_TO_GET_MESSAGES
&IF &WHICH = ALL &ALLINFO = 1
&IF &WHICH = ALL &GOTO -DISPLAY_LIMITS
&TYPE &BLANK
CP MSGNOH * !! ERROR !! - &WHICH IS INVALID
CP MSGNOH * -----------
&GOTO -DISPLAY_HELP_INFORMATION
-DISPLAY_LIMITS
EXECIO 1 CP (SKIP STRING SCREEN OUTAREA TURQUOIS
&TYPE &BLANK
&TYPE Limit thresholds to be checked against are
&TYPE &BLANK
MAKEBUF
EXECIO * DISKR LIMITS PARM &MODE 6
&STACK ENDOFFILE
-READLIMITS
&READ STRING &LIMITS
&IF &LIMITS = ENDOFFILE &GOTO -LIMITSDROPBUF
&TYPE &LIMITS
&GOTO -READLIMITS
-LIMITSDROPBUF
DROPBUF
&IF &ALLINFO = 1 &GOTO -DISPLAY_SERVICE_MACHINES
&GOTO -RELEASE
-DISPLAY_SERVICE_MACHINES
EXECIO 1 CP (SKIP STRING SCREEN OUTAREA PINK
&TYPE &BLANK
&MACHINECOUNT = 0
MAKEBUF
EXECIO * DISKR SERVMACH PARM &MODE 6
&STACK ENDOFFILE
-READSERVMACH
&READ STRING &SER
&IF &SER = ENDOFFILE &GOTO -SHOWMACHINES
&MACHINECOUNT = &MACHINECOUNT + 1
&IF &MACHINECOUNT = 1 &TYPE &MHDER
&IF &MACHINECOUNT = 1 &TYPE &BLANK
&TYPE &SER
&GOTO -READSERVMACH
-SHOWMACHINES
DROPBUF
&IF &MACHINECOUNT < 1 &TYPE
&IF &MACHINECOUNT < 1 &TYPE All machines ARE being checked against limits
&IF &ALLINFO = 1 &GOTO -DISPLAY_USERS_TO_GET_MESSAGES
&GOTO -RELEASE
-DISPLAY_USERS_TO_GET_MESSAGES
EXECIO 1 CP (SKIP STRING SCREEN OUTAREA YELLOW
&TYPE &BLANK
&TYPE Users receiving messages from XPERFORM when limits are passed are
&TYPE &BLANK
MAKEBUF
EXECIO * DISKR USERMSG PARM &MODE 6
&STACK ENDOFFILE
-READUSERS
&READ STRING &USERS
&IF &USERS = ENDOFFILE &GOTO -USERSDROPBUF
 &TYPE &USERS
&GOTO -READUSERS
-USERSDROPBUF
DROPBUF
&GOTO -RELEASE
-RELEASE
REL &MODE
&EXIT


LIMITS PARM

******* Parameter File That Lists Limits To Be Checked Against *******
******* by XPERFORM Via XSMART                                 *******
******* The first 3 lines must be commented out                *******
10 - % CPU
10 - I/O RATE/SECOND
10 - PAGING RATE/SECOND


SERVMACH PARM

******* Parameter File That Lists Those Service Machines NOT To *******
******* Be Checked by XPERFORM Via XSMART                       *******
******* The first 3 lines must be commented out                 *******
SQLPAZ SQLTST SYSTEM XPERFORM


USERMSG PARM

**** Parameter File That Lists Those Userids That Are To Get       ****
**** A Warning Message From XPERFORM If A Limit Has Been Exceeded. ****
**** Userids Must Be of Length Of 8 Characters.                    ****
1       9      17      25      33
+.......+.......+.......+.......+
MAINT   P0AA    P0MSH


XPERFORM DIRECTORY DEFINITION

USER XPERFORM XPERFORM 512K 512K ABG 32
  ACCOUNT XPERFORM XPERFORM
  ACIGROUP VMGRP
  OPTION SVCACCL ACCT
  IPL CMS
  CONSOLE 009 3215
  SPOOL 00C 2540 READER *
  SPOOL 00D 2540 PUNCH A
  SPOOL 00E 1403 A
*
 LINK MAINT    190 190 RR
 LINK MAINT    19D 19D RR
 LINK MAINT    19E 19E RR
 LINK MAINT    319 319 RR
*
 MDISK 191 FB-512 445871 002500 VMGCS1 MR RORM WORM MORM


SAMPLE XPERFORM MESSAGES

 FROM XPERFORM (09:11:36) --> BATCH3    -IS USING 45% OF THE CPU
 FROM XPERFORM (11:36:48) --> VMMAP     -IS PERFORMING I/O AT 22/SEC
 FROM XPERFORM (14:03:29) --> P0AA      -IS PAGING AT 15/SEC


Alan Kauf-Stern
 Systems Programmer
Paz Oil (Israel)    ) Xephon 1987
































































