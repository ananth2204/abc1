Checking the status of IO assist

IO assist (also called SIE assist) is a way of substantially
reducing the VM system overhead associated with guest
operating system I/O in VM/ESA and VM/XA environments.
To be eligible for IO assist, the device must be attached to a
V=R or V=F guest.

There are a number of conditions that can cause CP temporarily
or permanently to turn off IO assist for a single device or even
all the devices attached to a guest. (IBMLink Q/A item:
920804 IOASSIST and VM/ESA monitor records explains
these conditions in detail.) When IO assist is off, additional CP
overhead is incurred which could result in performance
degradation. The 'QUERY VIRTUAL Ývirtual addr¨' command
would show that the device is eligible for IO assist even though
IO assist is really off. There are no other commands that can be
used to display the status of IO assist for a given device.

Fortunately, CP keeps the information in the VDEVIOP1 field
at + B0 into the VDEV block. The CHECKSIE EXEC will
interrogate the block and display the status of IO assist for any
device. Input parameters are the virtual machine-id and the
virtual device address. Class C or E is required. Note that the
EXEC was written for a VM/ESA 1.1 environment and that it
uses CMS Pipelines. Installations that do not have Pipelines
can convert the Pipelines commands to EXECIO and REXX
commands. The VDEVIOP1 offset should be correct for both
ESA 1.0 and 1.1. VM/XA users should verify that the
VDEVIOP1 offset is correct.


CHECKSIE EXEC

/*THIS EXEC DISPLAYS THE IO ASSIST STATUS OF A VIRTUAL DEVICE       */
/*INPUT PARAMETERS ARE VIRTUAL MACHINE-ID AND VIRTUAL ADDRESS       */
ARG MACHINE DEVADDR
IF DEVADDR = ''
    THEN DO
    IF MACHINE = ''
       THEN DO
            SAY 'INPUT VIRTUAL MACHINE-ID'
            PULL MACHINE
       END
    SAY 'INPUT DEVICE ADDRESS'
    PULL DEVADDR
END
'PIPE CP LOCATE' MACHINE DEVADDR  '|DROP 1|SPECS WORDS 2|VAR VDEV'
IF RC = 3
   THEN DO
        SAY 'INVALID VIRTUAL MACHINE-ID'
        EXIT
   END
ELSE
IF RC = 40
   THEN DO
        SAY 'INVALID VIRTUAL ADDRESS'
        EXIT
   END
ELSE
IF RC > 0
   THEN DO
        SAY 'INVALID PARAMETERS SPECIFIED'
        EXIT
   END
PVDEVIOP1 = D2X(X2D(VDEV) + 176)   /*176 = X'B0'*/
/* WE ONLY WANT 1ST BYTE*/
'PIPE CP DISPLAY H'||PVDEVIOP1|| '.1 | SPECS WORDS 2-2 1-2|VAR VDEVIOP1'
IF VDEVIOP1 >= '40'
    THEN SAY DEVADDR 'IS OPERATING UNDER I/O PASS-THROUGH'
ELSE
IF VDEVIOP1 >= '20'
    THEN SAY DEVADDR 'IS BEING PLACED UNDER I/O PASS-THROUGH'
ELSE
IF VDEVIOP1 >= '10'
    THEN SAY DEVADDR 'WILL BE REMOVED FROM I/O PASS-THROUGH'
ELSE
IF VDEVIOP1 >= '08'
    THEN SAY DEVADDR 'IS BEING REMOVED FROM I/O PASS-THROUGH'
ELSE
IF VDEVIOP1 >= '02'
    THEN SAY DEVADDR 'IS ELIGIBLE FOR X/A PASS-THROUGH',
                     'BUT PASS-THROUGH IS NOT ACTIVE FOR THIS DEVICE'
ELSE
IF VDEVIOP1 >= '01'
    THEN SAY DEVADDR 'IS ELIGIBLE FOR 370 PASS-THROUGH',
                     'BUT PASS-THROUGH IS NOT ACTIVE FOR THIS DEVICE'
ELSE
    SAY DEVADDR 'IS NOT ELIGIBLE FOR PASS-THROUGH'

Robert Baxter
Senior Systems Programmer
Rocky Mountain News (USA)                              cXephon 1993

