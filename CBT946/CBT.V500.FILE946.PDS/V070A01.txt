Dynamic modification of CP user authority

When we updated the level of VM/HPO Release 4.2 to
VM/XA Release 2.1, the REXX routine, SETPRIV, published
in issue 30 of VM Update, did not work any more. So, I
rewrote the SETPRIV REXX routine.

This one does the same things as the old one and it was based
on the same idea, ie the modification of the VMDPCLB0 field
in the HCPVMDBK. It allows CP command class authority to
be modified for the duration of the current session.

This routine was written to work under VM/XA Release 2.1
only.


SETPRIV EXEC

/* FUNCTION   - Dynamic modification of command classes   */
/* FORMAT     - SETPRIV machine  ABCDEFGH  ON/OFF         */
/* COMMENTS   - Requires class C to run                   */
INICIO:
 set IMSG off
 set EMSG off
 arg maquina classes switch parametro4
 call VERIFICA_PARAMETROS
 if retcod = 0 & classes ¬= '?' then call EXECUTA_OPERACAO
 call MOSTRA_CLASSES
 set IMSG on
 set EMSG on
EXIT retcod
VERIFICA_PARAMETROS:
 call INICIA_VARIAVEIS_E_MENSAGENS
 call IDENTIFICA_CLASSES_EXISTENTES
 call CONSISTE_PARAMETROS
RETURN
EXECUTA_OPERACAO:
 if (switch = '' | switch = 'ON') & retcod = 0
 then do
      do indice = 1 to length(classes)
        if pos(substr(classes,indice,1),classes_atuais) = 0
        then classes_futuras = classes_futuras||substr(classes,indice,1)
      end
 end
 if switch = 'OFF' & retcod = 0
 then do
      do indice = 1 to length(classes)
        if pos(substr(classes,indice,1),classes_atuais) ¬= 0
        then classes_futuras = classes_futuras||substr(classes,indice,1)
      end
      if pos('C',classes_futuras) ¬= 0
      then do
           say hi'ATTENTION:'lo'You are turning off the',
                                    ' class "C" of user:'hi''maquina''lo
           say 'Answer'hi'N'lo'to cancel your intention,',
                                         ' or'hi'<ENTER>'lo'to confirm.'
           pull resposta
           if resposta = 'N'
           then do
                classes_futuras = ''
                retcod = 7
           end
           else retcod = 8
      end
 end
 if classes_futuras ¬= '' then call SWITCH_CLASSES
 if retcod = 0 then call IDENTIFICA_CLASSES_EXISTENTES
RETURN
MOSTRA_CLASSES:
 interpret 'mensagem = MSG'right(retcod,2,'0')
 if retcod = 0 then mensagem = mensagem||hi''classes_atuais''lo
 if retcod = 8 then mensagem = mensagem||hi''maquina''lo
 say hi'SETPRIV:'lo''mensagem
RETURN
INICIA_VARIAVEIS_E_MENSAGENS:
 hi = '1de8'x
 lo = '1d60'x
 a  = x2d(80);b=a/2;c=b/2;d=c/2;e=d/2;f=e/2;g=f/2;h=g/2
 retcod = 0
 resposta = 'Y'
 classes_futuras = ''
 if maquina = '*' then maquina = userid()
 MSG00 = 'The classes of user 'maquina' are:'
 MSG01 = 'This user didn't have authority to use the EXEC SETPRIV.'
 MSG02 = 'The EXEC SETPRIV requires two(2) or three(3) parameters.'
 MSG03 = 'The user 'maquina' is not logged on.'
 MSG04 = 'The possible classes to be set on are: A,B,C,D,E,F,G and H.'
 MSG05 = 'Classes are not in the correct sequence or repeated.'
 MSG06 = 'The third parameter should be blank(" "), "ON" or "OFF".'
 MSG07 = 'You cancelled the operation. Classes are still the same.'
 MSG08 = 'You turned off the class "C" of user:'
RETURN
CONSISTE_PARAMETROS:
 if classes = '' | parametro4 ¬= '' then retcod = 2
 if maquina ¬= '*'
 then do
      'execio 1 CP (VAR' lixo 'STRING Q U' maquina
      if rc ¬= 0 then retcod = 3
 end
 if length(classes) > 8 & retcod = 0
 then retcod = 4
 else do
      if length(classes) > 1
      then do
           do indice = 1 to length(classes)-1
              if substr(classes,indice,1) >= substr(classes,indice+1,1)
              then retcod = 5
            end
            do indice = 1 to length(classes)
         if pos(substr(classes,indice,1),'ABCDEFGH') = 0 then retcod = 4
            end
       end
       else if pos(classes,'ABCDEFGH?') = 0 then retcod = 4
 end
 if switch ¬= '' & switch ¬= 'ON' & switch ¬= 'OFF',
                                              & retcod = 0 then retcod=6
RETURN
IDENTIFICA_CLASSES_EXISTENTES:
  laddr = 'LADDR'
 hexclass = 'HEXCLASS'
 classes_atuais = ''
 'EXECIO 1 CP ( VAR' laddr 'STRING LOC' maquina
 if word(laddr,1) ¬= 'VMDBK'
 then retcod = 1
 else do
      naddr = d2x(x2d(word(laddr,3))+x2d(3c0))
      'EXECIO 1 CP ( VAR' hexclass 'STRING D H'naddr'.8'
      hexclass = word(hexclass,2)
      codigo_de_classes = x2d(substr(hexclass,1,2))
      do indice = 0 to 7
         if codigo_de_classes >= a/(2**indice)
         then do
              classes_atuais = classes_atuais||d2c(c2d('A')+indice)
              codigo_de_classes = codigo_de_classes - a/(2**indice)
         end
      end
 end
RETURN
SWITCH_CLASSES:
 codigo_de_classes = x2d(substr(hexclass,1,2))
 do indice = 1 to length(classes_futuras)
    interpret 'valor_de_classe = 'substr(classes_futuras,indice,1)
    if switch = 'OFF'
    then codigo_de_classes = codigo_de_classes-valor_de_classe
    else codigo_de_classes = codigo_de_classes+valor_de_classe
 end
 hexclass = overlay(right(d2x(codigo_de_classes),2,'0'),hexclass)
 'EXECIO 0 CP (STRING STORE H'naddr hexclass
RETURN


Stoessel Dourado Moitinho
Systems Programmer                           ) Stoessel Dourado Moitinho 1992
Companhia de Eletricidade do Estado da Bahia (Brazil)
