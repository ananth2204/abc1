Locating user passwords

I have recently been faced with a very thorny problem
regarding system security. There are occasions when, at very
short notice, I am required to access a particular user-id,
sometimes a CMS service or batch machine, in order to resolve
a system problem, or to retrieve a particular file. It is wholly
unacceptable to have a list of passwords simply lying around in
the Data Centre. Also, it has been a cause of some concern to
have staff linking to disks and accessing the directory, outside
the context of a tested procedure. ('Accidents' with WRITE
and MULT passwords are not unheard of!) I, therefore, wrote
the following routine, intended for the Manager's use only,
which scans the directory for a given user-id, and returns the
user password. We use DIRMAINT, although it should be
possible, with some minor modifications, to use this routine
where DIRMAINT is not used. In order to link to and access
the relevant mini-disk on which the directory file resides, the
GETFMADR module is executed to obtain a link address and
CMS filemode. An example of the use of the GP2 program is
shown in Figure 1.


PROGRAM CODE

* TITLE:   GET PASSWORD    (V3)                                       *
* DESCRIPTION:                                                        *
* "THIS MODULE AUTOMATES THE PROCESS OF LINKING TO, AND ACCESSING THE *
*  RELEVANT DIRMAINT MINI-DISK PRIOR TO SEARCHING FOR A GIVEN USER-ID.*
*  DASD ADDRESS AND FILEMODE ARE DETERMINED BY THE GETFMADR MODULE.   *
*  ACCESS IS READ-ONLY.   MODULE IS EXECUTED IN THE FORMAT            *
*  GP2 (NAME) WHERE (NAME) IS THE REQUIRED USER-ID."                  *
* RETURN CODES:                                                       *
* ************                                                        *
* 0   - SUCCESSFUL EXECUTION                                          *
* 24  - NO USER-ID SUPPLIED                                           *
* 28  - ERROR FROM ISSUING "STATE"                                    *
* 36  - ERROR IN ACCESSING DISK                                       *
* 114 - ERROR LINKING TO MINI-DISK                                    *
         EJECT
         CSECT
         USING *,R11               USE R11 AS BASE
         LR    R11,R15             MOVE CURRENT LOCATION TO BASE
         MVC   WANTED(8),8(R1)     STORE PASSED PARMS
         LA    R1,=C'VMFCLEAR'     POINT R1 TO CMS COMMAND
         SVC   202                 DO IT ...
         DC    AL4(1)              IGNORE ERRORS
         CLI   WANTED+0,X'FF'      HAVE WE A USER-ID?
         BE    PARMERR             NO?   GO ERR ROUTINE
         BAL   R12,PUTPWD          BRANCH ON R12 - PUTPWD
* ISSUE AN SVC 202 TO EXECUTE THE GETFMADR MODULE *
         LA    R1,GETFMADR         ROUTINE TO EXECUTE
         SVC   202                 CALL IT
         DC    AL4(GETERR)         ADDRESS OF ERROR ROUTINE
* READ THE RESULTS OF GETFMADR *
         RDTERM BUFFER1,LENGTH=7,EDIT=PHYS
         MVC   LINKCUU(3),NEWCUU   MOVE CUU TO LINK & ...
         MVC   ACCDASD(3),NEWCUU   ACCESS ROUTINES
         MVC   ACCMODE(1),NEWFM    MOVE FMODE TO ACCESS ...
         MVC   RELMODE(1),NEWFM    RELEASE ...
         MVC   STATEFM(1),NEWFM    & STATE ROUTINES
         LA    R1,LINKDASD         ROUTINE TO LINK TO DISK
         SVC   202                 CALL IT
         DC    AL4(LINKERR)        ADDRESS OF ERROR ROUTINE
         LA    R1,ACCIT            ROUTINE TO ACCESS DISK
         SVC   202                 CALL IT
         DC    AL4(ACCERR)         ADDRESS OF ERROR ROUTINE
         LA    R1,STATEDIR         LETS FIND OUT IF
         SVC   202                 THE DIRECTORY IS THERE ...
         DC    AL4(STATERR)        ADDRESS OF ERROR ROUTINE
         MVC   LOGMSG+24,WANTED    MOVE PARMS TO LOGMSG
         WRTERM LOGMSG,32          LOG USER-ID WE ARE LOOKING FOR
         EJECT
         LA    R3,0                LOAD R3 WITH 0
READDIR  FSREAD 'USER DIRECT *',BUFFER=BUFFER2,BSIZE=80
         CR    R3,R15              RETCODE EQ 0?
         BNE   DROP                NO?   GO EXIT
         CLC   BUFFER2(4),=C'USER' IS THIS A USER RECORD
         BNE   READDIR             NO?  GO READ ANOTHER RECORD
         MVC   BUFFER3,BUFFER2     SAVE THIS RECORD IN BUFFER 3
         CLC   WANTED,USER         IS THIS THE USER-ID WE WANT?
         BE    SHOWIT              YES - THEN DISPLAY SAVED RECORD
         MVI   USER+7,X'40'        TRIM USER FIELD DOWN TO 7 CHARS
         CLC   WANTED,USER         IS THIS THE USER-ID WE WANT?
         BE    SHOWIT              YES - THEN DISPLAY SAVED RECORD
         MVI   USER+6,X'40'        TRIM USER FIELD DOWN TO 6 CHARS
         CLC   WANTED,USER         IS THIS THE USER-ID WE WANT?
         BE    SHOWIT              YES - THEN DISPLAY SAVED RECORD
         MVI   USER+5,X'40'        TRIM USER FIELD DOWN TO 5 CHARS
         CLC   WANTED,USER         IS THIS THE USER-ID WE WANT?
         BE    SHOWIT              YES - THEN DISPLAY SAVED RECORD
         B     READDIR             GO READ ANOTHER RECORD
SHOWIT   WRTERM BUFFER3,72         DISPLAY REC (PEEL OFF SERIALIZATION)
         EJECT
DROP     LA    R1,DROPDASD         ROUTINE TO RELEASE/DETACH DISK
         SVC   202                 CALL IT
         DC    AL4(1)              ADDRESS OF ERROR ROUTINE
         B     RETURN              GO RETURN
* ERROR ROUTINES COMMENCE HERE ... *
ACCERR   MVC   ERRMSG,=CL80'ERROR ACCESSING TARGET DISK'
         WRTERM ERRMSG,80
         LA    R1,DETCUU
         SVC   202
         DC    AL4(1)
         LA    R15,36
         B     RETURN
GETERR   MVC   ERRMSG,=CL80'ERROR EXECUTING GETFMADR MODULE'
         WRTERM ERRMSG,80
         B     RETURN
LINKERR  MVC   ERRMSG,=CL80'ERROR LINKING TO TARGET DISK'
         WRTERM ERRMSG,80
         LA    R15,114
         B     RETURN
PARMERR  MVC   ERRMSG,=CL80'NO USERID SUPPLIED'
         WRTERM ERRMSG,80          DISPLAY ERROR
         LA    R15,24              SET RETURN CODE TO 24
         B     RETURN
STATERR  MVC   ERRMSG,=CL80'DIRECTORY FILE NOT AVAILABLE'
         WRTERM ERRMSG,80
         LA    R1,DROPDASD
         SVC   202
         DC    AL4(1)
         LA    R15,28
         B     RETURN
* PUT PASSWORD INTO DISK LINK ROUTINE *
PUTPWD   EQU   *
         MVI   LINKPWD+5,C'5'      LOAD PASSWORD IN
         MVI   LINKPWD+4,C'M'
         MVI   LINKPWD+3,C'R'
         MVI   LINKPWD+2,C'I'
         MVI   LINKPWD+1,C'D'
         MVI   LINKPWD+0,C'R'
         BR    R12
RETURN   EQU   *                   HEIGH HO ... HEIGH HO ...
         BR    R14                 TO CMS WE GO ...
         EJECT
ERRMSG   DS    CL80
GETFMADR DS    0D
         DC    CL8'GETFMADR'
         DC    8X'FF'
BUFFER1  DS    0CL7                RESULTS OF "GETFMADR"
         DS    CL2
NEWFM    DS    CL1                 FREE CMS FILEMODE
         DS    CL1
NEWCUU   DS    CL3                 FREE DASD ADDRESS
BUFFER2  DS    0CL80               RECORDS READ FROM DIRECTORY
         DS    CL5
USER     DS    CL8                 8 CHARACTER FIELD FOR USER-ID
         DS    CL67
BUFFER3  DS    CL80                SAVE AREA FOR DIRECTORY RECORDS
WANTED   DS    CL8                 SAVE AREA FOR PASSED PARAMETER
LOGMSG   DC    C'SEARCHING DIRECTORY FOR XXXXXXXX'
LINKDASD DS    0D
         DC    CL8'CP'
         DC    CL8'LINK'
         DC    CL8'DIRMAINT'
         DC    CL8'195'
LINKCUU  DC    CL8' '
         DC    CL8'RR'
LINKPWD  DC    CL8' '
         DC    8X'FF'
ACCIT    DS    0D
         DC    CL8'ACCESS'
ACCDASD  DC    CL8'   '
ACCMODE  DC    CL8' '
         DC    8X'FF'
DROPDASD DS    0D
         DC    CL8'RELEASE '
RELMODE  DC    CL8' '
         DC    CL8'('
         DC    CL8'DET'
         DC    8X'FF'
STATEDIR DS    0D
         DC    CL8'STATE'
         DC    CL8'USER'
         DC    CL8'DIRECT'
STATEFM  DC    CL8' '
         DC    8X'FF'
DETCUU   DS    0D
         DC    CL8'DET'
         DC    CL8' '
         DC    8X'FF'
         DC    C'COMPILED '
         DC    C'&SYSTIME '
         DC    C'&SYSDATE '
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END

M A Shears
Shift Leader (UK)                                                  cXephon 1992

