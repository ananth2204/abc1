Date check during IPL

Have your operators ever entered the wrong date during an
IPL?   If so you may be aware of the catastrophic effects this
can have on data.  Back-up catalogs condensed, schedules
corrupted, PROFS files purged, databases corrupted,
accounting corrupted etc.

The following EXECs were developed to prevent this
happening by double checking the date.  The EXEC, although
specific to our environment, may be useful as a skeleton for
other sites.  Some sites may prefer to remove some of the
checks so that the date is not always checked.

The date check package comprises several files.  It consists of
DATECHK EXEC and XEDIT which should reside on
OPERATOR's 191, and AUTODCHK EXEC which should
reside on AUTOLOG1's 191.  The DATECHK EXEC LINKs
to VMUTIL 191, so password/directory link etc should be
reviewed.

DATECHK EXEC will check the date on the first ALL line in
VMUTIL's WAKEUP PARMS (we schedule a dummy event
daily at 00:01).  If equal to today's date, it will confirm the
current time is later than the latest time entry for today in
WAKEUP PARMS.  If later it will then tell AUTOLOG1 (via
SMSG) to proceed with a normal IPL.

If either of these tests fail, the current date, day, month, and
year will be displayed on the OPERATOR's console, and the
operators will be asked to confirm that this date is correct.
They should reply YES to continue normal IPL or ABORT to
SHUTDOWN the system.  There is also an option of NONET
to allow system programmer testing without running
AUTOLOG1 normal start-up.

The AUTODCHK EXEC on AUTOLOG1 will wait until the
date is confirmed as correct before proceeding.

Other points to bear in mind are:

o      AUTOLOG1 uses WAKEUP module

o      The AUTOLOG1, EREP, CA-ACF2/VM, and
      DISKACNT virtual machines are started by the system
      even if operators decide to ABORT

o      Testing on a TEST virtual machine is highly recommended
      before implementation.

The following lines should be added at the start of
AUTOLOG1's PROFILE EXEC

      'CP SP CONS START *'
      'EXEC AUTODCHK'
      if rc¬=0 then 'CP LOGOFF'

The following lines should be added at the start of
OPERATOR's PROFILE EXEC

      'EXEC DATECHK'


DATECHK EXEC

/* Confirm correct date entered if first IPL today                    */
/* Check date on 1st ALL event on VMUTIL WAKEUP PARMS and if matching */
/* ensure time greater than VMUTIL last date entry                    */
/* If date does not match ask Operators to confirm it is correct.     */
trace o
address command
'EXECIO 0 CP (ST LINK VMUTIL 191 555 RR READ'
'SET CMSTYPE HT'
'ACC 555 H'
'SET CMSTYPE RT'
if rc¬=0 then do
  say 'PLEASE INFORM SYSTEMS PROGRAMMER - LINK TO VMUTIL in DATECHK',
      'EXEC has failed'
      signal confirm
end
'CLRSCRN'
push datechk
'XEDIT WAKEUP PARMS H'
if rc='0' then do
            do forever                 /* ENSURE AUTOLOG1 LOGGED ON */
            'EXECIO 0 CP (ST Q AUTOLOG1'
            if rc=0 then leave
            'CP SL 2 SEC'
            end
   'CP SMSG AUTOLOG1 OK'
   exit 0
end
confirm:
do forever
   say 'IPL DATE is' date(E) date(W) left(date(E),2) date(M)
   say ''
   say 'You MUST confirm this date is correct before the IPL will',
       'continue.'
   say 'If correct enter YES to proceed with normal IPL'
   say 'If incorrect enter ABORT to shutdown/re-IPL'
   pull reply
   if reply='YES'&&reply='ABORT'&&reply='NONET' then leave
   say 'Reply MUST be "YES" or "ABORT".'
   say ''
end
if reply='ABORT' then 'CP SHUTDOWN'
if reply='NONET' then do
        'CP SMSG AUTOLOG1 NONET'
        exit 55
end
'CP SMSG AUTOLOG1 OK'
'REL H'
'EXECIO 0 CP (ST DET 555'


DATECHK XEDIT

/* Confirm correct date entered if first IPL today                    */
/* Check date on 1st ALL event on VMUTIL WAKEUP PARMS and if matching */
/* ensure time greater than VMUTIL last date entry                    */
trace o
address xedit
top
find all
stack
pull . tim dat .
if dat¬=date(U) then signal confirm    /* DATES MATCH? */
bo
'-$' dat'$'
stack
pull . tim .
if tim < time() then do            /* VMUTIL TIME < current time? */
                push qquit 0
                exit
end
confirm:
push qquit 999
exit


AUTODCHK EXEC

/* Ensure correct date on IPL before running PROFILE  */
address command
trace o
'STATE WAKEUP MODULE *'
if rc¬=0 then do
         say 'WAKEUP MODULE not found - Inform SYS PROGS'
         exit 999
end
do forever
 'WAKEUP (CONS SMSG'        /* WAIT FOR '*SMSG OPERATOR: OK' */
if rc=6 then leave
if rc=1 then do
pull . . ok .
if ok='NONET' then do
   say 'NONET response from DATECHK - SYS PROGS testing.'
   exit 999
end
    if ok='OK' then do
               'CP SET SMSG OFF'
               exit 0
               end
               else do
               say 'Invalid message from DATECHK - Inform SYS PROGS'
            'CP MSG OP Invalid message from DATECHK - Inform SYS PROGS'
            /* Do not allow to continue */
               leave
               end
    end
end
exit 999        /* Invalid Exit - do not continue AUTOLOG */

Harry George
Systems Programmer
ICI (UK)

