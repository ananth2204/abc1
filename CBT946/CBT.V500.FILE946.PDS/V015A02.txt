 Date-stamping source code changes

This macro should prove useful to any shop that is maintaining its production
source code under VM via XEDIT.

The macro is called 'DTESTAMP' and its function is to insert a concatenation of
current Julian date and the first three characters of VM user-id into columns
73-80 of all records that were added or modified in the users XEDIT session.
The macro is currently running on a VM/SP Release 4 system.

Currently, the macro will only date-stamp CMS files that are a filetype of
COBOL, ASSEMBLE, or COPYBOOK.  It can be easily modified to do other filetypes
if desired.  DTESTAMP also checks the filename prefix, and will not modify any
code beginning with the prefixes DMK, DMS, DMT, or DMM.  It can be easily
modified to bypass other VM system or program product prefixes also.

To view the date-stamp in subsequent XEDIT sessions the user must issue the
following commands:

    SET TRUNC 80
    SET VERIFY 1 80

either manually from the XEDIT command line, or in his CMS file PROFILE XEDIT
A1.

The macro is implemented by inserting one line into the CMS file PROFILE XEDIT
A1, for any users that have the authority to modify production source code: EXEC
PGMRPROF.

The PGMRPROF EXEC does nothing more than set up XEDIT synonyms for the FILE,
FFILE, SAVE and SSAVE commands that cause them to invoke the DTESTAMP XEDIT
macro, prior to doing the FILE or the SAVE.

The PGMRPROF EXEC and the DTESTAMP XEDIT macro reside on a mini-disk that all
users have an R/O link to.  This mini-disk is the one our shop uses to contain
all common/shared EXECs, XEDIT macros, CMS files, etc.  This mini-disk is linked
to either in the user's VM DIRECTORY entry, or as one of the first commands in
his PROFILE EXEC, and accessed as one of the first commands in his PROFILE EXEC.

By using this approach, we can isolate all 'common' XEDIT options and settings
for our applications development department to one EXEC on one mini-disk,
thereby reducing the amount of work that would be required to implement 'global'
options/changes to each individual profile.


DTESTAMP XEDIT

/*--    D T E S T A M P   R E X X E D I T   M A C R O   ----*/
/*----                                              --------*/
/*-----   THIS MACRO IS INVOKED FROM AN XEDIT     ----------*/
/*-----   SESSION VIA SYNONYMS SET UP FOR THE     ----------*/
/*-----   'FILE', 'FFILE', 'SAVE', AND 'SSAVE'    ----------*/
/*-----   COMMANDS. THE PURPOSE OF THIS MACRO     ----------*/
/*-----   IS TO INSERT A DATE-STAMP IN COLS.       ----------*/
/*-----   73-80, FOR ANY RECORDS MODIFIED DURING    --------*/
/*-----   THIS XEDIT SESSION.                     ----------*/
/*-----                                           ----------*/
/*-----   FORMAT OF DATE-STAMP IS:                 ----------*/
/*-----                                           ----------*/
/*-----   YYDDDPGR                                ----------*/
/*-----     |   |                                 ----------*/
/*-----     |   |   1ST 3 CHARS OF VM USERID      ----------*/
/*-----     |   |-- THAT MADE THE CHANGE(S).      ----------*/
/*-----     |                                     ----------*/
/*-----     |------ TODAYS JULIAN DATE.           ----------*/
/*-----                                           ----------*/
 /*-----    MACRO WILL ONLY DATE-STAMP FILES WITH A   --------*/
/*-----    FILETYPE OF 'COBOL' OR 'ASSEMBLE' OR      -------*/
/*-----    'COPYBOOK'. THIS DATE-STAMP IS HELPFUL   ---------*/
/*-----    IN PROVIDING AN AUDIT TRAIL OF CHANGES   --------*/
/*-----    TO SOURCE CODE MAINTAINED UNDER CMS.   ----------*/
/*-----                                           ----------*/
/*-----    TO VIEW THIS DATE-STAMP IN SUBSEQUENT   ----------*/
/*-----    XEDIT SESSIONS, USER MUST ISSUE:       ----------*/
/*-----      SET TRUNC 80                         ----------*/
/*-----      SET VERIFY 1 80                      ----------*/
/*-----    EITHER MANUALLY OR IN 'PROFILE XEDIT'.  ---------*/
/*-----                                           ----------*/
/*-----    MACRO   W I L L   N O T   STAMP ANY    ----------*/
/*-----    FILES WITH AN FNAME PREFIX THAT BEGINS   --------*/
/*-----    WITH 'DMK' OR 'DMS' OR 'DMT' OR 'DMM'.   --------*/
/*-----                                           ----------*/
/*  TRACE R  */
ADDRESS COMMAND "SET CMSTYPE HT"
/*   EXTRACT FNAME FTYPE FMODE OF CURRENT FILE.  */
/*   THIS WILL BE DEFAULT FN FT FM.              */
/*   EXTRACT CURRENT LINE NUMBER TO RETURN TO    */
/*   AFTER A 'SAVE' OR 'SSAVE'. (LINE_PTR)       */
COMMAND EXTRACT "/FNAME/FTYPE/FMODE"
COMMAND EXTRACT "/LINE"
LINE_PTR = ":" || LINE.1
/*   PULL LAST COMMAND ENTERED ON XEDIT CMDLINE. */
/*   PULL ANY CHANGE TO DEFAULT FN FT FM         */
/*   THAT THE USER MAY HAVE KEYED.               */
PARSE SOURCE  . . . . . IPCMD .
PARSE ARG FNA FTY FMO .
/*   TRANSLATE THE IPCMD THE USER KEYED INTO THE         */
/*   ACTUAL XEDIT COMMAND, SINCE WE MUST SET             */
/*   SYNONYM OFF TEMPORARILY IN ORDER TO EXECUTE IT.     */
/*   ('SSAVE' & 'FFILE' ARE XEDIT SYNONYMS THEMSELVES)   */
SELECT
  WHEN IPCMD = "FILE"          THEN   IPCMD = "COMMAND PFILE"
  WHEN IPCMD = "SAVE"          THEN   IPCMD = "COMMAND PSAVE"
  WHEN LEFT(IPCMD,2) = "FF"    THEN   IPCMD = "COMMAND FILE"
  WHEN LEFT(IPCMD,2) = "SS"    THEN   IPCMD = "COMMAND SAVE"
END
/*   ASSIGN FNAME FTYPE FMODE WE WILL FILE THIS ONE AS.  */
IF FNA = ""  |  FNA = "="
    THEN
        FNA = FNAME.1
IF FTY = ""  |  FTY = "="
    THEN
        FTY = FTYPE.1
IF FMO = ""  |  FMO = "="
    THEN
        FMO = FMODE.1
/*   LETS NOT MODIFY ANY IBM SYSTEM SOURCE CODE.    */
/*   SAVE IT AND GET OUT.                           */
IF LEFT(FNA,3) = "DMK"  |  LEFT(FNA,3) = "DMS"
    THEN
        DO
          SET SYNONYM OFF
          ADDRESS COMMAND "SET CMSTYPE RT"
          IPCMD FNA FTY FMO
          SET SYNONYM ON
          EXIT
        END
IF LEFT(FNA,3) = "DMT"  |  LEFT(FNA,3) = "DMM"
    THEN
        DO
          SET SYNONYM OFF
          ADDRESS COMMAND "SET CMSTYPE RT"
           IPCMD FNA FTY FMO
          SET SYNONYM ON
          EXIT
        END
/*   DO NOT DATE-STAMP IF IT'S NOT A FILETYPE      */
/*   OF 'COBOL' OR 'ASSEMBLE' OR 'COPYBOOK'.       */
/*   SAVE IT AND GET OUT.                          */
IF FTY ¬= "COBOL"
    THEN
        IF FTY ¬= "ASSEMBLE"
            THEN
                IF FTY ¬= "COPYBOOK"
                    THEN
                        DO
                          SET SYNONYM OFF
                          ADDRESS COMMAND "SET CMSTYPE RT"
                          IPCMD FNA FTY FMO
                          SET SYNONYM ON
                          EXIT
                        END
/*   LOOKS GOOD, LETS CHECK FOR NEW/CHANGED LINES  */
/*   AND INSERT DATE-STAMP(S).                     */
/*                                                 */
/*   EXTRACT THE SIZE OF THE FILE BEING EDITED.    */
/*   THIS VARIABLE WILL CONTROL OUR "DO" LOOP.     */
/*   (SIZE.1)                                      */
COMMAND EXTRACT "/SIZE"
COMMAND PRESERVE
SET LRECL 80
SET RECFM F
SET TRUNC 80
SET ZONE 1 80
SET VERIFY ON 1 80
SET SPILL OFF
SET SERIAL OFF
/*  START FROM TOP OF FILE.    */
/*  SET COLUMN POINTER.        */
COL = ":73"
TOP
CLOCATE COL
STAMP_EM:
DO SIZE.1
   NEXT
   COMMAND EXTRACT "/CURLINE"
   IF CURLINE.4 = "ON"
       THEN
           COVERLAY DATE(J) || LEFT(USERID(),3)
END
/*   ALL DONE.                                   */
/*   FILE OR SAVE OUR DATE-STAMPED SOURCE,       */
/*   RESTORE USERS XEDIT SETTINGS, AND EXIT.     */
COL = ":1"
CLOCATE COL
ADDRESS XEDIT LINE_PTR
ADDRESS COMMAND "SET CMSTYPE RT"
SET SYNONYM OFF
IPCMD FNA FTY FMO
SET SYNONYM ON
COMMAND RESTORE
EXIT


PROFILE XEDIT

EXEC PGMRPROF
SET PREFIX ON LEFT
 SET AUTOSAVE 10
SET CASE upper IGNORE
SET FULLREAD ON
SET CMDLINE TOP
SET CURLINE ON 4
SET ENTER IGNORE
SET MSGLINE ON 3 OVERLAY
SET NULLS ON
SET NUM ON
SET PF1 SET SHADOW OFF
SET PF5 ?
SET PF6 =
SET PF2 CMS RAPS D RDR
SET PF9 TOP
SET PF12 CURSOR CMDLINE
SET PF13 CMS VSECMD VSEIPO
SET PF14 CMS VSEMSG VSEIPO LAST
SET PF15 CMS VSECMD VSETST
SET PF16 CMS VSEMSG VSETST LAST
SET PF17 RAPS LINK DOS VSETST D RDR
SET PF19 -1
SET PF20 1
SET PF22 SCHANGE 21
SET SCALE ON 3
SET STAY ON
SET TABLINE OFF
*
SET LINEND OFF
EXTRACT /LSCREEN/
&BOTTON_CMD = &LSCREEN.1
&BOTTOM_CMD = &BOTTOM_CMD - 5
&BOTTOM_CMD = &CONCAT OF - &BOTTOM_CMD
SET PF10 BOT#&BOTTOM_CMD
SET LINEND ON #


PGMRPROF EXEC

/*--   P R O F I L E   X E D I T                     ----*/
/*--   R E X X   E X E C                             ----*/
/*---                                                ----*/
/*--   ALL COMMON XEDIT OPTIONS FOR PROGRAMMERS      ----*/
/*--   XEDIT SESSIONS.                               ----*/
/*---                                                ----*/
ADDRESS XEDIT
SET SYNONYM ON
/* DATESTAMP MACRO FOR COBOL AND ASSEMBLE */
SET SYNONYM SAVE 4 DTESTAMP
SET SYNONYM SSAVE 2 DTESTAMP
SET SYNONYM FILE 4 DTESTAMP
SET SYNONYM FFILE 2 DTESTAMP
/* GET AND PUT PREFIX MACROS */
SET PREFIX SYNONYM G  PREFGET
SET PREFIX SYNONYM P  PREFPUT
SET PREFIX SYNONYM PP PREFPUT
EXIT

Scott D Northcote
Technical Support Programmer
Cascade Corporation (USA)     ) Cascade Corporation 1987






