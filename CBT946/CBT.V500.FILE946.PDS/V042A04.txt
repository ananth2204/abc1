Adding synonyms

In contrast with other operating systems, CMS does not permit
the use of more than a single synonym table at a time.  I have
found two methods of circumventing this limitation.  The first,
which I shall only mention here, is accomplished in a shared,
argument-sensitive, PROFILE EXEC adjunct which requires
adherence to a simple naming convention for personal synonym
files: (user-id) SYNONYM.  Other, departmental, synonym
files may be requested selectively and, once chosen, all files
are appended to one another in a dummy file and the synonym
command is then invoked against this file.

A second method permits the assignment, on the fly so to
speak, of a new synonym which will last for the duration of the
current log-on only.  The method employed is relatively
straightforward.  The current USER synonyms are loaded into
the program stack - along with the newly requested one.  They
are then read into a dummy file and the synonym command is
invoked against this dummy file.  It may be invoked
repeatedly.

ADDSYN EXEC

/*        Procedure to Add a synonym to those you currently have      */
/*           in effect for the duration of current logon only         */
/*    The ARGUMENT(s) may be passed in any of the following forms:    */
/*    (1)           (2)            (3)                                */
/*  COMMAND       SYNONYM        SHORTEST ABBREVIATION                */
/*  COMMAND       SYNONYM        (synonym considered shortest abbrev) */
/*                                 - or -                             */
/*  COMMAND       SYN*ONYM                                            */
/*  COM*MAND                                                          */
/*     (where letters LEFT of the * indicate the Shortest ABBREV.)    */

arg newreal newsyn extra
if newsyn = '' then
 do
   if newreal = '' then call helper
   star = pos('*',newreal)
   if star > 1 then
    do
      extra = star -1
      newsyn = substr(newreal,1,star-1)||substr(newreal,star+1)
      newreal = newsyn
    end
   else call helper
 end
else
 do
   if extra = '' then extra = length(strip(newsyn))
   star = pos(*',newsyn)
   if star > 1 then
    do
      extra = star -1
      newsyn = substr(newsyn,1,star-1)||substr(newsyn,star+1)
    end
   if datatype(extra,n) ¬=1 then extra = length(strip(extra))
 end
'q synonym user (STACK'
if queued() < 4 then
 do
   'dropbuf'
   y = 1
 end
else
 do
   do 3; pull junk; end
   do y = 1 to queued()
     parse pull real synm short
     if short ¬= '' then syn.y = real synm length (strip(short))
     else syn.y = real synm length(strip(synm))
   end
 end
syn.y = newreal newsyn extra
do z = 1 to y
 'execio 1 diskw $TMPSYN$ SYNONYM A3 'z' F (STRING 'syn.z
end
'finis $TMPSYN$ SYNONYM A3'
'synonym $TMPSYN$'
exit

helper:
'clrscrn'
'execio 1 cp (STRING Q SCREEN'
pull . . . . clr hlght
'execio 0 cp (STRING SCREEN VMOUT RED NONE'
say
 say substr(sourceline(1),3,68)
say
'execio 0 cp (STRING SCREEN VMOUT YELLOW NONE'
do y = 2 to 100
 if pos('Rick Frye',sourceline(y)) > 0 then leave
 say substr(sourceline(y),3,68)
end
'execio 0 cp (STRING SCREEN VMOUT 'clr hlght
exit 5

Rick Frye
Hardboard Division
Masonite Corporation (USA) ) Rick Frye 1990


