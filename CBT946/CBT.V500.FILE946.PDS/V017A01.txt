 EXEC2 tool for Assembler programmers
This EXEC2 program simplifies the coding and testing of Assembler programs.  It
edits an Assembler file (or builds a skeleton if there is none), loads, genmods
and executes it.
ALGO EXEC
&TRACE
&IF &N NE 0 &IF &1 NE ? &GOTO -GO
    &TYPE FORMAT&BLANK : &FILENAME FN <<<SYSTEM> <GO> <OPTONS>>
    &TYPE FUNCTION: XEDIT, ASSEMBLE, LOAD AND GENMOD OF FILE FN ASSEMBLE
    &TYPE NOTES &BLANK : IF FN ASSEMBLE DOES NOT EXIST, &FILENAME
&BEGTYPE -END
          WILL EDIT A SKELETON OF AN ASSEMBLER PROGRAM.
          IF YOU WISH TO SAVE THE FILE AND NO STEPS AFTER XEDIT, USE
          SAVE AND QUIT N (ANY DECIMAL NUMBER WILL DO). QQ N PROVIDES
          A FAST EXIT FROM XEDIT (WITH NO CHANGES SAVED)
OPTIONS : SYSTEM GENERATES A MODULE WITH A STORAGE PROTECT KEY OF ZERO
          GO EXECUTES THE MODULE
          OPTIONS ARE THE CMS ASSEMBLER OPTIONS (DEFAULT IS NOLIST)
-END
&EXIT
-GO
USERID
&READ VARS * &USERID
&FN = &1
STATE &FN ASSEMBLE *
&IF &RC = 0 &GOTO -XEDIT
* NEW FILE, BUILD SKELETON
&DT     = &LITERAL OF &SYSDATE &SYSTIME
&8B     = &LEFT OF &BLANK 8
&2B     = &LEFT OF &BLANK 2
&FN1    = &LEFT OF &FN 8
&STACK  COMMAND ADD 9
&STACK  COMMAND INPUT &FN1 CSECT
&BEGSTACK -END
 COMMAND INPUT          USING *,R12                ESTABLISH ADDRESSABILITY
 COMMAND INPUT          STM   R0,R14,8(R13)        SAVE REGS
 COMMAND INPUT          B     START                JUMP EYECATCHER
-END
&STACK  COMMAND INPUT &8B DC &2B C'&USERID ON &DT '
&BEGSTACK -END
 COMMAND INPUT START    DS    0F                   ALIGN FOR SPEED
 COMMAND BOTTOM
 COMMAND INPUT          LM    R0,R14,8(R13)        RESTORE REGS
 COMMAND INPUT          XR    R15,R15              ZERO RC
 COMMAND INPUT          BR    R14                  BACK TO CMS
 COMMAND INPUT          PRINT NOGEN
 COMMAND INPUT          REGEQU                     REGISTER EQUATES
-END
&STACK  COMMAND INPUT &8B END &BLANK &FN1
&STACK  COMMAND TOP
&STACK  COMMAND CURSOR FILE 7 10
-XEDIT &ERROR &EXIT &RC
       XEDIT &FN ASSEMBLE
STATE &FN ASSEMBLE *
&TYPE
&LOC        = 1 + &LOCATION OF ( &ARGSTRING
&IF &LOC    = 1 &GOTO -NEXT
* PARSE THE OPTIONS
    &SUBST = &PIECE OF &ARGSTRING &LOC *
    &STACK &SUBST
    &READ ARGS
    &K = 1
    &LOOP -ELOOP &N
           &POS = &POSITION OF &&K SYSTEM GO
           &IF &POS = 0 &OPTION = &STRING OF &OPTION &&K
           &IF &&K = NOOBJECT &NOBJ = YES
            &IF &POS = 1 &GENOPT = (&&K
           &IF &POS = 2 &GO = YES
    -ELOOP &K = &K + 1
-NEXT &ERROR
      &IF .&OPTION = . &OPTION = NOLIST
* TEST FOR ORIGINS OTHER THAN DEFAULT X'20000'
      EXECIO * DISKR &FN ASSEMBLE * (FINIS LOCATE /START X'/
      &IF &RC NE 0 &GOTO -ASM
          &READ VARS
          &READ VARS * * &ORG
          &ORG = &TRANS OF &ORG X'
          &STACK LIFO &ORG
          &READ VAR &ORG
          &IF .&ORG = .00E000 &ORG = TRANS
          &IF .&ORG NE .      &ORG = &STRING OF ORIGIN &ORG
-ASM  &ERROR &EXIT &RC
      ASSEMBLE &FN ( &OPTION
* IF NOOBJECT WAS OPTED, LEAVE
      &IF .NOBJ = .YES &EXIT
      LOAD &FN (NOMAP &ORG
      GENMOD &FN &GENOPT
* EXECUTE THE MODULE IF REQUESTED
      &IF .&GO NE . &FN
Daniel Arany
Technical Support Manager
Texaco (Brazil)     ) Daniel Arany 1988








































