A spreadsheet sum function in XEDIT

Typically, I write my reports in XEDIT, and sometimes they
contain tables with numeric values in one or more columns.
To summarize the numbers I usually use a pocket calculator
and rekey the numbers in the calculator.  To prevent a typing
error I redo the sum a second and sometimes a third time.
Then I wrote this small REXX program, which will take my
numbers from the edited file and give me a sum in the message
line.  I found it an easier and safer way to summarize the
numbers, and I type them only once.


SUM XEDIT

/*    Summarize specified numeric column in a edited file          */
TRACE Off
SIGNAL ON  SYNTAX
/* Initial-Routine                                                 */
Arg fcol tcol flin tlin .
"COMMAND PRESERVE"
/* Main-Routine                                                    */
MAIN:
Select
 when fcol = "?" | fcol = "" then
  "COMMAND CMSG SUM from-col to-col from-line to-line"
 when datatype(fcol) ¬= "NUM" then do
  "COMMAND CMSG SUM" fcol tcol flin tlin
  "COMMAND MSG From column must be numeric"
  end
 when datatype(tcol) ¬= "NUM" then do
  "COMMAND CMSG SUM" fcol tcol flin tlin
  "COMMAND MSG To column must be numeric"
  end
 when flin ¬= "" & datatype(flin) ¬= "NUM" then do
  "COMMAND CMSG SUM" fcol tcol flin tlin
  "COMMAND MSG From line must be numeric"
  end
 when tlin ¬= "" & tlin ¬= "*" & datatype(tlin) ¬= "NUM" then do
  "COMMAND CMSG SUM" fcol tcol flin tlin
  "COMMAND MSG To line must be numeric"
  end
 otherwise
  "COMMAND EXTRACT /LINE"
  if flin = "" then flin = line.1      /* Default from current line */
  if tlin = "" then tlin = "*"         /* Default to end of file    */
  sum = 0
  "COMMAND EXTRACT /RANGE"
  "COMMAND SET RANGE :"flin ":"tlin
  "COMMAND TOP"
  do forever
    "COMMAND NEXT 1"
    if rc ¬= 0 then do
      message = "Total =" sum
      leave
      end
    "COMMAND EXTRACT /CURLINE"
    col_val = word(substr(curline.3,fcol,tcol),1)
    if datatype(col_val) = "NUM" then
      sum = sum + col_val
     else do
      message = "Non-numeric data in column"
      leave
      end
    end
  "COMMAND SET RANGE :"range.1 ":"range.2
  "COMMAND :"line.1
  "COMMAND MSG" message
 end
/* End-of-Routine                                                     */
END_RETRN:
"COMMAND RESTORE"
EXIT
/* Error-Exits                                                        */
/*  Standard handler for SIGNAL ON SYNTAX                             */
SYNTAX:
 $error='REXX error' rc 'in line' sigl':' errortext(rc)
 say $error
 say sourceline(sigl)
 trace '?r'; nop             /* SUM XEDIT */
/******************end of program**************************************/
EXIT

Willi Kuhn
Freelance Systems Programmer (Germany)

