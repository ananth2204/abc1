Miscellaneous VM commands for operators

Because our site is primarily an MVS shop, the operations staff
are more accustomed to using the MVS flavours of commands
on that SCP. Therefore, I've tried to make it easier for them to
use the VM commands that they would use the most - namely
the DISPLAY and VARY commands.

The routines that follow only take into account the commands
that the operations staff may want to issue most often. In the
case of the DISPLAY command, we got into trouble
occasionally by an operator who would enter a DISPLAY ALL
command or something similar, and end up getting screen after
screen of displays of their own virtual storage. Not a pretty
sight! This routine was written to intercept the DISPLAY
command (which they would normally use by issuing the D
command on MVS) and issuing the proper VM equivalent.

As it's written now, the D EXEC supports the following
commands:

D T      Displays time used and time of day

D M      Displays everything about the environment

D M=CPU      Displays processors and INDICATE LOAD

D M=CHP(__)       Displays a CHPID and devices on that CHPID

D M=DEV(____)      Displays a device and CHPIDs to that device

D NET,...       Issues command to VTAM via SCIF.


D EXEC

Arg cmd;
cmd = translate(cmd);
if substr(cmd,1,3) = 'NET' then do;
   'SEND VTAM VTAM D' cmd;
   exit 0
end;
else if cmd = 'T' then do;
   'Q T';
   exit 0;
end;
else if cmd = 'M' then do;
   'Q ALL';
   'Q PROC';
   'Q T';
   exit 0;
end;
else if cmd = 'M=CPU' then do;
   'Q PROC';
   'IND';
   exit 0;
end;
else if substr(cmd,1,5) = 'M=CHP' then do;
   Parse value cmd with . '(' chpid ')' .;
   'Q CHPID' chpid;
   exit 0;
end;
else if substr(cmd,1,5) = 'M=DEV' then do;
   Parse value cmd with . '(' dev ')' .;
   'Q' dev;
   'Q PATHS TO' dev;
   exit 0;
end;
else do;
   say 'Invalid form of DISPLAY command. Consult CP COMMAND guide.';
   exit 4;
end;
exit 0

The second piece of code issues a VARY command either to
CP or VTAM, whichever is appropriate based on whether the
word NET is the second parameter entered on the command
line.

Again, what I was trying to accomplish was to make the syntax
that they would use on the MVS system available on the VM
system as well.

The V EXEC currently supports the following:

V NET,...      VTAM VARY command

V device,ONLINE      CP VARY ONLINE

V device,OFFLINE      CP VARY OFFLINE

V PATH(___,__),ONLINE      CP VARY PATH ONLINE

V PATH(___,__),OFFLINE      CP VARY PATH OFFLINE

Obviously, these routines may be modified to run with
whatever products use the DISPLAY or VARY commands as
part of their syntax, or enhanced to issue more than we
primarily use at our site.

Please note that the V EXEC and D EXEC may need to be
modified to run on different versions of VM. At our site, we
run VM/XA SP 2.1.


V EXEC

Arg cmd;
cmd = translate(cmd);
if substr(cmd,1,3) = 'NET' then do;
   'SEND VTAM VTAM V' cmd;
end;
else if substr(cmd,1,4) = 'PATH' then do;
   Parse value cmd with . '(' devices ',' path '),' status;
   if substr(status,1,2) = 'OF' then,
     'VARY' status 'PATH' path 'FROM' devices;
   else if substr(status,1,2) = 'ON' then,
     'VARY' status 'PATH' path 'TO' devices;
end;
else do;
   Parse value cmd with devices ',' status;
   'VARY' status devices;
end;
exit rc;

Patrick Tierce
Systems Software Specialist
State of Kansas (USA)

