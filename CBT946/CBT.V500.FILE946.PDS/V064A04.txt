Service machine print utility

In our installation the users are situated at one site while a laser
printer (3800 with AFP), connected to another system, is
located at a different site.  Although all the users have printers
in their own buildings, they still want, and need, to use the
laser printer for clarity, use of different fonts, etc.  We have a
service machine which formats the reports that the user wants
to print, adds a classification at the beginning of each page,
and prints a destination separator at the beginning of the
printout (for operator use).  The service machine receives a file
from a user, does the appropriate editing, and sends the file to
the laser printer using RSCS.

My problem was that the user could not see the status of the
report (with SMSG RSCS Q file), since the origin-id for the
file was the name of the service machine, and therefore the
user could not do any manipulation on the file (purge, hold,
etc).  As a solution, I wrote the following short utility, which
should be used by every worker service machine.

To assemble the program:

      global maclib dmssp cmslib osmacro
      assemble auth
      load auth
      genmod auth

To use as a command:

      auth userid <alternate user-id>

To use in REXX by a worker machine:

      auth userid() <alternate user-id>

In the alternate user-id you should have the variable with the
sending user-id.

* This program, which specifies an alternate USER-ID, is mostly used  *
* by WORKER SERVICE MACHINES.                                         *
* This program requires IBM CLASS B AUTHORIZATION.                    *
* "CP uses the ALTERNATE USER-ID in the following ways:               *
*    1) placed in the "IPVMID" field of the APPC/VM connection        *
*       pending interrupt data when the "worker" issues an APPC/VM    *
*       CONNECT.                                                      *
*    2) Used as the spool file ORIGIN ID for spool files created by   *
*       the "worker"."                                                *
* Before assembly you must enter :                                    *
*     GLOBAL MACLIB OSMACRO CMSLIB DMSSP                              *
* RC=XX is written in the error message, because this program is      *
* usually invoked from an EXEC which will cause CMS to give a zero    *
* RC, if you don't keep the RC from the command.  Therefore, if you   *
* spool the console and get an error message, you will obtain the     *
* return code right away.                                             *
* Use of registers:                                                   *
*     R4 : Return code from DIAGNOSE 'D4'.                            *
*     R5 : Address of diagnose parameter list.                        *
*     R8 : Address of correct error message.                          *
*     R9 : Address of input parameter list.                           *
*     R12: Base address of program.                                   *
         TITLE 'Authorize a user with ALT USERID'
AUTH     CSECT
         SAVE  (14,12)                   HOUSEKEEPING
         LR    R12,R15                   GET BASE ADDRESS
         USING AUTH,R12                  USE IT
         LA  R9,0(R1)                    LOAD ADDR. OF BEGINNING OF
*                                        PARAMETER LIST
         MVC PARMLIST,8(R9)              MOVE PARAMETERS
*                                        -FIRST IS PROGRAM NAME
*                                        WHICH WE DON'T NEED
*                                        -SECOND IS USERID TO BE AUTH'D
*                                        -THIRD IS ALT USERID
         CLC USERID,=8X'FF'              IS FENCE IN SECOND PARAMETER
         BE  SYNERR                      YES! THEN GIVE CORRECT SYNTAX
         CLC ALTUSER,=8X'FF'             CHECK FENCE IN THIRD PARAMETER
*                                        MEANING CLEAR ALT USERID OF
*                                        THE GIVEN USERID
         BNE NCLRALT                     NO ! THEN CHECK IF FENCE IS IN
*                                        THE FOURTH PARAMETER
         XC    ALTUSER,ALTUSER           CLEAR ATL USERID FIELD
         B     GOAHEAD                   GOAHEAD
NCLRALT  CLC 24(8,R9),=8X'FF'            CHECK IF FENCE IS IN THE
*                                        FOURTH PARAMETER
*                                        MEANING JUST THE RIGHT AMOUNT
*                                        OF PARAMETERS
         BE    GOAHEAD                   YES ! THEN PROCEED
SYNERR   WRTERM SYNTAX,64,EDIT=NO,COLOR=R WRITE SYNTAX MSG
         LA    R15,99                    SET RC = 99
         B     BYE                       BYE !
GOAHEAD  XR    R4,R4                     ZERO R4
         LA    R5,PARMLIST               LOAD ADDR. PARAMETER LIST
         DIAG  R4,R5,X'00D4'             DO IT
         LR    R5,R4                     SAVE RC
         SLA   R4,4                      * 16 TO GET CORRECT MSG
*                                        THAT IS WHY THEY ARE CL64
         LA    R8,BEGERR(R4)             GET ADDR OF CORRECT MSG
         WRTERM (R8),64,EDIT=NO,COLOR=R  WRITE ERROR MSG
         LINEDIT TEXT='User"s name is ........',                       C
               SUB=(CHARA,(PARMLIST,8))
         LINEDIT TEXT='Alt"s  name is set to = .......',               C
               SUB=(CHARA,(PARMLIST+8,8))
         LR    R15,R5                    RETURN RC
BYE      RETURN (14,12),RC=(15)          BYE
PARMLIST DS    0CL16
USERID   DC    CL8' '
ALTUSER  DC    CL8' '
BEGERR   EQU   *
ERROR0   DC CL64'RC=0, AUTH COMMAND COMPLETED SUCCESSFULLY'
ERROR4   DC CL64'RC=4, THERE WAS AN I/O ERROR WHILE PAGING IN PARMLIST'
ERROR8   DC CL64'RC=8, THE VMBLOK OF THE WORKER MACHINE WASN"T FOUND'
ERROR12  DC CL64'RC=12, NO RACF AUTHORIZATION'
SYNTAX   DC CL64'RC=99, SYNTAX IS : AUTH USERID <alt userid>'
         REGEQU
         END

M S Finkelstein
Head of VM System Team
Israeli Air Force (Israel)
