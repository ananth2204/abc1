Searching for mini-disks

The following program can be used to find the exact size and
position of mini-disks on a full pack DASD after deleting your
DIRECT file or having a very old back-up in DDR format, for
example.

It will scan, looking for labels 'CMS1' or 'VOL1', and create a
spool file with the information about the size and position of the
mini-disks in it.  The format is shown in Figure 1.

There are two significant points:

%   It requires DASD on virtual address 100.

%   It could take quite a long time to get the result on a shared
pack (if you 'link' it, not 'attach' it).


*  Description : Scan a full pack DASD for CMS minidisks              *
*  Requires    : DASD with virtual address of '100'                   *
*          only for CKD devices - 3330,3340,3350,3375,3380            *
*   Register usage :                                                  *
*   R1 - Not used         R5 - CYL range          R9  - Return code   *
*   R2 - Address of DASD  R6 - Work register      R10 - Not used      *
*   R3 - Address of CCW   R7 - Work register      R11 - Not used      *
*   R4 - Number CYL       R8 - Branch in program  R12 - Base register *
MAPDASD  CSECT
         PRINT NOGEN
         USING *,R12                ESTABLISH ADDRESSABILITY
         ST    R14,SAVEA            STORE RETURN ADDRESS
         LR    R12,R15              ESTABLISH ADDRESSABILITY
         LA    R15,0                DEFAULT RETURN CODE IS 0
         LH    R2,CUU               DASD VIRTUAL ADDRESS
         DIAG  R2,R3,X'0024'        CHECK FOR DEVICE TYPE
         BNZ   ERROR1               IF DEVICE ERROR
         ST    R3,BINPUT            STORE CHARACTERISTICS
         CLI   BINPUT,X'04'         IS THE UNIT DASD ?
         BNE   ERROR2               NO.. GO WRITE ERROR MESSAGE
         CLI   BINPUT+1,X'10'       DEVICE TYPE 3330 ?
         BNE   CHECK1               NO.. OTHER CHECKS
         LH    R5,TYP3330           NUMBER OF CYLINDERS FOR 3330
         MVC   LINE1+47(4),DEVT30   GET TEXT FOR DASD TYPE
         B     TITLE                GO WORK
CHECK1   CLI   BINPUT+1,X'01'       DEVICE TYPE 3340 ?
         BNE   CHECK2               NO.. OTHER CHECKS
         LH    R5,TYP3340           NUMBER OF CYLINDERS FOR 3340
         MVC   LINE1+47(4),DEVT40   GET TEXT FOR DASD TYPE
         B     TITLE                GO WORK
CHECK2   CLI   BINPUT+1,X'08'       DEVICE TYPE 3350 ?
         BNE   CHECK3               NO.. OTHER CHECKS
         LH    R5,TYP3350           NUMBER OF CYLINDERS FOR 3350
         MVC   LINE1+47(4),DEVT50   GET TEXT FOR DASD TYPE
         B     TITLE                GO WORK
CHECK3   CLI   BINPUT+1,X'04'       DEVICE TYPE 3375 ?
         BNE   CHECK4               NO.. ASSUME DASD 3380
         LH    R5,TYP3375           NUMBER OF CYLINDERS FOR 3375
         MVC   LINE1+47(4),DEVT75   GET TEXT FOR DASD TYPE
         B     TITLE                GO WORK
CHECK4   LH    R5,TYP3380           NUMBER OF CYLINDERS FOR 3380
         MVC   LINE1+47(4),DEVT80    GET TEXT FOR DASD TYPE
TITLE    SR    R4,R4
         STH   R4,CYL               START FROM CYLINDER 0
         STH   R4,HEAD                         HEAD     0
         LR    R6,R4                SAVE POSITION FOR COMPUTING
         LA    R3,CCW0              LOAD ADDRESS OF READ CCW
         DIAG  R2,R3,X'0020'        EXECUTE IT
         CLI   COUNT+4,X'03'        IS THE RECORD ¬3?
         BNE   TITLE                NO.. REPEAT OPERATION
         MVC   PRT,CLEAR            CLEAR OUTPUT AREA
         MVI   PRT,X'F1'            NEW PAGE
         MVC   LINE1+29(6),BINPUT+4 MOVE VOLSER IN HEADER LINE FOR PAGE
         MVC   PRT+10(51),LINE1     MOVE HEADER LINE IN OUTPUT AREA
         PRINTL PRT,133             PRINT HEADER LINE
         LINEDIT TEXTA=LINE0        MESSAGE TO TERMINAL
         MVC   PRT,CLEAR            CLEAR OUTPUT AREA
         MVC   PRT+15(42),LINE2     UNDERLINE TEXT AND WRITE HEADER OF
         PRINTL PRT,133                       PAGE
         MVC   PRT+15(42),LINE3
         PRINTL PRT,133
         MVC   PRT+15(42),LINE2
         PRINTL PRT,133
         MVC   PRT,CLEAR            CLEAR OUTPUT AREA
         MVC   PRT+16(6),BINPUT+4   SAVE LABEL IN OUTPUT AREA
         LA    R8,GOON              PROCEDURE FOR INCREMENT OF CYL
         BR    R8                   GO
LOOP     LA    R3,CCW0              LOAD ADDRESS OF READ CCW
         DIAG  R2,R3,X'0020'        EXECUTE IT
         BNZ   GOON                 ERROR.. NEXT CYLINDER
         CLI   COUNT+4,X'03'        IS THE RECORD ¬3 ?
         BNE   LOOP                 NO.. REPEAT OPERATION
         CLC   BINPUT(4),IDENTC     CHECK FOR MINIDISK LABEL **CMS1**
         BE    PUTIT                YES.. GO WRITE
         CLC   BINPUT(4),IDENTV     CHECK FOR MINIDISK LABEL **VOL1**
         BE    PUTIT                YES.. GO WRITE
GOON     AH    R4,ONE               NEXT CYLINDER
         STH   R4,CYL
         CR    R4,R5                LAST CYLINDER ?
         BNH   LOOP                 NO.. REPEAT
         LA    R8,ENDPR             END OF PROGRAM
         B     PUTIT                WRITE LAST LINE
ENDPR    MVC   PRT+15(42),LINE2     UNDERLINE TEXT
         PRINTL PRT,133
         LINEDIT TEXT='SPOOL PRT CLOSE',DOT=NO,DISP=CPCOMM
         L     R14,SAVEA            LOAD RETURN ADDRESS
         BR    R14                  GO HOME !
ERROR1   LR    R9,R15
         LINEDIT TEXT='DEVICE ON VIRTUAL ADDRESS 100 IS NOT READY'
         LINEDIT TEXT='RETURN CODE ........',SUB=(DEC,(R9))
         B     ENDPR
ERROR2   LINEDIT TEXT='DEVICE ON VIRTUAL ADDRESS 100 IS NOT DASD'
         B     ENDPR
PUTIT    CVD   R6,UTOT              PROCEDURE TO WRITE LINE
         MVC   REZUL,MASK           PREPARE OUTPUT FIELD
         ED    REZUL,UTOT           START CYLINDER OF THE MINIDISK
         MVC   PRT+30(4),REZUL+12
         SH    R4,ONE               END CYLINDER
         CVD   R4,UTOT
         MVC   REZUL,MASK           PREPARE OUTPUT FIELD
         ED    REZUL,UTOT           END CYLINDER OF THE MINIDISK
         MVC   PRT+41(4),REZUL+12
         AH    R4,ONE               RECOVER NUMBER OF CYLINDER
         LR    R7,R4                COMPUTE LENGTH OF MINIDISK
         SR    R7,R6
         CVD   R7,UTOT
         MVC   REZUL,MASK
         ED    REZUL,UTOT
         MVC   PRT+52(4),REZUL+12
         PRINTL PRT,133             WRITE OUTPUT LINE
         MVC   PRT+16(6),BINPUT+4   MOVE LABEL OF MINIDISK FOR NEXT
         LR    R6,R4                STORE NUMBER OF CYLINDER
         BR    R8                   GO WORK
*        END OF PROGRAM
         REGEQU
SAVEA    DS    F                    DEFINE AREA FOR REGISTER 14
COUNT    DS    CL8                  AREA FOR COUNT
PRT      DS    CL133                WRITE LINE
         DS    D
UTOT     DS    PL8                  AREA FOR PACKED RESULT
REZUL    DS    CL16                 OUTPUT FIELD FOR NUMERIC VALUE
*        C O N S T A N T S
MASK     DS    X'40202020202020202020202021202020'
CUU      DC    X'0100'
CCHH     DS    0CL6                 ADDRESS IN DASD - 00CCHH
         DC    XL2'0000'
CYL      DS    H
HEAD     DS    H
ONE      DC    H'1'
TYP3330  DC    H'807'               NUMBER OF CYLINDERS FOR DASD 3330
TYP3340  DC    H'695'               NUMBER OF CYLINDERS FOR DASD 3340
TYP3350  DC    H'554                NUMBER OF CYLINDERS FOR DASD 3350
TYP3375  DC    H'958'               NUMBER OF CYLINDERS FOR DASD 3375
TYP3380  DC    H'884'               NUMBER OF CYLINDERS FOR DASD 3380
DEVT30   DC    C'3330'              TEXT
DEVT40   DC    C'3340'              TEXT
DEVT50   DC    C'3350'              TEXT
DEVT75   DC    C'3375'              TEXT
DEVT80   DC    X'3380'              TEXT
IDENTV   DC    CL4'VOL1'            FOUR BYTE IDENTIFICATION
IDENTC   DC    CL4'CMS1'            FOUR BYTE IDENTIFICATION
CLEAR    DC    133C' '              CLEAR LINE
LINE0    DC    X'33'
LINE1    DC    C'MAP OF MINIDISKS ON VOLSER :...... DEVTYPE : .... '
LINE2    DC    C'----------------------------------------'
LINE3    DC    C' LABEL        START        END      LEN '
         DS    0D
*    C H A N N E L  C O M M A N D  W O R D S
CCW0     CCW   X'0B',CCHH,X'60',6       SET-UP  *** 00CCHH ***
         CCW   X'12',COUNT,X'60',8      READ COUNT FOR RECORD 0
         CCW   X'12',COUNT,X'60',8      READ COUNT FOR RECORD 1
         CCW   X'12',COUNT,X'60',8      READ COUNT FOR RECORD 2
         CCW   X'06',BINPUT,X'20',4096  READ RECORD 3
BINPUT   DS    CL4096                   READ AREA
         END   MAPDASD


A Stoianov, I Valkov
Students Town Christo Botev (Bulgaria) ) Xephon 1990


