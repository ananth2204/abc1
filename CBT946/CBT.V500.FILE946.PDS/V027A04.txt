 Saving points after leaving XEDIT
When XEDITing a large file, it is very useful to use the XEDIT command 'point'
or the prefix macro '.XXXX'.  However, when exiting the XEDIT environment and
re-XEDITing the file, one has to locate the lines and reset the points.  The
following XEDIT macro will save the points.
/* POINTSAV XEDIT */
PARSE SOURCE . . . . . SYN .
EXTRACT ' /FNAME/FTYPE/POINT */SYN 'SYN'/'
CMD=WORD(SYNONYM.3,3)
IF CMD=''
THEN DO;
  EMSG 'ERROR IN SETTING OF THIS SYNONYM'
  EXIT
END
IF POINT.0>0 THEN DO;
  LINE=''
  DO I=1 TO POINT.0
   LINE=LINE||WORD(POINT.I,2)' 'WORD(POINT.I,3)' '
  END
  GLOBALV SELECT XEDITPNT
  GLOBALV SETL STRIP(FNAME.1)STRIP(FTYPE.1) LINE
END
QUEUE 'COMMAND 'CMD
EXIT
/* END POINTSAV XEDIT */
The next lines at the end of your PROFILE XEDIT file will restore the points
setting.
/* RESTORE POINTS */
GLOBALV SELECT XEDITPNT
GLOBALV STACK STRIP(FNAME)STRIP(FTYPE)
PULL LINE
DO I=1 TO WORDS(LINE)-1 BY 2
 QUEUE ':'WORD(LINE,I)' POINT .'WORD(LINE,I+1)
END
QUEUE 'TOP'
SET SYNONYM FILE 4 MACRO POINTSAV PFILE
SET SYNONYM FFILE 2 MACRO POINTSAV FILE
SET SYNONYM QUIT 4 MACRO POINTSAV PQUIT
SET SYNONYM QQUIT 2 MACRO POINTSAV QUIT
/* END RESTORE POINTS */
Asher Shoshan
Migdal Insurance Company (Israel)  ) Xephon 1988
























