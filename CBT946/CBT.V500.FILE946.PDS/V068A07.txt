CMS housekeeping utility

Having a large number of files on my CMS user-id, it struck
me as a useful exercise to have an EXEC that would tidy up
my disk once in a while, thus saving me the time and trouble of
sorting through my files manually. The following EXEC is the
skeleton of such a routine, and has potential to be adapted for a
variety of uses. In its present format, the EXEC stacks
information relating to specified filetypes, and, if the file is not
already in packed format, packs it. (Some files are deleted as a
matter of course.) Some possible amendments would be to add
a check for the file date, and only to pack (or erase!) files that
have exceeded a specified retention period, or to direct the
EXEC to a different disk.

         &TRACE ALL
* DESCRIPTION: TIDY UP A CMS MINI-DISK BY PACKING OR ERASING SPECIFIED
*              FILETYPES.
         &PRESUME &COMMAND
         &BUFFER *
CLRSCRN
* PREPARE ASSEMBLER / AUTOSAVE FILETYPES FOR PACKING
MAKEBUF
LISTFILE * A%%%%%%E A (STACK FIFO LABEL
         &IF &RC = 28 &TYPE NO ASSEMBLE/AUTOSAVE FILES
         &IF &RC = 28 &GOTO -JCLPACK
         &STACK FIFO "END-OF-FILE"
-READA%
         &READ VARS &FN &FT &FM &RECFM &LRECL &EXT
         &IF &FN = "END-OF-FILE" &GOTO -JCLPACK
         &CALL -PACKED
         &IF &PACKED = YES &GOTO -READA%
         &IF &FT = ASSEMBLE COPYFILE &FN &FT &FM = = = (PACK REPL OLDD
         &IF &FT = AUTOSAVE COPYFILE &FN &FT &FM = = = (PACK REPL OLDD
         &IF &RC ¬= 0 &CALL -ERROR &RC PACK &FN &FT &FM
         &IF &FT = ASSEMBLE STATE &FN LISTING &FM
         &IF &RC = 0 ERASE &FN LISTING &FM
         &GOTO -READA%
* PREPARE JCL FILES FOR PACKING
-JCLPACK
LISTFILE * JCL A ( STACK FIFO LABEL
         &IF &RC = 28 &TYPE NO JCL FILES
         &IF &RC = 28 &GOTO -CBLDEL
         &STACK FIFO "END-OF-FILE"
-READJCL
         &READ VARS &FN &FT &FM &RECFM &LRECL &EXT
         &IF &FN = "END-OF-FILE" &GOTO -CBLDEL
         &CALL -PACKED
         &IF &PACKED = YES &GOTO -READJCL
COPYFILE &FN &FT &FM = = = (PACK REPL OLDD
         &GOTO -READJCL
* LISTINGS FROM CBLVCAT / VTOC IS DELETED
-CBLDEL
LISTFILE UC%%%% LISTING A (STACK FIFO LABEL
         &IF &RC = 28 &TYPE NO CBL VCAT LISTINGS
         &IF &RC = 28 &GOTO -GETOLD
         &STACK FIFO "END-OF-FILE"
-READL%
         &READ VARS &FN &LIST &FM &RECFM &LRECL &EXT
         &IF &FN = "END-OF-FILE" &GOTO -GETOLD
ERASE &FN &LIST &FM
         &GOTO -READL%
* FILETYPES 'OLD' OR 'OLD(N)' ARE PACKED
-GETOLD
LISTFILE * OLD* A (STACK FIFO LABEL
         &IF &RC = 28 &TYPE NO 'OLD' FILES
         &IF &RC = 28 &GOTO -EXIT
         &STACK FIFO "END-OF-FILE"
-READOLD
         &READ VARS &FN &OLD &FM &RECFM &LRECL &EXT
         &IF &FN = "END-OF-FILE" &GOTO -EXIT
         &CALL -PACKED
         &IF &PACKED = YES &GOTO -READOLD
COPYFILE &FN &OLD &FM = = = (PACK REPL OLDD
         &IF &RC ¬= 0 &CALL -ERROR &RC PACK &FN &OLD &FM
         &GOTO -READOLD
-EXIT
DROPBUF 1
QUERY DISK A
         &EXIT 0
* CHECK FOR FILETYPES ALREADY IN PACKED FORMAT
-PACKED
         &PACKED = NO
         &IF &LRECL = 1024 &IF &RECFM = F &PACKED = YES
         RETURN
-ERROR
CLRSCRN
         &TYPE * * * WARNING * * *
         &TYPE &BLANK
         &TYPE RETCODE &1 FROM COMMAND &2 &3 &4 &5 &6 &7
         &TYPE &BLANK
         &TYPE --------- < ANY KEY TO CONTINUE > ----------
CP SLEEP
CLRSCRN
         &RETURN

M A Shears
Shift Leader (UK)

