Establishing a secondary user-id - revisited

This is a newer version of the SECUSER EXEC which was
published in the March 1991 edition of VM Update.  It has
been updated to run under VM/XA SP 2.1.


SECUSER  EXEC

/*   Doc: Establish a secondary user-id for another user-id (SCIF)
    Note: You must have CP class C authority to use this EXEC.
          This has been modified to run with VM/XA SP 2.1.      */
secuser_offset = 1264; /* decimal offset of secuser in vmblok   */
                       /*        this is x'4F0'                 */
PARSE UPPER ARG disc_user my_id off .
if disc_user = '?' then do
   say 'Format is: SECUSER disconnected_user_id secondary_user_id'
   say '   -or-  : SECUSER disconnected_user_id OFF'
   say '   -or-  : SECUSER disconnected_user_id QUERY'
   exit 0
end
query = 'NO'
'EXECIO * CP (STRING Q' disc_user
if rc ¬= 0 then do
   'DESBUF'
   say 'SECUSER: User:' disc_user 'isn''t logged on.'
   exit 4
end
PARSE PULL . . terminal
if terminal ¬= 'DSC' then do
   say 'SECUSER: Warning!' disc_user 'is logged onto a terminal.'
end
if my_id = 'QUERY' then do
   CALL query_secuser;
   'desbuf';
   exit 0
end
if my_id = 'OFF' | off = 'OFF' then do
   off = 'OFF';
   my_id = left('',8,' ');
   CALL new_secoper;            /* set secoper to blanks */
   exit
end
if my_id = '' then,
  my_id = userid();
/* ------------ Now, set the secoper ----------------*/
CALL new_secoper;
exit 0
NEW_SECOPER:
  'EXECIO * CP (STRING LOC' disc_user
  PARSE PULL . . vmblok_addr
  dec = x2d(vmblok_addr) + secuser_offset; /* x'4F0' = vmblok offset */
  addr_secuser = d2x(dec,8);               /* change back to hex     */
  'EXECIO * CP (STRING D HT'addr_secuser'.8'
  if rc ¬= 0 then do
     say 'SECUSER: Display Host failed with rc='rc;
     'desbuf';
     exit 16
  end
  PARSE PULL . . . . . . stuff .
  current_secuser = substr(stuff,2,8);
  if current_secuser = my_id then do;
     say 'SECUSER: It''s already set to your requested id.'
     exit 4
  end
  my_id = left(my_id,8);
  my_id_hex = c2x(my_id);
  my_id_hex = substr(my_id_hex,1,8)||' '||substr(my_id_hex,9,8);
  if my_id ¬= ' ' then do
     'EXECIO 0 CP (STRING SP PUN' my_id
     if rc ¬= 0 then do
        say 'SECUSER:' my_id 'does not exist - request ignored.'
        exit 8
     end
     'EXECIO 0 CP (STRING SP PUN SYSTEM'
  end
  'EXECIO 0 CP (STRING ST HN'addr_secuser my_id_hex;
  if rc ¬= 0 then do
     say 'SECUSER: STCP failed with rc='rc
     exit rc
  end
  if my_id ¬= ' ' then,
     say 'SECUSER: Set SECOPER for' disc_user 'to' my_id;
  else say 'SECUSER: Set SECOPER for' disc_user 'to blanks';
return
QUERY_SECUSER:
  'EXECIO * CP (STRING LOC' disc_user
  PARSE PULL . . vmblok_addr
  dec = x2d(vmblok_addr) + secuser_offset; /* x'4F0' = vmblok offset */
  addr_secuser = d2x(dec,8);               /* change back to hex     */
  'EXECIO * CP (STRING D  HT'addr_secuser'.8'
  if rc ¬= 0 then do
     say 'SECUSER: DCP failed. rc='rc
     'desbuf';
   exit 16
  end
  PARSE PULL . . . . . . stuff .
  current_secuser = substr(stuff,2,8);
  temp = strip(current_secuser);
  if temp ¬= '' then,
  say 'SECUSER: The current secondary user for 9'disc_user' -is 9',
                          current_secuser ' -';
  else say 'SECUSER: The is no secondary console for:' disc_user'.'
return

Pat Tierce
Systems Software Specialist (USA)

