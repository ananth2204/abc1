 Extended FILEDEF option
VM/SP 3.1 does not display the RECFM, LRECL or BLOCK values of tapes or disks
when the LIST option (ie without operands) of the FILEDEF command is invoked.
The following code provides a mod to the CMS NUCLEUS MODULE DMSFLE.
I was unwilling to modify the IBM code so I created a stripped-down version of
DMSFLE with only the code necessary to perform the LIST option, inserted my code
and created a new MODULE called FLEX (FileList EXtended) at X'20000'.
I called the extended code via a BAL using R6 for return.  This should be
inserted after label LIST3 before the L R4, D0(,R4) instruction that locates the
next FCB block.
BRANCH TO CODE
LIST3    DS    0H
         LA    R1,LISTYP              ADDRESS OF DESCRIPTION
           |       |
           |       |
         BAL   R6,EXTL                BRANCH FOR EXTENDED LIST OPTION
         L     R4,D0(,R4)             GET PTR TO NEXT ENTRY
           |       |
           |       |

EXTENDED LIST DISPLAY
*                              E X T L                                *
PROVIDE EXTENDED LIST DETAILS (RECFM, LRECL AND BLOCK)
*        FOR DISK AND TAPE DEVICES ONLY.
EXTL     EQU   *
         CLI   FCBDEV,X'10'            IS IT TAPE DEVICE
         BE    EXTL1
         CLI   FCBDEV,X'14'            OR DISK DEVICE
         BNE   EXTLX                   DON'T BOTHER IF NOT TAPE OR DISK
*  CONVERT RECFM BYTE
*     01.. ....  -  VARIABLE
*     10.. ....  -  FIXED
*     11.. ....  -  UNDEFINED
*     ...1 ....  -  BLOCKED
*     .... 1...  -  SPANNED
EXTLI    TM    JFCRECFM,UNDMASK        IS IT UNDEFINED
         BO    EXTLUND
         TM    JFCRECFM,FIXMASK        FIXED
         BNZ   EXTLFIX
         TM    JFCRECFM,VARMASK        OR VARIABLE
         BZ    EXTLCON1                BINARY ZEROS IF NOTHING ELSE
EXTLVAR  MVI   LISTXREC,C'V'
         B     EXTLCON1
EXTLUND  MVI   LISTXREC,C'U'
         B     EXTLCON1
EXTLFIX  MVI   LISTXREC,C'F'
         B     EXTLCON1
EXTLCON1 LA    R10,LISTXREC+D1         LOAD NEXT MESSAGE POS INTO R10
         TM    JFCRECFM,BLOKMASK       IS IT A BLOCKED FILE
         BZ    EXTLCON2
         MVI   0(R10),C'B'             APPEND TO MESSAGE IF SO
         AH    R10,=H'1'               AND INCREMENT MESSAGE POS
EXTLCON2 TM    JFCRECFM,SPANMASK       IS IT A SPANNED FILE
         BZ    EXTLCON3
         MVI   0(R10),C'S'             APPEND TO MESSAGE IF SO
EXTLCON3 LH    R10,JFCBLKSI            LOAD BLOCKSIZE INTO R10
         CVD   R10,CVDSTORE            CONVERT BLOCKSIZE TO DECIMAL
         MVC   LISTXBLK,EDMASK1        SET UP EDIT MASK
         ED    LISTXBLK,CVDSTORE+4     EDIT TO DISPLAY
         LH    R10,JFCLRECL            LOAD LRECL INTO R10
         CVD   R10,CVDSTORE            CONVERT LRECL TO DECIMAL
         MVC   LISTXLRC,EDMASK2        SET UP EDIT MASK
         ED    LISTXLRC,CVDSTORE+5     EDIT TO DISPLAY
         LA    R1,LISTX                LOAD PLIST ADDRESS
         SVC   CMS                     ISSUE SVC
         MVI   LISTXMES,BLANK          CLEAR DOWN MESSAGE LINE
          MVC   LISTXMES+D1(LISTXLEN-L1),LISTXMES
EXTLX    BR    R6

EQUATES ETC
*        EXTENDED MESSAGE LINE TABLE                      *
         SPACE 4
LISTX    DC    CL8'TYPLIN'
         DC    AL1(1),AL3(LISTXMES),C'D'
LISTXLNG DC    AL3(L'LISTXMES)
LISTXMES DS    0CL35
LISTXIND DC    CL5' '
LISTXREC DC    CL6' '
LISTXLRC DC    CL7' '
LISTXBLK DC    CL10' '
         DC    CL8' '
LISTXEND DS    0H                 DELIMITER FOR END OF MESSAGE
         SPACE 2
EDMASK2  DC    X'20202021204040'
EDMASK1  DC    X'20202020202120404040'
*
LISTXLEN EQU   LISTXEND-LISTXMES  OVERALL LENGTH OF EXT MESSAGE TABLE
*
UNDMASK  EQU   192                BIT MASK B'11000000'
FIXMASK  EQU   128                BIT MASK B'10000000'
VARMASK  EQU   64                 BIT MASK B'01000000'
BLOKMASK EQU   16                 BIT MASK B'00010000'
SPANMASK EQU   8                  BIT MASK B'00001000'
C M Whitehead
Programmer/Analyst
Pfizer Ltd (UK)     ) Pfizer Ltd 1988




































