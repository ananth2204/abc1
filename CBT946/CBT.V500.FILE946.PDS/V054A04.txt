SEEKMAP for VM/XA

In issue 43 of VM Update there was an article for running
SEEKMAP under HPO.  I have modified the EXEC to run
under VM/XA.

/*  */
/* PRODUCE A SEEK-MAP FROM RDEVBLOK FOR A DEVICE */
arg CUU .
'set imescape off'
if length(CUU)=6 then do
                 VOLUME  = CUU
                 'execio 1 cp (string Q DASD' VOLUME
                 pull DASD CUU . . VOL .
                 if VOL ¬= VOLUME then do
                                  say 'Volume' VOLUME 'not found'
                                  say 'or no machine class-b    '
                                  signal ERROR
                                       end
                      end
'execio * cp (string LOC' CUU
if rc ¬= 0 then do
            if rc=1001 then say 'cannot examine real cntrl-block'
                       else say 'cannot find device' CUU
            signal ERROR
                end
pull .
pull RDEVADD
'execio * cp (string Q' CUU
pull DTYPE .
if DTYPE ¬= 'DASD' then do
                   say CUU 'is not a DASD device'
                   exit 8
                        end

ASKSIZE:
say 'Enter size of interval into which disk will be divided'
say 'Default is 1 cylinder'
pull INT .
say INT
if INT = '' then INT = 1
if          datatype(INT) = 'CHAR' then signal ASKSIZE
if INT = 1  then FILL = ''
            else FILL = '+'
immcmd set map
/*
CYLADD  = xa(RDEVADD,'26')
IOCTADD = xa(RDEVADD,'CC')
*/
CYLADD  = d2x(x2d(RDEVADD)+x2d(26))
IOCTADD = d2x(x2d(RDEVADD)+x2d(CC))
TOT = 0
MINTOT = 0
LINE = copies('****.',10)
say 'Maps will be shown every 60 samples'
say 'Force a map at any time by entering -map-'
say 'Terminate by entering -hi-'
say 'Immediate map/hi    sequence :'
say '      1. PA1              '
say '      2. CURSOR left 1    '
say '      3. enter            '
say '      4. HI/MAP -enter-   '
STIME = time()
HICYL = 0
do forever
   'sleep 1 sec'
   signal on HALT
   immcmd status map
   if rc = 1 then do
                        MINTOT = 0
                        call SHOW
                             end
   'execio 1 cp (string D H'IOCTADD
   pull . IOCT .
   if IOCT = OLDIOCT then iterate
   OLDIOCT = IOCT
   'execio 1 cp (string D H'CYLADD
   pull . WORD .
   CIL = right(WORD,4)
   CAL = x2d(CIL)
   CBL = CAL/INT
   CYL = trunc(CBL)
   if symbol('SEEK.CYL') = 'LIT' then SEEK.CYL = 0
   SEEK.CYL = SEEK.CYL + 1
   TOT      = TOT + 1
   if CYL > HICYL then HICYL = CYL
   MINTOT = MINTOT + 1
   if MINTOT = 60 then do
                        MINTOT = 0
                        call SHOW
                             end
end
HALT:
'conwait'
'desbuf'
 call SHOW
 exit
SHOW:
'clrscrn'
ETIM¬= time()
say 'Enter -hi- to terminate. Enter -map- to force map'
say 'Device' CUU TOT 'samples between' STIME 'and' ETIME
say 'Disk is divided into ' INT 'cylinder intervals'
say ' '
say 'Cylinder count percentage'
do CYL = 0 to HICYL
   if symbol('SEEK.CYL') ¬= 'LIT' then do
                         PCT = format(SEEK.CYL*100/TOT,,0)
                         LINELEN = format(SEEK.CYL*50/TOT,,0)
                         HLIN = left(LINE,LINELEN)
                         say ' ' right(CYL*INT,4)||FILL '  ' ,
                                 right(SEEK.CYL,4),
                                 right(PCT,3)'%' HLIN
                                       end
end
return
ERROR:
say DASD CUU VOL RDEVADD
'conwait'
'desbuf'
exit 8
XA: procedure; arg x . y .;
if substr(y,1,1) ¬= '-' then return d2x(x2d(x)+x2d(y))
                        else return d2x(x2d(x)-x2d(substr(y,2)))

O Jacobsen
System Programmer
Sony (The Netherlands)

