 SYSGEN help program
One advantage of VM/HPO Release 5 is the provision of alternate nucleus support.
We use it to prepare new nucleii, thus leaving the previous version on disk
should any problems occur.  The coding convention of DMKSYS is as described in
the Planning Guide, pages 187-9, ie
   SYSVOL=*,
   SYSRES=100,
where 100 is an unused virtual address of MAINT's.
The following program, SYSDISK, runs from MAINT's profile EXEC to remind the
user to define the non-IPL device as 100 before a SYSGEN.
SYSDISK ASSEMBLE
         TITLE 'PROGRAM TO DETERMINE THE CURRENT IPL DEVICE'
SYSDISK  START
         REGEQU
         SAVE  (14,12)
         BALR  12,0
         USING *,12
         ST    R13,SAVEAREA+4
         LA    R13,SAVEAREA
         SR    R15,R15                  ZERO R15
         WRTERM '***************************************************'
* FIRST IDENTIFY THE SYSTEM
         LA    R4,SYSID                 LOAD ADDRESS OF SYSTEM ID
         LA    R6,SYSTEMID              LOAD ADDRESS OF TARGET
         LA    R5,2                     COUNT OF CP REAL ADDRESSES
         DC    X'83450004'              EXAMINE REAL CORE
* NOW DETERMINE THE IPL DEVICE
         LA    R4,SYSIPLDV              LOAD ADDRESS OF SYSIPLDV
         LA    R6,RESULT                LOAD ADDRESS OF TARGET
         LA    R5,1                     COUNT OF CP REAL ADDRESSES
         DC    X'83450004'              EXAMINE REAL CORE
         MVC   TARGET,PATTERN
         ED    TARGET,RESULT
         WRTERM IPLMSG1,LMSG1
* CHECK THE DISK ADDRESS FOR VMSRES
VMSRES   CLC   RESULT,=X'0281'
         BNE   ALTNUC
         MVC   IPLDISK,VMPK01
         WRTERM IPLMSG2,LMSG2
         B     EXIT
ALTNUC   CLC   RESULT,=X'0282'
         BNE   NOIPLDEV
         WRTERM IPLMSG2,LMSG2
         B     EXIT
NOIPLDEV WRTERM 'PROGRAM ERROR ............ OR NO IPL DEVICE (?)'
         LA    R15,999
* THIS IS THE EXIT ROUTINE
EXIT     WRTERM '***************************************************'
         L     R13,SAVEAREA+4           RESTORE CALLER'S AREA
         RETURN (14,12),RC=(15)         EXIT RC
SYSIPLDV DC    X'00000378'
SYSID    DC    X'000006E8'
SYSID2   DC    X'000006EC'
         DS    0F
RESULT   DS    H
         DS    0F
IPLMSG1  DC    C'THE CURRENT IPL DEVICE ON '
SYSTEMID DC    CL8' '
         DC    C'IS '
TARGET   DC    CL4' '
LMSG1    EQU   *-IPLMSG1
IPLMSG2  DC    C'   DEFINE '
IPLDISK  DC    CL8' VMSRES '
         DC    C'AS 100 PRIOR TO REBUILDING NUCLEUS'
LMSG2    EQU   *-IPLMSG2
VMPK01   DC    CL8' VMPK01 '
 PATTERN  DC    X'20202020'
SAVEAREA DC    9D'0'
         LTORG
         END   SYSDISK
Roy Neil
Senior Technical Adviser
Rothmans International Ltd (UK)    ) R Neil 1989



























































