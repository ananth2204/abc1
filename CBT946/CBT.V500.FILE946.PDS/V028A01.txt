 VM activity monitor
This EXEC creates a formatted (and interpreted) real-time, monitor-type display
of the 'in queue' users extracted from the 'CP IND QUEUE' command, to show the
active queues in VM each time the enter key is pressed, or every n seconds if
the 'AUTO' parm is keyed.  The EXEC is terminated when 'Q' or 'QUIT' is keyed.
'AUTO' may also be keyed when in manual mode to enter auto mode.  Additional
information is also extracted from VM for each user in the active queue,
including whether the user is FAVORED or has QDROP off.  The display is limited
to the number of users that can fit on a screen.
It requires user class E or equivalent IPF WAKEUP module
/*Format: QACT <AUTO>                                              */
ADDRESS COMMAND
ARG AUTOPARM
MAXLINES = 18         /* MAX NO. OF DETAIL LINES ON 24X80 TERMINAL */
NSECONDS = 3                  /* REFRESH SCREEN EVERY # SECONDS    */
TYPING = CMSFLAG(CMSTYPE)
HI = X2C(1DE8)
LO = X2C(1D60)
"ESTATE WAKEUP MODULE *"
IF RC ¬= 0 THEN DO
                SAY "WAKEUP MODULE IS REQUIRED"
                EXIT RC
                END
DO FOREVER
   "VMFCLEAR"
   "EXECIO * CP (STEM INDF. STRING IND FAVORED"
    IF RC ¬= 0 THEN DO
                    SAY "CLASS E COMMAND PRIVILEGES ARE REQUIRED"
                    EXIT RC
                    END
    FAVORED_USERS = ""
    DO X = 1 TO INDF.0
       FAVORED_USERS = FAVORED_USERS INDF.X
    END
   "EXECIO * CP (STEM QDROP. STRING Q QDROP"
    QDROP = ""
    DO X = 1 TO QDROP.0
       QDROP = QDROP QDROP.X
    END
    SAY "     " DATE(U) HI "   VM/SP  DISPLAY OF IN QUEUE USERS  " LO  ,
        TIME() HI
    SAY
    SAY "                    IN                   RES    WSET"
    SAY "  USERID    PRI    QUEUE   WAIT         PAGES   PAGES    VCPU",
        "   TCPU    SIO "LO
    SAY "  ------   -----   -----   ----------   -----   -----    ----",
        "   ----   -----"
        "   EXECIO * CP (STEM INDQ. STRING IND QUEUE"
    LINECNT = 5
    DO X = 1 TO INDQ.0
       DO 3
          PARSE VAR INDQ.X USERID QUE WAIT PAGES INDQ.X
          IF USERID = "" THEN LEAVE
          PARSE VAR PAGES RES "/" WSET
          SELECT
              WHEN WAIT = "--" THEN NOP
              WHEN WAIT = "IO" THEN WAIT = "I/O WAIT"
              WHEN WAIT = "PS" THEN WAIT = "PSW WAIT"
              WHEN WAIT = "PG" THEN WAIT = "PAGE WAIT"
              WHEN WAIT = "RU" THEN WAIT = "RUNNING"
              WHEN WAIT = "EX" THEN WAIT = "INST. SIM."
              OTHERWISE NOP
          END
         "EXECIO 1 CP (VAR PRIORITY STRING Q PRIORITY" USERID
          PARSE VAR PRIORITY . . . PRIORITY
         "EXECIO * CP (STEM INDU. STRING IND USER" USERID
           PARSE VAR INDU.2 . "=" VCPU . "=" TCPU . "=" SIO .
          IF FIND(FAVORED_USERS,USERID) > 0 THEN FAVORED = " F"
                                       ELSE FAVORED = "  "
          IF FIND(QDROP,USERID) > 0 THEN FAVORED = FAVORED"Q"
          SAY "  "LEFT(USERID,8) RIGHT(STRIP(PRIORITY,L,"0"),2),
              LEFT(FAVORED,6) LEFT(QUE,6) LEFT(WAIT,10) ,
              RIGHT(X2D(RES),7) RIGHT(X2D(WSET),7),
              RIGHT(STRIP(VCPU,L,"0"),7) RIGHT(STRIP(TCPU,L,"0"),7),
              RIGHT(STRIP(SIO,L,"0"),7)
          LINECNT = LINECNT + 1
          IF LINECNT = MAXLINES THEN LEAVE
       END
       IF LINECNT = MAXLINES THEN LEAVE
    END
    DO LINECNT = LINECNT TO MAXLINES
       SAY
    END
    SAY
    SAY HI"        ENTER:"LO"REFRESH|HOLD   "HI"Q:"LO"QUIT     "HI,
        "AUTO:"LO"REFRESH" NSECONDS" SEC."
    IF AUTOPARM  = "AUTO" THEN DO
                              "WAKEUP AT +00:0"NSECONDS "(QUIET"
                               IF EXTERNALS() = 0 THEN ITERATE
                               END
    PARSE UPPER EXTERNAL RESP
    IF RESP = "Q" THEN LEAVE
    IF RESP = "AUTO" THEN AUTOPARM = "AUTO"
END
"VMFCLEAR"
EXIT
Duane Ternes
Cascade Corp (USA)  ) Xephon 1988


































