 Quick XEDIT start-up
We often XEDIT our CMS files.  During program development we probably re-edit
the same source program several times in a row, and type that filename and
filetype (and perhaps filemode).
I wrote an EXEC called 'E', which allows me to key, if I wish, just a filename,
or part of a filename, or perhaps no filename at all.  The basis is GLOBALV,
which I use in this EXEC to store and retrieve the complete file-id of the last
file I XEDITed since the last IPL.  When no parameters are keyed following the
'E' command, that's the file that appears.
When I need to look at something else, I can either spell out both the filename
and filetype as usual, or just the filename in whole or in part (using an '*' if
it's just part of the filename).  The EXEC does a LISTFILE to determine which
file(s) match that filename, then uses the first one on the list. You'll usually
retrieve what you're after.
Of course, regardless of what method you use to invoke XEDIT, whether all, part,
or none of the file-id is given, the resulting file-id is stored in the GLOBALV
for next time.
If the only parameter given is '?', the file-id stored in the GLOBALV is
displayed.  It should also be noted that although you can set up a GLOBALV to
remain across IPLs of CMS, it involves CMS storing the values in a CMS file,
which I opted not to do in this case.
/* Invoke an XEDIT session.
   1.  If full fileid is given, use it as/is.
   2.  If only filename and filetype are given, use filemode "*".
   3.  If only filename is given:
       a. If "?", display current GLOBALV values (last fileid used).
       b. Do a LISTFILE using the filename given.  Note that "*" may
          be keyed at end of filename to cause generic search.
          Use the first fileid from the resulting CMS EXEC.
   4.  If no parameters are given, use the fileid stored in GLOBALV.
       If not used since last IPL, display an error message.
*/
arg fn ft fm;
if fn = '?' then do;
   'GLOBALV SELECT $E$ GET F@ T@ M@'; say F@ T@ M@;
   exit 1;
   end;
if fm ¬ = '' then signal invoke;
if ft ¬ = '' then do;
   fm = '*';
   signal invoke;
   end;
if fn ¬ = '' then do;
   'LISTFILE' fn '* * ( EXEC';
   if Rc¬=0 then exit Rc;
   'EXECIO 1 DISKR CMS EXEC A 1';
   pull . . fn ft fm;
   'ERASE CMS EXEC A';
   signal invoke;
   end;
'GLOBALV SELECT $E$ GET F@ T@ M@';
fn=F@; ft=T@; fm=M@;
if fn = '' then do;
   say 'No fileid default established since last IPL';
   exit 2;
   end;
Invoke:
'XEDIT' fn ft fm;
F@=fn; T@=ft; M@=fm;
'GLOBALV SELECT $E$ PUT F@ T@ M@';
exit 0;
Lawrence Holder
Senior Programmer/Analyst
The University of Tennessee (USA)       ) UTMCC 1988


