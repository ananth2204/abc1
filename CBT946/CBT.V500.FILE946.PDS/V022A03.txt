 DASD space allocation reports (continued)
This month we conclude this article by printing the COBOL program that produces
the DASD space allocation report.

DASDSPC PROGRAM

       IDENTIFICATION DIVISION.
       PROGRAM-ID.    DASDSPC.
       REMARKS. THIS PROGRAM WILL READ A DISK FILE CREATED FROM DITTO
                VTOCS, AND VSAM LISTCATS OF ALL OUR DASD AND A CARD
                FILE CREATED FROM THE DIRECTORY DISKMAP FROM MAINTS
                191 MINIDISK AND PRINT A REPORT SHOWING ALL ALLOCATIONS
                BY  VOLUME/USER.
                ALSO, ALL FREE SPACE, VSAM DEFINED SPACE IS PRINTED
                AND A DASD RECAP IS PRINTED AS SEPARATE REPORTS.
                THERE ARE TWO 77-LEVEL ENTRIES, DASD-TRK-VOL, WHICH IS
                SET AT 13275 TRACKS PER VOLUME, AND DASD-TRK-CYL WHICH
                IS SET AT 15 TRACKS PER CYLINDER FOR 3380 DEVICES.
                IF DASD DEVICES ARE CHANGED FROM 3380, THESE VALUES
                SHOULD BE CHANGED.
                TABLE-VOLUMES HAS 50 OCCURS.  IF YOU HAVE MORE THAN 50
                DASD DEVICES, THEN CHANGE THIS VALUE ACCORDINGLY.
       ENVIRONMENT DIVISION.
       CONFIGURATION SECTION.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
           SELECT  DISK-FILE   ASSIGN SYS010-DA-3380-S.
           SELECT  CARD-FILE   ASSIGN SYS016-UR-2540R-S.
           SELECT  SORT-FILE   ASSIGN SYS001-DA-3380-S-SORTWK1.
           SELECT  PRINT-FILE1 ASSIGN SYS018-UR-1403-S.
           SELECT  PRINT-FILE2 ASSIGN SYS019-UR-1403-S.
       DATA DIVISION.
       FILE SECTION.
       FD DISK-FILE
              RECORDING MODE IS F
              RECORD CONTAINS 121 CHARACTERS
              LABEL RECORDS ARE STANDARD.
       01                 FILLER            PIC X(121).
       FD  CARD-FILE
               RECORDING MODE IS F
               RECORD CONTAINS 80 CHARACTERS
               LABEL RECORDS ARE OMITTED.
       01  CARD-REC1.
           03              FILLER           PIC X(4).
               88  CODE-ID1                             VALUE 'CODE'.
               88  CODE-ID2                             VALUE '----'.
               88  CODE-ID3                             VALUE '    '.
           03              FILLER           PIC X(4).
           03              FILLER           PIC X(6).
               88  USER-ID1                             VALUE 'USER  '.
               88  SYSTEM-ID1                           VALUE 'SYSTEM'.
           03  DIVISION-ID1                 PIC X(6).
               88 DIV-PHILA                             VALUE 'PHILA '.
               88 DIV-SPART                             VALUE 'SPART '.
               88 DIV-TEST                              VALUE 'TEST  '.
               88 DIV-DIST                              VALUE 'DIST  '.
           03              FILLER           PIC X(60).
       01  CARD-REC2.
           03  CARD-CODE2                   PIC X.
           03              FILLER           PIC X(7).
           03  CARD-NAME2                   PIC X(15).
           03              FILLER           PIC X(57).
       01  CARD-REC3.
           03  CARD-VOLUME                  PIC X(6).
               88  VOLUME-ID                        VALUE 'VOLUME'.
               88  DASH-ID                          VALUE '------'.
            03              FILLER           PIC XXX.
           03  CARD-NAME.
               05  CARD-USER                PIC X(8).
               05               FILLER      PIC X(4).
               05  CARD-CUU                 PIC XXX.
           03              FILLER           PIC XXX.
           03  CARD-DEVTYPE                 PIC X(6).
           03              FILLER           PIC XXX.
           03  CARD-BEGIN-X.
               05  CARD-BEGIN               PIC S9(6).
           03              FILLER           PIC XXX.
           03  CARD-END-X.
               05  CARD-END                 PIC S9(6).
           03              FILLER           PIC XXX.
           03  CARD-ALLOC-X.
               05 CARD-ALLOC                PIC S9(6).
           03             FILLER            PIC X.
           03             FILLER            PIC X(11).
               88 OVERLAP-ID                         VALUE '**OVERLAP**'.
               88 GAP-ID                             VALUE '   GAP     '.
           03             FILLER            PIC X(8).
       SD  SORT-FILE
               RECORDING MODE IS F
               LABEL RECORDS ARE STANDARD
               DATA RECORD IS SRT-REC.
       01  SRT-REC.
           03  SRT-KEY.
               05  SRT-CODE                 PIC X.
               05  SRT-USER                 PIC X(15).
               05  SRT-VOLUME               PIC X(6).
               05  SRT-NAME                 PIC X(46).
               05  SRT-SEQ                  PIC XX.
               05  SRT-SEQ-FS       COMP-3  PIC S9(5).
           03  SRT-VM-CODE                  PIC XX.
           03  SRT-BEGIN            COMP-3  PIC S9(5).
           03  SRT-END              COMP-3  PIC S9(5).
           03  SRT-NUMTRKS          COMP-3  PIC S9(5).
           03  SRT-EXTENTS          COMP-3  PIC S9(3).
           03  SRT-DATA                     PIC X(80).
       FD  PRINT-FILE1
               RECORDING MODE F
               RECORD CONTAINS 133 CHARACTERS
               LABEL RECORDS ARE OMITTED.
       01  PRT-REC1.
           03              FILLER           PIC X(133).
       FD  PRINT-FILE2
               RECORDING MODE F
               RECORD CONTAINS 133 CHARACTERS
               LABEL RECORDS ARE OMITTED.
       01  PRT-REC2.
           03              FILLER           PIC X(133).
           EJECT
       WORKING-STORAGE SECTION.
       77  PGM-ID                           PIC X(10)  VALUE 'DASDSPC'.
       77  TIME-COMPILED                    PIC X(16)  VALUE      ' '.
       77  DASD-TRK-VOL         COMP-3      PIC S9(9)  VALUE   +13275.
       77  DASD-TRK-CYL         COMP-3      PIC S99    VALUE     +015.
       77  LINE-CNT1            COMP-3      PIC S99    VALUE      +99.
       77  PAGE-CNT1            COMP-3      PIC 999    VALUE        0.
       77  LINE-CNT2            COMP-3      PIC S99    VALUE      +99.
       77  PAGE-CNT2            COMP-3      PIC 999    VALUE        0.
       77  WORK-PERCENT         COMP-3      PIC S999V9(7) VALUE    +0.
       77  TABLE-SW                         PIC X      VALUE      ' '.
       77  DIVISION-SW                      PIC X      VALUE      ' '.
       77  WRK-CODE                         PIC X      VALUE      ' '.
       77  WRK-SYS                          PIC XXX    VALUE      ' '.
        77  HOLD-CODE                        PIC X      VALUE      ' '.
       77  HOLD-VM-CODE                     PIC XX     VALUE      ' '.
       77  HOLD-USER                        PIC X(15)  VALUE      ' '.
       77  HOLD-VOLUME                      PIC X(6)   VALUE      ' '.
       77  HOLD-NAME                        PIC X(46)  VALUE      ' '.
       77  HOLD-DIVISOR         COMP-3      PIC S9(9)  VALUE       +0.
       77  WORK-BEGIN                       PIC S9(5)  VALUE       +0.
       77  WORK-END                         PIC S9(5)  VALUE       +0.
       77  WORK-NUMTRKS                     PIC S9(5)  VALUE       +0.
       77  WORK-NUMTRKS-X                   PIC X(6)   VALUE      ' '.
       77  ACCUM-EXTENTS        COMP-3      PIC S9(3)  VALUE       +0.
       77  ACCUM-NUMTRKS        COMP-3      PIC S9(7)  VALUE       +0.
       77  P-C1                             PIC X      VALUE      ' '.
       77  P-C2                             PIC X      VALUE      ' '.
       77  ZORRO                COMP        PIC S9(9)  VALUE       +0.
       77  DATA-SW                          PIC X      VALUE      'N'.
           88   DATA-SW                                VALUE      'Y'.
       77  TRACK-SW                         PIC X      VALUE      'N'.
           88    TRACK-ON                              VALUE      'Y'.
       01  WORK-NAME.
           03  WRK-FIELD1                   PIC XXX.
           03              FILLER           PIC X(5).
           03  WRK-REACH                    PIC X.
           03  WRK-FIELD2                   PIC XXX.
           03              FILLER           PIC X(36).
       01  TABLE-USERS.
           03  TABLE-USER   OCCURS  40 TIMES INDEXED BY NME-IND.
               05  TBL-NME-CODE             PIC X.
               05  TBL-NME                  PIC X(15).
       01  TABLE-SYSTEMS.
           03  TABLE-SYSTEM OCCURS 250 TIMES INDEXED BY SYS-IND.
               05  TBL-SYS-CODE             PIC X.
               05  TBL-SYS                  PIC XXX.
       01  TABLE-VOLUMES.
           03  TABLE-VOLUME OCCURS  50 TIMES INDEXED BY VOL-IND.
               05  TBL-VOL-NAME             PIC X(6).
               05  TBL-VOL-ALLOC    COMP-3  PIC S9(9).
               05  TBL-VSM-ALLOC    COMP-3  PIC S9(9).
               05  TBL-VOL-FREE     COMP-3  PIC S9(9).
               05  TBL-VSM-DASD     COMP-3  PIC S9(9).
       01  DISK-RECORD.
           03  DISK-CODE1                    PIC X.
           03              FILLER            PIC X(30).
           03              FILLER            PIC X(6).
               88  VOLUME-ID1                          VALUE 'VOLID='.
           03  DISK-VOLUME1                  PIC X(6).
           03              FILLER            PIC X(78).
       01                  FILLER REDEFINES DISK-RECORD.
           03              FILLER            PIC X.
           03  DISK-NAME2.
               05  DISK-NAME2-A              PIC X.
               05  DISK-NAME2-B.
                   07  DISK-NAME2-C          PIC X(6).
                   07      FILLER            PIC X(15).
               05  DISK-NAME2-D              PIC X(22).
           03  DISK-SEQ2                     PIC XX.
           03              FILLER            PIC X.
           03  DISK-IDENT2                   PIC X(6).
               88  PRIME-ID2                           VALUE 'PRIME '.
               88  FREE-ID2                            VALUE ' FREE '.
           03              FILLER            PIC X(17).
           03  DISK-NUMTRKS                  PIC X(13).
           03  DISK-TYPE2                    PIC X(4).
               88  SAM-ID2              VALUE ' SAM'.
               88  VSAM-ID2             VALUE 'VSAM'.
           03              FILLER            PIC X(33).
        01                  FILLER REDEFINES DISK-RECORD.
           03              FILLER            PIC X(8).
           03              FILLER            PIC X(18).
               88  VOLUME-ID3           VALUE 'VOLSER-------------- '.
           03  DISK-VOLUME3                  PIC X(6).
           03              FILLER            PIC X(89).
       01                  FILLER REDEFINES DISK-RECORD.
           03              FILLER            PIC X.
           03              FILLER            PIC X(16).
               88  CLUSTER-ID4                 VALUE 'CLUSTER ----- '.
               88  DATA-ID4                    VALUE '   DATA ----- '.
               88  INDEX-ID4                   VALUE '   INDEX ---- '.
           03  DISK-NAME4                   PIC X(45).
           03              FILLER           PIC X(59).
       01                  FILLER REDEFINES DISK-RECORD.
           03              FILLER           PIC X(8).
           03  FILLER                       PIC X(10).
               88  SPACE-ID5                        VALUE 'EXTENTS:  '.
           03              FILLER           PIC X(103).
       01                  FILLER REDEFINES DISK-RECORD.
           03              FILLER           PIC X(8).
           03              FILLER           PIC X(8).
               88  LOW-ID6                        VALUE 'LOW-CCHH'.
           03              FILLER           PIC X(50).
           03              FILLER           PIC X(6).
               88  TRACK-ID6                        VALUE 'TRACKS'.
           03  DISK-ALLOC6.
               05  DISK-ALLOC6-9            PIC 9(18).
           03              FILLER           PIC X(31).
       01  REPORT-TOTALS.
           03  TOT-NUM-VM-VOLS      COMP-3  PIC S999         VALUE +0.
           03  TOT-NUM-NON-VM-VOLS  COMP-3  PIC S999         VALUE +0.
           03  TOT-NUM-DASD-VOLS    COMP-3  PIC S999         VALUE +0.
           03  TOT-VOLUME-ALLOC     COMP-3  PIC S9(9)        VALUE +0.
           03  TOT-USER-ALLOC       COMP-3  PIC S9(9)        VALUE +0.
           03  TOT-VM-ALLOC         COMP-3  PIC S9(9)        VALUE +0.
           03  TOT-NON-VM-ALLOC     COMP-3  PIC S9(9)        VALUE +0.
           03  TOT-DASD-ALLOC       COMP-3  PIC S9(9)        VALUE +0.
           03  TOT-VOL-ALLOC        COMP-3  PIC S9(9)        VALUE +0.
           03  TOT-VSM-ALLOC        COMP-3  PIC S9(9)        VALUE +0.
           03  TOT-VOL-FREE         COMP-3  PIC S9(9)        VALUE +0.
           03  TOT-VSM-FREE         COMP-3  PIC S9(9)        VALUE +0.
       01  REPORT-TITLES.
           03  REPORT-TITLE-1               PIC X(42)    VALUE
                         'PROGRESS LIGHTING DASD USAGE REPORT FOR - '.
           03  REPORT-TITLE-2               PIC X(42)    VALUE
                         'PROGRESS LIGHTING DASD USAGE REPORT RECAP '.
           03  REPORT-TITLE-3               PIC X(42)    VALUE
                           '                                        '.
       01  HEAD-LINE-1.
           03              FILLER           PIC X(11) VALUE
                                                        '   DASDSPC-'.
           03  HD-1-RPT-NO                  PIC X     VALUE       ' '.
           03              FILLER           PIC X(20) VALUE       ' '.
           03  HD-1-REPORT-TITLE            PIC X(42) VALUE       ' '.
           03  HD-1-USER                    PIC X(16) VALUE       ' '.
           03              FILLER           PIC X(5)  VALUE   'DATE '.
           03  HD-1-RUN-DATE                PIC X(8)  VALUE       ' '.
           03              FILLER           PIC X(8)  VALUE
                                                           '   TIME '.
           03  HD-1-RUN-TIME                PIC XXBXXBXX.
           03              FILLER           PIC X(11) VALUE
                                                        '      PAGE '.
           03  HD-1-PAGE-NO                 PIC ZZZ.
       01  HEAD-LINE-2.
           03              FILLER           PIC X(56) VALUE
               '           DATASET                              SEQ'.
           03              FILLER           PIC X(39) VALUE
                              '    BEGINNING            ENDING      '.
           03              FILLER           PIC X(37) VALUE
                              '      TRACKS           TOTL DASD     '.
       01  HEAD-LINE-3.
           03              FILLER           PIC X(56)  VALUE
              '            NAME                                 NUM'.
           03              FILLER           PIC X(39)  VALUE
                              '      TRACK               TRACK      '.
           03              FILLER           PIC X(37)  VALUE
                              '    ALLOCATED            PERCENT     '.
           03              FILLER           PIC X.
       01  DETL-LINE-1.
           03              FILLER           PIC X.
           03  DETL-IMAGE.
               05          FILLER           PIC XX.
               05  DETL-NAME.
                   07      FILLER           PIC X(31).
                   07  DETL-USER-NAME       PIC X(15).
           03              FILLER           PIC X(4).
           03  DETL-SEQ-EXT.
                   05      FILLER           PIC X.
                   05  DETL-SEQ             PIC XX.
                   05      FILLER           PIC X.
           03              FILLER REDEFINES DETL-SEQ-EXT.
                   05  DETL-EXTENTS         PIC ZZZ.
                   05      FILLER           PIC X.
           03              FILLER           PIC XX.
           03  DETL-BEG-TRACK-X.
                   05  DETL-BEG-TRACK       PIC ZZZ,ZZZ,ZZ9.
           03              FILLER           PIC X(8).
           03  DETL-END-TRACK-X.
                   05  DETL-END-TRACK       PIC ZZZ,ZZZ,ZZ9.
           03              FILLER           PIC X(8).
           03  DETL-ALC-TRACK               PIC ZZZ,ZZZ,ZZZ.
           03              FILLER           PIC X(12).
           03  DETL-PERCENT-X.
               05  DETL-PERCENT             PIC ZZZ.ZZZZ.
           03  DETL-PERCENT-SIGN            PIC X.
           03              FILLER           PIC XXX.
       01  DETL-LINE-2                      PIC X(133).
           EJECT
       PROCEDURE DIVISION.
           MOVE WHEN-COMPILED TO TIME-COMPILED.
           DISPLAY 'BEGIN PROGRAM ' PGM-ID TIME-COMPILED UPON CONSOLE.
           MOVE HIGH-VALUES TO  TABLE-USERS TABLE-SYSTEMS TABLE-VOLUMES.
           SET NME-IND      TO  ZORRO.
           SET SYS-IND      TO  ZORRO.
           SET VOL-IND      TO  ZORRO.
       100-OPEN-FILES.
           OPEN INPUT  CARD-FILE    DISK-FILE.
           OPEN OUTPUT PRINT-FILE1  PRINT-FILE2.
       110-SORT-REPORT.
           SORT SORT-FILE
               ASCENDING  SRT-KEY
               INPUT PROCEDURE  INPROC-REPORT
               OUTPUT PROCEDURE OUTPROC-REPORT.
       150-END-PROGRAM.
           CLOSE CARD-FILE    DISK-FILE.
           CLOSE PRINT-FILE1  PRINT-FILE2.
           DISPLAY 'END - DASDSPC' UPON CONSOLE.
           STOP RUN.
           EJECT
       INPROC-REPORT SECTION.
       1000-READ-CARDS.
            READ CARD-FILE AT END
               CLOSE CARD-FILE
               OPEN INPUT CARD-FILE
                                   GO TO 1300-READ-DISK.
           IF CODE-ID1
               IF DIV-PHILA
                   MOVE 'P'  TO  DIVISION-SW
               ELSE
               IF DIV-SPART
                   MOVE 'S'  TO  DIVISION-SW
               ELSE
               IF DIV-TEST
                   MOVE 'T'  TO  DIVISION-SW
               ELSE
               IF DIV-DIST
                   MOVE 'D'  TO  DIVISION-SW.
           IF CODE-ID1
               IF USER-ID1
                   MOVE '1'  TO  TABLE-SW
               ELSE
                   IF SYSTEM-ID1
                       MOVE '2'  TO  TABLE-SW.
           IF CODE-ID1 OR CODE-ID2 OR CODE-ID3
                                   GO TO 1000-READ-CARDS.
           IF TABLE-SW = '1'
               PERFORM 1100-LOAD-TABLE  THRU  1150-TABLE-EXIT.
           IF TABLE-SW = '2'
               PERFORM 1200-LOAD-TABLE  THRU  1250-TABLE-EXIT.
           GO TO 1000-READ-CARDS.
       1100-LOAD-TABLE.
           SET NME-IND UP BY +1.
           MOVE CARD-CODE2  TO  TBL-NME-CODE (NME-IND).
           MOVE CARD-NAME2  TO  TBL-NME      (NME-IND).
       1150-TABLE-EXIT.
           EXIT.
       1200-LOAD-TABLE.
           SET SYS-IND UP BY +1.
           MOVE CARD-CODE2  TO  TBL-SYS-CODE (SYS-IND).
           MOVE CARD-NAME2  TO  TBL-SYS      (SYS-IND).
       1250-TABLE-EXIT.
           EXIT.
       1300-READ-DISK.
           READ DISK-FILE INTO DISK-RECORD AT END
               PERFORM 1425-BUILD-VSAM-SORT
                                   GO TO 1700-READ-CARDS.
           IF VOLUME-ID1 OR PRIME-ID2 OR FREE-ID2
              PERFORM 1400-BUILD-VTOC-SORT THRU 1450-BUILD-VTOC-EXIT.
           IF VOLUME-ID3 OR DATA-ID4 OR INDEX-ID4 OR SPACE-ID5 OR
              LOW-ID6
              PERFORM 1400-BUILD-VSAM-SORT THRU 1450-BUILD-VSAM-EXIT.
           GO TO 1300-READ-DISK.
       1400-BUILD-VTOC-SORT.
           IF VOLUME-ID1
               MOVE DISK-VOLUME1  TO  HOLD-VOLUME
               PERFORM 1600-CREATE-VOL-TABLE THRU 1650-CREATE-EXIT
                                   GO TO 1450-BUILD-VTOC-EXIT.
           IF DISK-NAME2 = SPACES
               MOVE HOLD-NAME  TO  DISK-NAME2
               ELSE
                   MOVE DISK-NAME2 TO HOLD-NAME.
           IF FREE-ID2
               MOVE 'S'  TO  WRK-CODE
               MOVE 'FREE SPACE'  TO  HOLD-USER
           ELSE
               IF VSAM-ID2
                   MOVE 'V'  TO  WRK-CODE
                    MOVE 'VSAM DATASETS'  TO  HOLD-USER
               ELSE
                   PERFORM 1500-SEARCH-TABLE THRU 1550-SEARCH-EXIT.
           MOVE SPACES TO SRT-REC.
           IF WRK-CODE = 'S' OR 'V'
               MOVE '2'  TO  SRT-CODE
           ELSE
               MOVE '1'  TO  SRT-CODE.
           MOVE HOLD-VOLUME  TO  SRT-VOLUME.
           MOVE HOLD-USER  TO  SRT-USER.
           IF PRIME-ID2
               MOVE DISK-SEQ2  TO  SRT-SEQ.
           MOVE HOLD-NAME  TO  SRT-NAME.
           UNSTRING DISK-NUMTRKS DELIMITED BY ','
                                        INTO WORK-BEGIN WORK-NUMTRKS-X.
           UNSTRING WORK-NUMTRKS-X DELIMITED BY ' '
                                        INTO WORK-NUMTRKS.
           COMPUTE WORK-END = (WORK-BEGIN + WORK-NUMTRKS) - 1.
           MOVE WORK-BEGIN TO SRT-BEGIN.
           MOVE WORK-END TO SRT-END.
           MOVE WORK-NUMTRKS TO SRT-NUMTRKS.
           IF PRIME-ID2
               IF WRK-CODE NOT = 'V'
                   ADD SRT-NUMTRKS TBL-VOL-ALLOC (VOL-IND)
                                   TOT-NON-VM-ALLOC
                                   TOT-DASD-ALLOC
               ELSE
                   ADD SRT-NUMTRKS TO TBL-VSM-DASD (VOL-IND).
           IF FREE-ID2
               ADD SRT-NUMTRKS  TO  TBL-VOL-FREE (VOL-IND)
               MOVE SRT-BEGIN  TO  SORT-SEQ-FS.
           MOVE SPACES TO SRT-DATA.
           RELEASE SRT-REC.
       1450-BUILD-VTOC-EXIT.
           EXIT.
       1400-BUILD-VSAM-SORT.
           IF VOLUME-ID3
               MOVE DISK-VOLUME3  TO  HOLD-VOLUME
               PERFORM 1600-CREATE-VOL-TABLE THRU 1650-CREATE-EXIT
                                   GO TO 1450-BUILD-VSAM-EXIT.
           IF DATA-ID4 OR INDEX-ID4
               MOVE 'Y'  TO  DATA-SW.
           IF SPACE-ID5 AND DATA-ON
               MOVE 'Y'  TO  TRK-SW
               MOVE 'N'  TO  DATA-SW
                                   GO TO 1450-BUILD-VSAM-EXIT.
           IF TRACK-ON
              IF LOW-ID6 AND TRACK-ID6
                  EXAMINE DISK-ALLOC6 REPLACIN ALL '-' BY '0'
                  ADD 1 TO ACCUM-EXTENTS
                  ADD DISK-ALLOC6-9 TO ACCUM-NUMTRKS
                                   GO TO 1450-BUILD-VSAM-EXIT
               ELSE
                   PERFORM 1425-BUILD-VSAM-SORT
                   MOVE 'N' TO TRACK-SW.
           IF DATA-ID4 OR INDEX-ID4
               MOVE DISK-NAME4  TO  HOLD-NAME
               PERFORM 1500-SEARCH-TABLE THRU 1550-SEARCH-EXIT.
           GO TO 1450-BUILD-VSAM-EXIT.
       1425-BUILD-VSAM-SORT.
           MOVE SPACES  TO  SRT-REC.
           IF WRK-CODE='V'
               MOVE '2'  TO  SRT-CODE
           ELSE
               MOVE '1'  TO  SRT-CODE.
           MOVE HOLD-VOLUME  TO  SRT-VOLUME.
            MOVE HOLD-USER TO SRT-USER.
           MOVE HOLD-NAME TO SRT-NAME.
           MOVE ACCUM-EXTENTS TO SRT-EXTENTS.
           MOVE ACCUM-NUMTRKS TO SRT-NUMTRKS.
           ADD SRT-NUMTRKS TO TBL-VSM-ALLOC (VOL-IND).
           MOVE ZEROS TO ACCUM-EXTENTS
                         ACCUM-NUMTRKS.
           IF WRK-CODE NOT = 'V'
                ADD SRT-NUMTRKS TO TBL-VOL-ALLOC (VOL-IND)
                                   TOT-NON-VM-ALLOC
                                   TOT-DASD-ALLOC.
           MOVE SPACES TO SRT-DATA.
           RELEASE SRT-REC.
       1450-BUILD-VSAM-EXIT.
           EXIT.
       1500-SEARCH-TABLE.
      *
      * A '@' IN WRK-REACH INDICATES A REACH DISK FILE.
      *
           IF PRIME-ID2  OR  FREE-ID2
               MOVE DISK-NAME2  TO  WORK-NAME.
           IF DATA-ID4  OR  INDEX-ID4
               MOVE DISK-NAME4  TO  WORK-NAME.
           IF WRK-REACH = '@'
               MOVE WRK-FIELD2  TO  WRK-SYS
           ELSE
               MOVE WRK-FIELD1  TO  WRK-SYS.
           SET SYS-IND TO +1.
           SEARCH TABLE-SYSTEM AT END
                   MOVE 'Z'  TO  WRK-CODE
               WHEN TABLE-SYSTEM (SYS-IND) = HIGH-VALUES
                   MOVE 'Z'  TO WRK-CODE
               WHEN WRK-SYS  =  TBL-SYS (SYS-IND)
                   MOVE TBL-SYS-CODE (SYS-IND) TO  WRK-CODE.
       1525-SEARCH-TABLE.
           SET NME-IND TO +1.
           SEARCH TABLE-USER AT END
                   MOVE ' '  TO  HOLD-USER
               WHEN TABLE-USER (NME-IND) = HIGH-VALUES
                   MOVE ' '  TO  HOLD-USER
               WHEN WRK-CODE  =  TBL-NME-CODE (NME-IND)
                   MOVE TBL-NME (NME-IND) TO  HOLD-USER.
       1550-SEARCH-EXIT.
           EXIT.
       1600-CREATE-VOL-TABLE.
           SET VOL-IND TO +1.
           SEARCH TABLE-VOLUME AT END
                   DISPLAY 'NO MORE ENTRIES IN VOLUME TABLE'
                   STOP RUN
               WHEN TABLE-VOLUME (VOL-IND) = HIGH-VALUES
                                   GO TO 1625-CREATE-ENTRY
               WHEN HOLD-VOLUME = TBL-VOL-NAME (VOL-IND)
                                   GO TO 1650-CREATE-EXIT.
       1625-CREATE-ENTRY.
           IF SRT-VM-CODE = 'VM'
               ADD 1         TO  TOT-NUM-VM-VOLS
           ELSE
               ADD 1         TO  TOT-NUM-NON-VM-VOLS.
           ADD 1             TO  TOT-NUM-DASD-VOLS.
           MOVE HOLD-VOLUME  TO  TBL-VOL-NAME  (VOL-IND).
           MOVE ZEROS        TO  TBL-VOL-ALLOC (VOL-IND)
                                 TBL-VSM-ALLOC (VOL-IND)
                                 TBL-VOL-FREE  (VOL-IND)
                                 TBL-VSM-DASD  (VOL-IND).
       1650-CREATE-EXIT.
           EXIT.
        1700-READ-CARDS.
       * READ "VMUSERS DISKMAP" FILE.
           READ CARD-FILE AT END
                                   GO TO 1900-END-INPROC.
           IF CARD-REC3 = SPACES
                                   GO TO 1700-READ-CARDS.
           IF VOLUME-ID  OR  DASH-ID  OR  OVERLAP-ID
                                   GO TO 1700-READ-CARDS.
           EXAMINE CARD-BEGIN-X  REPLACING ALL ' ' BY '0'.
           EXAMINE CARD-END-X    REPLACING ALL ' ' BY '0'.
           EXAMINE CARD-ALLOC-X  REPLACING ALL ' ' BY '0'.
           EXAMINE CARD-BEGIN = CARD-BEGIN * DASD-TRK-CYL.
           EXAMINE CARD-END   = CARD-END   * DASD-TRK-CYL.
           EXAMINE CARD-ALLOC = CARD-ALLOC * DASD-TRK-CYL.
           IF CARD-ALLOC = DASD-TRK-VOL
                                   GO TO 1700-READ-CARDS.
           PERFORM 1800-BUILD-SORT  THRU 1850-BUILD-EXIT.
           GO TO 1700-READ-CARDS.
       1800-BUILD-SORT.
           IF CARD-VOLUME NOT = SPACES
               MOVE CARD-VOLUME  TO  HOLD-VOLUME.
           IF CARD-VOLUME = SPACES
               MOVE HOLD-VOLUME  TO  CARD-VOLUME.
           MOVE SPACES            TO  SRT-REC.
           MOVE CARD-VOLUME       TO  SRT-VOLUME.
           IF GAP-ID
               MOVE 'FREE SPACE'  TO  SRT-USER
               MOVE '2'           TO  SRT-CODE
               MOVE '* GAP * '    TO  SRT-NAME
               MOVE CARD-BEGIN    TO SET-SEQ-FS
           ELSE
               MOVE 'V/M USAGE'   TO  SRT-USER
               MOVE '1'           TO  SRT-CODE
               MOVE CARD-NAME     TO SRT-NAME.
           MOVE 'VM'              TO SRT-VM-CODE.
           MOVE CARD-BEGIN        TO SRT-BEGIN.
           MOVE CARD-END          TO SRT-END.
           MOVE CARD-ALLOC        TO SRT-NUMTRKS.
           PERFORM 1600-CREATE-VOL-TABLE THRU 1650-CREATE-EXIT.
           IF NOT GAP-ID
               ADD SRT-NUMTRKS    TO TBL-VOL-ALLOC (VOL-IND)
                                     TOT-VM-ALLOC
                                     TOT-DASD-ALLOC.
           IF GAP-ID
               ADD SRT-NUMTRKS    TO TBL-VOL-FREE  (VOL-IND).
           MOVE CARD-REC3         TO SRT-DATA.
           RELEASE SRT-REC.
       1850-BUILD-EXIT.
           EXIT.
       1900-END-INPROC.
           EXIT.
           EJECT
       OUTPROC-REPORT SECTION.
           SET VOL-IND  TO  ZORRO.
           GO TO 2050-RETURN-ROUTINE.
       2000-PRINT-HEADINGS.
           MOVE REPORT-TITLE-1  TO  HD-1-REPORT-TITLE.
           MOVE SRT-USER        TO  HD-1-USER.
           MOVE SRT-CODE        TO  HD-1-RPT-NO.
           PERFORM 8010-PRINT-HEAD-1.
           PERFORM 8020-PRINT-HEAD-2.
           PERFORM 8030-PRINT-HEAD-3.
       2050-RETURN-ROUTINE.
           PERFORM 2100-SORT-RETURN THRU 2150-SORT-RETURN-EXIT
                                   UNTIL SRT-REC = HIGH-VALUES.
           PERFORM 2400-VOLUME-BREAK  THRU  2450-VOLUME-BREAK-EXIT.
            PERFORM 2500-USER-BREAK    THRU  2550-USER-BREAK-EXIT.
           PERFORM 2600-DASD-BREAK    THRU  2650-DASD-BREAK-EXIT.
           GO TO 2900-OUTPROC-EXIT.
       2100-SORT-RETURN.
           RETURN SORT-FILE AT END
               MOVE HIGH-VALUES  TO  SRT-REC
                                   GO TO 2150-SORT-RETURN-EXIT.
           IF SRT-VM-CODE = 'VM'
               MOVE SRT-DATA  TO  CARD-REC3
           ON 1
               MOVE SRT-VM-CODE  TO  HOLD-VM-CODE
               MOVE SRT-CODE     TO  HOLD-CODE
               MOVE SRT-USER     TO  HOLD-USER
               PERFORM 2200-PRINT-DETAIL THRU 2250-PRINT-DETAIL-EXIT
                                   GO TO 2150-SORT-RETURN-EXIT.
           IF SRT-VOLUME NOT = HOLD-VOLUME
               PERFORM 2400-VOLUME-BREAK THRU 2450-VOLUME-BREAK-EXIT.
           IF SRT-USER NOT = HOLD-USER
               PERFORM 2500-USER-BREAK   THRU 2550-USER-BREAK-EXIT.
           PERFORM 2200-PRINT-DETAIL THRU 2250-PRINT-DETAIL-EXIT.
       2150-SORT-RETURN-EXIT.
           EXIT.
       2200-PRINT-DETAIL.
           IF LINE-CNT1 > 52
               PERFORM 2000-PRINT-HEADINGS.
           IF SRT-VOLUME NOT = HOLD-VOLUME
               MOVE ' '           TO  DETL-LINE-1
               MOVE SRT-VOLUME    TO  DETL-IMAGE
               MOVE SRT-VOLUME    TO  HOLD-VOLUME
               MOVE SRT-VM-CODE   TO  HOLD-VM-CODE
               PERFORM 8100-PRINT-DETAIL-LINE
               ADD 2              TO LINE-CNT1.
           MOVE ' '               TO  DETL-LINE-1.
           IF SRT-VM-CODE = 'VM' OR SRT-CODE = '2'
               MOVE SRT-NAME           TO  DETL-NAME
           ELSE
               IF SRT-NAME NOT = HOLD-NAME
                   MOVE SRT-NAME        TO  HOLD-NAME
                   MOVE SRT-NAME        TO  DETL-NAME.
           IF SRT-EXTENTS NUMERIC
               MOVE '   * VSAM *'     TO  DETL-BEG-TRACK-X
               MOVE '   * VSAM *'     TO  DETL-END-TRACK-X
               MOVE SRT-EXTENTS       TO  DETL-EXTENTS
           ELSE
               MOVE SRT-BEGIN         TO  DETL-BEG-TRACK
               MOVE SRT-END           TO  DETL-END-TRACK
               MOVE SRT-SEQ           TO  DETL-SEQ.
           IF SRT-VM-CODE = 'VM'
               MOVE CARD-BEGIN        TO  DETL-BEG-TRACK
               MOVE CARD-END          TO  DETL-END-TRACK.
           MOVE SRT-NUMTRKS           TO  DETL-ALC-TRACK.
           ADD  SRT-NUMTRKS           TO  TOT-VOLUME-ALLOC
                                          TOT-USER-ALLOC.
           PERFORM 2300-SEARCH-TABLE  THRU  2350-SEARCH-EXIT.
      * TBL-VOL-ALLOC CALCULATES PERCENTAGE BASED ON ALLOCATED SPACE
      * DASD-TRK-VOL CALCULATES PERCENTAGE BASED ON ENTIRE DASD SPACE
           IF SRT-CODE = '1'
               MOVE TBL-VOL-ALLOC (VOL-IND)  TO  HOLD-DIVISOR
           ELSE
               MOVE DASD-TRK-ALLOC           TO  HOLD-DIVISOR.
           IF DETL-PERCENT-X NOT = ' ERR '
               COMPUTE WORK-PERCENT ROUNDED =
                   (SRT-NUMTRKS / HOLD-DIVISOR)
                       ON SIZE ERROR
                           MOVE ' SZE ' TO DETL-PERCENT-X.
           IF DETL-PERCENT-X NOT = ' SZE '
                COMPUTE DETL-PERCENT = WORK-PERCENT * 100.
           MOVE '%'               TO  DETL-PERCENT-SIGN.
           MOVE ' '               TO  P-C1.
           PERFORM 8100-PRINT-DETAIL-LINE.
           ADD 1                  TO  LINE-CNT1.
           IF SRT-SEQ NOT < ' 1' AND NOT > '99'
              PERFORM 8120-PRINT-DETAIL-LINE.
           IF SRT-EXTENTS NUMERIC
              IF SRT-EXTENTS > 1
                  PERFORM 8120-PRINT-DETAIL-LINE.
       2250-PRINT-DETAIL-EXIT.
           EXIT.
       2300-SEARCH-TABLE.
           SET VOL-IND TO +1.
           SEARCH TABLE-VOLUME AT END
                   MOVE ' ERR '  TO DETL-PERCENT-X
               WHEN TABLE-VOLUME (VOL-IND) = HIGH-VALUES
                   MOVE ' ERR '  TO DETL-PERCENT-X
               WHEN SRT-VOLUME  =  TBL-VOL-NAME (VOL-IND)
                   NEXT SENTENCE.
       2350-SEARCH-EXIT.
           EXIT.
       2400-VOLUME-BREAK.
           MOVE ' '                                 TO  DETL-LINE-1.
           MOVE 'TOTAL VOLUME DASD ALLOCATION FOR ' TO  DETL-IMAGE.
           MOVE HOLD-VOLUME                         TO  DETL-USER-NAME.
           MOVE TOT-VOLUME-ALLOC                    TO  DETL-ALC-TRACK.
      * TBL-VOL-ALLOC CALCULATES PERCENTAGE BASED ON ALLOCATED SPACE
      * DASD-TRK-VOL CALCULATES PERCENTAGE BASED ON ENTIRE DASD SPACE
           IF HOLD-VM-CODE NOT = 'VM'  AND  HOLD-CODE = '1'
               MOVE TBL-VOL-ALLOC (VOL-IND)  TO  HOLD-DIVISOR
           ELSE
               MOVE DASD-TRK-VOL               TO  HOLD-DIVISOR.
           COMPUTE WORK-PERCENT ROUNDED =
               (TOT-VOLUME-ALLOC / HOLD-DIVISOR)
                   ON SIZE ERROR
                       MOVE ' ERR ' TO DETL-PERCENT-X.
           IF DETL-PERCENT-X NOT = ' ERR '
               COMPUTE DETL-PERCENT = WORK-PERCENT * 100.
           MOVE '%'          TO  DETL-PERCENT-SIGN.
           MOVE ' '               TO  P-C1.
           PERFORM 8100-PRINT-DETAIL-LINE.
           ADD 1              TO  LINE-CNT1.
           MOVE ZEROS         TO  TOT-VOLUME-ALLOC.
       2450-VOLUME-BREAK-EXIT.
           EXIT.
       2500-USER-BREAK.
           MOVE ' '                                TO  DETL-LINE-1.
           MOVE 'TOTAL USER DASD ALLOCATIONS FOR ' TO  DETL-IMAGE.
           MOVE HOLD-USER                          TO  DETL-USER-NAME.
           MOVE TOT-USER-ALLOC                     TO  DETL-ALC-TRACK.
      * DASD-TRK-VOL * TOT-NUM-VM-VOLS CALCULATES PERCENTAGE BASED ON
      * THE ENTIRE DASD SPACE FOR ALL THE VM VOLUMES.
      * DASD-TRK-VOL * TOT-NUM-NON-VM-VOLS CALCULATES PERCENTAGE BASED
      * ON THE ENTIRE DASD SPACE FOR ALL THE NON/VM VOLUMES.
      * DASD-TRK-VOL * TOT-NUM-DASD-VOLS  CALCULATES PERCENTAGE BASED
      * ON THE ENTIRE DASD SPACE FOR BOTH VM AND NON/VM VOLUMES.
           IF HOLD-VM-CODE = 'VM'  AND  HOLD-CODE = '1'
               COMPUTE HOLD-DIVISOR = DASD-TRK-VOL * TOT-NUM-VM-VOLS
           ELSE
               IF HOLD-VM-CODE NOT = 'VM'  AND  HOLD-CODE = '1'
                  MOVE TOT-NON-VM-ALLOC  TO  HOLD-DIVISOR
               ELSE
                   IF HOLD-USER = 'VSAM DATASETS'
                     COMPUTE HOLD-DIVISOR =
                                     DASD-TRK-VOL * TOT-NUM-NON-VM-VOLS
                ELSE
                     COMPUTE HOLD-DIVISOR =
                                       DASD-TRK-VOL * TOT-NUM-DASD-VOLS.
           COMPUTE WORK-PERCENT ROUNDED =
               (TOT-USER-ALLOC / HOLD-DIVISOR)
                   ON SIZE ERROR
                       MOVE ' ERR ' TO DETL-PERCENT-X.
           IF DETL-PERCENT-X NOT = ' ERR '
               COMPUTE DETL-PERCENT = WORK-PERCENT * 100.
           MOVE '%'          TO  DETL-PERCENT-SIGN.
           MOVE '3'          TO  HD-1-RPT-NO.
           IF LINE-CNT2 > 52
               PERFORM 3000-PRINT-HEADINGS.
           MOVE DETL-LINE-1  TO  DETL-LINE-2.
           MOVE '0'          TO  P-C1.
           PERFORM 8100-PRINT-DETAIL-LINE.
           MOVE ' '          TO  P-C1.
           MOVE ' '          TO  DETL-LINE-1.
           PERFORM 8100-PRINT-DETAIL-LINE.
           IF HOLD-USER NOT = ('FREE SPACE'  AND  'VSAM DATASETS')
               MOVE '0' TO P-C2
               PERFORM 8100-PRINT-DETAIL-LINE.
           ADD 1             TO  LINE-CNT2.
           MOVE 99           TO  LINE-CNT1.
           MOVE ZEROS        TO  TOT-USER-ALLOC.
           MOVE SRT-USER     TO  HOLD-USER.
           MOVE SPACES       TO  HOLD-VOLUME.
           IF SRT-CODE NOT = HOLD-CODE
               MOVE ZEROS    TO  PAGE-CNT1
               MOVE SRT-CODE TO  HOLD-CODE.
       2550-USER-BREAK-EXIT.
           EXIT.
       2600-DASD-BREAK.
           MOVE ' '                        TO  DETL-LINE-1.
           MOVE 'TOTAL  V/M  ALLOCATION '  TO  DETL-IMAGE.
           MOVE TOT-VM-ALLOC               TO  DETL-ALC-TRACK.
      * DASD-TRK-VOL * TOT-NUM-VM-VOLS CALCULATES PERCENTAGE BASED ON
      * THE ENTIRE DASD SPACE FOR ALL THE VM VOLUMES.
           COMPUTE HOLD-DIVISOR = DASD-TRK-VOL * TOT-NUM-VM-VOLS.
           COMPUTE WORK-PERCENT ROUNDED =
               (TOT-VM-ALLOC / HOLD-DIVISOR)
                   ON SIZE ERROR
                       MOVE ' ERR '        TO DETL-PERCENT-X.
           IF DETL-PERCENT-X NOT = ' ERR '
               COMPUTE DETL-PERCENT = WORK-PERCENT * 100.
           MOVE '%'                        TO  DETL-PERCENT-SIGN.
           MOVE DETL-LINE-1                TO  DETL-LINE-2.
           MOVE '0'                        TO  P-C1 P-C2.
           PERFORM 8100-PRINT-DETAIL-LINE.
           PERFORM 8110-PRINT-DETAIL-LINE.
           MOVE ' '                        TO DETL-LINE-1.
           MOVE 'TOTAL NON-VM ALLOCATION'  TO DETL-IMAGE.
           MOVE TOT-NON-VM-ALLOC           TO DETL-ALC-TRACK.
      * DASD-TRK-VOL * TOT-NUM-NON-VM-VOLS CALCULATES PERCENTAGE BASED
      * ON THE ENTIRE DASD SPACE FOR ALL THE NON/VM VOLUMES.
           COMPUTE HOLD-DIVISOR = DASD-TRK-VOL * TOT-NUM-NON-VM-VOLS.
           COMPUTE WORK-PERCENT ROUNDED =
               (TOT-NON-VM-ALLOC / HOLD-DIVISOR)
                   ON SIZE ERROR
                       MOVE ' ERR '       TO DETL-PERCENT-X.
           IF DETL-PERCENT-X NOT = ' ERR '
               COMPUTE DETL-PERCENT = WORK-PERCENT * 100.
           MOVE '%'                       TO DETL-PERCENT-SIGN.
           MOVE DETL-LINE-1               TO DETL-LINE-2.
           MOVE ' '                       TO  P-C1 P-C2.
           PERFORM 8100-PRINT-DETAIL-LINE.
            PERFORM 8110-PRINT-DETAIL-LINE.
           MOVE ' '                        TO  DETL-LINE-1.
           MOVE 'TOTAL  DASD  ALLOCATION'  TO  DETL-IMAGE.
           MOVE TOT-DASD-ALLOC             TO  DETL-ALC-TRACK.
      * DASD-TRK-VOL * TOT-NUM-DASD-VOLS CALCULATES PERCENTAGE BASED ON
      * THE ENTIRE DASD SPACE FOR ALL THE VM AND NON/VM VOLUMES.
           COMPUTE HOLD-DIVISOR = DASD-TRK-VOL * TOT-NUM-DASD-VOLS.
           COMPUTE WORK-PERCENT ROUNDED =
               (TOT-DASD-ALLOC / HOLD-DIVISOR)
                   ON SIZE ERROR
                       MOVE ' ERR '       TO DETL-PERCENT-X.
           IF DETL-PERCENT-X NOT = ' ERR '
               COMPUTE DETL-PERCENT = WORK-PERCENT * 100.
           MOVE '%'                       TO  DETL-PERCENT-SIGN.
           MOVE DETL-LINE-1               TO  DETL-LINE-2.
           MOVE ' '                       TO P-C1  P-C2
           PERFORM 8100-PRINT-DETAIL-LINE.
           PERFORM 8110-PRINT-DETAIL-LINE.
           MOVE '4'     TO  HD-1-RPT-NO.
           MOVE ZEROS   TO  PAGE-CNT2.
           PERFORM 3000-PRINT-HEADINGS.
           SET VOL-IND  TO  ZORRO.
       2625-DASD-TOTAL.
           SET VOL-IND UP BY +1.
           IF TABLE-VOLUME (VOL-IND) = HIGH-VALUES
                                   GO TO 2630-DASD-BREAK-TOTAL.
           IF LINE-CNT2 > 52
               PERFORM 3000-PRINT-HEADINGS.
           MOVE ' '                                  TO  DETL-LINE-1.
           MOVE 'TOTAL NON-VSAM DASD ALLOCATION - '  TO  DETL-IMAGE.
           MOVE TBL-VOL-NAME  (VOL-IND)              TO  DETL-USER-NAME.
           COMPUTE DETL-ALC-TRACK =
                TBL-VOL-ALLOC (VOL-IND) - TBL-VSM-ALLOC (VOL-IND).
           COMPUTE TOT-VOL-ALLOC = TOT-VOL-ALLOC +
               (TBL-VOL-ALLOC (VOL-IND) - TBL-VSM-ALLOC (VOL-IND)).
           COMPUTE WORK-PERCENT ROUNDED =
               (TBL-VOL-ALLOC (VOL-IND) - TBL-VSM-ALLOC (VOL-IND))
                                                         / DASD-TRK-VOL
                   ON SIZE ERROR
                       MOVE ' ERR '       TO DETL-PERCENT-X.
           IF DETL-PERCENT-X NOT = ' ERR '
               COMPUTE DETL-PERCENT = WORK-PERCENT * 100.
           MOVE '%'                       TO  DETL-PERCENT-SIGN.
           MOVE DETL-LINE-1                          TO  DETL-LINE-2.
           MOVE '0'                       TO  P-C2.
           PERFORM 8110-PRINT-DETAIL-LINE.
           ADD 2                                     TO  LINE-CNT2.
           MOVE ' '                                  TO  DETL-LINE-1.
           MOVE 'TOTAL   VSAM   DASD ALLOCATION - '  TO  DETL-IMAGE.
           MOVE TBL-VOL-NAME  (VOL-IND)              TO  DETL-USER-NAME.
           MOVE TBL-VSM-ALLOC (VOL-IND)              TO  DETL-ALC-TRACK.
           COMPUTE TOT-VSM-ALLOC = TOT-VSM-ALLOC
                                              + TBL-VSM-ALLOC (VOL-IND).
           COMPUTE WORK-PERCENT ROUNDED =
               (TBL-VSM-ALLOC (VOL-IND) / DASD-TRK-VOL)
                   ON SIZE ERROR
                       MOVE ' ERR '       TO DETL-PERCENT-X.
           IF DETL-PERCENT-X NOT =' ERR '
               COMPUTE DETL-PERCENT = WORK-PERCENT * 100.
           MOVE '%'                       TO  DETL-PERCENT-SIGN.
           MOVE DETL-LINE-1                           TO  DETL-LINE-2.
           MOVE ' '                       TO  P-C2.
           PERFORM 8110-PRINT-DETAIL-LINE.
           ADD 1                                      TO  LINE-CNT2.
           MOVE ' '                                   TO  DETL-LINE-1.
           MOVE 'TOTAL NON-VSAM DASD FREE-SPACE - '   TO  DETL-IMAGE.
            MOVE TBL-VOL-NAME  (VOL-IND)               TO  DETL-USER-NAME.
           MOVE TBL-VOL-FREE  (VOL-IND)               TO  DETL-ALC-TRACK.
           COMPUTE TOT-VOL-FREE = TOT-VOL-FREE + TBL-VOL-FREE (VOL-IND).
           COMPUTE WORK-PERCENT ROUNDED =
               (TBL-VOL-FREE (VOL-IND) / DASD-TRK-VOL)
                   ON SIZE ERROR
                       MOVE ' ERR '       TO DETL-PERCENT-X.
           IF DETL-PERCENT-X NOT = ' ERR '
               COMPUTE DETL-PERCENT = WORK-PERCENT * 100.
           MOVE '%'                       TO  DETL-PERCENT-SIGN.
           MOVE DETL-LINE-1               TO  DETL-LINE-2.
           MOVE ' '                       TO  P-C2.
           PERFORM 8110-PRINT-DETAIL-LINE.
           ADD 1                                     TO  LINE-CNT2.
           MOVE ' '                                  TO  DETL-LINE-1.
           MOVE 'TOTAL   VSAM   DASD FREE-SPACE - '  TO  DETL-IMAGE.
           MOVE TBL-VOL-NAME  (VOL-IND)              TO  DETL-USER-NAME.
           COMPUTE DETL-ALC-TRACK =
                      TBL-VSM-DASD (VOL-IND) - TBL-VSM-ALLOC (VOL-IND).
           COMPUTE TOT-VSM-FREE = TOT-VSM-FREE +
                     (TBL-VSM-DASD (VOL-IND) - TBL-VSM-ALLOC (VOL-IND)).
           COMPUTE WORK-PERCENT ROUNDED =
               (TBL-VSM-DASD (VOL-IND) - TBL-VSM-ALLOC (VOL-IND))
                                                          / DASD-TRK-VOL
                   ON SIZE ERROR
                       MOVE ' ERR '       TO DETL-PERCENT-X.
           IF DETL-PERCENT-X NOT = ' ERR '
               COMPUTE DETL-PERCENT = WORK-PERCENT * 100.
           MOVE '%'                       TO  DETL-PERCENT-SIGN.
           MOVE DETL-LINE-1               TO  DETL-LINE-2.
           MOVE ' '                       TO  P-C2.
           PERFORM 8110-PRINT-DETAIL-LINE.
           ADD 1                          TO  LINE-CNT2.
           GO TO 2625-DASD-TOTAL.
       2630-DASD-BREAK-TOTAL.
           MOVE ' '                                  TO  DETL-LINE-1.
           MOVE 'TOTAL NON-VSAM DASD ALLOCATION  '   TO  DETL-IMAGE.
           MOVE TOT-VOL-ALLOC                        TO  DETL-ALC-TRACK.
      * DASD-TRK-VOL * TOT-NUM-DASD-VOLS CALCULATES PERCENTAGE BASED ON
      * THE ENTIRE DASD SPACE FOR ALL THE VM AND NON/VM VOLUMES.
           COMPUTE HOLD-DIVISOR = DASD-TRK-VOL * TOT-NUM-DASD-VOLS.
           COMPUTE WORK-PERCENT ROUNDED = (TOT-VOL-ALLOC / HOLD-DIVISOR)
                   ON SIZE ERROR
                       MOVE ' ERR '       TO DETL-PERCENT-X.
           IF DETL-PERCENT-X NOT = ' ERR '
               COMPUTE DETL-PERCENT = WORK-PERCENT * 100.
           MOVE '%'                       TO  DETL-PERCENT-SIGN.
           MOVE DETL-LINE-1                          TO  DETL-LINE-2.
           MOVE '0'                       TO  P-C2.
           PERFORM 8110-PRINT-DETAIL-LINE.
           MOVE ' '                                  TO  DETL-LINE-1.
           MOVE 'TOTAL   VSAM   DASD ALLOCATION  '   TO  DETL-IMAGE.
           MOVE TOT-VSM-ALLOC                       TO  DETL-ALC-TRACK.
           COMPUTE WORK-PERCENT ROUNDED = (TOT-VSM-ALLOC / HOLD-DIVISOR)
                   ON SIZE ERROR
                       MOVE ' ERR '       TO DETL-PERCENT-X.
           IF DETL-PERCENT-X NOT = ' ERR '
               COMPUTE DETL-PERCENT = WORK-PERCENT * 100.
           MOVE '%'                       TO  DETL-PERCENT-SIGN.
           MOVE DETL-LINE-1                          TO  DETL-LINE-2.
           MOVE ' '                       TO  P-C2.
           PERFORM 8110-PRINT-DETAIL-LINE.
           MOVE ' '                                  TO  DETL-LINE-1.
           MOVE 'TOTAL NON-VSAM DASD FREE-SPACE   '  TO  DETL-IMAGE.
           MOVE TOT-VOL-FREE                         TO  DETL-ALC-TRACK.
           COMPUTE WORK-PERCENT ROUNDED = (TOT-VOL-FREE / HOLD-DIVISOR)
                    ON SIZE ERROR
                       MOVE ' ERR '       TO DETL-PERCENT-X.
           IF DETL-PERCENT-X NOT = ' ERR '
               COMPUTE DETL-PERCENT = WORK-PERCENT * 100.
           MOVE '%'                       TO  DETL-PERCENT-SIGN.
           MOVE DETL-LINE-1                          TO  DETL-LINE-2.
           MOVE ' '                       TO  P-C2.
           PERFORM 8110-PRINT-DETAIL-LINE.
           MOVE ' '                                  TO  DETL-LINE-1.
           MOVE 'TOTAL   VSAM   DASD FREE-SPACE   '  TO  DETL-IMAGE.
           MOVE TOT-VSM-FREE                         TO  DETL-ALC-TRACK.
           COMPUTE WORK-PERCENT ROUNDED = (TOT-VSM-FREE / HOLD-DIVISOR)
                   ON SIZE ERROR
                       MOVE ' ERR '       TO DETL-PERCENT-X.
           IF DETL-PERCENT-X NOT = ' ERR '
               COMPUTE DETL-PERCENT = WORK-PERCENT * 100.
           MOVE '%'                       TO  DETL-PERCENT-SIGN.
           MOVE DETL-LINE-1               TO  DETL-LINE-2.
           MOVE ' '                       TO  P-C2.
           PERFORM 8110-PRINT-DETAIL-LINE.
       2650-DASD-BREAK-EXIT.
           EXIT.
       2900-OUTPROC-EXIT.
           EXIT.
           EJECT
       MISC-RTNS SECTION.
       3000-PRINT-HEADINGS SECTION.
           MOVE REPORT-TITLE-2  TO  HD-1-REPORT-TITLE.
           MOVE SPACES          TO  HD-1-USER.
           PERFORM 8040-PRINT-HEAD-1.
           PERFORM 8050-PRINT-HEAD-2.
           PERFORM 8060-PRINT-HEAD-3.
           MOVE ' '  TO  DETL-LINE-2.
           PERFORM 8110-PRINT-DETAIL-LINE.
       8010-PRINT-HEAD-1 SECTION.
           MOVE CURRENT-DATE  TO  HD-1-RUN-DATE.
           MOVE TIME-OF-DAY   TO  HD-1-RUN-TIME.
           EXAMINE HD-1-RUN-TIME REPLACING ALL ' ' BY '.'.
           ADD 1              TO  PAGE-CNT1.
           MOVE PAGE-CNT1     TO  HD-1-PAGE-NO.
           MOVE '1'                       TO  P-C1.
           WRITE PRT-REC1 FROM HEAD-LINE-1 AFTER POSITIONING P-C1 LINES.
           MOVE ZEROS         TO  LINE-CNT1.
       8020-PRINT-HEAD-2 SECTION.
           MOVE '0'                       TO  P-C1.
           WRITE PRT-REC1 FROM HEAD-LINE-2 AFTER POSITIONING P-C1 LINES.
       8030-PRINT-HEAD-3 SECTION.
           MOVE ' '                       TO  P-C1.
           WRITE PRT-REC1 FROM HEAD-LINE-3 AFTER POSITIONING P-C1 LINES.
       8040-PRINT-HEAD-1 SECTION.
           MOVE CURRENT-DATE  TO  HD-1-RUN-DATE.
           MOVE TIME-OF-DAY   TO  HD-1-RUN-TIME.
           EXAMINE HD-1-RUN-TIME REPLACING ALL ' ' BY '.'.
           ADD 1              TO  PAGE-CNT2.
           MOVE PAGE-CNT2     TO  HD-1-PAGE-NO.
           WRITE PRT-REC2 FROM HEAD-LINE-1  AFTER POSITIONING P-C2 LINES.
           MOVE ZEROS         TO  LINE-CNT2.
       8050-PRINT-HEAD-2 SECTION.
           WRITE PRT-REC2 FROM HEAD-LINE-2 AFTER POSITIONING P-C2 LINES.
           MOVE '0'                       TO  P-C2.
       8060-PRINT-HEAD-3 SECTION.
           MOVE ' '                       TO  P-C2.
           WRITE PRT-REC2 FROM HEAD-LINE-3 AFTER POSITIONING P-C2 LINES.
       8100-PRINT-DETAIL-LINE SECTION.
           WRITE PRT-REC1 FROM DETL-LINE-1 AFTER POSITIONING P-C1 LINES.
           MOVE SPACES TO DETL-LINE-1.
        8110-PRINT-DETAIL-LINE SECTION.
           WRITE PRT-REC2 FROM DETL-LINE-2 AFTER POSITIONING P-C2 LINES.
       8120-PRINT-DETAIL-LINE SECTION.
           WRITE PRT-REC1 FROM DETL-LINE-1 AFTER POSITIONING '+' LINES.
           MOVE ' ' TO DETL-LINE-1.
Robert Ferro
Systems Programmer
Progress Lighting (USA)  ) Xephon 1988


























































