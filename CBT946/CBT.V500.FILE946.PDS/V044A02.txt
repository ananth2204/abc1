Automatic expiration of spool files

In order to keep the VM spool space as small as possible, it is
advantageous to clean up the spool space regularly.  Under
VM/HPO Release 5 the number of spool files possible has been
increased to 9900 per user, resulting in a change in the
structure of the SFBLOCK.  This Assembler program has been
written for VM/SP HPO Release 5 and will not work under
VM/SP Release 5 nor previous releases of VM/SP HPO.

The program examines the SFBLOCK using the new
DIAGNOSE code X'D8'.  It then issues a CP PURGE
command against each spool file in the reader queue older than
the number of days specified in the parameter list.  The
'number of days' is the only parameter required.  A value of
zero will delete all reader files.

This module can be implemented from VMUTIL, or a similar
system, to clean up the spool space daily.  A sample entry in
VMUTIL's wake-up file could be as follows:

     .........1.........2.........3.........4....
     ALL     00:07:00 xx/xx/xx RDRPUR 6

The virtual machine requires class D authority to execute this
program.

A log of the files deleted is kept in RDRPURGE LOGFILE A.
This file is refreshed after each execution.

RDRPUR   START ,
         REGEQU ,
         USING *,R12               R12 IS BASE
         STM   R14,R12,12(R13)     SAVE CALLERS REGISTERS
         LR    R12,R15             LOAD BASE WITH ENTRY POINT ADDRESS
         ST    R13,SAVEA+4         CHAIN CALLERS SAVE AREA TO OURS
         LR    R15,R13             POINT R15 TO CALLERS SAVE AREA
         BAL   R13,SAVEA+72        POINT TO OUR SAVEA AND BRANCH ROUND
SAVEA    DS    18F
         ST    R13,8(0,R15)        CHAIN OUR SAVE AREA TO CALLERS
* GET THE PURGE DATE
         LR    R15,R0              POINT TO EXTENDED PLIST
         LM    R2,R3,4(R15)        GET PARM START AND END IN R2 R3
         SR    R3,R2               GET LENGTH OF PARM IN R3 = N+1
         BNP   BADPARM             IF NOT POSITIVE NO PARM GIVEN
         CH    R3,=H'3'            CHECK MAXIMUM PARM LENGTH
         BH    BADPARM             IF EXCEEDED PARM IS INVALID
         CLI   0(R2),C'0'          ************
         BL    BADPARM             *          *
         CLI   0(R2),C'9'          * VALIDATE *
         BH    BADPARM             *   PARM   *
         LA    R2,1(0,R2)          *          *
         BCT   R3,*-20             ************
         LM    R2,R3,4(R15)        GET PARM START AND END IN R2 R3
         SR    R3,R2               GET LENGTH OF PARM IN R3 = N+1
         LA    R3,15(0,R3)         INCREMENT R3 BY 15 TO GET X'1N'
         EX    R3,PACKIT           GET NUMBER OF DAYS PACKED
         CP    NUMDAYS(2),=PL2'365'     CHECK MAXIMUM PARM VALUE
         BH    BADPARM             IF EXCEEDED PARM IS INVALID
         LA    R2,VMDATE           POINT TO DIAG PARM LIST
         DC    X'83',X'20',XL2'000C'    GET SYSTEM DATE
         BAL   R14,CONVDATE        CONVERT DATE TO JULIAN FORMAT
         CP    JULDATE+1(2),NUMDAYS(2) ***********
         BH    GETPURGE                *         *
         SP    JULDATE(3),=PL2'635'    * COMPUTE *
         CP    LPYEAR(1),=PL1'1'       *  PURGE  *
         BNE   GETPURGE                *  DATE   *
         AP    JULDATE(3),=PL1'1'      *         *
GETPURGE SP    JULDATE(3),NUMDAYS(2)   *         *
         ZAP   PURDATE(3),JULDATE(3)   ***********
* GET THE SFBLOCK AND PURGE EXPIRED FILES
         FSERASE 'RDRPUR LOGFILE A'  ERASE OLD LOG FILE
GETSFBLK LA    R2,SSIPARM          POINT TO DIAG PARM LIST
         DC    X'83',X'20',XL2'00D8'    GET SFBLOCK
         BNZ   FINISHED            END OF RDR CHAIN OR QUEUE EMPTY
         MVC   VMDATE(8),SFBDATE
         BAL   R14,CONVDATE        CONVERT DATE TO JULIAN FORMAT
         MVC   D8FILID(2),SFBFILID SET PRIOR SPOOL ID
         XC    D8FUNC(2),D8FUNC    CLEAR SUBFUCTION
         MVC   D8USER(8),SFBUSER   GET PREVIOUS USERID
         CP    PURDATE(3),JULDATE(3)
         BL    GETSFBLK            IF LOW FILE NOT PURGED
         XC    D8FILID(2),D8FILID  CLEAR D8FILID FOR NEXT GET SFBLOCK
         MVC   PURUSER(8),SFBUSER  SET PURGE USERID
         LH    R0,SFBFILID         GET PURGE SPOOLID IN R0
         CVD   R0,WORK
         UNPK  WORK(5),WORK+5(3)
         OI    WORK+4,X'F0'        CHANGE ZONE TO X'F0'
         MVC   PURSPID(4),WORK+1   CONVERTED SPOOLID TO CMD
         LA    R2,PURCMD           POINT TO PURGE CMD AREA
         LA    R3,PURCMDL          GET LENGTH ON PURGE CMD
         DC    X'83',X'23',XL2'0008'    ISSUE PURGE RDR CMD
         FSWRITE 'RDRPUR LOGFILE A',BUFFER=PURCMD,BSIZE=40
         B     GETSFBLK
BADPARM  WRTERM 'INVALID NUMBER OF DAYS SPECIFIED'
FINISHED XR    R15,R15             SET RETURN CODE 0
         L     R13,SAVEA+4         POINT BACK TO CALLER SAVEA
         L     R14,12(0,R13)       RESTORE R14
         LM    R0,R12,20(R13)      RESTORE R0 TO R12
         BR    R14                 RETURN TO CALLER
PACKIT   PACK  NUMDAYS(0),0(0,R2)  PACK NUMBER OF DAYS
*  ROUTINE TO CONVERT DATE FROM MM/DD/YY FORMAT TO JULIAN FORMAT
CONVDATE PACK  YEAR(2),VMDATE+6(2) GET YEAR PACKED
         PACK  MONTH(2),VMDATE(2)  GET MONTH PACKED
         PACK  DAY(2),VMDATE+3(2)  GET DAY PACKED
         CP    MONTH(2),DAYSTAB+4(2) CHECK IF MARCH OR LATER
         BL    NOTLEAP             IF NOT DONT CORRECT FOR LEAPYEAR
         PACK  JULDATE(4),VMDATE+5(2)    ELSE GET YEAR
         DP    JULDATE(4),=PL1'4'  DIVIDE BY 4
         CP    LPYEAR(1),=PL1'0'   IF NO REMAINDER THEN
         BNE   NOTLEAP                 ITS NOT A LEAPYEAR
         AP    DAY(2),=PL1'1'      OTHERWISE ADJUST FOR LEAPYEAR
NOTLEAP  LA    R0,12               SET INDEX FOR LOOP
         LA    R1,DAYSTAB          POINT TO CUMULATIVE TABLE OF DAYS
         CP    MONTH(2),0(2,R1)    CHECK WHICH MONTH
         BE    *+12                IF FOUND GO CONVERT
         LA    R1,2(0,R1)          ELSE BUMP TO NEXT ENTRY IN TABLE
         BCT   R0,*-14             AND CONTINUE
         AP    DAY(2),24(2,R1)     ADD TOTAL DAYS FOR MONTH
         MVO   MONTH(2),YEAR(2)    0YY+0MM+DDD+ TO 0YY+YY++DDD+
         MVC   MONTH+1(1),MONTH    0YY+YY++DDD+ TO 0YY+YYYYDDD+
         ZAP   JULDATE(3),MONTH+1(3)    GET JULIAN DATE
         BR    R14                 RETURN TO CALLER
WORK     DS    D
PURCMD   DC    CL8'PURGE'          ** PURGE COMMAND AREA **
PURUSER  DC    CL9' '
PURRDR   DC    CL7'READER '
PURSPID  DC    CL8' '
PURCMDL  EQU   *-PURCMD
VMDATE   DC    CL8'MM/DD/YY',CL24' '
YEAR     DC    PL2'0'
MONTH    DC    PL2'0'
DAY      DC    PL2'0'
JULDATE  DC    XL3'0'
LPYEAR   DC    PL1'0'
NUMDAYS  DC    PL2'0'
PURDATE  DC    PL3'0'
DAYSTAB  DC    PL2'1,2,3,4,5,6,7,8,9,10,11,12'
         DC    PL2'0,31,59,90,120,151,181,212,243,273,304,334,365,0'
SSIPARM  DS    0D   **    PARMS FOR DIAG X'D8' **
D8FILID  DC    XL2'0'              SPOOL FILE ID OF PREVIOUS FILE
D8FUNC   DC    XL2'0'              SUBFUNCTION CODE
D8RETBUF DC    AL4(SFBLOCK)        ADDRESS OF BUFFER
D8USER   DC    CL8' '              VIRTUAL MACHINE ID
D8READ   DC    XL1'20'             READ NEXT READER SFBLOCK
D8BUFSZ  DC    XL1'14'             SFBLOCK SIZE = 20 DOUBLEWORDS
D8ANYVM  DC    XL1'80'             RETURN NEXT SFBLACK REAGARDLESS
D8RESRVD DC    XL5'0'              RESERVED
SFBLOCK  DS    0D   ******* SFBLOCK MAPPING *******
SFBPNT   DS    AL4   ADDRESS OF NEXT SFBLOCK
SFBSTART DS    XL4   DASD LOCATION OF FIRST PAGE
SFBUSER  DS    CL8   VM USER ID OF FILE OWNER
SFBORIG  DS    CL8   VM USER ID OF FILE ORIGIN
SFBRECNO DS    CL4   NUMBER OF DATA RECORDS IN FILE
SFBRECSZ DS    CL2   LOGICAL RECORD SIZE
SFBFILID DS    BL2   BINARY SYSTEM FILE NUMBER
SFBFLAG  DS    XL1   S*1 FLAGS
SFBTYPE  DS    XL1   S*2 DEVTYPE FOR SPOOL OUTPUT
SFBMISC1 DS    XL2   USE VARIES ACCORDING TO CALLER
SFBRECS  DS    XL4   ADDRESS OF RECBLOCKS FOR ACTIVE FILE
SFBFNAME DS    CL12  FILE NAME
SFBFTYPE DS    CL12  FILE TYPE
SFBDATE  DS    CL8   CREATION DATE OF SPOOL FILE
SFBTIME  DS    CL8   CREATION TIME OF SPOOL FILE
SFBLAST  DS    XL4   DASD LOCATION OF LAST PAGE
SFBCOPY  DS    BL2   NUMBER OF COPIES REQUESTED
SFBCLASS DS    XL1   S*3 SPOOL FILE CLASS
SFBFLAG2 DS    XL1   S*4 FLAGS
SFBDIST  DS    CL8   DISTRIBUTION CODE
SFBFLASH DS    CL4   3800 FLASH FORMS CODE
SFBSTCPY DS    BL1   S*5 CURRENT STARTING COPY NUMBER
SFBFLAG3 DS    XL1   S*6 FLAGS
SFBCKPMP DS    BL1   S*7 CHECKPOINT MAP NUMBER FOR SLOT
SFBFLAG4 DS    XL1   S*8 FLAGS
SFBUFORM DS    CL8   USER SPECIFIED FORM NUMBER
SFBOFORM DS    CL8   OPERATOR SPECIFIED FORM NUMBER
SFBFCBNL DS    BL2   LONGEST IMBEDDED FCB (3211-TYPE)
SFBFCBXL DS    BL2   LONGEST IMBEDDED FCB (EXTENDED)
SFBRSV1  DS    XL4   RESERVED FOR IBM USE
SFBDEST  DS    CL8   USER SPECIFIED DESTINATION
SFBXAB   DS    XL4   DASD ADDRESS OF EXTERNAL ATTRIBUTE BUFFER
SFBXABL  DS    BL2   LENGTH OF EXTERNAL ATTRIBUTE BUFFER
SFBRSV2  DS    XL2   RESERVED FOR IBM USE
SFBSYSID DS    CL4   SYSTEM UNIQUE SPOOL ID
SFBFLAG5 DS    XL1   S*9 FLAGS
SFBRSV3  DS    XL3   RESERVED FOR IBM USE
SFBRSV4  DS    XL8   RESERVED FOR IBM USE
         LTORG ,
         END   RDRPUR


Margaret Pekelaar
VM Systems Support
Mobil Oil (South Africa)     ) Mobil Oil 1990


