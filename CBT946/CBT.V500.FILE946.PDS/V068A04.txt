XEDIT macro to copy lines repeatedly

Sometimes I have to copy one or more lines in an XEDIT file
to different places within the file, which means that I have to
use the prefix commands C and F repeatedly.

I created two little XEDIT macros to mark the lines I wanted to
copy only once and from then on use only the XF macro to put
them into different places.

These macros are used just like the prefix commands C and F.
With the XC macro you can enter the number of lines to be
copied as an argument or the default of one line is used.


XF XEDIT

/* Prefix Command to copy lines repeatedly */
Arg . . Tline .                /* Get Arguments. LineNo             */
"Extract/Curline/point *"      /* Get some Data from XEDIT          */
                               /* Now Search for a Point belonging  */
  Do I=1 to Point.0 by 1       /* to us.                            */
    Parse Var Point.I . Lno Pname .
    if Substr(Pname,1,2) ¬= "xc" Then Iterate
    Numl = Strip(Substr(Pname,3,6))  /* get num of lines            */
  End
If Numl = "NUML" Then Do       /* Error if XC wasn't used previously*/
   Emsg "Error...there were no lines marked with 'XC' !!"
   Exit ;End
":"Lno                         /* Go to line where Data comes from  */
"Set msgmode off"              /* Skip the inevitable message       */
"Copy "Numl ":"Tline           /* Copy the Lines                    */
"Set msgmode on "
Push ":"TLine                  /* Go to the new Data                */
Exit


XC XEDIT

/* Prefix Command to copy lines repeatedly  */
Arg . . Pline Op .             /* Get Arguments. LineNo and operand */
If Op="" Then Op="1"           /* if no Operand give take one line  */
"Extract/Curline/Point *"      /* get some Data from XEDIT          */
                               /* now check all 'Points' to skip any*/
  Do I=1 to Point.0 by 1       /* old XC-Pointer                    */
    Parse Var Point.I . Lno Pname .
    If Substr(Pname,1,2) ¬= "xc" Then Iterate
    "Set Point ."Pname" Off"
  End
":"Pline                       /* go to desired Line..              */
"Set Point .xc"Op              /* set the Pointer..                 */
":"Curline.1                   /* and restore the Screen Image      */
Exit


W Kraibuehler
Systems Programmer (Germany)
