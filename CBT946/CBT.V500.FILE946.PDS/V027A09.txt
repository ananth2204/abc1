 Assembler utility
The CMS Assembler has three work files (SYSUT1/2/3), which, while it was writing
to them, managed to completely fill my A(191) disk.  At this point, the
Assembler abended.  Rather than go through the process of having the disk
enlarged it seemed much simpler to call in some temporary disk space to
accommodate the work files.
In ASM EXEC, the address D(192) is used for these work files.  The mini-disk is
defined and formatted, and suitable FILEDEFs are issued.  (If 192 already
exists, it is detached - therefore, some sites may wish to use another address.)
The output listing goes straight to the printer (another exercise in space-
saving), so users may wish to SPOOL the printer to a different class from usual.
Assuming that the Assembler returns a zero retcode, the EXEC goes on to load the
TEXT file, and generate a module.
If the user does not supply any Assembler listing options there are some default
options in the EXEC.  This EXEC was written on VM/SP Release 3.
&TRACE *
* DESCRIPTION :    INVOKES THE ASSEMBLE MODULE, HAVING FIRST ASSIGNED
*   WORK FILES TO D(192) DISK, (DETACH & REDEFINE 192), & SET UP
*   DEFAULT PARAMETERS FOR THE ASSEMBLER.  FORMAT IS 'ASM FN OPTIONS'
*   (WHERE OPTIONS ARE VALID ASSEMBLER OPTIONS), OR
*   'ASM FN' (FOR DEFAULT OPTIONS).
* V40
*%OPTIMIZED AT 08:27:17 ON 88/05/27
*%       N O T I C E:
*% THIS STATEMENT IS NECESSARY FOR THE OPTIMIZATION PROGRAM - VMFOPT
*% DE-OPTIMIZE THIS MACRO BEFORE MAKING ANY CHANGES USING - VMFDEOPT
         &PRESUME &COMMAND
         DESBUF
         FILEDEF * CLEAR
         &BUFFER *
         &UPPER ARGS
* MAKE SURE THE ASSEMBLE MODULE IS AVAILABLE
         STATE ASSEMBLE MODULE *
         &IF &RC ¬= 0 &GOTO 69 -NOMOD
* MAKE RELEVANT MACLIBS AVAILABLE
         GLOBAL MACLIB DMSSP CMSLIB DOSMACRO
* DO WE HAVE A SOURCE FILE ?
         &IF .&1 = . EXECIO 1 EMSG (STRING NO FILENAME SUPPLIED
         &IF .&1 = . &EXIT 28
         STATE &1 ASSEMBLE *
          &IF &RC ¬= 0 &GOTO 71 -NOFILE
* ASSEMBLE MODULE & SOURCE FILE AVAILABLE, NOW DETACH/REDEFINE D(192)
          EXECIO 0 CP (STRING Q V 192
          &IF &RC ¬= 0 &SKIP 1
          EXECIO 0 CP (STRING DET 192
          EXECIO 0 CP (STRING DEF TFB-512 192 2000
* STACK REPLIES TO PREVENT CMS FORMAT FROM RUNNING INTERACTIVELY
          &BEGSTACK 2
YES
SCRTCH
         FORMAT 192 D
* ASSEMBLE FILEDEFS - ENSURE WORK FILES ARE WRITTEN TO TEMP SPACE
        FILEDEF ASSEMBLE DISK &1 ASSEMBLE * (RECFM FB LRECL 80 BLOCK 800
        FILEDEF TEXT DISK &1 TEXT A2
        FILEDEF LISTING PRINTER (PERM
        FILEDEF SYSUT1 DISK &1 SYSUT1 D4 (BLOCK 7294
        FILEDEF SYSUT2 DISK &1 SYSUT2 D4 (BLOCK 7294
        FILEDEF SYSUT3 DISK &1 SYSUT3 D4 (BLOCK 7294
        CLRSCRN
* HAS THE USER SUPPLIED THE ARGS ?
          &IF &N LT 2 &CALL - DEFAULT
          &IF &N LT 2 &SKIP 3
          ASSEMBLE &1 &2 &3 &4 &5 &6 &7 &8 &9
          &IF &RC = 0 &GOTO 59 -LOAD
          &SKIP 2
          ASSEMBLE &1 (&TERM &LIB XREF(&XREF
           &IF &RC = 0 &GOTO 59 -LOAD
          REL 192
          EXECIO 0 CP (STRING DET 192
          &EXIT &RC
-LOAD     LOAD &1 (CLEAR NOMAP AUTO ORIGIN 20000
          &IF &RC = 0 &GOTO 65 -GENMOD
          REL 192
          EXECIO 0 CP (STRING DET 192
          EXECIO 1 EMSG (STRING NON ZERO RETCODE ENCOUNTERED
          &EXIT &RC
-GENMOD   GENMOD &1 (FROM &1 NOMAP ALL
          REL 192
          EXECIO 0 CP (STRING DET 192
          &EXIT &RC
-NOMOD    EXECIO 1 EMSG (STRING ASSEMBLE MODULE NOT AVAILABLE
          &EXIT 28
-NOFILE   EXECIO 1 EMSG (STRING &1 ASSEMBLE NOT FOUND
          &EXIT 28
-DEFAULT  &XREF = FULL
          &TERM = TERMINAL
          &LIB = LIBMAC
          &RETURN
Mike Shears
Credit Lyonnais (UK)     ) Credit Lyonnais 1988










































