 Artificial display operator console for CMS
In our installation, programmers needed to be able to look at the VSE display
operator console.  They would go back and forth to their offices checking on a
job.  Many times, they would call to have a job flushed.  Once, one of them
commented that they would like to have a display operator console at their desk.
They were told about some commands under CMS such as 'VSECMD', 'VSEMSG', and
'VSECP'.  This was more than they cared to learn since it required remembering
yet more commands.
This gave me the idea of using the CMS commands 'VSECMD', 'VSEMSG', and 'VSECP'
in one EXEC and one XEDIT file.  The result was a screen which displayed the
last 20 lines of the VSE console with an unprotected line at the bottom for
entering any VSE/POWER command.
Some examples of possible commands are:
     'D L,F3'  -    THIS SHOWS THE LAST 3 LINES OF THAT PARTITION.
     'L LST,*JUNK'  -    THIS WILL DELETE ALL 'JUNK' FROM LIST QUEUE.
     'A RDR,JUNK,CLASS=B'     -    THIS WILL ALTER JUNK TO CLASS B.
     'MSG F2'  -    THIS WILL GET THE ATTENTION OF THAT PARTITION.
     '* CP DET 380 VSE'  -    THIS WILL DETACH 380 FROM VSE.
Obviously, certain commands can be dangerous and for this reason they can be
stopped from within the program.
There is also a help key, refresh key, and a colour key.  The help key gives
'hints' and shows functions of PF keys.  The refresh key is used to get a new
display of the last 20 lines.  The colour key allows the user to change the
colours on the display.
The only drawback of using this simulated 'DOC' is that the CMS commands it uses
rely on VMCF.  If you have 'VRNA' running under a particular VSE, this 'DOC'
will not work for that VSE.
The command format is 'DOC XXX' where 'XXX' is the VSE name.
DOC EXEC
&TRACE
SET DOS OFF
&VSE = &1
&IF .&VSE = . &VSE = VSE1
&IF &VSE = VSE1 &GOTO -PROCEED
&IF &VSE = VSEIPO &GOTO -PROCEED
&GOTO -ERROR
-PROCEED
CP SET IMSG OFF
CP SET MSG OFF
CP SET SMSG OFF
MAKEBUF
&STACK &VSE
XEDIT &VSE CONSOLE A1 (PROFILE DOC
CP SPOOL CON TERM
CP SET IMSG ON
CP SET MSG ON
CP SET SMSG ON
&EXIT
-ERROR
&TYPE PLEASE RETRY USING A VALID DOS/VSE SYSTEM NAME
&EXIT
DOC XEDIT
&TRACE
&READ VARS &VSE
-TOP
&COLORN = 0
&COLOR1 = RED
&COLOR2 = BLU
&COLOR3 = WHI
COMMAND SET SCALE OFF
COMMAND SET PF3 QQUIT
COMMAND SET PREF OFF
COMMAND SET TOFEOF OFF
COMMAND SET CMDL OFF
MAKEBUF
IDENTIFY (LIFO
 &READ VARS &OWNERID
DROPBUF
CP SET EMSG OFF
COMMAND TRANSFER TERMINAL
&READ VARS &TERM
&IF &TERM = TYPEWRITER &CALL -ENDMACRO
COMMAND EXTRACT/LSCREEN/
&PANEL = 0
&CASE U
&COMMAND MAKEBUF
COMMAND SET COLOR CURL GRE NON NOHIGH
COMMAND SET COLOR FILEAREA GRE NON NOHIGH
COMMAND SET WRAP ON
COMMAND SET STAY ON
COMMAND SET SCALE OFF
COMMAND SET LINEND OFF
COMMAND SET PREFIX OFF
COMMAND SET CASE MIXED IGNORE
COMMAND SET CTLCHAR ! ESCAPE
COMMAND SET CTLCHAR " PROTECT GRE NOHIGH
COMMAND SET CTLCHAR  NOPROTECT &COLOR3 NON NOHIGH
MAKEBUF
&STACK !" !
&READ VARS &P &N1
&I = 1
&LOOP 2 UNTIL &I > 24
COMMAND SET PF&I *
&I = &I + 1
SET PA2 NULLKEY
COMMAND TRANSFER LSCREEN
&READ VARS &SCREEN
&PARTIT = LAST
&BAD1 = &STRING OF L RDR,*JUNK
&BAD2 = &STRING OF L RDR,ALL
&BAD3 = &STRING OF L LST,ALL
&BAD4 = &STRING OF PEND
&BAD5 = &STRING OF * CP LOG
-BEGIN
CP SET SMSG OFF
CP SET IMSG OFF
CP SET MSG OFF
-CALLPAN
&CALL -PANEL
COMMAND READ NOCHANGE TAG
&COMMAND SENTRIES
&NUMLINE = &RC
&LOOP -END &NUMLINE
&READ ARGS
&IF &1 = PFK &GOTO -PFK
&IF &1 = ETK &GOTO -END
&IF &1 = RES &GOTO -RES
&GOTO -END
-RES
&IF .&4 = . &GOTO -BEGIN
&C = &BLANK
&X = 4
&NA = &N - 3
&LOOP -ENDARG &NA
&C = &STRING OF &C &&X
&X = &X + 1
-ENDARG
&L = &LENGTH OF &C
&L = &L - 2
&C = &RIGHT OF &C &L
&CP = &CONCAT OF &4 &5
&IF &CP = *CP &GOTO -CPCOMND
 &IF &4 = D &GOTO -TESTDISP
&GOTO -COMMAND
-TESTDISP
&IF .&5 = . &PARTIT = LAST
&IF .&5 = . &GOTO -BEGIN
&PARTIT = &RIGHT OF &5 2
&DISP = &LEFT OF &5 2
&IF &DISP = L, &GOTO -BEGIN
&GOTO -COMMAND
-PFK
&IF &2 Â¬= 2 &REFRESH = NO
&IF &2 = 1 &GOTO -HELP
&IF &2 = 2 &GOTO -REFRESH
&IF &2 = 3 &GOTO -ENDMACRO
&IF &2 = 4 &GOTO -COLOR
-END
&REFRESH = NO
&GOTO -BEGIN
-COMMAND
&IF &C = &BAD1 &GOTO -ERROR1
&IF &C = &BAD2 &GOTO -ERROR1
&IF &C = &BAD3 &GOTO -ERROR1
* &IF &C = &BAD4 &GOTO -ERROR1
&L = &LENGTH OF &C
VSECMD &VSE &C
&IF &RC Â¬= 0 &GOTO -ERROR1
&PARTIT = LAST
&GOTO -BEGIN
-CPCOMND
* &IF &C = &BAD5 &GOTO -ERROR1
VSECP &VSE &C
&IF &RC Â¬= 0 &GOTO -ERROR1
&PARTIT = LAST
&GOTO -BEGIN
-REFRESH
&PARTIT = LAST
&GOTO -BEGIN
-PANEL
&COMMAND CONWAIT
&COMMAND DESBUF
MAKEBUF
EXECIO * CP (STRING Q TIME
&READ VARS * * &TIM * * &DAT
&READ VARS * * * * * *
DROPBUF
&BUFFER *
MAKEBUF
CMS SET CMSTYPE HT
CP SPOOL CON CLOSE NOTERM
CP CH RDR ALL HOLD
CP SPOOL CON CLOSE NOTERM
CP SPOOL CON * START NOTERM
CMS SET CMSTYPE RT
VSEMSG &VSE &PARTIT
&IF &RC Â¬= 0 &GOTO -ERROR2
CMS SET CMSTYPE HT
CP SPOOL CON * STOP NOTERM
CP SPOOL CON CLOSE NOTERM
CMS SET CMSTYPE HT
EXECIO * CARD
SENTRIES
&VSLINES = &RC
CMS SET CMSTYPE RT
CP SPOOL CON TERM
CP CH RDR ALL NOHOLD

 &TITLE = &STRING OF      &VSE DISPLAY OPERATOR CONSOLE   &TIM  &DAT      PF1-
HELP
COMMAND SET RESERVED 1 &COLOR1 UND HIGH &TITLE
&RESLINE = 2
&READ STRING &T
&READ STRING &T
&VSLINES = &VSLINES - 2
&LOOP -ENDSET &VSLINES
&READ STRING &T
&RESLINE = RESLINE + 1
&T = &STRING OF  &T
COMMAND SET RESERVED &RESLINE &COLOR2 NON HIGH &T
&T1 = &LEFT OF &T 4
&T1 = &RIGHT OF &T1 1
&IF &T1 = - COMMAND SET RESERVED &RESLINE &COLOR3 NON HIGH &T
-ENDSET
&RESLINE = &RESLINE + 1
COMMAND SET RESERVED &RESLINE &COLOR2 NON HIGH &BLANK
&C = &LEFT  OF &BLANK 77
&C = &CONCAT OF &N1 &C &N1
&RESLINE = &RESLINE + 1
COMMAND SET RESERVED &RESLINE WHI NON HIGH &C
&CMLINE = &RESLINE
COMMAND CURSOR SCREEN &CMLINE 2
&IF &RESLINE < 24 &CALL -ADD
&RETURN
-ADD
&NOSLINE = &SCREEN - &RESLINE
&LOOP 2 &NOSLINE
&RESLINE = &RESLINE + 1
COMMAND SET RESERVED &RESLINE &COLOR2 NON HIGH &BLANK
&RETURN
-HELP
&HELP = YES
MAKEBUF
&X = &STRING OF PF1 - HELP
&STACK &X
&X = &STRING OF PF2 - REFRESH
&STACK &X
&X = &STRING OF PF3 - QUIT
&STACK &X
&X = &STRING OF PF4 - COLOR
&STACK &X
&X = &STRING OF &BLANK
&STACK &X
&X = &STRING OF KEY IN ANY VSE/POWER COMMAND ON THE LINE WHERE CURSOR IS
&STACK &X
&X = &STRING OF VSEI & VSEIPO SHARE THE SAME LST Q & RDR Q,
         BUT NOT SAME PARTITIONS.
&STACK &X
SENTRIES
&LINES = &RC
EXECIO &LINES DISKW VSEICONS HELP A1
MAKEBUF
&STACK COMMAND SET PREF OFF
&STACK COMMAND SET SCALE OFF
&STACK COMMAND SET PF3 QQUIT
&STACK COMMAND CMDL OFF
CMS XEDIT VSEICONS HELP A1
CMS ERASE VSEICONS HELP A1
&GOTO -BEGIN
-COLOR
&COLORN = &COLORN + 1
&IF &COLORN = 1 &COLOR1 = GRE
&IF &COLORN = 1 &COLOR2 = YEL
&IF &COLORN = 1 &COLOR3 = TUR
 &IF &COLORN = 2 &COLOR1 = PIN
&IF &COLORN = 2 &COLOR2 = GRE
&IF &COLORN = 2 &COLOR3 = YEL
&IF &COLORN = 3 &COLOR1 = TUR
&IF &COLORN = 3 &COLOR2 = PIN
&IF &COLORN = 3 &COLOR3 = GRE
&IF &COLORN = 4 &COLOR1 = WHI
&IF &COLORN = 4 &COLOR2 = TUR
&IF &COLORN = 4 &COLOR3 = RED
&IF &COLORN = 5 &COLOR1 = BLU
&IF &COLORN = 5 &COLOR2 = RED
&IF &COLORN = 5 &COLOR3 = YEL
&IF &COLORN = 6 &COLOR1 = YEL
&IF &COLORN = 6 &COLOR2 = BLU
&IF &COLORN = 6 &COLOR3 = RED
&IF &COLORN = 7 &COLOR1 = GRE
&IF &COLORN = 7 &COLOR2 = TUR
&IF &COLORN = 7 &COLOR3 = PIN
&IF &COLORN = 8 &COLOR1 = RED
&IF &COLORN = 8 &COLOR2 = BLU
&IF &COLORN = 8 &COLOR3 = WHI
&IF &COLORN = 8 &COLORN = 0
COMMAND SET CTLCHAR ! ESCAPE
COMMAND SET CTLCHAR " PROTECT GRE NOHIGH
COMMAND SET CTLCHAR  NOPROTECT &COLOR3 NON NOHIGH
MAKEBUF
&STACK !" !
&READ VARS &P &N1
&GOTO -BEGIN
-ERROR1
&ER = &STRING OF ====>  &C IS A PROTECTED COMMAND              PRESS PF3
&Q = NO
&GOTO -BEGINER
-ERROR2
&ER = &STRING OF ====>  &VSE IS INACTIVE AT THIS TIME          PRESS PF3
&Q = YES
&GOTO -BEGINER
-BEGINER
&CALL -ERPAN
COMMAND READ NOCHANGE TAG
SENTRIES
&ERLIN = &RC
&LOOP -ENDERR &ERLIN
&READ ARGS
&IF &1 = PFK &GOTO -ERPFK
&GOTO -BEGINER
-ENDERR
&GOTO -BEGINER
-ERPFK
&IF &2 = 3 &IF &Q = YES &GOTO -ENDMACRO
&IF &2 = 3 &IF &Q = NO &GOTO -BEGIN
&GOTO -BEGINER
-ERPAN
REFRESH
COMMAND SET RESERVED 1 YEL REG HIGH &ER
COMMAND CURSOR SCREEN 1 2
&RETURN
-ENDMACRO
&COMMAND DROPBUF
QQUIT

Ray Dubcak
Computer Operator
Ralph Wilson Plastics (USA)   ) Ray Dubcak 1988




































































