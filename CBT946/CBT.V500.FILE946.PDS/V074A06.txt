An SAA/CUA menu system for VM

During the last six years I've worked on a number of XEDIT-
based menuing systems for VM. The menu systems have been
used as improved front-ends to PROFS to provide access to
other VM system products and home grown applications.

When IBM first announced it's SAA/CUA interface standard I
ordered a copy of the manual (Systems Application
Architecture Common User Access Basic Interface Design
Guide) so I could write a new menu driver and jump on the
SAA bandwagon. The SAA/CUA interface has been slow to
catch on in the VM world but I've found it an excellent
standard for providing a multi-faceted menuing system.

My first attempt worked but I was never really happy with it.
When IBM changed its IBM Info menu system to SAA/CUA it
gave me a number of ideas for improving my menu system.
Figures 1 and 2 are examples of the main menu screen.

I have only included the main programs required to bring up
and use the menuing system. Other programs include a bulletin
board flagging routine, a menu option panel, a routine to get a
PROFS calendar for displaying on the main menu (calendar
window), and a program for creating a personal menu. I've
also left out the other menu files and all of the help files.


OFFICE EXEC

/* OFFICE EXEC     Start the SAA/CUA menu system                    */
trace off
/* Set menu bypass */
Level = 0
Exit_mode = 'NORMAL'
address CMS 'GLOBALV SELECT CUAMENU PUT Level'
address CMS 'GLOBALV SELECT CUAMENU PUT Exit_mode'
/* Start the clock - see VM/UPDATE April 1991, pg. 31 */
address COMMAND 'EXEC XCLOCK PINK'
/* Check Bulletin Boards */
address COMMAND 'GLOBALV SELECT $OPTIONS GET WN'
Parse Var wn opt_wn ';' .
if opt_wn = 'YES' then address COMMAND 'EXEC CUABULLU'
/* Get Start_up and Wake_up commands */
address COMMAND 'STATE WITMENU DEFAULTS A'
if rc = 0 then do
   string = ''
   'EXECIO 2 DISKR WITMENU DEFAULTS (FINIS STEM LINE.'
   cur_date = substr(date(O),1,2)substr(date(O),4,2)substr(date(O),7,2)
   old_date = substr(line.2,1,6)
   if cur_date > old_date then do
      string = strip(substr(line.2,8,20))
      new_line = cur_date string
      'EXECIO 1 DISKW WITMENU DEFAULTS A 1 (FINIS STR' line.1
      'EXECIO 1 DISKW WITMENU DEFAULTS A 2 (FINIS STR' new_line
   end
   string = string strip(substr(line.1,1,20))
   if string ¬= '' then push 'RES R C 'string
end
/* Bring up main menu */
address CMS 'XEDIT WITMAIN $$MENU$$ (PROF WITMENU'
exit

This is the file used for the main menu. These items will be
grouped into folders on the screen. Multiple levels of folders
can be created using the same format. Note: if you wanted a
level with only two folders you would leave off all lines
starting with a 3 and a 4.


WITMAIN $$MENU$$

A Whanderful Office System
1 0  ¬A ¬0Office Services            A00¬6
1 1 'EXEC OPENMAIL' Open the Mail
1 2 'PROFS NOTE' Send a Note
1 3 'PROFS APPOINTM' Calendar
1 4 'EXEC CUALOG LOG' Note Logs
1 5 'EXEC REMINDERS' Reminders
1 6 'EXEC DISTRI' Distribution Lists
1 7 'EXEC PROFUSER' Nickname Lists
1 8 'EXEC SIGNOUT' Signout
1 9 'XEDIT OFFICE $$MENU$$ * (PROF WITMENU' More Services
2 0  ¬A ¬0Information Services       B00¬6
2 1 'EXEC WATTSNEW' WATTSNEW
2 2 'EXEC RPT00' General Information
2 3 'EXEC FAX' FAX Machines
2 4 'EXEC ASPEN' Aspen Users
2 5 'EXEC SERVICES' Head Office Services
2 6 'XEDIT BULLETIN TABLE (PROF CUABULL' Bulletins
2 7 'EXEC MIS' BMS Reports
2 8 'EXEC INFO' VM/PROFS Info
2 9 'XEDIT INFO $$MENU$$ * (PROF WITMENU' More Services
3 0  ¬A ¬0Tools and Applications     C00¬6
3 1 'EXEC RACFPW' Change Password
3 2 'EXEC PRNTLIST' Set Default PROFS Printer
3 3 'EXEC CMPRINT' Set Default CMS Printer
3 4 'SF' Send a File
3 5 'EXEC ISPF' SPF
3 6 'EXEC FORMS' Electronic Forms
3 7 'EXEC CAL' Calculator
3 8 'FILELIST' List Your Files
3 9 ''
4 0  ¬A ¬0Personal Folder            D00¬6
4 1 '' Use the Action Bar item
4 2 '' Options to customize this
4 3 '' folder.
4 4 ''
4 5 ''
4 6 ''
4 7 ''
4 8 ''
4 9 ''


WITMENU XEDIT

/*   WITMENU XEDIT        SAA/CUA menu driver                       */
trace off
/* Code for Menu Bypass */
NORMAL = 'NORMAL'
HOME = 'HOME'
address CMS 'GLOBALV SELECT CUAMENU GET Level'
Level = Level + 1
address CMS 'GLOBALV SELECT CUAMENU PUT Level'
/* Set up SAA/CUA colours */
'COMMAND SET CTLCHAR ¬ ESCAPE'
'COMMAND SET CTLCHAR 0 P WHITE     NON H'
'COMMAND SET CTLCHAR 1 P WHITE     NON N'
'COMMAND SET CTLCHAR 2 P BLUE      NON H'
'COMMAND SET CTLCHAR 3 P YELLOW    NON H'
'COMMAND SET CTLCHAR 4 P YELLOW    NON H'
'COMMAND SET CTLCHAR 5 N GREEN     NON N'
'COMMAND SET CTLCHAR 6 P GREEN     NON N'
'COMMAND SET CTLCHAR 7 P TURQUOISE NON H'
'COMMAND SET CTLCHAR 8 P TURQUOISE NON N'
'COMMAND SET CTLCHAR 9 P RED       NON H'
'COMMAND SET CTLCHAR A N WHITE     NON INVISIBLE'
/* Set up XEDIT environment */
'COMMAND SET REMOTE ON'
'COMMAND SET COLOUR MSGLINE YELLOW NON H'
'COMMAND SET COLOUR CURL GREEN NON N'
'COMMAND SET CURLINE ON 5'
'COMMAND SET CMD OFF'
'COMMAND SET PF4 OFF'
'COMMAND SET TOFEOF OFF'
'COMMAND SET PREFIX OFF'
'COMMAND SET SCALE OFF'
'COMMAND SET CASE MIXED I'
'COMMAND SET NULLS ON'
'COMMAND SET WRAP OFF'
/* Fancy or plain boxes */
address CMS 'QUERY DISPLAY (LIFO'
parse pull . . . . . . color? . . flag?
if color? = 'COLOR' & flag? = '0' then do
   TS = '8E'x; TC = '8E'x; BS = '8E'x; BC = '8E'x; RS = '8E'x;
                                                             LS = '8E'x
end
else do
   TS = '-'; TC = '.'; BS = '-'; BC = "'"; RS = '|'; LS = '|'
end
/* Gather some useful pieces of info */
'COMMAND EXTRACT /NBFILE/LS'
'COMMAND :1'
'COMMAND EXTRACT /CURLINE'
title = strip(curline.3)
address COMMAND 'STATE WITMENU $$TITLE$ A'
if rc = 0 then do
   'EXECIO 1 DISKR WITMENU $$TITLE$ A (FINIS STEM TITLE.'
   title = strip(title.1)
end
title_line = centre(title,78)
/* Get PERSONAL $$MENU$$ */
if Level = 1 then do
   address COMMAND 'STATE PERSONAL $$MENU$$ A'
   if rc = 0 then do
      'COMMAND SET MSGMODE OFF'
      'COMMAND LOCATE /Personal Folder'
      'COMMAND DELETE *'
      'COMMAND GET PERSONAL $$MENU$$ A'
      'COMMAND SET MSGMODE ON'
   end
end
/* Read in the menu */
'COMMAND EXTRACT /SIZE'
if size.1//10 ¬= 1 then do
   call Msg 'Menu file has been corrupted - contact Steve Whan.'
   call quit
end
num_folders = 0
do i = 1 to size.1
   'COMMAND :'i
   'COMMAND EXTRACT /CURLINE'
   x = substr(curline.3,1,1)
   if x > num_folders then num_folders = x
   y = substr(curline.3,3,1)
   menu.x.y = strip(substr(curline.3,5,79))
   parse value menu.x.y with "'" command.x.y "'" descr.x.y
end
/* Calendar and Port Id on Main Menu */
if queued() then parse pull wakeup_cmd
address COMMAND 'STATE $URGENT $MESSAGE *'
select
   when rc = 0 & Level = 1 then call urgent_msg
   when rc ¬= 0 & Level = 1 then do
      call calendar
      call cal_window
   end
   otherwise msg_window. = ''
end /* select */
'EXECIO * CP (STR QUERY' userid()
parse pull . '-' port_id
if substr(wakeup_cmd,1,3) = 'RES' then push wakeup_cmd
/* Create and display the folders */
next_folder.1 = '11'
next_folder.2 = '1212'
next_folder.3 = '123123'
next_folder.4 = '12341234'
screen_line. = ''
address COMMAND 'GLOBALV SELECT WITMENU GET FOLDER'
if folder = '' | Level > 1 then front_folder = num_folders
                           else front_folder = folder - 1
call toggle_folders
/* Display header and footer */
call Header_1
call Header_2
cmd_string = ''
orig_pfk_line = '¬2F1=Help  F2=Toggle Folder  F3=Exit    F9=Retrieve ,
F10=Action Bar,
F12=Cancel'
pfk_line = orig_pfk_line
call display_footer
/* Cursor on PF keys */
lastline = lscreen.1
linenum.lastline.1  = 'Help'
linenum.lastline.10 = 'Toggle_folders'
linenum.lastline.28 = 'Quit'
linenum.lastline.39 = 'Retrieve'
linenum.lastline.52 = 'Action'
linenum.lastline.69 = 'Quit'
linenum.helpline.1  = 'Bkwd'
linenum.helpline.32 = 'Fwd'
linenum.helpline.69 = 'Quit'

Editor's note: this article will be continued next month.

Steve Whan                           c Western Integrated Technologies Inc 1992
System Programmer
Western Integrated Technologies Inc (Canada)


