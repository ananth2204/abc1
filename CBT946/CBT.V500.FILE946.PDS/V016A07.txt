 Automating VM back-ups - revisited

The following are modifications made to the April 1987 VM Update article
Automating VM back-ups, to run at a client site.  These modifications allow the
utilities to run under VM/SP Release 4.

We installed the EXEC (BKUP EXEC) and called modules on the system 'Y2' disk,
thus giving everyone access who has clearance to read system disks (ie full pack
mini-disks owned only by MAINT).  The VM Directory provides links to each of the
full pack disks.

Probably the only installation-specific changes needed would be:

%    Real address for tapes in the CP ATTACH commands.

%    VOL-IDS for the DDR dumps - straightforward global changes.

As more packs are added to the system, simply create new EXECs to handle the new
VOL-IDS.

Each of the called DDR EXECs (NUCLEUS, VMSRES, etc) must have a blank line as
the final line to terminate DDR.

Note, the EXECs are set up to dump 884 cylinders to a single tape in COMPACT
format.  We run this against a 3380-AE4 hung off a 4381-M11 and it takes only
nine 2400-foot tapes per AE4.  With 3422 tape drives, it takes about 15 minutes
per VOL-ID and uses a small amount of the second tape - in all, about 90 minutes
per AE4!

First, the IPL DDR S2 is moved to tape (recovery tape), then the little 'just in
case' pieces, which all fit on a mini-reel.


BKUP EXEC

&TRACE OFF
*  &TRACE ON
VMFCLEAR
* ------------------------ C A U T I O N --------------------------- *
* BEFORE USING VERIFY VIA DISKMAP THIS USER ID HAS FULL              *
* VOLUME MDISK AT VIRTUAL ADDRESSES FOR ALL MOUNTED DASD             *
* AS WELL AS DASD END IS CORRECT - I.E. EQUAL TO ENDING              *
* ADDRESS IN THE FOLLOWING CONTROL BOOKS:                            *
*                       BKUP VMSRES fm                               *
*                       BKUP VMPK01 fm                               *
*                       BKUP VMGCS1 fm                               *
*                       BKUP PROFPK fm                               *
-START
*  Get two tape drives
CP ATTACH 501 * 181
CP ATTACH 502 * 182
-RESTART
*  Verify 181 is available
EXECIO 1 CP (LIFO STRING Q V 181
&READ STRING &NOMSG
&NODEV = &SUBSTR OF &NOMSG 1 3
&IF &NODEV EQ DEV &GOTO -NOTAPE
*  Verify 182 is available
EXECIO 1 CP (LIFO STRING Q V 182
&READ STRING &NOMSG
&NODEV = &SUBSTR OF &NOMSG 1 3
&IF &NODEV EQ DEV &GOTO -NOTAPE
*
-DOITALL
*   Make it look pretty
* VMFCLEAR
 &BEGTYPE
&BEGTYPE
|------------------------- VM FULL SYSTEM BACKUP --------------------|
&BEGTYPE
|       A full VM System Backup is about to start. Created will be   |
&BEGTYPE
|       multiple tape volumes, including a STANDALONE IPLable volume |
&BEGTYPE
|       and separate full volume backups of all CP OWNED DASD with   |
&BEGTYPE
|       VOLIDs: VMSRES - VMPK01 - VMGCS1 - PROFPK.                   |
&BEGTYPE
|                TAPE 501 WILL BE 181 AND TAPE 502 WILL BE 182       |
&BEGTYPE
| asleep --------------- HIT ENTER WHEN READY ---------------------- |
CP SLEEP 30 MIN
-IPLTAPE
*   Make it look pretty
VMFCLEAR
&BEGTYPE
&BEGTYPE
| ----------------------- T A P E  M O U N T ----------------------- |
&BEGTYPE
|        Load Tape   181 - Label = STANDALONE IPL  - VOL 01          |
&BEGTYPE
|                   DDR DSF FMT DIR NUCLEUS                          |
&BEGTYPE
| asleep -------------- HIT ENTER WHEN READY ----------------------- |
CP SLEEP 30 MIN
TAPE REW (181
*
* GET DDR
FILEDEF  INMOVE  DISK IPL DDR S2
FILEDEF  OUTDDR  TAP1 (DEN 6250
MOVEFILE INMOVE  OUTDDR
TAPE WTM (181
*
* GET DEVICE SUPPORT FACILITIES
FILEDEF  INMOVE  DISK IPL DSF S2
FILEDEF  OUTDSF  TAP1 (DEN 6250
MOVEFILE INMOVE  OUTDSF
TAPE WTM (181
*
* GET FORMAT/ALLOCATE
FILEDEF  INMOVE  DISK IPL FMT S2
FILEDEF  OUTFMT  TAP1 (DEN 6250
MOVEFILE INMOVE OUTFMT
TAPE WTM (181
*
* GET DIRECTORY
FILEDEF  INMOVE  DISK IPL DIR S2
FILEDEF  OUTDIR  TAP1 (DEN 6250
MOVEFILE INMOVE  OUTDIR
TAPE WTM (181
*
* GET NUCLEUS
DDR BKUP NUCLEUS   Y2
TAPE WTM 2 (181
TAPE RUN   (181
*
-VMSRES
*   Make it look pretty
* VMFCLEAR
&BEGTYPE
&BEGTYPE
| ----------------------- T A P E  M O U N T ----------------------- |
 &BEGTYPE
|          Load Tape 181 - Label = VMSRES BKUP       - VOL 01        |
&BEGTYPE
|          Load Tape 182 -       = Alternate Drive   - VOL 02        |
&BEGTYPE
| asleep --------------- HIT ENTER WHEN READY ---------------------- |
CP SLEEP 30 MIN
* Get VMSRES
VMFCLEAR
TAPE REW (181
TAPE REW (182
DDR BKUP VMSRES    Y2
*
ÝEditor's note: This code is repeated for each disk to be saved¨
-ENDITALL
*   Make it look pretty
*  VMFCLEAR
&BEGTYPE
&BEGTYPE
| ----------------------- E N D  O F  J O B ------------------------ |
&BEGTYPE
|        VM System Backup is complete. Remove Write rings from all   |
&BEGTYPE
|        volumes created and store inhouse / offsite as need be.     |
&BEGTYPE
| asleep --------------- HIT ENTER WHEN READY ---------------------- |
CP SLEEP 30 MIN
&EXIT
*
-NOTAPE
VMFCLEAR
&BEGTYPE
&BEGTYPE
&BEGTYPE
| ----------------------- E R R O R  M O D E ----------------------- |
&BEGTYPE
|                 You Need To Have Two Free Tapes                    |
&BEGTYPE
|        ATTACH a tape to yourself as 181 = Primary                  |
&BEGTYPE
|        ATTACH a tape to yourself as 182 = Secondary                |
&BEGTYPE
| asleep --------------- HIT ENTER WHEN READY ---------------------- |
CP SLEEP 30 MIN
&GOTO -RESTART
&EXIT
BKUP NUCLEUS
SYSPRINT CONS
INPUT  123 3380 VMSRES
OUTPUT 181 3430 (MODE 6250 LEAVE
DUMP NUCLEUS


BKUP VMSRES

SYSPRINT CONS
INPUT  123 3380 VMSRES
OUTPUT 181 3430 (MODE 6250 UNLOAD COMPACT
DUMP 0000 0884
INPUT  123 3380 VMSRES
OUTPUT 182 3430 (MODE 6250 UNLOAD COMPACT
DUMP 0885 1769
*
ÝEditor's note:  A separate file like BKUP VMSRES is required for each disk to
be backed up.  The changes that need to be made to the code are input address
and disk name.¨

AF Allen
President
AF Allen & Associates (USA)   ) AF Allen 1987






























































