 QQUOTE XEDIT
EXEC and EXEC2 use the somewhat infamous ampersand to signal the interpreter
that a variable needs to be translated.  The REXX interpreter, on the other
hand, will attempt to translate everything it finds outside quotes.  Surrounding
CMS and CP commands (to be passed on to CMS) with quotes saves the interpreter
the need to translate these (as it would any other variable) only to find that
the value of these 'variables' is the same as the NAME of the variable, itself.
An EXEC that is most useful when converting EXEC or EXEC2 code to REXX
surrounds the entire line with quotes - definitely not for every line, but
distinctly helpful during a conversion when a large number of lines will end up
being surrounded by quotes.
QQUOTE (QuickQuote) XEDIT will quote lines not already quoted.  It will remove
the quotes from lines that are already quoted.
If there is an internal quote within the line, only an error message results.
This macro is intended to be invoked via a PF key.
/*             Quick QUOTE inserter for REXX EXECS                 */
/*     If NO Quote resides on the line, the line becomes           */
/*     surrounded with single quotes.                              */
/*     If the line is already surrounded with single quotes,       */
/*     they are removed.                                           */
/*                                                                 */
/*     To invoke from the XEDIT Command Line, simply type the      */
/*     NAME of this Macro in the Command line while the line       */
/*     you wish to alter is at the Current line.                   */
/*     To invoke via a PF key, the key must be defined to be:      */
/*     QQUOTE PF       or      QQUOTE CURSOR                       */
arg key
'preserve'
'set tabs 1'
'extract/ftype/line/cursor/trunc'
'zone 1 'trunc.1
stopper = trunc.1 - 2
if (ftype.1 ¬= 'EXEC' & ftype.1 ¬= 'XEDIT') then
 call illegal 'T'
if (key = 'PF' | key = 'CURSOR' ) then ':'cursor.3
'stack'
add= 0
parse pull in
if length(strip(in)) = 0 then call illegal 'B'
if ((pos('/*',in)>0&pos('*/',in)=0)|(pos('/*',in)=0&pos('*/',in)>0))
 then call illegal 'M'
if (pos('"',in)=0&pos("'",in)=0) then
 do
   add = 1
   if length(strip(in,'T'))> stopper then call illegal 'L'
 end
else
 do
   start = pos(substr(strip(in),1,1),in)
   ends = lastpos(substr(strip(in),length(strip(in))),in)
   schar = substr(in,start,1)
   echar = substr(in,ends,1)
   if length(strip(in,'T'))> stopper then call illegal 'L'
   if schar ¬= "'" & schar ¬= '"' then call illegal 'S'
   if echar ¬= "'" & echar ¬= '"' then call illegal 'E'
   if schar ¬= echar then call illegal 'N'
   if start = ends then call illegal 'O'
 end
'del1'
'up1'
   if add then
      if verify(in,' ') = 1 then "input '"strip(in,'T')"'"
      else
       do
         one = verify(in,' ')
         "input "substr(in,1,one-1)"'"substr(strip(in,'T'),one)"'"
 /*       "input  '"strip(in,'T')"'" */
      end
  else
   do
     if pos(schar,in) = 1 then 'input ',
              substr(in,2,length(strip(in,T))-2)
     else
      do
        one = pos(schar,in)
        'input 'substr(in,1,one-1)||,
         substr(in,one+1,length(strip(in,T))-(one+1))
      end
   end
':'line.1
if key = 'PF' | key = 'CURSOR' then 'cursor file 'cursor.3 cursor.4
'restore'
exit
illegal:
arg why
if why = 'T' then 'EMSG  REXX files ONLY!'
if why = 'L' then 'EMSG  Line is Too LONG to Add Comment!'
if why = 'M' then 'EMSG  Only PART of COMMENT here!'
if why = 'B' then 'EMSG  This line is Totally BLANK!'
if why = 'S' then 'EMSG  Quote missing from START of line!'
if why = 'E' then 'EMSG  Quote missing from END of line!'
if why = 'O' then 'EMSG  Only a Single QUOTE Mark on this line!'
if why = 'N' then 'EMSG  Mixed Single & Double QUOTE on this line!'
':'line.1
if key = 'PF' | key = 'CURSOR'  then 'cursor file 'cursor.3 cursor.4
'restore'
exit 5
Rick Frye
Senior Programmer Analyst
Encyclopaedia Britannica (USA)
) Rick Frye 1988































