Mailing utility (continued)

This month we conclude FNBNOTES, a fullscreen 'electronic'
mailing utility that replaces the IBM supplied NOTE utility.


NOTEPF1 XEDIT

 /* THIS XEDIT MACRO ALLOWS THE USER TO CREATE A NEW NOTE WHICH */
 /* CAN LATER BE SENT.      */
  VMFCLEAR
 COUNT = 0; REDO = 0; ERRCODE = 0
 CS = 'COMMAND SET CTLCHAR';   CS '%' ESCAPE
 CS '@' NOPROTECT RED NOHIGH;  CS '$' PROTECT YELLOW HIGH
 CS '{' PROTECT WHITE HIGH;    CS 'Ý' PROTECT TURQ HIGH
 CS '&' PROTECT PINK HIGH;     CS '¨' PROTECT BLUE HIGH
 CS '}' PROTECT GREEN NOHIGH;  CS '!' PROTECT RED BLINK HIGH
 CS '\' PROTECT RED HIGH
 COMMAND SET MSGLINE ON 24 1 OVERLAY
 COMMAND SET SCALE OFF
 STRING.3 = 'N'
START:
 COUNT = COUNT + 1
 IF COUNT = 1 THEN DO Y = 1 TO 2
                      STRING.Y = ' '
                   END
 CALL DISPLAY_SCREEN
 MAKEBUF
 READ NOCHANGE TAG
 PULL KEY NUMBER .
 IF KEY = 'PFK' THEN DO
                      IF NUMBER = '3' | NUMBER = '15'THEN DO
                                                           'QQUIT'
                                                           EXIT 4
                                                          END
                     END
  DO QUEUED()
     PULL . LINE COL DATA
     IF LINE = 10 & COL = 8 THEN STRING.1 = DATA
     IF LINE = 16 & COL = 8 THEN STRING.2 = DATA
     IF LINE = 21 & COL = 8 THEN STRING.3 = DATA
  END
 DROPBUF
 DO X = 1 TO 2
   IF STRING.X = ' ' THEN DO
                           REDO = 1
                           SIGNAL START
                          END
 END
 IF STRING.3 <> 'N' & STRING.3 <> 'Y' THEN DO
                                            ERRCODE = 1
                                            SIGNAL START
                                           END
 DESBUF
 USER = USERID()
 LINE.1 = '*************************************************',
          ||'***********************'
 LINE.2 = '*       F N B N O T E S  -  I N T E R N A L   M E M O R',
          'A N D U M      *'
 LINE.3 = '*************************************************',
          ||'***********************'
 LINE.4 = ' '
 LINE.5 = 'TITLE:            'STRING.1
 LINE.6 = ' '
 LINE.7 = 'DATE:             'DATE(E)||'  -  '||TIME()
 LINE.8 = 'FROM:             'USER
 LINE.9 = 'SEND TO:          'STRING.2
 LINE.10 = 'REPLY REQUESTED:  'STRING.3
 LINE.11 = ' '
 LINE.12 = ' '
 LINE.13 = '-------------------------------------------------',
          ||'-----------------------'
 LINE.14 = ' '
 LINE.15 = ' WRITE YOUR MEMO FROM HERE.'
 LINE.16 = ' '
 LINE.17 = ' '
 'SET CMSTYPE HT'
 'ERASE 'USER' NOTES A'
 'SET CMSTYPE RT'
 DO N = 1 TO 17
    IF N = 1 THEN DO
            'EXECIO 1 DISKW 'USER' FNMAIL1 A 1 F 80 (STRING' LINE.N
            END
     ELSE 'EXECIO 1 DISKW 'USER' FNMAIL1 A (STRING' LINE.N
 END
 'EXECIO 0 DISKW 'USER' FNMAIL1 A (FINIS'
 DESBUF
 PUSH 'SET CASE M I'
 'CMS XEDIT 'USER' FNMAIL1 A'
 'SET CMSTYPE HT'
 'SET IMSG OFF'
 'SPOOL PUN *'
 'PUNCH 'USER' FNMAIL1 A (NOHEADER'
 'ERASE 'USER' FNMAIL1 A'
 'SPOOL PUN SYSTEM'
 'SET IMSG ON'
 'SET CMSTYPE RT'
 'QQUIT'
 EXIT 0
DISPLAY_SCREEN:
 IF COUNT = 1 THEN SEL.3 = 'N'
  ELSE SEL.3 = STRING.3
 IF COUNT = 1 THEN DO V = 1 TO 2
                     SEL.V = ' '
                   END
  ELSE DO V = 1 TO 2
          SEL.V = STRING.V
       END
 SR = 'SET RESERVED' ; SET CMDLINE OFF ; SET TOFEOF OFF; SET PREFIX OFF
 SR 1  NOH '%&                        NOTE CREATION PANEL'
 SR 2  NOH '%&                         *******************'
 SR 3  NOH
 SR 4  NOH '%{PLEASE ENTER INFORMATION TO ALL QUESTIONS BELOW AND THEN',
           'HIT ENTER,'
 SR 5  NOH '%{OR HIT PF3 TO QUIT.'
 SR 6  NOH
 SR 7  NOH
 SR 8  NOH '%$WHAT IS THE TITLE OF THE NOTE YOU WISH TO SEND',
           '(35 CHARS. MAX)?'
 SR 9  NOH
 SR 10 NOH '%$====>%@'LEFT(SEL.1,35)'%$'
 SR 11 NOH
 SR 12 NOH
 SR 13 NOH '%$WHO DO YOU WISH TO SEND THE NOTE TO?  ENTER, SEPARATED BY'
 SR 14 NOH '%$BLANKS, CMS USERID(S) AND/OR D-NAME(S).'
 SR 15 NOH
 SR 16 NOH '%$====>%@'LEFT(SEL.2,40)'%$'
 SR 17 NOH
 SR 18 NOH
 SR 19 NOH '%$DO YOU REQUIRE A REPLY FROM THE RECIPIENT(S) (Y OR N)?'
 SR 20 NOH
 SR 21 NOH '%$====>%@'LEFT(SEL.3,1)'%$'
 SR 22 NOH
 SR 23 NOH
 SR 24 NOH
 IF REDO <> 0 THEN DO
         SOS ALARM
         SR 24 NOH '%\YOU HAVE NOT ENTERED ALL OF THE REQUIRED',
                   'PARAMETERS, PLEASE DO SO.'
       END
  IF ERRCODE = 1 THEN DO
         SOS ALARM
         SR 24 NOH '%\'STRING.3' IS NOT VALID, ENTER A Y OR AN N.'
         CURSOR SCREEN 21 8
         RETURN
       END
 IF COUNT = 1 THEN CURSOR SCREEN 10 8
  ELSE SELECT
            WHEN STRING.1 = ' ' THEN CURSOR SCREEN 10 8
            WHEN STRING.2 = ' ' THEN CURSOR SCREEN 16 8
            WHEN STRING.3 = ' ' THEN CURSOR SCREEN 21 8
            OTHERWISE CURSOR SCREEN 10 8
       END
 REDO = 0
 ERRCODE = 0
 RETURN


NOTEPF4 XEDIT

 /* THIS XEDIT MACRO ALLOWS THE USER TO DELETE NOTES FROM         */
 /* THEIR NOTES LIST.         */
 PULL LINENUM
 'EXECIO 1 DISKR PANOTES XEDIT A 'LINENUM' (FINIS'
 PULL LINE
 NUMWORDS = WORDS(LINE)
 TIME = WORD(LINE,NUMWORDS); DATE = WORD(LINE,NUMWORDS - 1)
 FROM = WORD(LINE,NUMWORDS - 2); SIZE = WORD(LINE,NUMWORDS - 3)
 STAT = WORD(LINE,1)
 IF STAT = 'REPLY' THEN SIGNAL NO_DELETE2
 IF STAT = 'NEW' THEN SIGNAL NO_DELETE3
 'LISTFILE PANOTES XEDIT A (LIFO ALLOC'
 PULL . . . . . RECS .
 IF RECS = 1 THEN DO
                   'XEDIT FNBNOTES REPOS A (NOSC NOPROF NOM'
                   'TOP'
                   'LOCATE /'FROM'/ & +'DATE'+ & /'TIME'/ & /'SIZE'/'
                   IF RC <> 0 THEN DO; SAY 'LOCATE ERROR'; EXIT; END
                   EXTRACT '/CURLINE/'
                   PARSE UPPER VAR CURLINE.3 . . . . . . . TITLE
                   'QQUIT'
                   'ERASE PANOTES XEDIT A'
                   'ERASE FNBNOTES REPOS A'
                  END
  ELSE DO
         'XEDIT PANOTES XEDIT A (NOPROF NOSC NOM'
         'TOP'
         'DOWN' LINENUM
         'DELETE'
         'XEDIT FNBNOTES REPOS A (NOSC NOPROF NOM'
         'TOP'
         'LOCATE /'FROM'/ & +'DATE'+ & /'TIME'/ & /'SIZE'/'
         IF RC <> 0 THEN DO; SAY 'LOCATE ERROR'; EXIT; END
         EXTRACT '/CURLINE/'
         PARSE UPPER VAR CURLINE.3 . . . . . . . TITLE
         'DELETE'
         DO SIZE
           'DELETE'
         END
        'FILE'
        'FILE'
   END
 'SET CMSTYPE RT'
 TITLE = STRIP(TITLE,L)
 PUSH 'THE NOTE "'TITLE'" HAS BEEN DELETED.'
 EXIT
NO_DELETE2:
 PUSH 'THE DELETE OPTION IS NOT ALLOWED ON NOTES AWAITING A REPLY.'
 EXIT
NO_DELETE3:
 PUSH 'THE DELETE OPTION IS NOT ALLOWED ON NOTES WITH A STATUS OF NEW.'
 EXIT


NOTEPF5 EXEC

 /* THIS EXEC ALLOWS THE USER TO AMEND/CREATE A DIST-       */
 /* RIBUTION-LIST FILE.                                     */
 VMFCLEAR
 'SET CMSTYPE HT'
 'STATE D-LNOTES ' USERID() 'A'
 CODE = RC
 'SET CMSTYPE RT'
 IF CODE <> 0 THEN CALL NO_FILE
 'XEDIT D-LNOTES 'USERID() 'A'
 EXIT
NO_FILE:
 USTRING = LENGTH(USERID()) + 25
 USTRING = COPIES('=',USTRING)
 ST.1 = "           "USERID()"'S DISTRIBUTION-LIST FILE  -  CREATED",
        "ON "DATE(E)
 ST.2 = '           'USTRING'     ==================='
 ST.3 = ' '
 ST.4 = 'D-NAMES   VALID CMS USERIDS'
 ST.5 = '========  ==================================================',
        ||'=========='
 ST.6 = ' '
 ST.7 = 'D-EG1     CMSID1 CMSID2 CMSID3 CMSID4 CMSID5 CMSID6 CMSID7'
 ST.8 = 'D-EG1     CMSID8 CMSID9 ETC.'
 ST.9 = ' '
 ST.10 = 'D-EG2     TAH TGAS'
 ST.11 = ' '
 ST.12 = ' THE ABOVE 2 ITEMS ARE FIRSTLY THE GENERAL DISTRIBUTION LIST'
 ST.13 = ' FORMAT FOLLOWED BY AN EXAMPLE. ALL D-NAMES MUST BEGIN WITH'
 ST.14 = " 'D-' FOLLOWED BY UP TO 6 ALPHANUMERIC CHARACTERS AND MUST"
 ST.15 = ' START IN COLUMN 1. ALL LINES MUST BEGIN WITH A D-NAME.'
 ST.16 = ' '
 ST.17 = ' VALID CMS USERIDS LISTED MUST START IN COLUMN 11 OR MORE'
 ST.18 = ' AND BE SEPARATED BY BLANKS. IF MORE THAN ONE LINE IS REQ-'
 ST.19 = ' UIRED THEN CODE THESE SUBSEQUENT LINES USING THE SAME'
 ST.20 = ' D-NAME AS THE FIRST LINE.'
 ST.21 = ' '
 ST.22 = ' THERE IS NO LIMIT TO THE NUMBER OF CMS USERIDS LISTED OR'
 ST.23 = ' D-NAMES DEFINED, BAR STORAGE AVAILABLE.'
 ST.24 = ' '
 ST.25 = ' BLANK LINES ARE IGNORED, BUT IT IS SUGGESTED YOU USE THEM'
 ST.26 = ' TO AID READABILITY.'
 ST.27 = ' '
 ST.28 = ' TYPE YOUR DEFINITIONS BELOW AND THEN TYPE "FILE" ON THE'
 ST.29 = ' COMMAND LINE WHEN YOU HAVE FINISHED.'
 ST.30 = ' '
 ST.31 = ' NOTE:   DO NOT ALTER ANYTHING IN THE FIRST 37 LINES'
 ST.32 = '         (NON-USER AREA).'
 ST.33 = ' '
 ST.34 = '------------------------------------------------------------',
         ||'----------'
 ST.35 = ' '
 ST.36 = 'D-NAMES   VALID CMS USERIDS (ENTER YOUR DEFINITIONS BELOW)'
 ST.37 = '========  ==================================================',
         ||'=========='
 ST.38 = ' '
 ST.39 = ' '
 ST.40 = ' '
 ST.41 = ' '
 ST.42 = ' '
 ST.43 = ' '
 DO N = 1 TO 43
  IF N = 1 THEN 'EXECIO 1 DISKW D-LNOTES 'USERID()' A F 80 (STRING' ST.N
  'EXECIO 1 DISKW D-LNOTES 'USERID()' A (STRING' ST.N
 END
 'EXECIO 0 DISKW D-LNOTES 'USERID()' A (FINIS'
 RETURN


NOTEPF6 XEDIT

 /* This XEDIT macro allows the user to SEND the notes from       */
 /* their notes list.                                             */
 Pull Line
 Line_number = Line
 'EXEC D-LNOTEV'
 If rc <> 0 then Do
                  Parse pull Mess
                  'QQUIT'
                  Push Mess
                  Exit 4
                 End
 'EXECIO 1 DISKR PANOTES XEDIT A 'Line' (FINIS'
 Pull Line
 Numwords = WORDS(Line)
 Time = WORD(Line,Numwords); Date = WORD(Line,Numwords - 1)
 From = WORD(Line,Numwords - 2); Size = WORD(Line,Numwords - 3)
 Stat = WORD(Line,1)
 If Stat = 'NEW' then signal NO_SEND2
 If Stat = 'OPENED' then signal NO_SEND4
 'Top'
 'Locate /'From'/ & +'Date'+ & /'Time'/ & /'Size'/'
 If rc <> 0 then Do; Say 'LOCATE ERROR'; 'QQUIT'; Exit; End
 N = 1
 Extract '/Curline/'
 Parse var Curline.3 . . . . . Size Stat .
 Extract '/Line/'
 Parse var Line.1 Other_linenum
 'Down 1'
 Do N = 1 to Size
   Extract '/Curline/'
   Parse var Curline.3 Line.N
   'Down 1'
 End
 'Set Cmstype ht'
 'ERASE SENDING EXEC A'
 'ERASE 'Userid()' FNMAIL A'
 'Set Cmstype rt'
 Do S = 1 to Size
    If S = 1 then Do
           'EXECIO 1 DISKW 'USERID()' FNMAIL A 1 F 80 (STRING' Line.S
           End
     Else 'EXECIO 1 DISKW 'USERID()' FNMAIL A (STRING' Line.S
 End
 If Stat = 'REPLY' then call REPLY_SECTION
 'EXECIO * DISKR 'USERID()' FNMAIL A 1 (LOCATE /o:/ FINIS'
 Pull . .
 Pull To_line
 Num = WORDS(To_line)
 Y = 0
 Do N = 3 to Num
    Y = Y + 1
    User.Y = WORD(To_line,N)
 End
 'EXECIO 1 DISKW SENDING EXEC A (STRING /* */'
 Do X = 1 to Y
  If SUBSTR(User.X,1,2) = 'D-' then call DLIST_SEND
   Else Call USER_SEND
 End
 'EXECIO 0 DISKW SENDING EXEC A (FINIS'
 'Set Cmstype ht'
 'SET EMSG OFF'
 'SET IMSG OFF'
 'EXEC SENDING'
 'SPOOL PUNCH SYSTEM'
 'SET EMSG ON'
 'SET IMSG ON'
 'Set Cmstype rt'
 If rc <> 0 then Do
                 'Set Cmstype ht'
                 'ERASE SENDING EXEC A'
                 'Set Cmstype rt'
                 'QQUIT'
                 Push 'Error sending note, please contact Technical',
                      'Support.'
                 Exit 4
                End
 'Set Cmstype ht'
 'SET EMSG OFF'
 'ERASE SENDING EXEC A'
 'ERASE 'Userid()' FNMAIL A'
 If Stat = 'CREATE' then Do
                           'XEDIT PANOTES XEDIT A (NOPROF NOM NOSC'
                           'Top'
                           'Down' Line_number
                           'CHANGE / CREATE / SENT   / 1 1'
                           'FILE'
                           'Top'
                           'Down' Other_linenum
                           'CHANGE / CREATE / SENT   / 1 1'
                           'FILE'
                         End
 If Stat = 'REPLY' then Do
                          'XEDIT PANOTES XEDIT A (NOPROF NOM NOSC'
                          'Top'
                          'Down' Line_number
                          'CHANGE / REPLY  / SENT   / 1 1'
                          'FILE'
                          'Top'
                          'Down' Other_linenum
                          'CHANGE / REPLY  / SENT   / 1 1'
                          'FILE'
                        End
 If Stat = 'SENT' then 'QQUIT'
 'Set Cmstype rt'
 'SET EMSG ON'
 Push 'Note has been sent successfully.'
 Exit 0
DLIST_SEND:
 'Set Cmstype ht'
 'STATE D-LNOTES 'USERID()' A'
 Code = rc
 'Set Cmstype ht'
 If Code <> 0 then signal NO_DLIST
 N = 37
 Do forever
    N = N + 1
    'EXECIO * DISKR D-LNOTES 'USERID()' A 'N' (LOCATE /'User.X'/'
    If rc <> 0 then leave
    Pull . N
    Pull . Cmsids
    Numids = WORDS(Cmsids)
    Do Q = 1 to Numids
       Id = WORD(Cmsids,Q)
       'EXECIO * DISKR SENDING EXEC A 1 ( LOCATE /SPOOL PUNCH 'Id'/'
       If rc = 0 then Do
                       Pull .
                       Pull .
                       Iterate Q
                      End
       Line.1 = '"SPOOL PUNCH 'Id'"'
       Line.2 = '"PUNCH 'USERID()' FNMAIL A (NOHEADER"'
       Line.3 = '"M 'Id' I have just sent you a note, If you are      ',
                ' currently in FNBNOTES hit PF2 to receive it,',
                'otherwise enter FNBNOTES."'
       Do A = 1 to 3
                'EXECIO 1 DISKW SENDING EXEC A ( STRING 'Line.A
       End
    End
 End
 If N = 38 then Do
                 'Set Cmstype ht'
                 'ERASE SENDING EXEC A'
                 'Set Cmstype rt'
                  Signal BAD_DNAME
                End
 Return
USER_SEND:
 'EXECIO * DISKR D-LNOTEV EXEC F 1 (LOCATE /RESTRICTED_USERS:/'
 If rc <> 0 then say 'ERROR'
 Pull . N
 Pull .
 'EXECIO * DISKR D-LNOTEV EXEC F 1 (LOCATE /RES_USERS:/'
 If rc <> 0 then say 'ERROR'
 Pull . Stop1
 Pull .
 Stop1 = Stop1 - 1
 Do forever
    N = N + 1
    If N = Stop1 then leave
    'EXECIO * DISKR D-LNOTEV EXEC F 'N' (LOCATE / 'User.X' /'
    If rc = 0 then signal REST_USERS
 End
 'EXECIO 0 DISKR D-LNOTEV EXEC F (FINIS'
 'EXECIO * CP (STRING CP Q 'User.X
 Pull Response
 If WORD(Response,1) <> 'HCPCQU045E' &,
    WORD(Response,1) <> User.X then Do
                                      'Set Cmstype ht'
                                      'ERASE SENDING EXEC A'
                                      'Set Cmstype rt'
                                      Signal BAD_USER
                                    End
 'EXECIO * DISKR SENDING EXEC A ( LOCATE /'User.X'/'
 If rc = 0 then Do
                  Pull .
                  Pull .
                  Return
                End
 Line.1 = '"SPOOL PUNCH 'User.X'"'
 Line.2 = '"PUNCH 'USERID()' FNMAIL A (NOHEADER"'
 Line.3 = '"M 'User.X' I have just sent you a note, If you are      ',
          ' currently in FNBNOTES hit PF2 to receive it,',
          'otherwise enter FNBNOTES."'
 Do A = 1 to 3
   'EXECIO 1 DISKW SENDING EXEC A ( STRING 'Line.A
 End
 Return
NO_SEND2:
 'QQUIT'
 Push 'You are not allowed to SEND notes with a status of NEW.'
 Exit 4
NO_SEND4:
 'QQUIT'
 Push 'You are not allowed to SEND notes with a status of OPENED.'
 Exit 4
REST_USERS:
 Pull .
 Pull .
 'EXECIO 0 DISKR D-LNOTEV EXEC F (FINIS'
 'QQUIT'
 Push 'Send Aborted - User' User.X 'is a restricted user.'
 Exit 4
BAD_USER:
 'QQUIT'
 Push 'Send aborted - 'User.X' does not exist.'
 Exit 4
BAD_DNAME:
 'QQUIT'
 Push 'Send aborted - D-NAME definition 'User.X' does not exist.'
 Exit 4
NO_DLIST:
 'QQUIT'
 Push 'Send aborted - There is no distribution-list so 'User.X' is',
      'invalid.'
 Exit 4
REPLY_SECTION:
 'EXECIO * DISKR 'USERID()' FNMAIL A 1 (LOCATE /From:/'
 If rc <> 0 then Do; Say 'LOCATE ERROR'; 'QQUIT'; Exit; End
 Pull . .
 Pull . From1
 From1 = STRIP(From1)
 Line2 = 'From:             'USERID()
 Line3 = 'Returned to:      'From1
 Line5 = 'Reply requested:  N'
 Line11 = '*****  THIS IS THE REPLY YOU REQUESTED FOR THE ABOVE',
         'TITLED NOTE   *****'
 Line12 = '****************************************************',
         ||'********************'
 'EXECIO 1 DISKW 'USERID()' FNMAIL A 8 (STRING 'Line2
 If rc <> 0 then Do; Say 'EXECIO ERROR'; 'QQUIT'; Exit; End
 'EXECIO 1 DISKW 'USERID()' FNMAIL A 9 (STRING 'Line3
 If rc <> 0 then Do; Say 'EXECIO ERROR'; 'QQUIT'; Exit; End
 'EXECIO 1 DISKW 'USERID()' FNMAIL A 10 (STRING 'Line5
 If rc <> 0 then Do; Say 'EXECIO ERROR'; 'QQUIT'; Exit; End
 'EXECIO 1 DISKW 'USERID()' FNMAIL A 11 (STRING 'Line12
 If rc <> 0 then Do; Say 'EXECIO ERROR'; 'QQUIT'; Exit; End
 'EXECIO 1 DISKW 'USERID()' FNMAIL A 12 (STRING 'Line11
 If rc <> 0 then Do; Say 'EXECIO ERROR'; 'QQUIT'; Exit; End
 'EXECIO 1 DISKW 'USERID()' FNMAIL A 13 (STRING 'Line12
 If rc <> 0 then Do; Say 'EXECIO ERROR'; 'QQUIT'; Exit; End
 Return


NOTEPF11 XEDIT

 /* THIS XEDIT MACRO ALLOWS THE USER TO XEDIT THE NOTES FROM    */
 /* THEIR NOTES LIST.      */
  PULL LINENUM
 ONE_ENTRY = 0
 'LISTFILE PANOTES XEDIT A (LIFO ALLOC'
 PULL . . . . . RECS .
 IF RECS = 1 THEN ONE_ENTRY = 1
 'EXECIO 1 DISKR PANOTES XEDIT A 'LINENUM' (FINIS'
 PULL LINE
 NUMWORDS = WORDS(LINE)
 TIME = WORD(LINE,NUMWORDS); DATE = WORD(LINE,NUMWORDS - 1)
 FROM = WORD(LINE,NUMWORDS - 2); SIZE = WORD(LINE,NUMWORDS - 3)
 'XEDIT FNBNOTES REPOS A (NOSC NOM NOPROF'
 'TOP'
 'LOCATE /'FROM'/ & +'DATE'+ & /'TIME'/ & /'SIZE'/'
 IF RC <> 0 THEN DO; SAY 'LOCATE ERROR'; EXIT; END
 N = 1
 EXTRACT '/CURLINE/'
 PARSE VAR CURLINE.3 . . . . . SIZE STAT .
 'DELETE'
 DO N = 1 TO SIZE
   EXTRACT '/CURLINE/'
   PARSE VAR CURLINE.3 LINE.N
   IF N = 10 THEN REPLY_OPTION = WORD(LINE.N,3)
   'DELETE'
 END
 'FILE'
 'SET CMSTYPE HT'
 'ERASE 'FROM' FNMAIL2 A'
 'SET CMSTYPE RT'
 DO S = 1 TO SIZE
    IF S = 1 THEN DO
           'EXECIO 1 DISKW 'FROM' FNMAIL2 A 1 F 80 (STRING' LINE.S
           END
    ELSE 'EXECIO 1 DISKW 'FROM' FNMAIL2 A (STRING' LINE.S
 END
 'EXECIO 0 DISKW 'FROM' FNMAIL2 A (FINIS'
 IF ONE_ENTRY = 1 THEN DO
                         'ERASE PANOTES XEDIT A'
                         'ERASE FNBNOTES REPOS A'
                         SIGNAL CONTINUE
                       END
 'XEDIT PANOTES XEDIT A (NOSC NOM NOPROF'
 'TOP'
 ':'LINENUM
 'DELETE'
 'SET TERMINAL DISPLAY'
 'FILE'
CONTINUE:
 DESBUF
 ADDRESS CMS
 PUSH 'SET CASE M I'
 'XEDIT 'FROM' FNMAIL2 A'
 'SET CMSTYPE HT'
 'SET IMSG OFF'
 'SPOOL PUNCH *'
 'PUNCH 'FROM' FNMAIL2 A (NOHEADER'
 'ERASE 'FROM' FNMAIL2 A'
 'SPOOL PUNCH SYSTEM'
 'SET IMSG ON'
 'SET CMSTYPE RT'
 IF STAT = 'SENT' THEN DO
                         FLAG = 'SENT1'
                         PUSH DATE
                         PUSH TIME
                         PUSH FLAG
                         EXIT
                       END
 IF STAT = 'OPENED' THEN DO
                          FLAG = 'OPENED'
                          PUSH DATE
                          PUSH TIME
                          PUSH FLAG
                          EXIT
                         END
 IF STAT = 'CREATE' THEN DO
                          FLAG = 'CREATE1'
                          PUSH DATE
                          PUSH TIME
                          PUSH FLAG
                          EXIT
                         END
 IF REPLY_OPTION = 'Y' THEN DO
                             FLAG = 'REPLY'
                             PUSH DATE
                             PUSH TIME
                             PUSH FLAG
                             EXIT
                            END
 IF STAT = 'NEW' THEN DO
                        FLAG = 'OPENED'
                        PUSH DATE
                        PUSH TIME
                        PUSH FLAG
                        EXIT
                      END
 SAY 'WRONG'; EXIT


NOTEPF12 XEDIT

 /* THIS XEDIT MACRO ALLOWS THE USER TO PRINT THE NOTES FROM    */
 /* THEIR NOTES LIST.       */
 PULL LINENUM
 'LISTFILE PANOTES XEDIT A (LIFO ALLOC'
 PULL . . . . . RECS .
 'EXECIO 1 DISKR PANOTES XEDIT A 'LINENUM' (FINIS'
 PULL LINE
 NUMWORDS = WORDS(LINE)
 TIME = WORD(LINE,NUMWORDS); DATE = WORD(LINE,NUMWORDS - 1)
 FROM = WORD(LINE,NUMWORDS - 2); SIZE = WORD(LINE,NUMWORDS - 3)
 'XEDIT FNBNOTES REPOS A (NOSC NOM NOPROF'
 'TOP'
 'LOCATE /'FROM'/ & +'DATE'+ & /'TIME'/ & /'SIZE'/'
 IF RC <> 0 THEN DO; SAY 'LOCATE ERROR'; EXIT; END
 N = 1
 EXTRACT '/CURLINE/'
 PARSE VAR CURLINE.3 . . . . . SIZE STAT TITLE
 'DOWN 1'
 DO N = 1 TO SIZE
   EXTRACT '/CURLINE/'
   PARSE VAR CURLINE.3 LINE.N
 'DOWN 1'
 END
 'QQUIT'
 DO S = 1 TO SIZE
    IF S = 1 THEN DO
           'EXECIO 1 DISKW 'FROM' FNMAIL A 1 F 80 (STRING' LINE.S
           END
    ELSE 'EXECIO 1 DISKW 'FROM' FNMAIL A (STRING' LINE.S
 END
 'EXECIO 0 DISKW 'FROM' FNMAIL A (FINIS'
 'SET CMSTYPE HT'
 'SET IMSG OFF'
 'SPOOL PRT SYSTEM'
 'PRINT 'FROM' FNMAIL A (UP'
 'ERASE 'FROM' FNMAIL A'
 'SET IMSG ON'
 'SET CMSTYPE RT'
 'SET TERMINAL DISPLAY'
 'QQUIT'
 TITLE = STRIP(TITLE)
 PUSH TITLE
 EXIT

Adam Helman
Systems Programmer
First National Bank plc (UK)                   c First National Bank plc 1991
