 ICCFREAD EXEC

In order to accelerate our conversion from ICCF to CMS, I needed to find an
efficient way to migrate several hundred ICCF members to CMS.  The ICCFREAD EXEC
was developed to simplify this process by allowing an entire library to be
migrated at one time.

To use ICCFREAD you must first run the DTSUTIL utility in the DOS/VSE machine
running ICCF in order to generate a punch file that can be read by ICCFREAD.
DTSPUNCH JCL is a sample DTSUTIL job that punches the entire contents of ICCF
library 24.  Note that the PUNCTL option must be specified to generate separator
records between the ICCF members.  Any squeezed ICCF members should be un-
squeezed before running DTSUTIL.  Using ROUTER or some other facility, route the
punch output from DTSUTIL to the virtual reader of the CMS machine to receive
the ICCF members.

ICCFREAD reads the DTSUTIL punch file in the virtual reader and generates
separate CMS files for each ICCF member.  The CMS files are given the same name
as the ICCF members except that the special characters '/', '.' and '&' are
changed to '-' for compatibility with CMS naming restrictions.  By default all
files are given a filetype of JCL and a filemode of A.  These defaults can be
changed if necessary.  A CMS file named ICCF LOG A is also produced that
contains the names of the ICCF members read, the ICCF library number and user-
id, and the date and time.

/*  REMARKS....: READS A RDR FILE CONTAINING THE OUTPUT OF A       */
/*             : DTSUTIL PUNCH LIBRARY FUNCTION AND GENERATES      */
/*             : INDIVIDUAL CMS FILES FOR EACH ICCF MEMBER.        */
/*             : A LOG FILE IS GENERATED CONTAINING AN ENTRY       */
/*             : FOR EACH ICCF MEMBER RECEIVED. THE PUNCTL OPTION  */
/*             : OF THE DTSUTIL PUNCH UTILITY MUST BE USED TO      */
/*             : PROVIDE SEPARATORS BETWEEN THE ICCF MEMBERS. ANY  */
/*             : SPECIAL CHARACTERS IN ICCF FILENAMES ARE CHANGED  */
/*             : TO DASHES TO CONFORM WITH CMS FILE NAMING RULES.  */
TRACE N
EOFSW = 0
MAKEBUF
EXECIO 1 CARD
IF RC ¬= 0 THEN DO
   SAY 'THERE IS NO ICCF PUNCH FILE IN YOUR READER'
   EXIT
   END
DO UNTIL EOFSW = 1
   PULL IN_REC
   IF SUBSTR(IN_REC,1,10) = 'ADD MEMBER' THEN DO
      FN = SUBSTR(IN_REC,17,8)
      FN = TRANSLATE(FN,'---','/.&'  /* FILENAME TRANSLATION   */
      OUT_FILE = FN' JCL A'          /* DEFAULT FILETYPE, MODE */
      LOGREC = 'ICCF MEMBER' SUBSTR(IN_REC,17,8) 'LIB',
         SUBSTR(IN_REC,14,2) 'USER' SUBSTR(IN_REC,26,4),
         'RECEIVED AT' DATE(U) TIME()
      EXECIO 1 DISKW ICCF LOG A '(' STRING LOGREC
      END
   ELSE
      IF SUBSTR(IN_REC,1,13) = 'END OF MEMBER' THEN DO
         FINIS OUT_FILE
         END
      ELSE DO
         IF LEFT(IN_REC,4) = '..$$' THEN
            IN_REC = OVERLAY('* $$',IN_REC)
         IF LEFT(IN_REC,4) = '../*' THEN
            IN_REC = OVERLAY('/*  ',IN_REC)
         IF LEFT(IN_REC,4) = '../&' THEN
            IN_REC = OVERLAY('/&  ',IN_REC)
         EXECIO 1 DISKW OUT_FILE 0 F 80 '(' STRING IN_REC
         END
    EXECIO 1 CARD
   IF RC ¬= 0 THEN
      EOFSW = 1
END
FINIS ICCF LOG A
DROPBUF
EXIT


DTSPUNCH JCL

* $$ JOB JNM=DTSPUNCH,CLASS=Z
* $$ PUN CLASS=Y
* $$ LST FNO=P41,CLASS=Q
// JOB DTSPUNCH    TRANSFER AN ICCF LIBRARY TO CMS
// ASSGN SYS010,DISK,VOL=SYSWK1,SHR
// EXEC DTSUTIL
PUNCH LIBRARY(24) PUNCTL
/*
/&
* $$ EOJ

David Garcia
Systems Programmer
The O'Brien Corporation (USA)      ) The O'Brien Corporation 1987









































