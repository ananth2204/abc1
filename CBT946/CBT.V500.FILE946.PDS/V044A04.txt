Automatic purging of old spool files

The following REXX program purges spool files that are more
than a specified number of days old.  It is invoked every
morning by DISKACNT.  The number of days that a spool file
is allowed to age is specified in the variable, 'GRACE', which
we have set to 8.

While we do not exempt any user-id from this purging activity,
we do purge spool files created by VMARCH that are not
current, ie not today's files.  If a site wished to exempt user-
ids, this would be the place to specify those user-ids.

The EXEC collects information on spool files by spooling its
console to itself, and doing a QUERY READER ALL,
QUERY PUNCH ALL, and QUERY PRINT ALL; closing the
console file and then reading the console file into INFO
ALLFILES.  This also accomplishes the closing of
DISKACNT's console file so it does not become too large.

We assume that all years are leap years, and that all Februarys
have 29 days.

The only file we have run across that does not have a date
specified is SADUMP.  We handle this by transferring files
without dates to MAINT.

CLEAN$QS EXEC

/* Purge old spool files. */
GRACE=8                               /* Number of days file may age.*/
   ADDRESS COMMAND
   CALL SET.PURGE.DATE                /* Calculate purge date        */
   CALL GET.INFO.ALLFILES             /* Read info on all files      */
   CALL PURGE.OLDGUYS                 /* Purge the old files         */
   RETURN
SET.PURGE.DATE:
/* Calculate number of days since day 1 of year 0. Assume all        */
/* years are leap years.                                             */
                           /* DBM.03 => Days Before Month 3 (March). */
DBM.01=0;    DBM.02=31;   DBM.03=60;  DBM.04=91;  DBM.05=121; DBM.06=152
DBM.07=182;  DBM.08=213;  DBM.09=244; DBM.10=274; DBM.11=305; DBM.12=335
'EXECIO 1 CP (STRING QUERY TIME'
PULL . . . . . DATE .
PARSE VAR DATE MM '/' DD '/' YY
TODAY_MM=MM
TODAY_MMDD= MM || '/' ||DD
JULIAN_TODAY=YY*366 + DBM.MM + DD     /* Number of days before today */
JULIAN_PURGE=JULIAN_TODAY-GRACE
RETURN                                      /* end of SET.PURGE.DATE */
GET.INFO.ALLFILES:
/* Create a file containing information on all spool files on the    */
/* system.                                                           */
'ERASE INFO ALLFILES A'
'CP SPOOL CONSOLE STOP CLOSE'
'CP PURGE READER CLASS X'
'CP SPOOL CONSOLE START TO * CLASS X NOHOLD NOTERM'
SAY 'QUERY PRINT ALL'                 /* Flag to alert PURGE.OLDGUYS */
'CP QUERY PRINT ALL'
SAY 'QUERY PUNCH ALL'
'CP QUERY PUNCH ALL'
SAY 'QUERY READER ALL'
'CP QUERY READER ALL'
'CP SPOOL CONSOLE CLOSE TO *'
'CP SPOOL CONSOLE TO SYSTEM CLASS D HOLD TERM'
'CP SPOOL READER CLASS X'
'READ INFO ALLFILES A'
RETURN                                   /* end of GET.INFO.ALLFILES */
PURGE.OLDGUYS:
/* Read INFO ALLFILES one line at a time.  If the line describes a   */
/* file that is older than the purge date, then purge it.            */
'LISTFILE INFO ALLFILES A (DATE STACK )'
PULL . . . . . NUMFILES .
SAY NUMFILES 'LINES IN INFO ALLFILES TO PROCESS.'
DO I=1 TO NUMFILES
  'EXECIO 1 DISKR INFO ALLFILES A' I  /* Info on next spool file     */
  PULL LINE
  OWNERID=SUBSTR(LINE,1,8)            /* spool file owner            */
  FILE=SUBSTR(LINE,10,4)              /* spoolid                     */
  DATE=SUBSTR(LINE,39,5)              /* date spool file was closed  */
  FDIST=SUBSTR(LINE,73,8)             /* Distcode.                   */
  FNAME=SUBSTR(LINE,54,8)             /* spool file name             */
  FPTYPE=SUBSTR(LINE,64,3)            /* 1st 3 characters of filetype*/
  SELECT
    WHEN SUBSTR(OWNERID,1,5)='QUERY' THEN DEVICE=SUBSTR(LINE,7,6)
    WHEN SUBSTR(OWNERID,1,2)='NO'    THEN DEVICE=SUBSTR(LINE,7,6)
    WHEN OWNERID='OWNERID ' THEN NOP
    WHEN FNAME='VMARCH' & FPTYPE='VMA' & FDIST='LIBSYS' ,
         & DATEÂ¬=TODAY_MMDD THEN DO    /* Purge VMARCH files if they */
        'CP PURGE' OWNERID DEVICE FILE /*   are not from today.      */
        SAY OWNERID DEVICE FILE FNAME FPTYPE FDIST DATE
        END                              /* FNAME='VMARCH' & ...     */
    WHEN DATE=X2C('0000000000') /* => SADUMP file;  they have no date*/
        THEN 'CP TRAN' OWNERID DEVICE FILE 'TO MAINT'
    OTHERWISE DO
      PARSE VAR DATE MM '/' DD
      FYY=YY                      /* Year the file was closed.       */
      IF TODAY_MM=01 & MM=12 THEN FYY=YY-1 /* Adjust if necessary    */
      FILE_DATE=FYY*366+DBM.MM+DD /* Pseudo day that file was closed */
      IF FILE_DATE<JULIAN_PURGE,  /* Closed before Purge Date ?      */
        THEN DO                   /* Yes => Purge it then.           */
                'CP PURGE' OWNERID DEVICE FILE
                 SAY OWNERID DEVICE FILE DATE 'PURGED.'  /* record   */
                 END                                        /* PURGE */
      END                                               /* OTHERWISE */
    END                                                    /* SELECT */
  END                                              /* DO TO NUMFILES */
RETURN                                       /* end of PURGE.OLDGUYS */


John Kinne
Miami University (USA)  ) Miami University 1990


