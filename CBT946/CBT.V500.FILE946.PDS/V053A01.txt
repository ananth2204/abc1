Freeing a hung printer

In our VM/SP5 shop we do a large amount of printing.  We use
many IBM 3800, STC 6100, and Xerox 9700 page printers.
These devices (especially the STC 6100s) are prone to getting
'hung up' on the channel.  Quite often, we cannot free up the
device by using the normal VM commands such as FLUSH and
HALT.  Investigation of the control blocks shows the device
status to be as follows:

o      Device is in a ready status with no I/O blocks queued.

o      DMKRSP has issued an I/O to the device and is waiting for a
      CE/DE interrupt.

Because of this condition, any interrupts posted from the
device are thrown away (VM has no I/O queued), but no more
I/O is issued from DMKRSP as it is still waiting for the
completion of the previous I/O.  The missing interrupt handler
is of no help as VM does not think that any interrupts are
queued.

The following routines will update the real device control block
to make the system believe that the device is in fact busy, and
will issue a START to the device (with a new option) that will
result in VM creating a dummy interruption for the device.

This routine has saved us innumerable IPLs and saved us time
by not having a device unavailable while waiting to schedule
an IPL.

Note: even though there are all kinds of warnings to users of
the routine concerning system corruption, this routine has never
caused any problems of this nature.  The routine has been used
with no problems hundreds of times over the past year.

MODIFICATION TO DMKCSO

This local mod will create a new start option (TRASH) for
starting a unit record device.  The function is to allow VM to
create a dummy I/O interrupt to the device (much like an idle
device when started), even though a file is currently being
printed (or so the system thinks!).

The routines currently run on VM/SP Release 5 service level
523.


DMKCSO AUXLCL


        FILE: DMKCSO AUXLCL   ---  local auxfile

L110889  LCL UPDATE - ADD "TRASH" FUNCTION TO REDRIVE I/O ON HUNG DEV


DMKCSO L110889

        FILE: DMKCSO L110889  ---  modification code

./ I 05020000          $ 05020100 0100        11/08/89 "TRASH" FUNCTION
*--------------------------------------------------------------------*
*  "TRASH" FUNCTION ADDED BY LASON - LOCAL MOD                       *
*--------------------------------------------------------------------*
TSTTRASH DS    0H
         EX    R14,CLCTRASH         "TRASH" FUNCTION?
         BNE   TSTNOSEP             NO...
         OI    LASONSW,TRASH        SET TEMP SWITCH FOR "TRASH"
         B     STSCAN               AND GO LOOK FOR ANOTHER OPTION
./ I 10860000          $ 10860100 0100        11/08/89 "TRASH" FUNCTION
         TM    LASONSW,TRASH        "TRASH" FUNCTION?
         BO    LASFATAL             YES.. REDRIVE THE I/O
./ I 10950000          $ 10950100 0100        11/08/89 "TRASH" FUNCTION
LASFATAL DS    0H
./ I 10970000          $ 10970100 0100        11/08/89 "TRASH" FUNCTION
         NI    LASONSW,X'FF'-TRASH  TURN OFF TRASH SWITCH
./ I 16520000          $ 16520100 0100        11/08/89 "TRASH" FUNCTION
         SPACE
CLCTRASH CLC   0(0,R1),=C'TRASH'
LASONSW  DC    XL1'00'
TRASH    EQU   X'80'
         SPACE


TRASHDEV EXEC

/*      FILE: TRASHDEV EXEC   ---  EXEC to blow off a hung printer
                                   requires authority to issue "STCP"
     EXEC TO ISSUE BLOW A PRINTER OFFLINE.  USED FOR HUNG DEVICES THAT
     CANNOT BE TAKEN OFFLINE VIA REGULAR COMMANDS.       */
'EXECIO 0 CP (STRING DCP 0';
 IF RC Â¬=0 THEN DO;
     SAY 'YOU DO NOT HAVE THE PROPER PRIVILEGE CLASS FOR THIS EXEC';
     EXIT;
END;
'CLRSCRN';
SAY CENTER('BLOW OFF A HUNG PRINTER',80);
SAY ' ';
SAY 'THIS EXEC IS TO BE USED IN AN EMERGENCY SITUATION ONLY.';
SAY 'DO NOT CONTINUE UNLESS YOU HAVE EXHAUSTED ALL OTHER MEANS OF ';
SAY '   GETTING THE DEVICE OFFLINE.';
SAY' ';
SAY 'THIS INCLUDES TRYING A FULL POWER-DOWN OF THE DEVICE IN ADDITION '
SAY '   TO "DRAIN", "FLUSH", "HALT", ETC.';
SAY ' ';
SAY 'INDISCRIMINATE USE OF THIS PROCEDURE CAN RESULT IN THE LOSS OF';
SAY '    THE ENTIRE VM SYSTEM.';
SAY ' ';
SAY 'THIS PROCEDURE MUST NOT BE USED WITHOUT APPROVAL OF YOUR ';
SAY 'SUPERVISOR. ALL USAGES OF THIS PROCEDURE ARE BEING LOGGED OUT AND '
SAY '   WILL BE INVESTIGATED BY SYSTEMS AND MANAGEMENT PERSONNEL';
DO UNTIL $ANSWER = 'Y';
    SAY 'DO YOU WANT TO CONTINUE? Y/N';
    PULL $ANSWER;
    IF $ANSWER = 'N' THEN DO;
        SAY 'PROCEDURE ABORTED';
        EXIT;
    END;
END;
$DEVLIST = '',
           ||'40E ',
           ||'40F ',
           ||'501 ',
           ||'502 ',

Editor's note: other device addresses can be listed here.

           ||'428 ',
$DEVOK = 'N';
'CLRSCRN';
DO UNTIL $DEVOK = 'Y';
    SAY ' ';
    SAY 'ENTER DEVICE ADDRESS OF PRINTER TO BE BLOWN OFF';
    SAY ' ';
    SAY 'ENTER 'QUIT' TO ABORT THE PROCEDURE';
    PULL $DEVICE;
    IF $DEVICE = 'QUIT' THEN DO;
        SAY 'PROCEDURE ABORTED';
        EXIT;
    END;
    IF FIND($DEVLIST,$DEVICE) = 0 THEN DO;
        SAY 'DEVICE "'||$DEVICE||'" IS NOT VALID PRINTER ADDRESS';
        END;
            ELSE
        $DEVOK = 'Y';
END;
/*  LOG OUT THE USAGE OF THIS EXEC   */
SAY ' ';
SAY 'LOGGING OUT THE USAGE OF THIS PROCEDURE FOR MANAGEMENT REVEIW';
'IDEN (STACK LIFO';
PULL $USERID AT $NODE VIA RSCS $DATE $TIME .;
'CP SPOOL PCH TO MAINT';
'EXECIO 1 PUNCH (STRING' $DATE $TIME $USERID $DEVICE;
'CP CLOSE PCH NAME TRASHDEV LOGOUT';
'CP SP PCH TO SYSTEM';
SAY 'TRASHING DEVICE:' $DEVICE 'AT' $DATE $TIME;
/*  ATTEMPT TO CLEAR THE DEVICE VIA "NORMAL" MEANS.  IF NO GOOD, THEN
    WE WILL DO THE SCARY STUFF.   */
SAY ' ';
SAY 'TRASHDEV IS DRAINING' $DEVICE;
'CP DRAIN' $DEVICE;
SAY ' ';
SAY 'TRASHDEV IS FLUSHING' $DEVICE 'WITH HOLD';
'CP FLUSH' $DEVICE 'HOLD';
SAY ' ';
SAY 'TRASHDEV IS WAITING 5 SECONDS';
'CP SLEEP 5 SEC';
SAY ' ';
SAY 'TRASHDEV IS HALTING' $DEVICE;
'CP HALT' $DEVICE;
SAY ' ';
SAY 'TRASHDEV IS WAITING 5 MORE SECONDS TO ALLOW' $DEVICE;
SAY 'TO SETTLE DOWN';
'CP SLEEP 5 SEC';
/*  IF THE DEVICE CAME OFFLINE, THEN NO NEED TO CONTINUE.  */
'EXECIO * CP (STRING Q' $DEVICE;
$QUEUED = QUEUED();
PARSE PULL $DEVDIS;
IF $DEVDIS = 'PRT  '||$DEVICE||' OFFLINE' THEN DO;
    SAY ' ';
    SAY 'DEVICE' $DEVICE 'HAS GONE OFFLINE - PROCEDURE WAS NOT NEEDED';
    EXIT;
END;
/*  THROW AWAY THE REST OF THE RESULTS FROM THE QUERY  */
$QUEUED = $QUEUED - 1;
DO $QUEUED;
    PARSE PULL $THROWAWAY;
END;
/*  LOCATE THE RDEVICE BLOCK, FIND THE 2ND DOUBLEWORD, "OR" X'04' ONTO
    IT (SET RDEVNRDY IN RDEVSTAT), AND STORE IT BACK.  FOLLOW THIS WITH
    A "START" COMMAND, WHICH WILL (HOPEFULLY), BLOW OFF THE PRINTER  */
'EXECIO * CP ( STRING LOCATE' $DEVICE;
PARSE PULL $HEADER;
PARSE PULL $RCHBLOK $RCUBLOK $RDEVBLOK;
$RDEVBLOK = RIGHT($RDEVBLOK,6,0)
$RDEVFW2.A = X2D($RDEVBLOK);
$RDEVFW2.A = $RDEVFW2.A + 4;
$RDEVFW2.A = D2X($RDEVFW2.A);
'EXECIO * CP ( STRING DCP' $RDEVFW2.A||'.'||'4';
PARSE PULL $ADDRESS $RDEVFW2 .
$NEWRDEVFW2 = BITOR('04',$RDEVFW2);
$NEWRDEVFW2 = RIGHT($NEWRDEVFW2,8,0);
SAY 'ORIGINAL STATUS OF "RDEVSTAT" IN "RDEVBLOK:' $RDEVFW2;
SAY '     NEW STATUS OF "RDEVSTAT" IN "RDEVBLOK:' $NEWRDEVFW2;
'CP STCP' $RDEVFW2.A $NEWRDEVFW2;
SAY 'TRASHDEV IS DOING DUMMY START TO' $DEVICE 'TO BLOW IT OFFLINE';
'CP START' $DEVICE 'CL X TRASH';
EXIT;


Greg Carey
Senior Systems Programmer
Lason Systems Inc (USA)                            c Greg Carey 1991
