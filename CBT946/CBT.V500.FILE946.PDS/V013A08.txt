 Automatic expiration of VM spool files

In order to keep VM's used spool space as small as possible it is obviously
advantageous to have a maintenance tool for clean-up purposes.

VM does not supply such a mechanism except for a very drastic one, CP PURGE
SYSTEM RDR ALL.

To solve this problem I developed a simple, easily installed and very effective
EXEC called EXPSPOOL.

EXPSPOOL needs only one parameter: the number of days spool files are allowed to
live in VM's spool space, for example:

     EXEC EXPSPOOL 7

After execution of the above statement, all spool files older than seven days
will be disregarded.

It is advisable to implement this solution in a 'VMUTIL' or comparable
environment to trigger its daily execution.  A sample entry in 'VMUTIL's wake-up
file could be as follows:

     .........1.........2.........3.........4.........5
     ALL     00:01:00 XX/XX/XX EXEC EXPSPOOL 7

The user executing EXPSPOOL must have class D in its USER statement in the CP
DIRECTORY to authorise the special use of the CP PURGE command.


EXPSPOOL EXEC

&TRACE OFF
*%       N O T I C E:
*% THIS STATEMENT IS NECESSARY FOR THE OPTIMISATION PROGRAM - VMFOPT
*% DE-OPTIMISE THIS MACRO BEFORE MAKING ANY CHANGES USING - VMFDEOPT
CP SP CONS STA TO * PURGE
CP Q R ALL
CP Q P ALL
CP Q PU ALL
EXECIO 1 CP (ST CL CON
&IF &RC NE 0 &GOTO 69 -EOPUR
&READ VARS * * &SPID
&EXP = &1
&IF .&EXP EQ . &EXP = 999
&THISDD = &SUBSTR OF &DATE 7 2
&THISMM = &SUBSTR OF &DATE 4 2
&THISMM = &THISMM + 12
&THISYY = &SUBSTR OF &DATE 1 2
&PREVYY = &THISYY - 1
&CALL -LEAP THISYY THISFEB
&CALL -LEAP PREVYY PREVFEB
&DAYTAB = &CONCAT OF 0031
 &PREVFEB 31303130313130313031 31 &THISFEB 31303130313130313031
&CALL -JULIAN &THISMM &THISDD THISJUL
CP ORDER R &SPID
-NXTSPL DESBUF
EXECIO 1 CARD
&IF &RC EQ 2 &GOTO 69 -EOPUR
&IF &RC NE 0 &GOTO 25 -NXTSPL
&READ VARS * &SPID
&IF .&SPID EQ . &GOTO 25 -NXTSPL
&TIEP = &TYPE OF &SPID
&IF &TIEP NE NUM &GOTO 25 -NXTSPL
&TIEP = RDR
EXECIO * CP (ST Q &TIEP &SPID ALL
 &IF &RC EQ 0 &GOTO 42 -PURGEER
&TIEP = PRT
EXECIO * CP (ST Q &TIEP &SPID ALL
&IF &RC EQ 0 &GOTO 42 -PURGEER
&TIEP = PUN
EXECIO * CP (ST Q &TIEP &SPID ALL
&IF &RC NE 0 &GOTO 25 -NXTSPL
-PURGEER SENTRIES
&LOOP 1 &RC
&READ ARGS
&SPOLMM = &SUBSTR OF &8 1 2
&WORK1 = &TYPE OF &SPOLMM
&IF &WORK1 EQ CHAR &GOTO 25 -NXTSPL
&SPOLMM = &SPOLMM + 12
&IF &SPOLMM GT & THISMM &SPOLMM = &SPOLLMM - 12
&SPOLDD = &SUBSTR OF &8 4 2
&CALL -JULIAN &SPOLMM &SPOLDD SPOLJUL
&SPOLJUL = &SPOLJUL + &EXP
&IF &SPOLJUL LE &THISJUL CP PUR SYSTEM &TIEP &2
&GOTO 25 -NXTSPL
-LEAP &&2 = &DIV OF &&1 4
&&2 = &MULT OF &&2 4
&IF &&1 EQ &&2 &&2 = 29
&IF &&2 NE 29 &&2 = 28
&RETURN
-JULIAN &WORK1 = 1
&&3 = 0
&LOOP -ENDJUL &1
&WORK2 = &SUBSTR OF &DAYTAB &WORK1 2
&&3 = &&3 + &WORK2
&WORK1 = &WORK1 + 2
-ENDJUL
&&3 = &&3 + &2
&RETURN
-EOPUR CP SP CONS STOP OFF


A J M Van Tol
Senior Systems Programmer (The Netherlands)  ) Xephon 1987



























