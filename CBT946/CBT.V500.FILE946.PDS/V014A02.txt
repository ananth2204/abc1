 Â¨Initialise a CMS virtual machine

The Assembler program 'CMS', described in this article, acts like a PROFILE EXEC
in a CMS environment but is IPLable.

The example illustrates the initialisation of an ordinary CMS machine.  It
contains a table of CP commands issued by DIAG 8.  Because this program runs
directly under CP it can only contain instructions not causing interrupts and,
of course, DIAG instructions.

The first CP command must be SAVESYS CMS to make this program 'self saving'.
The last CP command must be an IPL (in the example, 'IPL CMSL AUTOCR') to
transfer control to a different environment, in fact, the real CMS.

The CP commands between SAVESYS CMS and IPL CMSL AUTOCR can vary from
installation to installation depending on specific needs.  In the example the
standard 'CMS' virtual devices are set up.

An entry in the USER DIRECTORY can therefore be very small and simple (see the
example User sample).  Because the USER DIRECTORY takes precedence, overrides
can very easily be installed.

With very few modifications this program can also be used to switch between a
number of copies of, for instance, the CMS system mini-disk 190.  Installations
with many concurrent CMS users can encounter contention on the 190 mini-disk.
Depending on the 'USERID', the time of day or whatever, you can alternate
between CP LINK commands, for example:

LINK MAINT 290 190 RR

LINK MAINT 190 190 RR

LINK MAINT 390 190 RR     etc etc

To install this program or a tailored version you have to update DMKSNT (see the
example DMKSNT sample).

The first CP IPL after changing DMKSNT must be guided by a systems programmer
because the usual IPL CMS will fail the first time because it is not yet saved.
This can be done by the following commands issued by user MAINT.

1    DEF STOR 16M
2    IPL 190
3    (Generate the 'LARGE' version of CMS, named 'CMSL' in the example)
4    IPL 190
5    SAVESYS CMSL
6    DEF STOR 1M
7    IPL CMSL
8    GLOBAL MACLIB DMSSP CMSLIB
9    ASSEMBLE CMS
10   LOAD CMS
11   START CMS.

CMS will now automatically be saved and executed for the first time, finally
resulting in an IPL of CMSL.

If changes are to be made to the program CMS simply restarts at step 8.  The
changes will be effective immediately.  This is a very big advantage because it
is effective for all CMS users after their next IPL CMS explicitly issued, or
implicitly by logging on again.

If everything runs smoothly it is time to get rid of a lot of redundant
statements in the USER DIRECTORY, making directory access much faster and
maintenance easier.


 CMS ASSEMBLE

         TITLE 'EXECUTE INITIAL CP COMMANDS AND IPL CMSL'
         LCLA  &CPL
         LCLC  &SYS
&CPL     SETA  50 LENGTH OF 1 ENTRY IN THE CP COMMAND LINE TABLE
&SYS     SETC  'CMS' NAME OF SYSTEM TO BE SAVED
&SYS     CSECT
         BALR  R12,0 ESTABLISH ADDRESSABILITY
         USING *,R12
         SSM   *+1
         LM    R1,R3,=A(CPFIRST,L'CPFIRST,CPLAST)
CPNEXT   LR    R4,R1 ADDRESS OF CP COMMAND LINE
         LA    R5,CPANSW ANSWER AREA
         LR    R14,R2 CP COMMAND LENGTH
         ICM   R14,B'1000',=X'40' REQ CP TO ANSWER
         LA    R15,L'CPANSW ANSWER AREA LENGTH
         DIAG  R4,R14,8 ISSUE THE CP COMMAND
         BXLE  R1,R2,CPNEXT
         DC    Y(0) THIS POINT WILL NEVER BE REACHED
CPFIRST  DC    CL&CPL'SAVESYS &SYS'
         DC    CL&CPL'SET EMSG OFF'
         DC    CL&CPL'SET IMSG OFF'
         DC    CL&CPL'DEFINE CON 009 3215'
         DC    CL&CPL'DEFINE RDR 00C 2540'
         DC    CL&CPL'DEFINE PUN 00D 2540'
         DC    CL&CPL'DEFINE PRT 00E 1403'
         DC    CL&CPL'SP 009 CLASS T'
         DC    CL&CPL'SP 00C CLASS *'
         DC    CL&CPL'SP 00D CLASS A'
         DC    CL&CPL'SP 00E CLASS A'
         DC    CL&CPL'LINK MAINT 190 190 RR'
         DC    CL&CPL'LINK MAINT 19D 19D RR'
         DC    CL&CPL'LINK MAINT 19E 19E RR'
         DC    CL&CPL'SET EMSG ON'
         DC    CL&CPL'SET IMSG ON'
CPLAST   DC    CL&CPL'IPL CMSL PARM AUTOCR'
CPANSW   DC    XL&CPL'0' ANSWER AREA
         REGEQU  SUP
         END


DMKSNT SAMPLE

***********************************************************************
*INITIALISE CMS VIRTUAL MACHINE****************************************
***********************************************************************
       NAMESYS SYSNAME=CMS,                                            X
               SYSSIZE=64K,                                            X
               SYSVOL=VMPKXX,                                          X
               SYSCYL=,                                                X
               SYSPGCT=2,                                              X
               SYSPGNM=(0,32),                                         X
               SYSSTRT=(7,1),                                          X
               SYSHRSG=,                                               X
               VSYSRES=,                                               X
               VSYSADR=IGNORE,                                         X
               VMGROUP=NO,                                             X
               PROTECT=ON
***********************************************************************
*5664167 VM/SP CMS REL 4****************************LOADED AT 'F40000'*
***********************************************************************
       NAMESYS SYSNAME=CMSL,                                           X
               SYSSIZE=256K,                                           X
               SYSVOL=VMPKXX,                                          X
               SYSCYL=,                                                X
                SYSPGCT=218,                                            X
               SYSPGNM=(0-5,14-33,3904-4095),                          X
               SYSSTRT=(7,4),                                          X
               SYSHRSG=(244,245,246,247,248,249,250,251,252,253,254,255X
               ),                                                      X
               VSYSRES=,                                               X
               VSYSADR=IGNORE,                                         X
               VMGROUP=NO,                                             X
               PROTECT=ON


USER SAMPLE

USER SAMPLE SAMPLEPW 1M 1M G
ACCOUNT 99999999 XXXXXXXX
IPL CMS
MDISK 191 3375 954 002 VMPKXX MR


A J M van Tol
Senior Systems Programmer (The Netherlands)  ) Xephon 1987













































