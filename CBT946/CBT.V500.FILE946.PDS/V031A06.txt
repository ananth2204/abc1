Executing CP commands at shut-down time

In our installation we needed to be able to issue a CP command at
VM shut-down time.  In our case we needed to stop some DASD
caches.  At first we used an EXEC file to execute this command.
This, however, was not perfect for two main reasons:

%        The OPERATOR had to IPL CMS in order to execute the
SHUTDOWN EXEC file

%        Occasionally, one of the systems programmers would issue a
SHUTDOWN command from a user-id that did not have the
SHUTDOWN EXEC file on any of its mini-disks.

In order to remedy these problems, we developed the following
modification to VM that will attempt to issue a CP command at
shut-down time.  The modification is relatively simple and
straightforward.  The mod first saves all registers in its own save
area.  It then prepares the registers that DMKCFCMD (the
command parser/interpreter) will need.  After calling
DMFCFCMD it checks the error return code.  If this code
indicates there is any kind of error, the modification attempts to
send a MSG to the OPERATOR.  If the call to DMKCFCMD was
successful, the registers are set up for the command to be called.
There is no checking done on the call to the command to be
processed.  The original registers are then restored and DMKCPS
continues with normal SHUTDOWN processing.  This
modification can be easily changed to issue multiple commands
rather than just one.

This mod was developed and used under VM/HPO Release 3.  It
is currently being used under VM/HPO Release 4.2.  The mod has
also been successfully tested on VM/HPO Release 5.  For other
releases, the mod must be inserted after the  'BO EXIT' statement,
three lines after the DMKCPSSH label in the module DMKCPS.
The EXTERN declaration goes with the rest of DMKCPS'
EXTERNs at the beginning of the source file.

./ I 01640000          $ 1643000 3000
         EXTRN DMKCFCMD
./ I 02590000          $ 2590100 100
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         DROP  R0
         STM   R14,R12,MYSAVE  SAVE ALL USEFUL REGISTERS
         LA    R0,8           LENGTH OF COMMAND
         SLR   R2,R2          CLEAR ERROR RETURN CODE
         LA    R1,PROGCMD     ADDR OF 1ST CHAR OF 1ST TOKEN
         ST    R1,CNXTCHR     SAVE ADDR IN COMMAND BUFFER
         LA    R9,PROGCMD     ADDR OF COMMAND BUFFER
         LA    R13,SAVEAREA   GET ADDR OF OUR SAVEAREA
         CALL  DMKCFCMD       GET ADDRESS OF COMMAND
         LTR   R15,R1         WAS ROUTINE ADDRESS FOUND?
         BZ    CANTDOIT       NO. INFORM THE SYSTEM OPERATOR
         LA    R1,PROGCMD+9   ADDR OF 1ST CHAR OF 1ST TOKEN
         ST    R1,CNXTCHR     SAVE ADDR IN COMMAND BUFFER
         LR    R0,R3          PASS DMKCFC OPERAND SCAN REGS TO
         LR    R1,R4          CALLED COMMAND PROCESSOR CODE
         CALL  (15)           CALL ROUTINE TO PROCESS COMMAND
         B     DONEPMOD       CONTINUE WITH STUFF
CANTDOIT DS    0H             BRANCHED WHEN CANT DO COMMAND
         LA    R0,7           LENGTH OF COMMAND
         SLR   R2,R2          CLEAR ERROR RETURN CODE
         LA    R1,TELLOP      ADDR OF 1ST CHAR OF 1ST TOKEN
         LA    R9,TELLOP      ADDR OF COMMAND BUFFER
         LA    R13,SAVEAREA   GET ADDR OF OUR SAVEAREA
         CALL  DMKCFCMD       GET ADDRESS OF COMMAND
         LTR   R15,R1         WAS ROUTINE ADDRESS FOUND?
         BZ    DONEPMOD       NO. CONTINUE WITH NORMAL SHUTDN
         LA    R1,TELLOP+8    GET ADDR OF USERID IN COMMAND
         ST    R1,TNXTCHR     SAVE ADDR IN COMMAND BUFFER
         LR    R0,R3          PASS DMKCFC OPERAND SCAN REGS TO
         LR    R1,R4          CALLED COMMAND PROCESSOR CODE
         CALL  (15)           CALL ROUTINE TO PROCESS COMMAND
         B     DONEPMOD       CONTINUE WITH SHUTDOWN STUFF
         DS    0D
TELLOP   DC    C'MESSAGE OPERATOR CANT ISSUE SHUTDOWN CMD'
         DC    X'15'
TELLOPLN EQU   *-TELLOP
         ORG   TELLOP+240
TNXTCHR  DS    F
TCMDLEN  DC    A(TELLOPLN-8)
         DS    0D
PROGCMD  DC    C'SDICACHE STOP ALL '
         DC    X'15'
CELLOPLN EQU   *-PROGCMD
         ORG   PROGCMD+240
CNXTCHR  DS    F
CCMDLEN  DC    A(PROGCMD-8)
         DS    0D
MYSAVE   DS    9D                      LOCAL SAVE AREA
         USING PSA,R0
         DS    0H
DONEPMOD EQU   *
         LM    R14,R12,MYSAVE GET BACK ALL USEFUL REGISTERS
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *


Bernard McIlroy
Systems Programmer
Progress (USA)  ) B McIlroy 1989

