RSCS fullscreen command interface

The following REXX EXEC was written to make
communication between RSCS users and RSCS more
convenient.  It was tested under VM/HPO 5.0 and 6.0, and
under VM/XA 2.1.  It should work under VM/SP 5.0 and
above.  It is very RSCS-release dependent (because of the
parsed message numbers) and this version works for RSCS
Version 2.3.  However it is easy to modify the parse
commands to adapt it for another RSCS version.

It also needs the IBM WAKEUP module, which comes with
IPF or, under XA, with CMS Utilities.  Alternatively, you can
use a user-written program that provides a similar function.

The command format is:

      RFCI <command> < (linkid>

Where command is any RSCS command you are authorized
for, and linkid is any link-id you wish to have displayed.
If you call RFCI without parameters, a fullscreen console
comes up showing the status of your default link-id and the
files that are queued there.  The default link-id is always taken
from the tag of your virtual 00E device.  If such a tag does not
exist, you are prompted at the first call to enter a default link-
id, which is then taken for further calls from your LASTING
GLOBALV file.

When you enter a command in the command line, the
responses are displayed behind the status information for the
link-id you are currently displaying.  If the responses (eg when
you enter Q SYS LINKS) exceed one page, you can scroll with
PF7 and PF8.

For further explanations see the HELP subroutine in the
procedure.  This EXEC calls itself again as an XEDIT macro,
so that it is not necessary to have a separate display manager
with filetype XEDIT and a caller with filetype EXEC.  It runs
with filetype EXEC only.  You can also call it from FILELIST
or from within an XEDIT session.

Do not remove the two trace commands at the beginning of the
program, because, if you do, you will see a REXX error
message at each call.


RFCI EXEC

/* RSCS fullscreen command interface                                 */
/* Format is RFCI  <command> <(linkid>                               */
/* For detailed help, press F1 during the session.                   */
parse upper source . . calledname .
trace off
address xedit
'EXTRACT /FNAME /FTYPE'
surround=fname.1 ftype.1
'TOP'
trace n
if rc < 0 | surround <> '$XX$$XX$ $$X$$' then do
  address command
  parse upper arg parm
  parse value diag(8,'Q SET') with work
  do while work <> ''
    parse var work msgtype work
    if msgtype='MSG' then do
       parse var work xmsg work
       xmsg=substr(xmsg,1,4)
       work=''
    end
  end
  'CP SET  MSG IUCV'
  'WAKEUP +00:00:01 (IUCVMSG QUIET'
  if queued() > 0 then 'DROPBUF'
  queue calledname parm
  'XEDIT $XX$$XX$ $$X$$ S(NOPROF NOMSG'
  'CP SET  MSG 'xmsg
  exit
end
address xedit
parse upper arg parm
parm=strip(parm)
if parm = '?' then do
  parm=''
  call help
end
'SET TOFEOF OFF'
'SET CMDLINE OFF'
'SET SCALE OFF'
'SET LINEND OFF'
'SET MSGMODE ON'
'SET MSGLIN ON 22 1 OVERLAY'
'SET COLOR MSGLIN RED REVV'
'SET PREFIX OFF'
'SET PF03 QQUIT'
'SET CTL " N TUR NONE HIGH'
'SET CTL % P TUR REVV NOH'
'SET CTL + N GRE NONE NOH'
'SET CTL & P WHI NONE HIGH'
'SET CTL ) ESCAPE'
$UT=')"'
$PT=')%'
$UG=')+'
$PW=')&'
mid.=''; mline.=''
userdispl=0
address command 'STATE MDLOOKUP MODULE *'
if rc=0 then userswitch=1
else userswitch=0
headr=$pw'Dist----Origid---File-CL-Pri-Recs-Cpy-Hold',
                                      '---Date---Time----Fname----Ftype'
parse var parm command '(' linkid
linkid=strip(linkid)
if linkid='' then do
  parse value diag(8,'TAG QUERY DEV 00E') with line1 '15'x linkid '15'x
  if index(line1,'NOT SET') > 0 then do
    address command 'GLOBALV SELECT RFCI GET LINKID'
    if linkid='' then do
      do while linkid=''
        say
        say 'No tag set at virtual device 00E and no RFCI-default found'
        say 'in LASTING GLOBALV file. Enter an RSCS-printer-id, which'
        say 'will be assumed as default for future RFCI-Calls:'
        parse upper pull linkid
      end
      address command 'GLOBALV SELECT RFCI PUTP LINKID'
      say 'Default set to 'linkid
    end
  end
  linkid=left(linkid,8)
end
start=1; nosend=0
call rscsinfo
getinfo=0;
oldcmd=''
filestart=2
do forever
  'RESER 1 YEL NONE HIGH 'center('RSCS Fullscreen Command Interface',
                                              ' displays'$ut||linkid,79)
  'RESER 23 YEL NONE HIGH 'center('Enter=REFRESH  PF1=Help  PF3=Quit',
                     '  PF5=Switch Orig-display  PF7=Back  PF8=Forw',79)
  'RESER 24 YEL NONE HIGH '$pt'====>'$ug''command
  'CURSOR SCREEN 24 8 P 1'
  if filestart > 2 & start >= filestart then 'RESER 2 WHI NONE',
                                                          ' HIGH ' headr
  else 'RESER 2 WHI NONE HIGH  '
  do i=start to start+17
    j=i+3-start
    'RESER 'j' WHI NONE NOHIGH  'mline.i
  end
  if mline.i <> '' then 'RESER 'j+1' WHI NONE HIGH',
                                                center('M O R E >>>',79)
  else 'RESER 'j+1' WHI NONE HIGH'
  'READ ALL TAG'
  do k=1 to queued()
    parse upper pull key row col changedata
    select
      when key='ETK' then do
        mid.=''; mline.=''
        getinfo=1
        start=1
      end
      when key='PFK' then do
        getinfo=0
        if row=1 then call help
        if row=3 then do
          'QQUIT'
          return
        end
        else if row=5 then do
          mid.=''; mline.=''
          getinfo=1
          start=1
          if userswitch then do
            if userdispl then do
              headr=$pw'Dist----Origid---File-CL-Pri-Recs'||,
              '-Cpy-Hold---Date---Time----Fname----Ftype'
              userdispl=0
            end
            else do
              headr=$pw'Dist----O r i g i n a t o r--Recs'||,
              '-Cpy-Hold---Date---Time----Fname----Ftype'
              userdispl=1
            end
            if fileid.0=0 then 'EMSG PF5 works only for queued files.'
          end
          else 'EMSG PF5 works only when HDS-Mail is installed.'
        end
        else if row=7 then do
          start=start-17
          if start < 1 then start=1
        end
        else if row=8 then do
          start=start+17
          if start > mline.0 then start=mline.0
        end
      end
      when key='RES' then do
        if row=1 then linkid=left(strip(changedata),8)
        else do
          if index(changedata,' =') > 0 then do
            parse var changedata part1 '=' part2
            command=strip(part1) linkid strip(part2)
            oldcmd=command
          end
          else if strip(changedata)='?' then do
            command=oldcmd
            nosend=1
          end
          else do
            parse  var changedata word1 .
            upper word1
            command=changedata
            oldcmd=command
            if word1='CMS' then do; changedata; command=''; end
          end
        end
      end
      otherwise nop
    end
  end
  if getinfo then call rscsinfo
end
return
rscsinfo:
'CP SMSG RSCS Q 'linkid
'CP SMSG RSCS Q 'linkid' ACT'
'CP SMSG RSCS Q 'linkid' Q'
if command <> '' then do
  if nosend then nosend=0
  else do
    'CP SMSG RSCS 'command
    command=''
  end
end
k=0;x=0
'CP SLEEP 1 SEC'
fileid.0=0
actfile=''
do forever
  address command 'WAKEUP +00:00:05 (IUCVMSG QUIET'
  if rc = 2 then leave
  if rc = -2 then leave
  k=k+1
  parse pull . 'DMT' +6 mid.k mline.k
  select
    when mid.k='652I' then parse var mline.k . mline.k
    when mid.k='655I' then do
      x=x+1
      parse var mline.k . fileid.x .
      fileid.0=x
      k=k-1
    end
    when mid.k='656I' then do
      x=x+1
      parse var mline.k . fileid.x . 'left' arecs . 1 mline.k 'blocks' .
      fileid.0=x
      actfile=fileid.x
    end
    when mid.k='657I' then parse var mline.k . mline.k 'autostart=' .
    otherwise nop
  end
End
mid.0=k; mline.0=k
do k=1 to fileid.0
  'CP SMSG RSCS Q FILE' fileid.k 'RSCS'
  'CP SMSG RSCS Q FILE' fileid.k 'VM'
end
'CP SLEEP 1 SEC'
k=mline.0+1
if fileid.0 > 0 then do; mline.k=headr; filestart=k+1; end
else mline.k=' '
myuser=userid()
do forever
  address command 'WAKEUP +00:00:05 (IUCVMSG QUIET'
  if rc = 2 then leave
  if rc = -2 then leave
  parse pull . 'DMT' +6 midx mlinex
  select
    when midx='662I' then do
      parse var mlinex . file . ')' . '(' origin ')' date time .
    end
    when midx='663I' then do
      parse var mlinex . '=' pri ' copy='copy ' distribution='dist',',
       . '"' fname ftype . '"'
    end
    when midx='669I' then do
      parse var mlinex . 'class=' class . 'hold=' hold .,
                                                       'records=' recs .
      k=k+1
      if actfile=file then do
        recs=arecs
        hold=$pw'ACT'$pt
      end
      else hold=$pt||right(hold,3)||$pt
      if userdispl then do
        address command 'MDLOOKUP ALL 'left(origin,8)
        if lookup.0 > 0 then parse var lookup.1 xname 22 30 xuser 38
        else xuser=''
        if xuser='' then xuser=origin
        else xuser=strip(xname)
        if length(xuser) > 18 then ,
          xuser=substr(xuser,1,1)'.' word(xuser,2)
        if origin=myuser then origin=$pw||left(xuser,18)||$pt
        else origin=$pt||left(xuser,18)||$pt
      end
      else do
        if origin=myuser then origin=$pw||left(origin,8)||$pt
        else origin=$pt||left(origin,8)||$pt
        origin=origin||right(file,4) class right(pri,3)
      end
      mline.k=left(dist,8)||origin right(recs,5) right(copy,3)||hold
      mline.k=mline.k||right(date,8) right(time,8),
                                             left(fname,8) left(ftype,8)
      mline.0=k
    end
    otherwise nop
  end
End
return
help:
htit=center('RSCS Fullscreen Command Interface  -  Help display',79)
h.=' '
h.2 ='  Usage:    RFCI  <command> <(linkid>'
h.4 ='     command ... any RSCS-command, for which you are authorized'
h.5 ='     linkid .... any RSCS link-id you wish to display'
h.6 ='            Default is your tag for your virtual 00E. If none is'
h.7 ='            set, you are prompted to enter a default at the first'
h.8 ='            call. For later default changes you must delete the'
h.9 ='            RFCI line in your LASTING GLOBALV file.'
h.11='  The fullscreen display:                                        '
h.12='  .) The first line identifies the queried link-id (printer-id), '
h.13='    and it can be changed by overtyping it.                      '
h.14='  .) The part above the file title line displays general info about'
h.15='    the link-id and all responses from eventually entered commands.'
h.16='  .) The file title line displays the header of each row. It is  '
h.17='    not displayed, when no files are queued. It can be changed by'
h.18='    pressing PF5 to display the originators complete name instead'
h.19='    of user-id and some file-info.                               '
h.25='  .) The part below the file title line displays info about all  '
h.26='    files queued at this link-id. Files sent by the displaying   '
h.27='    user-id have a highlighted originator field, and active files'
h.28='    have a highlighted hold-parameter.'
h.29='  .) The command line: Here you can enter any RSCS commands you  '
h.30='    are authorized for. An = sign will be replaced by the current'
h.31='    link-id (ie when you are displaying the link-id P90011 and   '
h.32='    you enter DRAIN =, then P90011 will be drained).             '
h.33='      A ? sign will retrieve the last command.                   '
h.34='    Prefixing a command with CMS will run this command in        '
h.35='    CMS subset mode in your user-id.                             '
'RESER 1 YEL NONE HIGH 'htit
do h=1 to 23
  jd=h+1
  'RESER 'jd' WHI NONE NOHIGH  'h.h
end
'RESER 23 YEL NONE HIGH 'center('Press ENTER to resume',79)
'READ ALL'
address command 'DROPBUF'
do h=25 to 46
  jd=h-22
  'RESER 'jd' WHI NONE NOHIGH  'h.h
end
'RESER 23 YEL NONE HIGH 'center('Press ENTER to return',79)
'READ ALL'
address command 'DROPBUF'
return

Herbert Szumovski
System Engineer
HDS (Austria)

