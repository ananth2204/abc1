 Displaying spool space
This EXEC is used to show how much VM spool (TEMP) space is  being used and
available.  Note: CP does not statistically update the values obtained by this
EXEC, so figures are approximate only.  This EXEC is subject to change after
every CP nucleus generation and will issue error messages if the CP level has
changed.
To update this EXEC after a CP nucleus generation, do the following:
1    Obtain a copy of the most recent CPNUC map (on MAINT's 194 disk) and find
the hex memory location for DMKPGTUU.  For example, it might be at address
X'01FF94'.  Update this hex value assigned to variable DMKPGTTU below.
2    Issue the CP command 'QUERY CPLEVEL' and obtain the date and time stamp of
when this system was generated.  Use that value in the variable CPLEVEL below.
DISPOOL EXEC
_INITIAL_:
   TRACE OFF                  /* USE TRACE R FOR DEBUG.             */
   DMKPGTTU = "01FF94"        /* MEMORY LOCATION OF SPOOL SPACE DATA*/
   CPLEVEL = "08/07/87 14:52:14" /*CP GENERATED AT TIME STAMP.      */
   CYL3380 = 150              /* PAGES PER 3380 CYLINDER.           */
   RCODE   = 0                /* INITIALIZE RETURN CODE.            */
_MAIN_LINE_:
   'EXECIO * CP (STRING QUERY CPLEVEL'
                          /* OBTAIN DATE/TIME STAMP OF IPL'D SYSTEM */
   IF RC > RCODE              /* CHECK RETURN CODE FROM THE LAST    */
      THEN RCODE = RC         /* OPERATION AND STORE IT IF WORSE    */
      ELSE NOP                /* THAN ALREADY SAVED. OTHERWISE,     */
                              /* CONTINUE.                          */
   DO WHILE QUEUED() > 0      /* OUTPUT IS QUEUED TO STACK.         */
      PULL FLAG . DATE TIME . /* COMPARE WITH THAT STORED IN REXX   */
      IF FLAG = "GENERATED"   /* VARIABLE CPLEVEL TO DETERMINE IF   */
         THEN DO              /* THIS VERSION OF THIS EXEC IS VALID.*/
              IF  CPLEVEL <> DATE||' '||TIME
                  THEN CALL _WRONG_CP_LEVEL_
                              /* IF CP LEVEL IS DIFFERENT, THEN THIS*/
                  ELSE NOP    /* EXEC WILL NOT GIVE VALID OUTPUT, SO*/
              END             /* GO AND ISSUE ERROR BLURB BEFORE    */
                              /* STOPPING.                          */
         ELSE NOP
END
'EXECIO * CP (STRING DCP '||DMKPGTTU||'.C'
                              /* OBTAIN INTERNAL CP SPOOL QUANTITIES*/
IF  RC > RCODE                /* CHECK RETURN CODE FROM THE LAST    */
    THEN RCODE = RC           /* OPERATION AND STORE IT IF WORSE    */
    ELSE NOP                  /* THAN ALREADY SAVED OTHERWISE,      */
                              /* CONTINUE.                          */
DO WHILE QUEUED() > 0         /* OUTPUT IS QUEUED TO STACK.         */
   PULL . SPOOLUSE SPOOLMAX SPOOL90
END
SPOOLUSE = X2C(SPOOLUSE)       /* CONVERT TO PACKED DECIMAL.        */
SPOOLMAX = X2C(SPOOLMAX)       /* CONVERT TO PACKED DECIMAL.        */
SPOOLUSE = C2D(SPOOLUSE)       /* CONVERT PACKED TO DECIMAL VALUE.  */
SPOOLMAX = C2D(SPOOLMAX)       /* CONVERT PACKED TO DECIMAL VALUE.  */
SPOOLCYL = SPOOLUSE / CYL3380  /* DIVIDE BY PAGES PER 3380 CYLINDER.*/
SPOOLCMX = SPOOLMAX / CYL3380  /* DIVIDE BY PAGES PER 3380 CYLINDER.*/
SPOOLPER = TRUNC((SPOOLUSE /  SPOOLMAX) * 100)
                                  /* PERCENTAGE USED.               */
IF RC > RCODE                 /* CHECK RETURN CODE FROM THE LAST    */
   THEN RCODE = RC            /* OPERATION AND STORE IT IF WORSE    */
   ELSE NOP                   /* THAN ALREADY SAVED.  OTHERWISE     */
                              /* CONTINUE                           */
SAY                           /* DISPLAY THE RESULTS WITH A HEADING.*/
SAY DATE(0) TIME() 'VM/SP SPOOL IS '||SPOOLPER||' % UTILIZED.'
SAY 'AVAILABLE PAGES: '||SPOOLMAX||'    PAGES USED: '||SPOOLUSE
SAY 'AVAILABLE CYLINDERS: '||SPOOLCMX||'   CYLINDERS USED: '||SPOOLCYL
SAY
IF RCODE <> 0                 /* ISSUE AN ERROR BLURB IN CASE       */
   THEN DO                    /* SOMETHING GOES A BIT UNEXPECTEDLY. */
         SAY
        SAY 'WARNING: NON-ZERO RETURN CODE FROM SPOOL EXEC.'
        SAY
        END
   ELSE NOP
EXIT RCODE                    /* USE HIGHEST RETURN CODE OBTAINED   */
                              /* EARLIER                            */
_WRONG_CP_LEVEL_:
 SAY                          /* ISSUE MESSAGES BEFORE EXITING EXEC */
 SAY 'WRONG CP LEVEL DETECTED. CANNOT DISPLAY ACCURATE SPOOL INFORMATION.'
 SAY 'PLEASE UPDATE SPOOL EXEC WITH CORRECT CP LEVEL AND DMKPGTUU LOCATION.'
 SAY
 EXIT RCODE                   /* USE HIGHEST RETURN CODE OBTAINED   */
                              /* EARLIER                            */
 RETURN
George Perkins
Senior Technical Services Specialist
Oscar Mayer Foods Corporation (USA)     ) Xephon 1989
















































