The STAMP macro - 'rubber stamps' for XEDIT

Software programs that imitate hardware counterparts such as
calculators, notepads, clipboards, and spreadsheets are often
the most practical applications of our trade. One handy office
tool in the 'real' world is the rubber stamp. I have written the
following XEDIT macro which imitates a carousel of rubber
stamps.

You can make up stamps for your name, the name of your
company, the date, time, day of the week, or just about
anything. The comments at the beginning of the macro describe
its use and are displayed as the help screen if 'stamp help' is
issued at the command line while in XEDIT. Please read the
comments for syntax and usage. Rather than repeat the
instructions I have included a short case study of how the
stamp macro can be used.


THE CASE STUDY

Mr R U Somebody is the accounting manager of the XYZ
company. He reviews a report showing the status of open
payables. Each account is listed as a line on the report, see
Figure 1.

Obviously this report is nonsense and this case study is
completely fictitious. Let's suppose that Mr Somebody is sent
this report as a CMS file every week. He invokes XEDIT on
the file (perhaps using a custom profile) and tabs to the
comments section. Then he moves the cursor to the third line
and presses PF5, which has been established as a stamp for "<-
- Pay immediately!". Then he moves the cursor to the last line
and presses PF6, which stamps the time and his initials, see
Figure 2. The file is then sent to the accounts payable
department.

I hope you find the macro more amusing than the case study. I
recommend assigning stamps in XEDIT files which can be
invoked as needed. Such an XEDIT macro can also display the
stamp settings on reserved lines at the bottom of the screen.


RUS XEDIT

/* RUS XEDIT A */
'STAMP PAYIT <-- Pay immediately!'
'COMMAND SET PF5 STAMP PAYIT'
'STAMP RUS reviewd by...R.U.S.' substr(time(),1,5)
'COMMAND SET PF6 STAMP RUS'
'COMMAND SET RESERVED -2 BLUE NONE NOHIGH' COPIES('_',78)
text = "PF5=Pay immediately!   PF6=R.U.S."
'COMMAND SET RESERVED -1 BLUE NONE NOHIGH' text
exit


STAMP XEDIT

/* STAMP XEDIT macro                                                  */
/*  The STAMP macro allows you to "stamp" a string of text anywhere   */
/*  in a CMS file.  Many stamps can be built and assigned names for   */
/*  insertion into files.  Stamps can be built on the fly:            */
/*            ====> stamp jqp John Q. Public                          */
/*            ====> stamp logo XYZ Corporation, Inc                   */
/*  or issued in your PROFILE XEDIT file:                             */
/*            'STAMP HEADING' userid() '----' date() '---' time()     */
/*  A PF key can be assigned to the STAMP macro for ease of use:      */
/*            ====> set pf6 stamp logo   (using logo as an example)   */
/*  To use a stamp during an XEDIT session, move the cursor to a      */
/*  row and column on the screen and press the PF key (or issue       */
/*  'stamp logo' on the command line before moving the cursor).       */
/*  To view a stamp's assigned text enter: ====> stamp logo ?         */
/*  To view the list of all stamps enter:  ====> stamp ?              */
  parse ARG id text
  parse upper var id id
  stamp = 'STAMP'
  if id = '' then,
     exit
  if id = '?' then,
     signal QUERY_ALL
  if id = 'HELP' then,
     signal HELP
  if text = '' then,
     signal INSERT
  if text = '?' then,
     signal QUERY
ASSIGN:
  STAMP.id = text
  'GLOBALV SELECT STAMP PUT STAMP.'id
  exit
INSERT:
  'GLOBALV SELECT STAMP GET STAMP.'id
  if STAMP.id = '' then,
     exit
  'COMMAND EXTRACT /CURSOR/SIZE/LINE'
  if CURSOR.3 < 1 then,                    /* CURSOR.3 = cursor line */
     exit                                  /* CURSOR.4 = cursor col  */
  if CURSOR.3 > SIZE.1 then,               /* SIZE.1   = file size   */
     exit                                  /* LINE.1   = current line*/
  'COMMAND :' CURSOR.3
  here = CURSOR.4 - 1
  'COMMAND CLOCATE' here
  'COMMAND CREPLACE' STAMP.id
  'COMMAND :' LINE.1
  'COMMAND CLOCATE :1'
  exit
QUERY:
  'GLOBALV SELECT STAMP GET STAMP.'id
  if STAMP.id = '' then,
     do
     msg "No text has been assigned to" id
     exit
     end
  else
     do
     msg "Text for" id "is:" SUBSTR(STAMP.id,1,60)
     exit
     end
QUERY_ALL:
  'VMFCLEAR'
  say " "
  say "The following is a list of XEDIT stamps assigned by" USERID()
  say "which are stored in the following global table..."
  say " "
  'GLOBALV SELECT STAMP LIST'
  exit
HELP:
  'EXTRACT /RESERVED *'                     /* Save reserved lines    */
  do i = 2 to 22                            /* Display help screen    */
     xxx  = CENTER(SOURCELINE(i),80)
     j = i + 1
     'SET RESERVED' j 'BLUE NONE H' xxx
  end
  msg "Press ENTER or any PF key to remove this help screen."
  'MAKEBUF'
  'READ ALL TAG'                            /* Intercept term. input  */
  'DROPBUF'
  do ix = 3 to 23                           /* Remove help screen     */
     'SET RESERVED' ix 'off'
  end
  do ix = 3 to RESERVED.0                   /* Restore reserved lines */
     'SET RESERVED' RESERVED.ix
  end
  exit

P C Shumway
Project Leader
Masonite Corporation (USA)                         c P C Shumway 1993

