Single line hex dump

This is a REXX program written for XEDIT to display the
current line in DITTO dump format.  The window contains the
current line and the hex representation of the current line.  If
the current line is longer than the screen size (ie greater than 80
columns) use the prefix commands L or R to move left or right
in the window.  The only restriction on line size is the amount
of virtual storage in the CMS virtual machine.  However, any
lines over 5000 characters could take a significant amount of
time to process.  If you work with large records you should
modify the program to dump specified columns.  IBM manual
CMS Users Guide SC19-6210-4 for VM/SP Release 5,
Chapters 9,15 and Appendix D list the window and prefix
commands as well as providing an overview of windows.

To use this program type the name HEXLINE on the command
line in XEDIT.  A window will be displayed on line 4 of the
screen showing the current line with the hex data.  Hitting enter
will remove the window from the screen.  The position on the
screen where the window is displayed can be changed in the
line 'DEFINE WIN HEXWIN 5 76 4 3'.  Change the 4 to the
line number you want the first line of the border to appear on.
If the current line is the TOF or EOF line or if the line has no
characters, then a message is displayed on the XEDIT
command line indicating the line has no length and cannot be
converted.

/*  HEXLINE displays the current line in DITTO dump format         */
trace N
MAIN:
 'SET CMSTYPE HT'
 call HEX_CURR_LINE
 call SET_UP_WINDOW
 call CLOSE_WINDOW
exit
HEX_CURR_LINE:
 ADDRESS XEDIT 'EXTRACT /LENGTH/CURLINE'
 if LENGTH.1 = 0 then do
    ADDRESS XEDIT 'CMSG Line has no length (TOF EOF)'
    exit
 end
 RECLEN    = LENGTH.1
 RECORD    = CURLINE.3
 HEXREC    = C2X(RECORD)
 RECORD    = '' || RECORD
 HEXRECTOP = ''
 HEXRECBOT = ''
 CHR_CNT   = 1
 do RECLEN
    HEXRECTOP = HEXRECTOP || substr(HEXREC,CHR_CNT,1)
    CHR_CNT   = CHR_CNT + 1
    HEXRECBOT = HEXRECBOT || substr(HEXREC,CHR_CNT,1)
    CHR_CNT   = CHR_CNT + 1
 end
 RECLEN = RECLEN + 1
return
SET_UP_WINDOW:
 'DEFINE VSC HEXSCRN' 5 RECLEN 2 0 '(PROTECT WHITE)'
 'DEFINE WIN HEXWIN 5 76 4 3'
 'SHOW WIN HEXWIN ON HEXSCRN 1 1'
 'SET BORDER HEXWIN ON (TOP - BOTTOM - LEFT | RIGHT | HIGH GREEN)'
 'WRITE VSC HEXSCRN 1 1' RECLEN '(PROTECT WHITE FIELD' RECORD
 'WRITE VSC HEXSCRN 2 1' RECLEN '(PROTECT WHITE FIELD' HEXRECTOP
 'WRITE VSC HEXSCRN 3 1' RECLEN '(PROTECT WHITE FIELD' HEXRECBOT
 'WAITREAD VSC HEXSCRN'
return
CLOSE_WINDOW:
 'DELETE VSC HEXSCRN'
 'DELETE WIN HEXWIN'
 'SET CMSTYPE RT'
 'DROPBUF'
return

John Stith (USA) ) Xephon 1990


