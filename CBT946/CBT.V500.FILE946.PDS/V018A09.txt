 Load PC from VSE/VSAM dataset
This EXEC was designed to download a file from a DOS/VSE VSAM file, through
VM/SP, onto the user's PC.  This need exists in today's environment where users
have to access corporate database files but prefer using the processing power of
the PC.
This was written in REXX from VM/SP 3.1.  VSAM is 3.5 running under DOS 3.1.
VSE/SP 2.1.4.  PCVMBOND 2.1 is used to move the files from CMS to PC DOS.  We
use 3370 (FBA) DASD.
Below are outlined the sections that need user tailoring for individual shops.
Replace the information that applies to our shop with your own environment
specific information:
1    Our VSAM packs are SYSWK3 through to SYSWK6
2    These VSAM packs are owned by the VM user WIIVSE
3    All VSAM files belong to a single user catalog, USER.CATALOG.ONE
4    The name of the output CMS file is DOWNLOAD WORK A
5    A work file called PCLOAD START is used to build the IDCAMS parameters
6    The information is loaded onto the PC as A:DOWNLOAD.DAT
7    The IDCAMS results are contained in a CMS file called PCLOAD LISTING A.
Since this procedure is not documented in any one place, I will outline the EXEC
section by section.
1    /* PROMPT FOR THE DOS/VSE/VSAM FILE NAME */  Here the user is asked to
supply the VSAM dataset name that is to be downloaded.  If the user presses the
ENTER key without entering a name, he will be re-prompted.  The VSAM dataset
name is then assigned to the variable, VSAMDSN.
2     /* PROMPT FOR THE DOS/VSE VSAM VOLUME NAME*/ Here the user is asked to
supply the VSAM volume name where the dataset entered above resides.  As above,
the user must enter a name, or will be re-prompted.  The VSAM volume name is
then assigned to the variable SYSWK.
3    /* CLEAR THE SCREEN AND...   */
     First the screen is cleared.  Next the contents of the SYSWK variable are
tested to match the specific environment.  If they do not match, an error
message is displayed and the processing is terminated with a return code of 3.
4    /* DISPLAY THE XEDIT MESSAGE */                        Here the user is
informed of what information must be entered in the IDCAMS parameter file when
it is XEDITed.
5    /* ACCESS THE VSAM USER CATALOG */                                    First
the console responses are disabled.  Next, all of the DOS DLBLs are cleared.
Then the User catalog file is assigned.  This is done by first releasing and
detaching a specific device and mode to assure no conflicts arise.  Then the
DOS/VSE volume wherein the user catalog resides is LINKed to, and then ACCESSed.
Lastly, the DLBL assigns the IJSYSCT (user catalog name).
6    /* ACCESS THE VSAM VOLUME NEEDED */                              This
section LINKs and ACCESSes the DOS VSAM volume that was entered earlier by the
user.  Likewise, the mini-disk is released and detached first to prevent
conflicts.
7    /* SPECIFY THE CMS OUTPUT (WORK)  FILE *     /             This is done
with a DLBL card that names the output file from the IDCAMS operation (FILEOT)
as being DOWNLOAD WORK A.
8    /* ERASE THE CMS...   */
                         First the resulting CMS file is erased, as is the
IDCAMS parameter file.  Next the work file (PCLOAD START) is copied to an AMSERV
file on the A disk (AMSERV filetype needed to work with VM IDCAMS process -
AMSERV).  Lastly, the IDCAMS parameter file is XEDITed for user tailoring.  In
this step, the record type (F or V - for Fixed or Variable) is entered, along
with the record size.  If only a selected number of records is desired, a COUNT
statement may be added.
9    /* CLEAR THE SCREEN AND DISPLAY THE 'PROCESSING' MESSAGE */
               First the screen is cleared.  Next the user is notified that the
IDCAMS REPRO process has begun.
10   /* EXECUTE THE VM VSAM IDCAMS PROCESS */               First, the console
responses are enabled once again.  Next the AMSERV (VM IDCAMS function) command
is executed, using the parameters supplied in the PCLOAD AMSERV A file that has
just been XEDITed.
11   /* IF THE IDCAMS PROCESS WAS SUCCESSFUL (RC=0), ...   */If the load was
successful, the file DOWNLOAD DAT is erased from the PCUs virtual disk and then
inserted from the CMS file DOWNLOAD WORK A.  Both of these functions use
 PCVMBOND.  A successful EOJ message is then displayed to the user.  If the load
was unsuccessful an error message is displayed to the user.
12   /* BEGIN THE CLEANUP PROCEDURES... */                            The IDCAMS
work file (PCLOAD AMSERV A) is erased.  Next, the user is asked if he would care
to view the results of the IDCAMS process.  If he responds with a 'Y', the
listing (PCLOAD LISTING A) will be XEDITed.  The name for this listing comes
from the name of the parameter file that is used, and is specified on AMSERV
command card.  Next, the user is asked if he cares to keep these IDCAMS results.
Any response, other than a 'Y' will cause the listing (PCLOAD LISTING A) to be
erased.
PCLOAD EXEC
/* *************************************************************** */
/* PCLOAD - DOWNLOAD A VSE/VSAM FILE TO A CMS CIRTUAL DISK         */
/* *************************************************************** */
     TRACE ?0
     ADDRESS COMMAND
/*  PROMPT FOR THE DOS/VSE VSAM FILE NAME                          */
   DO FOREVER
     VMFCLEAR
     SAY 'ENTER THE VSAM FILENAME TO DOWNLOAD FROM DOS/VSE:'
     PULL VSAMDSN
     IF VSAMDSN<>"" THEN LEAVE
   END
/*  PROMPT FOR THE DOS/VSE VSAM VOLUME NAME                        */
  DO FOREVER
     VMFCLEAR
     SAY 'ENTER PACK NAME WHERE' VSAMDSN 'RESIDES:'
     PULL SYSWK
     IF SYSWK<>"" THEN LEAVE
   END
/*  CLEAR THE SCREEN AND VERIFY THE VOLUME NAME ENTERED.  IF THE   */
/*  VOLUME NAME ENTERED IS INVALID, DISPLAY ERROR MESSAGE AND EXIT */
/*  WITH A RETURN CODE OF 3.                                       */
     VMFCLEAR
      DO FOREVER
         IF SYSWK="SYSWK3" THEN LEAVE
         IF SYSWK="SYSWK4" THEN LEAVE
         IF SYSWK="SYSWK5" THEN LEAVE
         IF SYSWK="SYSWK6" THEN LEAVE
         SAY " "
         SAY " "
         SAY "CANNOT PROCESS VOLUME" SYSWK
         SAY "    ...TERMINATING PCLOAD"
         EXIT 3
      END
/*  DISPLAY THE XEDIT MESSAGE                                      */
     SAY 'PLEASE XEDIT YOUR IDCAMS PARAMETERS NOW, ENTERING:'
     SAY '  '
     SAY '    1) RECFM PARM (F OR V).'
     SAY '    2) BLKSIZ PARM.'
     SAY '    3) ADD COUNT PARM IF SELECTIVE'
     SAY "        (*NOTE: ADD A '-' ON PRIOR LINE IF COUNT ADDED)."
/*  ACCESS THE VSAM USER CATALOG                                   */
     SET CMSTYPE HT
      DLBL '*' CLEAR
      RELEASE O
      DETACH 224
     "EXECIO * CP (STRING" LINK WIIVSE 332 224 RR
      ACCESS 224 O
      DLBL IJSYSCT O DSN USER.CATALOG.ONE '(' VSAM
/*  ACCESS THE VSAM VOLUME NEEDED                                  */
      IF SYSWK="SYSWK3" THEN DO
         RELEASE N
         DETACH 223
        "EXECIO * CP (STRING" LINK WIIVSE 223 223 RR
         ACCESS 223 N
          DLBL FILEIN N DSN VSAMDSN '(' VSAM
      END
      IF SYSWK="SYSWK4" THEN DO
         DLBL FILEIN O DSN VSAMDSN '(' VSAM
      END
      IF SYSWK="SYSWK5" THEN DO
         RELEASE P
         DETACH 225
        "EXECIO * CP (STRING" LINK WIIVSE 333 225 RR
         ACCESS 225 P
         DLBL FILEIN P DSN VSAMDSN '(' VSAM
      END
      IF SYSWK="SYSWK6" THEN DO
         RELEASE Q
         DETACH 226
        "EXECIO * CP (STRING" LINK WIIVSE 241 226 RR
         ACCESS 226 Q
         DLBL FILEIN Q DSN VSAMDSN '(' VSAM
      END
/*  SPECIFY THE CMS OUTPUT (WORK) FILE                             */
     DLBL FILEOT A CMS DOWNLOAD WORK
/*  ERASE THE CMS & AMSERV WORK FILES, COPY A NEW AMSER FILE AND   */
/*  XEDIT THE AMSERV FILE.                                         */
     "ERASE DOWNLOAD WORK A"
     "ERASE PCLOAD AMSERV A"
     "COPYFILE PCLOAD START * = AMSERV A"
     "XEDIT PCLOAD AMSERV A"
/*  CLEAR THE SCREEN AND DISPLAY THE 'PROCESSING' MESSAGE          */
     VMFCLEAR
     SAY 'DOWNLOADING VSAM FILE' VSAMDSN
     SAY '   TO CMS FILE DOWNLOAD WORK A'
     SAY '   ...AND... TO PC FILE DOWNLOAD.DAT:A'
/*  EXECUTE THE VM VSAM IDCAMS PROCESS                             */
     SET CMSTYPE RT
     AMSERV PCLOAD
/* IF THE IDCAMS PROCESS WAS SUCCESSFUL (RC=0), THEN DOWNLOAD THE  */
/*  CMS FILE TO THE PC WORK DISK, AND DISPLAY A GOOD MESSAGE...    */
/*  ...OTHERWISE DISPLAY AN ERROR MESSAGE.                         */
IF RC  = 0 THEN
        DO
           PCDISK ERASE  DOWNLOAD DAT   FROM WORK
           PCDISK INSERT DOWNLOAD WORK A INTO WORK
           SAY ' '
           SAY '*** VSAM DATASET LOADED TO YOUR "A" DISK ***'
           SAY '***   ...AND TO YOUR "WORK" VIRTUAL DISK ***'
        END
     ELSE
        DO
           SAY ' '
           SAY'**** ERROR HAS OCCURRED WHILE LOADING FILE ****'
        END
/*  BEGIN THE CLEANUP PROCEDURES:                                  */
/*    1) ERASE THE AMSERV WORK FILE.                               */
/*    2) PROMPT THE XEDITING THE IDCAMS RESULTS & HANDLE           */
/*    2) PROMPT FOR ERASEING THE IDCAMS RESULTS & HANDLE           */
     "ERASE PCLOAD AMSERV A"
     SAY "   "
     SAY "   "
     SAY "WOULD YOU LIKE TO SEE THE IDCAMS RESULT? (Y/N):"
     PULL ANSWER
     IF ANSWER = "Y" THEN "XEDIT PCLOAD LISTING A"
     SAY "   "
     SAY "   "
     SAY "DO YOU WANT TO KEEP THE IDCAMS RESULT? (Y/N):"
     PULL ANSWER
     IF ANSWER = "Y" THEN EXIT
      "ERASE PCLOAD LISTING A"
     REPRO INFILE  (FILEIN) -
          OUTFILE  (FILEOT ENV(RECFM() BLKSZ() PDEV(3370) ) )

Chris Maneely
Systems Programmer
Willamette Industries Inc (USA)    ) Willamette Industries Inc 1988



























































