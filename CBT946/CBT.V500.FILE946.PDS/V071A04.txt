Edit a file by giving only the filename

By using MXEDIT EXEC, you can edit a file by giving only
the filename. Of course you can give all the three parameters -
filename, filetype, and filemode. When you specify only
filename and if there is more than one entry against that
filename in all your mini-disks, then, after putting you in
XEDIT, an error message is flashed informing you that the
EXEC found more than one entry. If it is a new file and you
did not mention filetype, the filetype defaults to EXEC. The
default filemode is A.

If the file is a new one and:

o      If the filetype is EXEC or XEDIT, it adds lines of
      comments for documentation and makes it a REXX
      program.

o      If the filetype is RUNJCL, it adds POWER cards, JCL,
      and an EXEC card with the filename as the phase name.

o      If the filetype is VSAM, it generates an IDCAMS deck
      with JCL.

o      If the filetype is LIBR, it generates a Librarian deck with JCL.

Set a one character synonym in your synonym tables for this
EXEC to use it more conveniently. (You can set a synonym of
x, but beware of the consequences!)

/*   Rexx Program For Xediting           */
arg fname ftype fmode
if fname='' then do
   say 'At least filename should be specified for this XEDIT'
   exit 99
   end
qp=queued()
if qp>0 then qcmnd='LIFO'
 else qcmnd='FIFO'
if ftype='' then ftypet='*'
  else ftypet=ftype
if fmode='' then fmodet='*'
  else fmodet=fmode
set cmstype ht
listfile fname ftypet fmodet '(stack 'qcmnd
set cmstype rt
sw='n'
if queued()-qp=0 then do
   file_status='new'                        /* For New files          */
   if ftype='' then ftype='EXEC'            /* Default filetype       */
   if fmode='' then do
    if ftype='EXEC' | ftype='XEDIT' then fmode='C' /*My Disk For EXECs*/
     else fmode='A'
    end
   select
    when index('*EXEC*XEDIT*','*'ftype'*')>0 then call execdata
    when index('*RUNJCL*VSAM*LIBR*','*'ftype'*')>0 then call otherdata
    otherwise nop
    end
   end
 else do
   file_status='old'
   if queued()-qp>1 then sw='y'
   pull fname ftype fmode .                 /* Take the 1st entry     */
   select
    when qp=0 then desbuf
    when queued()=qp then nop
    otherwise do until queued()=qp
     pull junk
     end
   end /* select */
   end
msg='More than 1 entry for your selection - You have been warned'
if sw='y' then queue 'emsg' msg
address command 'XEDIT'  fname ftype fmode
exit
execdata:
queue 'I /* -------------------------------------- */'
queue 'I /*   Rexx Program For ??????????????????  */'
queue 'I /* -------------------------------------- */'
queue 'I /*  Written By:- xxxxxx xxxxxx xxxxxxxx   */'
queue 'I /*  Written On:-' date(e)'                 */'
queue 'I /*  Tested On:- ' date(e)'                 */'
queue 'I /* -------------------------------------- */'
queue 'I Trace'
queue ' '
queue 'Top'
queue 'Cursor file 2 23'
return
otherdata:
'ID (STACK LIFO'    /* Who am I? */
pull userid at nodeid .
queue 'I * $$ JOB JNM='fname',CLASS=A,DISP=D,USER='userid
queue 'I * $$ LST DISP=D,CLASS=V,DEST=('nodeid','userid')'
queue 'I // JOB 'fname  '       ' userid() date(e) time()
select
 when ftype='VSAM' then do
   queue "I // DLBL IJSYSUC,'XXXXX.YYYYY.YYYYY.YYYYY',,VSAM"
   queue 'I // EXEC IDCAMS,SIZE=AUTO'
   queue 'I  '
   line=4
   col=18
   end
 when ftype='LIBR' then do
   queue 'I // EXEC LIBR'
   queue 'I ACCESS SUBLIB=????.????????'
   queue 'I  '
   line=5
   col=15
   end
 when ftype='RUNJCL' then do
   queue 'I // LIBDEF PHASE,SEARCH=????.????????'
   queue 'I // EXEC' fname',SIZE=AUTO'
   line=4
   col=24
   end
 otherwise nop
end
queue 'I /*'
queue 'I /&'
queue 'I * $$ EOJ'
queue ' '
queue 'Top'
queue 'Cursor file' line col
return

Moyeen Ahmed Khan
Systems Analyst
Qatar General Petroleum Corporation (Qatar)            c QGPC 1992

