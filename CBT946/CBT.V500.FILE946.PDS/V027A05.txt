 Virtual SYSPCH/SYSIPT files revisited
We recently converted from VSE Version 1 VM mode supervisor to a Version 2
supervisor in VAE mode.  After the conversion, we discovered that the techniques
presented in my article Virtual SYSPCH/SYSIPT files  issue 25 do not work on a
VAE supervisor.  The customised program when run as shown can cause a VSE
hardwait.  The CPCMD method will not cause a hardwait, but it will not work
properly either.  To make the JCL work, CPCMD and SPCLOSE should be executed
with the REAL parameter (// EXEC CPCMD,REAL,SIZE=lOK or // EXEC
SPCLOSE,REAL,SIZE =lOK).  Also, steps containing the // EXEC CPCMD or // EXEC
SPCLOSE should be separated into distinct DOS jobs (be sure SYSPCH ASSGNs are
correct).
ÝEditor's note: unfortunately part of the code for the customised program was
published in the wrong order in issue 25.  A correct version of the  code is
given below.¨
CUSTOMISED PROGRAM
S090     TITLE 'ISSUE CP CMDS FOR SYSIPT/SYSPCH'
         PRINT ON,NOGEN
SPCLOSE   START 0
* REGISTERS 0-15 HERE
         USING BASE,R2
         BALR  R2,0
BASE     B     BEGIN
         DC    CL8'SPCLOSE  '
         DC    CL14'01.01.03/12/87' VERSION.MOD LEVEL & DATE ASSEMBLED
BEGIN    EQU   *
         CLR   R1,R15           PARM ON EXEC CARD?
         BE    PUNCHEOF         NO, PUNCH AN EOF CARD
         L     R1,0(,R1)        R1> ADDRESS OF PARM PARAMETER
         USING PDSECT,R1        YES
         CLC   INPARM,=C'NOEOF'  PARM='NOEOF'?
         BE    NOPUNCH          YES, BYPASS PUNCHING OF EOF CARD
PUNCHEOF OPEN  PCHFILE         *******************
         PUT   PCHFILE         * PUNCH EOF CARD  *
         CLOSE PCHFILE         *******************
NOPUNCH  COMRG                  GET COMM REGION ADDRESS
         LR    R3,R1            R3 POINTS TO COMREG
         USING CDSECT,R3
         CLI   CPIK+1,X'10'     BG PIC ?
         BNE   TBLPNT           NO, GO CALC PNTR FOR CUU & CLASS TABLE
         LA    R9,11            POINT TO LAST ENTRY IN TABLE
         B     GETCUU           GO TO SET UP CUU & CLASS
TBLPNT   SLR   R4,R4
         IC    R4,CPIK+1        ONE-BYTE PIK IN R4
         SRL   R4,4
         SLR   R8,R8            CLEAR R8
         L     R8,X'80'         GET ADDRESS OF SYSCOM
         LH    R9,44(0,R8)      LOAD # OF PARTITONS
         SLR   R9,R4            (# OF PARTS)=(PIK/10) = POINTER TO TBL
GETCUU   LA    R3,PIKCUU(R9)    INDEX TO CUU TBL - CHANGES PIK 10 TO 0;
         MVC   RDRCUU+1(1),0(R3)   AND PIK 30 TO 5 FOR F5;
         MVC   PCHCUU+1(1),0(R3)   AND PIK 60 TO 2 FOR F2 ETC
         MVC   PCHCUU2,PCHCUU
         LA    R3,PIKCLASS(R9)  INDEX TO SPOOL CLASS FOR THIS PIK
         MVC   CLASS,0(R3)      MOVE SPOOL CLASS TO PURGE CMD
* SET PROTECT KEY TO 0 *********
SETKEY   SLR   R0,R0           *
         L     1,=X'FF000000'  *
         SVC   13              *
         SR    R0,R0            SET SYSTEM MASKS TO DISABLE INTERRUPTS
         SVC   22               SEIZE SYSTEM TO GET EXCLUSIVE CONTROL
         MVC   NEWPSW,X'68'     SAVE PROG NEW PSW
         LA    R7,DIAG81        SAVE 'PROG OLD PSW' IN REG 7
         STCM  R7,B'0111',X'6D' PUT ADDR IN NEXT INSTR ADDR OF PROG NEW
         DC    H'0'             FORCE PROG CHECK TO GO TO SUPERV STATE
DIAG81   MVC   OLDPSW,X'28'     SAVE PROG OLD PSW
         MVC   X'68'(8),NEWPSW   RESTORE PROGRAM NEW PSW
          LA    R5,CLOSERDR
         LA    R6,L'CLOSERDR
         DC    X'83',X'56',XL2'0008' DO DIAGNOSE
         LA    R5,PURGERDR
         LA    R6,L'PURGERDR
         DC    X'83',X'56',XL2'0008' DO DIAGNOSE
         LA    R5,SPPCH
         LA    R6,L'SPPCH
         DC    X'83',X'56',XL2'0008' DO DIAGNOSE
         LA    R5,CLOSEPCH
         LA    R6,L'CLOSEPCH
         DC    X'83',X'56',XL2'0008' DO DIAGNOSE
         LA    R7,DIAG82        INITIALIZE NEXT INSTR ADDR
         STCM  R7,B'0111',OLDPSW+5 AREA OF THE PROGRAM OLD PSW
         LPSW  OLDPSW           LOAD THE PROG OLD PSW AS CURRENT PSW
DIAG82   LA    R0,X'FF'         SET SYSTEM MASKS TO ENABLE INTERRUPTS
         SVC   22               RELEASE SEIZE
* RESET PROTECT KEY  ***********
         SLR   R15,R15         *
         L     1,=X'FF0000FF'  *
         SVC   12              *
********************************
         EOJ                     DONE
OLDPSW   DC    D'0'
NEWPSW   DC    D'0'
* KEEP THESE CONSTANTS IN ORDER
* SYSPCH ADDRESS IS 6XD SEE PCHCUU2 X IS FROM PIKCUU (CU TABLE)
* SYSRDR ADDRESS IS 6XC SEE RDRCUU  X IS FROM PIKCUU (CU TABLE)
* PICKCUU & PIKCLASS ARE SELECTED BASED ON PARTITION
PURGERDR DS    0CL13
         DC    CL12'PUR RDR CL '
CLASS    DC    C'1'
CLOSEPCH DS    0CL9
         DC    CL6'CLOSE '
PCHCUU   DC    CL3'6AD'   << PCH ADDR ON CHAN=6 UNIT=D  CU FROM TABLE
SPPCH    DS    0CL8
         DC    CL3'SP '
PCHCUU2  DC    CL3'6AD'   << PCH ADDR ON CHAN=6 UNIT=D  CU FROM TABLE
         DC    CL2' *'
CLOSERDR DS    0CL9
         DC    CL6'CLOSE '
RDRCUU   DC    CL3'6AC'   << RDR ADDR ON CHAN=6 UNIT=C  CU FROM TABLE
* PARTITION         123456789AB0  << PARTITIONS F1 - FB, BG
PIKCUU   DC    CL12'123456789AB0' << CU TABLE
PIKCLASS DC    CL12'DEFGHIJKLMNO' << SPOOL CLASS TABLE
* EXAMPLES:
*          BG SYSPCH = 60D, SYSIPT = 60C, SPOOL CLASS=O
*          F2 SYSPCH = 62D, SYSIPT = 62C, SPOOL CLASS=E
*          F3 SYSPCH = 63D, SYSIPT = 63C, SPOOL CLASS=F
*                               ETC.
PCHFILE DTFCD DEVADDR=SYSPCH,IOAREA1=EOFCARD,TYPEFLE=OUTPUT,DEVICE=3525
         CDMOD DEVICE=3525,TYPEFLE=OUTPUT
EOFCARD  DC    CL80'/*'
CDSECT   DSECT         PARTITION COMMUNICATIONS REGION DSECT
         DS    CL8     DATE AS MM/DD/YY (FROM DATE CARD ELSE SYSTEM)
         DS    CL4     RESERVED
         DS    CL11    USER AREA (RESET TO ZERO AFTER EACH JOB CARD)
         DS    CL1     UPSI SWITCH
         DS    CL8     JOB NAME FROM // JOB CARD
         DS    CL14    N/A
CPIK     DS    H       PARTITION IDENTIFICATION KEY
         DS    CL42    N/A
         DS    H       ADDR OF 1ST PART OF PROGRAM INFORMATION BLOCK
PDSECT   DSECT
PARMLEN  DS    CL2
INPARM   DS    CL5
 SPCLOSE  CSECT
         LTORG
         END
Robert L Baxter
Senior Systems Programmer
Rocky Mountain News (USA)     ) Xephon 1988




























































