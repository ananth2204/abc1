Change between 370- and XA-mode

When we installed VM/XA on a 3090-20J we had a lot of
problems with the IBM products.  This was because some of
them have to run above the 16MB line because of memory
constraints, and the others work only in 370-mode, eg CSP/AD
and CSP/AE.

We developed a procedure to change a machine's mode and re-
IPL CMS without the user realizing.

All virtual machines are defined in the VM directory in 370-
mode, because we have more applications in 370-mode than in
XA-mode.  If the product demands that the machine be in XA-
mode, it will create a work-file on the 191 disk, with filename
and filetype in upper and lower case letters, containing the
original mode of the virtual machine, the storage size and
which application and option the user is using.

After that, the following commands are executed for the
application: CP SET MACHINE XA and IPL CMSSMXA,
which is the new CMS.  Messages indicating the CMS level
are suppressed.

During the IPL, SYSPROF EXEC is executed, and at the end
of it the work-file is renamed to indicate that the IPL was
executed in the middle of the user's application, and it will
recall the user's application with the correct option.

At the end of the application the virtual machine is returned to
its original characteristics.  If necessary we will change the
mode and the storage size, and we will delete the work-file
from the user application.

In order to do this, we create two shared segments for CMS,
with messages suppressed, called CMSSM370 and
CMSSMXA.  The DMSINI and DMSNGP modules are
modified.

For the DMSINI modules; in the INIWMSET routine we
moved blanks to NUCDATE and NUCTIME so that messages
from CMS do not appear.

INIWMSET DS    0H
         LR    R11,R12
         A     R11,ARDCCW0
         USING RDCCW0,R11
         ST    R11,R11ADDR
         LA    R1,REPLY
         DC    X'8310000C'
         MVC   NUCDATE,=8C' '         ***** we changed this line, ******
         MVC   NUCTIME,=5C' '         ***** and this too.         ******

In the CMS modules DMSNGP, we moved blanks to
VERSION and INSTID, and created two saved segments for
CMS (called CMSSM370 and CMSSMXA) with messages
suppressed.

DMSNGP   CSECT
         DEFNUC SYSDISK=190,
               YDISK=19E,
               HELP=19D,
               LANGID=AMENG,
               DBCS=NO,
               LANGLEV=S,
               SAVESYS=YES,
               SYSNAME=CMSSM370, ***** we changed this line with another
                                 ***** CMS saved segment
               USEINST=YES,
               INSTSEG=CMSINST,
               REWRITE=YES,
               IPLADDR=390,      ***** we created another mdisk to write
                                 ***** the new CMS-nucleus
               CYLADDR=003,
               IPLCYL0=YES,
               VERSION=' ',      ***** we moved blanks in the version,
               INSTID=' '        ***** and instid
         END

After that, we built a CMS nucleus on another mini-disk (390),
and saved the saved segments (CMSSM370 and CMSSMXA);
one in 370-mode and the other in XA-mode.

In the user applications we put ICU EXEC and SYSPROF EXEC.


ICU EXEC

/* Execute access to GDDM/XA                                          */
Trace off
Parse upper arg request
If Request= ' ' then Request = 'REQUEST'
ADDRESS COMMAND State ':ICU: End A0'
if Rc ¬=0
     then Do
          stor=(x2d(storage())/1024)
          Cmdqset=diag(08,'Q SET')
          Mode=Substr(Cmdqset,(Pos('MACHINE',cmdqset))+8,3)
          'desbuf'
           queue Stor
           queue Mode
           queue Request
           queue
           ADDRESS COMMAND 'EXECIO * DISKW :ICU: Execute A0 1'
           If RC ¬=0
              Then do
                   Say 'You have problems in your 191 disk'
                   Exit
                   End
           ADDRESS COMMAND 'FINIS :ICU: Execute A0'
           If Mode='370'
               then  Do
                     Commode = "CP SET MACHINE XA"
                     Comipl = "CP IPL CMSSMXA PARM AUTOCR"
                     AA=DIAGRC(08,Commode X2C(15) Comipl)
                     End
             Else ADDRESS COMMAND 'RENAME :ICU: Execute A0 :ICU: End A0'
          End
If Request = 'REQUEST' Then Request = ' '
'Admchart' Request
ADDRESS COMMAND 'EXECIO 1 DISKR :ICU: End A 2'
Parse upper pull mode .
ADDRESS COMMAND 'ERASE :ICU: End A'
If Rc ¬= 0 Then Do
                Say 'You have problems in your 191 disk'
                Exit
                End
If mode ¬= 'XA' Then Do
                     AA=Diagrc(08,"CP SET MACHINE" Mode X2C(15),
                        "CP IPL CMSSM"||Mode "PARM AUTOCR")
                     Exit
                     End
Exit


SYSPROF EXEC

Trace off
/* In this EXEC, we created a routine to verify
                   that there is a specific work-file, for that
                   application.                                      */
Address 'COMMAND'
Parse Arg insflags 'FF'x cp_keyword psw_spg hexloc 'FF'x      ,
          sysid 'FF'x insparms
If batch = 'no'
  Then Do
    If gotfirst = 'yes'
      Then Push usr_cmd
    If noprof = 'no'
      Then Do
/*************************************************************
 Routine to change 370-mode to XA-mode
**************************************************************/
       Address Command 'LISTFILE :*: Execute A0 (STACK'
       Do while queued() > 0
             Parse upper pull nameapli .
            'ESTATE' nameapli 'Execute A0'
             IF rc = 0     /*Did we find work-file on disk a */
             Then Do
                  Address Command 'EXECIO 1 DISKR',
                  nameapli 'Execute A0 3 (finis'
                  Pull Request
                  Address Command 'RENAME' nameapli 'Execute A0',
                  nameapli 'End A0'
                  if rc¬=0 then Do
                                Say 'You have problems in your 191-disk'
                                Exit
                                End
                  apli=strip(nameapli,,':')
                 'EXEC' apli Request
                  Exit
                  End
       End
/*************************************************************
 End of routine
**************************************************************/
        ADDRESS COMMAND 'SET CMSTYPE RT'
        'ESTATE PROFILE EXEC ' mdisk_mode
        IF rc = 0     /*Did we find profile on disk accessed?*/
          Then 'EXEC PROFILE'/*Yes, invoke user PROFILE EXEC */
      End
   End
  Else Push 'CMSBATCH'   /*This is batch machine, start it up*/
Exit

Vera Lucia Borges
Software Analyst
Philips de Brasil Ltda (Brazil)

