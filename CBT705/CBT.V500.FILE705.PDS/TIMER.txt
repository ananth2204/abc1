PROC 0 DEBUG

 /*                                                                  */
 /*      PDSDOC *TSSO CLIST TO EXECUTE FUNCTIONS AT CERTAIN TIMES....*/
 /*                                                                  */
 /* 26NOV04 DHC PRUNED OUT TEST IMAGE SETUP WORK                     */
 /* 14OCT04 DHC SET VALUE FOR CMDRESP                                */
 /* 07FEB03 DHC ENTIRELY RESTRUCTURED BY DAY                         */
 /*                                                                  */

  IF &DEBUG = DEBUG THEN CONTROL  MSG LIST CONLIST SYMLIST FLUSH
                    ELSE CONTROL  NOMSG NOLIST FLUSH
 /*                                                                  */
  SET HOURS = &SUBSTR(1:2,&SYSSTIME)
  SET MINS  = &SUBSTR(4:5,&SYSSTIME)
  DAYOWEEK
  SET DAYNUM = &LASTCC
  SET CMDRESP = NOWHERE
  NGLOBAL HOURS,MINS,DAYNUM,CMDRESP
 /*                                                                  */
 /*  PERFORM THIS CODE AT EVERY INVOCATION                           */
 /*                                                                  */
    %STORAGE
 /*                                                                  */
  SELECT (&DAYNUM)
    WHEN (3) DO
    SYSCALL WEDS
    END
    WHEN (5) DO
    SYSCALL SPACEMAP
    END
    WHEN (6) DO
    SYSCALL SATS
    END
  END
 /*                                                                  */
     SELECT (&HOURS)
      WHEN (6) DO
        SELECT (&MINS)
         WHEN (30:44) DO
                      %SYSDA
                      END
        END
      END
      WHEN (10) DO
        SELECT (&MINS)
         WHEN (15:29) DO
                      /* NOT SATURDAYS (TOO BUSY)                    */
                      IF &DAYNUM EQ 6 THEN EXIT CODE(0)
                      OSCMD F HSM,FREEVOL ML1BACKUPVERSIONS
                      END
        END
      END
      WHEN (18) DO
        SELECT (&MINS)
         WHEN (15:29) DO
                       %$CO
                      END
        END
      END
      WHEN (23) DO
        SELECT (&MINS)
         WHEN (30:44) DO
                      OSCMD S TSSORDR,MBR=FLEXJ01
                      END
        END
      END

  END

     SELECT (&MINS)
      WHEN (00:14) DO
                   /* WRITE FLEX CACHE STATISTICS TO A FILE       */
                   %CACHE
                   END
     END
                      /* END OF PROCESSING   */
  EXIT CODE(0)
                      /* WEDNESDAYS                                  */
WEDS: PROC 0
     SELECT (&HOURS)
      WHEN (18) DO
        SELECT (&MINS)
         WHEN (00:14) DO
                         OSCMD F LLA,REFRESH
                      END
        END
      END
     END
  RETURN CODE(0)
  END
                      /* SATURDAYS ONLY                              */
SATS: PROC 0
     SELECT (&HOURS)
      WHEN (6) DO
        SELECT (&MINS)
         WHEN (00:14) DO
                      OSCMD F HSM,BACKVOL CDS
                      END
         WHEN (15:29) DO
                       OSCMD F HSM,EXPIREBV EXECUTE +
                       NONSMSVERSIONS(DELETEIFBACKEDUP +
                       CATALOGEDDATA(365)  +
                       UNCATALOGEDDATA(100))
                      END
        END
      END
      /* THIS CODE DUMMIED OUT BY FALSE HOUR (16)            */
     END
  RETURN CODE(0)
  END
 /*                                                                  */
SPACEMAP: PROC 0
        SELECT (&MINS)
         WHEN (15:29) DO
                      OSCMD S TSSORDR,MBR=DISKMAPS
                      END
        END
  RETURN CODE(0)
  END
 /*                                                                  */
 /*                                                                  */
 /*                                                                  */
BUGS:  +
       EXIT CODE(8)
