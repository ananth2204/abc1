.*================================================================*
.*                                                                *
.*   Module name -                                                *
.*                 @RXEXIT                                        *
.*                 Creates exit procedure                         *
.*                                                                *
.*   Description -                                                *
.*                 Creates exit epilog and housekeeping           *
.*                 procedure                                      *
.*                                                                *
.*   Type -                                                       *
.*                 Assembly macro instruction                     *
.*                                                                *
.*   Syntax -                                                     *
.*                  @RXEXIT                                       *
.*                    ,rc=<YES|NO>             default NO         *
.*                    ,basereg=<KEEP|DROP>     default KEEP       *
.*                    ,ltorg=<YES|NO>          default YES        *
.*                                                                *
.*   Author -                                                     *
.*                 Deru Sudibyo                                   *
.*                 Freelance consultant                           *
.*                 Nusantara IT ProClub                           *
.*                 Indonesia                                      *
.*                                                                *
.*                                                                *
.*   Modification log -                                           *
.*                                                                *
.*  +----------+-------+---------+----------------------------+   *
.*  |Date      | VRM   | Eng/Supp| Description                |   *
.*  +----------+-------+---------+----------------------------+   *
.*  |2004/03/05| 1.1.0 | Deru S. | First developed            |   *
.*  |          |       |         |                            |   *
.*  +----------+-------+---------+----------------------------+   *
.*                                                                *
.*                                                                *
.*================================================================*
         macro
&label   @RXEXIT &RC=NO,&BASEREG=KEEP,&LTORG=YES,&TEMPREG=R2
         gblc  &wklen,&wkreg,&breg
         lclc  &llbl
&label   DS    0h
&llbl    setc  'DER'.'&SYSNDX'
         AIF   ('&RC' eq 'NO').relstor
         lr    &tempreg,r15
.relstor anop
         l     r0,savesize
         ltr   r0,r0
         bp    &llbl.a
         la    r0,&wklen
&llbl.a  equ   *
         lr    &wkreg,$SAVEREG
         STORAGE RELEASE,ADDR=(&wkreg),LENGTH=(0)
         AIF   ('&RC' ne 'NO').setrc
         SLR   R15,R15                      RC=0
         ago   .return
.setrc   anop
         lr    R15,&tempreg
.return  anop
         PR                                 Return
         aif  (('&BASEREG' ne 'DROP') and ('&BASEREG' ne 'RESET')).q1
         DROP  &breg
.q1      aif  ('&LTORG' ne 'YES').q2
         LTORG ,
.q2      anop
         mend
