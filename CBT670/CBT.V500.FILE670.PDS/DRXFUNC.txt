.*================================================================*
.*                                                                *
.*   Module name -                                                *
.*                 DRXFUNC                                        *
.*                 Creates rexx package directory entry           *
.*                                                                *
.*   Description -                                                *
.*                 Called by @RXFDIR macro to create function     *
.*                 directory entry on IRXFLOC or IRXFUSER         *
.*                 Note:  This macro is inner macro of @RXFDIR    *
.*                                                                *
.*                                                                *
.*   Type -                                                       *
.*                 Assembly macro instruction                     *
.*                                                                *
.*   Syntax -                                                     *
.*                  DRXFUNC                                       *
.*                    ,name=<function name>                       *
.*                    ,ep=<EP name>                               *
.*                    ,dd=<DD name from which EP is loaded>       *
.*                    ,addrtype=<A|V>                             *
.*                                                                *
.*   Author -                                                     *
.*                 Deru Sudibyo                                   *
.*                 Freelance consultant                           *
.*                 Nusantara IT ProClub                           *
.*                 Indonesia                                      *
.*                                                                *
.*                                                                *
.*   Modification log -                                           *
.*                                                                *
.*  +----------+-------+---------+----------------------------+   *
.*  |Date      | VRM   | Eng/Supp| Description                |   *
.*  +----------+-------+---------+----------------------------+   *
.*  |2004/03/05| 1.1.0 | Deru S. | First developed            |   *
.*  |          |       |         |                            |   *
.*  +----------+-------+---------+----------------------------+   *
.*                                                                *
.*                                                                *
.*================================================================*
         macro
&label   DRXFUNC &name=,&addrtype=A,&ep=,&dd=
         lclc  &atype,&epname,&ddname,&addr
         lclb  &epok
         aif   (t'&name eq 'O').errname
         aif  (t'&ep ne 'O').go1
&epname  setc ' '
         ago  .go2
.go1     anop
&epname  setc '&ep'
&epok    setb 1
.go2     aif  (t'&dd ne 'O').go3
&ddname  setc ' '
         ago  .go4
.go3     anop
&ddname  setc '&dd'
.go4     anop
         aif   (('&addrtype' eq 'A') or ('&addrtype' eq 'a')).atype
         aif   (('&addrtype' eq 'V') or ('&addrtype' eq 'v')).vtype
         ago   .errtype
.atype   anop
&atype   setc 'A'
&addr    setc '0'
         ago  .build
.vtype   aif  (not &epok).errep
&atype   setc 'V'
&addr    setc '&epname'
.build   anop
         DC   CL8'&name'
         DC   &atype.L4(&addr)
         DC   FL4'0'
         DC   CL8'&epname'
         DC   CL8'&ddname'
         mexit
.errname MNOTE 8,'DRX0001 Illegal name of function'
         mexit
.errep   MNOTE 8,'DRX0009 Address type V must use entrypoint name.'
         mexit
.errtype MNOTE 8,'DRX0002 Wrong address type, neither A nor V'
         mend
