.*================================================================*
.*                                                                *
.*   Module name -                                                *
.*                 @RXFDIR                                        *
.*                 Creates rexx package directory entry           *
.*                                                                *
.*   Description -                                                *
.*                 Creates rexx function package directory        *
.*                 entry on IRXFLOC or IRXFUSER component         *
.*                                                                *
.*   Type -                                                       *
.*                 Assembly macro instruction                     *
.*                                                                *
.*   Syntax -                                                     *
.*                 (1)  Header                                    *
.*                                                                *
.*                   @RXFDIR                                      *
.*                    ,start                                      *
.*                    ,rows=nn                 number of entry    *
.*                                                                *
.*                                                                *
.*                 (2)  Entry                                     *
.*                                                                *
.*                   @RXFDIR                                      *
.*                    ,function                                   *
.*                    ,name=<function name>                       *
.*                    ,ep=<EP name>                               *
.*                    ,dd=<DD name from which EP is loaded>       *
.*                    ,addrtype=<A|V>                             *
.*                                                                *
.*                 (3)  Trailer                                   *
.*                                                                *
.*                   @RXFDIR                                      *
.*                    ,<finish|end>  whether END mark is written  *
.*                                                                *
.*   Author -                                                     *
.*                 Deru Sudibyo                                   *
.*                 Freelance consultant                           *
.*                 Nusantara IT ProClub                           *
.*                 Indonesia                                      *
.*                                                                *
.*                                                                *
.*   Modification log -                                           *
.*                                                                *
.*  +----------+-------+---------+----------------------------+   *
.*  |Date      | VRM   | Eng/Supp| Description                |   *
.*  +----------+-------+---------+----------------------------+   *
.*  |2004/03/05| 1.1.0 | Deru S. | First developed            |   *
.*  |          |       |         |                            |   *
.*  +----------+-------+---------+----------------------------+   *
.*                                                                *
.*                                                                *
.*================================================================*
         macro
&label   @rxfdir &cmd,&name=,&addrtype=A,&ep=,&dd=,&rows=
         gbla &count
         gblb &started
         aif (t'&cmd eq 'O').nocmd
         aif (('&cmd' eq 'start') or ('&cmd' eq 'begin')).start
         aif ('&cmd' eq 'end').end
         aif ('&cmd' eq 'finish').finish
         aif (('&cmd' eq 'function') or ('&cmd' eq 'func')).func
         MNOTE 8,'DRX0003 Illegal command ''&cmd'''
         mexit
.func    anop
         DRXFUNC name=&name,addrtype=&addrtype,                        +
               ep=&ep,dd=&dd
         mexit
.nocmd   aif  (&started).end
.start   anop
         aif   (t'row ne 'O').start1
         MNOTE 8,'DRX0004 Invalid number directory rows'
         mexit
.start1  anop
&started setb  1
         DC    CL8'IRXFPACK'       String identifying directory
         DC    FL4'24'             Length of header
         DC    FL4'&rows'          Number of rows in directory
         DC    FL4'0'              Word of zeros
         DC    FL4'32'             Length of directory entry
.finish  mexit
.end     anop
         end
         mend
