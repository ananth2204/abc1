      *================================================================*KDS08257
      *           1         2         3         4         5         6123KDS08257
      ***123456789012345678901234567890123456789012345678901234567890123KDS08257
      *================================================================*KDS08257
      * KKKKKKKKKKKKK   DDDDDDDD    SSSSSSSS                           *KDS08257
      * KKKKKKKKKKKKK  DDDDDDDDD   SSSSSSSSS                           *KDS08257
      *      KKKK      DDDD        SSSS                                *KDS08257
      *      KKKK      DDDD        SSSS                                *KDS08257
      *      KKKK      DDDD        SSSS          1111          0000000 *KDS08257
      *      KKKK      DDDDDDDD    SSSSSSSS      1111        0000000000*KDS08257
      *      KKKK        DDDDDDD    SSSSSSSS     1111        0000  0000*KDS08257
      *      KKKK           DDDD        SSSS    1111         0000  0000*KDS08257
      *      KKKK           DDDD        SSSS    1111        0000  0000 *KDS08257
      *      KKKK           DDDD        SSSS    1111        0000  0000 *KDS08257
      * KKK  KKKK           DDDD        SSSS   1111   0000  0000  0000 *KDS08257
      * KKKKKKKK      DDDDDDDDDD   SSSSSSSSS   1111  0000  0000000000  *KDS08257
      * KKKKKKKK      DDDDDDDDD    SSSSSSSS    1111 0000    000000000  *KDS08257
      *================================================================*KDS08257
      * Change Log                                                     *KDS08257
      *================================================================*KDS08257
      * KDS08257    Kang Dengsong       INITIAL VERSION                *KDS08257
      *================================================================*KDS08257
       IDENTIFICATION DIVISION.                                         KDS08257
       PROGRAM-ID. JCSBBEGN.                                            KDS08257
       AUTHOR. KANG DENGSONG                                            KDS08257
       DATE-WRITTEN.  SEP 15,2008                                       KDS08257
      ******************************************************************KDS08257
      * CREATED   BY :KANG DENGSONG                                    *KDS08257
      * CREATED DATE :SEP 13, 2008                                     *KDS08257
      * REMARK       :UPDATE MAST FILE for begin                       *KDS08257
      *              :JOB SCHEDULER SYSTEM                             *KDS08257
      *================================================================*KDS08257
      *   This program is distributed without warranty of any kind,    *KDS08257
      * including  (without limitation) warranties of merchantability  *KDS08257
      * and of fitness for a particular purpose. It is up to the user  *KDS08257
      * to make his own determination of the usefulness of this program*KDS08257
      * in his own system.                                             *KDS08257
      *================================================================*KDS08257
      *                                                                *KDS08257
      *--------------------*                                            KDS08257
       ENVIRONMENT DIVISION.                                            KDS08257
      *--------------------*                                            KDS08257
       CONFIGURATION SECTION.                                           KDS08257
       SOURCE-COMPUTER. IBM-ES9000.                                     KDS08257
       OBJECT-COMPUTER. IBM-ES9000.                                     KDS08257
       INPUT-OUTPUT SECTION.                                            KDS08257
       FILE-CONTROL.                                                    KDS08257
                                                                        KDS08257
           SELECT JCSFMAST  ASSIGN         TO JCSFAPSB                  KDS08257
                            FILE STATUS    IS WS-JCSFMAST-STS           KDS08257
                            ACCESS MODE    IS RANDOM                    KDS08257
                            ORGANIZATION   IS INDEXED                   KDS08257
                            RECORD KEY     IS JCBMAST-KEY.              KDS08257
           SELECT JCSFDTTM  ASSIGN         TO JCSFDTTM                  KDS08257
                            FILE STATUS    IS WS-JCSFDTTM-STS           KDS08257
                            ACCESS MODE    IS SEQUENTIAL                KDS08257
                            ORGANIZATION   IS SEQUENTIAL.               KDS08257
       DATA DIVISION.                                                   KDS08257
       FILE SECTION.                                                    KDS08257
       FD JCSFMAST.                                                     KDS08257
       01 JCBMAST-REC.                                                  KDS08257
           COPY JCSCMAST  REPLACING ==:JCBMAST:== BY ==JCBMAST==.       KDS08257
       FD JCSFDTTM.                                                     KDS08257
       01 JCBDTTM-REC.                                                  KDS08257
          03 JCBDTTM-DTE                    PIC X(08).                  KDS08257
          03 JCBDTTM-TME                    PIC X(08).                  KDS08257
          03 JCBDTTM-RMD                    PIC X(64).                  KDS08257
      ******************************************************************KDS08257
       WORKING-STORAGE SECTION.                                         KDS08257
       COPY JCSCWK02.                                                   KDS08257
       COPY JCSCWK00.                                                   KDS08257
       LINKAGE SECTION.                                                 KDS08257
       01 PARM-TO-MAIN.                                                 KDS08257
          03 PARM-LENGTH                     PIC S9(4) COMP.            KDS08257
          03 PARM-CONTENT.                                              KDS08257
             15 PARM-AP-SYSID                PIC X(08).                 KDS08257
             15 PARM-PROC                    PIC X(08).                 KDS08257
             15 PARM-RC                      PIC 9(02).                 KDS08257
             15 PRM-MOD                      PIC X(01).                 KDS08257
             15 PRM-SKIP                     PIC X(01).                 KDS08257
             15 PRM-FLG                      PIC X(01).                 KDS08257
             15 PARM-PLUS                    PIC X(75).                 KDS08257
      *                                                                 KDS08257
       PROCEDURE DIVISION USING PARM-TO-MAIN.                           KDS08257
       MAIN SECTION.                                                    KDS08257
           PERFORM F100-OPEN-FILES                                      KDS08257
           ACCEPT WS-CUR-TIME FROM TIME                                 KDS08257
           ACCEPT WS-YYYYMMDD FROM DATE YYYYMMDD                        KDS08257
      *    DISPLAY WS-YYYYMMDD WS-CUR-TIME ':DTETME'                    KDS08257
           COPY JCSCWK05.                                               KDS08257
           MOVE PARM-PROC            TO JCBMAST-JOB                     KDS08257
           MOVE PARM-AP-SYSID        TO JCBMAST-AP-SYSID                KDS08257
           READ JCSFMAST  KEY IS JCBMAST-KEY                            KDS08257
           IF WS-JCSFMAST-STS NOT = NORMAL                              KDS08257
              DISPLAY 'READ FILE JCSFMAST ERROR CODE=' WS-JCSFMAST-STS  KDS08257
                      ' KEY IS ' PARM-PROC                              KDS08257
              MOVE ERROR8            TO WS-RETURN-CD                    KDS08257
              PERFORM A300-STOP-RUN                                     KDS08257
           END-IF                                                       KDS08257
           IF JCBMAST-JOB-STATUS NOT = SPACE                            KDS08257
           THEN                                                         KDS08257
              DISPLAY JCBMAST-JOB                                       KDS08257
               ' INVALID JOB STATUS SHOULD BE SPACE ='                  KDS08257
              JCBMAST-JOB-STATUS '=' PARM-PROC ' SYSID=' PARM-AP-SYSID  KDS08257
              ' EXEC-ST-TIME=' JCBMAST-EXEC-ST-TIME-9                   KDS08257
              MOVE ERROR8            TO WS-RETURN-CD                    KDS08257
              PERFORM A300-STOP-RUN                                     KDS08257
           END-IF                                                       KDS08257
           MOVE WS-YYYYMMDD                  TO JCBDTTM-DTE             KDS08257
           MOVE WS-CUR-TIME                  TO JCBDTTM-TME             KDS08257
           MOVE SPACE                        TO JCBDTTM-RMD             KDS08257
           WRITE JCBDTTM-REC                                            KDS08257
           IF WS-JCSFDTTM-STS NOT = NORMAL                              KDS08257
              DISPLAY 'WRITE FILE JCSFDTTM ERROR CODE=' WS-JCSFDTTM-STS KDS08257
              MOVE ERROR8            TO WS-RETURN-CD                    KDS08257
              PERFORM A300-STOP-RUN                                     KDS08257
           END-IF                                                       KDS08257
           PERFORM A300-STOP-RUN                                        KDS08257
           EXIT.                                                        KDS08257
       F100-OPEN-FILES.                                                 KDS08257
           PERFORM T00-OPEN-I-JCSFMAST                                  KDS08257
           PERFORM T00-OPEN-O-JCSFDTTM                                  KDS08257
           EXIT.                                                        KDS08257
       F200-CLOSE-FILES.                                                KDS08257
           IF OPEN-JCSFMAST-OK                                          KDS08257
              CLOSE JCSFMAST                                            KDS08257
              SET OPEN-JCSFMAST-NO   TO TRUE                            KDS08257
           END-IF                                                       KDS08257
           IF OPEN-JCSFDTTM-OK                                          KDS08257
              CLOSE JCSFDTTM                                            KDS08257
              SET OPEN-JCSFDTTM-NO   TO TRUE                            KDS08257
           END-IF                                                       KDS08257
           EXIT.                                                        KDS08257
       A300-STOP-RUN.                                                   KDS08257
           PERFORM F200-CLOSE-FILES                                     KDS08257
           MOVE WS-RETURN-CD         TO RETURN-CODE                     KDS08257
           STOP RUN.                                                    KDS08257
           EXIT.                                                        KDS08257
       T00-OPEN-I-JCSFMAST.                                             KDS08257
           COPY JCSCWK12.                                               KDS08257
       T00-OPEN-O-JCSFDTTM.                                             KDS08257
           COPY JCSCWK16.                                               KDS08257
      *================================================================*KDS08257
      *           1         2         3         4         5         6123KDS08257
      ***123456789012345678901234567890123456789012345678901234567890123KDS08257
      *================================================================*KDS08257
