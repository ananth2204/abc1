      *================================================================*KDS08257
      *           1         2         3         4         5         6123KDS08257
      ***123456789012345678901234567890123456789012345678901234567890123KDS08257
      *================================================================*KDS08257
      * KKKKKKKKKKKKK   DDDDDDDD    SSSSSSSS                           *KDS08257
      * KKKKKKKKKKKKK  DDDDDDDDD   SSSSSSSSS                           *KDS08257
      *      KKKK      DDDD        SSSS                                *KDS08257
      *      KKKK      DDDD        SSSS                                *KDS08257
      *      KKKK      DDDD        SSSS          1111          0000000 *KDS08257
      *      KKKK      DDDDDDDD    SSSSSSSS      1111        0000000000*KDS08257
      *      KKKK        DDDDDDD    SSSSSSSS     1111        0000  0000*KDS08257
      *      KKKK           DDDD        SSSS    1111         0000  0000*KDS08257
      *      KKKK           DDDD        SSSS    1111        0000  0000 *KDS08257
      *      KKKK           DDDD        SSSS    1111        0000  0000 *KDS08257
      * KKK  KKKK           DDDD        SSSS   1111   0000  0000  0000 *KDS08257
      * KKKKKKKK      DDDDDDDDDD   SSSSSSSSS   1111  0000  0000000000  *KDS08257
      * KKKKKKKK      DDDDDDDDD    SSSSSSSS    1111 0000    000000000  *KDS08257
      *================================================================*KDS08257
      * Change Log                                                     *KDS08257
      *================================================================*KDS08257
      * KDS08257    Kang Dengsong       INITIAL VERSION                *KDS08257
      *================================================================*KDS08257
      **          1         2         3         4         5         6123KDS08257
      ***123456789012345678901234567890123456789012345678901234567890123KDS08257
       IDENTIFICATION DIVISION.                                         KDS08257
       PROGRAM-ID. JCSBAPNM.                                            KDS08257
       AUTHOR. KANG DENGSONG                                            KDS08257
       DATE-WRITTEN.  SEP 15,2008                                       KDS08257
      ******************************************************************KDS08257
      * Created   By :Kang Dengsong                                    *KDS08257
      * Created Date :SEP 13, 2008                                     *KDS08257
      * Remark       :Process when AP return with normal               *KDS08257
      *              :Job Scheduler System                             *KDS08257
      *================================================================*KDS08257
      *   This program is distributed without warranty of any kind,    *KDS08257
      * including  (without limitation) warranties of merchantability  *KDS08257
      * and of fitness for a particular purpose. It is up to the user  *KDS08257
      * to make his own determination of the usefulness of this program*KDS08257
      * in his own system.                                             *KDS08257
      *================================================================*KDS08257
      *                                                                *KDS08257
      *--------------------*                                            KDS08257
       ENVIRONMENT DIVISION.                                            KDS08257
      *--------------------*                                            KDS08257
       CONFIGURATION SECTION.                                           KDS08257
       SOURCE-COMPUTER. IBM-ES9000.                                     KDS08257
       OBJECT-COMPUTER. IBM-ES9000.                                     KDS08257
       INPUT-OUTPUT SECTION.                                            KDS08257
       FILE-CONTROL.                                                    KDS08257
           SELECT JOBSUB    ASSIGN         TO JOBSUB                    KDS08257
                            ORGANIZATION   IS SEQUENTIAL                KDS08257
                            ACCESS MODE    IS SEQUENTIAL                KDS08257
                            FILE STATUS    IS WS-JOBSUB-STS.            KDS08257
           SELECT DATECNTL  ASSIGN         TO DATECNTL                  KDS08257
                            ORGANIZATION   IS SEQUENTIAL                KDS08257
                            ACCESS MODE    IS SEQUENTIAL                KDS08257
                            FILE STATUS    IS WS-DATECNTL-STS.          KDS08257
           SELECT JCSFMAST  ASSIGN         TO JCSFAPSB                  KDS08257
                            FILE STATUS    IS WS-JCSFMAST-STS           KDS08257
                            ACCESS MODE    IS RANDOM                    KDS08257
                            ORGANIZATION   IS INDEXED                   KDS08257
                            RECORD KEY     IS JCBMAST-KEY.              KDS08257
           SELECT JCSFJOBH  ASSIGN         TO JCSFJOBH                  KDS08257
                            FILE STATUS    IS WS-JCSFJOBH-STS           KDS08257
                            ACCESS MODE    IS SEQUENTIAL                KDS08257
                            ORGANIZATION   IS INDEXED                   KDS08257
                            RECORD KEY     IS JCBHD-SEQ.                KDS08257
           SELECT JCSFDTTM  ASSIGN         TO JCSFDTTM                  KDS08257
                            FILE STATUS    IS WS-JCSFDTTM-STS           KDS08257
                            ACCESS MODE    IS SEQUENTIAL                KDS08257
                            ORGANIZATION   IS SEQUENTIAL.               KDS08257
       DATA DIVISION.                                                   KDS08257
       FILE SECTION.                                                    KDS08257
       FD JOBSUB.                                                       KDS08257
       01 JOBSUB-REC                     PIC   X(80).                   KDS08257
       FD DATECNTL.                                                     KDS08257
           COPY DTCCNTL.                                                KDS08257
       FD JCSFMAST.                                                     KDS08257
       01 JCBMAST-REC.                                                  KDS08257
           COPY JCSCMAST  REPLACING ==:JCBMAST:== BY ==JCBMAST==.       KDS08257
       FD JCSFJOBH.                                                     KDS08257
           COPY JCSCJOBH.                                               KDS08257
       FD JCSFDTTM.                                                     KDS08257
       01 JCBDTTM-REC.                                                  KDS08257
          03 JCBDTTM-DTE                    PIC X(08).                  KDS08257
          03 JCBDTTM-TME                    PIC X(08).                  KDS08257
          03 JCBDTTM-RMD                    PIC X(64).                  KDS08257
      ******************************************************************KDS08257
       WORKING-STORAGE SECTION.                                         KDS08257
       COPY JCSCWK02.                                                   KDS08257
       COPY JCSCWK00.                                                   KDS08257
       COPY JCSCWK03.                                                   KDS08257
       01 JCBMAS1-REC.                                                  KDS08257
           COPY JCSCMAST  REPLACING ==:JCBMAST:== BY ==JCBMAS1==.       KDS08257
       01 JCBMAS2-REC.                                                  KDS08257
           COPY JCSCMAST  REPLACING ==:JCBMAST:== BY ==JCBMAS2==.       KDS08257
       01 WS-PROC                     PIC  X(08).                       KDS08257
       01 MAX                         PIC  9(03) COMP.                  KDS08257
       01 WS-AP-SYSID                 PIC  X(08).                       KDS08257
       01 PARM-FROM-PGM.                                                KDS08257
        05 PRM-IND                      PIC X(1).                       KDS08257
        05 PRM-SYSID-PGM                PIC X(8).                       KDS08257
        05 PRM-PROC-PGM                 PIC X(8).                       KDS08257
        05 PRM-FROM-PGM                 PIC X(8).                       KDS08257
        05 PRM-RERUN-PGM                PIC 9(2).                       KDS08257
       LINKAGE SECTION.                                                 KDS08257
       01 PARM-TO-MAIN.                                                 KDS08257
         05 PARM-LENGTH          PIC S9(4) COMP.                        KDS08257
         05 PARM-TEXT.                                                  KDS08257
            15 PARM-AP-SYSID     PIC X(08).                             KDS08257
            15 PARM-PROC         PIC X(08).                             KDS08257
            15 PARM-RC           PIC 9(02).                             KDS08257
            15 PRM-MOD           PIC X(01).                             KDS08257
            15 PRM-SKIP          PIC X(01).                             KDS08257
            15 PRM-FLG           PIC X(01).                             KDS08257
            15 PARM-PLUS         PIC X(77).                             KDS08257
       01 FILLER REDEFINES PARM-TO-MAIN.                                KDS08257
            15 PARM-SKIP-K       PIC X(01).                             KDS08257
            15 PARM-AP-SYSID-K   PIC X(08).                             KDS08257
            15 PARM-PROC-K       PIC X(08).                             KDS08257
            15 PARM-PLUS-K       PIC X(91).                             KDS08257
       PROCEDURE DIVISION USING PARM-TO-MAIN.                           KDS08257
       MAIN SECTION.                                                    KDS08257
TSTKDS*    DISPLAY 'JCSBAPNM BEGIN '                                    KDS08257
           PERFORM A000-INITIALIZE                                      KDS08257
           ACCEPT WS-CUR-TIME FROM TIME                                 KDS08257
           ACCEPT WS-YYYYMMDD FROM DATE YYYYMMDD                        KDS08257
           COPY JCSCWK05.                                               KDS08257
           IF PARM-LENGTH NOT = ZERO                                    KDS08257
              IF PARM-SKIP-K = K                                        KDS08257
                 MOVE PARM-PROC-K        TO WS-PROC                     KDS08257
                 MOVE PARM-AP-SYSID-K    TO WS-AP-SYSID                 KDS08257
              ELSE                                                      KDS08257
                 MOVE PARM-PROC          TO WS-PROC                     KDS08257
                 MOVE PARM-AP-SYSID      TO WS-AP-SYSID                 KDS08257
              END-IF                                                    KDS08257
              IF WS-PROC = JCSPEND                                      KDS08257
                 DISPLAY 'End running sheduler job'  UPON CONSOLE       KDS08257
      *          MOVE WARN               TO WS-RETURN-CD                KDS08257
                 PERFORM A300-STOP-RUN                                  KDS08257
              END-IF                                                    KDS08257
              PERFORM D000-MAIN-PROCESS                                 KDS08257
           ELSE                                                         KDS08257
              DISPLAY 'No job to process'                               KDS08257
              MOVE WARN                  TO WS-RETURN-CD                KDS08257
              PERFORM A300-STOP-RUN                                     KDS08257
           END-IF                                                       KDS08257
TSTKDS*    DISPLAY ':: JCSBAPNM  END ::'                                KDS08257
           PERFORM A300-STOP-RUN                                        KDS08257
           EXIT.                                                        KDS08257
       A000-INITIALIZE.                                                 KDS08257
           PERFORM F100-OPEN-FILES                                      KDS08257
           EXIT.                                                        KDS08257
       D000-MAIN-PROCESS.                                               KDS08257
           PERFORM D001-READ-DATECNTL                                   KDS08257
           MOVE 0             TO WS-I                                   KDS08257
           PERFORM R001-READ-JCSFJOBH                                   KDS08257
           PERFORM UNTIL WS-JCSFJOBH-STS = END-FILE                     KDS08257
              COMPUTE WS-I = WS-I + 1                                   KDS08257
              IF (LENGTH OF WS-JOB-HEAD / LENGTH80) >= WS-I             KDS08257
                 MOVE JCBHD-CONTENT         TO WS-HEAD-JCL(WS-I)        KDS08257
              END-IF                                                    KDS08257
              PERFORM R001-READ-JCSFJOBH                                KDS08257
           END-PERFORM                                                  KDS08257
           MOVE WS-I                        TO WS-JOB-HEAD-CNT          KDS08257
           IF WS-JOB-HEAD-CNT > LENGTH OF WS-JOB-HEAD / LENGTH80        KDS08257
              DISPLAY 'THE JOB HEAD''S NO IS ERROR'                     KDS08257
              MOVE ERROR8                   TO WS-RETURN-CD             KDS08257
              PERFORM A300-STOP-RUN                                     KDS08257
           END-IF                                                       KDS08257
           PERFORM R002-READ-JCSFMAST                                   KDS08257
           IF JCBMAS1-SUCC-JOB-CNT = ZERO AND                           KDS08257
              JCBMAS1-END-JOB-IND = WS-YES                              KDS08257
           THEN                                                         KDS08257
              DISPLAY 'No sucessive job to process'                     KDS08257
              MOVE 'JCSPEND'                TO WS-HEAD-JCL(1)(3:8)      KDS08257
              MOVE 'JCSPEND'                TO WS-HEAD-JCL(1)(35:8)     KDS08257
              MOVE 'JCSPEND'                TO WS-HEAD-JCL(11)(38:8)    KDS08257
              MOVE SPACE                    TO WS-HEAD-JCL(7)(25:50)    KDS08257
              PERFORM VARYING WS-K FROM 1 BY 1                          KDS08257
              UNTIL WS-K > LENGTH OF WS-JOB-HEAD / LENGTH80             KDS08257
                  WRITE JOBSUB-REC FROM WS-HEAD-JCL(WS-K)               KDS08257
                  DISPLAY 'WS-HEAD-JCL(WS-K)=' WS-HEAD-JCL(WS-K)        KDS08257
                  IF WS-JOBSUB-STS NOT = NORMAL                         KDS08257
                    DISPLAY 'WRITE JOBSUB ERROR CODE=' WS-JOBSUB-STS    KDS08257
                    MOVE ERROR8    TO WS-RETURN-CD                      KDS08257
                    PERFORM A300-STOP-RUN                               KDS08257
                 END-IF                                                 KDS08257
              END-PERFORM                                               KDS08257
              WRITE JOBSUB-REC FROM END-RUN-JCS-JCL                     KDS08257
TSTKDS        DISPLAY 'END TO RUN BATCH'   UPON CONSOLE                 KDS08257
              PERFORM A300-STOP-RUN                                     KDS08257
           END-IF                                                       KDS08257
           COMPUTE MAX = JCBMAS1-SUCC-JOB-CNT                           KDS08257
           PERFORM VARYING WS-I FROM 1 BY 1                             KDS08257
                   UNTIL WS-I > MAX                                     KDS08257
              PERFORM R003-READ-JCSFMAST                                KDS08257
TSTKDS*       DISPLAY 'CNTX=' JCBMAS1-SUCC-JOB-CNT ':' WS-I             KDS08257
              IF WS-I NOT > JCBMAS1-SUCC-JOB-CNT                        KDS08257
                 MOVE WS-YES                TO WS-CHK-PREV-JOB          KDS08257
                 PERFORM D010-CHECK-SUCCESS-JOB                         KDS08257
              END-IF                                                    KDS08257
TSTKDS*       DISPLAY 'WS-I999=' WS-I                                   KDS08257
           END-PERFORM.                                                 KDS08257
           EXIT.                                                        KDS08257
       D001-READ-DATECNTL.                                              KDS08257
           READ DATECNTL                                                KDS08257
           IF WS-DATECNTL-STS NOT = NORMAL                              KDS08257
              DISPLAY 'READ FILE DATECNTL ERROR CODE=' WS-DATECNTL-STS  KDS08257
              MOVE ERROR8                    TO WS-RETURN-CD            KDS08257
              PERFORM A300-STOP-RUN                                     KDS08257
           END-IF                                                       KDS08257
           READ JCSFDTTM                                                KDS08257
           IF WS-JCSFDTTM-STS NOT = NORMAL                              KDS08257
              DISPLAY 'READ FILE JCSFDTTM ERROR CODE=' WS-JCSFDTTM-STS  KDS08257
              MOVE  ERROR8              TO WS-RETURN-CD                 KDS08257
              PERFORM A300-STOP-RUN                                     KDS08257
           END-IF                                                       KDS08257
           EXIT.                                                        KDS08257
       D010-CHECK-SUCCESS-JOB.                                          KDS08257
TSTKDS*    DISPLAY 'Success job=' JCBMAS2-JOB ' WS-PROC=' WS-PROC       KDS08257
TSTKDS*         ' CNT=' JCBMAS2-PREV-JOB-CNT                            KDS08257
           PERFORM VARYING WS-J  FROM 1 BY 1                            KDS08257
                   UNTIL WS-J > JCBMAS2-PREV-JOB-CNT                    KDS08257
              IF JCBMAS2-PREV-JOB-STS(WS-J) NOT = S                     KDS08257
                 IF JCBMAS2-PREV-JOB(WS-J) = WS-PROC                    KDS08257
                    MOVE S               TO JCBMAS2-PREV-JOB-STS(WS-J)  KDS08257
                    REWRITE JCBMAST-REC  FROM JCBMAS2-REC               KDS08257
                 ELSE                                                   KDS08257
                    MOVE WS-NO           TO WS-CHK-PREV-JOB             KDS08257
                 END-IF                                                 KDS08257
              END-IF                                                    KDS08257
           END-PERFORM                                                  KDS08257
           IF WS-CHK-PREV-JOB = WS-YES AND JCBMAS2-JOB-STATUS = SPACE   KDS08257
              AND JCBMAS2-AUTO-TRIGER = WS-YES AND JCBMAS2-STS = ACT    KDS08257
           THEN                                                         KDS08257
              PERFORM D200-BEGIN-RUN-JOB                                KDS08257
           EXIT.                                                        KDS08257
       R001-READ-JCSFJOBH.                                              KDS08257
           READ JCSFJOBH                                                KDS08257
           IF WS-JCSFJOBH-STS NOT = NORMAL AND                          KDS08257
              WS-JCSFJOBH-STS NOT = END-FILE                            KDS08257
              DISPLAY 'READ FILE JCSFJOBH ERROR CODE=' WS-JCSFJOBH-STS  KDS08257
              MOVE ERROR8                    TO WS-RETURN-CD            KDS08257
              PERFORM A300-STOP-RUN                                     KDS08257
           END-IF                                                       KDS08257
           EXIT.                                                        KDS08257
       R002-READ-JCSFMAST.                                              KDS08257
           MOVE WS-PROC              TO JCBMAST-JOB                     KDS08257
           MOVE WS-AP-SYSID          TO JCBMAST-AP-SYSID                KDS08257
           READ JCSFMAST  INTO JCBMAS1-REC KEY IS JCBMAST-KEY           KDS08257
           IF WS-JCSFMAST-STS NOT = NORMAL                              KDS08257
              DISPLAY 'READ FILE JCSFMAST ERROR CODE=' WS-JCSFMAST-STS  KDS08257
                      ' KEY IS ' WS-PROC                                KDS08257
              MOVE ERROR8            TO WS-RETURN-CD                    KDS08257
              PERFORM A300-STOP-RUN                                     KDS08257
           END-IF                                                       KDS08257
           IF (JCBMAS1-JOB-STATUS NOT = SPACE AND                       KDS08257
               JCBMAS1-JOB-STATUS NOT = A     AND                       KDS08257
               JCBMAS1-JOB-STATUS NOT = K)                              KDS08257
           THEN                                                         KDS08257
              DISPLAY JCBMAS1-JOB                                       KDS08257
               ' INVALID JOB STATUS SHOULD BE SPACE OR A OR K='         KDS08257
              JCBMAS1-JOB-STATUS '=' WS-PROC ' SYSID=' JCBMAS1-AP-SYSID KDS08257
              ' EXEC-ST-TIME=' JCBMAST-EXEC-ST-TIME-9                   KDS08257
              MOVE ERROR8            TO WS-RETURN-CD                    KDS08257
              PERFORM A300-STOP-RUN                                     KDS08257
           END-IF                                                       KDS08257
TSTKDS*    DISPLAY 'R002 KEY JCBMAST-JOB=' JCBMAST-JOB                  KDS08257
TSTKDS*            ' OUT =' JCBMAS1-JOB                                 KDS08257
           IF PARM-SKIP-K = K                                           KDS08257
              MOVE K                 TO JCBMAS1-JOB-STATUS              KDS08257
           ELSE                                                         KDS08257
              MOVE S                 TO JCBMAS1-JOB-STATUS              KDS08257
           END-IF                                                       KDS08257
           MOVE WS-YYYYMMDD          TO JCBMAS1-EXEC-ED-DATE            KDS08257
           MOVE WS-CUR-TIME          TO JCBMAS1-EXEC-ED-TIME            KDS08257
           MOVE JCBDTTM-DTE          TO JCBMAS1-EXEC-ST-DATE-9          KDS08257
           MOVE JCBDTTM-TME          TO JCBMAS1-EXEC-ST-TIME-9          KDS08257
           REWRITE JCBMAST-REC FROM JCBMAS1-REC                         KDS08257
           IF WS-JCSFMAST-STS NOT = NORMAL                              KDS08257
              DISPLAY 'REWRITE JCSFMAST ERROR CODE=' WS-JCSFMAST-STS    KDS08257
              MOVE  ERROR8           TO WS-RETURN-CD                    KDS08257
              PERFORM A300-STOP-RUN                                     KDS08257
           END-IF                                                       KDS08257
           EXIT.                                                        KDS08257
       R003-READ-JCSFMAST.                                              KDS08257
           MOVE JCBMAS1-SUCC-JOB(WS-I) TO JCBMAST-JOB                   KDS08257
           MOVE WS-AP-SYSID            TO JCBMAST-AP-SYSID              KDS08257
TSTKDS*    DISPLAY 'READ FILE JCSFMAST KEY =' JCBMAST-KEY ':' WS-I      KDS08257
           READ JCSFMAST  INTO JCBMAS2-REC KEY IS JCBMAST-KEY           KDS08257
           IF WS-JCSFMAST-STS NOT = NORMAL                              KDS08257
              DISPLAY 'READ FILE JCSFMAST ERROR CODE=' WS-JCSFMAST-STS  KDS08257
                     ' KEY IS '  JCBMAST-JOB                            KDS08257
              MOVE ERROR8            TO WS-RETURN-CD                    KDS08257
              PERFORM A300-STOP-RUN                                     KDS08257
           END-IF                                                       KDS08257
           EXIT.                                                        KDS08257
      *D100-CHECK-MAST-JOB.                                             KDS08257
      *    COPY JCSCCHCK.                                               KDS08257
      *    EXIT.                                                        KDS08257
       D200-BEGIN-RUN-JOB.                                              KDS08257
TSTKDS*    DISPLAY 'D200-BEGIN-RUN-JOB JCBMAS2-JOB=' JCBMAS2-JOB        KDS08257
           ACCEPT WS-CUR-TIME FROM TIME                                 KDS08257
           MOVE JCBMAS2-JOB                 TO WS-HEAD-JCL(1)(3:8)      KDS08257
           MOVE JCBMAS2-JOB                 TO WS-HEAD-JCL(1)(35:8)     KDS08257
           MOVE JCBMAS2-JOB                 TO WS-HEAD-JCL(11)(38:8)    KDS08257
           MOVE SPACE                       TO WS-HEAD-JCL(7)(25:50)    KDS08257
           MOVE '//*'                       TO WS-HEAD-JCL(18)          KDS08257
           MOVE JCBMAS2-JOB                 TO WS-FRAME-JCL(3)(3:8)     KDS08257
           MOVE JCBMAS2-JOB                 TO WS-FRAME-JCL(3)(17:50)   KDS08257
           MOVE JCBMAS2-MAX-ACEPT-RC        TO WS-FRAME-JCL(4)(20:2)    KDS08257
           MOVE JCBMAS2-MAX-ACEPT-RC        TO WS-FRAME-JCL(10)(22:2)   KDS08257
           MOVE JCBMAS2-MAX-ACEPT-RC        TO WS-FRAME-JCL(17)(22:2)   KDS08257
           MOVE ''''                        TO WS-PRM-BEGIN             KDS08257
           MOVE ''''                        TO WS-PRM-END               KDS08257
           MOVE WS-AP-SYSID                 TO WS-PRM-SYSID             KDS08257
           MOVE JCBMAS2-JOB                 TO WS-PRM-PROC              KDS08257
           MOVE OK                          TO WS-PRM-ACEPT-RC          KDS08257
           MOVE WS-FROM-MODE                TO WS-PRM-MOD               KDS08257
           MOVE JCBMAS2-SKIP-IND            TO WS-PRM-SKIP              KDS08257
           MOVE WS-PARM-TO-PROC             TO WS-FRAME-JCL(5)(32:)     KDS08257
           MOVE WARN                        TO WS-PRM-ACEPT-RC          KDS08257
           MOVE WS-PARM-TO-PROC             TO WS-FRAME-JCL(11)(32:)    KDS08257
           MOVE ZERO                        TO WS-PRM-ACEPT-RC          KDS08257
           MOVE WS-PARM-TO-PROC             TO WS-FRAME-JCL(19)(32:)    KDS08257
           MOVE WS-PARM-TO-PROC             TO WS-FRAME-JCL(2)(32:)     KDS08257
           IF NOT OPEN-JOBSUB-OK                                        KDS08257
              PERFORM A500-OPEN-JOBSUB                                  KDS08257
           END-IF                                                       KDS08257
           PERFORM VARYING WS-K FROM 1 BY 1                             KDS08257
              UNTIL WS-K > LENGTH OF WS-JOB-HEAD / LENGTH80             KDS08257
              WRITE JOBSUB-REC FROM WS-HEAD-JCL(WS-K)                   KDS08257
TSTKDS*       DISPLAY 'WS-HEAD-JCL(WS-K)=' WS-HEAD-JCL(WS-K)            KDS08257
              IF WS-JOBSUB-STS NOT = NORMAL                             KDS08257
                 DISPLAY 'WRITE JOBSUB ERROR CODE=' WS-JOBSUB-STS       KDS08257
                 MOVE ERROR8    TO WS-RETURN-CD                         KDS08257
                 PERFORM A300-STOP-RUN                                  KDS08257
              END-IF                                                    KDS08257
           END-PERFORM                                                  KDS08257
           PERFORM VARYING WS-L FROM 1 BY 1                             KDS08257
              UNTIL WS-L > LENGTH OF WS-JOB-FRAME / LENGTH80            KDS08257
TSTKDS*       DISPLAY 'WS-FRAME-JCL(WS-L)=' WS-FRAME-JCL(WS-L)          KDS08257
              WRITE JOBSUB-REC FROM WS-FRAME-JCL(WS-L)                  KDS08257
              IF WS-JOBSUB-STS NOT = NORMAL                             KDS08257
                 DISPLAY 'WRITE JOBSUB ERROR CODE=' WS-JOBSUB-STS       KDS08257
                 MOVE ERROR8    TO WS-RETURN-CD                         KDS08257
                 PERFORM A300-STOP-RUN                                  KDS08257
              END-IF                                                    KDS08257
           END-PERFORM                                                  KDS08257
           IF OPEN-JOBSUB-OK                                            KDS08257
              PERFORM A300-CLOSE-FILES-JOBSUB                           KDS08257
           END-IF                                                       KDS08257
           EXIT.                                                        KDS08257
       A300-CLOSE-FILES-JOBSUB.                                         KDS08257
           CLOSE JOBSUB.                                                KDS08257
           SET OPEN-JOBSUB-NO   TO TRUE                                 KDS08257
           EXIT.                                                        KDS08257
       A300-STOP-RUN.                                                   KDS08257
           MOVE WS-RETURN-CD      TO RETURN-CODE                        KDS08257
TSTKDS*    MOVE 13                TO RETURN-CODE                        KDS08257
TSTKDS*    DISPLAY 'JCSBAPNM END=' WS-RETURN-CD ' B=' WS-RETURN-CD-B    KDS08257
           PERFORM F200-CLOSE-FILES                                     KDS08257
           GOBACK.                                                      KDS08257
           EXIT.                                                        KDS08257
       A500-OPEN-JOBSUB.                                                KDS08257
           PERFORM T00-OPEN-O-JOBSUB                                    KDS08257
           EXIT.                                                        KDS08257
      *                                                                 KDS08257
       F100-OPEN-FILES.                                                 KDS08257
           PERFORM T00-OPEN-IO-JCSFMAST                                 KDS08257
           PERFORM T00-OPEN-I-JCSFDTTM                                  KDS08257
           PERFORM T00-OPEN-I-DATECNTL                                  KDS08257
           PERFORM T00-OPEN-I-JCSFJOBH                                  KDS08257
           PERFORM T00-OPEN-O-JOBSUB                                    KDS08257
           EXIT.                                                        KDS08257
       F200-CLOSE-FILES.                                                KDS08257
           IF OPEN-JCSFMAST-OK                                          KDS08257
              CLOSE JCSFMAST                                            KDS08257
              SET OPEN-JCSFMAST-NO   TO TRUE                            KDS08257
           END-IF                                                       KDS08257
           IF OPEN-JCSFJOBH-OK                                          KDS08257
              CLOSE JCSFJOBH                                            KDS08257
              SET OPEN-JCSFJOBH-NO   TO TRUE                            KDS08257
           END-IF                                                       KDS08257
           IF OPEN-DATECNTL-OK                                          KDS08257
              CLOSE DATECNTL                                            KDS08257
              SET OPEN-DATECNTL-NO   TO TRUE                            KDS08257
           END-IF                                                       KDS08257
           IF OPEN-JOBSUB-OK                                            KDS08257
              CLOSE JOBSUB                                              KDS08257
              SET OPEN-JOBSUB-NO     TO TRUE                            KDS08257
           END-IF                                                       KDS08257
           IF OPEN-JCSFDTTM-OK                                          KDS08257
              CLOSE JCSFDTTM                                            KDS08257
              SET OPEN-JCSFDTTM-NO   TO TRUE                            KDS08257
           END-IF                                                       KDS08257
           EXIT.                                                        KDS08257
       T00-OPEN-IO-JCSFMAST.                                            KDS08257
           COPY JCSCWK06.                                               KDS08257
       T00-OPEN-I-JCSFDTTM.                                             KDS08257
           COPY JCSCWK08.                                               KDS08257
       T00-OPEN-I-DATECNTL.                                             KDS08257
           COPY JCSCWK10.                                               KDS08257
       T00-OPEN-I-JCSFJOBH.                                             KDS08257
           COPY JCSCWK11.                                               KDS08257
       T00-OPEN-O-JOBSUB.                                               KDS08257
           COPY JCSCWK09.                                               KDS08257
      *================================================================*KDS08257
      *           1         2         3         4         5         6123KDS08257
      ***123456789012345678901234567890123456789012345678901234567890123KDS08257
      *================================================================*KDS08257
