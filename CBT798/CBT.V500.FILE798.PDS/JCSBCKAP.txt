      *================================================================*KDS08257
      *           1         2         3         4         5         6123KDS08257
      ***123456789012345678901234567890123456789012345678901234567890123KDS08257
      *================================================================*KDS08257
      * KKKKKKKKKKKKK   DDDDDDDD    SSSSSSSS                           *KDS08257
      * KKKKKKKKKKKKK  DDDDDDDDD   SSSSSSSSS                           *KDS08257
      *      KKKK      DDDD        SSSS                                *KDS08257
      *      KKKK      DDDD        SSSS                                *KDS08257
      *      KKKK      DDDD        SSSS          1111          0000000 *KDS08257
      *      KKKK      DDDDDDDD    SSSSSSSS      1111        0000000000*KDS08257
      *      KKKK        DDDDDDD    SSSSSSSS     1111        0000  0000*KDS08257
      *      KKKK           DDDD        SSSS    1111         0000  0000*KDS08257
      *      KKKK           DDDD        SSSS    1111        0000  0000 *KDS08257
      *      KKKK           DDDD        SSSS    1111        0000  0000 *KDS08257
      * KKK  KKKK           DDDD        SSSS   1111   0000  0000  0000 *KDS08257
      * KKKKKKKK      DDDDDDDDDD   SSSSSSSSS   1111  0000  0000000000  *KDS08257
      * KKKKKKKK      DDDDDDDDD    SSSSSSSS    1111 0000    000000000  *KDS08257
      *================================================================*KDS08257
      * Change Log                                                     *KDS08257
      *================================================================*KDS08257
      * KDS08257    Kang Dengsong       INITIAL VERSION                *KDS08257
      *================================================================*KDS08257
       IDENTIFICATION DIVISION.                                         KDS08257
       PROGRAM-ID. JCSBCKAP.                                            KDS08257
       AUTHOR. KANG DENGSONG                                            KDS08257
       DATE-WRITTEN.  SEP 15,2008                                       KDS08257
      ******************************************************************KDS08257
      * Created   By :Kang Dengsong                                    *KDS08257
      * Created Date :SEP 13, 2008                                     *KDS08257
      * Remark       :Check ap file and initialize AP file             *KDS08257
      *================================================================*KDS08257
      *   This program is distributed without warranty of any kind,    *KDS08257
      * including  (without limitation) warranties of merchantability  *KDS08257
      * and of fitness for a particular purpose. It is up to the user  *KDS08257
      * to make his own determination of the usefulness of this program*KDS08257
      * in his own system.                                             *KDS08257
      *================================================================*KDS08257
      *                                                                *KDS08257
      *--------------------*                                            KDS08257
       ENVIRONMENT DIVISION.                                            KDS08257
      *--------------------*                                            KDS08257
       CONFIGURATION SECTION.                                           KDS08257
       SOURCE-COMPUTER. IBM-ES9000.                                     KDS08257
       OBJECT-COMPUTER. IBM-ES9000.                                     KDS08257
       INPUT-OUTPUT SECTION.                                            KDS08257
       FILE-CONTROL.                                                    KDS08257
           SELECT JCSFMAST  ASSIGN         TO JCSFMAST                  KDS08257
                            FILE STATUS    IS WS-JCSFMAST-STS           KDS08257
                            ACCESS MODE    IS SEQUENTIAL                KDS08257
                            ORGANIZATION   IS INDEXED                   KDS08257
                            RECORD KEY     IS JCBMAST-KEY.              KDS08257
           SELECT JCSFJOBH  ASSIGN         TO JCSFJOBH                  KDS08257
                            FILE STATUS    IS WS-JCSFJOBH-STS           KDS08257
                            ACCESS MODE    IS RANDOM                    KDS08257
                            ORGANIZATION   IS INDEXED                   KDS08257
                            RECORD KEY     IS JCBHD-SEQ.                KDS08257
       DATA DIVISION.                                                   KDS08257
       FILE SECTION.                                                    KDS08257
       FD JCSFMAST.                                                     KDS08257
       01 JCBMAST-REC.                                                  KDS08257
           COPY JCSCMAST  REPLACING ==:JCBMAST:== BY ==JCBMAST==.       KDS08257
       FD JCSFJOBH.                                                     KDS08257
           COPY JCSCJOBH.                                               KDS08257
      ******************************************************************KDS08257
       WORKING-STORAGE SECTION.                                         KDS08257
       01 TEMP                    PIC X(08) VALUE SPACE.                KDS08257
       01 WS-END                  PIC X(01) VALUE SPACE.                KDS08257
       01 WS-STOP                 PIC X(01) VALUE SPACE.                KDS08257
       01 WS-CNT1                 PIC 9(04) COMP VALUE 0.               KDS08257
       01 WS-CNT2                 PIC 9(04) COMP VALUE 0.               KDS08257
       01 WS-CNT3                 PIC 9(04) COMP VALUE 0.               KDS08257
       01 WS-CNT4                 PIC 9(04) COMP VALUE 0.               KDS08257
       01 WS-F                    PIC X(64) VALUE SPACE.                KDS08257
       01 WS-P-JOB-TBL REDEFINES WS-F.                                  KDS08257
          05 WS-P-JOB             OCCURS 8 TIMES PIC X(08).             KDS08257
       COPY JCSCWK00.                                                   KDS08257
       COPY JCSCWK02.                                                   KDS08257
      *                                                                 KDS08257
       LINKAGE SECTION.                                                 KDS08257
       01 PARM-TO-MAIN.                                                 KDS08257
          03 PARM-LENGTH          PIC S9(4) COMP.                       KDS08257
          03 PARM-SYSID           PIC X(008).                           KDS08257
          03 PARM-CONTENT         PIC X(092).                           KDS08257
      *                                                                 KDS08257
       PROCEDURE DIVISION USING PARM-TO-MAIN.                           KDS08257
       MAIN SECTION.                                                    KDS08257
TSTKDS*    DISPLAY 'JCSBCKAP BEGIN '                                    KDS08257
           PERFORM A000-INITIALIZE                                      KDS08257
           ACCEPT WS-YYYYMMDD FROM DATE YYYYMMDD                        KDS08257
           COPY JCSCWK05.                                               KDS08257
           PERFORM S100-READ-JCSFMAST                                   KDS08257
           PERFORM UNTIL WS-JCSFMAST-STS = END-FILE                     KDS08257
TSTKDS*       DISPLAY 'JCBMAST-JOB=' JCBMAST-JOB                        KDS08257
              IF PARM-SYSID = JCBMAST-AP-SYSID                          KDS08257
                 PERFORM S100-REWRITE-JCSFMAST                          KDS08257
              END-IF                                                    KDS08257
              PERFORM S100-READ-JCSFMAST                                KDS08257
           END-PERFORM                                                  KDS08257
      *    IF WS-CNT4 > 0 AND WS-CNT3 = 0                               KDS08257
      *       INITIALIZE JCBMAST-REC                                    KDS08257
      *       MOVE PARM-SYSID             TO JCBMAST-AP-SYSID           KDS08257
      *       MOVE JCSPEND                TO JCBMAST-JOB                KDS08257
      *       MOVE ACT                    TO JCBMAST-STS                KDS08257
      *       COMPUTE WS-K = 1                                          KDS08257
      *       PERFORM UNTIL WS-P-JOB(WS-K) = SPACE OR                   KDS08257
      *          WS-K <= LENGTH OF WS-P-JOB-TBL / LENGTH OF WS-P-JOB(1) KDS08257
      *          MOVE WS-P-JOB(WS-K)        TO JCBMAST-PREV-JOB(WS-K)   KDS08257
      *          COMPUTE WS-K = WS-K + 1                                KDS08257
      *       END-PERFORM                                               KDS08257
      *       WRITE JCBMAST-REC                                         KDS08257
      *       IF WS-JCSFMAST-STS NOT = NORMAL                           KDS08257
      *          DISPLAY 'WRITE JCSFMAST ERROR CODE=' WS-JCSFMAST-STS   KDS08257
      *          MOVE ERROR8       TO WS-RETURN-CD                      KDS08257
      *          PERFORM A300-STOP-RUN                                  KDS08257
      *       END-IF                                                    KDS08257
      *    END-IF                                                       KDS08257
           PERFORM F200-CLOSE-FILES                                     KDS08257
           PERFORM A300-STOP-RUN                                        KDS08257
           EXIT.                                                        KDS08257
       A000-INITIALIZE.                                                 KDS08257
           PERFORM F100-OPEN-FILES                                      KDS08257
           EXIT.                                                        KDS08257
       S100-READ-JCSFMAST.                                              KDS08257
           READ JCSFMAST                                                KDS08257
TSTKDS*    DISPLAY 'S100-READ-JCSFMAST=' WS-JCSFMAST-STS                KDS08257
           IF WS-JCSFMAST-STS NOT = NORMAL AND                          KDS08257
              WS-JCSFMAST-STS NOT = END-FILE                            KDS08257
           THEN                                                         KDS08257
              DISPLAY 'READ FILE JCSFMAST ERROR CODE=' WS-JCSFMAST-STS  KDS08257
              MOVE  ERROR8              TO WS-RETURN-CD                 KDS08257
              PERFORM A300-STOP-RUN                                     KDS08257
           END-IF                                                       KDS08257
           EXIT.                                                        KDS08257
      *                                                                 KDS08257
       S100-REWRITE-JCSFMAST.                                           KDS08257
TSTKDS*    DISPLAY 'S100-REWRITE-JCSFMAST '                             KDS08257
           COMPUTE WS-CNT4 = WS-CNT4 + 1                                KDS08257
           MOVE 1                           TO WS-I                     KDS08257
           MOVE ZERO                        TO WS-J                     KDS08257
           MOVE SPACE                       TO WS-END                   KDS08257
           COMPUTE HJK = LENGTH OF JCBMAST-PREV-JOB(1) +                KDS08257
                         LENGTH OF JCBMAST-PREV-JOB-STS(1)              KDS08257
           PERFORM VARYING WS-I FROM 1 BY 1                             KDS08257
             UNTIL WS-I > LENGTH OF JCBMAST-PREV-JOB-GRP / HJK          KDS08257
              IF JCBMAST-PREV-JOB(WS-I) NOT = SPACE AND                 KDS08257
                 JCBMAST-PREV-JOB(WS-I) NOT = WS-8-0 AND                KDS08257
                 JCBMAST-PREV-JOB(WS-I) NOT = LOW-VALUE                 KDS08257
              THEN                                                      KDS08257
                 ADD 1                     TO WS-J                      KDS08257
                 MOVE SPACE                TO JCBMAST-PREV-JOB-STS(WS-I)KDS08257
              END-IF                                                    KDS08257
              IF JCBMAST-PREV-JOB(WS-I) = WS-8-0 OR                     KDS08257
                 JCBMAST-PREV-JOB(WS-I) = LOW-VALUE                     KDS08257
              THEN                                                      KDS08257
                MOVE SPACE                 TO  JCBMAST-PREV-JOB(WS-I)   KDS08257
              END-IF                                                    KDS08257
           END-PERFORM                                                  KDS08257
           IF JCBMAST-PREV-JOB-CNT NOT = WS-J                           KDS08257
              MOVE  4                       TO WS-RETURN-CD             KDS08257
              DISPLAY 'INVALID PREVIOUS JOB NO MUST BE ='               KDS08257
               ' JCBMAST-PREV-JOB-CNT=' JCBMAST-PREV-JOB-CNT            KDS08257
               ' REAL PREV JOB NO=' WS-J                                KDS08257
              MOVE WS-J                     TO JCBMAST-PREV-JOB-CNT     KDS08257
           END-IF                                                       KDS08257
           COMPUTE WS-J = 0                                             KDS08257
           COMPUTE HJK = LENGTH OF JCBMAST-SUCC-JOB(1)                  KDS08257
           PERFORM VARYING WS-I FROM 1 BY 1                             KDS08257
             UNTIL WS-I > LENGTH OF JCBMAST-SUCC-JOB-GRP / HJK          KDS08257
              IF JCBMAST-SUCC-JOB(WS-I) NOT = SPACE AND                 KDS08257
                 JCBMAST-SUCC-JOB(WS-I) NOT = ALL ZERO AND              KDS08257
                 JCBMAST-SUCC-JOB(WS-I) NOT = LOW-VALUE                 KDS08257
              THEN                                                      KDS08257
                 ADD 1                      TO WS-J                     KDS08257
              END-IF                                                    KDS08257
              IF JCBMAST-SUCC-JOB(WS-I) = WS-8-0 OR                     KDS08257
                 JCBMAST-SUCC-JOB(WS-I) = LOW-VALUE                     KDS08257
              THEN                                                      KDS08257
                 MOVE SPACE                 TO JCBMAST-SUCC-JOB(WS-I)   KDS08257
              END-IF                                                    KDS08257
           END-PERFORM                                                  KDS08257
           IF JCBMAST-SUCC-JOB-CNT NOT = WS-J                           KDS08257
              MOVE  4                       TO WS-RETURN-CD             KDS08257
              DISPLAY 'INVALID PREVIOUS JOB NO MUST BE ='               KDS08257
               ' JCBMAST-SUCC-JOB-CNT=' JCBMAST-SUCC-JOB-CNT            KDS08257
               ' REAL SUCESS JOB NO=' WS-J                              KDS08257
              MOVE WS-J                     TO JCBMAST-SUCC-JOB-CNT     KDS08257
           END-IF                                                       KDS08257
           IF JCBMAST-JOB = JCSPEND                                     KDS08257
              COMPUTE WS-CNT3 = 1                                       KDS08257
           END-IF                                                       KDS08257
           IF JCBMAST-SUCC-JOB-CNT = 0                                  KDS08257
              COMPUTE WS-K = WS-K + 1                                   KDS08257
              IF WS-K <= LENGTH OF WS-P-JOB-TBL / LENGTH OF WS-P-JOB(1) KDS08257
                 MOVE JCBMAST-JOB           TO WS-P-JOB(WS-K)           KDS08257
              ELSE                                                      KDS08257
                 DISPLAY 'TOO MANY PREVIOUS JOBS FOR JCSPEND'           KDS08257
                 MOVE WARN                  TO WS-RETURN-CD             KDS08257
              END-IF                                                    KDS08257
           END-IF                                                       KDS08257
      *    IF JCBMAST-SUCC-JOB-CNT = 0 AND JCBMAST-JOB NOT = JCSPEND    KDS08257
      *        MOVE K1                      TO JCBMAST-SUCC-JOB-CNT     KDS08257
      *        MOVE JCSPEND                 TO JCBMAST-SUCC-JOB(1)      KDS08257
      *    END-IF                                                       KDS08257
           MOVE ZERO                        TO JCBMAST-EXEC-RC-LAST     KDS08257
           MOVE ZERO                        TO JCBMAST-EXEC-ST-DATE-9   KDS08257
           MOVE ZERO                        TO JCBMAST-EXEC-ST-TIME-9   KDS08257
           MOVE ZERO                        TO JCBMAST-EXEC-ED-DATE     KDS08257
           MOVE ZERO                        TO JCBMAST-EXEC-ED-TIME     KDS08257
           MOVE SPACE                       TO JCBMAST-JOB-STATUS       KDS08257
           MOVE SPACE                       TO JCBMAST-ERROR-STEP       KDS08257
           MOVE SPACE                       TO JCBMAST-ERROR-CODE       KDS08257
           MOVE SPACE                       TO JCBMAST-EXEC-MODE        KDS08257
           MOVE SPACE                       TO WS-END                   KDS08257
           COMPUTE WS-CNT1 = LENGTH OF JCBMAST-PREV-JOB-GRP / HJK       KDS08257
           COMPUTE WS-CNT2 = LENGTH OF JCBMAST-SUCC-JOB-GRP / HJK       KDS08257
TSTKDS*    DISPLAY 'WS-CNT1 WS-CNT12=' WS-CNT1 ':' WS-CNT2
TSTKDS*    DISPLAY 'PREV-JOB-GRP1=' JCBMAST-PREV-JOB-GRP
           PERFORM VARYING WS-I FROM 1 BY 1                             KDS08257
           UNTIL WS-I > WS-CNT1                                         KDS08257
              PERFORM VARYING WS-J FROM 1 BY 1                          KDS08257
              UNTIL WS-J > WS-CNT1 - 1                                  KDS08257
                IF JCBMAST-PREV-JOB(WS-J)                               KDS08257
                   < JCBMAST-PREV-JOB(WS-J + 1)                         KDS08257
                THEN                                                    KDS08257
TSTKDS*            DISPLAY 'XCHANGE PREV JOB '
                   MOVE JCBMAST-PREV-JOB(WS-J)  TO TEMP                 KDS08257
                   MOVE JCBMAST-PREV-JOB(WS-J + 1)                      KDS08257
                     TO JCBMAST-PREV-JOB(WS-J)                          KDS08257
                   MOVE TEMP                                            KDS08257
                     TO JCBMAST-PREV-JOB(WS-J + 1)                      KDS08257
               END-IF                                                   KDS08257
             END-PERFORM                                                KDS08257
           END-PERFORM                                                  KDS08257
TSTKDS*    DISPLAY 'PREV-JOB-GRP2=' JCBMAST-PREV-JOB-GRP
TSTKDS*    DISPLAY 'SUCC-JOB-GRP1=' JCBMAST-SUCC-JOB-GRP
           PERFORM VARYING WS-I FROM 1 BY 1                             KDS08257
           UNTIL WS-I > WS-CNT2                                         KDS08257
              PERFORM VARYING WS-J FROM 1 BY 1                          KDS08257
              UNTIL WS-J > WS-CNT2 - 1                                  KDS08257
                IF JCBMAST-SUCC-JOB(WS-J)                               KDS08257
                   < JCBMAST-SUCC-JOB(WS-J + 1)                         KDS08257
                THEN                                                    KDS08257
TSTKDS*            DISPLAY 'XCHANGE SUCC JOB '
                   MOVE JCBMAST-SUCC-JOB(WS-J)  TO TEMP                 KDS08257
                   MOVE JCBMAST-SUCC-JOB(WS-J + 1)                      KDS08257
                     TO JCBMAST-SUCC-JOB(WS-J)                          KDS08257
                   MOVE TEMP                                            KDS08257
                     TO JCBMAST-SUCC-JOB(WS-J + 1)                      KDS08257
               END-IF                                                   KDS08257
             END-PERFORM                                                KDS08257
           END-PERFORM                                                  KDS08257
TSTKDS*    DISPLAY 'SUCC-JOB-GRP2=' JCBMAST-SUCC-JOB-GRP
           COMPUTE JCBMAST-COST-TIME = ZERO                             KDS08257
           REWRITE JCBMAST-REC                                          KDS08257
           IF WS-JCSFMAST-STS NOT = NORMAL                              KDS08257
              DISPLAY 'REWRITE JCSFMAST ERROR CODE=' WS-JCSFMAST-STS    KDS08257
              MOVE     ERROR8   TO WS-RETURN-CD                         KDS08257
              PERFORM A300-STOP-RUN                                     KDS08257
           END-IF                                                       KDS08257
           EXIT.                                                        KDS08257
       A300-STOP-RUN.                                                   KDS08257
           PERFORM F200-CLOSE-FILES                                     KDS08257
           MOVE WS-RETURN-CD      TO RETURN-CODE                        KDS08257
TSTKDS*    DISPLAY 'JCSBCKAP END  WS-RETURN-CD=' WS-RETURN-CD           KDS08257
           GOBACK.                                                      KDS08257
      *    EXIT.                                                        KDS08257
      *COPY JCSCWK03.                                                   KDS08257
       F100-OPEN-FILES.                                                 KDS08257
           PERFORM T00-OPEN-IO-JCSFMAST                                 KDS08257
           PERFORM T00-OPEN-IO-JCSFJOBH                                 KDS08257
           MOVE K18                     TO JCBHD-SEQ                    KDS08257
           READ JCSFJOBH KEY IS JCBHD-SEQ                               KDS08257
           IF WS-JCSFJOBH-STS NOT = NORMAL                              KDS08257
           THEN                                                         KDS08257
              DISPLAY 'READ FILE JCSFJOBH ERROR CODE=' WS-JCSFJOBH-STS  KDS08257
              MOVE  ERROR8              TO WS-RETURN-CD                 KDS08257
              PERFORM A300-STOP-RUN                                     KDS08257
           ELSE                                                         KDS08257
             IF JCBHD-CONTENT = BEGIN-RUN-JCS-BATCH                     KDS08257
                MOVE ERROR8               TO WS-RETURN-CD               KDS08257
                MOVE SPACE                TO WS-RESP                    KDS08257
                PERFORM UNTIL WS-RESP = WS-YES                          KDS08257
                    DISPLAY 'Another batch have been started STOP(Y)?'  KDS08257
                    UPON CONSOLE                                        KDS08257
                    ACCEPT WS-RESP  FROM CONSOLE                        KDS08257
                END-PERFORM                                             KDS08257
                PERFORM A300-STOP-RUN                                   KDS08257
             ELSE                                                       KDS08257
                MOVE BEGIN-RUN-JCS-BATCH  TO JCBHD-CONTENT              KDS08257
                DISPLAY 'Begin to start to run scheduler batch'         KDS08257
                UPON CONSOLE                                            KDS08257
                REWRITE JCBHD-REC                                       KDS08257
                IF WS-JCSFJOBH-STS NOT = NORMAL                         KDS08257
                THEN                                                    KDS08257
                   DISPLAY 'REWRITE JCSFJOBH ERROR CODE='               KDS08257
                    WS-JCSFJOBH-STS                                     KDS08257
                   MOVE     ERROR8   TO WS-RETURN-CD                    KDS08257
                   PERFORM A300-STOP-RUN                                KDS08257
                END-IF                                                  KDS08257
             END-IF                                                     KDS08257
           END-IF                                                       KDS08257
           EXIT.                                                        KDS08257
       F200-CLOSE-FILES.                                                KDS08257
           IF OPEN-JCSFMAST-OK                                          KDS08257
              CLOSE JCSFMAST                                            KDS08257
              SET OPEN-JCSFMAST-NO   TO TRUE                            KDS08257
           END-IF                                                       KDS08257
           IF OPEN-JCSFJOBH-OK                                          KDS08257
              CLOSE JCSFJOBH                                            KDS08257
              SET OPEN-JCSFJOBH-NO   TO TRUE                            KDS08257
           END-IF                                                       KDS08257
           EXIT.                                                        KDS08257
       T00-OPEN-IO-JCSFMAST.                                            KDS08257
           COPY JCSCWK06.                                               KDS08257
       T00-OPEN-IO-JCSFJOBH.                                            KDS08257
           COPY JCSCWK13.                                               KDS08257
      *================================================================*KDS08257
      *           1         2         3         4         5         6123KDS08257
      ***123456789012345678901234567890123456789012345678901234567890123KDS08257
      *================================================================*KDS08257
