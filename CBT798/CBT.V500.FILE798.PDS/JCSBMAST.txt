      *================================================================*KDS08257
      *           1         2         3         4         5         6123KDS08257
      ***123456789012345678901234567890123456789012345678901234567890123KDS08257
      *================================================================*KDS08257
      * KKKKKKKKKKKKK   DDDDDDDD    SSSSSSSS                           *KDS08257
      * KKKKKKKKKKKKK  DDDDDDDDD   SSSSSSSSS                           *KDS08257
      *      KKKK      DDDD        SSSS                                *KDS08257
      *      KKKK      DDDD        SSSS                                *KDS08257
      *      KKKK      DDDD        SSSS          1111          0000000 *KDS08257
      *      KKKK      DDDDDDDD    SSSSSSSS      1111        0000000000*KDS08257
      *      KKKK        DDDDDDD    SSSSSSSS     1111        0000  0000*KDS08257
      *      KKKK           DDDD        SSSS    1111         0000  0000*KDS08257
      *      KKKK           DDDD        SSSS    1111        0000  0000 *KDS08257
      *      KKKK           DDDD        SSSS    1111        0000  0000 *KDS08257
      * KKK  KKKK           DDDD        SSSS   1111   0000  0000  0000 *KDS08257
      * KKKKKKKK      DDDDDDDDDD   SSSSSSSSS   1111  0000  0000000000  *KDS08257
      * KKKKKKKK      DDDDDDDDD    SSSSSSSS    1111 0000    000000000  *KDS08257
      *================================================================*KDS08257
      * Change Log                                                     *KDS08257
      *================================================================*KDS08257
      * KDS08257    Kang Dengsong       INITIAL VERSION                *KDS08257
      *================================================================*KDS08257
      **          1         2         3         4         5         6123KDS08257
      ***123456789012345678901234567890123456789012345678901234567890123KDS08257
       IDENTIFICATION DIVISION.                                         KDS08257
       PROGRAM-ID. JCSBMAST.                                            KDS08257
       AUTHOR. KANG DENGSONG                                            KDS08257
       DATE-WRITTEN.  SEP 15,2008                                       KDS08257
      ******************************************************************KDS08257
      * Created   By :Kang Dengsong                                    *KDS08257
      * Created Date :SEP 13, 2008                                     *KDS08257
      * REMARK       :READ MAST JOB CONTROL FILE AND SCHDULE IT        *KDS08257
      *              :Job scheduler system                             *KDS08257
      *================================================================*KDS08257
      *   This program is distributed without warranty of any kind,    *KDS08257
      * including  (without limitation) warranties of merchantability  *KDS08257
      * and of fitness for a particular purpose. It is up to the user  *KDS08257
      * to make his own determination of the usefulness of this program*KDS08257
      * in his own system.                                             *KDS08257
      *================================================================*KDS08257
      *                                                                *KDS08257
      *--------------------*                                            KDS08257
       ENVIRONMENT DIVISION.                                            KDS08257
      *--------------------*                                            KDS08257
       CONFIGURATION SECTION.                                           KDS08257
       SOURCE-COMPUTER. IBM-ES9000.                                     KDS08257
       OBJECT-COMPUTER. IBM-ES9000.                                     KDS08257
       INPUT-OUTPUT SECTION.                                            KDS08257
       FILE-CONTROL.                                                    KDS08257
           SELECT JOBSUB    ASSIGN         TO JOBSUB                    KDS08257
                            ORGANIZATION   IS SEQUENTIAL                KDS08257
                            ACCESS MODE    IS SEQUENTIAL                KDS08257
                            FILE STATUS    IS WS-JOBSUB-STS.            KDS08257
           SELECT DATECNTL  ASSIGN         TO DATECNTL                  KDS08257
                            ORGANIZATION   IS SEQUENTIAL                KDS08257
                            ACCESS MODE    IS SEQUENTIAL                KDS08257
                            FILE STATUS    IS WS-DATECNTL-STS.          KDS08257
           SELECT JCSFMAST  ASSIGN         TO JCSFMAST                  KDS08257
                            FILE STATUS    IS WS-JCSFMAST-STS           KDS08257
                            ACCESS MODE    IS SEQUENTIAL                KDS08257
                            ORGANIZATION   IS INDEXED                   KDS08257
                            RECORD KEY     IS JCBMAST-KEY.              KDS08257
           SELECT JCSFJOBH  ASSIGN         TO JCSFJOBH                  KDS08257
                            FILE STATUS    IS WS-JCSFJOBH-STS           KDS08257
                            ACCESS MODE    IS SEQUENTIAL                KDS08257
                            ORGANIZATION   IS INDEXED                   KDS08257
                            RECORD KEY     IS JCBHD-SEQ.                KDS08257
       DATA DIVISION.                                                   KDS08257
       FILE SECTION.                                                    KDS08257
       FD JOBSUB.                                                       KDS08257
       01 JOBSUB-REC                     PIC   X(80).                   KDS08257
       FD DATECNTL.                                                     KDS08257
           COPY DTCCNTL.                                                KDS08257
       FD JCSFMAST.                                                     KDS08257
       01 JCBMAST-REC.                                                  KDS08257
           COPY JCSCMAST  REPLACING ==:JCBMAST:== BY ==JCBMAST==.       KDS08257
       FD JCSFJOBH.                                                     KDS08257
           COPY JCSCJOBH.                                               KDS08257
      ******************************************************************KDS08257
       WORKING-STORAGE SECTION.                                         KDS08257
       COPY JCSCWK00.                                                   KDS08257
       COPY JCSCWK02.                                                   KDS08257
       COPY JCSCWK01.                                                   KDS08257
       LINKAGE SECTION.                                                 KDS08257
       01 PARM-TO-MAIN.                                                 KDS08257
         05 PARM-LENGTH          PIC S9(4) COMP.                        KDS08257
         05 PARM-TEXT.                                                  KDS08257
            10 PARM-SYSID        PIC X(08).                             KDS08257
            10 PARM-DELAY        PIC X(04).                             KDS08257
            10 PARM-MOD          PIC X(01).                             KDS08257
            10 PARM-PLUS         PIC X(87).                             KDS08257
       PROCEDURE DIVISION USING PARM-TO-MAIN.                           KDS08257
       MAIN SECTION.                                                    KDS08257
TSTKDS*    DISPLAY 'JCSBMAST BEGIN '                                    KDS08257
           PERFORM UNTIL WS-ACCEPT = WS-YES                             KDS08257
              PERFORM A000-INITIALIZE                                   KDS08257
              ACCEPT WS-CUR-TIME FROM TIME                              KDS08257
              ACCEPT WS-YYYYMMDD FROM DATE YYYYMMDD                     KDS08257
              COPY JCSCWK05.                                            KDS08257
              IF OPEN-JOBSUB-OK                                         KDS08257
TSTKDS*          DISPLAY '0001A300-CLOSE-FILES-JOBSUB'                  KDS08257
                 PERFORM A300-CLOSE-FILES-JOBSUB                        KDS08257
              END-IF                                                    KDS08257
              PERFORM D000-MAIN-PROCESS                                 KDS08257
              IF OPEN-JOBSUB-OK                                         KDS08257
                 PERFORM A300-CLOSE-FILES-JOBSUB                        KDS08257
              END-IF                                                    KDS08257
              IF WS-FIRST = SPACE                                       KDS08257
                 MOVE Z          TO WS-FIRST                            KDS08257
              END-IF                                                    KDS08257
              PERFORM F200-CLOSE-FILES                                  KDS08257
              ACCEPT WS-CUR-TIME FROM TIME                              KDS08257
      *       DISPLAY '1CALL ILBOWAT0 =' WS-DELAY-AMT ':' WS-CUR-TIME   KDS08257
              MOVE WS-DELAY-AMT        TO DELAY-AMT                     KDS08257
              CALL ILBOWAT0 USING DELAY-AMT                             KDS08257
              ACCEPT WS-CUR-TIME FROM TIME                              KDS08257
TSTKDS*       DISPLAY '2CALL ILBOWAT0=' DELAY-AMT ':' WS-CUR-TIME       KDS08257
           END-PERFORM                                                  KDS08257
      *    GOBACK                                                       KDS08257
           EXIT.                                                        KDS08257
       A000-INITIALIZE.                                                 KDS08257
           IF PARM-DELAY IS NUMERIC                                     KDS08257
              MOVE PARM-DELAY          TO WS-DELAY-AMT                  KDS08257
TSTKDS*       DISPLAY 'DELAY-AMT=' WS-DELAY-AMT                         KDS08257
           ELSE                                                         KDS08257
              DISPLAY 'THE PARM FROM JCL FIRST 4 BYTES MUST BE NUM'     KDS08257
              MOVE ERROR8              TO WS-RETURN-CD                  KDS08257
              PERFORM A300-STOP-RUN                                     KDS08257
           END-IF                                                       KDS08257
           IF PARM-MOD       = IPL     OR                               KDS08257
              PARM-MOD       = CONSOLE OR                               KDS08257
              PARM-MOD       = JES2    OR                               KDS08257
              PARM-MOD       = CICS    OR                               KDS08257
              PARM-MOD       = OTHR                                     KDS08257
           THEN                                                         KDS08257
              MOVE PARM-TEXT(5:1)                                       KDS08257
                TO WS-FROM-MODE                                         KDS08257
           ELSE                                                         KDS08257
              MOVE ERROR8              TO WS-RETURN-CD                  KDS08257
              DISPLAY 'INVALID EXEC MODE MUST BE I C J C O'             KDS08257
              PERFORM A300-STOP-RUN                                     KDS08257
           END-IF                                                       KDS08257
           EXIT.                                                        KDS08257
       D000-MAIN-PROCESS.                                               KDS08257
TSTKDS*    DISPLAY 'WS-FIRST=' WS-FIRST                                 KDS08257
           PERFORM F100-OPEN-FILES                                      KDS08257
           IF WS-FIRST = SPACE                                          KDS08257
              PERFORM D001-READ-DATECNTL                                KDS08257
              MOVE 0          TO WS-I                                   KDS08257
              PERFORM D002-READ-JCSFJOBH                                KDS08257
              PERFORM UNTIL WS-JCSFJOBH-STS = END-FILE                  KDS08257
                 COMPUTE WS-I = WS-I + 1                                KDS08257
                 IF (LENGTH OF WS-JOB-HEAD / LENGTH80) >= WS-I          KDS08257
                    MOVE JCBHD-CONTENT      TO WS-HEAD-JCL(WS-I)        KDS08257
                 END-IF                                                 KDS08257
                 PERFORM D002-READ-JCSFJOBH                             KDS08257
              END-PERFORM                                               KDS08257
              MOVE WS-I                     TO WS-JOB-HEAD-CNT          KDS08257
              IF WS-JOB-HEAD-CNT > LENGTH OF WS-JOB-HEAD / LENGTH80     KDS08257
                 DISPLAY 'THE JOB HEAD''S NO IS ERROR'                  KDS08257
                 MOVE 9                     TO WS-RETURN-CD             KDS08257
                 PERFORM F200-CLOSE-FILES                               KDS08257
                 PERFORM A300-STOP-RUN                                  KDS08257
              END-IF                                                    KDS08257
           END-IF                                                       KDS08257
           MOVE 1                       TO WS-I                         KDS08257
           PERFORM D004-READ-JCSFMAST                                   KDS08257
           PERFORM UNTIL WS-JCSFMAST-STS = END-FILE                     KDS08257
TSTKDS*       DISPLAY 'JCBMAST-JOB=' JCBMAST-JOB                        KDSKDS08
              IF PARM-SYSID = JCBMAST-AP-SYSID                          KDSKDS08
                 PERFORM D100-CHECK-MAST-JOB                            KDSKDS08
TSTKDS           DISPLAY JCBMAST-JOB 'WS-RUN=' WS-RUN                   KDSKDS08
TSTKDS             ' JOB ST=' JCBMAST-JOB-STATUS ' WS-RUN2=' WS-RUN2    KDSKDS08
TSTKDS          ' FREQ=' JCBMAST-EXEC-FREQ1 ' ST='                      KDSKDS08
TSTKDS           JCBMAST-EXEC-ST-DATE                                   KDSKDS08
                 IF JCBMAST-STS = ACT AND                               KDSKDS08
                   ((JCBMAST-AUTO-TRIGER = WS-YES AND                   KDSKDS08
                      WS-RUN = WS-YES) OR                               KDS08257
                   (JCBMAST-AUTO-TRIGER = WS-NO AND WS-RUN2 = WS-YES))  KDS08257
                 THEN                                                   KDS08257
                    PERFORM D200-PREPARE-RUN-JOB                        KDSKDS08
TSTKDS              DISPLAY JCBMAST-JOB ' SUBMIT!'                      KDSKDS08
                 END-IF                                                 KDSKDS08
              END-IF                                                    KDSKDS08
              PERFORM D004-READ-JCSFMAST                                KDS08257
           END-PERFORM                                                  KDS08257
TSTKDS*    DISPLAY 'D000-MAIN-PROCESS END'                              KDSKDS08
           EXIT.                                                        KDS08257
       D001-READ-DATECNTL.                                              KDS08257
           READ DATECNTL                                                KDS08257
           IF WS-DATECNTL-STS NOT = NORMAL                              KDS08257
              DISPLAY 'READ FILE DATECNTL ERROR CODE=' WS-DATECNTL-STS  KDS08257
              MOVE  ERROR8                   TO WS-RETURN-CD            KDS08257
              PERFORM A300-STOP-RUN                                     KDS08257
           END-IF                                                       KDS08257
           DISPLAY 'DATECNTL-TWD=' DATECNTL-TWD                         KDS08257
           EXIT.                                                        KDS08257
       D002-READ-JCSFJOBH.                                              KDS08257
           READ JCSFJOBH                                                KDS08257
           IF WS-JCSFJOBH-STS NOT = NORMAL AND                          KDS08257
              WS-JCSFJOBH-STS NOT = END-FILE                            KDS08257
              DISPLAY 'READ FILE JCSFJOBH ERROR CODE=' WS-JCSFJOBH-STS  KDS08257
              MOVE  ERROR8                   TO WS-RETURN-CD            KDS08257
              PERFORM A300-STOP-RUN                                     KDS08257
           END-IF                                                       KDS08257
           EXIT.                                                        KDS08257
TSTKDS*D003-READ-JCSFFRAM.                                              KDS08257
TSTKDS*    READ JCSFFRAM                                                KDS08257
TSTKDS*    IF WS-JCSFFRAM-STS NOT = NORMAL AND                          KDS08257
TSTKDS*       WS-JCSFFRAM-STS NOT = END-FILE                            KDS08257
TSTKDS*       DISPLAY 'READ FILE JCSFFRAM ERROR CODE=' WS-JCSFFRAM-STS  KDS08257
TSTKDS*       MOVE  ERROR8            TO WS-RETURN-CD                   KDS08257
TSTKDS*       PERFORM A300-STOP-RUN                                     KDS08257
TSTKDS*    END-IF                                                       KDS08257
TSTKDS*    EXIT.                                                        KDS08257
       D004-READ-JCSFMAST.                                              KDS08257
           READ JCSFMAST                                                KDS08257
           IF WS-JCSFMAST-STS NOT = NORMAL AND                          KDS08257
              WS-JCSFMAST-STS NOT = END-FILE                            KDS08257
              DISPLAY 'READ FILE JCSFMAST ERROR CODE=' WS-JCSFMAST-STS  KDS08257
              MOVE ERROR8            TO WS-RETURN-CD                    KDS08257
              PERFORM A300-STOP-RUN                                     KDS08257
           END-IF                                                       KDS08257
           EXIT.                                                        KDS08257
       D100-CHECK-MAST-JOB.                                             KDS08257
           COPY JCSCCHCK.                                               KDS08257
       D200-PREPARE-RUN-JOB.                                            KDS08257
           ACCEPT WS-CUR-TIME FROM TIME                                 KDS08257
           MOVE JCBMAST-JOB                 TO WS-HEAD-JCL(1)(3:8)      KDS08257
           MOVE JCBMAST-JOB                 TO WS-HEAD-JCL(1)(35:8)     KDS08257
           MOVE JCBMAST-JOB                 TO WS-HEAD-JCL(11)(38:8)    KDS08257
           MOVE SPACE                       TO WS-HEAD-JCL(7)(25:50)    KDS08257
           MOVE JCBMAST-JOB                 TO WS-FRAME-JCL(3)(3:8)     KDS08257
           MOVE JCBMAST-JOB                 TO WS-FRAME-JCL(3)(17:50)   KDS08257
           MOVE JCBMAST-MAX-ACEPT-RC        TO WS-FRAME-JCL(4)(20:2)    KDS08257
           MOVE JCBMAST-MAX-ACEPT-RC        TO WS-FRAME-JCL(10)(22:2)   KDS08257
           MOVE JCBMAST-MAX-ACEPT-RC        TO WS-FRAME-JCL(17)(22:2)   KDS08257
           MOVE PARM-SYSID                  TO WS-PRM-SYSID             KDS08257
           MOVE ''''                        TO WS-PRM-BEGIN             KDS08257
           MOVE ''''                        TO WS-PRM-END               KDS08257
           MOVE JCBMAST-JOB                 TO WS-PRM-PROC              KDS08257
           MOVE OK                          TO WS-PRM-ACEPT-RC          KDS08257
           MOVE WS-FROM-MODE                TO WS-PRM-MOD               KDS08257
           MOVE JCBMAST-SKIP-IND            TO WS-PRM-SKIP              KDS08257
           MOVE WS-PARM-TO-PROC             TO WS-FRAME-JCL(5)(32:)     KDS08257
           MOVE WARN                        TO WS-PRM-ACEPT-RC          KDS08257
      *    MOVE WS-CUR-TIME-9               TO WS-PRM-ST-TME            KDS08257
      *    MOVE WS-YYYYMMDD-9               TO WS-PRM-ST-DTE            KDS08257
           MOVE WS-PARM-TO-PROC             TO WS-FRAME-JCL(11)(32:)    KDS08257
           MOVE ZERO                        TO WS-PRM-ACEPT-RC          KDS08257
           MOVE WS-PARM-TO-PROC             TO WS-FRAME-JCL(19)(32:)    KDS08257
           MOVE WS-PARM-TO-PROC             TO WS-FRAME-JCL(2)(32:)     KDS08257
           IF NOT OPEN-JOBSUB-OK                                        KDS08257
TSTKDS*       DISPLAY '2A500-OPEN-JOBSUB'                               KDS08257
              PERFORM A500-OPEN-JOBSUB                                  KDS08257
           END-IF                                                       KDS08257
TSTKDS*    ACCEPT WS-CUR-TIME FROM TIME                                 KDS08257
TSTKDS*    DISPLAY 'WRITE JOBSUB     ' WS-CUR-TIME                      KDS08257
           PERFORM VARYING WS-I FROM 1 BY 1                             KDS08257
              UNTIL WS-I > LENGTH OF WS-JOB-HEAD / LENGTH80             KDS08257
TSTKDS*       DISPLAY 'WS-HEAD-JCL(WS-I)=' WS-HEAD-JCL(WS-I)            KDS08257
              WRITE JOBSUB-REC FROM WS-HEAD-JCL(WS-I)                   KDS08257
              IF WS-JOBSUB-STS NOT = NORMAL                             KDS08257
                 DISPLAY 'WRITE JOBSUB ERROR CODE=' WS-JOBSUB-STS       KDS08257
                 MOVE  ERROR8   TO WS-RETURN-CD                         KDS08257
                 PERFORM A300-STOP-RUN                                  KDS08257
              END-IF                                                    KDS08257
           END-PERFORM                                                  KDS08257
           PERFORM VARYING WS-I FROM 1 BY 1                             KDS08257
              UNTIL WS-I > LENGTH OF WS-JOB-FRAME / LENGTH80            KDS08257
TSTKDS*       DISPLAY 'WS-FRAME-JCL(WS-I)=' WS-FRAME-JCL(WS-I)          KDS08257
              WRITE JOBSUB-REC FROM WS-FRAME-JCL(WS-I)                  KDS08257
              IF WS-JOBSUB-STS NOT = NORMAL                             KDS08257
                 DISPLAY 'WRITE JOBSUB ERROR CODE=' WS-JOBSUB-STS       KDS08257
                 MOVE  ERROR8   TO WS-RETURN-CD                         KDS08257
                 PERFORM A300-STOP-RUN                                  KDS08257
              END-IF                                                    KDS08257
           END-PERFORM                                                  KDS08257
           IF OPEN-JOBSUB-OK                                            KDS08257
              PERFORM A300-CLOSE-FILES-JOBSUB                           KDS08257
           END-IF                                                       KDS08257
      *    COMPUTE DELAY-AMT = 3                                        KDS08257
      *    CALL ILBOWAT0 USING DELAY-AMT                                KDS08257
           EXIT.                                                        KDS08257
       A300-CLOSE-FILES-JOBSUB.                                         KDS08257
           CLOSE JOBSUB.                                                KDS08257
           SET OPEN-JOBSUB-NO   TO TRUE                                 KDS08257
           EXIT.                                                        KDS08257
       A300-STOP-RUN.                                                   KDS08257
           MOVE WS-RETURN-CD      TO RETURN-CODE                        KDS08257
TSTKDS*    DISPLAY 'JCSBMAST END  WS-RETURN-CD=' WS-RETURN-CD           KDS08257
           GOBACK.                                                      KDS08257
           EXIT.                                                        KDS08257
       A500-OPEN-JOBSUB.                                                KDS08257
           PERFORM T00-OPEN-O-JOBSUB.                                   KDS08257
           EXIT.                                                        KDS08257
       F100-OPEN-FILES.                                                 KDS08257
           PERFORM T00-OPEN-O-JOBSUB.                                   KDS08257
           PERFORM T00-OPEN-I-JCSFMAST.                                 KDS08257
           PERFORM T00-OPEN-I-JCSFJOBH.                                 KDS08257
           PERFORM T00-OPEN-I-DATECNTL.                                 KDS08257
           EXIT.                                                        KDS08257
       F200-CLOSE-FILES.                                                KDS08257
           IF OPEN-JCSFMAST-OK                                          KDS08257
              CLOSE JCSFMAST                                            KDS08257
              SET OPEN-JCSFMAST-NO   TO TRUE                            KDS08257
           END-IF                                                       KDS08257
           IF OPEN-JCSFJOBH-OK                                          KDS08257
              CLOSE JCSFJOBH                                            KDS08257
              SET OPEN-JCSFJOBH-NO   TO TRUE                            KDS08257
           END-IF                                                       KDS08257
           IF OPEN-JOBSUB-OK                                            KDS08257
              CLOSE JOBSUB                                              KDS08257
              SET OPEN-JOBSUB-NO     TO TRUE                            KDS08257
           END-IF                                                       KDS08257
           EXIT.                                                        KDS08257
       T00-OPEN-O-JOBSUB.                                               KDS08257
           COPY JCSCWK09.                                               KDS08257
       T00-OPEN-I-JCSFMAST.                                             KDS08257
           COPY JCSCWK12.                                               KDS08257
       T00-OPEN-I-JCSFJOBH.                                             KDS08257
           COPY JCSCWK11.                                               KDS08257
       T00-OPEN-I-DATECNTL.                                             KDS08257
           COPY JCSCWK10.                                               KDS08257
      *================================================================*KDS08257
      *           1         2         3         4         5         6123KDS08257
      ***123456789012345678901234567890123456789012345678901234567890123KDS08257
      *================================================================*KDS08257
