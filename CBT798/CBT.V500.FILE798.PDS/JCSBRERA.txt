      *================================================================*KDS08257
      *           1         2         3         4         5         6123KDS08257
      ***123456789012345678901234567890123456789012345678901234567890123KDS08257
      *================================================================*KDS08257
      * KKKKKKKKKKKKK   DDDDDDDD    SSSSSSSS                           *KDS08257
      * KKKKKKKKKKKKK  DDDDDDDDD   SSSSSSSSS                           *KDS08257
      *      KKKK      DDDD        SSSS                                *KDS08257
      *      KKKK      DDDD        SSSS                                *KDS08257
      *      KKKK      DDDD        SSSS          1111          0000000 *KDS08257
      *      KKKK      DDDDDDDD    SSSSSSSS      1111        0000000000*KDS08257
      *      KKKK        DDDDDDD    SSSSSSSS     1111        0000  0000*KDS08257
      *      KKKK           DDDD        SSSS    1111         0000  0000*KDS08257
      *      KKKK           DDDD        SSSS    1111        0000  0000 *KDS08257
      *      KKKK           DDDD        SSSS    1111        0000  0000 *KDS08257
      * KKK  KKKK           DDDD        SSSS   1111   0000  0000  0000 *KDS08257
      * KKKKKKKK      DDDDDDDDDD   SSSSSSSSS   1111  0000  0000000000  *KDS08257
      * KKKKKKKK      DDDDDDDDD    SSSSSSSS    1111 0000    000000000  *KDS08257
      *================================================================*KDS08257
      * Change Log                                                     *KDS08257
      *================================================================*KDS08257
      * KDS08257    Kang Dengsong       INITIAL VERSION                *KDS08257
      *================================================================*KDS08257
       IDENTIFICATION DIVISION.                                         KDS08257
       PROGRAM-ID. JCSBRERA.                                            KDS08257
       AUTHOR. KANG DENGSONG                                            KDS08257
       DATE-WRITTEN.  SEP 15,2008                                       KDS08257
      ******************************************************************KDS08257
      * CREATED   BY :KANG DENGSONG                                    *KDS08257
      * CREATED DATE :SEP 13, 2008                                     *KDS08257
      * REMARK       :RERUN IF ABEND                                   *KDS08257
      *              :JOB SCHEDULER SYSTEM                             *KDS08257
      *================================================================*KDS08257
      *   This program is distributed without warranty of any kind,    *KDS08257
      * including  (without limitation) warranties of merchantability  *KDS08257
      * and of fitness for a particular purpose. It is up to the user  *KDS08257
      * to make his own determination of the usefulness of this program*KDS08257
      * in his own system.                                             *KDS08257
      *================================================================*KDS08257
      *                                                                *KDS08257
      *--------------------*                                            KDS08257
       ENVIRONMENT DIVISION.                                            KDS08257
      *--------------------*                                            KDS08257
       CONFIGURATION SECTION.                                           KDS08257
       SOURCE-COMPUTER. IBM-ES9000.                                     KDS08257
       OBJECT-COMPUTER. IBM-ES9000.                                     KDS08257
       INPUT-OUTPUT SECTION.                                            KDS08257
       FILE-CONTROL.                                                    KDS08257
           SELECT JOBSUB    ASSIGN         TO JOBSUB                    KDS08257
                            ORGANIZATION   IS SEQUENTIAL                KDS08257
                            ACCESS MODE    IS SEQUENTIAL                KDS08257
                            FILE STATUS    IS WS-JOBSUB-STS.            KDS08257
           SELECT JCSFMAST  ASSIGN         TO JCSFAPSB                  KDS08257
                            FILE STATUS    IS WS-JCSFMAST-STS           KDS08257
                            ACCESS MODE    IS RANDOM                    KDS08257
                            ORGANIZATION   IS INDEXED                   KDS08257
                            RECORD KEY     IS JCBMAST-KEY.              KDS08257
           SELECT JCSFSTEP  ASSIGN         TO JCSFSTEP                  KDS08257
                            ACCESS MODE    IS RANDOM                    KDS08257
                            ORGANIZATION   IS INDEXED                   KDS08257
                            RECORD KEY     IS JCBST-KEY                 KDS08257
                            FILE STATUS    IS WS-JCSFSTEP-STS.          KDS08257
           SELECT JCSFJOBH  ASSIGN         TO JCSFJOBH                  KDS08257
                            FILE STATUS    IS WS-JCSFJOBH-STS           KDS08257
                            ACCESS MODE    IS SEQUENTIAL                KDS08257
                            ORGANIZATION   IS INDEXED                   KDS08257
                            RECORD KEY     IS JCBHD-SEQ.                KDS08257
       DATA DIVISION.                                                   KDS08257
       FILE SECTION.                                                    KDS08257
       FD JOBSUB.                                                       KDS08257
       01 JOBSUB-REC                     PIC   X(80).                   KDS08257
       FD JCSFMAST.                                                     KDS08257
       01 JCBMAST-REC.                                                  KDS08257
           COPY JCSCMAST  REPLACING ==:JCBMAST:== BY ==JCBMAST==.       KDS08257
       FD JCSFSTEP.                                                     KDS08257
           COPY JCSCJBST.                                               KDS08257
       FD JCSFJOBH.                                                     KDS08257
           COPY JCSCJOBH.                                               KDS08257
      ******************************************************************KDS08257
       WORKING-STORAGE SECTION.                                         KDS08257
       COPY JCSCWK00.                                                   KDS08257
       COPY JCSCWK02.                                                   KDS08257
       COPY JCSCWK03.                                                   KDS08257
       01 WS-PROCSTEP                        PIC X(08) VALUE SPACE.     KDS08257
       LINKAGE SECTION.                                                 KDS08257
       01 PARM-TO-MAIN.                                                 KDS08257
          03 PARM-FROM-JCL.                                             KDS08257
             04 PARM-LENGTH                  PIC S9(4) COMP.            KDS08257
             04 PARM-CONTENT.                                           KDS08257
                05 PRM-SYSID                 PIC X(8).                  KDS08257
                05 PRM-PROC                  PIC X(8).                  KDS08257
                05 FILLER                    PIC X(8).                  KDS08257
          03 PARM-FROM-PGM REDEFINES PARM-FROM-JCL.                     KDS08257
             05 PRM-IND                      PIC X(1).                  KDS08257
             05 PRM-SYSID-PGM                PIC X(8).                  KDS08257
             05 PRM-PROC-PGM                 PIC X(8).                  KDS08257
             05 PRM-FROM-PGM                 PIC X(8).                  KDS08257
       PROCEDURE DIVISION USING PARM-TO-MAIN.                           KDS08257
       MAIN SECTION.                                                    KDS08257
TSTKDS*    DISPLAY 'JCSBRERA BEGIN PARM-TO-MAIN=' PARM-FROM-PGM         KDS08257
           IF PARM-LENGTH NOT = 0                                       KDS08257
              ACCEPT WS-CUR-TIME FROM TIME                              KDS08257
              ACCEPT WS-YYYYMMDD FROM DATE YYYYMMDD                     KDS08257
              COPY JCSCWK05.                                            KDS08257
              PERFORM A000-INITIALIZE                                   KDS08257
              PERFORM D001-READ-JCSFMAST                                KDS08257
              PERFORM D002-PROCESS-JCSFJOBH                             KDS08257
              PERFORM D005-PREPARE-SUBJCL                               KDS08257
              PERFORM F200-CLOSE-FILES                                  KDS08257
           ELSE                                                         KDS08257
              DISPLAY 'NO JOB TO PROCESS OR PARM IS INVALID'            KDS08257
           END-IF                                                       KDS08257
           PERFORM F200-CLOSE-FILES                                     KDS08257
           PERFORM A300-STOP-RUN                                        KDS08257
           EXIT.                                                        KDS08257
       A000-INITIALIZE.                                                 KDS08257
           PERFORM F100-OPEN-FILES                                      KDS08257
           EXIT.                                                        KDS08257
       D001-READ-JCSFMAST.                                              KDS08257
           IF PRM-IND = WS-YES
              MOVE PRM-PROC-PGM          TO JCBMAST-JOB                 KDS08257
              MOVE PRM-SYSID-PGM         TO JCBMAST-AP-SYSID            KDS08257
           ELSE                                                         KDS08257
              MOVE PRM-PROC              TO JCBMAST-JOB                 KDS08257
              MOVE PRM-SYSID             TO JCBMAST-AP-SYSID            KDS08257
           END-IF                                                       KDS08257
           READ JCSFMAST KEY IS JCBMAST-KEY                             KDS08257
           IF WS-JCSFMAST-STS NOT = NORMAL AND                          KDS08257
              WS-JCSFMAST-STS NOT = END-FILE AND                        KDS08257
              WS-JCSFMAST-STS NOT = END-FILE2 AND                       KDS08257
              WS-JCSFMAST-STS NOT = LENGTHERR                           KDS08257
           THEN                                                         KDS08257
              DISPLAY 'READ FILE JCSFMAST ERROR CODE=' WS-JCSFMAST-STS  KDS08257
              MOVE  ERROR8              TO WS-RETURN-CD                 KDS08257
              PERFORM F200-CLOSE-FILES                                  KDS08257
              PERFORM A300-STOP-RUN                                     KDS08257
           ELSE                                                         KDS08257
              IF WS-JCSFMAST-STS = END-FILE OR END-FILE2                KDS08257
                 DISPLAY 'READ FILE NOT FOUND KEY IS= ' JCBMAST-JOB     KDS08257
                 MOVE  10               TO WS-RETURN-CD                 KDS08257
                 PERFORM A300-STOP-RUN                                  KDS08257
              END-IF                                                    KDS08257
           END-IF                                                       KDS08257
           IF JCBMAST-JOB-STATUS NOT = A AND PRM-IND NOT = WS-YES       KDS08257
              DISPLAY 'NO NEED TO RERUN'                                KDS08257
              MOVE  ERROR8               TO WS-RETURN-CD                KDS08257
              PERFORM F200-CLOSE-FILES                                  KDS08257
              PERFORM A300-STOP-RUN                                     KDS08257
           END-IF                                                       KDS08257
           MOVE JCBMAST-JOB             TO JCBST-PROC                   KDS08257
           MOVE JCBMAST-ERROR-STEP      TO JCBST-STEPNAME               KDS08257
           READ JCSFSTEP KEY IS JCBST-KEY                               KDS08257
           IF WS-JCSFSTEP-STS NOT = NORMAL AND                          KDS08257
              WS-JCSFSTEP-STS NOT = END-FILE AND                        KDS08257
              WS-JCSFSTEP-STS NOT = END-FILE2 AND                       KDS08257
              WS-JCSFSTEP-STS NOT = LENGTHERR                           KDS08257
           THEN                                                         KDS08257
              DISPLAY 'READ FILE JCSFSTEP ERROR CODE=' WS-JCSFSTEP-STS  KDS08257
              MOVE  ERROR8               TO WS-RETURN-CD                KDS08257
              PERFORM F200-CLOSE-FILES                                  KDS08257
              PERFORM A300-STOP-RUN                                     KDS08257
           ELSE                                                         KDS08257
              IF WS-JCSFSTEP-STS = END-FILE OR                          KDS08257
                 WS-JCSFSTEP-STS = END-FILE2                            KDS08257
              THEN                                                      KDS08257
                 DISPLAY 'NOT FOUND ERROR ST STEP'                      KDS08257
                 MOVE JCBMAST-ERROR-STEP TO WS-RERUN-STEP               KDS08257
              ELSE                                                      KDS08257
                 DISPLAY 'FOUND ERROR ST STEP=' JCBST-ERR-ST-STEP       KDS08257
                 MOVE JCBST-ERR-ST-STEP  TO WS-RERUN-STEP               KDS08257
              END-IF                                                    KDS08257
           END-IF                                                       KDS08257
           EXIT.                                                        KDS08257
       D002-PROCESS-JCSFJOBH.                                           KDS08257
           PERFORM D003-READ-JCSFJOBH                                   KDS08257
           PERFORM UNTIL WS-JCSFJOBH-STS = END-FILE                     KDS08257
              COMPUTE WS-I = WS-I + 1                                   KDS08257
              IF (LENGTH OF WS-JOB-HEAD / LENGTH80) >= WS-I             KDS08257
                 MOVE JCBHD-CONTENT      TO WS-HEAD-JCL(WS-I)           KDS08257
              END-IF                                                    KDS08257
              PERFORM D003-READ-JCSFJOBH                                KDS08257
           END-PERFORM                                                  KDS08257
           MOVE WS-I                     TO WS-JOB-HEAD-CNT             KDS08257
           IF WS-JOB-HEAD-CNT > LENGTH OF WS-JOB-HEAD / LENGTH80        KDS08257
              DISPLAY 'THE JOB HEAD''S NO IS ERROR'                     KDS08257
              MOVE 9                     TO WS-RETURN-CD                KDS08257
              PERFORM F200-CLOSE-FILES                                  KDS08257
              PERFORM A300-STOP-RUN                                     KDS08257
           END-IF                                                       KDS08257
           EXIT.                                                        KDS08257
       D003-READ-JCSFJOBH.                                              KDS08257
           READ JCSFJOBH                                                KDS08257
           IF WS-JCSFJOBH-STS NOT = NORMAL AND                          KDS08257
              WS-JCSFJOBH-STS NOT = END-FILE                            KDS08257
              DISPLAY 'READ FILE JCSFJOBH ERROR CODE=' WS-JCSFJOBH-STS  KDS08257
              MOVE  ERROR8                   TO WS-RETURN-CD            KDS08257
              PERFORM F200-CLOSE-FILES                                  KDS08257
              PERFORM A300-STOP-RUN                                     KDS08257
           END-IF                                                       KDS08257
           EXIT.                                                        KDS08257
       D005-PREPARE-SUBJCL.                                             KDS08257
           ACCEPT WS-CUR-TIME FROM TIME                                 KDS08257
           MOVE JCBMAST-JOB                 TO WS-HEAD-JCL(1)(3:8)      KDS08257
           MOVE JCBMAST-JOB                 TO WS-HEAD-JCL(1)(35:8)     KDS08257
           MOVE JCBMAST-JOB                 TO WS-HEAD-JCL(11)(38:8)    KDS08257
           IF PRM-FROM-PGM NOT = SPACE AND WS-RERUN-STEP = SPACE        KDS08257
              MOVE PRM-FROM-PGM             TO WS-RERUN-STEP            KDS08257
           END-IF                                                       KDS08257
           IF WS-RERUN-STEP = JCSPBGAP OR JCSPWARA OR JCSPWELA          KDS08257
                              OR JCSPABDA                               KDS08257
              IF WS-RERUN-STEP = JCSPBGAP                               KDS08257
                 MOVE SPACE             TO WS-STRING                    KDS08257
              ELSE                                                      KDS08257
                 MOVE WS-RERUN-STEP     TO WS-PROCSTEP                  KDS08257
                 STRING ',RESTART=' WS-PROCSTEP '.'                     KDS08257
                     WS-RERUN-STEP DELIMITED BY SPACE                   KDS08257
                                        INTO WS-STRING                  KDS08257
                 END-STRING                                             KDS08257
              END-IF                                                    KDS08257
           ELSE                                                         KDS08257
              MOVE JCBMAST-JOB          TO WS-PROCSTEP                  KDS08257
              STRING ',RESTART=' WS-PROCSTEP '.'                        KDS08257
                  WS-RERUN-STEP  DELIMITED BY SPACE                     KDS08257
                                     INTO WS-STRING                     KDS08257
              END-STRING                                                KDS08257
           END-IF                                                       KDS08257
           MOVE WS-STRING                   TO WS-HEAD-JCL(7)(25:50)    KDS08257
           MOVE '//*'                       TO WS-HEAD-JCL(18)          KDS08257
           MOVE JCBMAST-JOB                 TO WS-FRAME-JCL(3)(3:8)     KDS08257
           MOVE JCBMAST-JOB                 TO WS-FRAME-JCL(3)(18:50)   KDS08257
           MOVE JCBMAST-MAX-ACEPT-RC        TO WS-FRAME-JCL(4)(20:2)    KDS08257
           MOVE JCBMAST-MAX-ACEPT-RC        TO WS-FRAME-JCL(10)(22:2)   KDS08257
           MOVE JCBMAST-MAX-ACEPT-RC        TO WS-FRAME-JCL(17)(22:2)   KDS08257
           MOVE ''''                        TO WS-PRM-BEGIN             KDS08257
           MOVE ''''                        TO WS-PRM-END               KDS08257
           IF PRM-IND = WS-YES
              MOVE PRM-PROC-PGM             TO WS-PRM-PROC              KDS08257
              MOVE PRM-SYSID-PGM            TO WS-PRM-SYSID             KDS08257
           ELSE                                                         KDS08257
              MOVE PRM-PROC                 TO WS-PRM-PROC              KDS08257
              MOVE PRM-SYSID                TO WS-PRM-SYSID             KDS08257
           END-IF                                                       KDS08257
           MOVE OK                          TO WS-PRM-ACEPT-RC          KDS08257
           MOVE SPACE                       TO WS-PRM-MOD               KDS08257
           MOVE JCBMAST-SKIP-IND            TO WS-PRM-SKIP              KDS08257
           MOVE WS-PARM-TO-PROC             TO WS-FRAME-JCL(5)(32:)     KDS08257
           MOVE WARN                        TO WS-PRM-ACEPT-RC          KDS08257
           MOVE WS-PARM-TO-PROC             TO WS-FRAME-JCL(11)(32:)    KDS08257
           MOVE ZERO                        TO WS-PRM-ACEPT-RC          KDS08257
           MOVE WS-PARM-TO-PROC             TO WS-FRAME-JCL(19)(32:)    KDS08257
           MOVE WS-PARM-TO-PROC             TO WS-FRAME-JCL(2)(32:)     KDS08257
           ACCEPT WS-CUR-TIME FROM TIME                                 KDS08257
TSTKDS*    DISPLAY 'WRITE JOBSUB     ' WS-CUR-TIME                      KDS08257
           PERFORM VARYING WS-I FROM 1 BY 1                             KDS08257
              UNTIL WS-I > LENGTH OF WS-JOB-HEAD / LENGTH80             KDS08257
TSTKDS*       DISPLAY 'WS-HEAD-JCL(WS-I)=' WS-HEAD-JCL(WS-I)            KDS08257
              WRITE JOBSUB-REC FROM WS-HEAD-JCL(WS-I)                   KDS08257
              IF WS-JOBSUB-STS NOT = NORMAL                             KDS08257
                 DISPLAY 'WRITE JOBSUB ERROR CODE=' WS-JOBSUB-STS       KDS08257
                 MOVE  ERROR8   TO WS-RETURN-CD                         KDS08257
                 PERFORM A300-STOP-RUN                                  KDS08257
              END-IF                                                    KDS08257
           END-PERFORM                                                  KDS08257
           PERFORM VARYING WS-I FROM 1 BY 1                             KDS08257
              UNTIL WS-I > LENGTH OF WS-JOB-FRAME / LENGTH80            KDS08257
TSTKDS*       DISPLAY 'WS-FRAME-JCL(WS-I)=' WS-FRAME-JCL(WS-I)          KDS08257
              WRITE JOBSUB-REC FROM WS-FRAME-JCL(WS-I)                  KDS08257
              IF WS-JOBSUB-STS NOT = NORMAL                             KDS08257
                 DISPLAY 'WRITE JOBSUB ERROR CODE=' WS-JOBSUB-STS       KDS08257
                 MOVE  ERROR8   TO WS-RETURN-CD                         KDS08257
                 PERFORM A300-STOP-RUN                                  KDS08257
              END-IF                                                    KDS08257
           END-PERFORM                                                  KDS08257
           EXIT.                                                        KDS08257
       A300-STOP-RUN.                                                   KDS08257
           MOVE WS-RETURN-CD      TO RETURN-CODE                        KDS08257
TSTKDS*    DISPLAY 'JCSBRERA END WS-RETURN-CD=' WS-RETURN-CD            KDS08257
           GOBACK.                                                      KDS08257
      *COPY JCSCWK03.                                                   KDS08257
      *    EXIT.                                                        KDS08257
       F100-OPEN-FILES.                                                 KDS08257
           PERFORM T00-OPEN-I-JCSFMAST                                  KDS08257
           PERFORM T00-OPEN-I-JCSFJOBH                                  KDS08257
           PERFORM T00-OPEN-0-JOBSUB                                    KDS08257
           PERFORM T00-OPEN-I-JCSFSTEP                                  KDS08257
           EXIT.                                                        KDS08257
       F200-CLOSE-FILES.                                                KDS08257
           IF OPEN-JCSFMAST-OK                                          KDS08257
              CLOSE JCSFMAST                                            KDS08257
              SET OPEN-JCSFMAST-NO   TO TRUE                            KDS08257
           END-IF                                                       KDS08257
           IF OPEN-JCSFJOBH-OK                                          KDS08257
              CLOSE JCSFJOBH                                            KDS08257
              SET OPEN-JCSFJOBH-NO   TO TRUE                            KDS08257
           END-IF                                                       KDS08257
           IF OPEN-JCSFSTEP-OK                                          KDS08257
              CLOSE JCSFSTEP                                            KDS08257
              SET OPEN-JCSFSTEP-NO   TO TRUE                            KDS08257
           END-IF                                                       KDS08257
           IF OPEN-JOBSUB-OK                                            KDS08257
              CLOSE JOBSUB                                              KDS08257
              SET OPEN-JOBSUB-NO     TO TRUE                            KDS08257
           END-IF                                                       KDS08257
           EXIT.                                                        KDS08257
       T00-OPEN-0-JOBSUB.                                               KDS08257
           COPY JCSCWK09.                                               KDS08257
       T00-OPEN-I-JCSFMAST.                                             KDS08257
           COPY JCSCWK12.                                               KDS08257
       T00-OPEN-I-JCSFJOBH.                                             KDS08257
           COPY JCSCWK13.                                               KDS08257
       T00-OPEN-I-JCSFSTEP.                                             KDS08257
           COPY JCSCWK14.                                               KDS08257
      *================================================================*KDS08257
      *           1         2         3         4         5         6123KDS08257
      ***123456789012345678901234567890123456789012345678901234567890123KDS08257
      *================================================================*KDS08257
