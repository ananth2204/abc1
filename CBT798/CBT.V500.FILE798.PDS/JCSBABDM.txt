      *================================================================*KDS08257
      *           1         2         3         4         5         6123KDS08257
      ***123456789012345678901234567890123456789012345678901234567890123KDS08257
      *================================================================*KDS08257
      * KKKKKKKKKKKKK   DDDDDDDD    SSSSSSSS                           *KDS08257
      * KKKKKKKKKKKKK  DDDDDDDDD   SSSSSSSSS                           *KDS08257
      *      KKKK      DDDD        SSSS                                *KDS08257
      *      KKKK      DDDD        SSSS                                *KDS08257
      *      KKKK      DDDD        SSSS          1111          0000000 *KDS08257
      *      KKKK      DDDDDDDD    SSSSSSSS      1111        0000000000*KDS08257
      *      KKKK        DDDDDDD    SSSSSSSS     1111        0000  0000*KDS08257
      *      KKKK           DDDD        SSSS    1111         0000  0000*KDS08257
      *      KKKK           DDDD        SSSS    1111        0000  0000 *KDS08257
      *      KKKK           DDDD        SSSS    1111        0000  0000 *KDS08257
      * KKK  KKKK           DDDD        SSSS   1111   0000  0000  0000 *KDS08257
      * KKKKKKKK      DDDDDDDDDD   SSSSSSSSS   1111  0000  0000000000  *KDS08257
      * KKKKKKKK      DDDDDDDDD    SSSSSSSS    1111 0000    000000000  *KDS08257
      *================================================================*KDS08257
      * Change Log                                                     *KDS08257
      *================================================================*KDS08257
      * KDS08257    Kang Dengsong       INITIAL VERSION                *KDS08257
      *================================================================*KDS08257
       IDENTIFICATION DIVISION.                                         KDS08257
       PROGRAM-ID. JCSBABDM.                                            KDS08257
       AUTHOR. KANG DENGSONG                                            KDS08257
       DATE-WRITTEN.  SEP 15,2008                                       KDS08257
      ******************************************************************KDS08257
      * CREATED   BY :KANG DENGSONG                                    *KDS08257
      * CREATED DATE :SEP 13, 2008                                     *KDS08257
      * REMARK       :UPDATE MAST FILE IF ABEND                        *KDS08257
      *              :JOB SCHEDULER SYSTEM                             *KDS08257
      *================================================================*KDS08257
      *   This program is distributed without warranty of any kind,    *KDS08257
      * including  (without limitation) warranties of merchantability  *KDS08257
      * and of fitness for a particular purpose. It is up to the user  *KDS08257
      * to make his own determination of the usefulness of this program*KDS08257
      * in his own system.                                             *KDS08257
      *================================================================*KDS08257
      *                                                                *KDS08257
      *--------------------*                                            KDS08257
       ENVIRONMENT DIVISION.                                            KDS08257
      *--------------------*                                            KDS08257
       CONFIGURATION SECTION.                                           KDS08257
       SOURCE-COMPUTER. IBM-ES9000.                                     KDS08257
       OBJECT-COMPUTER. IBM-ES9000.                                     KDS08257
       INPUT-OUTPUT SECTION.                                            KDS08257
       FILE-CONTROL.                                                    KDS08257
           SELECT JCSFMAST  ASSIGN         TO JCSFMAST                  KDS08257
                            FILE STATUS    IS WS-JCSFMAST-STS           KDS08257
                            ACCESS MODE    IS RANDOM                    KDS08257
                            ORGANIZATION   IS INDEXED                   KDS08257
                            RECORD KEY     IS JCBMAST-KEY.              KDS08257
           SELECT JCSFSPRC  ASSIGN         TO JCSFSPRC                  KDS08257
                            ACCESS MODE    IS SEQUENTIAL                KDS08257
                            ORGANIZATION   IS SEQUENTIAL                KDS08257
                            FILE STATUS    IS WS-JCSFSPRC-STS.          KDS08257
           SELECT JCSFDTTM  ASSIGN         TO JCSFDTTM                  KDS08257
                            FILE STATUS    IS WS-JCSFDTTM-STS           KDS08257
                            ACCESS MODE    IS SEQUENTIAL                KDS08257
                            ORGANIZATION   IS SEQUENTIAL.               KDS08257
       DATA DIVISION.                                                   KDS08257
       FILE SECTION.                                                    KDS08257
       FD JCSFMAST.                                                     KDS08257
       01 JCBMAST-REC.                                                  KDS08257
           COPY JCSCMAST  REPLACING ==:JCBMAST:== BY ==JCBMAST==.       KDS08257
       FD JCSFSPRC.                                                     KDS08257
       01 JCSFSPRC-REC.                                                 KDS08257
          03 JCSFSPRC-STEP                   PIC X(08).                 KDS08257
          03 JCSFSPRC-RC                     PIC X(04).                 KDS08257
          03 FILLER                          PIC X(108).                KDS08257
       FD JCSFDTTM.                                                     KDS08257
       01 JCBDTTM-REC.                                                  KDS08257
          03 JCBDTTM-DTE                    PIC X(08).                  KDS08257
          03 JCBDTTM-TME                    PIC X(08).                  KDS08257
          03 JCBDTTM-RMD                    PIC X(64).                  KDS08257
      ******************************************************************KDS08257
       WORKING-STORAGE SECTION.                                         KDS08257
       COPY JCSCWK02.                                                   KDS08257
       COPY JCSCWK00.                                                   KDS08257
       01 WS-RESP-2               PIC X(1) VALUE SPACE.                 KDS08257
       01 PARM-FROM-PGM.                                                KDS08257
          05 PRM-IND                      PIC X(1).                     KDS08257
          05 PRM-SYSID-PGM                PIC X(8).                     KDS08257
          05 PRM-PROC-PGM                 PIC X(8).                     KDS08257
          05 PRM-FROM-PGM                 PIC X(8).                     KDS08257
          05 PRM-RERUN-PGM                PIC 9(2).                     KDS08257
                                                                        KDS08257
       LINKAGE SECTION.                                                 KDS08257
       01 PARM-TO-MAIN.                                                 KDS08257
          03 PARM-LENGTH          PIC S9(4) COMP.                       KDS08257
          03 PARM-CONTENT.                                              KDS08257
             05 PRM-AP-SYSID                 PIC X(8).                  KDS08257
             05 PRM-PROC                     PIC X(8).                  KDS08257
             05 PRM-ACEPT-RC                 PIC 9(2).                  KDS08257
             05 PRM-MOD                      PIC X(1).                  KDS08257
             05 PRM-SKIP                     PIC X(1).                  KDS08257
             05 PRM-FLG                      PIC X(1).                  KDS08257
             05 PRM-RERUN-CNT                PIC 9(2).                  KDS08257
      *                                                                 KDS08257
       PROCEDURE DIVISION USING PARM-TO-MAIN.                           KDS08257
       MAIN SECTION.                                                    KDS08257
           DISPLAY 'JCSBABDM BEGIN '                                    KDS08257
           IF PARM-LENGTH NOT = 0                                       KDS08257
              ACCEPT WS-CUR-TIME FROM TIME                              KDS08257
              ACCEPT WS-YYYYMMDD FROM DATE YYYYMMDD                     KDS08257
              COPY JCSCWK05.                                            KDS08257
              PERFORM A000-INITIALIZE                                   KDS08257
              PERFORM D004-READ-JCSFMAST                                KDS08257
              PERFORM F200-CLOSE-FILES                                  KDS08257
           ELSE                                                         KDS08257
              DISPLAY 'NO JOB TO PROCESS OR PARM IS INVALID'            KDS08257
           END-IF                                                       KDS08257
           PERFORM A300-STOP-RUN                                        KDS08257
           EXIT.                                                        KDS08257
       A000-INITIALIZE.                                                 KDS08257
           PERFORM F100-OPEN-FILES                                      KDS08257
           READ JCSFSPRC                                                KDS08257
           IF WS-JCSFSPRC-STS NOT = NORMAL                              KDS08257
              DISPLAY 'READ FILE JCSFSPRC ERROR CODE=' WS-JCSFSPRC-STS  KDS08257
              MOVE  ERROR8              TO WS-RETURN-CD                 KDS08257
              PERFORM A300-STOP-RUN                                     KDS08257
           END-IF                                                       KDS08257
TSTKDS*    MOVE WS-YES              TO WS-RESP                          KDS08257
TSTKDS*    DISPLAY 'JCSFSPRC-STEP=' JCSFSPRC-STEP ':RC=' JCSFSPRC-RC    KDS08257
           IF (JCSFSPRC-STEP = JCSPBEGN OR JCSPWARM OR JCSPWELM)        KDS08257
              AND JCSFSPRC-RC = K0093                                   KDS08257
           THEN                                                         KDS08257
              MOVE R                       TO WS-RESP-2                 KDS08257
           ELSE                                                         KDS08257
              IF PRM-SKIP = WS-YES                                      KDS08257
TSTKDS*          MOVE K                TO WS-RESP-2                     KDS08257
                 PERFORM UNTIL WS-RESP-2 = K OR P                       KDS08257
                    DISPLAY PRM-PROC                                    KDS08257
                             ' **** ABEND ****, SKIP OR STOP(K/P)?'     KDS08257
                              UPON CONSOLE                              KDS08257
                    ACCEPT WS-RESP-2 FROM CONSOLE                       KDS08257
                 END-PERFORM                                            KDS08257
              ELSE                                                      KDS08257
TSTKDS*          MOVE WS-YES           TO WS-RESP                       KDS08257
                 PERFORM UNTIL WS-RESP = WS-YES OR WS-NO                KDS08257
                    DISPLAY PRM-PROC                                    KDS08257
                           ' **** ABEND ****, STOP?(Y/N)'               KDS08257
                          UPON CONSOLE                                  KDS08257
                    ACCEPT WS-RESP FROM CONSOLE                         KDS08257
                 END-PERFORM                                            KDS08257
              END-IF                                                    KDS08257
           END-IF                                                       KDS08257
           READ JCSFDTTM                                                KDS08257
           IF WS-JCSFDTTM-STS NOT = NORMAL                              KDS08257
              DISPLAY 'READ FILE JCSFDTTM ERROR CODE=' WS-JCSFDTTM-STS  KDS08257
              MOVE  ERROR8              TO WS-RETURN-CD                 KDS08257
              PERFORM A300-STOP-RUN                                     KDS08257
           END-IF                                                       KDS08257
           EXIT.                                                        KDS08257
       D004-READ-JCSFMAST.                                              KDS08257
           MOVE PRM-PROC                TO JCBMAST-JOB                  KDS08257
           MOVE PRM-AP-SYSID            TO JCBMAST-AP-SYSID             KDS08257
           READ JCSFMAST KEY IS JCBMAST-KEY                             KDS08257
           IF WS-JCSFMAST-STS NOT = NORMAL AND                          KDS08257
              WS-JCSFMAST-STS NOT = END-FILE AND                        KDS08257
              WS-JCSFMAST-STS NOT = END-FILE2                           KDS08257
              DISPLAY 'READ FILE JCSFMAST ERROR CODE=' WS-JCSFMAST-STS  KDS08257
              MOVE  ERROR8              TO WS-RETURN-CD                 KDS08257
              PERFORM A300-STOP-RUN                                     KDS08257
           ELSE                                                         KDS08257
              IF WS-JCSFMAST-STS = END-FILE OR END-FILE2                KDS08257
                 DISPLAY 'READ FILE NOT FOUND KEY IS= ' JCBMAST-JOB     KDS08257
                 MOVE  10               TO WS-RETURN-CD                 KDS08257
                 PERFORM A300-STOP-RUN                                  KDS08257
              END-IF                                                    KDS08257
           END-IF                                                       KDS08257
           MOVE JCBDTTM-DTE                    TO JCBMAST-EXEC-ST-DATE-9KDS08257
           MOVE JCBDTTM-TME                    TO JCBMAST-EXEC-ST-TIME-9KDS08257
           MOVE WS-YYYYMMDD                    TO JCBMAST-EXEC-ED-DATE  KDS08257
           MOVE WS-CUR-TIME                    TO JCBMAST-EXEC-ED-TIME  KDS08257
           IF WS-RESP-2 = K                                             KDS08257
              MOVE WS-SKIP                     TO JCBMAST-EXEC-RC-LAST-CKDS08257
              MOVE K                           TO JCBMAST-JOB-STATUS    KDS08257
           ELSE                                                         KDS08257
              MOVE ABEND                       TO JCBMAST-EXEC-RC-LAST-CKDS08257
              MOVE A                           TO JCBMAST-JOB-STATUS    KDS08257
           END-IF                                                       KDS08257
           MOVE JCSFSPRC-STEP                  TO JCBMAST-ERROR-STEP    KDS08257
           MOVE JCSFSPRC-RC                    TO JCBMAST-ERROR-CODE    KDS08257
           MOVE PRM-MOD                        TO JCBMAST-EXEC-MODE     KDS08257
           MOVE WS-NO                          TO JCBMAST-RECOVER-STS   KDS08257
           COMPUTE WS-DAYS     =                                        KDS08257
             FUNCTION INTEGER-OF-DATE(JCBMAST-EXEC-ED-DATE-9) -         KDS08257
             FUNCTION INTEGER-OF-DATE(JCBMAST-EXEC-ST-DATE-9)           KDS08257
           COMPUTE JCBMAST-COST-TIME = WS-DAYS * 3600 * 24 +            KDS08257
                (JCBMAST-EXEC-ED-TIME-HH - JCBMAST-EXEC-ST-TIME-HH)     KDS08257
                + 60 *                                                  KDS08257
                (JCBMAST-EXEC-ED-TIME-MM - JCBMAST-EXEC-ST-TIME-MM)     KDS08257
                +                                                       KDS08257
                (JCBMAST-EXEC-ED-TIME-SS - JCBMAST-EXEC-ST-TIME-SS)     KDS08257
                +                                                       KDS08257
                (JCBMAST-EXEC-ED-TIME-MS - JCBMAST-EXEC-ST-TIME-MS)     KDS08257
                / 100                                                   KDS08257
           REWRITE JCBMAST-REC                                          KDS08257
           IF WS-JCSFMAST-STS NOT = NORMAL                              KDS08257
              DISPLAY 'REWRITE JCSFMAST ERROR CODE=' WS-JCSFMAST-STS    KDS08257
              MOVE     ERROR8   TO WS-RETURN-CD                         KDS08257
              PERFORM A300-STOP-RUN                                     KDS08257
           END-IF                                                       KDS08257
           EXIT.                                                        KDS08257
       A300-STOP-RUN.                                                   KDS08257
           MOVE WS-RETURN-CD      TO RETURN-CODE                        KDS08257
           DISPLAY 'JCSBABDM END =' WS-RETURN-CD                        KDS08257
           GOBACK.                                                      KDS08257
           EXIT.                                                        KDS08257
       F100-OPEN-FILES.                                                 KDS08257
           PERFORM T00-OPEN-IO-JCSFMAST                                 KDS08257
           PERFORM T00-OPEN-I-JCSFSPRC                                  KDS08257
           PERFORM T00-OPEN-I-JCSFDTTM                                  KDS08257
           EXIT.                                                        KDS08257
       F200-CLOSE-FILES.                                                KDS08257
           IF OPEN-JCSFMAST-OK                                          KDS08257
              CLOSE JCSFMAST                                            KDS08257
              SET OPEN-JCSFMAST-NO   TO TRUE                            KDS08257
           END-IF                                                       KDS08257
           IF OPEN-JCSFSPRC-OK                                          KDS08257
              CLOSE JCSFSPRC                                            KDS08257
              SET OPEN-JCSFSPRC-NO   TO TRUE                            KDS08257
           END-IF                                                       KDS08257
           IF OPEN-JCSFDTTM-OK                                          KDS08257
              CLOSE JCSFDTTM                                            KDS08257
              SET OPEN-JCSFDTTM-NO   TO TRUE                            KDS08257
           END-IF                                                       KDS08257
           EXIT.                                                        KDS08257
       T00-OPEN-IO-JCSFMAST.                                            KDS08257
           COPY JCSCWK06.                                               KDS08257
       T00-OPEN-I-JCSFSPRC.                                             KDS08257
           COPY JCSCWK07.                                               KDS08257
       T00-OPEN-I-JCSFDTTM.                                             KDS08257
           COPY JCSCWK08.                                               KDS08257
      *================================================================*KDS08257
      *           1         2         3         4         5         6123KDS08257
      ***123456789012345678901234567890123456789012345678901234567890123KDS08257
      *================================================================*KDS08257
