Extending NetView commands

Having found the article VTAM extended display command in
the December 1991 issue useful, I have extended the idea to
allow a wide range of NetView commands to be issued against
a group of terminals.

Usage of the GROUP command is explained in the help panel
below. It is not essential to enter the lower terminal address
first. The terminal-ids specified to this command must be
between 3 and 8 characters in length, the last 3 characters
being a hexadecimal address. Different terminal-id structures
can be catered for by adjusting the HEXLEN variable.
Messages that result from the command being issued against a
non-existent terminal are suppressed.


GROUP REXX EXEC

/**********************************************************************/
/* GROUP      :- REXX EXEC issue commands to groups of terminals.     */
/**********************************************************************/
/* Initialize error handling.                                          /
/**********************************************************************/
Trace E
HEXLEN = 3
/**********************************************************************/
/* Get input variables (P1 - low addr / P2 - high addr).              */
/**********************************************************************/
COMMAND = MSGVAR(1)
P1 = MSGVAR(2)
P2 = MSGVAR(3)
/**********************************************************************/
/* Checks for valid input variables.                                  */
/**********************************************************************/
Select
   When COMMAND = '?' then SIGNAL HELP
   When P1 = '' then SIGNAL HELP
   When P2 = '' then SIGNAL HELP
   When Length(P1) > 8 then
      Do
         Say "Length of name 1 ('P1') > 8 chars. - GROUP terminating.'
         Exit
      End
   When Length(P1) < 3 then
      Do
         Say 'Length of name 1 ('P1') < 3 chars. - GROUP terminating.'
         Exit
      End
   When Length(P2) > 8 then
      Do
         Say 'Length of name 2 ('P2') > 8 chars. - GROUP terminating.'
         Exit
      End
   When Length(P2) < 3 then
      Do
         Say 'Length of name 2 ('P2') < 3 chars. - GROUP terminating.'
         Exit
      End
   Otherwise
      Do
         ADDR = Length(P1) - (HEXLEN - 1)
         LOADD = Substr(P1,ADDR,HEXLEN)
         If Datatype(LOADD,'X') = 0 then
            Do
Say 'Unable to get address from name 1 ('P1') - GROUP terminating.'
               Exit
            End
         TERMID = Substr(P1,1,ADDR - 1)
         If length(P2) = HEXLEN then
            Do
               HIADD = P2
               If Datatype(HIADD,'X') = 0 then
                  Do
Say 'Unable to get address from name 2 ('P2') - GROUP terminating.'
                     Exit
                  End
            End
         Else
            Do
               HIADD = Substr(P2,ADDR,HEXLEN)
               If Datatype(HIADD,'X') = 0 then
                  Do
Say 'Unable to get address from name 2 ('P2') - GROUP terminating.'
                     Exit
                  End
               TERMID2 = Substr(P2,1,ADDR - 1)
               If TERMID Â¬= TERMID2 then
                  Do
 Say 'Mismatch in names ('TERMID'/'TERMID2') - GROUP terminating.'
                     Exit
                  End
            End
/**********************************************************************/
/* Issue command for each terminal address in the specified range     */
/* and trap error messages for all terminals that do not exist.       */
/**********************************************************************/
         LOADD = X2D(LOADD)
         HIADD = X2D(HIADD)
         COUNT = HIADD - LOADD
         If COUNT < 0 then
            Do
               TEMP = HIADD
               HIADD = LOADD
               LOADD = TEMP
               COUNT = HIADD - LOADD
            End
         'TRAP AND SUPPRESS MESSAGES IST453I IST097I'
         Do I = 0 to COUNT
            ADDR = D2X(LOADD + I,HEXLEN)
            COMMAND TERMID||ADDR
         End
         'TRAP NO MESSAGES'
         'FLUSHQ'
      End
End
Exit
HELP:
   HELP GROUP
Exit

John Cameron
Technical Specialist
The Life Association of Scotland (UK)     c Life Association of Scotland 1992

