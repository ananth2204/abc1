Displaying the Network Name Table


INTRODUCTION

When my company converted from 3725s to 3745s last year,
we encountered an interesting problem with a new control
block introduced at the NCP Version 5 level, the Network
Name Table (NNT). The available IBM documentation was
unclear on when we would require an entry in the NNT.
During the conversion process, we encountered an SNA sense
code of 0812000E, signifying insufficient space in the NNT.

The size of the NNT is determined by the NAMTAB operand
of the NCP BUILD macro. In order to determine exactly what
types of entry need to be stored in the NNT, we wrote the
CLIST listed below. The CLIST command should be entered
from a NetView console in the following format:

      NNT <ncpname>

where <ncpname> is the name of the NCP whose NNT is to be
interrogated. The command was developed under NetView
Version 1.3 and NCP Version 5.3 and was written in the
NetView CLIST language.

When an SSCP name needs to be added to the NNT and
insufficient table space is available, the above failed sense is
issued and no cross domain/network communications will be
possible with that SSCP. We have found this CLIST to be very
helpful in avoiding such situations.

NNT      CLIST
&CONTROL ERR
&IF &PARMCNT EQ 0 &THEN &GOTO -HELP
&IF .&1 EQ .? &THEN &GOTO -HELP
&GOTO -START
-HELP
&BEGWRITE -ENDHELP
********************************************************************
*    NETVIEW CLIST NNT:                                            *
*    DESCRIPTION:                                                  *
*THIS CLIST WILL LIST ALL THE ENTRIES IN THE NNT FOR A GIVEN NCP.  *
*COMMAND:                                                          *
* NNT    (NCPNAME)                                                 *
*EXAMPLE:                                                          *
* NNT     NCP21                                                    *
**-----------------------------------------*                       *
*|   NNT    |    NAME      |  NNT  | Usage |                       *
*|  NAME    |    TYPE      | FLAGS | Count |                       *
*|----------|--------------|-------|-------|                       *
*| NETA     | NETWORK NAME |  0010 |   16  |                       *
*| NETB     | NETWORK NAME |  0013 |   19  |                       *
*| NETC     | NETWORK NAME |  0005 |    5  |                       *
*| A01M     | SSCP/CP NAME |  4005 |    5  |                       *
**----------|--------------|-------|-------*                       *
*TOTAL #NNTS IN USE =                                              *
*TOTAL #NNTS AVAILABLE =      NNTAVENT                             *
*MINIMUM #NNTS AVAILABLE =    NNTMNAVE                             *
*TOTAL #NNTS DEFINED =        NNTENTRS                             *
*TOTAL #NETWORKS DEFINED =    NNTUSES0                             *
**** END OF NETWORK NAME TABLE *                                   *
*N N T   H E A D E R                                               *
*            NNTUSES0 - THE NUMBER OF NETWORKS DEFINED IN THIS NCP.*
*                       (THIS NUMBER INCLUDES THE NATIVE NETWORK.) *
*            NNTENTRS - THE NUMBER OF NNT ENTRIES DEFINED.THIS IS  *
*                       THE NAMTAB OPERAND OF THE NCP BUILD MACRO. *
*            NNTAVENT - THE NUMBER OF AVAILABLE NNT ENTRIES.       *
*            NNTMNAVE - THE MINIMUM NUMBER OF AVAILABLE NNT ENTRIES*
*            NNTLIUSE - THE INDEX OF THE LAST IN-USE NNT ENTRY.    *
*N N T   E N T R Y                                                 *
*NNT NAME    NNTNAME  - THIS IS THE NETWORK NAME                   *
*NAME TYPE   NNTNDATA - BYTE 0                                     *
*                       XX.. .... WHERE                            *
*                       00 = NETWORK IDENTIFIER                    *
*                       01 = CONTROL POINT NAME                    *
*USE COUNT   NNTNDATA   BYTE 1                                     *
*                       NUMBER OF USES OF THIS NAME                *
*    NOTE: FLAG VALUES ARE REPRESENTED IN HEX NOT DECIMAL.         *
********************************************************************
* HERE ARE THE EXPECTED MESSAGES FROM DISPLAY NCPSTOR:             *
*IST097I (5A97I) NCPSTOR ACCEPTED                                  *
*IST244I (5C44I) NCP STORAGE FOR ID = (NCP)                        *
*IST245I (5C45I) 000260 00000000 00000000 00000000 00000000        *
*IST314I (5D14I) END                                               *
*IST241I (5C41I) NCPSTOR COMMAND COMPLETE FOR ID = (NCP)           *
*            OR                                                    *
*IST242I (5C42I) NCPSTOR COMMAND FAILED FOR ID =(NCP) SENSE = 0806 *
*CONTROL BLOCKS                                                    *
*  (06E8) ---> XDA                      WORD DIRECT ADDRESSABLE    *
*  XDA + X'5C'(92) ---> HWX             HWE EXTENSION              *
*  HWX + X'6C'(108) ---> NNT            NETWORK NAMES TABLE        *
********************************************************************
-ENDHELP
&EXIT
-START
**********************************************************
* THIS CODE CHECKS TO SEE IF VTAM IS UP
**********************************************************
&IF .&VTAM NE . &THEN &GOTO -GOODY
MESSAGE 385I,NNT
&EXIT
-GOODY
&NCP = &1
-CHKDONE
*           SET UP VARIABLE TO CONVERT HEX TO DEC
&H1B0 = 0
&H1B  = 0
&H1B1 = 1
&H1B2 = 2
&H1B3 = 3
&H1B4 = 4
&H1B5 = 5
&H1B6 = 6
&H1B7 = 7
&H1B8 = 8
&H1B9 = 9
&H1BA = 10
&H1BB = 11
&H1BC = 12
&H1BD = 13
&H1BE = 14
&H1BF = 15
&H2B0 = 0
&H2B  = 0
&H2B1 = 16
&H2B2 = 32
&H2B3 = 48
&H2B4 = 64
&H2B5 = 80
&H2B6 = 96
&H2B7 = 112
&H2B8 = 128
&H2B9 = 144
&H2BA = 160
&H2BB = 176
&H2BC = 192
&H2BD = 208
&H2BE = 224
&H2BF = 240
&H3B  = 0
&H3B0 = 0
&H3B1 = 256
&H3B2 = 512
&H3B3 = 768
&H3B4 = 1024
&H3B5 = 1280
&H3B6 = 1536
&H3B7 = 1792
&H3B8 = 2048
&H3B9 = 2304
&H3BA = 2560
&H3BB = 2816
&H3BC = 3072
&H3BD = 3328
&H3BE = 3584
&H3BF = 3840
&H4B  = 0
&H4B0 = 0
&H4B1 = 4096
&H4B2 = 8192
&H4B3 = 12288
&H4B4 = 16384
&H4B5 = 20480
&H4B6 = 24576
&H4B7 = 28672
&H4B8 = 32768
&H4B9 = 36864
&H4BA = 40960
&H4BB = 45056
&H4BC = 49152
&H4BD = 53248
&H4BE = 57344
&H4BF = 61440
&H5B  = 0
&H5B0 = 0
&H5B1 = 65536
&H5B2 = 131072
&H5B3 = 196608
&H5B4 = 262144
&H5B5 = 327680
&H5B6 = 393216
&H5B7 = 458752
&H5B8 = 524288
&H5B9 = 589824
&H5BA = 655360
&H5BB = 720896
&H5BC = 786432
&H5BD = 851968
&H5BE = 917504
&H5BF = 983040
&H6B  = 0
&H6B0 = 0
&H6B1 = 1048576
&H6B2 = 2097152
&H6B3 = 3145728
&H6B4 = 4194304
&H6B5 = 5242880
&H6B6 = 6291456
&H6B7 = 7340032
&H6B8 = 8388608
&H6B9 = 9437184
&H6BA = 10485760
&H6BB = 11534336
&H6BC = 12582812
&H6BD = 13631488
&H6BE = 14680064
&H6BF = 15728640
*   TABLE TO CONVERT EBCDIC TO CHARACTERS
&CC1 = A
&CC2 = B
&CC3 = C
&CC4 = D
&CC5 = E
&CC6 = F
&CC7 = G
&CC8 = H
&CC9 = I
&CD1 = J
&CD2 = K
&CD3 = L
&CD4 = M
&CD5 = N
&CD6 = O
&CD7 = P
&CD8 = Q
&CD9 = R
&CE2 = S
&CE3 = T
&CE4 = U
&CE5 = V
&CE6 = W
&CE7 = X
&CE8 = Y
&CE9 = Z
&C40 = ' '
&C5B = '$'
&C7C = '@'
&C7B = '#'
&CF0 = 0
&CF1 = 1
&CF2 = 2
&CF3 = 3
&CF4 = 4
&CF5 = 5
&CF6 = 6
&CF7 = 7
&CF8 = 8
&CF9 = 9
*         ASSIGN DECIMAL ARRAY
&H0 = 0
&H1 = 1
&H2 = 2
&H3 = 3
&H4 = 4
&H5 = 5
&H6 = 6
&H7 = 7
&H8 = 8
&H9 = 9
&H10 = 10
&H11 = 11
&H12 = 12
&H13 = 13
&H14 = 14
&H15 = 15
&H16 = 16
&H17 = 32
&H18 = 48
&H19 = 64
&H20 = 80
&H21 = 96
&H22 = 112
&H23 = 128
&H24 = 144
&H25 = 160
&H26 = 176
&H27 = 192
&H28 = 208
&H29 = 224
&H30 = 240
&H31 = 256
&H32 = 512
&H33 = 768
&H34 = 1024
&H35 = 1280
&H36 = 1536
&H37 = 1792
&H38 = 2048
&H39 = 2304
&H40 = 2560
&H41 = 2816
&H42 = 3072
&H43 = 3328
&H44 = 3584
&H45 = 3840
&H46 = 4096
&H47 = 8192
&H48 = 12288
&H49 = 16384
&H50 = 20480
&H51 = 24576
&H52 = 28672
&H53 = 32768
&H54 = 36864
&H55 = 40960
&H56 = 45056
&H57 = 49152
&H58 = 53248
&H59 = 57344
&H60 = 61440
&H61 = 65536
&H62 = 131072
&H63 = 196608
&H64 = 262144
&H65 = 327680
&H66 = 393216
&H67 = 458752
&H68 = 524288
&H69 = 589824
&H70 = 655360
&H71 = 720896
&H72 = 786432
&H73 = 851968
&H74 = 917504
&H75 = 983040
&H76 = 1048576
&H77 = 2097152
&H78 = 3145728
&H79 = 4194304
&H80 = 5242880
&H81 = 6291456
&H82 = 7340032
&H83 = 8388608
&H84 = 9437184
&H85 = 10485760
&H86 = 11534336
&H87 = 12582912
&H88 = 13631488
&H89 = 14680064
&H90 = 15728640
*         ASSIGN HEX ARRAY
&HEX1 = 1
&HEX2 = 2
&HEX3 = 3
&HEX4 = 4
&HEX5 = 5
&HEX6 = 6
&HEX7 = 7
&HEX8 = 8
&HEX9 = 9
&HEX10 = A
&HEX11 = B
&HEX12 = C
&HEX13 = D
&HEX14 = E
&HEX15 = F
&HEX16 = 10
&HEX17 = 20
&HEX18 = 30
&HEX19 = 40
&HEX20 = 50
&HEX21 = 60
&HEX22 = 70
&HEX23 = 80
&HEX24 = 90
&HEX25 = A0
&HEX26 = B0
&HEX27 = C0
&HEX28 = D0
&HEX29 = E0
&HEX30 = F0
&HEX31 = 100
&HEX32 = 200
&HEX33 = 300
&HEX34 = 400
&HEX35 = 500
&HEX36 = 600
&HEX37 = 700
&HEX38 = 800
&HEX39 = 900
&HEX40 = A00
&HEX41 = B00
&HEX42 = C00
&HEX43 = D00
&HEX44 = E00
&HEX45 = F00
&HEX46 = 1000
&HEX47 = 2000
&HEX48 = 3000
&HEX49 = 4000
&HEX50 = 5000
&HEX51 = 6000
&HEX52 = 7000
&HEX53 = 8000
&HEX54 = 9000
&HEX55 = A000
&HEX56 = B000
&HEX57 = C000
&HEX58 = D000
&HEX59 = E000
&HEX60 = F000
&HEX61 = 10000
&HEX62 = 20000
&HEX63 = 30000
&HEX64 = 40000
&HEX65 = 50000
&HEX66 = 60000
&HEX67 = 70000
&HEX68 = 80000
&HEX69 = 90000
&HEX70 = A0000
&HEX71 = B0000
&HEX72 = C0000
&HEX73 = D0000
&HEX74 = E0000
&HEX75 = F0000
&HEX76 = 100000
&HEX77 = 200000
&HEX78 = 300000
&HEX79 = 400000
&HEX80 = 500000
&HEX81 = 600000
&HEX82 = 700000
&HEX83 = 800000
&HEX84 = 900000
&HEX85 = A00000
&HEX86 = B00000
&HEX87 = C00000
&HEX88 = D00000
&HEX89 = E00000
&HEX90 = F00000
*   END   ASSIGN HEX ARRAY
*     INDEX BOUNDS ARRAY FOR STARTING POINT OF SCAN
&C1   = 9
&C2   = 22
&C3   = 34
&C4   = 48
&C5   = 62
&C6   = 76
&C7   = 85
&C8   = 90
***********************************************************************
* ROUTINE TO GET THE HWX ADDRESS
*NCP 8.3(3725) L'6E4' POINTS TO THE XDA
*              XDA+X'58'(88) POINTS TO THE HWE
***********************************************************************
&DISADDR = 6E0
&DISLEN  = 12
&DISVAR  = WD
&DISRET  = LOWCOR
&GOTO -DIS
-LOWCOR
&NNTLAST = 12
&HWEA = 7D8
&XDH = &SUBSTR &WD2 3 6
*     TEST FOR PRE- 8.3 3725    L'6E0' = 00000000
&IF &WD1 EQ 00000000 &THEN &GOTO -HAVHWE
*     TEST FOR 3705            L'6E0' = 00C80000
&IF &WD1 EQ 00C80000 &THEN &GOTO -HAVHWE
*     THE ADDRESS OF THE XDA IS IN L'6E8'
&XDA = &SUBSTR &WD3 3 6
&NNTLAST = 12
***********************************************************************
&DISADDR = &XDA
&DISLEN  = 96
&DISVAR  = WD
&DISRET  = ENDXDAA
&GOTO -DIS
************************************************************************
*      THE ADDRESS OF THE HWX IS X'5C'(92) INTO THE XDA
*      THIS WILL BE IN THE LAST WORD OF THE LAST DISPLAY
***********************************************************************
-ENDXDAA
&HWE  = &SUBSTR &DISLAST 3 6
&GOTO -STRHWE
-HAVHWE
***********************************************************************
&DISADDR = &HWEA
&DISLEN  = 4
&DISVAR  = WD
&DISRET  = AHWE
&GOTO -DIS
***********************************************************************
-AHWE
&HWE = &SUBSTR &DISLAST 3 6
-STRHWE
***********************************************************************
&DISADDR = &HWE
&DISLEN  = 112
&DISVAR  = WD
&DISRET  = HWENNTA
&GOTO -DIS
***********************************************************************
*      THE ADDRESS OF THE NNT IS X'6C (108) INTO THE HWE
*      THIS WILL BE IN THE LAST WORD OF THE LAST DISPLAY
-HWENNTA
&NNTA = &SUBSTR &DISLAST 3 6
************************************************
* IF NNT = 0 THEN THIS IS NOT A V4.3 OR V5 BOX
************************************************
&IF &NNTA NE 000000 &THEN &GOTO -UPLEVEL
&BEGWRITE SUB -DNLEVEL
*** NCP MUST BE VERSION 4.3(3725)/VERSION 5(3745) OR LATER TO RUN NNT
*** END OF N.N.T. FOR &NCP
-DNLEVEL
&EXIT
-UPLEVEL
***********************************************************************
* DISPLAY THE NNT HEADER                                              *
***********************************************************************
-HAVEPSB
&DISADDR = &NNTA
&DISLEN = 10
&DISVAR = NNTHD
&DISRET = ANNTHD
&GOTO -DIS
-ANNTHD
&CVDPARM = &SUBSTR &NNTHD1 1 4
&CVDRET = HDENT
&GOTO -CVD
-HDENT
&NETS = &CVDNUM
&CVDPARM = &SUBSTR &NNTHD1 5 4
&CVDRET = HDENT1
&GOTO -CVD
-HDENT1
&ENTS = &CVDNUM
&CVDPARM = &SUBSTR &NNTHD2 1 4
&CVDRET = HDSIZ
&GOTO -CVD
-HDSIZ
&ANNTSZ = &CVDNUM
&CVDPARM = &SUBSTR &NNTHD2 5 4
&CVDRET = HDSIZ1
&GOTO -CVD
-HDSIZ1
&MAENTS = &CVDNUM
&CVDPARM = &SUBSTR &NNTHD3 1 4
&CVDRET = HDSIZ2
&GOTO -CVD
-HDSIZ2
&INUSEN = &CVDNUM
&NUMNNT = &CVDNUM
&NNTSIZ = 10
&II = 0
&XX = &NNTSIZ
&YY = 4
-DIVIDE
&XX = &XX - &YY
&II = &II + 1
&IF &XX GT 0 &THEN &GOTO -DIVIDE
&NNTLAST = &II
&NNTLAST = &INUSEN
&III = 1
&OUTL&III = '*-----------------------------------------*'
&III = 2
&OUTL&III = '|   NNT    |     Name     |  NNT  | Usage |'
&III = 3
&OUTL&III = '|   NAME   |     Type     | Flags | Count |'
&III = 4
&OUTL&III = '|----------|--------------|-------|-------|'
&NNTOFFS = 248
&CVDPARM = &NNTA
&CVDRET = NNTCVD
&GOTO -CVD
-NNTCVD
&NNTDEC = &CVDNUM
&NNTDEC = &NNTDEC + 10
&DECIN = &NNTDEC
&DECRET = FSTNNT
&GOTO -DECHEX
-FSTNNT
&LCLNNTA = &HEXVAL
-ANNTA
&DISADDR = &LCLNNTA
&DISLEN = &NNTSIZ
&DISVAR = NNT
&DISRET = ONENNT
&GOTO -DIS
***********************************************************
* DECODE ONE FULL NNT CONTROL BLOCK AND PRINT IT OUT
***********************************************************
-ONENNT
***********************************************************
*   EXTRACT  NETWORK ID NAME
***********************************************************
&NWID =
&NHEX = &CONCAT &NNT1 &NNT2
&NHEX = &CONCAT &NHEX &NNT3
&NTFLG = &SUBSTR &NHEX 1 4
&NHEX = &SUBSTR &NHEX 5 16
&NTTYP = 'NETWORK NAME'
&IF &SUBSTR &NTFLG 1 1 GE 4 &THEN &NTTYP = 'SSCP/CP NAME'
&CVDPARM = &SUBSTR &NTFLG 2 3
&CVDRET = NNTUSE
&GOTO -CVD
-NNTUSE
&NTUSE = &CVDPARM
*WRITE  &NTFLG -- &NHEX $$$$ -- HEX &NTUSE
&H = 15
***** CONVERT EBCDIC TO CHARACTER *************************
-CONV
&CHAR = &SUBSTR &NHEX &H 2
&CHAR = &CONCAT C &CHAR
&NWID = &CONCAT &&CHAR &NWID
&H = &H - 2
&IF &H GT 0 &THEN &GOTO -CONV
&LENNWID = &LENGTH &NWID
&LENPAD = 8 - &LENNWID
&PADNWID = &SUBSTR '        ' 1 &LENPAD
&NWID = &CONCAT &NWID &PADNWID
***********************************************************
*   EXTRACT LOCAL NETWORK ID
***********************************************************
&III = &III + 1
&OUTL&III = &CONCAT &OUTL&III '| '
&OUTL&III = &CONCAT &OUTL&III &NWID
&OUTL&III = &CONCAT &OUTL&III ' | '
&OUTL&III = &CONCAT &OUTL&III &NTTYP
&OUTL&III = &CONCAT &OUTL&III ' |  '
&OUTL&III = &CONCAT &OUTL&III &NTFLG
&OUTL&III = &CONCAT &OUTL&III ' |  '
&OUTL&III = &CONCAT &OUTL&III &NTUSE
&OUTL&III = &CONCAT &OUTL&III ' |'
***********************************************************
*   POINT TO NEXT NNT ENTRY AND REPEAT
***********************************************************
&INUSEN = &INUSEN - 1
&IF &INUSEN LE 0 &THEN &GOTO -NNTDONE
&NNTDEC = &NNTDEC + &NNTSIZ
&DECIN = &NNTDEC
&DECRET = NNTHEXR
&GOTO -DECHEX
-NNTHEXR
&LCLNNTA = &HEXVAL
&GOTO -ANNTA
-NNTDONE
&CVDPARM = &HEXVAL
&CVDRET = TOTAL
&GOTO -CVD
-TOTAL
&III = &III + 1
&OUTL&III = '*-----------------------------------------*'
&III = &III + 1
&OUTL&III = &CONCAT 'TOTAL #NNTS IN USE                =   ' &NUMNNT
&III = &III + 1
&OUTL&III = &CONCAT 'TOTAL #NNTS AVAILABLE             =   ' &ANNTSZ
&III = &III + 1
&OUTL&III = &CONCAT 'MINIMUM #NNTS AVAILABLE           =   ' &MAENTS
&III = &III + 1
&OUTL&III = &CONCAT 'TOTAL #NNTS DEFINED               =   ' &ENTS
&III = &III + 1
&OUTL&III = &CONCAT 'TOTAL #NETWORKS DEFINED           =   ' &NETS
&III = &III + 1
&OUTL&III = '*** END OF NNT ***'
&JJJ = 1
-WRITEIT
&WRITE &OUTL&JJJ
&JJJ = &JJJ + 1
&IF &JJJ LE &III &THEN &GOTO -WRITEIT
&EXIT
******************************************************************
* SUBROUTINE TO CONVERT HEX TO DECIMAL AND THEN PAD WITH BLANKS  *
*    INPUT:  CVDPARM 1 TO  6 NIBBLES IN HEX                      *
*            CVDRET  LABEL OF RETURN                             *
*                    AND BASE VARIABLE FOR OUTPUT                *
*    OUTPUT: CVDPARM DECIMAL NUMBER RIGHT JUSTIFIED              *
*                    OUTPUT LENGTH = LENGTH OF CVDPARM + 1       *
*                    ALSO PUT IN CVDRET VARIABLE                 *
*            CVDNUM  DECIMAL NUMBER NOT PADDED WITH BLANKS       *
*                    IN CVDRET VARIABLE WITH N APPENDED          *
******************************************************************
-CVD
&CVDLEN = &LENGTH &CVDPARM
&OUTLEN = &CVDLEN + 1
&POS = 1
&CVDNUM = 0
-CVDLOOP
 &IF &CVDLEN = 0 &THEN &GOTO -PADS
 &E = &SUBSTR &CVDPARM &POS 1
 &P = &CONCAT H&CVDLEN B
 &P = &CONCAT &P &E
 &CVDNUM = &CVDNUM + &&P
 &CVDLEN = &CVDLEN - 1
 &POS = &POS + 1
 &GOTO -CVDLOOP
*     ENTRY POINT JUST TO PAD WITH BLANKS
-PADS
&ELEN = &LENGTH &CVDNUM
&ELEN = &OUTLEN - &ELEN
&PAD = &SUBSTR '        ' 1 &ELEN
&CVDPARM = &CONCAT &PAD &CVDNUM
&&CVDRET = &CVDPARM
* APPEND A CHARACTER N TO END OF INPUT VARIABLE
&VN = &CONCAT &CVDRET N
&&VN = &CVDNUM
&GOTO -&CVDRET
********************** END OF CVD *************************************
***********************************************************************
*   D I S   S U B R O U T I N E                                       *
* FUNCTION: DISPLAY NCP STORAGE WITH VTAM NCPSTOR COMMANDS            *
* INPUT:                                                              *
*   DISADDR = HEX ADDRESS TO BE DISPLAYED                             *
*   NCP     = NCP TO BE DISPLAYED                                     *
*   DISLEN  = LENGTH OF STORAGE DISPLAY                               *
*   DISRET  = RETURN ADDRESS FROM DIS ROUTINE                         *
*   DISVAR  = VARIABLE PREFIX FOR OUTPUT ARRAY                        *
* OUTPUT:                                                             *
*   DISLAST = VALUE OF LAST WORD OF STORAGE DISPLAY                   *
*   DISVAR1.= ARRAY OF WORDS FROM 1 TO N WITH OUTPUT OF DISPLAY       *
***********************************************************************
-DIS
&D = 0
&WAIT CONTWAIT  SUPPRESS
&WAIT 'D NET,NCPSTOR,ID=&NCP,LENGTH=&DISLEN,ADDR=&DISADDR' +
IST097I=-NOPVER IST244I=-VER IST245I=-VER IST314I=-NOPVER +
5A97I=-NOPVER   5C44I=-VER   5C45I=-VER   5D14I=-NOPVER +
IST241I=-ENDVER  IST242I=-REJECT +
5C41I=-ENDVER    5C42I=-REJECT +
*ERROR=-ERROR *ENDWAIT=-STRVER  *60=-ERROR
***********************************************************************
* IST245I (5C45I) 000260 00000000 00000000 00000000 00000000
***********************************************************************
-VER
&MSZI = 1
GETMSIZE MSZ
-NEWMSG
&I = 2
GETMLINE MSGTXT &MSZI
PARSEL2R MSGTXT MSID XX1 XX2 XX3 XX4 XX5 DUMMY
&IF &MSID EQ 5C45I   &THEN &GOTO -FOUNDMSG
&IF &MSID NE IST245I &THEN &GOTO -NXTMSG
-FOUNDMSG
&WORD = &XX&I
-VERLOOP
&D = &D + 1
&A = &CONCAT &DISVAR &D
&&A = &WORD
&I = &I + 1
&WORD = &XX&I
&IF .&WORD NE . &THEN &GOTO -VERLOOP
-NXTMSG
&MSZI = &MSZI + 1
&IF &MSZI LE &MSZ &THEN &GOTO -NEWMSG
&WAIT CONTINUE
***********************************************************************
* IGNORE THESE MESSAGES
* IST097I (5A97I) NCPSTOR ACCEPTED
* IST244I (5C44I) NCP STORAGE FOR ID = (NCP)
* IST314I (5D14I) END
***********************************************************************
-NOPVER
&WAIT CONTINUE
***********************************************************************
* IST241I (5C41I) NCPSTOR COMMAND COMPLETE FOR ID = (NCP)
***********************************************************************
-ENDVER
GO
-STRVER
&LASTNCP = &NCP
&DISLAST  = &&A
&GOTO -&DISRET
************************************************************************
* IST242I (5C42I) NCPSTOR COMMAND FAILED FOR ID = (NCP) SENSE = 08060000
************************************************************************
-REJECT
* NCP STORAGE DISPLAY COMMAND REJECTED WITH MSG IST242I (5C42I)
&IF &&MSGCNT NE '08060000' &THEN &GOTO -NOT0806
&BEGWRITE SUB -REJ0806
*** NCP &NCP IS AN UNKNOWN RESOURCE
*** N.N.T. ENDED
-REJ0806
&GOTO -REJEXIT
-NOT0806
&BEGWRITE SUB -NOID
*** INFORMATION IS NOT AVAILABLE FOR NCP &NCP
*** N.N.T.  ENDED
-NOID
-REJEXIT
&EXIT
-ERROR
&BEGWRITE
*** CLIST NNT  HAS TIMED OUT, EXPECTED RESULTS WERE NOT RECEIVED
&EXIT
***********************************************************************
*    OPERATOR TYPE CANCEL WHILE IN PENDING STATE.
-CANCEL
MESSAGE 317I,NNT
&EXIT
-TIMEOUT
&WAIT DISPLAY
MESSAGE 314I,NNT,60,SECONDS
&EXIT
*******************( END OF D I S )************************************
***********************************************************************
* DECHEX SUBROUTINE
* ROUTINE TO CONVERT DECIMAL TO HEXADECIMAL
* THE ONLY INPUT IS THE DECIMAL NUMBER UP TO 16777215
* DECIN = DECIMAL INPUT
* HEXVAL = HEX OUTPUT
* DECRET = RETURN LOCATION
***********************************************************************
-DECHEX
&HEXVAL =
&VAL =
&NUM = &DECIN
-INRANGE
&I = &LENGTH &NUM
&I = &C&I
&I = &I + 1
-DLOOP
&I = &I - 1
&IF &NUM <  &H&I &THEN &GOTO -DLOOP
&NUM = &NUM - &H&I
&IF .&HEXVAL = . &THEN &GOTO -INITVAL
&HLEN = &LENGTH &HEXVAL
&LEN = &LENGTH &HEX&I
&LEN = &HLEN - &LEN
&VAL = &SUBSTR &HEXVAL 1 &LEN
-INITVAL
&HEXVAL = &CONCAT &VAL &HEX&I
&IF &NUM > 0 &THEN &GOTO -INRANGE
&GOTO -&DECRET


Anthony J Cieri IV
Senior Network Consultant
SEI Corporation (USA)

