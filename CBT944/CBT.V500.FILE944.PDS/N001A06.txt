Using printers in SNA

The purpose of this article is to give a clearer understanding of
the way printers are handled in an SNA environment and to
assist in the development of applications to drive printers.
SNA printer session types and protocols will be explained and
details will be given of how the increasingly sophisticated
printer capabilities can be invoked using the Function
Management (FM) protocols defined by SNA.  Finally, advice
will be given on coping with printer problems.

A printer can be defined to SNA as one of the following LU
types:

o       LU type 0 - session protocol and printer control must be
       agreed between the printer and the application.  There are
       no SNA functions defined.  LU 0 is employed in SNA
       JES2 remote printer sessions where printer control is ANSI.

o       LU type 1 - using SNA Character String (SCS) and SNA
       protocol defined for a printer device (eg 3286).

o       LU type 3 - using 3270 data stream and SNA protocol
       defined for a printer (eg 3287).

o       LU type 4 - using SCS and SNA protocol defined for a
       printer device attached to an SNA batch LU (eg a 3770
       RJE workstation).

We will concern ourselves primarily with LU types 1 and 3.
Typically these will be printers attached to a cluster controller
(eg 3174/3274).


HOST APPLICATION PRINTER HANDLING

To establish an SNA LU session and allow data to flow, a
printer must first be logged on to a host application.  This can
be achieved in several ways:

%      By being attached to a batch LU which can generate the
       log-on request.

o       By an SSCP-generated log-on (V NET,LOGON=applid, ID=printer).

o       By VTAM configuration activation processing
       (LOGAPPL= applid specified on the printer network
       definition LU statement).

o       A host application simulating a log-on by issuing a
       SIMLOGON request.

Use of SIMLOGON is the most common method used to
establish a session with a printer.  An outline of the coding
necessary to achieve this is as follows:

< Mainline code entry protocol>
           .
         OPEN  APPLACB                                               <note 1>
         SETLOGON RPL=APPLRPL,ACB=APPLACB,OPTCD=START                     <2>
           .
         SIMLOGON RPL=LOGONRPL,ACB=APPLACB,NIB=APPLNIB,OPTCD=SYN          <3>
         TESTCB AM=VTAM,NIB=APPLNIB,CON=YES                               <7>
         LTR   R15,R15
         BNZ   <error processing>
         INQUIRE RPL=LOGONRPL,NIB=APPLNIB,OPTCD=SESSPARMS,             C  <8>
               AREA=SESSPRMS
         LA    6,SESSPRMS
         USING ISTDBIND,6
           .

APPLACB  ACB    AM=VTAM,APPLID=APPLID,EXLST=APPLIST,MACRF=LOGON
APPLID   DC     X'06',C'APPLID'
APPLIST  EXLST  AM=VTAM,LOGON=LOGONXIT
APPLRPL  RPL    AM=VTAM
LOGONRPL RPL    AM=VTAM
APPLNIB  NIB    NAME=PRINTER1,LOGMODE=0,BNDAREA=0,LISTEND=YES
SESSPRMS DS     100C
           .
         ISTDBIND

         CSECT ,
LOGONXIT DS    0H
   < entry protocol >
         L     2,=A(LOGONRPL)                                             <5>
         L     3,=A(APPLNIB)
         OPNDST RPL=(2),OPTCD=(SYN,ACCEPT,CA),NIB=(3)                     <4>
         LTR   15,15
         BNZ   LOGONERR
   < exit protocol >
         BR    14
LOGONERR DS    0H
   < flag mainline code >                                                  <6>
   < exit protocol >
         BR    14


Notes

The following notes refer to the statements flagged in the above
sample code.

1       A VTAM major node containing a definition for the
       application ACB must be active, for example:

               VBUILD TYPE=APPL
       APPLACB APPL  ACB=APPLACB,AUTH=(ACQ,PASS)

       The AUTH parameters specify the application is able to
       ACQUIRE printers and PASS them on to other applications.

2       The ACB specifies MACRF=LOGON, which means that
       the application is prepared to accept log-on requests, the
       SETLOGON tells VTAM that the application is now ready
       to handle such requests.

3       The application requests a session with the printer whose
       network name (VTAM-id) is specified in the NIB (ie
       NAME=PRINTER1).  An INITIATE is sent to the SSCP
       owning PRINTER1 and the SSCP generates a CINIT
       which drives the application log-on exit LOGONXIT.
       Coding OPTCD=SYN will cause the APPL to wait until
       the SIMLOGON has completed (ie the log-on exit has
       finished).  VTAM will POST an ECB in the RPL specified
       by the SIMLOGON (LOGONRPL).

4       Results in a BIND request followed by an SDT, VTAM
       sends a +RESP (positive response) to both of these on
       behalf of the printer.  The NIB addressed by the OPNDST
       specifies LOGMODE=0 and BNDAREA=0 - VTAM will
       use the defaults supplied in the CINIT RU resulting from
       the SIMLOGON.  The session parameters cannot be
       changed because the BIND is not negotiable for printers.

5       VTAM uses SCHEDULE (SVC 6) to execute the
        application log-on exit, therefore the register contents
       cannot be assumed.  Addressability to the mainline code
       must be re-established.

6       Since the session establishment failed, CLSDST must not
       be issued as this will cause a SYNAD error.  Mainline
       code can be notified by flagging (or POSTing of an ECB).
       Errors in the log-on exit may result in an LERAD error.

7       The success of the OPNDST is checked by testing whether
       the NIB CONnect field is set to YES
       (NIBFLG1=NIBCON).

8       VTAM copies the session parameters (ie BIND data) from
       the CINIT RU to the area SESSPRMS, where they can be
       examined using a standard mapping DSECT, ISTDBIND.


PRINTER SESSION PROTOCOL

Protocol rules governing the session between a host application
(the PLU) and a printer (the SLU) are the same for both the
LU1 and LU3 mode sessions.  Bracket protocol is used: the
printer begins the bracket (ie is the first speaker) but the
bracket can only be terminated by the host application.
Whether or not the application terminates a bracket depends
upon whether the printer is either to be shared with another
application or used for local print requests.  An application can
only ACQUIRE a printer if the current owning application does
not have an open bracket.  Similarly, requests for local prints
can only be honoured if the printer has been configured in the
controller as being shareable between brackets and the printer
does not currently have an open bracket with an application.
Note that for a local print operation it is not necessary for the
host session to be terminated, only the open bracket.  This is
known as 'between bracket sharing'.

As explained above, to request a session with a printer, a host
application must issue a SIMLOGON request.  VTAM will
determine whether the printer is available and, if it is, drive the
application log-on exit.  If the printer is already in session with
another application and the requesting application issued
SIMLOGON with OPTCD= (RELRQ,Q), VTAM will
schedule the RELREQ exit (if supplied) of the other
application.  Assuming that a RELREQ exit was provided, this
must decide whether to release the printer or not, ie terminate
the bracket and issue a CLSDST OPTCD=PASS.  If the
session is continued, the SIMLOGON request is queued until
such time as the printer becomes free.  The session request is
similarly queued if no RELREQ exit was provided by the
application currently in session with the printer.

Pacing is the mechanism by which the rate of data flow to the
printer can be controlled.  Unconstrained, a printer could easily
utilize most of the capacity of a line.  If the cluster controller to
which the printer was attached also supported display
terminals, the response time at these terminals would suffer
network delays.  Obviously this would not be a problem if the
printer were the only active device on the line or only printers
were attached to the cluster controller.  If the pacing counts are
set to 1 in the SRCVPAC and PSNDPAC fields of the session
BIND parameters, VTAM will pace the data flow such that:

o       Only one message from the controlling host application can
       be queued ready for sending.

o       Further data will only be sent to the printer once the
       previous data has been printed.

It is also possible to specify pacing for the data sent from a
printer to the host application (the SSNDPAC and PRCVPAC
parameters) but the volume of this data (if any) will be very
small.

PRINTER FUNCTIONS
Until fairly recently printers in the SNA environment have been
dumb devices capable only of accepting print streams with
limited control information (new line, carriage return etc).
Printers capable of supporting SNA sessions now have a full
range of print functions, including:

o       Colour support, normally four basic colours which can be
       mixed to produce four more colours (eg cyan and yellow
       makes green).

o       Graphics capabilities, including both graphs (eg pie charts
       etc) and perspective view diagrams.

o       Variable fonts, subscripting, underlining, overprinting,
       gothic script.

o       Variable print densities and qualities, the print CPI can be
       altered to provide either fast draft quality or slow letter
       quality.

o       Form control, stationery type changes etc.

o       Variable character sets, languages etc.

o       Data compression and compaction.

Printers have buffers capable of receiving fonts, characters
sets, compression tables, etc downloaded from the host.  They
can also communicate back to the host the current status of
these buffers and the range of printer facilities available to the
host.

This capability is necessary to overcome the shortfall of
information in the session BIND command, which simply
describes the set of protocol rules to be used (ie contention
rules, bracket protocol, request unit size, pacing, etc) and
presentation services (ie stream type, screen size, etc).  No
details are supplied in the BIND relating to the extended
functions of the printer described above.

SNA allows for the possibility of two-way communication
between host and printer via an exchange of data at the
Function Management level and this capability has been
implemented in the IBM 4224 range of printers.

Two-way communication is achieved by an exchange of
messages containing data in a format understood by the printer
to contain command codes.  These are termed 'structured data
fields'.  A host application can send a message containing a
QUERY code which requests information from the printer.
Information is returned via a message containing a QUERY
REPLY code.

IBM 4224 printers support two modes of operation:

o       LU type 1 mode       -       SNA Character Stream (SCS)
                             -       Intelligent Printer Data Stream (IPDS).

o       LU type 3 mode       -       SNA Data Stream Emulation (DSE).

Full function capabilities are available to a printer operating in
LU 1 SCS mode.  While in this mode, FM Headers (FMHs)
can be used to describe and initiate print operations and as the
means of host enquiries on the status and capability of a
printer.  A host application indicates to the printer that an FMH
is contained in the Request Unit (RU) by setting the FMH flag
in the SEND macro:

SEND  RPL=rpl,OPTCD=(FMHDR)

It is the responsibility of the sending application to ensure that
the content and format of the message containing the FMH is
correct.


FUNCTION MANAGEMENT HEADERS

To indicate that FMHs are to be used, bit 1 in the BIND
session parameter's common protocol field (COMPROT) must
be set.  Which subset of FMHs is to be used, is specified in the
second byte of the Presentation Services field (PSERVIC).
Which particular functions of the FMH subset are to be used,
are specified in the FMH flags (bytes 3 and 4 of PSERVIC).
For printers, FMH subset flags can be set to indicate support
for the following:

o       Structured data fields

o       Query of structured fields

o       Use of structured fields for error recovery

o       Data compaction.

Sample BIND parameters (LOGMODE table entries) are
supplied at the end of this article.  The format of the
COMPROT and PSERVIC field are as follows (note the byte
numbers refer to the location within the BIND session
parameters):

COMPROT=X'7080'
          | |
          | Byte 6 - Send/Receive Indicators (Half Duplex  Flip/flop)
          Byte 5 - FMH (X'40') and Bracket (X'30') Indicators

PSERVIC=X'01000000E100185000007E00'
          |             | |   |
          |             | |   Bytes 23,24 - Data Stream Flags
          |             | Bytes 21,22 - FMH Flags
          |             Byte 20 - FMH Subset and Data stream
          |                       Profile numbers
          Byte 13 - LU type

SNA defines three types of FMH for use in SCS printer
sessions:

o       FMH type 1 - selects the destination (ie target device) for a
       session (an LU session can logically support more than one
       device) and any handling characteristics in effect (eg data
       compression and/or compaction, special data stream etc).
       The range of functions that can be described by the FMH1
       are grouped into subsets, for example subset 0 describes
       the use of structured  fields.

o       FMH type 2 - specifies operations or data manipulations to
       be carried out by the selected device.

o       FMH type 3 - specifies the same functions as the FMH2
       but the operations apply to all devices.

SNA requires that an FMH appears in the beginning of an RU
but it may span more than one RU.  Provided that this
restriction is observed, an RU containing FMHs can appear
anywhere in a chain, thus enabling switching between data
stream profiles and printer functionality during the same
session or bracket, for example:

SEND    CHAIN=FIRST,OPTCD=(NFMHDR)       RU 1 - No FMH, BC
SEND    CHAIN=MIDDLE,OPTCD=(NFMHDR)      RU 2 - No FMH, ¬BC, ¬EC
SEND    CHAIN=MIDDLE,OPTCD=(FMHDR)       RU 3 - FMH, ¬BC, ¬EC
SEND    CHAIN=MIDDLE,OPTCD=(NFMHDR)      RU 4 - No FMH, ¬BC, ¬EC
SEND    CHAIN=LAST,OPTCD=(NFMHDR)        RU 5 - No FMH, EC

Note that RU 3 could contain all of the FMH(s) or they could
be continued on to RU 4.  Any space remaining in the RU (ie
up to the RU size agreed in the BIND) can be filled with data
to be printed.

For most communications with a printer an RU containing an
FMH is sent as the only message in a chain:

SEND    CHAIN=ONLY,OPTCD=(FMHDR)

In fact it is mandatory with certain communications (eg
changing a printer to IPDS mode, see below).

All FMHs begin with the following 2-byte format:
Byte       Bits       Meaning
0       0-7              FMH length (including this length byte)
1       0              FM concatenation indicator:
                     0 - No other FMHs follow this one
                     1 - Another FMH follows
       1-7              FMH type (1, 2, 3, 4, etc).

The content of the remainder of the FMH depends upon the type.


SNA LU TYPE 1 PRINTER SESSION

This session operates in SCS mode and employs FMHs to
control printer operation.  An RU sent to the printer must begin
with a 6-byte type 1 FMH of the following format:
Byte       Content       Meaning
0       X'06'              FMH length
1       X'01'              FMH-1 indicator
2       X'00'              Select, console, logical address 0
3       X'0B'              Data stream profile, structured fields follow
4       X'60'              FMH properties (no compression or compaction)
5       X'00'              Reserved.

Byte 3 specifies DSP select as X'0B' which means that the
remainder of the request will contain structured fields of the
following format:

Byte       Content       Meaning
0-1       X'....'              Length in bytes (including length and type bytes)
2       X'41'              Type (data)
3       X'00'              Not used
4-       ......              Data.

The data portion of the request (following the FMH) will
contain SCS codes of which there are 2 types:

o       Character codes which are printable characters (A-Z, 0-9, etc)

o       Control codes which represent printer control functions,
       possibly one of the following:

Code       Function
05               Horizontal tab
08              Graphic escape
0B              Vertical tab
0C              Form feed
0D              Carriage return
15              New line
16              Back space
25              Line feed
28              Set attribute (data attributes, eg colour)
2BC1              Set horizontal format (line width)
2BC2               Set vertical format (page length)
2BC6               Set line density (lines per inch)
2BD2nn29       Set print density (CPI specified by nn).

SCS printer-to-host communication

The functional capabilities of a printer operating in SCS (LU
type 1) mode can be determined by a host application sending a
query list request to the printer:

Byte       Content       Meaning
0-1       X'0005'       Length in bytes (including length and type bytes)
2       X'01'       ype (read partition)
3       X'FF'       Query (partition-id)
4       X'02'       Code for query.

An SCS printer is capable of returning data in response to the
host query.  In this case, byte 3 of the FMH in the in-bound
RU is set to X'8B' to indicate in-bound transmission.  The
structured data field for the Summary Query Reply from an
SCS printer (in LU type 1 mode) is as follows:

Byte       Content       Meaning
0-1       X'....'       Length in bytes (including length and type bytes)
2       X'81'       Type (Summary Query Reply)
3       X'80'       Summary indicator
4-       X' ',X' ',       Data (list of replies).

The list of replies describes the functions supported by the
printer.  These could include:

Reply Code       Function
81       User area supported
85       Multiple character sets supported
86       Colour supported
A0       Data streams supported (eg IPDS).

Note, since the session between the host program (PLU) and
the printer (SLU) operates in half duplex flip-flop mode, the
RU containing the Query List Request must have the Change
Direction indicator set to allow the printer to send the Summary
Reply back.  The SEND must be coded with the CHNGDIR
option:

SEND   CHNGDIR=CMD

In order for this to be effective there must be an open bracket,
ie EB must not have been sent by the host application.  Failure
to observe these conditions will result in a message reject with
sense code X'0829'.

Intelligent Print Data Stream (IPDS)

Printer graphics and image control are not supported by SCS
but by IPDS, which is also a structured-field data stream
containing more printer control codes.  To activate IPDS an
FMH type 1, subset 4 is sent to the printer:

Byte       Content       Meaning
0       X'06'       FMH length
1      X'01'      FMH-1 indicator
2      X'03'      Medium select, document, logical address 0
3      X'0D'      Data stream profile, IPDS
4      X'40'      FMH properties, begin IPDS
5      X'00'      Reserved

To deactivate IPDS, a similar FMH1 is sent with byte 4 set to
X'20'.  This will cause the printer to return to the default SCS
data stream mode.  Termination of IPDS mode implies
termination of the open bracket.

Note that both IPDS FMHs must be sent CHAIN=ONLY with
no accompanying data.



As with SCS data stream the data comprises structured fields of
the following format:

Byte      Content      Meaning
0-1      X'....'      Length in bytes (including length and type bytes)
2-3      X'....'      Printer control code
4      X'00'      Flag      (Bit 0 is set if printer must acknowledge)
                  (Bit 1 is set if correlation-id included)
5-6      X'....'      Correlation-id (if included)
7-            Data (codes, subcommands, and print data).

Again, as with SCS a series of printer controls is included in
the structured data field, for example:

Code      Meaning      Function
D6E4      Sense type and model      Device control
D697      Set home state      Device control
D6AF      Begin page      Device control
D6BF      End page      Device control
D62D      Write text      Text presentation
D64D      Write image      Image data presentation
D685      Write graphic      Graphic data presentation
D61E      Load symbol set      Font control


SNA LU TYPE 3 PRINTER SESSION

This session uses 3270 data stream.  RUs contain 3270 control
codes followed by the control orders and data to be printed.
Once the session has been established there are no defined
SNA functions to control the printer.  LU type 3 does not
support FMHs and cannot therefore utilize the full range of
printer capabilities.

A 3270 data stream write command is sent with the start print
bit set followed by a Write Control Character (WCC) as
follows:

Byte       Content      Meaning
0      27      Escape character
1      F5      Write command (with start print bit set)
2      C8      WCC
3      11      Set Buffer Address (SBA)
4-5      4040      Column 1, row 1
6-            EBCDIC data and printer control

RUs contain 3270 buffer control orders (eg SBA, repeat to
address) and print format orders (eg new line, form feed, etc).
Buffer control orders are executed upon receipt by the control
unit (eg 3174) and supervise the way in which the printer
buffer is filled.  Print format orders together with the data to be
printed are stored in the printer buffer as received.  The orders
are executed and the data is printed only when the buffer has
been filled.  It is possible to fill the printer buffer using a series
of write commands (ie multiple SEND requests from the host
application) with SBA control codes, delaying printing until a
final SEND with the start print bit set on in the WCC.


HANDLING PRINTER ERRORS

Failure of a SEND macro instruction to deliver the data
message to the printer controller or failure of the printer to print
can be detected issuing a CHECK against the originating RPL
which will cause VTAM to schedule the SYNAD exit in the
application.  Sample coding of such a sequence and related exit
would be as follows:

< Mainline code entry protocol>
           .
< Mainline printer session set-up code>
           .
           .
         SEND   RPL=SENDRPL,ACB=APPLACB,ARG=(6),                       C  <1>
                AREA=MESSAGE,AREALEN=MSGLEN,                           C
                CONTOL=DATA,RESPOND=(NEX,FME,NQRESP),                  C
                CHAIN=ONLY,BRACKET=(NBB,NEB),                          C
                OPTCD=(SYN,CA,FMHDR)
         CHECK  RPL=SENDRPL                                               <2>

   < Test for SYNAD exit posting error >

         SHOWCB AM=VTAM,RPL=SENDRPL,AREA=SHOWAREA,LENGTH=24,           C  <6>
                FIELDS=(RTNCD,FDBK2,USENSEI,SSENSEI)
           .

APPLACB  ACB   AM=VTAM,APPLID=APPLID,EXLST=APPLIST,MACRF=LOGON
APPLID   DC    X'06',C'APPLID'
APPLIST  EXLST AM=VTAM,LOGON=LOGONXIT,SYNAD=SYNADXIT
SENDRPL  RPL   AM=VTAM
MESSAGE  DC    X'0601000B6000'                                            <3>
         DC    H'23',X'4100'
         DC    X'0C0D'
         DC    C'PRINTER LOGGED ON'
MSGLEN   EQU   *-MESSAGE
SHOWAREA DS    24C


SYNADXIT DS    0H
   < entry protocol >
         L     2,=A(SENDRPL)
         SESSIONC RPL=(2),CONTROL=CLEAR,STYPE=REQ,OPTCD=SYN               <4>
         LTR   15,15
         BNZ   CLSDST
   < flag mainline SYNAD tripped but recovered>
   < exit protocol >
         BR    14
CLSDST   DS    0H
         CLSDST RPL=(2)                                                   <5>
   < flag mainline, session closed due to SYNAD>
   < exit protocol >
         BR    14

Notes

The following notes refer to the statements flagged in the above
sample code.


1      The SEND message contains an FMH, the message is the
       only one in a chain and the message flows in an already
       open bracket (ie no begin bracket, no end bracket
       specified).  The application will wait until the message has
       been sent (OPTCD=SYN).

2      This will force the scheduling of the SYNAD exit if there
       had been a problem with the printer or the data sent to it.

3      The message contains an FMH1 indicating that a
       structured data field follows.  Code 41 indicates that the
       field is SCS-type data, the SCS data follows (LF, CR, etc).

4      Attempts to CLEAR the current messages on the session.
       If this succeeds, the mainline code can be flagged to re-
       send the messages.

5      If the message flow cannot be cleared, the session should
       be terminated.

6      VTAM places in SHOWAREA the contents of the fields
       specified.  Each field occupies 4 bytes and is placed in the
       order specified by the FIELDS parameter.

Within the SYNAD exit it is possible to retry the failing SEND
instruction by using the EXECRPL macro.  This is issued
against the original SEND macro RPL.  If this is used, care
must be taken to check for recursive scheduling of the SYNAD
exit.  On balance it is probably safer to allow mainline
application code to determine whether to retry the SEND.  A
retry could be preceded by a single chain message containing a
page throw.  Possibly a prompt could be sent to the host
operating system console to initiate manual intervention.


ERROR CODES AND PROBABLE CAUSES

In the course of normal message flow, RTNCD=04 is the most
probable error.  Any other error will relate to the session
control protocol in which case the only course of action is to
abandon the session.  Examine the sense information returned
for one of the following errors relating to the message data sent
to the printer:

Sense Code      Meaning      Probable cause(s)
0802      Intervention Required      Printer has returned a
            hardware error condition, eg
            paper jam.
1002      RU length error        RU containing message is
            longer than agreed in BIND
            parameters.
1003      Function not supported      FMH reserved bits not zero.
            Invalid structured field type.
            Invalid query code field.
            Invalid SCS control code.
1005      Invalid parameter      Invalid structured field length.
            Print control error (eg print
            beyond boundary of page).
1008      Invalid FMH      FMH does not conform to
            standard requirements.
2002      Chaining error      Chaining sequence is not
            correct, eg CHAIN=
            MIDDLE sent without a
            CHAIN=FIRST.
2003      Bracket error      Bracket sequence is not
            correct, eg BRACKET=
            (BB,NEB) sent without EB
            first.
2008      No Begin Bracket      Message sent to the printer
            but there is no open bracket.
2009      Session control or data      Either a session control request
      flow control error      (eg change direction) or data
            flow control request has been
            sent but is not valid for the
            current printer session state.

SNA allows for a further two bytes of sense code to be
provided.  These follow immediately after the sense code (eg
X'100808nn').  Use of this is currently limited (if used at all).

If the logical processing of errors is insufficient to determine
the problem, the printer hardware buffers can be examined.
In the controller to which the printer is attached, there is a
communications buffer comprising two 4K buffers: a character
buffer and an attribute buffer.  The character buffer contains
command codes, printer codes, and the data to be printed.  The
attribute buffer contains additional information to describe how
each character is to be printed.  A Printer Control Information
Area (PCIA) is contained in the first 80 bytes of each buffer.
By stopping the printer and putting it into TEST mode, the
PCIA can be printed (assuming that the printer still functions!).
Examine the PCIA for possible errors as follows:

Byte      Description      Setting      Reason
X'00'      Status register      X'40'      Data check
            X'10'      Equipment check
X'03'      Sense data      X'02'      Invalid control code parameter
            X'03'      Invalid SCS code
            X'04'      Order reject
            X'07'      Invalid FMH
            X'08'      Invalid structured field
X'11'      Mode      X'05'      Data stream emulation (SNA)
            X'06'      LU 1 mode
X'12'      Message...            Address of the start of ...
X'13'      ...start address              ...   the message in buffer
X'14'      Message...            Length of ...
X'15'      ... length             ... current message
X'16'      Order      X'01'      Abort
            X'03'      Printing or FMH processing
X'17'      Order parameters      X'00'      SCS data stream
            X'01'      FMH data stream
            X'03'      In SCS receive state.

Note that the above interpretation applies only to a printer
operating in LU 1 (SCS) mode.

When examining the message area in the buffer be aware that
the text and printer control codes will have been translated into
printer internal codes, for example:

Control      EBCDIC      4224 Internal
 NL       X'15'       X'03'
 EM       X'19'       X'01'


SAMPLE BIND SESSION PARAMETERS

Standard SCS printer (eg 3268, 3278 in LU 1 mode)

      MSCS     MODEENT LOGMODE=MSCS,
                    FMPROF=X'03',
                    TSPROF=X'03',
                    PRIPROT=X'B1',
                    SECPROT=X'90',
                    COMPROT=X'3080',
                    RUSIZES=X'87C6',
                    PSNDPAC=X'01',
                    SRCVPAC=X'01',
                    PSERVIC=X'01000000E100000000000000'

3287 Model 2 operating in 3270 data stream mode (LU 3)

      M3287DS  MODEENT LOGMODE=M3287DS,
                    FMPROF=X'03',
                    TSPROF=X'03',
                    PRIPROT=X'B1',
                    SECPROT=X'90',
                    COMPROT=X'3080',
                    RUSIZES=X'87C6',
                    PSNDPAC=X'01',
                    SRCVPAC=X'01',
                    PSERVIC=X'03800000000018501B847F00'

4224 Model 2 remote attached using IPDS (LU 1)

      M4224IPR MODEENT LOGMODE=M4224IPR,
                    FMPROF=X'03',
                    TSPROF=X'03',
                    PRIPROT=X'B1',
                    SECPROT=X'90',
                    COMPROT=X'7080',  <== Note, FMH bit set
                    RUSIZES=X'85C7',
                    PSNDPAC=X'01',
                    SRCVPAC=X'01',
                    PSERVIC=X'01000000E100185000007E00'


Brian Alford
Systems Consultant
Aranda (UK)

