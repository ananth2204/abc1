VTAM buffer pools

I was given the task of reviewing the VTAM buffers at our site
and tuning them, if required. Before I could perform any part
of the tuning I needed to gather statistics on how they were
working over a period of time. As we do not have any software
to provide this information, I had two options:

1      Issue the VTAM command 'D NET,BFRUSE' and read
      the information from the log.
2      Run a program that will extract the data and write it to a file.

I chose the second option and thus the following program was
created. As it is, this program should be run from a batch job
that will either write the output to the batch SYSOUT file or to
a dataset. It would be possible for this program to have a
started task wrapped around it and monitor the pools
automatically.

**********************************************************************
*  NAME        -   BUFFPOOL                                          *
*  FUNCTION    -   THIS PROGRAM WILL DISPLAY THE VTAM BUFFER POOLS.  *
*                  THE ADDRESS MODE MUST BE 31 AS THE POOLS ARE      *
*                  STORED ABOVE THE LINE.                            *
*                  THIS PROGRAM MUST RUN AUTHORIZED.                 *
*                  THE LABEL POOLIDS INSTRUCTS THIS PROGRAM WHICH    *
*                  VTAM BUFFER POOLS SHOULD BE REPORTED ON.          *
*                  THE EXAMPLE SHOWN BELOW WILL MONITOR THE IO00,    *
*                  LP00, WP00, LF00, CRPL, SF00, AND AP00.           *
*  EXAMPLE:                                                          *
*   POOLIDS  DC    C'IO00LP00WP00LF00CRPLSF00SP00AP00FFFF'           *
*   NOTE: THE LAST ID IN THE FIELD MUST BE 'FFFF'                    *
**********************************************************************
*  THIS MODULE MUST BE LINKED AUTHORIZED.   EG. SETCODE AC(1)        *
*  IT MUST ALSO RESIDE IN AN AUTHORIZED LIBRARY                      *
*  THIS IS REQUIRED SO THAT SUPERVISOR STATE CAN BE SET.             *
**********************************************************************
*  TESTED ON   -   MVS/XA V2.2.1                                     *
*                  VTAM   V3.3                                       *
**********************************************************************
*  THE FOLLOWING JCL IS REQUIRED TO RUN BUFFPOOL.                    *
*      STEP1    EXEC PGM=BUFFPOOL                                    *
*      STEPLIB  DD DSN=MY.AUTH.LIBRARY,DISP=SHR                      *
*      SYSABEND DD SYSOUT=*                                          *
*      SYSPRINT DD SYSOUT=*                                          *
*      SYSOUT   DD SYSOUT=*                                          *
*  OR                                                                *
*      SYSOUT   DD DSN=STORE.THE.DATA,DISP=MOD                       *
**********************************************************************
BUFFPOOL CSECT
BUFFPOOL AMODE 31
BUFFPOOL RMODE ANY
         STM   R14,R12,12(R13)    SAVE CALLER'S REGISTERS
         LR    R12,R15            INITIALIZE BASE REGISTER
         USING BUFFPOOL,R12       ESTABLISH ADDRESSABILITY
         LA    R14,SAVEAREA       MY SAVE AREA
         ST    R14,8(R13)         LINK TO CALLER'S
         ST    R13,SAVEAREA+4     LINK TO MINE
         LA    R13,SAVEAREA       POINT R13 TO MY SAVEAREA
         OPEN  (SYSOUT,(OUTPUT))  OPEN SYSOUT DATASET
******************************
*  ESTABLISH ADDRESSABILITY  *
******************************
         L     R3,CVTPTR          LOAD CVT ADDRESS
         USING CVTMAP,R3          ADDRESSABILITY TO CVT
         L     R4,CVTEXT2         LOAD EXTENDED STORAGE ADDRESS
         USING CVTXTNT2,R4        ADDRESSABILITY TO CVT EXTENDED STORAG
         L     R5,CVTATCVT        POINTER TO ATCVT
         L     R5,0(R5)           LOAD POINTER TO THE BPDTY
         MODESET KEY=ZERO,MODE=SUP CHANGE TO SUPERVISOR STATE
         L     R5,980(R5)         POINTER TO BUFFER POOL DIRECTORY
************************************************************
*  EXTRACT INFORMATION FROM BUFFER POOL DIRECTORY - BPDTY  *
************************************************************
         LH    R6,0(R5)           LOAD NUMBER OF ENTRIES
         ST    R6,NUMENTS         STORE NUMBER OF ENTRIES
         LA    R5,368(R5)         POINTER TO BUFFER POOL ENTRIES BPENT
********************************************************
*  EXTRACT INFORMATION FROM BUFFER POOL ENTRY - BPENT  *
********************************************************
         LA    R7,POOL            LOAD ADDRESS OF POOL ARRAY
A0000    MVC   0(12,R7),0(R5)     STORE POOL ID, FORMAT AND BUFFER SIZE
         LA    R5,16(R5)          INCREMENT TO NEXT BUFFER POOL
         LA    R7,44(R7)          MOVE POINTER ALONG ARRAY
         BCT   R6,A0000           SUBTRACT ONE AND BRANCH
***************************************************************
*  EXTRACT INFORMATION FROM BUFFER POOL CONTROL BLOCK - BPCB  *
***************************************************************
         LA    R7,POOL            LOAD ADDRESS OF POOL ARRAY
         USING POOLDEF,R7         ESTABLISH ADDRESSABILITY
         L     R6,NUMENTS         LOAD NUMBER OF ENTRIES
B0000    DS    0H
         L     R8,BPCBADDR        LOAD BPCP ADDRESS
         MVC   CURRTOT,36(R8)     STORE CURRENT TOTAL
         MVC   MAXUSED,56(R8)     STORE MAXIMUM USED
         MVC   CURRVL,88(R8)      STORE CURRENT AVAILABLE
         MVC   CONTHR,148(R8)     STORE CONTRACTION THRESHOLD
         MVC   EXPTME,152(R8)     STORE TIMES EXPANDED
         MVC   EXPTHR,156(R8)     STORE EXPANSION THRESHOLD
         MVC   MAXTOT,160(R8)     STORE MAXIMUM TOTAL
         MVC   EXPINC,166(R8)     STORE EXPANSION INCREMENT
         LA    R7,44(R7)          MOVE POINTER ALONG ARRAY
         BCT   R6,B0000           SUBTRACT ONE AND BRANCH
*************************
*  RESET AUTHORIZATION  *
*************************
C0000    DS    0H
         MODESET KEY=NZERO,MODE=PROB CHANGE BACK TO NORMAL STATE
         LA    R1,C0010           LOAD BRANCH ADDRESS
         BSM   0,R1               REVERT TO 24-BIT ADDRESSING
****************************************
*  SKIP THRU ARRAY AND PRINT OUT DATA  *
****************************************
C0010    DS    0H
         PUT   SYSOUT,TITLE1      WRITE OUT TITLE
         PUT   SYSOUT,BLANKLNE    SKIP A LINE
         LA    R7,POOL            POINT TO POOL ARRAY - RESET
         USING POOLDEF,R7         ESTABLISH ADDRESSABILITY
         L     R8,NUMENTS         RETRIEVE NUMBER OF BUFFER ENTRIES
D0000    LA    R10,POOLIDS        MOVE IN POOL IDENTIFIER
E0000    CLC   0(4,R10),POOLID    LOOK FOR A MATCH
         BE    F0000              BRANCH IF MATCHED
         CLC   0(4,R10),=C'FFFF'  END OF TABLE
         BE    K0000              BRANCH IF MATCHED
         A     R10,=F'4'          INDEX TO NEXT ID
         B     E0000              TRY AGAIN
F0000    MVC   OUT1(4),POOLID     MOVE IN BUFFER POOL NAME
***********************
*  SETUP BUFFER SIZE  *
***********************
         SR    R9,R9              CLEAR REGISTER 9
         ICM   R9,B'0011',BUFFSIZE  MOVE IN BUFFER SIZE
         CVD   R9,WORKCVD         CONVERT TO DECIMAL
         UNPK  NUM5,WORKCVD       UNPACK
         OI    NUM5+L'NUM5-1,X'F0' MAKE IT PRINTABLE
         MVC   OUT2,NUM5          MOVE TO SYSOUT
*******************************
*  SETUP EXPANSION THRESHOLD  *
*******************************
         SR    R9,R9              CLEAR REGISTER 9
         ICM   R9,B'0011',EXPTHR  MOVE IN THRESHOLD SIZE
         CVD   R9,WORKCVD         CONVERT TO DECIMAL
         CH    R9,=X'7FFF'        DOES POOL HAVE EXPANSION ?
         BNE   G0000              YES, CONTINUE
         MVC   NUM5,=C'*N/A*'     NO, SET OUTPUT
         B     H0000              BRANCH AROUND WORK
G0000    UNPK  NUM5,WORKCVD       UNPACK
         OI    NUM5+L'NUM5-1,X'F0' MAKE IT PRINTABLE
H0000    MVC   OUT3,NUM5          MOVE TO SYSOUT
********************************
*  SETUP EXPANSION INCREMENTS  *
********************************
         SR    R9,R9              CLEAR REGISTER 9
         ICM   R9,B'0011',EXPINC  MOVE IN EXPANSION INCREMENTS
         CVD   R9,WORKCVD         CONVERT TO DECIMAL
         UNPK  NUM5,WORKCVD       UNPACK
         OI    NUM5+L'NUM5-1,X'F0' MAKE IT PRINTABLE
         MVC   OUT4,NUM5          MOVE TO SYSOUT
********************
*  SETUP MAX USED  *
********************
         SR    R9,R9              CLEAR REGISTER 9
         ICM   R9,B'1111',MAXUSED MOVE IN MAX USED
         CVD   R9,WORKCVD         CONVERT TO DECIMAL
         UNPK  NUM8,WORKCVD       UNPACK
         OI    NUM8+L'NUM8-1,X'F0' MAKE IT PRINTABLE
         MVC   OUT5,NUM8          MOVE TO SYSOUT
**************************
*  SETUP TIMES EXPANDED  *
**************************
         SR    R9,R9              CLEAR REGISTER 9
         ICM   R9,B'1111',EXPTME  MOVE IN TIMES EXPANDED
         CVD   R9,WORKCVD         CONVERT TO DECIMAL
         UNPK  NUM8,WORKCVD       UNPACK
         OI    NUM8+L'NUM8-1,X'F0' MAKE IT PRINTABLE
         MVC   OUT6,NUM8          MOVE TO SYSOUT
*********************
*  SETUP MAX TOTAL  *
*********************
         SR    R9,R9              CLEAR REGISTER 9
         ICM   R9,B'1111',MAXTOT  MOVE IN MAX TOTAL
         CVD   R9,WORKCVD         CONVERT TO DECIMAL
         UNPK  NUM8,WORKCVD       UNPACK
         OI    NUM8+L'NUM8-1,X'F0' MAKE IT PRINTABLE
         MVC   OUT7,NUM8          MOVE TO SYSOUT
*************************
*  SETUP CURRENT TOTAL  *
*************************
         SR    R9,R9              CLEAR REGISTER 9
         ICM   R9,B'1111',CURRTOT MOVE IN CURRENT TOTAL
         CVD   R9,WORKCVD         CONVERT TO DECIMAL
         UNPK  NUM8,WORKCVD       UNPACK
         OI    NUM8+L'NUM8-1,X'F0' MAKE IT PRINTABLE
         MVC   OUT8,NUM8          MOVE TO SYSOUT
*********************************
*  SETUP CONTRACTION THRESHOLD  *
*********************************
         SR    R9,R9              CLEAR REGISTER 9
         ICM   R9,B'0011',CONTHR  MOVE IN CONTRACTION THRESHOLD
         CVD   R9,WORKCVD         CONVERT TO DECIMAL
         CH    R9,=X'7FFF'        DOES POOL HAVE EXPANSION ?
         BNE   I0000              YES, CONTINUE
         MVC   NUM5,=C'*N/A*'     NO, SET OUTPUT
         B     J0000              BRANCH AROUND WORK
I0000    UNPK  NUM5,WORKCVD       UNPACK
         OI    NUM5+L'NUM5-1,X'F0' MAKE IT PRINTABLE
J0000    MVC   OUT9,NUM5          MOVE TO SYSOUT
*****************************
*  SETUP CURRENT AVAILABLE  *
*****************************
         SR    R9,R9              CLEAR REGISTER 9
         ICM   R9,B'1111',CURRVL
         CVD   R9,WORKCVD         CONVERT TO DECIMAL
         UNPK  NUM8,WORKCVD       UNPACK
         OI    NUM8+L'NUM8-1,X'F0' MAKE IT PRINTABLE
         MVC   OUT10,NUM8         MOVE TO SYSOUT
************************
*  PRINT ALL THE DATA  *
************************
         PUT   SYSOUT,LINE1       WRITE OUT BUFFER INFORMATION
         PUT   SYSOUT,LINE2       WRITE OUT BUFFER INFORMATION
         PUT   SYSOUT,LINE3       WRITE OUT BUFFER INFORMATION
         PUT   SYSOUT,LINE4       WRITE OUT BUFFER INFORMATION
         PUT   SYSOUT,LINE5       WRITE OUT BUFFER INFORMATION
***********************************************
*  BRANCH BACK AND GET THE NEXT POOL DETAILS  *
***********************************************
K0000    LA    R7,44(R7)          MOVE POINTER ALONG ARRAY
         BCT   R8,D0000           SUBTRACT ONE AND BRANCH
         CLOSE SYSOUT
*********
*  END  *
*********
EXIT     DS    0H                 RETURN
         L     R13,SAVEAREA+4     POINT TO CALLER'S SAVEAREA
         SR    R15,15             SET RETURN CODE TO 0
         LM    R14,R12,12(R13)    RELOAD CALLER'S REGISTERS
         BR    R14                RETURN TO CALLER
*************************
*  STORAGE DEFINITIONS  *
*************************
SAVEAREA DS    18F                USER SAVE AREA
NUMENTS  DS    CL4                NUMBER OF ENTRIES
WORKCVD  DS    D                  DOUBLE WORD WORK AREA
NUM5     DC    CL5'00000'         NUMBER
NUM8     DC    CL8'00000000'      NUMBER
SYSOUT   DCB   DDNAME=SYSOUT,DSORG=PS,MACRF=(PM),LRECL=80,             X
               BLKSIZE=80,RECFM=FB
POOLIDS  DC    C'IO00LP00WP00LF00CRPLSF00SP00AP00FFFF'
         LTORG
*******************
*  REPORT FORMAT  *
*******************
TITLE1   DS    0CL80              SYSOUT FORMAT
         DC    C'                            VTAM BUFFER POOL'
         DC    CL35' '
BLANKLNE DC    CL80' '
LINE1    DS    0CL80
OUT1     DC    CL5' '             POOL ID
         DC    CL10' '
         DC    C'BUFF SIZE  '
OUT2     DC    CL5' '             BUFFER SIZE
         DC    C'     EXP INCREMENT   '
OUT4     DC    CL5' '             EXPANSION INCREMENT
         DC    CL21' '
LINE2    DS    0CL80
         DC    CL15' '
         DC    C'TIMES EXP  '
OUT6     DC    CL10' '            TIMES EXPANDED
         DC    C'EXP/CONT THRESH '
OUT3     DC    CL5' '             EXPANSION THRESHOLD
         DC    C'/'
OUT9     DC    CL5' '             CONTRACTION THRESHOLD
         DC    CL16' '
LINE3    DS    0CL80
         DC    CL15' '
         DC    C'CURR TOTAL '
OUT8     DC    CL10' '            CURRENT TOTAL
         DC    C'CURR AVAILABLE  '
OUT10    DC    CL10' '            CURRENT AVAILABLE
         DC    CL16' '
LINE4    DS    0CL80
         DC    CL15' '
         DC    C'MAX TOTAL  '
OUT7     DC    CL10' '            MAX TOTAL
         DC    C'MAX USED        '
OUT5     DC    CL10' '            MAX USED
         DC    CL18' '
LINE5    DC    70C'-'
         DC    10C' '
**********************************
*  BUFFER POOL ARRAY  (12 X 44)  *
**********************************
POOL     DS    12CL44
POOLDEF  DSECT
********************************
*   BUFFER POOL ENTRY (BPENT)  *
********************************
POOLID   DS    F                  I/O BUFFER
FORMAT   DS    H                  POOL FORMAT
BUFFSIZE DS    H                  BUFFER SIZE
BPCBADDR DS    F                  ADDRESS OF BPCB
****************************************
*   BUFFER POOL CONTROL BLOCK  (BPCB)  *
****************************************
CURRTOT  DS    F                  CURRENT TOTAL
MAXUSED  DS    F                  MAX USED
CURRVL   DS    F                  CURRENT AVAILABLE
CONTHR   DS    F                  CONTRACTION THRESHOLD
EXPTME   DS    F                  TIMES EXPANDED
EXPTHR   DS    F                  EXPANSION THRESHOLD
MAXTOT   DS    F                  MAX TOTAL
EXPINC   DS    F                  EXPANSION INCREMENTS
*
         CSECT
         DC    C'END BUFFER POOL' EYE CATCH
         CVT   DSECT=YES          COMMUNICATIONS VECTOR TABLE
         $REGS
         END


Craig Brown
Systems Programmer
Australian Paper Manufacturers (Australia)

