NCCF CLIST for switched off controllers

Users on an SNA line on a 3725 controller with NCP
experienced slow response when any of the controllers on the
line were switched off.  These switched-off controllers were
minis or micros emulating 3274s.  A VTAM display on such a
line would show status PCTD2 for a switched-off controller.

An NCCF CLIST was written to periodically check the line,
vary off-line any controller with status PCTD2, and vary on-
line any controller which had been switched on.

The CLIST loops for every controller which is likely to be
turned off.  IST486I is the status line from a display command.
If ACTIV, it gets out of the loop; if PCTD2, it varies the
controller off-line; otherwise it varies it on-line and checks
whether the controller has been switched on recently.  The
count loop gives extra time if 'pending connect'.  Another
display must give an active status or it is varied off-line.  The
CLIST starts itself some time later.

PENDINGS CLIST
         &CONTROL ERR
         AUTOWRAP FULL
         CLEAR
         PURGE TIMER=PENDERS
         &PU1 = P02007A
         &PU2 = P02007C
         &PU3 = P02007D
         &PU4 = P02007E
         &PU5 = P02007F
         &PU6 = P02007J
         &PU7 = P02007G
         &PU8 = P02007H
         &PU9 = P02007I
         &MAXPU = 9
         &NO  = 1
-LOOP    &PUNIT = &PU&NO
         &WAIT 'D NET,ID=&PUNIT',IST486I=-FIRSTD
         &EXIT
-FIRSTD  CLEAR
         &IF &5 EQ ACTIV &THEN &GOTO -LOOPEND
         &IF &5 EQ PDISC &THEN &GOTO -LOOPEND
         &IF &5 EQ PCTD2 &THEN &GOTO -VAROFF
         &IF &5 EQ IINOP &THEN &GOTO -VARON
         &IF &5 EQ INACT &THEN &GOTO -VARON
         &IF &5 EQ NEVAC &THEN &GOTO -VARON
         &IF &5 EQ PCON2 &THEN &GOTO -VARON
         &WRITE COULD NOT RESOLVE STATUS OF IFS. &5 NOT FOUND
         &EXIT
-VARON   &WAIT 'V NET,ACT,SCOPE=ALL,ID=&PUNIT',IST093I=-FIRSTVA,+
                                               *10=-FIRSTVA
         &EXIT
-FIRSTVA &COUNT = 0
-COLOOP  &IF &COUNT GT 10 &THEN &GOTO -VAROFF
         &WAIT 'D NET,ID=&PUNIT',IST486I=-SECNDD
         &EXIT
-SECNDD  &COUNT = &COUNT + 1
         &IF &5 EQ PCON2 &THEN &GOTO -COLOOP
         &IF &5 EQ ACTIV &THEN &GOTO -LOOPEND
-VAROFF  &WAIT 'V NET,INACT,ID=&PUNIT',IST105I=-LOOPEND
         &EXIT
-LOOPEND CLEAR
         &NO = &NO + 1
         &IF &NO LE &MAXPU &THEN &GOTO -LOOP
         EVERY 5,ID=PENDERS,PENDINGS
         AUTOWRAP NO
         &EXIT 4


Derek Davidson
Systems Programmer
Province of New Brunswick (Canada)

