.* ------------------------------------------------------------------*
.*                                                                   *
.*                        TRACE MACRO                                *
.*                                                                   *
.*       One or two data elements (8 bytes each) may be captured at  *
.*       any trace point.  The data to be captured may be specified  *
.*       in either of two ways: DATA1 or RDATA1 and DATA2 or RDATA2. *
.*       DATA1 and RDATA1 are mutually exclusive as are DATA2 and    *
.*       RDATA2.  DATA1 and DATA2 specify the address of data to be  *
.*       captured, RDATA1 and RDATA2 specify a register whose        *
.*       contents are to be captured.                                *
.*                                                                   *
.*       For example:                                                *
.*                                                                   *
.*                 ITRACE ID=X1,DATA1=VALUE1,RDATA2=R4               *
.*                                                                   *
.*          In this case the trace entry would include the TCB       *
.*          address, CSECT name, ID "X1", 8 bytes of data            *
.*          beginning at value1's address and the contents of        *
.*          register 4.                                              *
.*                                                                   *
.*                                                                   *
.*                                                                   *
.*       The ID is different in this than other code I have written. *
.*       This is a trade-off to allow the trace entries to be 32     *
.*       bytes long and still capture all the information I wanted.  *
.*       Because there are multiple subtasks, I wanted to capture    *
.*       the TCB addresses.  TCB addresses are (at least as of       *
.*       z/OS 1.4) 24-bits, TCB addresses use 3 bytes per trace      *
.*       entry.  So... data per trace entry is..                     *
.*                                                                   *
.*              TCB address    3 bytes                               *
.*              CSECT name     8 bytes                               *
.*              ID             5 bytes                               *
.*              Data 1         8 bytes                               *
.*              DATA 2         8 bytes                               *
.*                                                                   *
.*                                                                   *
.* ------------------------------------------------------------------*
         MACRO
&NAME    ITRACE &ID=,&DATA1=,&DATA2=,&RDATA1=,&RDATA2=
         AIF   (T'&DATA1 NE 'O' AND T'&RDATA1 NE 'O').ERR0010
         AIF   (T'&DATA2 NE 'O' AND T'&RDATA2 NE 'O').ERR0020
         AIF   ('&SYSPARM' EQ 'NOTRACE').MEXIT
         AIF   (T'&DATA1 EQ 'O').DATA020
         AIF   ('&DATA1'(1,1) EQ '(').DATA010
         LA    R14,&DATA1            DATA ADDRESS
         MVC   TRACE_DATA_1,0(R14)   MOVE DATA
         AGO   .DATA030
.DATA010 ANOP
         MVC   TRACE_DATA_1,0&DATA1  MOVE DATA
         AGO   .DATA030
.DATA020 ANOP
         AIF   (T'&RDATA1 EQ 'O').DATA030
         STCM  &RDATA1,15,TRACE_DATA_1 SAVE REGISTER CONTENTS
.DATA030 ANOP
         AIF   (T'&DATA2 EQ 'O').DATA050
         AIF   ('&DATA2'(1,1) EQ '(').DATA040
         LA    R14,&DATA2            DATA ADDRESS
         MVC   TRACE_DATA_2,0(R14)   MOVE DATA
         AGO   .DATA060
.DATA040 ANOP
         MVC   TRACE_DATA_2,0&DATA2  MOVE DATA
         AGO   .DATA060
.DATA050 ANOP
         AIF   (T'&RDATA2 EQ 'O').DATA060
         STCM  &RDATA2,15,TRACE_DATA_2 SAVE REGISTER CONTENTS
.DATA060 ANOP
         BAL   R14,TRACE000          ENTER TRACE ROUTINE
         AIF      (T'&ID EQ 'O').NOID
&NAME    DC    CL8'&ID'              TRACE ID
         AGO   .ID
.NOID    ANOP
&NAME    DC    CL8'**NOID**'
.ID      ANOP
         MEXIT
.ERR0010 ANOP
         MNOTE 12,'DATA1 AND RDATA1 ARE MUTUALLY EXCLUSIVE'
         MEXIT
.ERR0020 ANOP
         MNOTE 12,'DATA2 AND RDATA2 ARE MUTUALLY EXCLUSIVE'
.MEXIT   ANOP
         MEXIT
         MEND
