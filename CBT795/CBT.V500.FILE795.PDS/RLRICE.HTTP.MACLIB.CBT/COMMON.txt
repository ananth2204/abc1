         MACRO
         COMMON &COMM,&TYPE=
         AIF   (T'&COMM EQ 'O').NOCOMM
         AIF   ('&TYPE' EQ 'DSECT').LBL0010
         AIF   ('&TYPE' EQ 'CSECT').LBL0010
         MNOTE 12,'TYPE MUST BE -DSECT- or -CSECT-'
         MEXIT
.LBL0010 ANOP
         AIF   ('&COMM' EQ 'MAIN').MAIN
         MNOTE 12,'UNKNOWN COMM TYPE'
         MEXIT
.MAIN    ANOP
         AIF   ('&TYPE' EQ 'DSECT').LBL0020
HTTPCOMM CSECT
         AGO   .LBL0030
.LBL0020 ANOP
HTTPCOMM DSECT
.LBL0030 ANOP
*---------------------------------------------------------------------*
*                                                                     *
*          ADD ITRACE ENTRY                                           *
*                                                                     *
*  During initialization the "main" module will have obtained storage *
*  for the ITRACE table. The "main" module will have set TRACE_FIRST, *
*  TRACE_LAST, AND TRACE_CURRENT. THE "MAIN" module will also have    * 1* o *
*  set R11 to label "COMM" at the very top of this CSECT.  All other  *
*  modules depend on this remaining unchanged.  Any module except this*
*  module may call the trace routine.  Macro ITRACE  generates the    *
*  calling sequence.  R14 is the only register altered by the ITRACE  *
*  macro.                                                             *
*                                                                     *
*  The trace table is a "wrap-around" table. TRACE_FIRST is the first *
*  entry's address, TRACE_LAST is the last entry's address, and       *
*  TRACE_CURRENT is the address of the last entry added to the table. *
*                                                                     *
*  Data captured in each ITRACE entry is:                             *
*        The TCB address of the calling task                          *
*        The calling CSECT's suffix                                   *
*        ID coded on the ITRACE macro                                 *
*        DATA1 or RDATA1 or zeros                                     *
*        DATA2 or RDATA2 or zeros                                     *
*                                                                     *
*                                                                     *
*      This code depends on:                                          *
*           R11  base address of this code                            *
*           R12  current module's base address                        *
*           R13  the standard work area address for the caller        *
*           R14  XTRACE ID'S ADDRESS AND R14 + 8 WILL BE THE RETURN   *
*                address                                              *
*                                                                     *
*---------------------------------------------------------------------*
TRACE000 DS    0H
         USING REGISTER_SAVE_AREA,R13             DEFINE BASE
         USING MODPRFX,R12                        DEFINE BASE
         USING TRACE_ENTRY,R2                     DEFINE BASE
         STM   R14,R12,12(R13)                    SAVE REGISTERS
         ICM   R2,15,COMM_LOG_QUEUE
         BZ    TRACE030
TRACE010 DS    0H
         CLC   X_LOGBLOK,0(R2)
         BNE   TRACE020
         ICM   R2,15,8(R2)
         BZ    TRACE030
         B     TRACE010
TRACE020 DS    0H
         DC    H'0'
TRACE030 DS    0H
         ICM   R2,15,TRACE_CURRENT                CURRENT TRACE ENTRY
         BZ    TRACD050                           NO TRACE TABLE
         OC    0(TRACE_ENTRY_LENGTH,R2),0(R2)     EMPTY ENTRY?
         BZ    TRACE040                           YES.. USE THIS ENTRY
         LA    R2,TRACE_ENTRY_LENGTH(R2)          NEXT TRACE ENTRY
         C     R2,TRACE_LAST                      BEYOND END OF TABLE?
         BNH   TRACE040                           NO
         L     R2,TRACE_FIRST                     'WRAP' TRACE TABLE
TRACE040 DS    0H
         L     R1,TRACE_CURRENT                   CURRENT TRACE ENTRY
         CS    R1,R2,TRACE_CURRENT                UPDATE CURRENT
         BC    4,TRACE030                         TRY AGAIN
         ST    R2,TRACE_CURRENT                   CURRENTY ENTRY ADDR
         USING PSA,R0                             DEFINE BASE
         MVC   TRACE_ENTRY_TCB,PSATNEW+1          COPY TCB ADDRESS
         MVC   TRACE_ENTRY_MODULE_NAME,PFXMODNM+3 COPY MODULE NAME
         MVC   TRACE_ENTRY_TRACE_ID,0(R14)        TRACE ID
         MVC   TRACE_ID,0(R14)
         MVC   TRACE_ENTRY_DATA_1,TRACE_DATA_1    COPY TRACE DATA 1
         MVC   TRACE_ENTRY_DATA_2,TRACE_DATA_2    COPY TRACE DATA 2
         XC    TRACE_DATA_1,TRACE_DATA_1          CLEAR TRACE DATA
         XC    TRACE_DATA_2,TRACE_DATA_2          CLEAR TRACE DATA
         TM    X_TRACE_FLAG,$X_TRACE_ON
         BNO   TRACD050                           NO
         MVC   X_TRACE_DATA(TRACE_ENTRY_LENGTH),TRACE_ENTRY
         MVI   X_TRACE_LENGTH+0,0                 SET LENGTH
         MVI   X_TRACE_LENGTH+1,36                SET LENGTH
         GETMAIN R,LV=72
         ST    R13,4(R1)
         ST    R1,8(R13)
         LR    R13,R1
         PUT   X_TRACE_DCB,X_TRACE_RECORD
         LR    R1,R13
         L     R13,4(R1)
         FREEMAIN R,A=(1),LV=72
TRACD050 DS    0H
         ICM   R2,15,COMM_LOG_QUEUE
         BZ    TRACE080
TRACD060 DS    0H
         CLC   X_LOGBLOK,0(R2)
         BNE   TRACE070
         ICM   R2,15,8(R2)
         BZ    TRACE080
         B     TRACD060
TRACE070 DS    0H
         DC    H'0'
TRACE080 DS    0H
         LM    R14,R12,12(R13)             SAVE REGISTERS
         B     8(R14)                      RETURN
X_LOGBLOK DC   CL8'LOGBLOK'
*---------------------------------------------------------------------*
*                                                                     *
*           TRACE CONTROL                                             *
*                                                                     *
*---------------------------------------------------------------------*
                    DS    0A
                    DC    CL4'1STX'        FIRST TRACE ENTRY
TRACE_FIRST         DC    A(0)
                    DC    CL4'LAST'        LAST TRACE ENTRY
TRACE_LAST          DC    A(0)
                    DC    CL4'CURR'        CURRENT XTRACE ENTRY
TRACE_CURRENT       DC    A(0)
TRACE_TABLE_ADDRESS DC    A(0)             TRACE TABLE STORAGE ADDRESS
TRACE_TABLE_SIZE    DC    A(1000*TRACE_ENTRY_LENGTH)
X_TRACE_FLAG        DC    X'00'
$X_TRACE_ON         EQU   X'80'            EXTERNAL TRACE DD PRESENT
$X_TRACE_OPEN       EQU   X'40'            EXTERNAL XTRACE IS OPEN

X_TRACE_RECORD      DS    0H
X_TRACE_LENGTH      DS    XL2
X_TRACE_BB          DS    XL2
X_TRACE_DATA        DS    (TRACE_ENTRY_LENGTH)C
X_TRACE_DCB    DCB DSORG=PS,                                           +
               DDNAME=HTTPXTR,                                         +
               RECFM=VB,                                               +
               LRECL=16000,                                            +
               BLKSIZE=16004,                                          +
               MACRF=PM
.*
.*   END OF COMMON COMMON
.*
*---------------------------------------------------------------------*
*                                                                     *
*     HTTP main address space common                                  *
*                                                                     *
*---------------------------------------------------------------------*

COMM_DXD_ADDRESS      DC   A(0)      DXD (WORK AREA) ADDRESS
                      DC   CL8'HTTPMAIN'
COMM_HTTPMAIN_ADDRESS DC   A(0)
                      DC   CL8'HTTPCONS'
COMM_HTTPCONS_ADDRESS DC   A(0)
                      DC   CL8'HTTPDAIR'
COMM_HTTPDAIR_ADDRESS DC   A(0)
                      DC   CL8'HTTPLOCL'
COMM_HTTPLOCL_ADDRESS DC   A(0)
                      DC   CL8'HTTPLOG'
COMM_HTTPLOG_ADDRESS  DC   A(0)
                      DC   CL8'HTTPPRT'
COMM_HTTPPRT_ADDRESS  DC   A(0)
                      DC   CL8'HTTPSERV'
COMM_HTTPSERV_ADDRESS DC   A(0)
                      DC   CL8'HTTPTCP '
COMM_HTTPTCP_ADDRESS  DC   A(0)
                      DC   CL8'HTTPURL '
COMM_HTTPURL_ADDRESS  DC   A(0)
                      DC   CL8'HTTPWEB'
COMM_HTTPWEB_ADDRESS  DC   A(0)
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
COMM_TERMINATION_ECB  DC   A(0)
COMM_MAIN_WORK_ECB    DC   A(0)
*---------------------------------------------------------------------*
*                                                                     *
*          PRINT SUBTASK CONTROL                                      *
*                                                                     *
*---------------------------------------------------------------------*
COMM_PRINT_TCB        DC   A(0)      PRINT SUBTASK'S TCB ADDRESS
COMM_PRINT_QUEUE      DC   A(0)      PRINT SUBTASK PRTBLOK QUEUE
COMM_PRINT_TERM_ECB   DC   A(0)      PRINT SUBTASK TERMINATION ECB
COMM_PRINT_WORK_ECB   DC   A(0)      PRINT WORK TO DO ECB
COMM_PRINT_WORK_AREA  DC   A(0)      PRINT SUBTASK'S WORK AREA
*---------------------------------------------------------------------*
*                                                                     *
*          Console subtask control                                    *
*                                                                     *
*---------------------------------------------------------------------*
COMM_CONSOLE_TCB      DC   A(0)      CONSOLE SUBTASK'S ECB ADDRESS
COMM_CONSOLE_TERM_ECB DC   A(0)      CONSOLE SUBTASK TERMINATION ECB
COMM_CONSOLE_WORK_ECB DC   A(0)      CONSOLE WORK TO DO ECB
COMM_CONSOLE_WORK_AREA DC  A(0)      CONSOLE SUBTASK'S WORK AREA
*---------------------------------------------------------------------*
*                                                                     *
*          Logging subtask control                                    *
*                                                                     *
*---------------------------------------------------------------------*
COMM_LOG_TCB          DC   A(0)      LOGGING SUBTASK'S ECB ADDRESS
COMM_LOG_TERM_ECB     DC   A(0)      LOGGING SUBTASK TERMINATION ECB
COMM_LOG_WORK_ECB     DC   A(0)      LOGGING WORK TO DO ECB
COMM_LOG_QUEUE        DC   A(0)      LOGBLOK QUEUE
COMM_LOG_WORK_AREA    DC   A(0)      LOGGING SUBTASK'S WORK AREA
*---------------------------------------------------------------------*
*                                                                     *
*          URL subtask control                                        *
*                                                                     *
*---------------------------------------------------------------------*
COMM_URL_TCB          DC   A(0)      URL SUBTASK'S ECB ADDRESS
COMM_URL_TERM_ECB     DC   A(0)      URL SUBTASK TERMINATION ECB
COMM_URL_WORK_ECB     DC   A(0)      URL WORK TO DO ECB
COMM_URL_QUEUE        DC   A(0)      URL QUEUE
COMM_URL_WORK_AREA    DC   A(0)      URL SUBTASK'S WORK AREA
*---------------------------------------------------------------------*
*                                                                     *
*          WEB COMMUNICATION SUBTASK                                  *
*                                                                     *
*---------------------------------------------------------------------*
COMM_WEB_TCB          DC   A(0)      WEB SUBTASK'S ECB ADDRESS
COMM_WEB_TERM_ECB     DC   A(0)      WEB SUBTASK TERMINATION ECB
COMM_WEB_WORK_ECB     DC   A(0)      WEB WORK TO DO ECB
COMM_WEB_INITIALIZED  DC   A(0)      WEB SUBTASK IS INITIALIZED
COMM_WEB_COMPLETED    DC   A(0)      COMPLETED REQUESTS
COMM_WEB_WORK_AREA    DC   A(0)      WEB SUBTASK'S WORK AREA
*---------------------------------------------------------------------*
*                                                                     *
*          Main to server communcation                                *
*                                                                     *
*---------------------------------------------------------------------*
COMM_HTTPTCP_TCB       DC  A(0)      TCP/IP SUBTASK'S ECB ADDRESS
COMM_HTTPTCP_TERM_ECB  DC  A(0)      TCP/IP SUBTASK TERMINATION ECB
COMM_HTTPTCP_WORK_ECB  DC  A(0)      TCP/IP WORK TO DO ECB
COMM_HTTPTCP_INITIALIZED DC A(0)     TCP/IP SUBTASK IS INITIALIZED
COMM_HTTPTCP_CONTACTS  DC  A(0)      NUMBER OF CONTACTS
COMM_HTTPTCP_WORK_AREA DC  A(0)      TCP/IP SUBTASK'S WORK AREA
                       CLIENT   PREFIX=COMM_HTTPTCP
*---------------------------------------------------------------------*
*                                                                     *
*          Local URL server subtask control                           *
*                                                                     *
*---------------------------------------------------------------------*
COMM_LOCAL_TCB        DC   A(0)      LOCAL URL TASK'S ECB ADDRESS
COMM_LOCAL_TERM_ECB   DC   A(0)      LOCAL URL TASK TERMINATION ECB
COMM_LOCAL_WORK_ECB   DC   A(0)      LOCAL URL TASK WORK TO DO ECB
COMM_LOCAL_URL_QUEUE  DC   A(0)      LOCAL URL TASK URL QUEUE
COMM_LOCAL_WORK_AREA  DC   A(0)      LOCAL URL SUBTASK'S WORK AREA
COMM_LOCAL_INIT_ECB   DC   A(0)      LOCAL URL SUBTASK IS INITIALIZED

*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
COMM_SERVERS          DC   A(0)      SERVER ADDRESS SPACE CHAIN
COMM_SERVER_ECB       DC   F'0'      ECB POSTED FOR NEW/LOST SERVERs
COMM_NEW_SERVERS      DC   A(0)      NEW SERVER SPACE CHAIN
COMM_LOST_SERVERS     DC   A(0)      LOST SERVER SPACE CHAIN


*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
COMM_NBR_URLS         DC     F'0'    NUMBER OF URLS IN DATASPACE
COMM_URL_SPACE_TOKEN  DC     XL8'00'
COMM_URL_SPACE_ALET   DC     XL4'0'

*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
COMM_URLS_RECEIVED    DC     F'0'
COMM_WEB_PORT         DC     H'0'
COMM_WEB_PORT_CHAR    DC     CL5'00000'
COMM_SERVER_PORT      DC     H'0'
COMM_SERVER_PORT_CHAR DC     CL5'00000'
COMM_MAX_LINES        DC     PL3'65'
COMM_SNAP_CLASS       DC     C' '
COMM_SNAP_NUMBER      DC     PL4'0'

*---------------------------------------------------------------------*
*            CLIENT ID for HTTPWEB                                    *
*---------------------------------------------------------------------*
                      CLIENT PREFIX=COMM_HTTPWEB

*---------------------------------------------------------------------*
*            CLIENT ID for HTTPLOCL                                   *
*---------------------------------------------------------------------*
                      CLIENT PREFIX=COMM_HTTPLOCL

*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
COMM_SUBTASKS         DC     X'00'
$CONSOLE_SUBTASK      EQU    X'80'   CONSOLE SUBTASK ACTIVE
$LOCAL_SUBTASK        EQU    X'40'   LOCAL URL SUBTASK ACTIVE
$LOGGING_SUBTASK      EQU    X'20'   LOGGING SUBTASK ACTIVE
$PRINT_SUBTASK        EQU    X'10'   PRINT SUBTASK ACTIVE
$WEB_SUBTASK          EQU    X'08'   WEB SUBTASK ACTIVE
$TCP_SUBTASK          EQU    X'04'   SERVER CONTACT SUBTASK ACTIVE
$URL_SUBTASK          EQU    X'02'   URL MANAGER SUBTASK ACTIVE

*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
COMM_FLAGS            DC     X'00'
$COMM_ABEND           EQU    X'80'   SERIOUS ERROR.. ABEND
$COMM_ABORT           EQU    X'40'   SERIOUS ERROR.. ABORT

*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
COMM_TRACE_FLAGS      DC     X'00'
$TRACE_URLS           EQU    X'80'

*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
COMM_EOJ              DC     X'00'
$EOJ_PHASE_1          EQU    X'80'
$EOJ_PHASE_2          EQU    X'40'
$EOJ_PHASE_3          EQU    X'20'
$EOJ_PHASE_4          EQU    X'10'

*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
COMM_BLANKS           DC     CL256' '

                      ETOA
                      ATOE

*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
URL_BUFFER_SIZE       EQU    32000
         COPY   TRENTRY
         COPY   MODPRFX
         IHAPSA DSECT=YES,LIST=NO
         MEND
