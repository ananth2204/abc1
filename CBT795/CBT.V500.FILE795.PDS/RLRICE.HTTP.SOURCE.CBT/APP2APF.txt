* ------------------------------------------------------------------- *
*                                                                     *
*   This application returns a list libraries in the system's         *
*   authorized libraries (APF) list.                                  *
*                                                                     *
* ------------------------------------------------------------------- *
APP2APF  CSECT
APP2APF  AMODE 31
APP2APF  RMODE ANY
         STM   R14,R12,12(R13)       SAVE REGISTERS
         LR    R12,R15               SET BASE REGISTER
         USING APP2APF,R12           DEFINE PROGRAM BASE
         B     INIT0000
MODID    DC    CL8'APP2APF'
         DC    CL8'&SYSDATE'
         DC    CL6'&SYSTIME'
INIT0000 DS    0H
         LR    R11,R1                COPY PARM BLOCK ADDRESS
         USING HTTP_PARMS,R11
         GETMAIN RU,                                                   +
               LV=WORKL,                                               +
               LOC=ANY
         ST    R1,8(R13)
         ST    R13,4(R1)
         LR    R13,R1
         USING WORK,R13
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         MVC   WORK_CSVAPF(CSVAPFL),CSVAPFI
         LH    R0,H8000
INIT0010 DS    0H
         ST    R0,WORK_ANSLEN
         GETMAIN RU,                                                   +
               LV=(0),                                                 +
               LOC=ANY
         ST    R1,WORK_ANSAREA
         LR    R10,R1
         USING APFHDR,R10
         CSVAPF REQUEST=LIST,                                          +
               ANSAREA=(R10),                                          +
               ANSLEN=WORK_ANSLEN,                                     +
               MF=(E,WORK_CSVAPF)
         LTR   R15,R15
         BZ    INIT0020
         TM    WORK_FLAG,$WORK_CSVERROR
         BO    ERR0010
         L     R0,WORK_ANSLEN
         MVC   WORK_ANSLEN,APFHTLEN
         FREEMAIN RU,                                                  +
               A=(10),                                                 +
               LV=(0)
         L     R0,WORK_ANSLEN
         B     INIT0010
INIT0020 DS    0H
         XC    HTTP_ALET,HTTP_ALET
         MVI   HTTP_FUNCTION,$HTTP_WRITE

         MVC   WORK_BUFFER(HTML_HEADERL),HTML_HEADER
         LA    R1,WORK_BUFFER
         ST    R1,HTTP_DATA
         LA    R1,HTML_HEADERL
         ST    R1,HTTP_LENGTH
         LA    R1,HTTP_PARMS
         L     R15,HTTP_HTTPSUB
         BALR  R14,R15

         L     R1,CVTPTR
         USING CVT,R1
         MVC   WORK_BUFFER(SYSTEM_HEADERL),SYSTEM_HEADER
         MVC   WORK_BUFFER+(SYSTEM_NAME-SYSTEM_HEADER)(8),CVTSNAME
         LA    R1,WORK_BUFFER
         ST    R1,HTTP_DATA
         LA    R1,SYSTEM_HEADERL
         ST    R1,HTTP_LENGTH
         LA    R1,HTTP_PARMS
         L     R15,HTTP_HTTPSUB
         BALR  R14,R15

         LA    R1,WORK_BUFFER
         ST    R1,HTTP_DATA
         MVI   HTTP_FUNCTION,$HTTP_WRITE
         LA    R1,APF_DETAILL
         ST    R1,HTTP_LENGTH
         L     R9,APFH#REC
         LR    R8,R10
         A     R8,APFHOFF
         USING APFE,R8
APF0000  DS    0H
         MVC   WORK_BUFFER(APF_DETAILL),APF_DETAIL
         MVC   WORK_BUFFER+(APF_DSNAME-APF_DETAIL)(44),APFEDSNAME
         MVC   WORK_BUFFER+(APF_VOLSER-APF_DETAIL)(6),APFEVOLUME
         LA    R1,HTTP_PARMS
         L     R15,HTTP_HTTPSUB
         BALR  R14,R15
         AH    R8,APFELEN
         BCT   R9,APF0000

         MVC   WORK_BUFFER(HTML_TRAILERL),HTML_TRAILER
         LA    R1,WORK_BUFFER
         ST    R1,HTTP_DATA
         LA    R1,HTML_TRAILERL
         ST    R1,HTTP_LENGTH
         LA    R1,HTTP_PARMS
         L     R15,HTTP_HTTPSUB
         BALR  R14,R15
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0010  DS    0H
         ST    R15,WORK_DWORD
         MVC   WORK_BUFFER(MSG01L),MSG01
         UNPK  WORK_BUFFER+(MSG01_RC-MSG01)(5),WORK_DWORD+2(3)
         TR    WORK_BUFFER+(MSG01_RC-MSG01)(4),HEXCHAR
         MVI   WORK_BUFFER+(MSG01_RC-MSG01)+4,C' '
         MVI   HTTP_FUNCTION,$HTTP_WRITE
         LA    R1,WORK_BUFFER
         ST    R1,HTTP_DATA
         LA    R1,MSG01L
         ST    R1,HTTP_LENGTH
         LA    R1,HTTP_PARMS
         L     R15,HTTP_HTTPSUB
         BALR  R14,R15
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         LR    R1,R13                  COPY WORK AREA ADDRESS
         L     R13,4(R13)              RESTORE CALLER'S R13
         FREEMAIN RU,A=(1),LV=WORKL    FREE THE WORK AREA
         L     R14,12(R13)             RESTORE R14
         SR    R15,R15
         LM    R0,R12,20(R13)
         BR    R14
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
CSVAPFI  CSVAPF MF=(L,WORK_CSV_PARMS,0D)
CSVAPFL  EQU    *-CSVAPFI

H8000               DC  H'8000'

HEXCHAR             EQU *-C'0'
                    DC  C'0123456789ABCDEF'

MSG01               DS  0C
                    DC  C'Error from CSVAPF '
MSG01_RC            DC  CL4' '
                    DC  C' '
MSG01L              EQU *-MSG01

HTML_HEADER         DS  0C
                    DC  C'<HTML>'
                    DC  C'<BODY>'
                    DC  C'<CENTER>'
                    DC  AL1($LF)
HTML_HEADERL        EQU *-HTML_HEADER
SYSTEM_HEADER       DS  0C
                    DC  C'<H1>Authorized library (APF) list for '
SYSTEM_NAME         DC  CL8' '
                    DC  C'</H1>'
                    DC  C'<PRE>'
SYSTEM_HEADERL      EQU *-SYSTEM_HEADER

HTML_TRAILER        DS  0C
                    DC  C'</CENTER>'
                    DC  C'</BODY>'
                    DC  C'</HTML>'
HTML_TRAILERL       EQU *-HTML_TRAILER
APF_DETAIL          DS  0C
APF_DSNAME          DC  CL44' '
                    DC  CL2' '
APF_VOLSER          DC  CL6' '
                    DC  C'<P>'
APF_DETAILL         EQU *-APF_DETAIL
* ------------------------------------------------------------------- *
*        WORK AREAS                                                   *
* ------------------------------------------------------------------- *
WORK                DSECT
                    DS    18F
WORK_DWORD          DS    D
WORK_CSVAPF         DS    0F,(CSVAPFL)X
WORK_ANSAREA        DS    F
WORK_ANSLEN         DS    F
WORK_FLAG           DS    X
$WORK_CSVERROR      EQU   X'80'
WORK_BUFFER         DS    CL256
WORKL               EQU   *-WORK
                    SUBPARM  TYPE=DSECT
         CVT  DSECT=YES,LIST=NO
         CSVAPFAA
         COPY REGEQU
$LF      EQU  X'0A'
         END  APP2APF
