* ------------------------------------------------------------------- *
*                                                                     *
*  Name: HTTPINIT                                                     *
*                                                                     *
*  PURPOSE: HTTP Initialization                                       *
*                                                                     *
*  This is LOADed and called by HTTPMAIN at start-up time.            *
*  This is to reduce the amount of code that would be needed only     *
*  at start-up.                                                       *
*                                                                     *
*     . Obtain the trace table storage                                *
*     . Verify the required DD statements are present                 *
*     . ATTACH the print subtask HTTPPRT                              *
*     . LOAD and link to the parameter reader HTTPPARM                *
*     . ATTACH the console subtask (HTTPCONS)                         *
*     . ATTACH to logging subtask (HTTPLOG)                           *
*     . Create a DATASPACE for the URLs                               *
*     . ATTACH the web subtask                                        *
*     . ATTACH the server space contact subtask (HTTPTCP)             *
*     . ATTACH the local URL subtask (HTTPLOCL)                       *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
HTTPINIT CSECT
HTTPINIT AMODE 31
HTTPINIT RMODE ANY
         USING HTTPINIT,R12                   DEFINE BASE
         USING HTTPCOMM,R11
         STM   R14,R12,12(R13)                SAVE ENTRY REGISTERS
         LR    R12,R15                        COPY BASE
         B     INIT0000                       BRANCH AROUND EYECATCHER
MODID    DC    CL8'HTTPINIT'                  MODULE NAME
         DC    CL8'&SYSDATE'                  DATE OF ASSEMBLY
         DC    CL6'&SYSTIME'                  TIME OF ASSEMBLY
INIT0000 DS    0H
         GETMAIN RU,                                                   +
               LV=DXDL,                                                +
               LOC=ANY
         ST    R13,4(R1)
         ST    R1,8(R13)
         LR    R13,R1
         USING DXDINIT,R13
* ------------------------------------------------------------------- *
*        Process PARM if given                                        *
*                                                                     *
*        PARM address is passed from HTTPMAIN in R6.                  *
*                                                                     *
*        If specified, the PARM is the number of trace table entries. *
*        The minimum is 500.                                          *
* ------------------------------------------------------------------- *
         LTR   R6,R6                          PARM GIVEN?
         BZ    INIT0030                       NO PARM
         SR    R1,R1                          CLEAR REGISTER
         ICM   R1,3,0(R6)                     LENGTH OF PARM
         BZ    INIT0030                       NO PARM
         SH    R1,H3
         DC    H'0'
         CH    R1,H5                          TOO MANY DIGITS?
         BH    INIT0010                       YES
         MVC   DXD_PARM_WORK(5),=C'00000'
         LA    R14,DXD_PARM_WORK+5
         SR    R14,R1                         MINUS LENGTH OF PARM
         BCTR  R1,0
         EX    R1,PARMMVC                     MOVE PARM
         PACK  DXD_DWORD,DXD_PARM_WORK
         CVB   R1,DXD_DWORD                   CONVERT TO BINARY
         MH    R1,=Y(TRACE_ENTRY_LENGTH)
         C     R1,TRACE_TABLE_SIZE            MORE THAN MINIMUM?
         BL    INIT0010                       NO
         ST    R1,TRACE_TABLE_SIZE
         B     INIT0030
PARMMVC  MVC   DXD_PARM_WORK(0),2(R6)
INIT0010 DS    0H
         OI    COMM_FLAGS,$COMM_ABORT
         GETMAIN RU,                                                   +
               LV=WTO02L,                                              +
               LOC=BELOW
         LR    R2,R1
         MVC   0(WTO02L,R2),WTO02
         WTO   MF=(E,(R2))
         FREEMAIN RU,                                                  +
               A=(R2),                                                 +
               LV=WTO02L
         B     EXIT0000
INIT0020 DS    0H
         GETMAIN RU,                                                   +
               LV=WTO03L,                                              +
               LOC=BELOW
         LR    R2,R1
         MVC   0(WTO03L,R2),WTO03
         WTO   MF=(E,(R2))
         FREEMAIN RU,                                                  +
               A=(R2),                                                 +
               LV=WTO03L
* ------------------------------------------------------------------- *
*        Acquire storage for trace table                              *
* ------------------------------------------------------------------- *
INIT0030 DS    0H
         L     R3,TRACE_TABLE_SIZE            TRACE TABLE SIZE
         GETMAIN RU,                          ACQUIRE TRACE TABLE      +
               LV=(R3),                       .. SIZE IS IN R3         +
               LOC=ANY                        .. MAY BE ABOVE OR BELOW
         ST    R1,TRACE_TABLE_ADDRESS         TRACE TABLE ADDRESS
         SR    R0,R0                          ASSUME 32-BYTE BOUNDARY
         TM    TRACE_TABLE_ADDRESS+3,X'07'    ON 32-BYTE BOUNDARY?
         BNO   INIT0040                       YES
         SRL   R1,5                           SHIFT OUT LOW BITS ..
         SLL   R1,5                           .. TO ROUND
         LA    R1,32(R1)                      PLUS 32
         LA    R0,TRACE_ENTRY_LENGTH
INIT0040 DS    0H
         ST    R1,TRACE_FIRST                 FIRST TRACE ENTRY
         ST    R1,TRACE_CURRENT               CURRENT TRACE ENTRY
         A     R1,TRACE_TABLE_SIZE            LAST ENTRY IN TABLE
         SH    R1,=Y(TRACE_ENTRY_LENGTH)      MINUS 1 ENTRY
         SR    R1,R0                          MINUS 1 MORE IF NECESSARY
         ST    R1,TRACE_LAST                  LAST TRACE ENTRY ADDRESS


* ---- ITRACE is availble after this point


* ------------------------------------------------------------------- *
*        Determine DD's present                                       *
* ------------------------------------------------------------------- *
         USING PSA,R0                         DEFINE BASE
         L     R1,PSATNEW                     OUR TCB ADDRESS FROM PSA
         USING TCB,R1                         DEFINE TCB BASE
         L     R2,TCBTIO                      TIOT ADDRESS FROM OUR TCB
         USING TIOT1,R2                       DEFINE TIOT BASE
         LA    R3,TIOENTRY                    FIRST TIOT ENTRY
         USING TIOENTRY,R3                    DEFINE TIOT ENTRY BASE
         SR    R4,R4                          CLEAR FOR LENGTHS
INIT0050 DS    0H
         ICM   R4,1,TIOELNGH                  LENGTH OF THIS ENTRY
         BZ    INIT0090                       END OF TABLE
         ITRACE ID=TIOTDDNM,                  TRACE EACH TIOT ENTRY    +
               DATA1=(TIOEDDNM,8)
         LA    R5,DD_TABLE                    REQUIRED DD NAME TABLE
         USING DD_DSECT,R5                    DEFINE BASE
INIT0060 DS    0H
         CLI   0(R5),X'FF'                    END OF TABLE?
         BE    INIT0080                       YES, NOT IN TABLE
         CLC   TIOEDDNM,DD_NAME               DD NAME FOUND?
         BE    INIT0070                       YES
         LA    R5,DD_LENGTH(R5)               NEXT DD
         B     INIT0060                       LOOP
INIT0070 DS    0H
         ITRACE ID=SETDD,                     SETTING DD ID            +
               DATA1=(TIOEDDNM,8),            .. DDNAME                +
               DATA2=(DD_ID,8)                .. FLAG VALUE
         OC    DXD_DD,DD_ID                   TURN ON MATCHING ID
INIT0080 DS    0H
         AR    R3,R4                          NEXT ENTRY
         B     INIT0050                       LOOP
INIT0090 DS    0H
         ITRACE ID=TIOTDONE,                  TIOT SCAN COMPLETE       +
               DATA1=(DXD_DD,8)               .. INCLUDE FLAG BYTE
         TM    DXD_DD,X'40'                   PRINT DD PRESENT?
         BO    INIT0100                       YES
         GETMAIN RU,                          ACQUIRE STORAGE FOR WTO  +
               LV=WTO01L,                     .. STORAGE SIZE REQUIRED +
               LOC=BELOW                      .. FROM BELOW THE LINE
         LR    R2,R1                          COPY STORAGE ADDRESS
         MVC   0(WTO01L,R2),WTO01             COPY WTO
         WTO   MF=(E,(R2))                    ISSUE WTO
         FREEMAIN RU,                                                  +
               A=(R2),                                                 +
               LV=WTO01L
         ABEND 1,DUMP,,USER
* ------------------------------------------------------------------- *
*        ATTACH print sub-task                                        *
* ------------------------------------------------------------------- *
INIT0100 DS    0H
         MVC   DXD_ATTACH(ATTCHL),ATTCHI
         MVC   DXD_SUBTASK,HTTPPRT            TASK BEING ATTACHED
         LA    R2,COMM_PRINT_TERM_ECB
         LA    R3,DXD_ATTACH_PARM             PARM LIST ADDRESS
         ITRACE ID=ATCHPRT                    ATTACHING PRINT SUBTASK
         ATTACH EP=HTTPPRT,                   ATTACH PRINT SUBTASK     +
               PARAM=((R11)),                 .. COMMON MODULE ADDR    +
               ECB=(R2),                      .. TERMINATION ECB       +
               PURGE=QUIESCE,                 .. FINISH UP I/O         +
               MF=(E,(R3)),                   .. SUBTASK PARM LIST     +
               SF=(E,DXD_ATTACH)              ..
         STH   R15,DXD_RC                     SAVE RETURN CODE
         LTR   R15,R15                        ATTACH SUCCESSFUL?
         BNZ   ERR0010                        NO
         ST    R1,COMM_PRINT_TCB              TCB ADDRESS
         ITRACE ID=PRTOK,                     PRINT SUBTASK ATTACHED   +
               RDATA1=R1                      .. TCB ADDRESS
         OI    COMM_SUBTASKS,$PRINT_SUBTASK
* ------------------------------------------------------------------- *
*        Print start-up messages                                      *
* ------------------------------------------------------------------- *
         MVC   PRT_CC(PRT_LRECL),COMM_BLANKS
         MVI   PRT_COMMAND,$PRT_HEAD
         BAL   R8,PRT0000
         MVI   PRT_COMMAND,$PRT_PRINT
         MVC   PRT_MESSAGE_ID(MSG04L),MSG04
         BAL   R8,PRT0000
* ------------------------------------------------------------------- *
*        Make sure all required DD's are present                      *
* ------------------------------------------------------------------- *
         LA    R5,DD_TABLE                    FIRST DDNAME
         SR    R2,R2                          CLEAR REGISTER
INIT0110 DS    0H
         TM    DD_FLAG,$DD_REQUIRED           IS THIS A REQUIRED DD?
         BNO   INIT0120                       NO
         IC    R2,DD_ID                       INSERT DD ID
         EX    R2,DDTM                        IS DD PRESENT?
         BO    INIT0120                       YES
         MVC   PRT_MESSAGE_ID(MSG05L),MSG05
         MVC   PRT_MESSAGE_ID+(MSG05D-MSG05)(L'MSG05D),DD_NAME
         BAL   R8,PRT0000                     PRINT MESSAGE
         OI    COMM_FLAGS,$COMM_ABORT
INIT0120 DS    0H
         LA    R5,DD_LENGTH(R5)               NEXT DD
         CLI   0(R5),X'FF'                    END OF TABLE?
         BNE   INIT0110                       NO
         TM    COMM_FLAGS,$COMM_ABORT         ABORT FLAG SET?
         BNO   INIT0130                       NO
         MVC   PRT_MESSAGE_ID(MSG06L),MSG06
         BAL   R8,PRT0000                     PRINT MESSAGE
         DC    H'0'
         B     EXIT0000                       EXIT
DDTM     TM    DXD_DD,X'00'                   TEST FOR PRESENCE OF DD
* ------------------------------------------------------------------- *
*        Call parameter interpreter                                   *
* ------------------------------------------------------------------- *
INIT0130 DS    0H
         LOAD  EP=HTTPPARM                    LOAD PARM CONVERTER
         LR    R15,R0                         PARAMETER READER
         BALR  R14,R15                        LINK TO PARAMETER READER
         DELETE EP=HTTPPARM                   DELETE PARAMETER READER
         TM    COMM_FLAGS,$COMM_ABORT         ERRORS IN PARAMETERS?
         BNO   INIT0140                       NO
         MVC   PRT_MESSAGE_ID(MSG09L),MSG09
         BAL   R8,PRT0000                     PRINT MESSAGE
         B     EXIT0000                       EXIT
* ------------------------------------------------------------------- *
*        ATTACH console subtask                                       *
* ------------------------------------------------------------------- *
INIT0140 DS    0H
         LA    R2,COMM_CONSOLE_TERM_ECB
         LA    R3,DXD_ATTACH_PARM             PARM LIST ADDRESS
         MVC   DXD_SUBTASK,HTTPCONS           SUBTASK BEING ATTACHED
         ITRACE ID=ATCHCONS                   ATTACHING CONSOLE SUBTASK
         ATTACH EP=HTTPCONS,                  ATTACH CONSOLE SUBTASK   +
               PARAM=((R11)),                 .. COMMON                +
               ECB=(R2),                      .. TERMINATION ECB       +
               PURGE=QUIESCE,                 .. FINISH UP I/O         +
               MF=(E,(R3)),                   .. SUBTASK PARM LIST     +
               SF=(E,DXD_ATTACH)              ..
         LTR   R15,R15                        ATTACH SUCCESSFUL?
         BNZ   ERR0010                        NO
         ST    R1,COMM_CONSOLE_TCB            TCB ADDRESS
         ITRACE ID=CONSOK,                    CONSOLE SUBTASK ATTACHED +
               RDATA1=R1                      .. TCB ADDRESS
         OI    COMM_SUBTASKS,$CONSOLE_SUBTASK
         MVC   PRT_MESSAGE_ID(MSG07L),MSG07
         MVC   PRT_MESSAGE_ID+(MSG07TSK-MSG07)(L'MSG07TSK),HTTPCONS
         MVC   PRT_MESSAGE_ID+(MSG07DSC-MSG07)(L'CSUB_DESC),CSUB_DESC
         BAL   R8,PRT0000                     PRINT MESSAGE
* ------------------------------------------------------------------- *
*        ATTACH logging subtask                                       *
* ------------------------------------------------------------------- *
         LA    R2,COMM_LOG_TERM_ECB
         LA    R3,DXD_ATTACH_PARM             PARM LIST ADDRESS
         MVC   DXD_SUBTASK,HTTPLOG            SUBTASK BEING ATTACHED
         ITRACE ID=ATCHLOG                    ATTACHING LOG SUBTASK
         ATTACH EP=HTTPLOG,                   ATTACH LOGGING SUBTASK   +
               PARAM=((R11)),                 .. COMMON                +
               ECB=(R2),                      .. TERMINATION ECB       +
               PURGE=QUIESCE,                 .. FINISH UP I/O         +
               MF=(E,(R3)),                   .. SUBTASK PARM LIST     +
               SF=(E,DXD_ATTACH)              ..
         LTR   R15,R15                        ATTACH SUCCESSFUL?
         BNZ   ERR0010                        NO
         ST    R1,COMM_LOG_TCB                TCB ADDRESS
         ITRACE ID=LOGOK,                     LOGGING SUBTASK ATTACHED +
               RDATA1=R1                      .. TCB ADDRESS
         OI    COMM_SUBTASKS,$LOGGING_SUBTASK
         MVC   PRT_MESSAGE_ID(MSG07L),MSG07
         MVC   PRT_MESSAGE_ID+(MSG07TSK-MSG07)(L'MSG07TSK),HTTPLOG
         MVC   PRT_MESSAGE_ID+(MSG07DSC-MSG07)(L'CSUB_DESC),LOG_DESC
         BAL   R8,PRT0000                     PRINT MESSAGE
* ------------------------------------------------------------------- *
*        ATTACH URL manager subtask                                   *
* ------------------------------------------------------------------- *
         LA    R2,COMM_URL_TERM_ECB
         LA    R3,DXD_ATTACH_PARM             PARM LIST ADDRESS
         MVC   DXD_SUBTASK,HTTPURL            SUBTASK BEING ATTACHED
         ITRACE ID=ATCHURL                    ATTACHING URL SUBTASK
         ATTACH EP=HTTPURL,                   ATTACH URL SUBTASK       +
               PARAM=((R11)),                 .. COMMON                +
               ECB=(R2),                      .. TERMINATION ECB       +
               PURGE=QUIESCE,                 .. FINISH UP I/O         +
               MF=(E,(R3)),                   .. SUBTASK PARM LIST     +
               SF=(E,DXD_ATTACH)              ..
         LTR   R15,R15                        ATTACH SUCCESSFUL?
         BNZ   ERR0010                        NO
         ST    R1,COMM_URL_TCB                TCB ADDRESS
         ITRACE ID=URLOK,                     URL SUBTASK ATTACHED     +
               RDATA1=R1                      .. TCB ADDRESS
         OI    COMM_SUBTASKS,$URL_SUBTASK
         MVC   PRT_MESSAGE_ID(MSG07L),MSG07
         MVC   PRT_MESSAGE_ID+(MSG07TSK-MSG07)(L'MSG07TSK),HTTPURL
         MVC   PRT_MESSAGE_ID+(MSG07DSC-MSG07)(L'CSUB_DESC),URL_DESC
         BAL   R8,PRT0000                     PRINT MESSAGE
* ------------------------------------------------------------------- *
*        ATTACH WEB subtask                                           *
* ------------------------------------------------------------------- *
         LA    R2,COMM_WEB_TERM_ECB
         LA    R3,DXD_ATTACH_PARM             PARM LIST ADDRESS
         MVC   DXD_SUBTASK,HTTPWEB            SUBTASK BEING ATTACHED
         ITRACE ID=ATCHWEB                    ATTACHING WEB SUBTASK
         ATTACH EP=HTTPWEB,                   ATTACH WEB SUBTASK       +
               PARAM=((R11)),                 .. HTTPCOMM ADDRESS      +
               ECB=(R2),                      .. TERMINATION ECB       +
               PURGE=QUIESCE,                 .. FINISH UP I/O         +
               MF=(E,(R3)),                   .. SUBTASK PARM LIST     +
               SF=(E,DXD_ATTACH)              ..
         LTR   R15,R15                        ATTACH SUCCESSFUL?
         BNZ   ERR0010                        NO
         ST    R1,COMM_WEB_TCB                TCB ADDRESS IN HTTPCOMM
         ITRACE ID=WEBOK,                     WEB SUBTASK ATTACHED     +
               RDATA1=R1                      .. TCB ADDRESS
         OI    COMM_SUBTASKS,$WEB_SUBTASK
         MVC   PRT_MESSAGE_ID(MSG07L),MSG07
         MVC   PRT_MESSAGE_ID+(MSG07TSK-MSG07)(L'MSG07TSK),HTTPWEB
         MVC   PRT_MESSAGE_ID+(MSG07DSC-MSG07)(L'CSUB_DESC),WEB_DESC
         BAL   R8,PRT0000                     PRINT MESSAGE
         ITRACE ID=INITWAIT
         WAIT  ECB=COMM_WEB_INITIALIZED       ALLOW TASK TO INITIALIZE
* ------------------------------------------------------------------- *
*        ATTACH SERVER space initial contact subtask                  *
* ------------------------------------------------------------------- *
         LA    R2,COMM_HTTPTCP_TERM_ECB
         LA    R3,DXD_ATTACH_PARM             PARM LIST ADDRESS
         MVC   DXD_SUBTASK,HTTPTCP            SUBTASK BEING ATTACHED
         ITRACE ID=ATCHTCP                    ATTACHING SERVER CONTACT
         ATTACH EP=HTTPTCP,                   ATTACH SERVER CONTACT    +
               PARAM=((R11)),                 .. HTTPCOMM ADDRESS      +
               ECB=(R2),                      .. TERMINATION ECB       +
               PURGE=QUIESCE,                 .. FINISH UP I/O         +
               MF=(E,(R3)),                   .. SUBTASK PARM LIST     +
               SF=(E,DXD_ATTACH)              ..
         LTR   R15,R15                        ATTACH SUCCESSFUL?
         BNZ   ERR0010                        NO
         ST    R1,COMM_HTTPTCP_TCB            TCB ADDRESS IN HTTPCOMM
         ITRACE ID=HTTPTCP_OK,                WEB SUBTASK ATTACHED     +
               RDATA1=R1                      .. TCB ADDRESS
         OI    COMM_SUBTASKS,$TCP_SUBTASK
         MVC   PRT_MESSAGE_ID(MSG07L),MSG07
         MVC   PRT_MESSAGE_ID+(MSG07TSK-MSG07)(L'MSG07TSK),HTTPTCP
         MVC   PRT_MESSAGE_ID+(MSG07DSC-MSG07)(L'CSUB_DESC),TCP_DESC
         BAL   R8,PRT0000                     PRINT MESSAGE
         ITRACE ID=INITWAIT
         WAIT  ECB=COMM_HTTPTCP_INITIALIZED   ALLOW TASK TO INITIALIZE
* ------------------------------------------------------------------- *
*        ATTACH local URL subtask                                     *
* ------------------------------------------------------------------- *
         LA    R2,COMM_LOCAL_TERM_ECB
         LA    R3,DXD_ATTACH_PARM             PARM LIST ADDRESS
         MVC   DXD_SUBTASK,HTTPLOCL           SUBTASK BEING ATTACHED
         ITRACE ID=ATCHLOCL                   ATTACH LOCAL URL SUBTASK
         ATTACH EP=HTTPLOCL,                  ATTACH LOCAL URL SUBTASK +
               PARAM=((R11)),                 .. COMMON                +
               ECB=(R2),                      .. TERMINATION ECB       +
               PURGE=QUIESCE,                 .. FINISH UP I/O S       +
               MF=(E,(R3)),                   .. SUBTASK PARM LIST     +
               SF=(E,DXD_ATTACH)              ..
         LTR   R15,R15                        ATTACH SUCCESSFUL?
         BNZ   ERR0010                        NO
         ST    R1,COMM_LOCAL_TCB              TCB ADDRESS
         ITRACE ID=LOCLOK,                    URL SUBTASK ATTACHED     +
               RDATA1=R1                      .. TCB ADDRESS
         OI    COMM_SUBTASKS,$LOCAL_SUBTASK
         MVC   PRT_MESSAGE_ID(MSG07L),MSG07
         MVC   PRT_MESSAGE_ID+(MSG07TSK-MSG07)(L'MSG07TSK),HTTPLOCL
         MVC   PRT_MESSAGE_ID+(MSG07DSC-MSG07)(L'LOCAL_DESC),LOCAL_DESC
         BAL   R8,PRT0000                     PRINT MESSAGE
         WAIT  ECB=COMM_LOCAL_INIT_ECB        WAIT FOR INITIALIZATION
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
PRT0000  DS    0H
         ITRACE ID=PRINT
         GETMAIN RU,                                                   +
               LV=PRTBLOKL,                                            +
               LOC=ANY
         MVC   0(PRTBLOKL,R1),PRTBLOK
         MVC   PRT_CC(PRT_LRECL),COMM_BLANKS
PRT0010  DS    0H
         L     R14,COMM_PRINT_QUEUE           FIRST BLOCK ON QUEUE
         ST    R14,PRT_NEXT-PRTBLOK(R1)
         CS    R14,R1,COMM_PRINT_QUEUE        ADD NEW BLOCK TO CHAIN
         BC    4,PRT0010                      TRY AGAIN
         POST  COMM_PRINT_WORK_ECB            WAKE UP THE PRINT SUBTASK
         BR    R8                             COMPLETE
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0010  DS    0H
         ITRACE ID=ATCHFAIL                   SUBTASK ATTACH FAILED
         STH   R15,DXD_RC                     SAVE RETURN CODE
         MVC   PRT_MESSAGE_ID(MSG12L),MSG12
         MVC   PRT_MESSAGE_ID+(MSG12T-MSG12)(L'MSG12),DXD_SUBTASK
         UNPK  PRT_MESSAGE_ID+(MSG12R-MSG12)(5),DXD_RC(3)
         TR    PRT_MESSAGE_ID+(MSG12R-MSG12)(L'MSG12R),HEXCHAR
         MVI   PRT_MESSAGE_ID+(MSG12R-MSG12)+4,C' '
         BAL   R8,PRT0000                     PRINT MESSAGE
         ABEND 2,DUMP,,USER
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         LR    R2,R13
         L     R13,4(R13)
         FREEMAIN RU,                                                  +
               A=(R2),                                                 +
               LV=DXDL
         LM    R14,R12,12(R13)
         SR    R15,R15
         BR    R14
* ------------------------------------------------------------------- *
*                                                                     *
*        CONSTANTS                                                    *
*                                                                     *
* ------------------------------------------------------------------- *
H3          DC    H'3'
H5          DC    H'5'

HEXCHAR     EQU   *-C'0'
            DC    C'0123456789ABCDEF'

HTTPPRT     DC    CL8'HTTPPRT'
HTTPLOG     DC    CL8'HTTPLOG'
HTTPCONS    DC    CL8'HTTPCONS'
HTTPLOCL    DC    CL8'HTTPLOCL'
HTTPWEB     DC    CL8'HTTPWEB'
HTTPTCP     DC    CL8'HTTPTCP'
HTTPURL     DC    CL8'HTTPURL'
PRT_ID      DC    CL8'PRTBLOK'

DD_TABLE    DS    0C
            DC    CL8'SYSIN',AL1($DD_REQUIRED),X'80'
            DC    CL8'SYSPRINT',AL1($DD_REQUIRED),X'40'
            DC    X'FF'

CSUB_DESC   DC    CL35'console communication'
LOCAL_DESC  DC    CL35'local URL'
LOG_DESC    DC    CL35'Logging'
TCP_DESC    DC    CL35'TCP (server contact)'
URL_DESC    DC    CL35'URL (URL manager)'
WEB_DESC    DC    CL35'Web (web user contact)'

WTO01    WTO   'HTTPINIT01E * SYSPRINT  DD STATEMENT IS NOT PRESENT',  +L
               MF=L
WTO01L   EQU   *-WTO01
WTO02    WTO   'HTTPINIT02E too many digits in parm (5 is max)',MF=L
WTO02L   EQU   *-WTO02
WTO03    WTO   'HTTPINIT03E trace size in PARM is less than 500',MF=L
WTO03L   EQU   *-WTO03
MSG04    DS    0C
         DC    CL15'HTTPINIT04I'
         DC    C'Initialization starting'
MSG04L   EQU   *-MSG04
MSG05    DS    0C
         DC    CL15'HTTPINIT05E'
         DC    C'Required DD '
MSG05D   DC    CL8' '
         DC    C' is not present'
MSG05L   EQU   *-MSG05
MSG06    DS    0C
         DC    CL15'HTTPINIT06E'
         DC    C'Add required DD(s) to your JCL and re-submit'
MSG06L   EQU   *-MSG06
MSG07    DC    CL15'HTTPINIT07I '
MSG07TSK DC    CL8' '
         DC    C' ('
MSG07DSC DC    CL35' '
         DC    C')'
         DC    C' attached'
MSG07L   EQU   *-MSG07

WTO08    WTO   'HTTPINIT08I HTTP main initialized',MF=L
WTO08L   EQU   *-WTO08

MSG09    DS    0C
         DC    CL15'HTTPINIT09I'
         DC    C'Error(s) have been detected. Aborting.'
MSG09L   EQU   *-MSG09

MSG12    DS    0C
         DC    CL15'HTTPINIT12E'
         DC    C'ATTACH failed for '
MSG12T   DC    CL8' '
         DC    C'  RC='
MSG12R   DC    CL5' '
         DC    C' '
MSG12L   EQU   *-MSG12
ATTCHI   ATTACH EP=HTTPCONS,        ATTACH LIST FORM                   +
               ECB=*,               .. TERMINATION ECB'S ADDRESS       +
               PURGE=QUIESCE,       .. FINISH UP I/O IN PROGRESS       +
               SF=L                 .. LIST FORM
ATTCHL   EQU   *-ATTCHI
         LTORG
* ------------------------------------------------------------------- *
*                                                                     *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
DXDINIT          DSECT
                 STDSAVE DSECT=NO
DXD_ID           DS       CL8
DXD_DWORD        DS       D
DXD_ATTACH_PARM  DS       A                ATTACH PARM
DXD_URLS         DS       F
DXD_ATTACH       DS       (ATTCHL)X        ATTACH LIST FORM

DXD_RC           DS       H
DXD_DD           DS       X                DD'S PRESENT
DXD_SUBTASK      DS       CL8              SUBTASK ATTACHED/DETACHED
DXD_FLAGS        DS       X
$SNAPPED         EQU      X'80'
DXD_PARM_WORK    DS       CL5
                 PRTBLOK  DSECT=NO
DXDL             EQU      *-DXDINIT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COMMON   MAIN,TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 WEBURL
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 SERVBLOK DSECT=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DD_DSECT         DSECT
DD_NAME          DS    CL8
DD_FLAG          DS    X
$DD_REQUIRED     EQU   X'80'
DD_ID            DS    X
DD_LENGTH        EQU   *-DD_DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 IKJTCB   LIST=NO
                 IEFTIOT1
SERVER_IO_SIZE   EQU      32760
                 COPY     REGEQU
                 END   HTTPINIT
