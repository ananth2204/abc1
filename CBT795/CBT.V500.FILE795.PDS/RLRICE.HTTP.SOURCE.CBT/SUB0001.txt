* ------------------------------------------------------------------- *
*                                                                     *
*     This subroutine determines the "DSORG" of a given data set.     *
*                                                                     *
*     The subroutine requires two parameters:                         *
*       1)  the data set's name (44 characters)                       *
*       2)  the returned data set organization (1 byte)               *
*                                                                     *
*     Returned values for the DSORG are:                              *
*         'P'    ... Partitioned (PO)                                 *
*         'S'    ... Physically sequential (PS)                       *
*         'U'    ... Unknown (not PO, PS, or VSAM)                    *
*         'V'    ... VSAM                                             *
*         'X'    ... Not cataloged                                    *
*                                                                     *
* ------------------------------------------------------------------- *
SUB0001  CSECT
SUB0001  AMODE 31
SUB0001  RMODE ANY
         USING SUB0001,R12           DEFINE BASE
         STM   R14,R12,12(R13)       SAVE ENTRY REGISTERS
         LR    R12,R15               COPY BASE
         B     INIT0000              BRANCH AROUND EYECATCHER
MODID    DC    CL8'SUB0001'          MODULE NAME
         DC    CL8'&SYSDATE'         DATE OF ASSEMBLY
         DC    CL6'&SYSTIME'         TIME OF ASSEMBLY
INIT0000 DS    0H
         LR    R10,R1                COPY PARM LIST ADDRESS
         USING PARM_LIST,R10         DEFINE BASE
         GETMAIN RU,                                                   +
               LOC=BELOW,                                              +
               LV=WKL
         ST    R13,4(R1)
         ST    R1,8(R13)
         LR    R13,R1
         USING WK,R13                DEFINE BASE
         LA    R1,WKCSIRC            CATALOG SYSTEM RETURN CODE
         ST    R1,WKCSIPL            SET ADDRESS IN PARMLIST
         LA    R1,WKCSISP            CATALOG SYSTEM SEARCH PARMS
         ST    R1,WKCSIPL+4          SET ADDRESS IN PARMLIST
         MVC   WKCSISP(CSISPL),CSISPI
         L     R2,CSISIZE            SIZE OF CATALOG SEARCH DATA AREA
         GETMAIN RU,                                                   +
               LV=(R2),              .. SIZE OF AREA                   +
               LOC=ANY               ..
         ST    R1,WKCSIPL+8          SET ADDRESS IN PARMIST
         ST    R2,0(R1)              SET SIZE FOR CATALOG SEARCH
         MVC   WKCSISP(44),PARM_LIST_DSN  DATASET NAME
         LOAD  EP=IGGCSI00           LOAD THE CSI ENTERFACE
         LR    R15,R0                COPY THE ENTRY POINT
         LA    R1,WKCSIPL            CATALOG SEARCH PARM LIST
         BALR  R14,R15               LINK TO CSI
         DELETE EP=IGGCSI00          DELETE THE CSI INTERFACE
         OC    WKCSIRC+2(2),WKCSIRC+2
         BNZ   ERR0010               ERROR FROM CATALOG SEARCH
         L     R2,WKCSIPL+8          RETURNED DATA ADDRESS
         USING DATA,R2               DEFINE BASE
         L     R3,DATAUL             LENGTH OF DATA RETURNED
         LA    R2,DATADST            SKIP HEADER STUFF
         USING DATADST,R2
         SH    R3,=Y(DATADST-DATA)
PROC0010 DS    0H
         CLI   DATADST,$DATANVS      NONVSAM?
         BE    PROC0020              YES
         CLI   DATADST,$DATAVSC      VSAM CLUSTER?
         BE    PROC0100              YES
         CLI   DATADST,$DATAVSD      VSAM DATA COMPONENT?
         BE    PROC0100              YES
         CLI   DATADST,$DATAAIX      VSAM ALTERNATE INDEX?
         BE    PROC0100              YES
         CLI   DATADST,$DATAVSI      VSAM INDEX COMPONENT?
         BNE   ERR0010               NO.. SKIP
PROC0020 DS    0H
         MVC   WKCAMLST(CAMLSTL),CAMLSTI
         MVC   WKDSN,PARM_LIST_DSN
         MVC   WKVOL,DATAVOL
         LA    R0,WKDSN              DATASET NAME'S ADDRESS
         ST    R0,WKCAMLST+4         SET ADDRESS IN PARM LIST
         LA    R0,WKVOL              VOLSER'S ADDRESS
         ST    R0,WKCAMLST+8         SET ADDRESS IN PARM LIST
         LA    R0,DS1FMTID           VTOC DATA ADDRESS
         ST    R0,WKCAMLST+12        SET ADDRESS IN PARM LIST
         LA    R1,WKCAMLST           OBTAIN PARM LIST
         OBTAIN (1)                  ISSUE OBTAIN
         CLI   DS1DSORG,DS1DSGPS     SEQUENTIAL?
         BE    PROC0030              YES
         CLI   DS1DSORG,DS1DSGPO     PARTITIONED?
         BE    PROC0040              YES
         MVI   PARM_LIST_DSORG,C'U'  UNKNOWN
         B     EXIT0000              EXIT
PROC0030 DS    0H
         MVI   PARM_LIST_DSORG,$PARM_LIST_DSORG_PS
         B     EXIT0000              EXIT
PROC0040 DS    0H
         MVI   PARM_LIST_DSORG,$PARM_LIST_DSORG_PO
         B     EXIT0000              EXIT
PROC0100 DS    0H
         MVI   PARM_LIST_DSORG,$PARM_LIST_DSORG_VS
         B     EXIT0000              EXIT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0010  DS    0H
         MVI   PARM_LIST_DSORG,C'X'  NOT CATALOGED
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         L     R1,WKCSIPL+8          CATALOG SEARCH DATA AREA
         L     R2,CSISIZE            SIZE OF CATALOG SEARCH DATA AREA
         FREEMAIN RU,A=(1),LV=(R2)   FREEMAIN THE WORK AREA
         LR    R1,R13                COPY WORK AREA ADDRESS
         L     R13,4(R13)            RESTORE R13
         FREEMAIN RU,A=(1),LV=WKL    FREE THE WORK AREA
         LM    R14,R12,12(R13)       RESTORE ALL OTHER REGISTERS        ASE01680
         SR    R15,R15               GIVE GOOD RETURN CODE              ASE01690
         BR    R14                   RETURN TO CALLER                   ASE01700
* ------------------------------------------------------------------- *
*                                                                     *
*        Constants                                                    *
*                                                                     *
* ------------------------------------------------------------------- *

CSISIZE  DC    F'2048'
         LTORG

CSISPI   DS    0F
         DC    CL44'RLRICE.SUB0001' DATASET NAME
         DC    CL44' '            CATALOG NAME (OR BLANK)
         DC    CL44' '            RESUME NAME OR BLANKS
         DC    CL16'ACGH'         ENTRY TYPES
         DC    CL1'Y'             RETURN DATA AND INDEX INFO FOR VSAM
         DS    0CL4               OPTIONS
         DC    CL1' '             .. RESUME INDICATOR
         DC    CL1' '             .. SEARCH MULTIPLE CATALOGS
         DC    XL1'00'            RESERVED
         DC    H'1'               NUMBER OF ENTRIES FOLLOWING
         DC    CL8'VOLSER  '      FIELD NAME
CSISPL   EQU   *-CSISPI
CAMLSTI  CAMLST SEARCH,              READ VTOC BY DATASET NAME         +
               *,                    .. DATASET NAME'S ADDRESS         +
               *,                    .. VOLSER'S ADDRESS               +
               *                     .. RETURNED DATA ADDRESS
CAMLSTL  EQU   *-CAMLSTI
* ------------------------------------------------------------------- *
*                                                                     *
*        Work areas                                                   *
*                                                                     *
* ------------------------------------------------------------------- *
WK       DSECT
         DS    18F
WKCSIPL  DS       0A
         DS    A               CSI RETURN CODE INFO ADDRESS
         DS    A               CSI SEARCH ARGS ADDRESS
         DS    A               CSI RETURNED DATA

WKVOL    DS    CL6
WKDSN    DS    CL44
         ORG   WKDSN
WKVTOC   IECSDSL1   1

WKCSIRC  DS    0A
         DS    CL2             .. MODULE SUFFIX
         DS    X               .. RETURN CODE
         DS    X               .. REASON CODE
WKCSISP  DS    (CSISPL)X       SEARCH PARAMETERS

WKCAMLST DS    0A,(CAMLSTL)X

WKL      EQU   *-WK            BASIC WORK ARE SIZE
*--------------------------------------------------------------------*
DATA     DSECT
DATAAL   DS    F                  AREA LENGTH
         DS    F
DATAUL   DS    F                  USED LENGTH
DATACATI DS    XL4                CATALOG INDICATORS
DATACATN DS    CL44               CATALOG NAME
         DS    XL5
DATADST  DS    C
$DATANVS EQU   C'A'               NONVSAM
$DATAVSC EQU   C'C'               VSAM CLUSTER
$DATAVSD EQU   C'D'               VSAM DATA
$DATAAIX EQU   C'G'               VSAM ALTERNATE INDEX
$DATAVSI EQU   C'I'               VSAM INDEX
DATADSN  DS    CL44
DATAVDL  DS    XL2                VOLSER DATA LENGTH
         DS    XL2
DATAVEL  DS    XL2                VOLSER ENTRY LENGTH (TOTAL)
DATAVOL  DS    CL6                FIRST (OR ONLY) VOLSER
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
PARM_LIST           DSECT
PARM_LIST_DSN       DS  CL44
PARM_LIST_DSORG     DS  C
$PARM_LIST_DSORG_PS EQU C'S'      SEQUENTIAL
$PARM_LIST_DSORG_PO EQU C'P'      PARTITIONED
$PARM_LIST_DSORG_VS EQU C'V'      VSAM
* ------------------------------------------------------------------- *
*              EQUATES                                                *
* ------------------------------------------------------------------- *
         COPY REGEQU
         END  SUB0001
