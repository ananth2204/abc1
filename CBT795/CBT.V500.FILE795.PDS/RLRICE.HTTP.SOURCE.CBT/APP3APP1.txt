APP3APP1 CSECT
APP3APP1 AMODE 31
APP3APP1 RMODE ANY
         STM   R14,R12,12(R13)         SAVE REGISTERS
         LR    R12,R15                 SET BASE REGISTER
         USING APP3APP1,R12            DEFINE PROGRAM BASE
         B     INIT0000
MODID    DC    CL8'APP3APP1'
         DC    CL8'&SYSDATE'
         DC    CL6'&SYSTIME'
INIT0000 DS    0H
         LR    R11,R1
         USING HTTP_PARMS,R11
         GETMAIN RU,                                                   +
               LV=WORKL,                                               +
               LOC=BELOW
         ST    R1,8(R13)
         ST    R13,4(R1)
         LR    R13,R1
         USING WORK,R13
         ST    R13,WORK_ESTAE_WORK
         LA    R1,ERR0080
         ST    R1,WORK_ESTAE_RETRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ALLOC000 DS    0H
         MVC   WORK_DSNAME,BLANKS
         MVC   WORK_MEMBER,BLANKS
ALLOC010 DS    0H
         ICM   R1,15,HTTP_PARM
         ICM   R2,15,HTTP_PARM_LENGTH
         CH    R2,H53                  TOO MUCH DATA?
         BH    ERR0020
         LA    R4,WORK_DSNAME
         SR    R15,R15
ALLOC020 DS    0H
         CLI   0(R1),C' '              END OF NAME
         BE    ALLOC045
         CLI   0(R1),C'/'              DSN/MEMBER NAME DELIMITER?
         BE    ALLOC030
         MVC   0(1,R4),0(R1)           COPY TO DSNAME
         LA    R4,1(R4)
         LA    R15,1(R15)
         LA    R1,1(R1)
         BCT   R2,ALLOC020
         B     ALLOC045
ALLOC030 DS    0H
         STH   R15,WORK_DSNAME_LENGTH  SAVE LENGTH
         LA    R4,WORK_MEMBER
         LA    R1,1(R1)
         BCT   R2,ALLOC040
         B     ALLOC050
ALLOC040 DS    0H
         CLI   0(R1),C' '
         BE    ALLOC050
         MVC   0(1,R4),0(R1)
         LA    R4,1(R4)
         LA    R1,1(R1)
         BCT   R2,ALLOC040
         B     ALLOC050
ALLOC045 DS    0H
         STH   R15,WORK_DSNAME_LENGTH  SAVE LENGTH
ALLOC050 DS    0H
         MVC   WORK_SUB0001_DSNAME,WORK_DSNAME
         LA    R1,WORK_SUB0001_PARMS
         L     R15,VSUB0001
         BALR  R14,R15
         CLI   WORK_SUB0001_DSORG,C'S' SEQUENTIAL FILE?
         BE    ALLOC070                YES
         CLI   WORK_SUB0001_DSORG,C'P' PDS?
         BNE   ERR0030                 NO
         CLC   WORK_MEMBER,BLANKS      HAVE A MEMBER NAME?
         BNE   ALLOC060                YES
         OI    WORK_FLAG,$WORK_DIR     DOING A MEMBER LIST
         MVC   WORK_DCB(DIRDCBL),DIRDCBI
         B     ALLOC080
ALLOC060 DS    0H
         MVC   WORK_DCB(NORMDCBL),NORMDCBI
         B     ALLOC080
ALLOC070 DS    0H
         CLC   WORK_MEMBER,BLANKS
         BNE   ERR0040                 MEMBER NOT ALLOWED FOR PS
ALLOC080 DS    0H
         MVI   HTTP_FUNCTION,$HTTP_ALLOCATE
         MVC   HTTP_DSN,WORK_DSNAME
         MVC   HTTP_MEMBER,WORK_MEMBER
         MVC   HTTP_DD,DDNAME
         LA    R1,HTTP_PARMS
         L     R15,HTTP_HTTPSUB
         BALR  R14,R15                 ALLOCATE
         CLI   HTTP_RETURN_CODE,$HTTP_OK
         BNE   ERR0050
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
HEAD0000 DS    0H
         MVC   WORK_MESSAGE(HTML_HEADERL),HTML_HEADER
         LA    R1,WORK_MESSAGE
         STCM  R1,15,HTTP_DATA
         LA    R1,HTML_HEADERL
         STCM  R1,15,HTTP_LENGTH
         XC    HTTP_ALET,HTTP_ALET
         MVI   HTTP_FUNCTION,$HTTP_WRITE
         LA    R1,HTTP_PARMS
         L     R15,HTTP_HTTPSUB
         BALR  R14,R15
         CLI   HTTP_RETURN_CODE,$HTTP_OK
         BNE   ERR0060
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
OPEN0000 DS    0H
         MVC   WORK_OPEN_CLOSE(OPENL),OPENI
         MVC   WORK_ESTAE(ESTAEL),ESTAEI
         LA    R2,ESTAE000
         ESTAE (R2),                                                   +
               PARAM=WORK_ESTAE_WORK,                                  +
               MF=(E,WORK_ESTAE)
         OPEN  (WORK_DCB,INPUT),                                       +
               MODE=31,                                                +
               MF=(E,WORK_OPEN_CLOSE)
         ESTAE 0,                                                      +
               MF=(E,WORK_ESTAE)
         TM    WORK_FLAG,$WORK_DIR
         BO    OPEN0010
         LA    R9,WORK_DCB
         USING IHADCB,R9
         SR    R2,R2
         ICM   R2,3,DCBLRECL
         B     OPEN0020
OPEN0010 DS    0H
         LA    R2,256
OPEN0020 DS    0H
         ST    R2,WORK_IOAREA_SIZE
         GETMAIN RU,                                                   +
               LV=(R2),                                                +
               LOC=BELOW
         ST    R1,WORK_IOAREA
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
READ0000 DS    0H
         LA    R9,WORK_DCB
         L     R8,WORK_IOAREA
         GET   WORK_DCB,0(R8)
         TM    WORK_FLAG,$WORK_DIR
         BO    READ0030
         TM    DCBRECFM,DCBRECV
         BO    READ0010
         STCM  R8,15,HTTP_DATA
         SR    R1,R1
         ICM   R1,3,DCBLRECL
         STCM  R1,15,HTTP_LENGTH
         B     READ0020
READ0010 DS    0H
         SR    R1,R1
         ICM   R1,3,0(R8)
         STCM  R1,15,HTTP_LENGTH
         LA    R8,4(,R8)
         STCM  R8,15,HTTP_DATA
READ0020 DS    0H
         XC    HTTP_ALET,HTTP_ALET
         MVI   HTTP_FUNCTION,$HTTP_WRITE
         LA    R1,HTTP_PARMS
         L     R15,HTTP_HTTPSUB
         BALR  R14,R15
         CLI   HTTP_RETURN_CODE,$HTTP_OK
         BNE   ERR0060
         MVC   WORK_MESSAGE(HTML_PL),HTML_P
         LA    R1,WORK_MESSAGE
         STCM  R1,15,HTTP_DATA
         LA    R1,HTML_PL
         STCM  R1,15,HTTP_LENGTH
         LA    R1,HTTP_PARMS
         L     R15,HTTP_HTTPSUB
         BALR  R14,R15
         B     READ0000
READ0030 DS    0H
         LH    R7,0(R8)
         SH    R7,H2
         LA    R8,2(R8)
         USING DIR_DSECT,R8
         LA    R6,WORK_MESSAGE
         USING WORK_MEMBER_DETAIL,R6
         STCM  R6,15,HTTP_DATA
         SR    R5,R5
READ0040 DS    0H
         CLI   0(R8),X'FF'
         BE    READ0060
         MVC   0(WORK_MEMBER_DETAILL,R6),HTML_MEMBER_I
         MVC   WORK_MEMBER_APP,MODID
         MVC   WORK_MEMBER_LINK(44),WORK_DSNAME
         LH    R14,WORK_DSNAME_LENGTH
         LA    R15,WORK_MEMBER_LINK(R14)
         MVI   0(R15),C'/'
         MVC   1(8,R15),DIR_NAME
         MVC   WORK_MEMBER_NAME,DIR_NAME
         LA    R6,WORK_MEMBER_DETAILL(R6)
         LA    R5,WORK_MEMBER_DETAILL(R5)
         NI    DIR_C,X'1F'
         SR    R1,R1
         IC    R1,DIR_C
         AR    R1,R1
         LA    R1,DIR_USER-DIR_DSECT(R1)
         SR    R7,R1
         BZ    READ0050
         AR    R8,R1
         B     READ0040
READ0050 DS    0H
         STCM  R5,15,HTTP_LENGTH
         LA    R1,HTTP_PARMS
         L     R15,HTTP_HTTPSUB
         BALR  R14,R15
         B     READ0000
READ0060 DS    0H
         LTR   R5,R5
         BZ    EOF0000
         STCM  R5,15,HTTP_LENGTH
         LA    R1,HTTP_PARMS
         L     R15,HTTP_HTTPSUB
         BALR  R14,R15
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EOF0000  DS     0H
         MVC   WORK_OPEN_CLOSE(CLOSEL),CLOSEI
         CLOSE WORK_DCB,                                               +
               MODE=31,                                                +
               MF=(E,WORK_OPEN_CLOSE)
         ICM   R1,15,WORK_IOAREA
         BZ    EXIT0000
         L     R0,WORK_IOAREA_SIZE
         FREEMAIN RU,                                                  +
               A=(1),                                                  +
               LV=(0)
         XC    WORK_IOAREA,WORK_IOAREA
         XC    WORK_IOAREA_SIZE,WORK_IOAREA_SIZE
         MVI   HTTP_FUNCTION,$HTTP_FREE
         LA    R1,HTTP_PARMS
         L     R15,HTTP_HTTPSUB
         BALR  R14,R15                 FREE THE DATA SET
         CLI   HTTP_RETURN_CODE,$HTTP_OK
         BNE   ERR0070
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0010  DS    0H
         MVC   WORK_MESSAGE(MSG01L),MSG01
         LA    R0,MSG01L
         B     ERR9900
ERR0020  DS    0H
         MVC   WORK_MESSAGE(MSG02L),MSG02
         LA    R0,MSG02L
         B     ERR9900
ERR0030  DS    0H
         MVC   WORK_MESSAGE(MSG03L),MSG03
         LA    R0,MSG03L
         B     ERR9900
ERR0040  DS    0H
         MVC   WORK_MESSAGE(MSG04L),MSG04
         LA    R0,MSG04L
         B     ERR9900
ERR0050  DS    0H
         MVC   WORK_MESSAGE(MSG05L),MSG05
         MVC   WORK_MESSAGE+MSG05L(80),HTTP_MESSAGE_1
         LA    R0,MSG05L+80
         B     ERR9900
ERR0060  DS    0H
         MVC   WORK_MESSAGE(MSG06L),MSG06
         MVC   WORK_MESSAGE+MSG06L(80),HTTP_MESSAGE_1
         LA    R0,MSG06L+80
         B     ERR9900
ERR0070  DS    0H
         MVC   WORK_MESSAGE(MSG07L),MSG07
         MVC   WORK_MESSAGE+MSG07L(80),HTTP_MESSAGE_1
         LA    R0,MSG07L+80
         B     ERR9900
ERR0080  DS    0H
         ESTAE 0
         MVI   HTTP_FUNCTION,$HTTP_FREE
         LA    R1,HTTP_PARMS
         L     R15,HTTP_HTTPSUB
         BALR  R14,R15
         MVC   WORK_MESSAGE(MSG08L),MSG08
         UNPK  WORK_DWORD(7),WORK_ABEND_CODE(4)
         TR    WORK_DWORD(6),HEXCHAR
         MVC   WORK_MESSAGE+(MSG08_SYSTEM-MSG08)(3),WORK_DWORD
         MVC   WORK_MESSAGE+(MSG08_USER-MSG08)(3),WORK_DWORD+3
         LA    R0,MSG08L
ERR9900  DS    0H
         LA    R1,WORK_MESSAGE
         STCM  R1,15,HTTP_DATA
         STCM  R0,15,HTTP_LENGTH
         MVI   HTTP_FUNCTION,$HTTP_WRITE
         LA    R1,HTTP_PARMS
         L     R15,HTTP_HTTPSUB
         BALR  R14,R15
*-------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         MVI   HTTP_FUNCTION,$HTTP_CLOSE
         LA    R1,HTTP_PARMS
         L     R15,HTTP_HTTPSUB
         BALR  R14,R15
         LR    R1,R13                  COPY WORK AREA ADDRESS
         L     R13,4(R13)              RESTORE CALLER'S R13
         FREEMAIN RU,A=(1),LV=WORKL    FREE THE WORK AREA
         LM    R14,R12,12(R13)         RESTORE REGS
         SR    R15,R15                 SET RETURN CODE
         BR    R14
         DROP  R12,R13
*-------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ESTAE000 DS    0H
         STM   R14,R12,12(R13)
         LR    R12,R15
         USING ESTAE000,R12
         USING SDWA,R1
         L     R2,SDWAPARM
         L     R3,0(R2)
         USING WORK,R3
         MVC   WORK_ABEND_CODE,SDWACMPC
         L     R4,WORK_ESTAE_RETRY
         SETRP RETADDR=(R4),                                           +
               RETREGS=YES,                                            +
               REGS=(14,12),                                           +
               FRESDWA=YES,                                            +
               RC=4
* ------------------------------------------------------------------- *
VSUB0001       DC    V(SUB0001)
H2             DC    H'2'
H53            DC    H'53'
BLANKS         DC    CL256' '
DDNAME         DC    CL8'DD1'

HEXCHAR        EQU   *-C'0'
               DC    C'0123456789ABCDEF'

ESTAEI   ESTAE ESTAE000,                                               +
               CT,                                                     +
               MF=L
ESTAEL   EQU   *-ESTAEI

NORMDCBI DCB   DSORG=PS,                                               +
               DDNAME=DD1,                                             +
               EODAD=EOF0000,                                          +
               MACRF=GM
NORMDCBL EQU   *-NORMDCBI

DIRDCBI  DCB   DSORG=PS,                                               +
               DDNAME=DD1,                                             +
               RECFM=U,                                                +
               LRECL=256,                                              +
               EODAD=EOF0000,                                          +
               MACRF=GM
DIRDCBL  EQU   *-DIRDCBI


OPENI    OPEN  (*,INPUT),                                              +
               MODE=31,                                                +
               MF=L
OPENL    EQU   *-OPENI
CLOSEI   CLOSE *,                                                      +
               MODE=31,                                                +
               MF=L
CLOSEL         EQU   *-CLOSEI

MSG01          DS    0C
               DC    C'ESTAE failed'
MSG01L         EQU   *-MSG01

MSG02          DS    0C
               DC    C'Parameter info is too long (length > 53 bytes)'
MSG02L         EQU   *-MSG02

MSG03          DS    0C
               DC    C'Dataset DSORG not PS or PDS'
MSG03L         EQU   *-MSG03

MSG04          DS    0C
               DC    C'Member name not allowed for sequential datasets'
MSG04L         EQU   *-MSG04

MSG05          DS    0C
               DC    C'Allocation failed. '
MSG05L         EQU   *-MSG05

MSG06          DS    0C
               DC    C'TCP/IP write failed. '
MSG06L         EQU   *-MSG06

MSG07          DS    0C
               DC    C'Dataset deallocate failed.'
MSG07L         EQU   *-MSG07

MSG08          DS    0C
               DC    C'Error occurred during OPEN.  System code '
MSG08_SYSTEM   DS    CL3
               DC    C'     User code '
MSG08_USER     DS    CL3
MSG08L         EQU   *-MSG08

HTTP_HEADER    DS    0C
               DC    C'HTTP/1.1 200 OK'
               DC    AL1($LF)
               DC    C'Content-Type: text/html'
               DC    AL1($LF)
               DC    AL1($LF)
HTTP_HEADERL   EQU   *-HTTP_HEADER
HTML_HEADER    DS    0C
               DC    C'<HTML>'
               DC    C'<BODY>'
               DC    C'<PRE>'
               DC    AL1($LF)
HTML_HEADERL   EQU   *-HTML_HEADER
HTML_P         DC    C'<P>'
HTML_PL        EQU   *-HTML_P
HTML_TRAILER   DS    0C
               DC    C'</BODY>'
               DC    C'</HTML>'
HTML_TRAILERL  EQU   *-HTML_TRAILER

HTML_MEMBER_I  DS    0C
               DC    C'<A HREF='
               DC    CL8' '
               DC    C'?'
               DC    CL53' '
               DC    C'>'
               DC    CL8' '
               DC    C'</A>'
               DC    C'<P>'
HTML_MEMBER_IL EQU   *-HTML_MEMBER_I
* ------------------------------------------------------------------- *
*        WORK AREAS                                                   *
* ------------------------------------------------------------------- *
WORK                DSECT
                    DS    18F

WORK_DWORD          DS    D

WORK_ESTAE_PARMS    DS    0A
WORK_ESTAE_WORK     DS    A
WORK_ESTAE_RETRY    DS    A

WORK_DCB            DS    0F,(NORMDCBL)X
WORK_OPEN_CLOSE     DS    0F,(OPENL)X
WORK_ESTAE          DS    0F,(ESTAEL)X

WORK_IOAREA         DS    A
WORK_IOAREA_SIZE    DS    F

WORK_ABEND_CODE     DS    XL3

WORK_FLAG           DS    X
$WORK_DIR           EQU   X'80'

WORK_DSNAME_LENGTH  DS    H
WORK_DSNAME         DS    CL44
WORK_MEMBER         DS    CL8
                    DS    CL50

WORK_SUB0001_PARMS  DS    0C
WORK_SUB0001_DSNAME DS    CL44
WORK_SUB0001_DSORG  DS    X

WORK_MESSAGE        DS    CL4096
WORKL               EQU   *-WORK
*---------------------------------------------------------------------*
WORK_MEMBER_DETAIL  DSECT
                    DS    CL8               '<A HREF='
WORK_MEMBER_APP     DS    CL8               'APP3APP1'
                    DS    C                 '?'
WORK_MEMBER_LINK    DS    CL53              LINK INFO
                    DS    C                 '>'
WORK_MEMBER_NAME    DS    CL8
                    DS    CL4               '</A>'
                    DS    CL3               '<P>'
WORK_MEMBER_DETAILL EQU   *-WORK_MEMBER_DETAIL
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
DIR_DSECT           DSECT
DIR_NAME            DS    CL8
DIR_TTR             DS    CL3
DIR_C               DS    X
DIR_USER            DS    0C
* ------------------------------------------------------------------- *
         SUBPARM TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         IHASDWA
         DCBD DSORG=PS
         COPY REGEQU
$LF      EQU  X'0A'
         END  APP3APP1
