         MACRO
         MYWTO &SWITCH=,&T
         GBLB  &WTOSW
         AIF   (T'&SWITCH EQ 'O').SW00
         AIF   ('&SWITCH' EQ 'ON').SW01
         AIF   ('&SWITCH' EQ 'OFF').SW02
         MNOTE 12,'BAD SWITCH'
         MEXIT
.SW00    WTO   &T
         MEXIT
.SW01    ANOP
&WTOSW   SETB  1
         MEXIT
.SW02    ANOP
&WTOSW   SETB  0
         MEND
HTTPTEST CSECT
HTTPTEST AMODE 31
HTTPTEST RMODE 24
         USING HTTPTEST,R12,R10     DEFINE BASE
         STM   R14,R12,12(R13)      SAVE ENTRY REGISTERS
         LR    R12,R15              COPY BASE
         B     INIT0000             BRANCH AROUND EYECATCHER
MODID    DC    CL8'HTTPTEST'        MODULE NAME
         DC    CL8'&SYSDATE'        DATE OF ASSEMBLY
         DC    CL6'&SYSTIME'        TIME OF ASSEMBLY
INIT0000 DS    0H
         LA    R10,2048(R12)
         LA    R10,2048(R10)
         L     R11,0(R1)             COPY COMM AREA BASE
         GETMAIN RU,                 OBTAIN WORK AREA STORAGE          +
               LV=WORKL,                                               +
               LOC=BELOW
         ST    R13,4(R1)
         ST    R1,8(R13)
         LR    R13,R1                SAVE WORK AREA ADDRESS
         USING WORKAREA,R13          DEFINE BASE
         MVI   PR_CC,C' '
         MVC   PR_LINE,PR_CC
         GETMAIN RU,                                                   +
               LV=GWALENTH,                                            +
               LOC=ANY
         ST    R1,WORK_TCPIP_GWA     TCP/IP GLOBAL WORK AREA
* ------------------------------------------------------------------- *
*     OPEN SYSIN and SYSPRINT                                         *
* ------------------------------------------------------------------- *
         MYWTO 'OPEN SYSIN AND SYSPRINT'
         MVC   WORK_OCPL(OCPLL),OCPLI
         MVC   WORK_SYSIN(SYSINL),SYSINI
         OPEN  (WORK_SYSIN,INPUT),                                     +
               MF=(E,WORK_OCPL),                                       +
               MODE=31
         MVC   WORK_SYSPRINT(SYSPRINTL),SYSPRINTI
         OPEN  (WORK_SYSPRINT,OUTPUT),                                 +
               MF=(E,WORK_OCPL),                                       +
               MODE=31
* ------------------------------------------------------------------- *
*     Make the INITAPI call                                           *
* ------------------------------------------------------------------- *
         MYWTO 'INITAPI'
         XC    WORK_TCPIP_TASK(TIELENTH),WORK_TCPIP_TASK
         MVC   WORK_TCPIP_FUNCTION,=CL16'INITAPI'
         EZASMI TYPE=INITAPI,                                          +
               APITYPE=2,                                              +
               ASYNC='NO',                                             +
               MAXSNO=WORK_MAX_SOCKET,                                 +
               SUBTASK=C_HTTPTEST,                                     +
               RETCODE=WORK_TCPIP_RETURN_CODE,                         +
               ERRNO=WORK_TCPIP_ERROR_NUMBER,                          +
               TASK=WORK_TCPIP_TASK
         TM    WORK_TCPIP_RETURN_CODE,X'80'
         BO    ERR0010
* ------------------------------------------------------------------- *
*     Read the first control statement (Port number)                  *
* ------------------------------------------------------------------- *
         MYWTO 'READ FIRST'
         GET   WORK_SYSIN,WORK_CARD
         MVC   PR_LINE(MSG01L),MSG01
         MVC   PR_LINE+(MSG01_CARD-MSG01)(L'MSG01_CARD),WORK_CARD
         BAL   R9,PRT0000
         XC    WORK_DWORD,WORK_DWORD
         PACK  WORK_DWORD+5(3),WORK_CARD(5)
         CVB   R1,WORK_DWORD
         STH   R1,WORK_OUR_PORT
* ------------------------------------------------------------------- *
*                                                                     *
*     Read a control statement (URL)                                  *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0000 DS    0H
         MYWTO 'MAIN0000'
         GET   WORK_SYSIN,WORK_CARD
         MVC   PR_LINE(MSG01L),MSG01
         MVC   PR_LINE+(MSG01_CARD-MSG01)(L'MSG01_CARD),WORK_CARD
         BAL   R9,PRT0000
         MVC   WORK_PORT,H80
         LA    R1,WORK_CARD
         SR    R2,R2
MAIN0010 DS    0H
         CLI   0(R1),C':'
         BE    MAIN0020
         CLI   0(R1),C'/'
         BE    MAIN0050
         LA    R1,1(R1)
         LA    R2,1(R2)
         B     MAIN0010
MAIN0020 DS    0H
         LA    R1,1(R1)
         MVC   WORK_PACK_IN,=C'00000'
MAIN0030 DS    0H
         CLI   0(R1),C'/'
         BE    MAIN0050
         MVC   WORK_PACK_IN(4),WORK_PACK_IN+1
         MVC   WORK_PACK_IN+4(1),0(R1)
         LA    R1,1(R1)
         B     MAIN0030
MAIN0040 DS    0H
         XC    WORK_DWORD,WORK_DWORD
         PACK  WORK_DWORD+5(3),WORK_PACK_IN(5)
         CVB   R1,WORK_DWORD
         STH   R1,WORK_PORT
MAIN0050 DS    0H
         ST    R1,WORK_URL_DATA_ADDRESS
         ST    R2,WORK_HOSTNAME_LENGTH
* ------------------------------------------------------------------- *
*     Create the socket                                               *
* ------------------------------------------------------------------- *
         MYWTO 'CREATE SOCKET'
         MVC   WORK_TCPIP_FUNCTION,=CL16'SOCKET'
         EZASMI TYPE=SOCKET,                                           +
               AF='INET',                                              +
               SOCTYPE='STREAM',                                       +
               RETCODE=WORK_TCPIP_RETURN_CODE,                         +
               ERRNO=WORK_TCPIP_ERROR_NUMBER,                          +
               TASK=WORK_TCPIP_TASK
         TM    WORK_TCPIP_RETURN_CODE,X'80'
         BO    ERR0010
         MVC   WORK_SOCKET_DESCRIPTOR,WORK_TCPIP_RETURN_CODE+2
* ------------------------------------------------------------------- *
*     BIND the socket                                                 *
* ------------------------------------------------------------------- *
         MYWTO 'BIND'
         XC    WORK_SOCKET_STRUCTURE,WORK_SOCKET_STRUCTURE
         MVC   WORK_SOCKET_FAMILY,C_AF_INET
         MVC   WORK_SOCKET_PORT,WORK_OUR_PORT
         MVC   WORK_TCPIP_FUNCTION,=CL16'BIND'
         EZASMI TYPE=BIND,                                             +
               S=WORK_SOCKET_DESCRIPTOR,                               +
               NAME=WORK_SOCKET_STRUCTURE,                             +
               RETCODE=WORK_TCPIP_RETURN_CODE,                         +
               ERRNO=WORK_TCPIP_ERROR_NUMBER,                          +
               TASK=WORK_TCPIP_TASK
         TM    WORK_TCPIP_RETURN_CODE,X'80'
         BO    ERR0010
* ------------------------------------------------------------------- *
*     Determine peer's IP address                                     *
* ------------------------------------------------------------------- *
         MYWTO 'GETHOSTBYNAME'
         MVC   WORK_TCPIP_FUNCTION,=CL16'GETHOSTBYNAME'
         EZASMI TYPE=GETHOSTBYNAME,                                    +
               NAMELEN=WORK_HOSTNAME_LENGTH,                           +
               NAME=WORK_CARD,                                         +
               HOSTENT=WORK_HOSTENT,                                   +
               RETCODE=WORK_TCPIP_RETURN_CODE,                         +
               ERROR=WORK_TCPIP_ERROR_NUMBER
         TM    WORK_TCPIP_RETURN_CODE,X'80'
         BO    ERR0010
* ------------------------------------------------------------------- *
*     Establish a connection to the peer                              *
* ------------------------------------------------------------------- *
         MYWTO 'CONNECT'
         L     R1,WORK_HOSTENT
         USING HOSTENT,R1
         L     R2,HOSTENT_IP_LIST
         L     R3,0(R2)
         XC    WORK_PEER,WORK_PEER
         MVC   WORK_PEER_AF,C_AF_INET
         MVC   WORK_PEER_PORT,WORK_PORT
         MVC   WORK_PEER_IP_ADDRESS,0(R3)
         MVC   WORK_TCPIP_FUNCTION,=CL16'CONNECT'
         EZASMI TYPE=CONNECT,                                          +
               S=WORK_SOCKET_DESCRIPTOR,                               +
               NAME=WORK_PEER,                                         +
               RETCODE=WORK_TCPIP_RETURN_CODE,                         +
               ERRNO=WORK_TCPIP_ERROR_NUMBER
         TM    WORK_TCPIP_RETURN_CODE,X'80'
         BO    ERR0010
* ------------------------------------------------------------------- *
*     Send (write) the URL to the server                              *
* ------------------------------------------------------------------- *
         MVC   WORK_URL,BLANKS
         MVC   WORK_URL_METHOD,=CL4'GET'
         L     R1,WORK_URL_DATA_ADDRESS
         MVC   WORK_URL_DATA,0(R1)
         LA    R2,WORK_URL_DATA-WORK_URL
MAIN0060 DS    0H
         CLI   0(R1),C' '
         BE    MAIN0070
         LA    R2,1(R2)
         LA    R1,1(R1)
         B     MAIN0060
MAIN0070 DS    0H
         BAL   R9,PRT0000
         MVC   PR_LINE(MSG02L),MSG02
         MVC   PR_LINE+(MSG02_URL-MSG02)(L'MSG02_URL),WORK_URL
         BAL   R9,PRT0000
         ST    R2,WORK_URL_LENGTH
         MVC   WORK_TCPIP_FUNCTION,=CL16'WRITE'
         TR    WORK_URL,EBCDIC_TO_ASCII
         ZAP   WORK_BUFFER_NUMBER,P0
         MYWTO 'WRITE'
         EZASMI TYPE=WRITE,                                            +
               S=WORK_SOCKET_DESCRIPTOR,                               +
               BUF=WORK_URL,                                           +
               NBYTE=WORK_URL_LENGTH,                                  +
               RETCODE=WORK_TCPIP_RETURN_CODE,                         +
               ERRNO=WORK_TCPIP_ERROR_NUMBER
         ICM   R1,15,WORK_TCPIP_RETURN_CODE
         BNH   ERR0010
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         MVC   WORK_TCPIP_FUNCTION,=CL16'SHUTDOWN'
         MYWTO 'SHUTDOWN'
         EZASMI TYPE=SHUTDOWN,                                         +
               S=WORK_SOCKET_DESCRIPTOR,                               +
               HOW=1,                                                  +
               RETCODE=WORK_TCPIP_RETURN_CODE,                         +
               ERRNO=WORK_TCPIP_ERROR_NUMBER
         TM    WORK_TCPIP_RETURN_CODE,X'80'
         BO    ERR0010
* ------------------------------------------------------------------- *
*     Receive the response(s)                                         *
* ------------------------------------------------------------------- *
MAIN0080 DS    0H
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         MYWTO 'IOCTL'
         AGO   .NOGEN
         EZASMI TYPE=IOCTL,                                            +
               S=WORK_SOCKET_DESCRIPTOR,                               +
               COMMAND='FIONREAD',                                     +
               RETARG=WORK_RECV_BUFFER_SIZE,                           +
               REQARG=WORK_DWORD,                                      +
               RETCODE=WORK_TCPIP_RETURN_CODE,                         +
               ERRNO=WORK_TCPIP_ERROR_NUMBER
.NOGEN   ANOP
         L     R6,WORK_RECV_BUFFER_SIZE
         MVC   WORK_TCPIP_FUNCTION,=CL16'READ'
         MVC   WORK_RECV_BUFFER_SIZE,RECV_BUFFER_SIZE
         MYWTO 'READ'
         EZASMI TYPE=READ,                                             +
               S=WORK_SOCKET_DESCRIPTOR,                               +
               BUF=WORK_RECV_BUFFER,                                   +
               NBYTE=WORK_RECV_BUFFER_SIZE,                            +
               RETCODE=WORK_TCPIP_RETURN_CODE,                         +
               ERRNO=WORK_TCPIP_ERROR_NUMBER
         TM    WORK_TCPIP_RETURN_CODE,X'80'
         BO    ERR0010
         AP    WORK_BUFFER_NUMBER,P1
         MVC   PR_LINE(MSG03L),MSG03
         ED    PR_LINE+(MSG03_BUFFER_NUMBER-MSG03)(L'MSG03_BUFFER_NUMBE+
               R),WORK_BUFFER_NUMBER
         ICM   R1,15,WORK_TCPIP_RETURN_CODE
         CVD   R1,WORK_DWORD
         ED    PR_LINE+(MSG03_BYTES_RECEIVED-MSG03)(L'MSG03_BYTES_RECEI+
               VED),WORK_DWORD+5
         UNPK  PR_LINE+(MSG03_BYTES_HEX-MSG03)(9),WORK_TCPIP_RETURN_COD+
               E(5)
         TR    PR_LINE+(MSG03_BYTES_HEX-MSG03)(8),HEXCHAR
         MVI   PR_LINE+(MSG03_BYTES_HEX-MSG03)+8,C')'
         BAL   R9,PRT0000
         ICM   R1,15,WORK_TCPIP_RETURN_CODE
         BZ    MAIN0000
* ------------------------------------------------------------------- *
*     Translate from ASCII to EBCDIC                                  *
* ------------------------------------------------------------------- *
         LA    R1,WORK_RECV_BUFFER
         LA    R2,(32768/256)-1
MAIN0090 DS    0H
         TR    0(256,R1),ASCII_TO_EBCDIC
         LA    R1,256(R1)
         BCT   R2,MAIN0090
* ------------------------------------------------------------------- *
*     Print the received data                                         *
* ------------------------------------------------------------------- *
         SR    R2,R2
         LA    R3,WORK_RECV_BUFFER
MAIN0100 DS    0H
         L     R4,WORK_TCPIP_RETURN_CODE
         SR    R4,R2
         CH    R4,H32
         BNH   MAIN0110
         LH    R4,H32
MAIN0110 DS    0H
         MVC   PR_LINE(MSG04L),MSG04
         ST    R2,WORK_DWORD
         UNPK  PR_LINE+(MSG04_DISPLACEMENT-MSG04)(9),WORK_DWORD(5)
         TR    PR_LINE+(MSG04_DISPLACEMENT-MSG04)(8),HEXCHAR
         MVI   PR_LINE+(MSG04_DISPLACEMENT-MSG04)+8,C' '
         BCTR  R4,0
         EX    R4,CHAR_MVC
         EX    R4,CHAR_TR
         LA    R4,1(R4)
         SLL   R4,1
         UNPK  WORK_UNPACK+00(9),00(5,R3)
         UNPK  WORK_UNPACK+08(9),04(5,R3)
         UNPK  WORK_UNPACK+16(9),08(5,R3)
         UNPK  WORK_UNPACK+24(9),12(5,R3)
         UNPK  WORK_UNPACK+32(9),16(5,R3)
         UNPK  WORK_UNPACK+40(9),20(5,R3)
         UNPK  WORK_UNPACK+48(9),24(5,R3)
         UNPK  WORK_UNPACK+56(9),28(5,R3)
         TR    WORK_UNPACK(64),HEXCHAR
         BCTR  R4,0
         EX    R4,HEX_MVC
         BAL   R9,PRT0000
         AH    R2,H32
         AH    R3,H32
         C     R2,WORK_TCPIP_RETURN_CODE
         BL    MAIN0100
         MYWTO 'PRINT'
         BAL   R9,PRT0000
         B     MAIN0080
CHAR_MVC MVC   PR_LINE+(MSG04_CHAR-MSG04)(0),0(R3)
CHAR_TR  TR    PR_LINE+(MSG04_CHAR-MSG04)(0),PRINTABLE
HEX_MVC  MVC   PR_LINE+(MSG04_HEX-MSG04)(0),WORK_UNPACK
* ------------------------------------------------------------------- *
*     CLOSE the socket                                                *
* ------------------------------------------------------------------- *
MAIN0200 DS    0H
         MYWTO 'CLOSE SOCKET'
         MVC   WORK_TCPIP_FUNCTION,=CL16'CLOSE'
         EZASMI TYPE=CLOSE,                                            +
               S=WORK_SOCKET_DESCRIPTOR,                               +
               RETCODE=WORK_TCPIP_RETURN_CODE,                         +
               ERRNO=WORK_TCPIP_ERROR_NUMBER
         TM    WORK_TCPIP_RETURN_CODE,X'80'
         BO    ERR0010
         B     MAIN0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EOF0000  DS    0H
         MYWTO 'TERMAPI'
         MVC   WORK_TCPIP_FUNCTION,=CL16'TERMAPI'
         EZASMI TYPE=TERMAPI,                                          +
               TASK=WORK_TCPIP_TASK
         TM    WORK_TCPIP_RETURN_CODE,X'80'
         BO    ERR0010
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0010  DS    0H
         BAL   R10,INFO0000
         DC    H'0'
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
INFO0000 DS    0H
         MVC   PR_LINE(MSG90_L),MSG90
         MVC   PR_LINE+(MSG90_FUNCTION-MSG90)(L'MSG90_FUNCTION),WORK_TC+
               PIP_FUNCTION
         UNPK  PR_LINE+(MSG90_RETURN_CODE-MSG90)(9),WORK_TCPIP_RETURN_C+
               ODE(5)
         TR    PR_LINE+(MSG90_RETURN_CODE-MSG90)(8),HEXCHAR
         MVI   PR_LINE+(MSG90_RETURN_CODE-MSG90)+8,C' '
         L     R1,WORK_TCPIP_ERROR_NUMBER
         CVD   R1,WORK_DWORD
         ED    PR_LINE+(MSG90_ERROR_NUMBER-MSG90)(L'MSG90_ERROR_NUMBER)+
               ,WORK_DWORD+5
         BAL   R9,PRT0000
         BR    R10
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
PRT0000  DS    0H
         PUT   WORK_SYSPRINT,PR_CC
         MVI   PR_CC,C' '
         MVC   PR_LINE,PR_CC
         BR    R9
* ------------------------------------------------------------------- *
*        Exit                                                         *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ICM   R1,15,WORK_TCPIP_GWA
         BZ    EXIT0010
         FREEMAIN RU,A=(1),LV=GWALENTH
EXIT0010 DS    0H
         CLOSE WORK_SYSIN,                                             +
               MF=(E,WORK_OCPL),                                       +
               MODE=31
         CLOSE WORK_SYSPRINT,                                          +
               MF=(E,WORK_OCPL),                                       +
               MODE=31
         LR    R1,R13                COPY SAVE AREA ADDRESS
         L     R13,4(R13)            CALLER'S SAVE AREA ADDRESS
         FREEMAIN RU,A=(1),LV=WORKL  FREE WORK AREA
         LM    R14,R12,12(R13)
         SR    R15,R15               ALWAYS A GOOD RETURN CODE
         BR    R14                   RETURN TO CALLER

* ------------------------------------------------------------------- *
*                                                                     *
*        CONSTANTS                                                    *
*                                                                     *
* ------------------------------------------------------------------- *
OCPLI     OPEN (*,INPUT),MF=L,MODE=31
OCPLL     EQU *-OCPLI
SYSINI    DCB  DSORG=PS,                                               +
               DDNAME=SYSIN,                                           +
               RECFM=FB,                                               +
               LRECL=80,                                               +
               DCBE=SYSIN_DCBE,                                        +
               MACRF=GM
SYSINL    EQU  *-SYSINI
SYSIN_DCBE DCBE EODAD=EOF0000
SYSPRINTI DCB  DSORG=PS,                                               +
               DDNAME=SYSPRINT,                                        +
               RECFM=FBA,                                              +
               LRECL=133,                                              +
               MACRF=PM
SYSPRINTL EQU  *-SYSPRINTI

H32                      DC    H'32'
H80                      DC    H'80'
P0                       DC    P'0'
P1                       DC    P'1'
RECV_BUFFER_SIZE         DC    F'32768'

HEXCHAR                  EQU   *-C'0'
                         DC    C'0123456789ABCDEF'

C_HTTPTEST               DC    CL8'HTTPTEST'
C_AF_INET                DC    X'0002'

MSG01                    DS    0C
                         DC    CL15'HTTPTEST01I'
                         DC    C'Control statement: '
MSG01_CARD               DC    CL80' '
MSG01L                   EQU   *-MSG01
MSG02                    DS    0C
                         DC    CL15'HTTPTEST02I'
                         DC    C'URL sent: '
MSG02_URL                DC    CL80' '
MSG02L                   EQU   *-MSG02
MSG03                    DS    0C
                         DC    CL15'HTTPTEST03I'
                         DC    C'Received buffer number '
MSG03_BUFFER_NUMBER      DC    X'40202120'
                         DC    C'  bytes received '
MSG03_BYTES_RECEIVED     DC    X'4020206B202120'
                         DC    C' (hex '
MSG03_BYTES_HEX          DC    CL8' '
                         DC    C')'
MSG03L                   EQU   *-MSG03
MSG04                    DS    0C
                         DC    CL16' '
MSG04_DISPLACEMENT       DC    CL8' '
                         DC    CL3' '
MSG04_CHAR               DC    CL32' '
                         DC    CL3' '
MSG04_HEX                DC    CL64' '
MSG04L                   EQU   *-MSG04
MSG90                    DS    0C
                         DC    CL15'HTTPTEST90I'
                         DC    C'TCP/IP function '
MSG90_FUNCTION           DC    CL16' '
                         DC    C'  Return code: '
MSG90_RETURN_CODE        DC    CL8' '
                         DC    C'  Error number:'
MSG90_ERROR_NUMBER       DC    X'402020202120'
MSG90_L                  EQU   *-MSG90

BLANKS                   DC    CL256' '
EBCDIC_TO_ASCII          ETOA
ASCII_TO_EBCDIC          ATOE

PRINTABLE                DC    XL16'40404040404040404040404040404040'
                         DC    XL16'40404040404040404040404040404040'
                         DC    XL16'40404040404040404040404040404040'
                         DC    XL16'40404040404040404040404040404040'
                         DC    XL16'404040404040404040404A4B4C4D4E4F'
                         DC    XL16'504040404040404040405A5B5C5D5E5F'
                         DC    XL16'60614040404040404040406B6C6D6E6F'
                         DC    XL16'404040404040404040407A7B7C7D7E7F'
                         DC    XL16'40818283848586878889408B8C8D8E8F'
                         DC    XL16'40919293949596979899409B9C9D9E9F'
                         DC    XL16'4040A2A3A4A5A6A7A8A940ABACADAEAF'
                         DC    XL16'4040404040404040404040BBBCBDBEBF'
                         DC    XL16'40C1C2C3C4C5C6C7C8C9404040404040'
                         DC    XL16'40D1D2D3D4D5D6D7D8D9404040404040'
                         DC    XL16'4040E2E3E4E5E6E7E8E9404040404040'
                         DC    XL16'F0F1F2F3F4F5F6F7F8F9404040404040'

                         LTORG
* ------------------------------------------------------------------- *
*              WORK AREA                                              *
* ------------------------------------------------------------------- *
WORKAREA                 DSECT
                         DS   18F
WORK_DWORD               DS   D
WORK_MAX_SOCKET          DS   F
WORK_BUFFER_LENGTH       DS   F
WORK_URL_LENGTH          DS   F
WORK_URL_DATA_ADDRESS    DS   A
WORK_RECV_BUFFER_SIZE    DS   F

WORK_PORT                DS   H
WORK_OUR_PORT            DS   H
WORK_SOCKET_DESCRIPTOR   DS   H

WORK_HOSTNAME_LENGTH     DS   F

WORK_HOSTENT             DS   A

                         DS   0F
WORK_SOCKET_STRUCTURE    DS   0XL16
WORK_SOCKET_FAMILY       DS   AL2           FAMILY (ALWAYS = 2)
WORK_SOCKET_PORT         DS   XL2           PORT NUMBER
WORK_SOCKET_CLIENT_IP    DS   XL4           CLIENT'S IP ADDRESS
                         DS   XL8           RESERVED

                         CLIENT PREFIX=WORK
WORK_CLIENTL             EQU  *-WORK_CLIENT

WORK_OCPL                DS   0F,(OCPLL)X
WORK_SYSIN               DS   0F,(SYSINL)X
WORK_SYSPRINT            DS   0F,(SYSPRINTL)X

WORK_TCPIP_ERROR_NUMBER  DS   F
WORK_TCPIP_FUNCTION      DS   CL16
WORK_TCPIP_GWA           DS   F
WORK_TCPIP_RETURN_CODE   DS   F

WORK_PEER                DS   0F,0XL16
WORK_PEER_AF             DS   XL2
WORK_PEER_PORT           DS   XL2
WORK_PEER_IP_ADDRESS     DS   F
WORK_PEER_RESERVED       DS   XL8

WORK_CARD                DS   CL80

WORK_PACK_IN             DS   CL5

WORK_URL                 DS   0CL84
WORK_URL_METHOD          DS   CL4
                         DS   C
WORK_URL_DATA            DS   CL80

WORK_BUFFER_NUMBER       DS   PL2

WORK_UNPACK              DS   CL65

PR_CC                    DS   C
PR_LINE                  DS   CL132

WORK_TCPIP_TASK          EZASMI TYPE=TASK,STORAGE=CSECT

WORK_RECV_BUFFER         DS   32768X

WORKL                    EQU  *-WORKAREA
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
HOSTENT                  DSECT
HOSTENT_HOSTNAME_ADDRESS DS  A
HOSTENT_ALIAS_LIST       DS  A
HOSTENT_FAMILY           DS  XL4
HOSTENT_IP_LIST_LENGTH   DS  XL4
HOSTENT_IP_LIST          DS  A
* ------------------------------------------------------------------- *
*              TCP/IP GLOBAL WORK AREA                                *
* ------------------------------------------------------------------- *
         EZASMI TYPE=GLOBAL,STORAGE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                COPY   REGEQU
         END    HTTPTEST
