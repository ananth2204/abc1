*---------------------------------------------------------------------*
*                                                                     *
*    This utility builds an HTML INDEX member for PDS's.              *
*                                                                     *
*---------------------------------------------------------------------*
HTTPINDX CSECT
HTTPINDX AMODE 24
HTTPINDX RMODE 24
         USING HTTPINDX,R12                   DECLARE ENTRY BASE
         STM   R14,R12,12(R13)                STORE CALLERS REGISTERS
         LR    R12,R15                        LOAD BASE REGISTER
         B     INIT0000
         DC    CL8'HTTPINDX'
         DC    CL6'&SYSDATE'
         DC    CL6'&SYSTIME'
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
INIT0000 DS    0H
         GETMAIN RU,                                                   +
               LV=WORKL,                                               +
               LOC=BELOW
         ST    R13,4(R1)
         ST    R1,8(R13)
         LR    R13,R1                         COPY WORK AREA ADDRESS
         USING WORK,R13
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
         ZAP   WORK_LINE_NUMBER,P0
         ZAP   WORK_PAGE_NUMBER,P0
         ZAP   WORK_MEMBERS,P0
         MVC   WORK_OCPL(OPENL),OPENI
         MVC   WORK_DIR(DIRL),DIRI            INITIALIZE DCB(S)
         MVC   WORK_SYSPRINT(SYSPRINTL),SYSPRINTI
         MVC   WORK_SYSIN(SYSINL),SYSINI
         MVC   WORK_INDEX(INDEXL),INDEXI
         OPEN  (WORK_DIR,INPUT),              OPEN PDS DIRECTORY       +
               MODE=31,                                                +
               MF=(E,WORK_OCPL)
         OPEN  (WORK_SYSPRINT,OUTPUT),                                 +
               MODE=31,                                                +
               MF=(E,WORK_OCPL)
         OPEN  (WORK_SYSIN,INPUT),            OPEN CONTROL STATEMENTS  +
               MODE=31,                                                +
               MF=(E,WORK_OCPL)
         MVI   WORK_PLINE-1,C' '
         MVC   WORK_PLINE,WORK_PLINE-1
         MVC   WORK_LINK,BLANKS               INITIALIZE LINK
         BAL   R10,HEAD0000                   PRINT HEADING
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
INIT0100 DS    0H
         GET   WORK_SYSIN,WORK_CONTROL_STATEMENT
         MVC   WORK_PLINE+1(WORK_CONTROL_STATEMENTL),WORK_CONTROL_STATE+
               MENT
         BAL   R10,PRT0000                    PRINT IT
         CLC   WORK_CONTROL_KEYWORD,=CL9'LINK'
         BE    INIT0110                       YES
         CLC   WORK_CONTROL_KEYWORD,=CL9'EXCLUDE'
         BE    INIT0150                       YES
         MVC   WORK_PLINE(ERROR_01L),ERROR_01
         BAL   R10,PRT0000                    PRINT
         MVI   WORK_RC,X'08'                  SET RC
         B     INIT0100                       READ NEXT STATEMENT
INIT0110 DS    0H
         MVC   WORK_LINK,WORK_CONTROL_LINK
         SR    R1,R1
         LA    R2,WORK_LINK
INIT0120 DS    0H
         CLI   0(R2),C' '
         BE    INIT0130
         LA    R1,1(R1)
         LA    R2,1(R2)
         B     INIT0120
INIT0130 DS    0H
         ST    R1,WORK_LINK_LENGTH
         B     INIT0100                       READ NEXT STATEMENT
INIT0150 DS    0H
         GETMAIN RU,                                                   +
               LV=EXCLUDEL,                                            +
               LOC=BELOW
         LR    R6,R1                          COPY ADDRESS
         USING EXCLUDE_DSECT,R6               DEFINE BASE
         MVC   EXCLUDE_NEXT,WORK_EXCLUDE
         ST    R6,WORK_EXCLUDE
         MVC   EXCLUDE_MEMBER,WORK_CONTROL_MEMBER
         B     INIT0100
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
PROC0000 DS    0H
         CLI   WORK_RC,0                      BELOW ERRORS?
         BNE   EXIT0000                       YES
PROC0010 DS    0H
         GET   WORK_DIR,WORK_DIR_IOAREA
         LH    R8,WORK_DIR_IOAREA             BYTES OF DATA IN RECORD
         SH    R8,H2                          MINUS 2 FOR LENGTH
         LA    R7,WORK_DIR_IOAREA+2           FIRST ENTRY
         USING DIRDSECT,R7                    DEFINE BASE
PROC0020 DS    0H
         CLI   0(R7),X'FF'                    END OF DIRECTORY?
         BE    PROC0100                       YES
         CLC   DIRNAME,=CL8'INDEX'            INDEX MEMBER ITSELF?
         BE    PROC0040                       YES
         ICM   R6,15,WORK_EXCLUDE             FIRST EXCLUDED MEMBER
         BZ    PROC0040                       YES
PROC0030 DS    0H
         CLC   EXCLUDE_MEMBER,DIRNAME         EXCLUDE THIS MEMBER?
         BE    PROC0070                       YES
         ICM   R6,15,EXCLUDE_NEXT             NEXT EXCLUDED MEMBER
         BNZ   PROC0030
PROC0040 DS    0H
         AP    WORK_MEMBERS,P1                ADD TO MEMBER COUNTER
         GETMAIN RU,                                                   +
               LV=MEMBERL,                                             +
               LOC=BELOW
         OC    WORK_MEMBER,WORK_MEMBER
         BNZ   PROC0050
         ST    R1,WORK_MEMBER
PROC0050 DS    0H
         LR    R5,R1
         ICM   R1,15,WORK_LAST_MEMBER
         BZ    PROC0060
         ST    R5,MEMBER_NEXT-MEMBER_DSECT(R1)
PROC0060 DS    0H
         ST    R5,WORK_LAST_MEMBER
         USING MEMBER_DSECT,R5
         XC    MEMBER_NEXT,MEMBER_NEXT
         MVC   MEMBER_MEMBER,DIRNAME
         B     PROC0080
PROC0070 DS    0H
         OI    EXCLUDE_FLAGS,$EXCLUDE_FOUND
PROC0080 DS    0H
         NI    DIRC,X'1F'                     only LENGTH BITS
         SR    R1,R1                          CLEAR REGISTER
         IC    R1,DIRC                        HALFWORDS OF USER DATA
         AR    R1,R1                          BYTES OF USER DATA
         LA    R1,DIRUSER-DIRDSECT(R1)        TOTAL LENGTH
         SR    R8,R1                          DIRECTORY DATA REMAINING
         BZ    PROC0010                       READ NEXT DIRECTORY BLOCK
         AR    R7,R1                          NEXT MEMBER
         B     PROC0020                       PROCESS NEXT MEMBER
* ------------------------------------------------------------------- *
*                                                                     *
*     PDS directory has been read.                                    *
*                                                                     *
* ------------------------------------------------------------------- *
PROC0100 DS    0H
         OPEN  (WORK_INDEX,OUTPUT),                                    +
               MODE=31,                                                +
               MF=(E,WORK_OCPL)
         LA    R11,WORK_INDEX
         USING IHADCB,R11
         LH    R0,DCBLRECL
         CH    R0,INDEX_LINK_LL
         BL    ERR0010
         AH    R0,H8
         GETMAIN RU,                                                   +
               LV=(0),                                                 +
               LOC=BELOW
         ST    R1,WORK_OUTPUT_BUFFER
         BAL   R10,CLEAR000
         L     R2,WORK_OUTPUT_BUFFER
         MVC   0(HTML_STARTL+4,R2),HTML_START_LL
         TM    DCBRECFM,DCBRECV
         BO    PROC0110
         LA    R2,4(R2)
PROC0110 DS    0H
         BAL   R10,PUT0000
         ICM   R5,15,WORK_MEMBER              FIRST MEMBER
         BZ    PROC0140
PROC0120 DS    0H
         L     R2,WORK_OUTPUT_BUFFER
         MVC   0(INDEX_LINKL+4,R2),INDEX_LINK_LL
         MVC   INDEX_LINK_LINK-INDEX_LINK_LL(10,R2),WORK_LINK
         L     R1,WORK_LINK_LENGTH
         LA    R1,INDEX_LINK_LINK-INDEX_LINK_LL(R1,R2)
         MVC   0(8,R1),MEMBER_MEMBER
         MVC   INDEX_LINK_MEMBER-INDEX_LINK_LL(8,R2),MEMBER_MEMBER
         TM    DCBRECFM,DCBRECV
         BO    PROC0130
         LA    R2,4(R2)
PROC0130 DS    0H
         BAL   R10,PUT0000
         L     R4,MEMBER_NEXT
         FREEMAIN RU,                                                  +
               A=(R5),                                                 +
               LV=MEMBERL
         LTR   R5,R4
         BNZ   PROC0120
         B     PROC0160
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
PROC0140 DS    0H
         L     R2,WORK_OUTPUT_BUFFER
         MVC   0(NO_MEMBERSL+4,R2),NO_MEMBERS
         TM    DCBRECFM,DCBRECV
         BO    PROC0150
         LA    R2,4(R2)
PROC0150 DS    0H
         BAL   R10,PUT0000
PROC0160 DS    0H
         L     R2,WORK_OUTPUT_BUFFER
         MVC   0(HTML_ENDL+4,R2),HTML_END_LL
         TM    DCBRECFM,DCBRECV
         BO    PROC0170
         LA    R2,4(R2)
PROC0170 DS    0H
         BAL   R10,PUT0000
         ICM   R6,15,WORK_EXCLUDE
         BZ    EXIT0000
PROC0180 DS    0H
         TM    EXCLUDE_FLAGS,$EXCLUDE_FOUND
         BO    PROC0190
         MVC   WORK_PLINE(ERROR_02L),ERROR_02
         MVC   WORK_PLINE+(ERROR_02_MEMBER-ERROR_02)(8),EXCLUDE_MEMBER
         BAL   R10,PRT0000
         MVI   WORK_RC,4
PROC0190 DS    0H
         L     R4,EXCLUDE_NEXT
         FREEMAIN RU,                                                  +
               A=(R6),                                                 +
               LV=EXCLUDEL
         LTR   R6,R4
         BNZ   PROC0180
         MVC   WORK_PLINE(TOTAL_MSGL),TOTAL_MSG
         ED    WORK_PLINE+(TOTAL_MEMBERS-TOTAL_MSG)(L'TOTAL_MEMBERS),WO+
               RK_MEMBERS
         BAL   R10,PRT0000
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
PUT0000  DS    0H
         PUT   WORK_INDEX,0(R2)
CLEAR000 DS    0H
         L     R0,WORK_OUTPUT_BUFFER
         LH    R1,DCBLRECL
         AH    R1,H8
         SR    R14,R14
         SR    R15,R15
         ICM   R15,8,BLANKS
         MVCL  R0,R14
         BR    R10
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0010  DS    0H
         MVC   WORK_PLINE(ERROR_03L),ERROR_03
         BAL   R10,PRT0000
         MVI   WORK_RC,12
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         CLOSE WORK_INDEX,                                             +
               MODE=31,                                                +
               MF=(E,WORK_OCPL)
EXIT0010 DS    0H
         CLOSE WORK_SYSPRINT,                                          +
               MODE=31,                                                +
               MF=(E,WORK_OCPL)
         CLOSE WORK_SYSIN,                                             +
               MODE=31,                                                +
               MF=(E,WORK_OCPL)
         SR    R2,R2
         IC    R2,WORK_RC
         LR    R1,R13
         L     R13,4(R13)
         FREEMAIN RU,                                                  +
               A=(1),                                                  +
               LV=WORKL
         LR    R15,R2
         L     R14,12(R13)
         LM    R0,R12,20(R13)
         BR    R14                                                      5)
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
PRT0000  DS    0H
         PUT   WORK_SYSPRINT,WORK_PLINE
         AP    WORK_LINE_NUMBER,P1            ADD 1 TO LINE COUNT
PRT0010  DS    0H
         MVC   WORK_PLINE,WORK_PLINE-1        CLEAR PRINT AREA
         CP    WORK_LINE_NUMBER,P65           TIME FOR HEADING??
         BLR   R10                            NO
HEAD0000 DS    0H
         AP    WORK_PAGE_NUMBER,P1            ADD 1 TO PAGE COUNT
         MVC   WORK_PLINE(HEADINGL),HEADING
         MVC   WORK_PLINE+(HEADPAGE-HEADING)(L'HEADPAGE),=X'40202020212+
               0'
         ED    WORK_PLINE+(HEADPAGE-HEADING)(L'HEADPAGE),WORK_PAGE_NUMB+
               ER
         PUT   WORK_SYSPRINT,WORK_PLINE
         MVC   WORK_PLINE,WORK_PLINE-1
         ZAP   WORK_LINE_NUMBER,P0
         BR    R10                            RETURN
*---------------------------------------------------------------------*
*                      DATA AREAS                                     *
*---------------------------------------------------------------------*
                LTORG
OPENI           OPEN (*,INPUT),MODE=31,MF=L
OPENL           EQU  *-OPENI
H2              DC   H'2'
H8              DC   H'8'
P0              DC   P'0'
P1              DC   P'1'
P65             DC   P'65'
BLANKS          DC   CL10' '
HEADING         DS   0C
                DC   C'1'
                DC   C'HTTPINDX   HTML  PDS   INDEX builder'
                DC   CL20' '
                DC   C'PAGE '
HEADPAGE        DC   X'40202120212'
HEADINGL        EQU  *-HEADING

ERROR_01        DS   0C
                DC   C' Invalid control statement'
ERROR_01L       EQU  *-ERROR_01

ERROR_02        DS   0C
                DC   C'0EXCLUDED MEMBER '
ERROR_02_MEMBER DC   CL8' '
                DC   c' not found'
ERROR_02L       EQU  *-ERROR_02

ERROR_03        DS   0C
                DC   C'-SYSUT1''s LRECL is too small for index records'
ERROR_03L       EQU  *-ERROR_03

TOTAL_MSG       DS   0C
                DC   C'-'
                DC   C'NUMBER OF MEMBERS INDEXED WAS '
TOTAL_MEMBERS   DC   X'40206B2020206B202120'
TOTAL_MSGL      EQU  *-TOTAL_MSG

HTML_START_LL   DC   Y(HTML_STARTL+4)
                DC   AL2(0)
HTML_START      DS   0C
                DC   C'<HTML>'
                DC   C'<BODY>'
                DC   C'<PRE>'
                DC   AL1($LF)
HTML_STARTL     EQU  *-HTML_START

HTML_END_LL     DC   Y(HTML_ENDL+4)
                DC   AL2(0)
HTML_END        DS   0C
                DC   C'</PRE>'
                DC   C'</BODY>'
                DC   C'</HTML>'
                DC   AL1($LF)
HTML_ENDL       EQU  *-HTML_END

INDEX_LINK_LL   DC   Y(INDEX_LINKL+4)
                DC   AL2(0)
INDEX_LINK      DS   0C
                DC   C'<A HREF='
INDEX_LINK_LINK DC   CL20' '
                DC   C'>'
INDEX_LINK_MEMBER DC CL8' '
                DC   C'</A>'
                DC   AL1($LF)
INDEX_LINKL     EQU  *-INDEX_LINK

NO_MEMBERS_LL   DC   Y(NO_MEMBERSL+4)
                DC   AL2(0)
NO_MEMBERS      DC   C'NO MEMBERS'
                DC   AL1($LF)
NO_MEMBERSL     EQU  *-NO_MEMBERS
         SPACE 2
*---------------------------------------------------------------------*
*                      DCB'S                                          *
*---------------------------------------------------------------------*
DIRI     DCB   DSORG=PS,                                               +
               DDNAME=SYSUT1,                                          +
               RECFM=U,                                                +
               LRECL=256,                                              +
               EODAD=PROC0100,                                         +
               MACRF=GM
DIRL     EQU   *-DIRI
SYSINI   DCB   DSORG=PS,                                               +
               DDNAME=SYSIN,                                           +
               RECFM=FB,                                               +
               LRECL=80,                                               +
               EODAD=PROC0000,                                         +
               MACRF=GM
SYSINL   EQU   *-SYSINI
SYSPRINTI DCB  DSORG=PS,                                               +
               RECFM=FBA,                                              +
               LRECL=133,                                              +
               DDNAME=SYSPRINT,                                        +
               MACRF=PM
SYSPRINTL EQU  *-SYSPRINTI
INDEXI   DCB   DSORG=PS,                                               +
               DDNAME=INDEX,                                           +
               MACRF=PM
INDEXL    EQU  *-INDEXI

*---------------------------------------------------------------------*
*                      DATA AREAS                                     *
*---------------------------------------------------------------------*
WORK                 DSECT
WORK_SAVE            DS   18F
WORK_EXCLUDE         DS   A
WORK_MEMBER          DS   A
WORK_LAST_MEMBER     DS   A

WORK_DIR             DS   0F,(DIRL)X
WORK_SYSPRINT        DS   0F,(SYSPRINTL)X
WORK_SYSIN           DS   0F,(SYSINL)X
WORK_INDEX           DS   0F,(INDEXL)X


WORK_OCPL            DS   0F,(OPENL)C

WORK_LINK_LENGTH     DS   F
WORK_LINK            DS   CL10

                     DS   C
WORK_PLINE           DS   CL133
WORK_LINE_NUMBER     DS   PL3
WORK_PAGE_NUMBER     DS   PL3
WORK_MEMBERS         DS   PL4

WORK_DIR_IOAREA      DS   256C

WORK_OUTPUT_BUFFER   DS   A

WORK_RC              DS   X

WORK_CONTROL_STATEMENT   DS  0C
WORK_CONTROL_KEYWORD     DS  CL9
WORK_CONTROL_MEMBER      DS  CL8
                         ORG WORK_CONTROL_MEMBER
WORK_CONTROL_LINK        DS  CL10
                         ORG
                         DS  CL63
WORK_CONTROL_STATEMENTL  EQU *-WORK_CONTROL_STATEMENT

WORKL                EQU  *-WORK
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
DIRDSECT             DSECT
DIRNAME              DS   CL8
DIRTTR               DS   CL3
DIRC                 DS   X
DIRUSER              DS   0C
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
EXCLUDE_DSECT        DSECT
EXCLUDE_NEXT         DS   A
EXCLUDE_MEMBER       DS   CL8
EXCLUDE_FLAGS        DS   X
$EXCLUDE_FOUND       EQU  X'80'
EXCLUDEL             EQU  *-EXCLUDE_DSECT
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
MEMBER_DSECT         DSECT
MEMBER_NEXT          DS   A
MEMBER_MEMBER        DS   CL8
MEMBERL              EQU  *-MEMBER_DSECT
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
                     IEZIOB
                     DCBD   DSORG=PO
*---------------------------------------------------------------------*
*                      EQUATES                                        *
*---------------------------------------------------------------------*
                     COPY   REGEQU
$LF                  EQU    X'0A'
$OUTPUT_BLOCKSIZE    EQU    32760
         END    HTTPINDX
