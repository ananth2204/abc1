*---------------------------------------------------------------------*
*                                                                     *
*  Module name: HTTPURL                                               *
*                                                                     *
*   This subtask manages the URL data space                           *
*                                                                     *
* ------------------------------------------------------------------- *
HTTPURL  CSECT
HTTPURL  AMODE 31
HTTPURL  RMODE ANY
         USING HTTPURL,R12                    DEFINE BASE
         USING HTTPCOMM,R11                   DEFINE BASE
         STM   R14,R12,12(R13)                CHAIN REGISTERS
         LR    R12,R15                        COPY ENTRY POINT ADDRESS
         B     INIT0000
         DC    CL8'HTTPURL'
         DC    CL8'&SYSDATE'
         DC    CL6'&SYSTIME'
*---------------------------------------------------------------------*
*                                                                     *
*                                                                     *
*            Initialization                                           *
*                                                                     *
*                                                                     *
*---------------------------------------------------------------------*
INIT0000 DS    0H
         L     R11,0(R1)                      HTTPCOMM'S ADDRESS
         GETMAIN RU,                          STORAGE FOR WORK AREA    +
               LV=WORK_AREA_LENGTH,                                    +
               LOC=ANY
         ST    R13,4(R1)                      CHAIN PREVIOUS AREA
         ST    R1,8(R13)                      CHAIN OUR AREA
         LR    R13,R1                         COPY WORK AREA ADDRESS
         USING WORKAREA,R13
         ITRACE ID=INIT
         ST    R12,COMM_HTTPURL_ADDRESS
         ST    R13,COMM_URL_WORK_AREA
*---------------------------------------------------------------------*
*            Create the URL data space                                *
*---------------------------------------------------------------------*
         ITRACE ID=DSPSERV
         MVC   WORK_DSPSERV(DSPSERV_L),DSPSERV_I
         MVC   WORK_ALESERV(ALESERV_L),ALESERV_I
         DSPSERV CREATE,                      CREATE A DATASPACE       +
               NAME=URL_SPACE_NAME,           .. DATASPACE'S NAME      +
               STOKEN=COMM_URL_SPACE_TOKEN,   .. RETURNED TOKEN        +
               SCOPE=SINGLE,                  .. FOR OUR EYS ONLY      +
               TYPE=BASIC,                    .. NOT HIPERSPACE        +
               BLOCKS=0,                      .. SIZE IN 4K BLOCKS     +
               MF=(E,WORK_DSPSERV)
         STH   R15,WORK_RC                    SAVE RETURN CODE
         OC    WORK_RC,WORK_RC                SUCESSFUL?
         BNZ   ERR0010                        NO
         ITRACE ID=ALESERV
         ALESERV ADD,                         ADD TO ACCESS LIST       +
               STOKEN=COMM_URL_SPACE_TOKEN,                            +
               ALET=COMM_URL_SPACE_ALET,                               +
               MF=(E,WORK_ALESERV)
         STH   R15,WORK_RC                    SAVE RETURN CODE
         OC    WORK_RC,WORK_RC                SUCESSFUL?
         BNZ   ERR0020                        NO
         ITRACE ID=ALEOK,                                              +
               DATA1=(COMM_URL_SPACE_TOKEN,8),                         +
               DATA2=(COMM_URL_SPACE_ALET,4)
         MVI   PRT_COMMAND,$PRT_PRINT
         XC    PRT_NEXT,PRT_NEXT
         MVI   PRT_CC,C' '
         MVC   PRT_MESSAGE_ID(L'PRT_MESSAGE_ID+L'PRT_DATA),PRT_CC
*---------------------------------------------------------------------*
*                                                                     *
*                                                                     *
*            Main processing                                          *
*                                                                     *
*                                                                     *
*---------------------------------------------------------------------*
URL0010  DS    0H
         TM    COMM_EOJ,$EOJ_PHASE_1          SHUTDOWN PHASE 1?
         BO    EXIT0000                       YES.. IT'S MILLER TIME
         TM    COMM_URL_WORK_ECB,X'40'        ECB ALREADY POSTED?
         BO    URL0020                        GET BUSY
         ITRACE ID=WORKWAIT,                  WAITING FOR WORK         +
               RDATA1=R1                      .. ECB'S ADDRESS
         WAIT  ECB=COMM_URL_WORK_ECB          WAIT FOR WORK TO DO ECB
URL0020  DS    0H
         ITRACE ID=WOKEUP
         XC    COMM_URL_WORK_ECB,COMM_URL_WORK_ECB
URL0030  DS    0H
         SR    R1,R1                          CLEAR REGISTER
         L     R4,COMM_URL_QUEUE
         CS    R4,R1,COMM_URL_QUEUE
         BC    4,URL0030
         USING WEBURL,R10                     DEFINE BASE
         USING URL,WEBURL_URL                 DEFINE BASE
         ITRACE ID=HAVEQ,                     WE HAVE THE QUEUE        +
               RDATA1=R4                      .. LAST QUEUE BLOCK
         LTR   R3,R4                          COPY ADDRESS
         BZ    URL0010
         LA    R1,4                           INITIALIZE GETMAIN LENGTH
URL0050  DS    0H
         ITRACE ID=VEC1,                                               +
               RDATA1=R3,                                              +
               RDATA2=R1
         ICM   R3,15,WEBURL_NEXT-WEBURL(R3)
         BZ    URL0060                        NEXT BLOCK'S ADDRESS
         LA    R1,4(R1)                       ADD TO VECTOR LENGTH
         B     URL0050                        LOOP
URL0060  DS    0H
         ITRACE ID=GETVEC,                                             +
               RDATA1=R1,                     .. SIZE OF VECTOR LIST   +
               RDATA2=R4                      .. 1ST TO GO ON CHAIN
         ST    R1,WORK_VECTOR_SIZE            SAVE VECTOR SIZE
         GETMAIN RU,                          GETMAIN STORAGE          +
               LV=(1),                                                 +
               LOC=ANY
         ST    R1,WORK_VECTOR_ADDRESS         SAVE VECTOR ADDRESS
         ITRACE ID=VECADDR,                                            +
               RDATA1=R1,                     .. VECTOR ADDRESS        +
               RDATA2=R4                      .. 1ST TO GO ON CHAIN
URL0070  DS    0H
         ITRACE ID=BLDVEC,                                             +
               RDATA1=R1,                     .. CURRENT IN VECTOR     +
               RDATA2=R4                      .. WEBURL'S ADDRESS
         ST    R4,0(R1)                       SET ADDRESS
         ST    R1,WORK_VECTOR_CURRENT
         LA    R1,4(R1)                       NEXT
         ICM   R4,15,WEBURL_NEXT-WEBURL(R4)
         BNZ   URL0070
*---------------------------------------------------------------------*
*       Process work on queue                                         *
*---------------------------------------------------------------------*
URL0080  DS    0H
         L     R1,WORK_VECTOR_CURRENT
         L     R10,0(R1)                      WEBURL'S ADDRESS
         ITRACE ID=URL,                                                +
               RDATA1=R1,                                              +
               RDATA2=R10
         CLI   WEBURL_STATUS,$URL_DEFINE_SPECIFIC
         BE    URL0100                        YES
         CLI   WEBURL_STATUS,$URL_DEFINE_PREFIX
         BE    URL0100                        YES
         CLI   WEBURL_STATUS,$URL_DELETE      DELETE URL(S)?
         BE    URL0300                        YES
         CLI   WEBURL_STATUS,$URL_LOOKUP      LOOKUP URL?
         BE    URL0200                        YES
         CLI   WEBURL_STATUS,$URL_SNAP        SNAP DUMP?
         BE    URL0500                        YES
         DC    H'0'
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
URL0100  DS    0H
         ITRACE ID=DEF_URL,                                            +
               DATA1=(URL_LENGTH,4),                                   +
               DATA2=(URL_TEXT,8)
         SR    R7,R7                          START AT ZERO
         LAM   R7,R7,COMM_URL_SPACE_ALET      ALET FOR DATA SPACE
         SAC   512                            TURN ON AR MODE
         SR    R1,R1
         ICM   R6,15,COMM_NBR_URLS            NBR OF ACTIVE URLS
         BZ    URL0120                        NO ACTIVE URLS
URL0110  DS    0H
         USING DATASPACE,R7
         ICM   R1,3,DATASPACE_LENGTH          URL TEXT LENGTH
         LA    R7,DATASPACE_L(R1,R7)          NEXT URL
         BCT   R6,URL0110                     LOOP
URL0120  DS    0H
         MVC   DATASPACE_SERVBLOK,WEBURL_SERVBLOK
         ICM   R1,3,URL_LENGTH                URL TEXT LENGTH
         STCM  R1,3,DATASPACE_LENGTH          SET LENGTH
         LA    R0,URL_TEXT                    TEXT IN REQ BLOCK
         LA    R14,DATASPACE_TEXT             TEXT IN DATA SPACE
         LR    R15,R1                         COPY LENGTH
         LAM   R14,R14,COMM_URL_SPACE_ALET
         MVCL  R14,R0                         COPY TO DATA SPACE
         LAM   R14,R14,F0
         CLI   WEBURL_STATUS,$URL_DEFINE_SPECIFIC
         BE    URL0130
         MVI   DATASPACE_URL_TYPE,$URL_PREFIX SET URL TYPE
         B     URL0140
URL0130  DS    0H
         MVI   DATASPACE_URL_TYPE,$URL_SPECIFIC
URL0140  DS    0H
         LAM   R7,R7,F0
         SAC   0
         LA    R0,1
         A     R0,COMM_NBR_URLS               PLUS NBR OF URLS
         ST    R0,COMM_NBR_URLS               UPDATE
         MVC   PRT_MESSAGE_ID(MSG03_L),MSG03
         UNPK  PRT_MESSAGE_ID+(MSG03_SERVBLOK-MSG03)(9),WEBURL_SERVBLOK+
               (5)
         TR    PRT_MESSAGE_ID+(MSG03_SERVBLOK-MSG03)(8),HEXCHAR
         MVI   PRT_MESSAGE_ID+(MSG03_SERVBLOK-MSG03)+8,C' '
         ICM   R15,15,WEBURL_SERVBLOK
         USING SERVBLOK,R15
         BM    URL0150
         MVC   PRT_MESSAGE_ID+(MSG03_NAME-MSG03)(L'MSG03_NAME),PARTNER_+
               CLIENT_NAME
         MVC   PRT_MESSAGE_ID+(MSG03_TASK-MSG03)(L'MSG03_TASK),PARTNER_+
               CLIENT_TASK
         B     URL0160
URL0150  DS    0H
         MVC   PRT_MESSAGE_ID+(MSG03_NAME-MSG03)(L'MSG03_NAME),LOCAL
URL0160  DS    0H
         SR    R1,R1
         ICM   R1,3,URL_LENGTH
         CVD   R1,WORK_DWORD
         ED    PRT_MESSAGE_ID+(MSG03_LENGTH-MSG03)(L'MSG03_LENGTH),WORK+
               _DWORD+6
         CH    R1,=Y(L'MSG03_TEXT)
         BNH   URL0170
         LH    R1,=Y(L'MSG03_TEXT)
URL0170  DS    0H
         BCTR  R1,0
         EX    R1,MVC01
         BAL   R9,PRT0000
         B     URL1000
MVC01    MVC   PRT_MESSAGE_ID+(MSG03_TEXT-MSG03)(0),URL_TEXT
*---------------------------------------------------------------------*
*        Look-up a URL (Determine server task)                        *
*---------------------------------------------------------------------*
URL0200  DS    0H
         ITRACE ID=LOOKUP
         SR    R6,R6
         ICM   R6,15,COMM_NBR_URLS            NBR OF URLS
         BZ    URL0250                        NO URLS
         SR    R7,R7                          URLS START AT ZERO
         LAM   R7,R7,COMM_URL_SPACE_ALET      SET AR
         SAC   512                            TURN ON AR MODE
URL0210  DS    0H
         SR    R1,R1
         ICM   R1,3,URL_LENGTH                URL TEXT LENGTH
         CLI   DATASPACE_URL_TYPE,$URL_SPECIFIC
         BE    URL0230                        LOCATE SPECIFIC URL
URL0220  DS    0H
         CLM   R1,3,DATASPACE_LENGTH          COMPARE LENGTHS
         BL    URL0240                        SHORTER..
         ICM   R1,3,DATASPACE_LENGTH          USE STASHED LENGTH
         LA    R0,URL_TEXT                    TEXT FROM URL
         LA    R14,DATASPACE_TEXT             TEXT IN DATA SPACE
         LR    R15,R1                         COPY LENGTH
         LAM   R14,R14,COMM_URL_SPACE_ALET    SET AR
         CLCL  R0,R14                         COMPARE URLS
         BE    URL0260
         B     URL0240
URL0230  DS    0H
         CLM   R1,3,DATASPACE_LENGTH          COMPARE LENGTHS
         BNE   URL0240                        NOT EQUAL
         LA    R0,URL_TEXT                    TEXT IN URL
         LA    R14,DATASPACE_TEXT             TEXT IN DATA SPACE
         LR    R15,R1                         COPY TO LENGTH 2
         LAM   R14,R14,COMM_URL_SPACE_ALET    SET AR
         CLCL  R0,R14                         COMPARE URLS
         BE    URL0260                        FOUND THE URL
URL0240  DS    0H
         LAM   R14,R14,F0                     RESET AR
         ICM   R1,3,DATASPACE_LENGTH          URL TEXT LENGTH
         LA    R7,DATASPACE_L(R1,R7)          NEXT URL
         BCT   R6,URL0210                     LOOP
URL0250  DS    0H
         SAC   0
         LAM   R7,R7,F0
         ITRACE ID=NOT_FND
         XC    WEBURL_SERVBLOK,WEBURL_SERVBLOK   URL NOT LOCATED
         B     URL1000
URL0260  DS    0H
         MVC   WEBURL_SERVBLOK,DATASPACE_SERVBLOK
         SAC   0
         LAM   R7,R7,F0
         LAM   R14,R14,F0
         ITRACE ID=FND_SERV,                                           +
               DATA1=(WEBURL_SERVBLOK,4)
         B     URL1000
*---------------------------------------------------------------------*
*        Delete URLs for a server                                     *
*---------------------------------------------------------------------*
URL0300  DS    0H
         ITRACE ID=DELETE,                                             +
               DATA1=WEBURL_SERVBLOK
         SR    R2,R2                          CLEAR R2
         SR    R4,R4                          CLEAR R4
         ICM   R6,15,COMM_NBR_URLS            NBR OF URLS NOW
         BZ    URL0390
         XC    COMM_NBR_URLS,COMM_NBR_URLS    RESET NBR OF URLS
         LAM   R2,R2,COMM_URL_SPACE_ALET
         LAM   R4,R4,COMM_URL_SPACE_ALET
         SAC   512                            TURN ON AR MODE
OLD      USING DATASPACE,R2
NEW      USING DATASPACE,R4
URL0310  DS    0H
         SR    R3,R3
         ICM   R3,3,OLD.DATASPACE_LENGTH      LENGTH OF THE URL TEXT
         LA    R3,DATASPACE_L(,R3)            LENGTH OF URL BLOCK
         CLC   OLD.DATASPACE_SERVBLOK,WEBURL_SERVBLOK
         BE    URL0330                        YES.. DON'T COPY THIS URL
         LA    R0,1
         A     R0,COMM_NBR_URLS               PLUS NBR OF URLS
         ST    R0,COMM_NBR_URLS               SAVE TOTAL
         CR    R2,R4                          AT SAME ADDRESS?
         BE    URL0320                        SKIP THE MVCL
         LR    R5,R3                          COPY LENGTH
         MVCL  R4,R2                          COPY URL
         B     URL0370
URL0320  DS    0H
         AR    R4,R3                          NEXT URL
         B     URL0370
URL0330  DS    0H
         MVC   PRT_MESSAGE_ID(MSG04_L),MSG04
         UNPK  PRT_MESSAGE_ID+(MSG04_SERVBLOK-MSG04)(9),WEBURL_SERVBLOK+
               (5)
         TR    PRT_MESSAGE_ID+(MSG04_SERVBLOK-MSG04)(8),HEXCHAR
         MVI   PRT_MESSAGE_ID+(MSG04_SERVBLOK-MSG04)+8,C' '
         ICM   R15,15,OLD.DATASPACE_SERVBLOK
         BM    URL0340
         MVC   PRT_MESSAGE_ID+(MSG04_NAME-MSG04)(L'MSG04_NAME),PARTNER_+
               CLIENT_NAME
         MVC   PRT_MESSAGE_ID+(MSG04_TASK-MSG04)(L'MSG04_TASK),PARTNER_+
               CLIENT_TASK
         B     URL0350
URL0340  DS    0H
         MVC   PRT_MESSAGE_ID+(MSG04_NAME-MSG04)(L'MSG04_NAME),LOCAL
URL0350  DS    0H
         SR    R14,R14
         ICM   R14,3,OLD.DATASPACE_LENGTH
         CVD   R14,WORK_DWORD
         ED    PRT_MESSAGE_ID+(MSG04_LENGTH-MSG04)(L'MSG04_LENGTH),WORK+
               _DWORD+6
         CH    R14,=Y(L'MSG04_TEXT)
         BNH   URL0360
         LH    R14,=Y(L'MSG04_TEXT)
URL0360  DS    0H
         BCTR  R14,0
         EX    R14,MVC02
         LAM   R2,R2,F0
         LAM   R4,R4,F0
         SAC   0
         BAL   R9,PRT0000
         LAM   R2,R2,COMM_URL_SPACE_ALET
         LAM   R4,R4,COMM_URL_SPACE_ALET
         SAC   512
URL0370  DS    0H
         AR    R2,R3                          NEXT URL
URL0380  DS    0H
         BCT   R6,URL0310                     LOOP
         SAC   0
         LAM   R2,R2,F0
         LAM   R4,R4,F0
URL0390  DS    0H
         ITRACE ID=ACTIVE,                                             +
               DATA1=(COMM_NBR_URLS,4)
         B     URL1000
MVC02    MVC   PRT_MESSAGE_ID+(MSG04_TEXT-MSG04)(0),OLD.DATASPACE_TEXT
*---------------------------------------------------------------------*
*        The address of the DCB is passed in WEBURL_SERVBLOK          *
*---------------------------------------------------------------------*
URL0500  DS    0H
         ITRACE ID=DUMP
         L     R5,WEBURL_SERVBLOK             DCB ADDRESS
         ICM   R6,15,COMM_NBR_URLS            NBR OF URLS
         BZ    URL0550
         SR    R7,R7
         LAM   R7,R7,COMM_URL_SPACE_ALET
URL0510  DS    0H
         ITRACE ID=DUMP_URL,                                           +
               RDATA1=R7,                                              +
               RDATA2=R6
         MVI   WORK_CC,C' '                   INITIALIZE CC
         MVC   WORK_PRINT_DATA,WORK_CC        INITALIZE PRINT I/O
         MVC   WORK_PRINT_DATA(MSG01_L),MSG01
         SAC   512
         UNPK  WORK_PRINT_DATA+(MSG01_SERVBLOK-MSG01)(9),DATASPACE_SERV+
               BLOK(5)
         TR    WORK_PRINT_DATA+(MSG01_SERVBLOK-MSG01)(8),HEXCHAR
         MVI   WORK_PRINT_DATA+(MSG01_SERVBLOK-MSG01)+8,C' '
         CLI   DATASPACE_URL_TYPE,$URL_SPECIFIC
         BE    URL0520
         MVI   WORK_PRINT_DATA+(MSG01_TYPE-MSG01),C'P'
         B     URL0530
URL0520  DS    0H
         MVI   WORK_PRINT_DATA+(MSG01_TYPE-MSG01),C'S'
URL0530  DS    0H
         SR    R2,R2
         ICM   R2,3,DATASPACE_LENGTH          URL LENGTH
         CVD   R2,WORK_DWORD                  CONVERT TO DECIMAL
         ED    WORK_PRINT_DATA+(MSG01_LENGTH-MSG01)(L'MSG01_LENGTH),WOR+
               K_DWORD+6
         CH    R2,=Y(L'WORK_PRINT_DATA)
         BNH   URL0540
         LH    R2,=Y(L'WORK_PRINT_DATA)
URL0540  DS    0H
         BCTR  R2,0
         EX    R2,URL_MVC                     COPY TEXT
         SAC   0
         PUT   (R5),WORK_CC
         LA    R7,DATASPACE_L+1(R2,R7)        NEXT URL
         BCT   R6,URL0510                     LOOP
         LAM   R7,R7,F0
         B     URL1000
URL_MVC  MVC   WORK_PRINT_DATA+(MSG01_TEXT-MSG01)(0),DATASPACE_TEXT
URL0550  DS    0H
         MVI   WORK_CC,C' '                   INITIALIZE CC
         MVC   WORK_PRINT_DATA,WORK_CC        INITALIZE PRINT I/O
         MVC   WORK_PRINT_DATA(MSG02_L),MSG02
         PUT   (R5),WORK_CC
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
URL1000  DS    0H
         LA    R2,WEBURL_ECB
         ITRACE ID=POSTECB,                                            +
               RDATA1=R2,                                              +
               DATA2=(0(R2),8)
         POST  WEBURL_ECB
         L     R1,WORK_VECTOR_CURRENT         CURRENT IN VECTOR
         C     R1,WORK_VECTOR_ADDRESS         AT FIRST ENTRY?
         BE    URL1010                        YES
         SH    R1,H4                          BACK UP 1 ENTRY
         ST    R1,WORK_VECTOR_CURRENT         SAVE CURRENT ADDRESS
         B     URL0080                        PROCESS THIS WEBURL
URL1010  DS    0H
         L     R0,WORK_VECTOR_SIZE            VECTOR SIZE
         ITRACE ID=FREEVEC,                                            +
               RDATA1=R1,                                              +
               RDATA2=R0
         FREEMAIN RU,                         FREE THE VECTOR          +
               A=(1),                                                  +
               LV=(0)
         B     URL0030
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
PRT0000  DS    0H
         ITRACE ID=PRINT,RDATA1=R10
         GETMAIN RU,                                                   +
               LV=PRTBLOKL,                                            +
               LOC=ANY
         MVC   0(PRTBLOKL,R1),PRTBLOK
PRT0010  DS    0H
         L     R14,COMM_PRINT_QUEUE           FIRST BLOCK ON QUEUE
         ST    R14,PRT_NEXT-PRTBLOK(R1)                       '
         CS    R14,R1,COMM_PRINT_QUEUE        ADD NEW BLOCK TO CHAIN
         BC    4,PRT0010                      TRY AGAIN
         ITRACE ID=POST_PRT
         POST  COMM_PRINT_WORK_ECB            WAKE UP THE PRINT SUBTASK
         MVC   PRT_MESSAGE_ID(L'PRT_MESSAGE_ID+L'PRT_DATA),PRT_CC
         BR    R9
*---------------------------------------------------------------------*
*                                                                     *
*                                                                     *
*            Termination/clean-up                                     *
*                                                                     *
*                                                                     *
*---------------------------------------------------------------------*
EXIT0000 DS    0H
         ITRACE ID=DEL_ALET,                                           +
               DATA1=(COMM_URL_SPACE_ALET,4)
         ALESERV DELETE, ,                                             +
               ALET=COMM_URL_SPACE_ALET,                               +
               MF=(E,WORK_ALESERV)
         ITRACE ID=ALE_RC,                                             +
               RDATA1=R15
         LTR   R15,R15                        SUCCESSFUL?
         BNZ   ERR0030                        NO
         ITRACE ID=DEL_SPAC,                                           +
               DATA1=(COMM_URL_SPACE_TOKEN,8)
         DSPSERV DELETE,                                               +
               STOKEN=COMM_URL_SPACE_TOKEN,                            +
               MF=(E,WORK_DSPSERV)
         ITRACE ID=DSP_RC,                                             +
               RDATA1=R15
         LTR   R15,R15                        SUCCESSFUL?
         BNZ   ERR0040                        NO
         LR    R1,R13                         COPY WORK AREA ADDRESS
         L     R13,4(R13)                     RESTORE REGISTER 13       ASE01670
         FREEMAIN RU,                         FREE WORK AREA           +
               A=(1),                                                  +
               LV=WORK_AREA_LENGTH
         LM    R14,R12,12(R13)                RESTORE REGISTERS         ASE01680
         SR    R15,R15                        RETURN CODE               ASE01690
         BR    R14                            RETURN TO CALLER          ASE01700
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0010  DS    0H
         DC    H'0'
ERR0020  DS    0H
         DC    H'0'
ERR0030  DS    0H
         DC    H'0'
ERR0040  DS    0H
         DC    H'0'
* ------------------------------------------------------------------- *
*                                                                     *
*        CONSTANTS                                                    *
*                                                                     *
* ------------------------------------------------------------------- *
F0             DC    F'0'
H4             DC    H'4'
DSPSERV_I      DSPSERV MF=(L,DSP)
DSPSERV_L      EQU   *-DSPSERV_I
ALESERV_I      ALESERV MF=L
ALESERV_L      EQU   *-ALESERV_I
URL_SPACE_NAME DC    CL8'HTTPURL'
LOCAL          DC    CL8'LOCAL'

HEXCHAR        EQU   *-C'0'
               DC    C'0123456789ABCDEF'

MSG01          DS    0C
               DC    C'SERVBLOK: '
MSG01_SERVBLOK DC    CL8' '
               DC    CL3' '
               DC    C'URL type: '
MSG01_TYPE     DC    C' '
               DC    C'   Length'
MSG01_LENGTH   DC    X'40202120'
               DC    C' '
MSG01_TEXT     DC    CL100' '
MSG01_L        EQU   *-MSG01

MSG02          DC    C'NO URLS'
MSG02_L        EQU   *-MSG02

MSG03          DS    0C
               DC    CL15'HTTPURL03I'
               DC    C'URL registered for '
MSG03_SERVBLOK DC    CL8' '
               DC    C' '
MSG03_NAME     DC    CL8' '
               DC    C' '
MSG03_TASK     DC    CL8' '
               DC    C' '
MSG03_LENGTH   DC    X'40202120'
               DC    C' '
MSG03_TEXT     DC    CL50' '
MSG03_L        EQU   *-MSG03

MSG04          DS    0C
               DC    CL15'HTTPURL04I'
               DC    C'URL deleted for '
MSG04_SERVBLOK DC    CL8' '
               DC    C' '
MSG04_NAME     DC    CL8' '
               DC    C' '
MSG04_TASK     DC    CL8' '
               DC    C' '
MSG04_LENGTH   DC    X'40202120'
               DC    C' '
MSG04_TEXT     DC    CL50' '
MSG04_L        EQU   *-MSG04

               LTORG
* ------------------------------------------------------------------- *
*                                                                     *
*        WORK AREAS                                                   *
*                                                                     *
* ------------------------------------------------------------------- *
WORKAREA              DSECT
                      STDSAVE DSECT=NO
WORK_DWORD            DS  D
WORK_VECTOR_SIZE      DS  F          ADDRESS VECTOR SIZE
WORK_VECTOR_ADDRESS   DS  A          ADDRESS OF ADDRESS VECTOR
WORK_VECTOR_CURRENT   DS  A          CURRENT ADDRESS IN ADDRESS VECTOR
WORK_DSPSERV          DS  0F,(DSPSERV_L)X
WORK_ALESERV          DS  0F,(ALESERV_L)X
WORK_RC               DS  H

WORK_CC               DS  C
WORK_PRINT_DATA       DS  CL132

                      PRTBLOK        DSECT=NO
WORK_AREA_LENGTH      EQU *-WORKAREA
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DATASPACE             DSECT
DATASPACE_SERVBLOK    DS   XL4
DATASPACE_URL_TYPE    DS   X
$URL_SPECIFIC         EQU  X'01'
$URL_PREFIX           EQU  X'02'
DATASPACE_LENGTH      DS   XL2
DATASPACE_TEXT        DS   0C
DATASPACE_L           EQU  *-DATASPACE
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                      COMMON    MAIN,TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                      WEBURL
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                      URL
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                      SERVBLOK  DSECT=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY REGEQU
         END  HTTPURL
