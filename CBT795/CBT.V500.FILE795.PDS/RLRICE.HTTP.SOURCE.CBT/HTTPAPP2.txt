         MACRO
         DEFURL &URL,&ADDR
         DC    AL2(B&SYSNDX-A&SYSNDX)
         DC    VL4(&ADDR)
A&SYSNDX DS    0C
         DC    C'&URL'
B&SYSNDX DS    0C
         MEND
* ------------------------------------------------------------------- *
*                                                                     *
*   This sample application has these functions:                      *
*                                                                     *
*       ACT     Active system regions (JOBs, STC, TSO users)          *
*       APF     Libraries in APF list                                 *
*       IPL     System IPL info including "IEASYSxx" info             *
*       LLA     Libraries in system link list                         *
*                                                                     *
* ------------------------------------------------------------------- *
HTTPAPP2 CSECT
HTTPAPP2 AMODE 31
HTTPAPP2 RMODE 24
         STM   R14,R12,12(R13)           SAVE REGISTERS
         LR    R12,R15                   SET BASE REGISTER
         USING HTTPAPP2,R12              DEFINE PROGRAM BASE
         B     INIT0000
MODID    DC    CL8'HTTPAPP2'
         DC    CL8'&SYSDATE'
         DC    CL6'&SYSTIME'
INIT0000 DS    0H
         GETMAIN RU,                     (DCB MUST BE BELOW 16M LINE)  +
               LV=WORKL,                                               +
               LOC=BELOW
         ST    R1,8(R13)
         ST    R13,4(R1)
         LR    R13,R1
         USING WORK,R13
         LOAD  EP=HTTPSUB
         ST    R0,HTTP_HTTPSUB
         XC    WORK_TRACE(TRACEL),WORK_TRACE
         XC    WORK_TRACE_IOAREA,WORK_TRACE_IOAREA
         MVC   WORK_SYSIN(SYSINL),SYSINI
         MVC   WORK_SYSPRINT(SYSPRINTL),SYSPRINTI
         MVC   WORK_OPEN_CLOSE(OPENL),OPENI
         OPEN  (WORK_SYSIN,INPUT),                                     +
               MODE=31,                                                +
               MF=(E,WORK_OPEN_CLOSE)
         OPEN  (WORK_SYSPRINT,OUTPUT),                                 +
               MODE=31,                                                +
               MF=(E,WORK_OPEN_CLOSE)
         ZAP   WORK_LINE_COUNT,P0
         ZAP   WORK_PAGE_COUNT,P0
         MVI   WORK_PRINT_AREA-1,C' '
         MVC   WORK_PRINT_AREA,WORK_PRINT_AREA-1
         BAL   R11,HEAD0000
* ------------------------------------------------------------------- *
*                                                                     *
*     Determine if TRACE DD is present                                *
*                                                                     *
* ------------------------------------------------------------------- *
         USING PSA,R0                    DEFINE BASE
         L     R1,PSATNEW                OUR TCB ADDRESS FROM PSA
         USING TCB,R1                    DEFINE TCB BASE
         L     R2,TCBTIO                 TIOT ADDRESS FROM OUR TCB
         USING TIOT1,R2                  DEFINE TIOT BASE
         LA    R3,TIOENTRY               FIRST TIOT ENTRY
         USING TIOENTRY,R3               DEFINE TIOT ENTRY BASE
INIT0010 DS    0H
         SR    R4,R4                     CLEAR FOR LENGTHS
         ICM   R4,1,TIOELNGH             LENGTH OF THIS ENTRY
         BZ    INIT0030                  END OF TABLE
         CLC   TRACEDD,TIOEDDNM          TRACE DD?
         BE    INIT0020                  YES
         AR    R3,R4                     NEXT ENTRY
         B     INIT0010                  LOOP
* ------------------------------------------------------------------- *
*     Trace DD is present.  Open TRACE file.                          *
* ------------------------------------------------------------------- *
INIT0020 DS    0H
         MVC   WORK_TRACE(TRACEL),TRACEI
         OPEN  (WORK_TRACE,OUTPUT),                                    +
               MODE=31,                                                +
               MF=(E,WORK_OPEN_CLOSE)
         LA    R1,WORK_TRACE
         ST    R1,HTTP_TRACE_DCB
         GETMAIN RU,                                                   +
               LV=32768,                                               +
               LOC=BELOW
         ST    R1,WORK_TRACE_IOAREA
         ST    R1,HTTP_TRACE_IOAREA
         BAL   R11,PRT0000
         B     INIT0040
* ------------------------------------------------------------------- *
*     Trace DD is NOT present.                                        *
* ------------------------------------------------------------------- *
INIT0030 DS    0H
         MVC   WORK_PRINT_AREA(MSG02L),MSG02
         BAL   R11,PRT0000
* ------------------------------------------------------------------- *
*                                                                     *
*     Read the control statements                                     *
*                                                                     *
* ------------------------------------------------------------------- *
INIT0040 DS    0H
         GET   WORK_SYSIN,WORK_CONTROL
         MVC   WORK_PRINT_AREA(WORK_CONTROLL),WORK_CONTROL
         BAL   R11,PRT0000
         CLI   WORK_CONTROL,C'*'
         BE    INIT0040
         CLC   HTTPMAIN_PORT,WORK_CONTROL
         BE    INIT0050
         MVC   WORK_PRINT_AREA(MSG03L),MSG03
         BAL   R11,PRT0000
         OI    WORK_FLAGS,$WORK_ERROR
         B     INIT0040
INIT0050 DS    0H
         PACK  WORK_DWORD+5(3),WORK_CONTROL+L'HTTPMAIN_PORT(5)
         CVB   R1,WORK_DWORD
         STH   R1,WORK_HTTPMAIN_PORT
         B     INIT0040
* ------------------------------------------------------------------- *
*                                                                     *
*     Initialize the interface to the HTTP main server                *
*                                                                     *
* ------------------------------------------------------------------- *
INIT0070 DS    0H
         TM    WORK_FLAGS,$WORK_ERROR
         BO    EXIT0010
         MVI   HTTP_FUNCTION,$HTTP_INIT
         LA    R1,HTTP_PARMS
         L     R15,HTTP_HTTPSUB
         BALR  R14,R15
         CLI   HTTP_RETURN_CODE,$HTTP_OK
         BNE   ERR0010
* ------------------------------------------------------------------- *
*                                                                     *
*     Connect to the HTTP main server                                 *
*                                                                     *
* ------------------------------------------------------------------- *
         MVC   HTTP_PORT,WORK_HTTPMAIN_PORT
         MVI   HTTP_FUNCTION,$HTTP_CONNECT
         LA    R1,HTTP_PARMS
         L     R15,HTTP_HTTPSUB
         BALR  R14,R15
         CLI   HTTP_RETURN_CODE,$HTTP_OK
         BNE   ERR0010
* ------------------------------------------------------------------- *
*                                                                     *
*     Send URLs to the HTTP main server                               *
*                                                                     *
*     This lets the HTTP main server "know" which URLs we want        *
*     to process.  You can send multiple URLs (one at a time).        *
*                                                                     *
* ------------------------------------------------------------------- *
DEFURL00 DS    0H
         MVI   HTTP_FUNCTION,$HTTP_DEFINE_SPEC
         LA    R2,URL_TABLE
         USING URL_DSECT,R2
DEFURL01 DS    0H
         SR    R3,R3
         ICM   R3,3,URL_LENGTH
         ST    R3,HTTP_LENGTH
         LA    R1,URL_TEXT
         ST    R1,HTTP_DATA
         LA    R1,HTTP_PARMS
         L     R15,HTTP_HTTPSUB
         BALR  R14,R15
         CLI   HTTP_RETURN_CODE,$HTTP_OK
         BNE   ERR0010
         LA    R2,URL_FIXEDL(R2,R3)
         CLI   0(R2),X'FF'
         BNE   DEFURL01

* ------------------------------------------------------------------- *
*                                                                     *
*     Wait for the main address space to get a reference to           *
*     one of the URLs or command from the console.                    *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0000 DS    0H
         MVI   HTTP_FUNCTION,$HTTP_GET_URL
         LA    R1,HTTP_PARMS
         L     R15,HTTP_HTTPSUB
         BALR  R14,R15
         CLI   HTTP_RETURN_CODE,$HTTP_CONSOLE
         BE    CONS0000
         CLI   HTTP_RETURN_CODE,$HTTP_MAIN_SHUTDOWN
         BE    SHUT0000
         CLI   HTTP_RETURN_CODE,$HTTP_OK
         BNE   ERR0010
* ------------------------------------------------------------------- *
*                                                                     *
*     We have a URL                                                   *
*                                                                     *
*     WORK_HTTP_URL_LENGTH will have been set to the URL's length     *
*     HTTP_DATA will be set to the URL text's address                 *
*                                                                     *
*     Process the URL                                                 *
*                                                                     *
* ------------------------------------------------------------------- *
         L     R1,HTTP_DATA
         LA    R2,URL_TABLE
MAIN0010 DS    0H
         SR    R3,R3
         ICM   R3,3,URL_LENGTH
         BCTR  R3,0
         EX    R3,URL_CLC
         BE    MAIN0020
         LA    R2,URL_FIXEDL+1(R2,R3)
         CLI   0(R2),X'FF'
         BE    ERR0010
         B     MAIN0010
MAIN0020 DS    0H
         LA    R1,HTTP_PARMS
         ICM   R15,15,URL_ADDR
         BALR  R14,R15
* ------------------------------------------------------------------- *
*                                                                     *
*       The URL has been processed.                                   *
*       CLOSE this SOCKET                                             *
*                                                                     *
* ------------------------------------------------------------------- *
         MVI   HTTP_FUNCTION,$HTTP_CLOSE
         LA    R1,HTTP_PARMS
         L     R15,HTTP_HTTPSUB
         BALR  R14,R15
         CLI   HTTP_RETURN_CODE,$HTTP_OK
         BNE   ERR0010
         B     MAIN0000
* ------------------------------------------------------------------- *
*                                                                     *
*   A command has arrived from the console                            *
*                                                                     *
* ------------------------------------------------------------------- *
CONS0000 DS    0H
         CLC   =C'STOP ',HTTP_MESSAGE_1
         BE    EXIT0000
         B     MAIN0000
* ------------------------------------------------------------------- *
*                                                                     *
*   The HTTPMAIN has shutdown                                         *
*                                                                     *
* ------------------------------------------------------------------- *
SHUT0000 DS    0H
         GETMAIN RU,                                                   +
               LV=WTO01L,                                              +
               LOC=BELOW
         LR    R2,R1
         MVC   0(WTO01L,R2),WTO01I
         WTO   MF=(E,(R2))
         FREEMAIN RU,                                                  +
               A=(R2),                                                 +
               LV=WTO01L
         B     EXIT0000
* ------------------------------------------------------------------- *
*       PRINT                                                         *
* ------------------------------------------------------------------- *
PRT0000  DS    0H
         PUT   WORK_SYSPRINT,WORK_PRINT_AREA
         MVC   WORK_PRINT_AREA,WORK_PRINT_AREA-1
         AP    WORK_LINE_COUNT,P1
         CP    WORK_LINE_COUNT,P60
         BLR   R11
HEAD0000 DS    0H
         MVC   WORK_PRINT_AREA(HEAD01L),HEAD01
         AP    WORK_PAGE_COUNT,P1
         ED    WORK_PRINT_AREA+(HEAD_PAGE-HEAD01)(6),WORK_PAGE_COUNT
         PUT   WORK_SYSPRINT,WORK_PRINT_AREA
         MVC   WORK_PRINT_AREA,WORK_PRINT_AREA-1
         ZAP   WORK_LINE_COUNT,P0
         BR    R11
* ------------------------------------------------------------------- *
*       Error from the INIT call                                      *
* ------------------------------------------------------------------- *
ERR0010  DS    0H
         MVC   WORK_PRINT_AREA(L'HTTP_MESSAGE_1),HTTP_MESSAGE_1
         BAL   R11,PRT0000
         MVC   WORK_PRINT_AREA(L'HTTP_MESSAGE_2),HTTP_MESSAGE_2
         BAL   R11,PRT0000
         MVC   WORK_PRINT_AREA(L'HTTP_MESSAGE_3),HTTP_MESSAGE_3
         BAL   R11,PRT0000
         MVC   WORK_PRINT_AREA(L'HTTP_MESSAGE_4),HTTP_MESSAGE_4
         BAL   R11,PRT0000
         MVC   WORK_PRINT_AREA(L'HTTP_MESSAGE_5),HTTP_MESSAGE_5
         BAL   R11,PRT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         MVI   HTTP_FUNCTION,$HTTP_CLEAN_UP
         LA    R1,HTTP_PARMS
         L     R15,HTTP_HTTPSUB
         BALR  R14,R15
         CLI   HTTP_RETURN_CODE,$HTTP_OK
         BNE   ERR0010
         ICM   R1,15,WORK_TRACE_IOAREA
         BZ    EXIT0010
         FREEMAIN RU,                                                  +
               A=(1),                                                  +
               LV=32768
         MVC   WORK_OPEN_CLOSE(CLOSEL),CLOSEI
         CLOSE WORK_TRACE,                                             +
               MODE=31,                                                +
               MF=(E,WORK_OPEN_CLOSE)
EXIT0010 DS    0H
         MVC   WORK_OPEN_CLOSE(CLOSEL),CLOSEI
         CLOSE WORK_SYSIN,                                             +
               MODE=31,                                                +
               MF=(E,WORK_OPEN_CLOSE)
         CLOSE WORK_SYSPRINT,                                          +
               MODE=31,                                                +
               MF=(E,WORK_OPEN_CLOSE)
         DELETE EP=HTTPSUB
         LR    R1,R13                    COPY WORK AREA ADDRESS
         L     R13,4(R13)                RESTORE CALLER'S R13
         FREEMAIN RU,A=(1),LV=WORKL      FREE THE WORK AREA
         L     R14,12(R13)               RESTORE R14
         SR    R15,R15
         LM    R0,R12,20(R13)
         BR    R14
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
URL_CLC  CLC   URL_TEXT(0),0(R1)
* ------------------------------------------------------------------- *
TRACEI   DCB   DSORG=PS,                                               +
               DDNAME=TRACE,                                           +
               RECFM=VB,                                               +
               LRECL=32756,                                            +
               BLKSIZE=32760,                                          +
               MACRF=PM
TRACEL   EQU   *-TRACEI
SYSINI   DCB   DSORG=PS,                                               +
               DDNAME=SYSIN,                                           +
               RECFM=FB,                                               +
               LRECL=80,                                               +
               EODAD=INIT0070,                                         +
               MACRF=GM
SYSINL   EQU   *-SYSINI
SYSPRINTI DCB  DSORG=PS,                                               +
               DDNAME=SYSPRINT,                                        +
               RECFM=FBA,                                              +
               LRECL=133,                                              +
               MACRF=PM
SYSPRINTL EQU  *-SYSPRINTI
OPENI    OPEN  (*,OUTPUT),MODE=31,MF=L
OPENL    EQU   *-OPENI
CLOSEI   CLOSE *,MODE=31,MF=L
CLOSEL   EQU   *-CLOSEI

P0                  DC  P'0'
P1                  DC  P'1'
P60                 DC  P'60'

TRACEDD             DC  CL8'TRACE'
HTTPMAIN_PORT       DC  C'PORT='

WTO01I              WTO 'Shutdown received from HTTPMAIN',MF=L
WTO01L              EQU *-WTO01I


HEAD01              DC  C'1'
                    DC  C'HTTPAPP2'
                    DC  CL70' '
                    DC  C'PAGE '
HEAD_PAGE           DC  X'402020202120'
HEAD01L             EQU *-HEAD01

MSG01               DC  C'TRACE activated.'
MSG01L              EQU *-MSG01

MSG02               DC  C'TRACE DD not present.  Trace not active.'
MSG02L              EQU *-MSG02

MSG03               DC  C'Invalid control statement'
MSG03L              EQU *-MSG03
* ------------------------------------------------------------------- *
*
*      Sample URLs
*
* ------------------------------------------------------------------- *
URL_TABLE           DS    0C
                    DEFURL AAA,APP2ACT
                    DEFURL ACT,APP2ACT
                    DEFURL APF,APP2APF
                    DEFURL IPL,APP2IPL
                    DEFURL LLA,APP2LLA
                    DC    X'FF'

                    LTORG
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
URL_DSECT           DSECT
URL_LENGTH          DS    AL2
URL_ADDR            DS    AL4
URL_FIXEDL          EQU   *-URL_DSECT
URL_TEXT            DS    0C

* ------------------------------------------------------------------- *
*        WORK AREAS                                                   *
* ------------------------------------------------------------------- *
WORK                DSECT
                    DS    18F
WORK_DWORD          DS    D
WORK_TRACE          DS    0F,(TRACEL)X
WORK_SYSIN          DS    0F,(SYSINL)X
WORK_SYSPRINT       DS    0F,(SYSPRINTL)X
WORK_OPEN_CLOSE     DS    0F,(OPENL)X
WORK_TRACE_IOAREA   DS    A

WORK_HTTPMAIN_PORT  DS    H

WORK_FLAGS          DS    X
$WORK_ERROR         EQU   X'80'

WORK_CONTROL        DS    CL80
WORK_CONTROLL       EQU   *-WORK_CONTROL

                    DS    X
WORK_PRINT_AREA     DS    CL133

WORK_LINE_COUNT     DS    PL3
WORK_PAGE_COUNT     DS    PL3

                    SUBPARM  TYPE=CSECT
WORKL               EQU   *-WORK
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         IHAPSA     DSECT=YES,LIST=NO
         IKJTCB     LIST=NO
         IEFTIOT1
         CVT        DSECT=YES
         COPY       REGEQU
         END        HTTPAPP2
