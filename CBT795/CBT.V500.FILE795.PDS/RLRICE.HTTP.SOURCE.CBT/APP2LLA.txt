* ------------------------------------------------------------------- *
*                                                                     *
*   This application returns the datasets in the systems link list    *
*                                                                     *
* ------------------------------------------------------------------- *
APP2LLA  CSECT
APP2LLA  AMODE 31
APP2LLA  RMODE ANY
         STM   R14,R12,12(R13)       SAVE REGISTERS
         LR    R12,R15               SET BASE REGISTER
         USING APP2LLA,R12           DEFINE PROGRAM BASE
         B     INIT0000
MODID    DC    CL8'APP2LLA'
         DC    CL8'&SYSDATE'
         DC    CL6'&SYSTIME'
INIT0000 DS    0H
         LR    R11,R1                COPY PARM BLOCK ADDRESS
         USING HTTP_PARMS,R11
         GETMAIN RU,                                                   +
               LV=WORKL,                                               +
               LOC=ANY
         ST    R1,8(R13)
         ST    R13,4(R1)
         LR    R13,R1
         USING WORK,R13
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         XC    HTTP_ALET,HTTP_ALET
         MVI   HTTP_FUNCTION,$HTTP_WRITE

         MVC   WORK_BUFFER(HTML_HEADERL),HTML_HEADER
         LA    R1,WORK_BUFFER
         ST    R1,HTTP_DATA
         LA    R1,HTML_HEADERL
         ST    R1,HTTP_LENGTH
         LA    R1,HTTP_PARMS
         L     R15,HTTP_HTTPSUB
         BALR  R14,R15

         L     R1,CVTPTR
         USING CVT,R1
         L     R2,CVTLLTA               LLA TABLE ADDRESS
         USING LLAT,R2                  DEFINE BASE
         CLC   LLAT_EYE,C_LLT           EYECATCHER PRESENT?
         BNE   ERR0010
         L     R3,LLAT_NBR              NUMBER OF DATASETS IN LIST
         LA    R4,LLAT_ENTRY
         USING LLAT_ENTRY,R4

         MVC   WORK_BUFFER(SYSTEM_HEADERL),SYSTEM_HEADER
         MVC   WORK_BUFFER+(SYSTEM_NAME-SYSTEM_HEADER)(8),CVTSNAME
         LA    R1,WORK_BUFFER
         ST    R1,HTTP_DATA
         LA    R1,SYSTEM_HEADERL
         ST    R1,HTTP_LENGTH
         LA    R1,HTTP_PARMS
         L     R15,HTTP_HTTPSUB
         BALR  R14,R15

         LA    R1,WORK_BUFFER
         ST    R1,HTTP_DATA
         MVI   HTTP_FUNCTION,$HTTP_WRITE
LLA0010  DS    0H
         MVC   WORK_BUFFER,LLAT_ENTRY_DSN
         SR    R1,R1
         IC    R1,LLAT_ENTRY_LENGTH
         ST    R1,HTTP_LENGTH
         LA    R1,HTTP_PARMS
         L     R15,HTTP_HTTPSUB
         BALR  R14,R15

         MVI   WORK_BUFFER,$LF
         LA    R1,1
         ST    R1,HTTP_LENGTH
         LA    R1,HTTP_PARMS
         L     R15,HTTP_HTTPSUB
         BALR  R14,R15

         LA    R4,LLAT_ENTRYL(R4)
         BCT   R3,LLA0010

         MVC   WORK_BUFFER(HTML_TRAILERL),HTML_TRAILER
         LA    R1,WORK_BUFFER
         ST    R1,HTTP_DATA
         LA    R1,HTML_TRAILERL
         ST    R1,HTTP_LENGTH
         LA    R1,HTTP_PARMS
         L     R15,HTTP_HTTPSUB
         BALR  R14,R15
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0010  DS    0H
         MVI   HTTP_FUNCTION,$HTTP_WRITE
         MVC   WORK_BUFFER(MSG01L),MSG01
         LA    R1,WORK_BUFFER
         ST    R1,HTTP_DATA
         LA    R1,MSG01L
         ST    R1,HTTP_LENGTH
         LA    R1,HTTP_PARMS
         L     R15,HTTP_HTTPSUB
         BALR  R14,R15
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         LR    R1,R13                  COPY WORK AREA ADDRESS
         L     R13,4(R13)              RESTORE CALLER'S R13
         FREEMAIN RU,A=(1),LV=WORKL    FREE THE WORK AREA
         L     R14,12(R13)             RESTORE R14
         SR    R15,R15
         LM    R0,R12,20(R13)
         BR    R14
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
C_LLT               DC  CL4'LLT '

MSG01               DS  0C
                    DC  C'Internal error.  LLA table ID not found.'
MSG01L              EQU *-MSG01

HTML_HEADER         DS  0C
                    DC  C'<HTML>'
                    DC  C'<BODY>'
                    DC  C'<CENTER>'
                    DC  AL1($LF)
HTML_HEADERL        EQU *-HTML_HEADER
SYSTEM_HEADER       DS  0C
                    DC  C'<H1>Link List for '
SYSTEM_NAME         DC  CL8' '
                    DC  C'</H1>'
                    DC  C'<PRE>'
SYSTEM_HEADERL      EQU *-SYSTEM_HEADER
HTML_TRAILER        DS  0C
                    DC  C'</PRE>'
                    DC  C'</CENTER>'
                    DC  C'</BODY>'
                    DC  C'</HTML>'
HTML_TRAILERL       EQU *-HTML_TRAILER
* ------------------------------------------------------------------- *
*        WORK AREAS                                                   *
* ------------------------------------------------------------------- *
WORK                DSECT
                    DS    18F
WORK_BUFFER         DS    CL256
WORKL               EQU   *-WORK
                    SUBPARM  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
LLAT               DSECT
LLAT_EYE           DS    CL4
LLAT_NBR           DS    F
LLAT_ENTRY         DS    0X
LLAT_ENTRY_LENGTH  DS    X
LLAT_ENTRY_DSN     DS    CL44
LLAT_ENTRYL        EQU   *-LLAT_ENTRY
* ------------------------------------------------------------------- *
         IHAPSA    DSECT=YES,LIST=NO
         CVT       DSECT=YES,LIST=YES
         COPY REGEQU
$LF      EQU  X'0A'
         END  APP2LLA
