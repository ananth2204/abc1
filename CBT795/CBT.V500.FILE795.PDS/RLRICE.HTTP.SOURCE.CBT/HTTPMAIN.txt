* ------------------------------------------------------------------- *
*                                                                     *
*  Name: HTTPMAIN                                                     *
*                                                                     *
*  PURPOSE: HTTP MAINLINE                                             *
*                                                                     *
*                                                                     *
*                                                                     *
*   1) Obtain storage for work area(s)                                *
*   2) LOAD the common area (HTTPCOMM)                                *
*   3) Initialize fields in the common area                           *
*   4) LOAD and link to HTTPINIT                                      *
*   5) If errors were detected by HTTPINIT, clean up and exit         *
*   6) WAIT for an event                                              *
*        . connect or loss of a server space                          *
*        . STOP command from the console                              *
*                                                                     *
*   Connect of a server..                                             *
*     . HTTPTCP will have built a SERVBLOK                            *
*       and added it to the chain anchored at COMM_NEW_SERVERS        *
*     . ATTACH an HTTPSERV subtask                                    *
*                                                                     *
*   Loss of a server                                                  *
*     . Remove any URLs in the URL dataspace for this server          *
*     . Move any WEBURL blocks to HTTPLOCL's queue                    *
*     . Remove the SERVBLOK from the chain anchored at COMM_SERVERS.  *
*     . DETACH the HTTPSERV subtask                                   *
*                                                                     *
*                                                                     *
*   STOP from the console                                             *
*     . LOAD and link to HTTPTERM                                     *
*     . FREEMAIN the trace table                                      *
*     . DELETE the common data area (HTTPCOMM)                        *
*     . FREEMAIN the work area                                        *
*     . exit                                                          *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
HTTPMAIN CSECT
HTTPMAIN AMODE 31
HTTPMAIN RMODE ANY
         USING HTTPMAIN,R12                   DEFINE BASE
         STM   R14,R12,12(R13)                SAVE ENTRY REGISTERS
         LR    R12,R15                        COPY BASE
         B     INIT0000                       BRANCH AROUND EYECATCHER
MODID    DC    CL8'HTTPMAIN'                  MODULE NAME
         DC    CL8'&SYSDATE'                  DATE OF ASSEMBLY
         DC    CL6'&SYSTIME'                  TIME OF ASSEMBLY
INIT0000 DS    0H
         L     R6,0(R1)                       ADDRESS OF PARM
* ------------------------------------------------------------------- *
*        ACQUIRE STORAGE FOR WORK AREAS                               *
* ------------------------------------------------------------------- *
         L     R3,DXD_SIZE                    WORK AREA SIZE
         GETMAIN RU,                          ACQUIRE WORK AREA        +
               LV=(R3),                       .. WORK AREA SIZE        +
               LOC=ANY                        .. MAY BE ABOVE OR BELOW
         LR    R4,R1                          COPY STORAGE ADDRESS
         LR    R10,R1                         COPY STORAGE ADDRESS
         LR    R2,R1                          COPY WORK AREA ADDRESS
         SR    R1,R1                          CLEAR REGISTER
         MVCL  R2,R0                          CLEAR WORK AREA
         A     R10,DXD_ADDRESS                PLUS DISPLACEMENT
         USING DXDMAIN,R10                    DEFINE WORK AREA BASE
         MVC   DXD_ID,MODID                   IDENTIFY WORK AREA OWNER
         LA    R2,REGISTER_SAVE_AREA          SAVE AREA ADDRESS
         ST    R13,4(R2)                      CHAIN CALLER'S SAVE AREA
         ST    R2,8(R13)                      OUR SAVE AREA TO CALLER'S
         LR    R13,R2                         SET SAVE AREA ADDRESS
         MVC   PRT_EYE,PRT_ID                 IDENTIFY CONTROL BLOCK
* ------------------------------------------------------------------- *
*        Load common data         (HTTPCOMM)                          *
* ------------------------------------------------------------------- *
         LOAD  EP=HTTPCOMM                    LOAD COMMON DATA MODULE
         LR    R11,R0                         COPY TO PERMANENT BASE
         USING HTTPCOMM,R11                   DEFINE COMMON AREA BASE
         ST    R12,COMM_HTTPMAIN_ADDRESS
         ST    R4,COMM_DXD_ADDRESS            SET WORK AREA ADDRESS
         MVC   COMM_HTTPDAIR_ADDRESS,V_HTTPDAIR
         MVI   PRT_COMMAND,$PRT_PRINT
         MVC   PRT_CC(PRT_LRECL),COMM_BLANKS
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         LA    R0,COMM_TERMINATION_ECB
         ST    R0,DXD_ECB_1
         LA    R0,COMM_MAIN_WORK_ECB
         ST    R0,DXD_ECB_2
         OI    DXD_ECB_2,X'80'
         XC    COMM_MAIN_WORK_ECB,COMM_MAIN_WORK_ECB
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         LOAD  EP=HTTPINIT
         LR    R15,R0
         BALR  R14,R15
         DELETE EP=HTTPINIT
         TM    COMM_FLAGS,$COMM_ABORT
         BO    TERM0000
* ------------------------------------------------------------------- *
*   At the present time there is no work to do.                       *
*                                                                     *
*   The number of ECBs we need to WAIT on varies.                     *
*   The main task waits for:                                          *
*                                                                     *
*    Since the number of users can vary, the number of ECBs can also  *
*    vary.  There are always the ECBs for the ATTENTION exit and the  *
*    termination ECB (POSTed by HTTPCONS).                            *
* ------------------------------------------------------------------- *
PROC0000 DS    0H
         ITRACE ID=WAIT
         WAIT  1,ECBLIST=DXD_WAIT
         TM    COMM_TERMINATION_ECB,X'40'
         BO    TERM0000
         XC    COMM_MAIN_WORK_ECB,COMM_MAIN_WORK_ECB
* ------------------------------------------------------------------- *
*        Attach HTTPSERV subtask for any new servers                  *
* ------------------------------------------------------------------- *
         ITRACE ID=NEW,                                                +
               DATA1=(COMM_NEW_SERVERS,8)
         ICM   R7,15,COMM_NEW_SERVERS
         BZ    PROC0040
PROC0010 DS    0H
         SR    R1,R1
         L     R7,COMM_NEW_SERVERS
         CS    R7,R1,COMM_NEW_SERVERS
         BC    4,PROC0010
         USING SERVBLOK,R7
         LTR   R7,R7                          SERVBLOK ADDRESS PRESENT?
         BZ    PROC0040                       NO
PROC0020 DS    0H
         MVC   DXD_ATTACH(ATTACHL),ATTACHI
         LA    R2,SERVER_TERM_ECB             TERMINATION ECB'S ADDRESS
         LA    R3,DXD_ATTACH_PARM             PARM LIST ADDRESS
         ITRACE ID=ATCHSERV                   ATTACHING HTTPSERV
         ATTACH EP=HTTPSERV,                  ATTACH SERVER SUBTASK    +
               PARAM=((R7)),                  .. PARAMETER IS SERVBLOK +
               ECB=(R2),                      .. TERMINATION ECB       +
               PURGE=QUIESCE,                 .. FINISH UP I/O         +
               MF=(E,(R3)),                   .. PARM LIST             +
               SF=(E,DXD_ATTACH)              ..
         LTR   R15,R15                        ATTACH SUCCESSFUL?
         BNZ   ERR0010                        NO
         ST    R1,SERVER_TCB                  SET TCB'S ADDRESS
         ITRACE ID=NEWSERV,                   NEW SUBTASK IS ACTIVE    +
               RDATA1=R1                      .. TCB ADDRESS
         MVC   PRT_MESSAGE_ID(MSG08L),MSG08
         MVC   PRT_MESSAGE_ID+(MSG08TSK-MSG08)(L'MSG08TSK),SERVER_SUBTA+
               SK
         BAL   R8,PRT0000                     PRINT THE MESSAGE
         LR    R2,R7                          COPY SERVBLOK ADDRESS
         L     R7,SERVER_NEXT
PROC0030 DS    0H
         L     R1,COMM_SERVERS
         ST    R1,SERVER_NEXT-SERVBLOK(,R2)
         CS    R1,R2,COMM_SERVERS
         BC    4,PROC0030
         ITRACE ID=NEXT_NEW,                                           +
               RDATA1=R7
         LTR   R7,R7
         BNZ   PROC0020                       PROCESS NEXT SERVER
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
PROC0040 DS    0H
         ICM   R7,15,COMM_LOST_SERVERS        ANY LOST SERVERS?
         BZ    PROC0000                       NO
PROC0050 DS    0H
         SR    R1,R1
         L     R7,COMM_LOST_SERVERS
         CS    R7,R1,COMM_LOST_SERVERS
         BC    4,PROC0050
PROC0060 DS    0H
         ITRACE ID=LOST,                                               +
               RDATA1=R7
         WAIT  ECB=SERVER_TERM_ECB            WAIT FOR TERMINATION
         MVC   PRT_MESSAGE_ID(MSG09L),MSG09
         MVC   PRT_MESSAGE_ID+(MSG09TCB-MSG09)(L'MSG09TCB),SERVER_SUBTA+
               SK
         MVC   PRT_MESSAGE_ID+(MSG09NAME-MSG09)(L'MSG09NAME),PARTNER_CL+
               IENT_NAME
         MVC   PRT_MESSAGE_ID+(MSG09TASK-MSG09)(L'MSG09TASK),PARTNER_CL+
               IENT_TASK
         BAL   R8,PRT0000                     PRINT THE MESSAGE
* ------------------------------------------------------------------- *
*   DETACH the subtask                                                *
* ------------------------------------------------------------------- *
         WAIT   ECB=SERVER_TERM_ECB           WAIT FOR SUBTASK
         ITRACE ID=DETACH,                                             +
               RDATA1=R7,                                              +
               DATA2=(SERVER_TCB,8)
         DETACH SERVER_TCB                    DETACH SUBTASK
* ------------------------------------------------------------------- *
*   Free URLs queued to server                                        *
* ------------------------------------------------------------------- *
         ICM   R2,15,SERVER_URL               1ST URL
         BZ    PROC0080                       NONE
PROC0070 DS    0H
         L     R3,WEBURL_NEXT-WEBURL(R2)
         L     R0,WEBURL_LENGTH-WEBURL(R2)    LENGTH OF THIS URL
         ITRACE ID=FREE_URL,                                           +
               RDATA1=R2,                                              +
               RDATA2=R0
         FREEMAIN RU,                                                  +
               A=(R2),                                                 +
               LV=(0)
         LTR   R2,R3                          COPY NEXT URL ADDRESS
         BNZ   PROC0070                       LOOP
* ------------------------------------------------------------------- *
*        FREEMAIN the SERVBLOK                                        *
* ------------------------------------------------------------------- *
PROC0080 DS    0H
         LR    R1,R7
         L     R7,SERVER_NEXT
         ITRACE ID=FREESERV,                                           +
               RDATA1=R1
         FREEMAIN RU,                                                  +
               A=(1),                                                  +
               LV=SERVBLOKL
         LTR   R7,R7                          NEXT BLOCK EXIST?
         BNZ   PROC0060                       YES
         B     PROC0000                       OTHERWISE WAIT FOR WORK
* ------------------------------------------------------------------- *
*                                                                     *
*  Error(s) detected in the start-up parameters or the                *
*  termination ECB has been POSTed.                                   *
*                                                                     *
* ------------------------------------------------------------------- *
TERM0000 DS    0H
         ITRACE ID=EOJ
         LOAD  EP=HTTPTERM
         LR    R15,R0
         BALR  R14,R15
         DELETE EP=HTTPTERM
         B     EXIT0000
* ------------------------------------------------------------------- *
*        ERROR ROUTINES                                               *
* ------------------------------------------------------------------- *
ERR0010  DS    0H
         ITRACE ID=ATCHFAIL                   SUBTASK ATTACH FAILED
         STH   R15,DXD_RC                     SAVE RETURN CODE
         MVC   PRT_MESSAGE_ID(MSG15L),MSG15
         MVC   PRT_MESSAGE_ID+(MSG15T-MSG15)(L'MSG15),DXD_SUBTASK
         UNPK  PRT_MESSAGE_ID+(MSG15R-MSG15)(5),DXD_RC(3)
         TR    PRT_MESSAGE_ID+(MSG15R-MSG15)(L'MSG15R),HEXCHAR
         MVI   PRT_MESSAGE_ID+(MSG15R-MSG15)+4,C' '
         BAL   R8,PRT0000                     PRINT MESSAGE
         ABEND 1,DUMP,,USER
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
PRT0000  DS    0H
         ITRACE ID=PRINT
         GETMAIN RU,                                                   +
               LV=PRTBLOKL,                                            +
               LOC=ANY
         MVC   0(PRTBLOKL,R1),PRTBLOK
         MVC   PRT_CC(PRT_LRECL),COMM_BLANKS
PRT0010  DS    0H
         L     R14,COMM_PRINT_QUEUE           FIRST BLOCK ON QUEUE
         ST    R14,PRT_NEXT-PRTBLOK(R1)
         CS    R14,R1,COMM_PRINT_QUEUE        ADD NEW BLOCK TO CHAIN
         BC    4,PRT0010                      TRY AGAIN
         POST  COMM_PRINT_WORK_ECB            WAKE UP THE PRINT SUBTASK
         BR    R8                             COMPLETE
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ICM   R1,15,TRACE_TABLE_ADDRESS      TRACE TABLE ADDRESS
         BZ    EXIT0010
         L     R0,TRACE_TABLE_SIZE            TRACE TABLE SIZE
         FREEMAIN RU,                         FREE TRACE TABLE STORAGE +
               A=(1),                                                  +
               LV=(0)
EXIT0010 DS    0H
         L     R2,COMM_DXD_ADDRESS            WORK AREA ADDRESS
         DELETE EP=HTTPCOMM                   DELETE COMMON MODULE
         L     R13,4(R13)                     RESTORE CALLER'S R13
         LTR   R2,R2                          WORK AREA GETMAIN'D?
         BZ    EXIT0020                       NO
         L     R0,DXD_SIZE                    WORK AREA SIZE
         FREEMAIN RU,                         FREE WORK AREA           +
               A=(R2),                                                 +
               LV=(0)
EXIT0020 DS    0H
         LM    R14,R12,12(R13)                RESTORE REGISTERS
         SR    R15,R15                        CLEAR REGISTER
         BR    R14                            EXIT
* ------------------------------------------------------------------- *
*                                                                     *
*        CONSTANTS                                                    *
*                                                                     *
* ------------------------------------------------------------------- *
DXD_ADDRESS DC    Q(DXDMAIN)           DISPLACEMENT TO WORK AREA
DXD_SIZE    CXD                        WORK AREA TOTAL SIZE
V_HTTPDAIR  DC    V(HTTPDAIR)
ATTACHI  ATTACH EP=HTTPSERV,        ATTACH LIST FORM                   +
               ECB=*,               .. TERMINATION ECB'S ADDRESS       +
               PURGE=QUIESCE,       .. FINISH UP I/O IN PROGRESS       +
               SF=L                 .. LIST FORM
ATTACHL     EQU   *-ATTACHI

HEXCHAR     EQU   *-C'0'
            DC    C'0123456789ABCDEF'

PRT_ID      DC    CL8'PRTBLOK'

MSG07    DS    0C
         DC    CL15'HTTPMAIN07I'
         DC    C'Error(s) have been detected. Aborting.'
MSG07L   EQU   *-MSG07

MSG08    DS    0C
         DC    CL15'HTTPMAIN08I'
         DC    C'HTTPSERV subtask '
MSG08TSK DC    CL8' '
         DC    C' ATTACHed'
MSG08L   EQU   *-MSG08

MSG09     DS    0C
          DC    CL15'HTTPMAIN09I'
          DC    C'HTTPSERV subtask '
MSG09TCB  DC    CL8' '
          DC    C' for '
MSG09NAME DC    CL8' '
          DC    C'/'
MSG09TASK DC    CL8' '
          DC    C' DETACHed'
MSG09L    EQU   *-MSG09

MSG15    DS    0C
         DC    CL15'HTTPMAIN15E'
         DC    C'ATTACH failed for '
MSG15T   DC    CL8' '
         DC    C'  RC='
MSG15R   DC    CL5' '
         DC    C' '
MSG15L   EQU   *-MSG15
MSG16    DS    0C
         DC    CL15'HTTPMAIN16E'
         DC    C'DETACH failed for '
MSG16T   DC    CL8' '
         DC    C'  RC='
MSG16R   DC    CL5' '
         DC    C' '
MSG16L   EQU   *-MSG16
MSG80    DS    0C
         DC    CL15'HTTPMAIN80I'
         DC    C'Termination phase 1 beginning.'
MSG80L   EQU   *-MSG80
MSG81    DS    0C
         DC    CL15'HTTPMAIN81I'
         DC    C'TCP/IP subtask terminated, ECB='
MSG81ECB DC    CL8' '
         DC    C' '
MSG81L   EQU   *-MSG81
MSG82    DS    0C
         DC    CL15'HTTPMAIN82I'
         DC    C'Termination phase 2 beginning.'
MSG82L   EQU   *-MSG82
MSG86    DS    0C
         DC    CL15'HTTPMAIN86I'
         DC    C'Termination phase 3 beginning.'
MSG86L   EQU   *-MSG86
MSG87    DS    0C
         DC    CL15'HTTPMAIN87I'
         DC    C'Local URL subtask terminated, ECB='
MSG87ECB DC    CL8' '
         DC    C' '
MSG87L   EQU   *-MSG87
MSG88    DS    0C
         DC    CL15'HTTPMAIN88I'
         DC    C'Console communcation subtask terminated, ECB='
MSG88ECB DC    CL8' '
         DC    C' '
MSG88L   EQU   *-MSG88
MSG89    DS    0C
         DC    CL15'HTTPMAIN89I'
         DC    C'Logging subtask terminated, ECB='
MSG89ECB DC    CL8' '
         DC    C' '
MSG89L   EQU   *-MSG89
MSG90    DS    0C
         DC    CL15'HTTPMAIN90I'
         DC    C'Local URL references='
MSG90REF DC    x'402020206b2020206b202120'
         DC    C' '
MSG90URL DC    CL80' '
MSG90L   EQU   *-MSG90
MSG99    DS    0C
         DC    CL15'HTTPMAIN99I'
         DC    C'Termination complete.'
MSG99L   EQU   *-MSG99
         LTORG
* ------------------------------------------------------------------- *
*                                                                     *
*        WORK AREA                                                    *
*                                                                     *
* ------------------------------------------------------------------- *
DXDMAIN          DSECT
                 STDSAVE DSECT=NO
DXD_ID           DS       CL8
DXD_DWORD        DS       D
DXD_ATTACH_PARM  DS       A                ATTACH PARM
DXD_URLS         DS       F
DXD_ATTACH       DS       (ATTACHL)X       ATTACH LIST FORM

DXD_WAIT         DS       0A
DXD_ECB_1        DS       A
DXD_ECB_2        DS       A

DXD_RC           DS       H
DXD_DD           DS       X                DD'S PRESENT
DXD_SUBTASK      DS       CL8              SUBTASK ATTACHED/DETACHED
DXD_PARM_WORK    DS       CL5
                 PRTBLOK  DSECT=NO
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COMMON   MAIN,TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         WEBURL
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         SERVBLOK DSECT=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                  IKJTCB   LIST=NO
SERVER_IO_SIZE    EQU      32760
                  COPY     REGEQU
         END   HTTPMAIN
