* ------------------------------------------------------------------- *
*                                                                     *
*   This sample application returns a list of the MVS address spaces  *
*   in HTML TABLE form.                                               *
*                                                                     *
* ------------------------------------------------------------------- *
APP2ACT  CSECT
APP2ACT  AMODE 31
APP2ACT  RMODE ANY
         STM   R14,R12,12(R13)           SAVE REGISTERS
         LR    R12,R15                   SET BASE REGISTER
         USING APP2ACT,R12               DEFINE PROGRAM BASE
         B     INIT0000
MODID    DC    CL8'APP2ACT'
         DC    CL8'&SYSDATE'
         DC    CL6'&SYSTIME'
INIT0000 DS    0H
         LR    R11,R1                    COPY PARM BLOCK ADDRESS
         USING HTTP_PARMS,R11
         GETMAIN RU,                                                   +
               LV=WORKL,                                               +
               LOC=ANY
         ST    R1,8(R13)
         ST    R13,4(R1)
         LR    R13,R1
         USING WORK,R13
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         XC    HTTP_ALET,HTTP_ALET
         MVI   HTTP_FUNCTION,$HTTP_WRITE

         MVC   WORK_BUFFER(HTML_HEADERL),HTML_HEADER
         LA    R1,WORK_BUFFER
         ST    R1,HTTP_DATA
         LA    R1,HTML_HEADERL
         ST    R1,HTTP_LENGTH
         LA    R1,HTTP_PARMS
         L     R15,HTTP_HTTPSUB
         BALR  R14,R15

         L     R1,CVTPTR
         USING CVT,R1
         MVC   WORK_BUFFER(SYSTEM_HEADERL),SYSTEM_HEADER
         MVC   WORK_BUFFER+(SYSTEM_NAME-SYSTEM_HEADER)(8),CVTSNAME

         TIME  DEC
         ST    R0,WORK_TIME
         ST    R1,WORK_DATE

         UNPK  WORK_DWORD(7),WORK_TIME(4)
        MVC   WORK_BUFFER+(SYSTEM_TIME-SYSTEM_HEADER)+0(2),WORK_DWORD+0
         MVI   WORK_BUFFER+(SYSTEM_TIME-SYSTEM_HEADER)+2,C':'
        MVC   WORK_BUFFER+(SYSTEM_TIME-SYSTEM_HEADER)+3(2),WORK_DWORD+2
         MVI   WORK_BUFFER+(SYSTEM_TIME-SYSTEM_HEADER)+5,C':'
        MVC   WORK_BUFFER+(SYSTEM_TIME-SYSTEM_HEADER)+6(2),WORK_DWORD+4
         AP    WORK_DATE,P_1900000
         MVC   WORK_BUFFER+(SYSTEM_DATE-SYSTEM_HEADER)(09),=X'F02020202+
               04B202020'
         ED    WORK_BUFFER+(SYSTEM_DATE-SYSTEM_HEADER)(9),WORK_DATE
         MVI   WORK_BUFFER+(SYSTEM_DATE-SYSTEM_HEADER),C' '
         LA    R1,WORK_BUFFER
         ST    R1,HTTP_DATA
         LA    R1,SYSTEM_HEADERL
         ST    R1,HTTP_LENGTH
         LA    R1,HTTP_PARMS
         L     R15,HTTP_HTTPSUB
         BALR  R14,R15

         MVC   WORK_BUFFER(TABLE_HEADERL),TABLE_HEADER
         LA    R1,WORK_BUFFER
         ST    R1,HTTP_DATA
         LA    R1,TABLE_HEADERL
         ST    R1,HTTP_LENGTH
         LA    R1,HTTP_PARMS
         L     R15,HTTP_HTTPSUB
         BALR  R14,R15

         MVC   DETAIL(DETAILL),DETAIL_INIT
         MVC   DTLJOB,=C'JOB NAME'
         MVC   DTLSTEP,=C'STEP    '
         MVC   DTLPROC,=C'PROC    '
         MVI   DTLTYPE,C'T'
         MVC   DTLSTAT,=C'STA'
         MVC   DTLUSER,=C'USER    '
         MVC   DTLSWAP,=C'SWAP'
         MVC   DTLSRVC,=C'SERVICE '
         MVC   DTLIO,=C'   I/O''S'
         MVC   DTLCPU,=C'   CPU TIME'

         MVC   WORK_BUFFER(ROW_HEADERL),ROW_HEADER
         LA    R1,WORK_BUFFER
         ST    R1,HTTP_DATA
         LA    R1,ROW_HEADERL
         ST    R1,HTTP_LENGTH
         LA    R1,HTTP_PARMS
         L     R15,HTTP_HTTPSUB
         BALR  R14,R15
         LA    R1,DETAIL
         ST    R1,HTTP_DATA
         LA    R1,DETAILL
         ST    R1,HTTP_LENGTH
         LA    R1,HTTP_PARMS
         L     R15,HTTP_HTTPSUB
         BALR  R14,R15

         MVC   WORK_BUFFER(ROW_TRAILERL),ROW_TRAILER
         LA    R1,WORK_BUFFER
         ST    R1,HTTP_DATA
         LA    R1,ROW_TRAILERL
         ST    R1,HTTP_LENGTH
         LA    R1,HTTP_PARMS
         L     R15,HTTP_HTTPSUB
         BALR  R14,R15
ACT0010  DS    0H
         USING PSA,R0                    DEFINE PSA BASE
         L     R1,FLCCVT                 ADDRESS OF CVT FROM PSA
         USING CVT,R1                    DEFINE CVT BASE
         L     R1,CVTASVT                ASVT ADDRESS FROM CVT
         USING ASVT,R1                   DEFINE ASVT BASE
         L     R3,ASVTMAXU               MAXIMUM NUMBER OF ENTRIES
         LA    R4,ASVTFRST               FIRST ENTRY
*---------------------------------------------------------------------*
*    R4 is the index into the address space vector table.  If the     *
*    high bit is on, the address space is not active.                 *
*---------------------------------------------------------------------*
ACT0030  DS    0H
         LA    R4,4(,R4)                 BUMP BY FOUR.
         ICM   R5,15,0(R4)               ASCB ADDRESS FROM ASVT
         BM    ACT0280                   ADDRESS SPACE NOT IN USE
         USING ASCB,R5                   DEFINE ASCB BASE
         MVC   DETAIL(DETAILL),DETAIL_INIT
*---------------------------------------------------------------------*
*    TEST FOR SWAPPED IN/SWAPPED OUT                                  *
*---------------------------------------------------------------------*
         L     R6,ASCBOUCB               OUCB ADDRESS FROM ASCB
         USING OUCB,R6                   DEFINE OUCB BASE
         TM    OUCBQFL,OUCBLSW           LOGICAL SWAP?
         BO    ACT0040                   LOGICAL SWAPS ARE 'IN'
         TM    ASCBRCTF,ASCBOUT          SWAPPED OUT?
         BNO   ACT0040                   NO
ACT0040  DS    0H
         MVC   WORK_JOB,=CL8'STARTING'   INITIALIZE JOB NAME
         ICM   R7,15,ASCBJBNI            JOB NAME ADDRESS
         BZ    ACT0050                   NO JOB NAME
         MVC   WORK_JOB,0(R7)            MOVE JOBNAME
         S     R7,=A(CHKEY-CSCB)         CSCB
         B     ACT0060
ACT0050  DS    0H
         ICM   R7,15,ASCBJBNS            STC/MOUNT/LOGON NAME ADDRESS
         BZ    ACT0060                   NO STC/MOUNT/LOGON NAME
         MVC   WORK_JOB,0(R7)            MOVE STC/MOUNT/LOGON NAME
         S     R7,=A(CHCLS-CSCB)         CSCB
         CLI   WORK_JOB,C'*'             MASTER SCHEDULER?
         BNE   ACT0060                   NO
         SR    R7,R7                     MASTER
ACT0060  DS    0H
         TM    OUCBYFL,OUCBLOG           TSO ADDRESS SPACE?
         BNO   ACT0080                   NO
         LA    R7,8(R7)                  READJUST CSCB POINTER
         MVI   WORK_TYPE,C'T'            SET ADDRESS SPACE TYPE
         B     ACT0110
*---------------------------------------------------------------------*
*    TEST FOR STC AND MOUNT ADDRESS SPACES                            *
*---------------------------------------------------------------------*
ACT0080  DS    0H
         LTR   R7,R7                     CSCB PRESENT?
         BZ    ACT0090                   NO, ASSUME STC
         USING CSCB,R7
         CLI   CHTRKID,CHJOBID           INITIATED JOB?
         BE    ACT0100                   YES
ACT0090  DS    0H
         CLC   WORK_JOB,=CL8'INIT'       INITIATOR?
         BE    ACT0280                   YES, DON'T SHOW INITIATORS
         MVI   WORK_TYPE,C'S'            SET ADDRESS SPACE TYPE
         B     ACT0110
*---------------------------------------------------------------------*
*    BATCH JOB - CHECK JOBNAME FOR SPECIAL PROCESSING                 *
*---------------------------------------------------------------------*
ACT0100  DS    0H
         CLI   CHVCD,X'04'               START, MOUNT, OR LOGON?
         BE    ACT0090                   YES
         CLI   CHVCD,X'0C'               START, MOUNT, OR LOGON?
         BE    ACT0090                   YES
         MVI   WORK_TYPE,C'B'            BATCH ADDRESS SPACE
*---------------------------------------------------------------------*
*    ADDRESS SPACE IS TO BE DISPLAYED, BUILD THE OUTPUT DETAIL.       *
*---------------------------------------------------------------------*
ACT0110  DS    0H
         MVC   DTLJOB,WORK_JOB           SET NAME
         MVC   DTLTYPE,WORK_TYPE         SET ADDRESS SPACE TYPE
         LTR   R7,R7                     CSCB PRESENT?
         BZ    ACT0150                   NO
         CLI   CHVCD,X'04'               STC, MOUNT, OR LOGON?
         BE    ACT0120                   YES
         CLI   CHVCD,X'0C'               STC, MOUNT, OR LOGON?
         BE    ACT0120                   YES
         CLI   CHSTEP,X'00'              STEP NAME PRESENT?
         BE    ACT0130                   NO
         MVC   DTLSTEP,CHSTEP            SET STEP NAME
         B     ACT0130
ACT0120  DS    0H
         CLI   CHKEY,X'40'               STEP NAME GIVEN?
         BNH   ACT0150                   NO
         MVC   DTLSTEP,CHKEY             SET STEP NAME
ACT0130  DS    0H
         CLI   WORK_TYPE,C'T'            TSO USER?
         BNE   ACT0140                   NO
         MVC   DTLPROC,=CL8'TSO USER'
         CLI   CHCLS,X'00'               STEP NAME PRESENT?
         BE    ACT0150                   NO
         MVC   DTLSTEP,CHCLS             SET STEP NAME
         B     ACT0150
ACT0140  DS    0H
         CLI   CHPROCSN,X'00'            PROC STEP PRESENT?
         BE    ACT0150                   NO
         CLI   WORK_TYPE,C'B'            BATCH
         BNE   ACT0150                   NO
         MVC   DTLPROC,CHPROCSN          SET PROC STEP
         DROP  R7
         EJECT
*---------------------------------------------------------------------*
*    SHOW IF JOB IS SWAPPED IN OR OUT, OR IS IN TRANSITION            *
*---------------------------------------------------------------------*
ACT0150  DS    0H
         TM    OUCBQFL,X'E0'             TRANSITIONING STATUS?
         BZ    ACT0160                   NO
         MVC   DTLSTAT,=CL3'<->'         INDICATE TRANSITIONING
         B     ACT0200
ACT0160  DS    0H
         TM    OUCBQFL,OUCBLSW           LOGICAL SWAP INDICATOR?
         BZ    ACT0170                   NO
         MVC   DTLSTAT,=CL3'LSW'         SET LOGICAL SWAP INDICATOR
         B     ACT0200
ACT0170  DS    0H
         TM    ASCBRCTF,ASCBOUT          SWAPPED OUT?
         BO    ACT0190                   YES
         CLC   OUCBNDS,=H'0'             NON-SWAPPABLE?
         BNE   ACT0180                   NO
         MVC   DTLSTAT,=CL3'N/S'         MOVE IN NON-SWAP INDICATION
         B     ACT0200
ACT0180  DS    0H
         MVC   DTLSTAT,=CL3'IN'          SET MEMORY POSITION TO IN
         B     ACT0200
ACT0190  DS    0H
         MVC   DTLSTAT,=CL3'OUT'         SET STATUS
         B     ACT0210
ACT0200  DS    0H
         MVC   DTLUSER,OUCBUSRD          SET USER
         B     ACT0240
*---------------------------------------------------------------------*
*    JOB IS SWAPPED OUT - PUT SWAP REASON IN PLACE OF USER            *
*---------------------------------------------------------------------*
ACT0210  DS    0H
         LA    R1,SWAPTBL                SWAP REASON TABLE ADDRESS
ACT0220  DS    0H
         CLC   OUCBSRC(1),0(R1)          REASON FOUND?
         BE    ACT0230                   YES
         LA    R1,6(R1)                  NEXT ENTRY
         CLI   0(R1),X'FF'               END OF TABLE?
         BNE   ACT0220                   NO
ACT0230  DS    0H
         MVI   DTLSTAT+4,C'-'
         MVC   DTLUSER(5),1(R1)          SET SWAP REASON
*---------------------------------------------------------------------*
*    GET SWAP COUNT                                                   *
*---------------------------------------------------------------------*
ACT0240  DS    0H
         LH    R7,OUCBSWC                NBR OF SWAPS
         CVD   R7,WORK_DWORD             CONVERT TO DECIMAL
         MVC   DTLSWAP,=X'40202120'      MOVE IN THE EDIT MASK
         ED    DTLSWAP,WORK_DWORD+6      EDIT THE OUTPUT FIELD
*---------------------------------------------------------------------*
*    GET SERVICE UNITS                                                *
*---------------------------------------------------------------------*
ACT0250  DS    0H
         L     R7,OUCBWMS                LOAD SERVICE UNITS
         CVD   R7,WORK_DWORD             CONVERT TO DECIMAL
         MVC   DTLSRVC,=X'4020202020202120'   INITIALIZE EDIT MASK
         ED    DTLSRVC,WORK_DWORD+4      EDIT THE RESULT
*---------------------------------------------------------------------*
*    GET NON-SPOOL I/O COUNT                                          *
*---------------------------------------------------------------------*
         L     R7,ASCBIOSC               NBR OF IO'S
         CVD   R7,WORK_DWORD             CONVERT TO DECIMAL
         MVC   DTLIO,=X'4020202020202120'     INITIALIZE EDIT MASK
         ED    DTLIO,WORK_DWORD+4        EDIT I/O'S
*---------------------------------------------------------------------*
*    CALCULATE CPU TIME                                               *
*---------------------------------------------------------------------*
ACT0260  DS    0H
         LM    R0,R1,ASCBEJST            LOAD TASK TIME
         SRDL  R0,12                     CONVERT TO MICRO SEC
         D     R0,=F'10000'              CONVERT TO HUNDREDTHS
         LM    R14,R15,ASCBSRBT          LOAD SRB TIME
         SRDL  R14,12                    CONVERT TO MICRO SECONDS
         D     R14,=F'10000'             CONVERT TO HUNDREDTHS
         AR    R1,R15                    TASK + SRB TIME
         MVC   DTLCPU(11),=C'  .  .  .  '
         SR    R0,R0
         D     R0,=F'100'                CONVERT TO WHOLE SECONDS
         CVD   R0,WORK_DWORD             CONVERT TO DECIMAL
         UNPK  DTLCPU+9(2),WORK_DWORD
         OI    DTLCPU+10,X'F0'           MAKE IT PRINTABLE
         SR    R0,R0
         D     R0,=F'60'                 CONVERT TO MINUTES
         CVD   R0,WORK_DWORD             CONVERT TO DECIMAL
         UNPK  DTLCPU+6(2),WORK_DWORD
         OI    DTLCPU+7,X'F0'            MAKE IT PRINTABLE
         SR    R0,R0
         D     R0,=F'60'                 MINUTES AND HOURS
         CVD   R0,WORK_DWORD             CONVERT MINUTES TO DECIMAL
         UNPK  DTLCPU+3(2),WORK_DWORD
         OI    DTLCPU+4,X'F0'            MAKE IT PRINTABLE
         CVD   R1,WORK_DWORD             CONVERT HOURS TO DECIMAL
         UNPK  DTLCPU(2),WORK_DWORD
         OI    DTLCPU+1,X'F0'            MAKE IT PRINTABLE
*---------------------------------------------------------------------*
*    Detail for this address space complete.                          *
*---------------------------------------------------------------------*
         TR    DETAIL(DETAILL),TRTAB
ACT0270  DS    0H
         MVC   WORK_BUFFER(ROW_HEADERL),ROW_HEADER
         LA    R1,WORK_BUFFER
         ST    R1,HTTP_DATA
         LA    R1,ROW_HEADERL
         ST    R1,HTTP_LENGTH
         LA    R1,HTTP_PARMS
         L     R15,HTTP_HTTPSUB
         BALR  R14,R15
         LA    R1,DETAIL
         ST    R1,HTTP_DATA
         LA    R1,DETAILL
         ST    R1,HTTP_LENGTH
         LA    R1,HTTP_PARMS
         L     R15,HTTP_HTTPSUB
         BALR  R14,R15
         MVC   WORK_BUFFER(ROW_TRAILERL),ROW_TRAILER
         LA    R1,WORK_BUFFER
         ST    R1,HTTP_DATA
         LA    R1,ROW_TRAILERL
         ST    R1,HTTP_LENGTH
         LA    R1,HTTP_PARMS
         L     R15,HTTP_HTTPSUB
         BALR  R14,R15

ACT0280  DS    0H
         BCT   R3,ACT0030                PROCESS NEXT ADDRESS SPACE

         MVC   WORK_BUFFER(TABLE_TRAILERL),TABLE_TRAILER
         LA    R1,WORK_BUFFER
         ST    R1,HTTP_DATA
         LA    R1,TABLE_TRAILERL
         ST    R1,HTTP_LENGTH
         LA    R1,HTTP_PARMS
         L     R15,HTTP_HTTPSUB
         BALR  R14,R15

         MVC   WORK_BUFFER(HTML_TRAILERL),HTML_TRAILER
         LA    R1,WORK_BUFFER
         ST    R1,HTTP_DATA
         LA    R1,HTML_TRAILERL
         ST    R1,HTTP_LENGTH
         LA    R1,HTTP_PARMS
         L     R15,HTTP_HTTPSUB
         BALR  R14,R15
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         LR    R1,R13                    COPY WORK AREA ADDRESS
         L     R13,4(R13)                RESTORE CALLER'S R13
         FREEMAIN RU,A=(1),LV=WORKL      FREE THE WORK AREA
         L     R14,12(R13)               RESTORE R14
         SR    R15,R15
         LM    R0,R12,20(R13)
         BR    R14
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
P_1900000           DC  P'1900000'
HTTP_HEADER         DS  0C
                    DC  C'HTTP/1.0 200 OK'
                    DC  AL1($LF)
                    DC  C'Content-type: text/html'
                    DC  AL1($LF)
                    DC  AL1($LF)
HTTP_HEADERL        EQU *-HTTP_HEADER
HTML_HEADER         DS  0C
                    DC  C'<HTML>'
                    DC  C'<BODY>'
                    DC  C'<CENTER>'
                    DC  AL1($LF)
HTML_HEADERL        EQU *-HTML_HEADER
SYSTEM_HEADER       DS  0C
                    DC  C'<H1>Active address spaces on '
SYSTEM_NAME         DC  CL8' '
                    DC  C'</H1>'
                    DC  C'<H3>'
SYSTEM_DATE         DC  CL8' '
                    DC  CL2' '
SYSTEM_TIME         DC  CL9' '
                    DC  C'</H3>'
SYSTEM_HEADERL      EQU *-SYSTEM_HEADER
HTML_TRAILER        DS  0C
                    DC  C'</CENTER>'
                    DC  C'</BODY>'
                    DC  C'</HTML>'
HTML_TRAILERL       EQU *-HTML_TRAILER
TABLE_HEADER        DS  0C
                    DC  C'<TABLE BORDER>'
                    DC  AL1($LF)
TABLE_HEADERL       EQU *-TABLE_HEADER
TABLE_TRAILER       DS  0C
                    DC  C'</TABLE>'
                    DC  AL1($LF)
TABLE_TRAILERL      EQU *-TABLE_TRAILER
ROW_HEADER          DS  0C
                    DC  C'<TR>'
                    DC  AL1($LF)
ROW_HEADERL         EQU *-ROW_HEADER
ROW_TRAILER         DS  0C
                    DC  C'</TR>'
                    DC  AL1($LF)
ROW_TRAILERL        EQU *-ROW_TRAILER

DETAIL_INIT         DS    0C
                    DC    CL4'<TD>'
                    DC    CL8' '
                    DC    CL5'</TD>'
                    DC    CL4'<TD>'
                    DC    CL8' '
                    DC    CL5'</TD>'
                    DC    CL4'<TD>'
                    DC    CL8' '
                    DC    CL5'</TD>'
                    DC    CL4'<TD>'
                    DC    C' '
                    DC    CL5'</TD>'
                    DC    CL4'<TD>'
                    DC    CL3' '
                    DC    CL5'</TD>'
                    DC    CL4'<TD>'
                    DC    CL8' '
                    DC    CL5'</TD>'
                    DC    CL4'<TD>'
                    DC    CL4' '
                    DC    CL5'</TD>'
                    DC    CL4'<TD>'
                    DC    CL8' '
                    DC    CL5'</TD>'
                    DC    CL4'<TD>'
                    DC    CL8' '
                    DC    CL5'</TD>'
                    DC    CL4'<TD>'
                    DC    CL11' '
                    DC    CL5'</TD>'

SWAPTBL             DS  0C
                    DC  X'01',CL5'TRMOT'
                    DC  X'02',CL5'TRMIN'
                    DC  X'03',CL5'LWAIT'
                    DC  X'04',CL5'AUXST'
                    DC  X'05',CL5'RSTOR'
                    DC  X'06',CL5'DWAIT'
                    DC  X'07',CL5'RQSWP'
                    DC  X'08',CL5'ENQEX'
                    DC  X'09',CL5'EXCHG'
                    DC  X'0A',CL5'UNLAT'
                    DC  X'FF',CL5'UNK  '

TRTAB               DS    0CL256
                    DC    C'                '     X'00' - X'0F'
                    DC    C'                '     X'10' - X'1F'
                    DC    C'                '     X'20' - X'2F'
                    DC    C'                '     X'30' - X'3F'
                    DC    C'          ¢.<(+|'     X'40' - X'4
                    DC    C'&&         !$*);¬'    X'50' - X'5F'
                    DC    C'-/        ¦,%_>?'     X'60' - X'6F'
                    DC    C'          :#@''="'    X'70' - X'7F'
                    DC    C'{ABCDEFGHI      '     X'80' - X'8F'
                    DC    C' JKLMNOPQR      '     X'90' - X'9F'
                    DC    C'  STUVWXYZ      '     X'A0' - X'AF'
                    DC    C'                '     X'B0' - X'BF'
                    DC    C'}ABCDEFGHI      '     X'C0' - X'CF'
                    DC    C' JKLMNOPQR      '     X'D0' - X'DF'
                    DC    C'  STUVWXYZ      '     X'E0' - X'EF'
                    DC    C'0123456789      '     X'F0' - X'FF'
* ------------------------------------------------------------------- *
*        WORK AREAS                                                   *
* ------------------------------------------------------------------- *
WORK                DSECT
                    DS    18F
WORK_DWORD          DS    D
WORK_TIME           DS    F
WORK_DATE           DS    F

WORK_JOB            DC    CL8' '
WORK_TYPE           DC    C' '

DETAIL              DS    0C
                    DS    CL4
DTLJOB              DS    CL8
                    DS    CL5
                    DS    CL4
DTLSTEP             DS    CL8
                    DS    CL5
                    DS    CL4
DTLPROC             DS    CL8
                    DS    CL5
                    DS    CL4
DTLTYPE             DS    C
                    DS    CL5
                    DS    CL4
DTLSTAT             DS    CL3
                    DS    CL5
                    DS    CL4
DTLUSER             DS    CL8
                    DS    CL5
                    DS    CL4
DTLSWAP             DS    CL4
                    DS    CL5
                    DS    CL4
DTLSRVC             DS    CL8
                    DS    CL5
                    DS    CL4
DTLIO               DS    CL8
                    DS    CL5
                    DS    CL4
DTLCPU              DS    CL11
                    DS    CL5
DETAILL             EQU   *-DETAIL

WORK_BUFFER         DS    256C

WORKL               EQU   *-WORK
                    SUBPARM  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         IHAPSA     DSECT=YES,LIST=NO
         IKJTCB     LIST=NO
         IEFTIOT1
         CVT   DSECT=YES
         IHAASVT
         IHAASCB
         IRAOUCB
CSCB     DSECT
         IEECHAIN
         COPY REGEQU
$LF      EQU  X'0A'
         END  APP2ACT
