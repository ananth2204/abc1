         TITLE 'HTTPPRT    PRINT SUBTASK'
*---------------------------------------------------------------------*
*                                                                     *
*  Module name: HTTPPRT                                               *
*                                                                     *
*   This subtask performs printing for the HTTP main address space.   *
*                                                                     *
*   The HTTPMAIN address space runs as multiple subtasks.  This       *
*   module allows any subtask in the main address space to send       *
*   SYSPRINT to a common output dataset.                              *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
HTTPPRT  CSECT
HTTPPRT  AMODE 31
HTTPPRT  RMODE ANY
         USING HTTPPRT,R12                    DEFINE BASE
         USING PRTBLOK,R7                     DEFINE BASE
         USING HTTPCOMM,R11                   DEFINE BASE
         STM   R14,R12,12(R13)                CHAIN REGISTERS
         LR    R12,R15                        COPY ENTRY POINT ADDRESS
         B     PRT0000
         DC    CL8'HTTPPRT'
         DC    CL8'&SYSDATE'
         DC    CL6'&SYSTIME'
PRT0000  DS    0H
         L     R11,0(R1)                      LOAD HTTPCOMM'S ADDRESS
         GETMAIN RU,                          STORAGE FOR WORK AREA    +
               LV=WORK_AREA_LENGTH,                                    +
               LOC=BELOW
         ST    R13,4(R1)                      CHAIN PREVIOUS AREA
         ST    R1,8(R13)                      CHAIN OUR AREA
         LR    R13,R1                         COPY WORK AREA ADDRESS
         USING WORKAREA,R13
         ITRACE ID=INIT
         ST    R12,COMM_HTTPPRT_ADDRESS
         ST    R13,COMM_PRINT_WORK_AREA
         MVC   WORK_DCB(DCBL),DCBI            INITIALIZE DCB
         MVI   WORK_CC,C' '                   INITIALIZE CC
         MVC   WORK_PRINT_DATE(WORK_PRINT_LRECL-1),WORK_CC
         MVC   WORK_OC_PARMS(OPENL),OPENI                               T
         ZAP   WORK_LINE,P_ZERO               INITIALIZE LINE COUNT
         ZAP   WORK_PAGE,P_ZERO               INITIALIGE PAGE COUNT
         OPEN  (WORK_DCB,OUTPUT),                                      +
               MF=(E,WORK_OC_PARMS),                                   +
               MODE=31
         B     PRT0030                        CHECK THE QUEUE
PRT0010  DS    0H
         TM    COMM_EOJ,$EOJ_PHASE_4          SHUTDOWN PHASE 4?
         BO    EXIT0000                       YES.. IT'S MILLER TIME
         TM    COMM_PRINT_WORK_ECB,X'40'      ECB ALREADY POSTED?
         BO    PRT0020                        GET BUSY
         ITRACE ID=WORKWAIT,                  WAITING FOR WORK         +
               RDATA1=R1                      .. ECB'S ADDRESS
         WAIT  ECB=COMM_PRINT_WORK_ECB        WAIT FOR WORK TO DO ECB
         ITRACE ID=WOKEUP
PRT0020  DS    0H
         XC    COMM_PRINT_WORK_ECB,COMM_PRINT_WORK_ECB
PRT0030  DS    0H
         SR    R1,R1                          CLEAR REGISTER
PRT0040  DS    0H
         ICM   R2,15,COMM_PRINT_QUEUE
         BZ    PRT0010
         CS    R2,R1,COMM_PRINT_QUEUE
         BC    4,PRT0040
         USING PRTBLOK,R10                    DEFINE BASE
         ITRACE ID=HAVEQ,                     WE HAVE THE QUEUE        +
               RDATA1=R2                      .. LAST QUEUE BLOCK
         LR    R3,R2                          COPY ADDRESS
         LA    R1,4                           INITIALIZE GETMAIN LENGTH
PRT0050  DS    0H
         ICM   R3,15,PRT_NEXT-PRTBLOK(R3)
         BZ    PRT0060                        NEXT BLOCK'S ADDRESS
         ITRACE ID=VEC1,RDATA1=R3,RDATA2=R1
         LA    R1,4(R1)                       ADD TO VECTOR LENGTH
         B     PRT0050                        LOOP
PRT0060  DS    0H
         ITRACE ID=GETVEC,                                             +
               RDATA1=R1                      .. SIZE OF VECTOR LIST
         ST    R1,WORK_VECTOR_SIZE            SAVE VECTOR SIZE
         GETMAIN RU,                          GETMAIN STORAGE          +
               LV=(1),                                                 +
               LOC=ANY
         ST    R1,WORK_VECTOR_ADDRESS         SAVE VECTOR ADDRESS
         ITRACE ID=VECADDR,                                            +
               RDATA1=R1                      .. VECTOR ADDRESS
PRT0070  DS    0H
         ITRACE ID=BLDVEC,                                             +
               RDATA1=R1,                     .. CURRENT IN VECTOR     +
               RDATA2=R2                      .. PRTBLOK'S ADDRESS
         ST    R2,0(R1)                       SET ADDRESS
         ST    R1,WORK_VECTOR_CURRENT
         LA    R1,4(R1)                       NEXT
         ICM   R2,15,PRT_NEXT-PRTBLOK(R2)
         BNZ   PRT0070
PRT0080  DS    0H
         L     R1,WORK_VECTOR_CURRENT
         L     R10,0(R1)                      PRTBLOK'S ADDRESS
         ITRACE ID=PRT,                                                +
               RDATA1=R1,                                              +
               RDATA2=R10
         CLI   PRT_COMMAND,$PRT_HEAD          FORCED HEADING?
         BE    PRT0170                        YES
         MVC   WORK_CC,PRT_CC                 COPY CARRIAGE CONTROL
         TIME  DEC
         STM   R0,R1,WORK_TIME_DATE
         UNPK  WORK_UNPACK(9),WORK_TIME(5)
         MVZ   WORK_UNPACK,HEXZERO
         TR    WORK_UNPACK,HEXCHAR
         MVC   WORK_PRINT_TIME+00(2),WORK_UNPACK+00
         MVC   WORK_PRINT_TIME+03(2),WORK_UNPACK+02
         MVC   WORK_PRINT_TIME+06(2),WORK_UNPACK+04
         MVC   WORK_PRINT_TIME+09(2),WORK_UNPACK+06
         MVI   WORK_PRINT_TIME+2,C':'
         MVI   WORK_PRINT_TIME+5,C':'
         MVI   WORK_PRINT_TIME+8,C'.'
         MVC   WORK_PRINT_DATE,DATE_EDIT_WORD
         AP    WORK_DATE,P_190000
         ED    WORK_PRINT_DATE,WORK_DATE
         MVC   WORK_MESSAGE_ID,PRT_MESSAGE_ID
         LA    R1,WORK_MESSAGE_ID+(L'WORK_MESSAGE_ID-1)
PRT0090  DS    0H
         CLI   0(R1),C' '                     BLANK?
         BNE   PRT0100                        NO
         BCT   R1,PRT0090                     LOOP
PRT0100  DS    0H
         CLI   0(R1),C'E'                     'ERROR' MESSAGE
         BNE   PRT0110
         MVI   WORK_ERROR_FLAG,C'*'           FLAG ERROR MESSAGE
PRT0110  DS    0H
         CLI   PRT_COMMAND,$PRT_ASIS          PRINT AS IS?
         BE    PRT0140                        YES
         LA    R1,WORK_DATA                   FIRST PRINT AREA BYTE
         LA    R2,PRT_DATA                    PRINT OUTPUT AREA BYTE
         LA    R3,L'PRT_DATA                  MAXIMUM LOOPS
PRT0120  DS    0H
         CLC   COMM_BLANKS(2),0(R2)           2 BLANKS IN A ROW?
         BE    PRT0130                        YES
         CLC   =C' )',0(R2)                   BLANK AND AN OPEN PAREN?
         BE    PRT0130                        YES
         CLC   =C' ,',0(R2)                   BLANK AND A COMMA?
         BE    PRT0130                        YES
         MVC   0(1,R1),0(R2)                  COPY TO OUTPUT AREA
         LA    R1,1(R1)                       NEXT OUTPUT BYTE
PRT0130  DS    0H
         LA    R2,1(R2)                       NEXT INPUT BYTE
         BCT   R3,PRT0120                     LOOP
         B     PRG0150
PRT0140  DS    0H
         MVC   WORK_DATA,PRT_DATA             COPY AS IS
PRG0150  DS    0H
         PUT   WORK_DCB,WORK_CC               PRINT
         AP    WORK_LINE,P_ONE                ADD 1 TO LINE COUNT
         CLI   WORK_CC,C' '                   SINGLE SPACE
         BE    PRT0160                        YES
         AP    WORK_LINE,P_ONE                ADD 1 TO LINE COUNT
         CLI   WORK_CC,C'0'                   DOUBLE SPACE?
         BE    PRT0160                        YES
         AP    WORK_LINE,P_ONE                ADD 1 TO LINE COUNT
PRT0160  DS    0H
         MVI   WORK_CC,C' '                   INITIALIZE Cc
         MVC   WORK_PRINT_DATE(WORK_PRINT_LRECL-1),WORK_CC
         CP    WORK_LINE,COMM_MAX_LINES
         BNH   PRT0200
PRT0170  DS    0H
         AP    WORK_PAGE,P_ONE
         MVC   WORK_DATA(HEADINGL),HEADING
        MVC   WORK_DATA+(HEAD_PAGE-HEADING)(L'HEAD_PAGE),PAGE_EDIT_WORD
         ED    WORK_DATA+(HEAD_PAGE-HEADING)(L'HEAD_PAGE),WORK_PAGE
         PUT   WORK_DCB,WORK_CC
         ZAP   WORK_LINE,P_ONE
         MVI   WORK_CC,C' '
         MVC   WORK_PRINT_DATE(WORK_PRINT_LRECL-1),WORK_CC
PRT0200  DS    0H
         ITRACE ID=FREEPRT,                                            +
               RDATA1=R10
         FREEMAIN RU,                                                  +
               A=(10),                                                 +
               LV=PRTBLOKL
         L     R1,WORK_VECTOR_CURRENT         CURRENT IN VECTOR
         C     R1,WORK_VECTOR_ADDRESS         AT FIRST ENTRY?
         BE    PRT0210                        YES
         SH    R1,H4                          BACK UP 1 ENTRY
         ST    R1,WORK_VECTOR_CURRENT         SAVE CURRENT ADDRESS
         B     PRT0080                        PROCESS THIS PRTBLOK
PRT0210  DS    0H
         L     R0,WORK_VECTOR_SIZE            VECTOR SIZE
         ITRACE ID=FREEVEC,                                            +
               RDATA1=R1,                                              +
               RDATA2=R0
         FREEMAIN RU,                         FREE THE VECTOR          +
               A=(1),                                                  +
               LV=(0)
         B     PRT0020
* ------------------------------------------------------------------- *
*        EXIT                                                         *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=EXIT
         MVC   WORK_OC_PARMS(CLOSEL),CLOSEI   COPY CLOSE LIST FORM
         CLOSE WORK_DCB,                      CLOSE DCB                +
               MF=(E,WORK_OC_PARMS),                                   +
               MODE=31
         LR    R1,R13                         COPY WORK AREA ADDRESS
         L     R13,4(R13)                     RESTORE REGISTER 13       ASE01670
         FREEMAIN RU,                         FREE WORK AREA           +
               A=(1),                                                  +
               LV=WORK_AREA_LENGTH
         LM    R14,R12,12(R13)                RESTORE REGISTERS         ASE01680
         SR    R15,R15                        RETURN CODE               ASE01690
         BR    R14                            RETURN TO CALLER          ASE01700
* ------------------------------------------------------------------- *
PRTMVC   MVC   WORK_PRINT_DATE(0),PRT_DATA    COPY PRINT DATA
* ------------------------------------------------------------------- *
*                                                                     *
*        CONSTANTS                                                    *
*                                                                     *
* ------------------------------------------------------------------- *
H4             DC    H'4'
DATE_EDIT_WORD DC    X'40212020204B202020'
PAGE_EDIT_WORD DC    X'402020202120'
HEXZERO        DC    16X'00'
HEXCHAR        DC    C'0123456789ABCDEF'
P_ZERO         DC    P'0'
P_ONE          DC    P'1'
P_190000       DC    P'1900000'
HEADING        DS    0C
               DC    50C' '
               DC    C'HTTP MAIN LOG'
               DC    50C' '
               DC    C'PAGE '
HEAD_PAGE      DC    CL6' '
HEADINGL       EQU   *-HEADING
DCBI     DCB   DSORG=PS,                                               +
               DDNAME=SYSPRINT,                                        +
               RECFM=FBA,                                              +
               LRECL=WORK_PRINT_LRECL,                                 +
               MACRF=PM
DCBL     EQU   *-DCBI
OPENI    OPEN  (*,OUTPUT),MODE=31,MF=L
OPENL    EQU   *-OPENI
CLOSEI   CLOSE (*),MODE=31,MF=L
CLOSEL   EQU   *-CLOSEI
         LTORG
* ------------------------------------------------------------------- *
*                                                                     *
*        WORK AREAS                                                   *
*                                                                     *
* ------------------------------------------------------------------- *
WORKAREA              DSECT
                      STDSAVE DSECT=NO
WORK_TIME_DATE        DS  0F
WORK_TIME             DS  F          TIME/DATE
WORK_DATE             DS  F
WORK_VECTOR_SIZE      DS  F          ADDRESS VECTOR SIZE
WORK_VECTOR_ADDRESS   DS  A          ADDRESS OF ADDRESS VECTOR
WORK_VECTOR_CURRENT   DS  A          CURRENT ADDRESS IN ADDRESS VECTOR
WORK_LINE             DS  PL3
WORK_PAGE             DS  PL3
WORK_UNPACK           DS  CL16


WORK_CC               DS  C          CARRIAGE CONTROL
WORK_PRINT_DATE       DS  CL9        DATE
                      DS  C
WORK_PRINT_TIME       DS  CL11       TIME
                      DS  C
WORK_MESSAGE_ID       DS  CL15       MESSAGE ID
                      DS  C
WORK_ERROR_FLAG       DS  C          ERROR MESSAGE FLAG
                      DS  C
WORK_DATA             DS  CL200      PRINT DATA
WORK_PRINT_LRECL      EQU *-WORK_CC


WORK_DCB              DS  0A,(DCBL)X RESERVE SPACE FOR DCB
WORK_OC_PARMS         DS  (OPENL)X
WORK_AREA_LENGTH      EQU *-WORKAREA
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                      COMMON MAIN,TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                      PRTBLOK
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY REGEQU
         END  HTTPPRT
