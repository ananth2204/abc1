         TITLE 'CONSOLE COMMUNICATION'
         MACRO
         CMD   &TEXT,&ADDR
         LCLA  &LEN
         AIF   (T'&TEXT EQ 'O').ERR0010
         AIF   (T'&ADDR EQ 'O').ERR0020
&LEN     SETA  K'&TEXT
CMDS&SYSNDX DS  0C
         DC    AL2(CMDE&SYSNDX-CMDS&SYSNDX)
         DC    AL2(&LEN)
         DC    &ADDR
         DC    C'&TEXT'
CMDE&SYSNDX DS  0C
         MEXIT
.ERR0010 ANOP
         MNOTE 12,'COMMAND TEXT IS REQUIRED'
         MEXIT
.ERR0020 ANOP
         MNOTE 12,'COMMAND PROCESSOR ADDRESS IS REQUIRED'
         MEND
*---------------------------------------------------------------------*
*                                                                     *
*  Module name: HTTPCONS                                              *
*                                                                     *
*  Console subtask                                                    *
*                                                                     *
*                                                                     *
*  This module is ATTACHed as a subtask of the main task HTTPMAIN     *
*  by HTTPINIT during initialization of the main address space.       *
*                                                                     *
*  As of now there are only two functions.                            *
*                                                                     *
*   1) allow for SNAP dumps                                           *
*                                                                     *
*   2) allow the main address space to be shutdown via a STOP (P)     *
*      console command.                                               *
*                                                                     *
* ------------------------------------------------------------------- *
HTTPCONS CSECT
HTTPCONS AMODE 31
HTTPCONS RMODE ANY
         USING HTTPCONS,R12                   DEFINE BASE
         USING HTTPCOMM,R11                   DEFINE BASE
         STM   R14,R12,12(R13)                SAVE ENTRY REGISTERS
         LR    R12,R15                        COPY BASE
         B     INIT0000                       BRANCH AROUND EYECATCHER
MODID    DC    CL8'HTTPCONS'                  MODULE NAME
         DC    CL8'&SYSDATE'                  DATE OF ASSEMBLY
         DC    CL6'&SYSTIME'                  TIME OF ASSEMBLY
INIT0000 DS    0H
         L     R11,0(R1)                      COPY COMM AREA BASE
         ST    R12,COMM_HTTPCONS_ADDRESS
         GETMAIN RU,                          OBTAIN WORK AREA STORAGE +
               LV=WORKL,                                               +
               LOC=BELOW
         ST    R13,4(R1)
         ST    R1,8(R13)
         LR    R13,R1                         SAVE WORK AREA ADDRESS
         ST    R13,COMM_CONSOLE_WORK_AREA
         USING WORKAREA,R13                   DEFINE BASE
         ITRACE ID=INIT
         MVC   WORK_ID,MODID                  IDENTIFY WORK AREA OWNER
         MVC   PRT_EYE,PRT_ID                 IDENTIFY CONTROL BLOCK
         MVC   PRT_CC(PRT_LRECL),COMM_BLANKS
         GETMAIN RU,                          STORAGE FOR EXTRACT      +
               LV=EXTRACTL+4,                 .. LENGTH                +
               LOC=BELOW                      .. 24-BIT AREA
         LR    R2,R1                          SAVE THE ADDRESS
         LA    R3,EXTRACTL(R2)                RETURNED DATA ADDRESS
         MVC   0(EXTRACTL,R2),EXTRACTI
         EXTRACT (R3),                        ISSUE EXTRACT            +
               FIELDS=COMM,                   .. RETURN COMLIST        +
               MF=(E,(R2))                    ..
         L     R9,0(R3)                       COMM LIST ADDRESS
         FREEMAIN RU,                         FREE EXTRACT WORK AREA   +
               A=(R2),                                                 +
               LV=EXTRACTL+4
         ITRACE ID=COMLIST,                   TRACE COMLIST ADDRESS    +
               RDATA1=R9
         ST    R9,WORK_COMM                   SAVE COMLIST ADDRESS
         USING COMLIST,R9                     DEFINE COMLIST BASE
         MVC   WORK_CECB,COMECBPT             SAVE ECB ADDRESS
         ITRACE ID=SETCTR                     SETTING CIB LIMIT
         QEDIT ORIGIN=COMCIBPT,               SET CIB LIMIT            +
               CIBCTR=2                       .. ALLOW 2 CIBS
         LTR   R15,R15                        SUCCESSFUL?
         BNZ   ERR0020                        NO
         LA    R1,COMM_CONSOLE_WORK_ECB
         ST    R1,WORK_WORK_ECB               SET ADDRESS IN ECB LIST
         OI    WORK_WORK_ECB,X'80'            FLAG END-OF-LIST
         MVC   WORK_WTO(WTOL),WTOI
         LA    R4,WORK_WTO
         USING WPL,R4                         DEFINE WTO PARM LIST BASE
CONS0000 DS    0H
         LA    R2,WORK_ECB_LIST               ECB LIST ADDRESS
         ITRACE ID=WAIT,                                               +
               RDATA1=(R2)                    .. WAIT LIST ADDRESS
         TM    COMM_EOJ,$EOJ_PHASE_3          PHASE 3 OF SHUTDOWN?
         BO    EXIT0000                       YES
         WAIT  ECBLIST=(R2)                   WAIT
CONS0010 DS    0H
         ICM   R8,15,COMCIBPT                 FIRST CIB
         BZ    CONS0000                       NO CIB'S
         USING CIB,R8                         DEFINE CIB BASE
CONS0020 DS    0H
         ITRACE ID=CIB,                       TRACE EACH CIB           +
               RDATA1=R8,                     .. CIB ADDRESS           +
               DATA2=(CIBVERB,1)              .. VERB FROM CIB
         MVC   PRT_MESSAGE_ID(MSG07L),MSG07
         SR    R5,R5                          MAXIMUM GISTER
         ICM   R5,3,CIBDATLN                  DATA LENGTH
         CH    R5,H50                         TOO LONG?
         BNH   CONS0030                       NO
         LH    R5,H50                         LIMIT LENGTH
CONS0030 DS    0H
         LR    R15,R5                         COPY LENGTH
         BCTR  R15,0                          ADJUST FOR EXECUTE
         EX    R15,MVC01                      MOVE TO I/O AREA
         CLI   CIBVERB,CIBSTOP                STOP COMMAND?
         BNE   CONS0040                       NO
         MVC   PRT_MESSAGE_ID+(MSG07TXT-MSG07)(L'STOP),STOP
CONS0040 DS    0H
         BAL   R10,PRT0000                    PRINT THE MESSAGE
         ITRACE ID=CIBDATA,                                            +
               DATA1=(MSG07TXT,8)
         CLI   CIBVERB,CIBSTART               START COMMAND?
         BE    CONS0110                       YES
         LH    R15,CIBXOFF
         AR    R15,R8
         USING CIBX,R15
         MVC   WORK_CONSOLE,CIBXCNNM          COPY CONSOLE NAME
         CLI   CIBVERB,CIBSTOP                STOP COMMAND?
         BE    SHUT0000                       YES
         CLI   CIBVERB,CIBMODFY               MODIFY?
         BNE   CONS0110                       NO
         LA    R1,CIBDATA                     COMMAND
         SR    R2,R2                          LENGTH
CONS0050 DS    0H
         CLI   0(R1),C' '                     BLANK?
         BE    CONS0060                       YES
         CLI   0(R1),C','                     COMMA?
         BE    CONS0060                       YES
         LA    R1,1(,R1)                      PLUS 1
         LA    R2,1(,R2)                      PLUS 1
         BCT   R5,CONS0050                    LOOP
         ICM   R2,3,CIBDATLN                  RESTORE TO FULL LENGTH
CONS0060 DS    0H
         LA    R6,COMMANDS_TABLE              OUR LIST OF COMMANDS
         USING COMMAND_DSECT,R6               DEFINE BASE
         SR    R1,R1                          CLEAR REGISTER
CONS0070 DS    0H
         CLI   0(R6),X'FF'                    END OF TABLE?
         BE    CONS0090                       YES
         ICM   R1,3,COMMAND_TEXT_LENGTH
         CLR   R2,R1                          LENGTH CORRECT?
         BNE   CONS0080                       NO
         BCTR  R1,0                           MINUS 1 FOR EXECUTE
         EX    R1,CMDCLC                      COMMAND FOUND?
         BE    CONS0100                       YES
CONS0080 DS    0H
         ICM   R1,3,COMMAND_LENGTH            LENGTH OF THIS COMMAND
         AR    R6,R1                          NEXT COMMAND
         B     CONS0070                       LOOP
CMDCLC   CLC   COMMAND_TEXT,CIBDATA           COMMAND LOCATED?
CONS0090 DS    0H
         MVC   WPLTXT(MSG01L),MSG01           MESSAGE TO WTO
         BAL   R10,WTO0000                    ISSUE WTO
         B     CONS0110                       RELEASE THIS CIB
CONS0100 DS    0H
         LA    R1,CIBDATA                     DATA ADDRESS
         SR    R2,R2                          CLEAR REGISTER
         ICM   R2,3,CIBDATLN                  COMMAND LENGTH
         ICM   R15,15,COMMAND_ADDRESS         COMMAND PROCESSOR ADDRESS
         BALR  R14,R15                        PROCESS THE COMMAND
CONS0110 DS    0H
         L     R2,CIBNEXT                     NEXT CIB
         QEDIT ORIGIN=COMCIBPT,               FREE THIS CIB            +
               BLOCK=(R8)
         LTR   R15,R15                        QEDIT SUCCESSFUL?
         BNZ   ERR0010                        NO
         LTR   R8,R2                          NEXT CIB'S ADDRESS
         BNZ   CONS0020                       LOOP
         B     CONS0000                       WAIT FOR ANOTHER EVENT
* ------------------------------------------------------------------- *
*   PERFORM A SNAP DUMP                                               *
* ------------------------------------------------------------------- *
SNAP0000 DS    0H
         ITRACE ID=SNAP
         LOAD  EP=HTTPSNAP
         LR    R15,R0
         BALR  R14,R15                        LINK TO HTTPSNAP
         DELETE EP=HTTPSNAP
         ITRACE ID=SNAPDONE
         B     CONS0110
* ------------------------------------------------------------------- *
*   Shutdown the STC                                                  *
* ------------------------------------------------------------------- *
SHUT0000 DS    0H
         ITRACE ID=STOP
         TM    COMM_EOJ,$EOJ_PHASE_1          EOJ ALREADY IN PROGRESS?
         BO    SHUT0010                       YES
         POST  COMM_TERMINATION_ECB           POST THE TERMINATION ECB
         MVC   WPLTXT(MSG04L),MSG04           COPY WTO
         BAL   R10,WTO0000                    ISSUE WTO
         B     CONS0110                       RELEASE THIS CIB
SHUT0010 DS    0H
         MVC   WPLTXT(MSG05L),MSG05           COPY MESSAGE
         BAL   R10,WTO0000                    ISSUE WTO
         B     CONS0110                       RELEASE THIS CIB
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
WTO0000  DS    0H
         ITRACE ID=WTO
         WTO   MF=(E,WORK_WTO),               ISSUE WTO                +
               CONSNAME=WORK_CONSOLE          .. TO SAME CONSOLE
         MVC   WORK_WTO(WTOL),WTOI
         BR    R10
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
PRT0000  DS    0H
         ITRACE ID=PRINT
         GETMAIN RU,                                                   +
               LV=PRTBLOKL,                                            +
               LOC=ANY
         MVC   0(PRTBLOKL,R1),PRTBLOK
         MVI   PRT_CC,C' '                    SINGLE SPACE
         MVC   PRT_MESSAGE_ID(L'PRT_MESSAGE_ID+L'PRT_DATA),PRT_CC
PRT0010  DS    0H
         L     R2,COMM_PRINT_QUEUE            FIRST BLOCK ON QUEUE
         ST    R2,PRT_NEXT-PRTBLOK(R1)                        '
         CS    R2,R1,COMM_PRINT_QUEUE         ADD NEW BLOCK TO CHAIN
         BC    4,PRT0010                      TRY AGAIN
         POST  COMM_PRINT_WORK_ECB            WAKE UP THE PRINT SUBTASK
         BR    R10
* ------------------------------------------------------------------- *
*        Error routines                                               *
* ------------------------------------------------------------------- *
ERR0010  DS    0H
         ITRACE ID=CIBDELF,                   DELETE CIB  FAILED       +
               RDATA1=R15                     .. RETURN CODE
         ABEND 1,DUMP,,USER
ERR0020  DS    0H
         ITRACE ID=CIBCTRF,                   SET CIB LIMIT) FAILED    +
               RDATA1=R15                     .. RETURN CODE
         ABEND 1,DUMP,,USER
* ------------------------------------------------------------------- *
*        Exit                                                         *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=EXIT
         LR    R1,R13                         COPY SAVE AREA ADDRESS
         L     R13,4(R13)                     CALLER'S SAVE AREA
         FREEMAIN RU,                         FREE WORK AREA           +
               A=(1),                                                  +
               LV=WORKL
         LM    R14,R12,12(R13)                RESTORE REGISTERS
         SR    R15,R15                        RETURN CODE
         BR    R14                            RETURN TO CALLER
*
* ------------------------------------------------------------------- *
*
MVC01    MVC   PRT_MESSAGE_ID+(MSG07TXT-MSG07)(0),CIBDATA
* ------------------------------------------------------------------- *
*                                                                     *
*        CONSTANTS                                                    *
*                                                                     *
* ------------------------------------------------------------------- *
EXTRACTI EXTRACT *,                  EXTRACT LIST FORM                 +
               FIELDS=COMM,          .. COMLIST ADDRESS PLEASE         +
               MF=L
EXTRACTL EQU   *-EXTRACTI
WTOI     WTO   '                                                       +
                                                                     ',+
               CONSID=,                                                +
               MF=L
WTOL     EQU   *-WTOI
* -------------------------------------------------------------------
H50      DC    H'50'
PRT_ID   DC    CL8'PRTBLOK'
STOP     DC    C'STOP'
MSG01    DS    0C
         DC    CL15'HTTPCONS01E'
         DC    C'* Invalid command'
MSG01L   EQU   *-MSG01
MSG02    DS    0C
         DC    CL15'HTTPCONS02I'
         DC    C'taking snap dump'
MSG02L   EQU   *-MSG02
MSG04    DS    0C
         DC    CL15'HTTPCONS04I'
         DC    C'HTTP main shutting down'
MSG04L   EQU   *-MSG04
MSG05    DS    0C
         DC    CL15'HTTPCONS05E'
         DC    C'* Shutdown already in progress'
MSG05L   EQU   *-MSG05
MSG07    DS    0C
         DC    CL15'HTTPCONS07I'
         DC    C'Command from console: '
MSG07TXT DC    CL50' '
MSG07L   EQU   *-MSG07
HEXZERO  DC    XL8'0000000000000000'
HEXCHAR  DC    C'0123456789ABCDEF'
* ------------------------------------------------------------------- *
*              COMMANDS TABLE                                         *
* ------------------------------------------------------------------- *
COMMANDS_TABLE DS 0C
         CMD   QUIT,AL4(SHUT0000)     STOP (SHUTDOWN THE HTTP MAIN)
         CMD   SNAP,AL4(SNAP0000)     SNAP SHOT
         CMD   STOP,AL4(SHUT0000)     STOP (SHUTDOWN THE HTTP MAIN)
         DC    X'FF'
* ------------------------------------------------------------------- *
*              WORK AREA                                              *
* ------------------------------------------------------------------- *
WORKAREA        DSECT
                STDSAVE  DSECT=NO
WORK_ID         DS   CL8
WORK_DWORD      DS   D
WORK_COMM       DS   F                ADDRESS OF COMM LIST
WORK_CONSOLE    DS   CL8              CONSOLE NAME
*
WORK_ECB_LIST   DS   0A
WORK_CECB       DS   A                ADDRESS OF ECB POSTED BY MVS
WORK_WORK_ECB   DS   A                ADDRESS IF ECB POSTED BY MAIN
*
WORK_WTO        DS   0F,(WTOL)X
WORK_WORK       DS   CL10
                PRTBLOK  DSECT=NO
WORKL           EQU  *-WORKAREA
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
COMMAND_DSECT       DSECT
COMMAND_LENGTH      DS   AL2          LENGTH OF THIS ENTRY
COMMAND_TEXT_LENGTH DS   AL2          COMMAND TEXT LENGTH
COMMAND_ADDRESS     DS   AL4          COMMAND PROCESSOR ENTRY POINT
COMMAND_TEXT        DS   C            COMMAND TEXT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COMMON  MAIN,TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         IEZCOM
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
CIB      DSECT
         IEZCIB
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         IEZWPL DSECT=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY REGEQU
         END  HTTPCONS
