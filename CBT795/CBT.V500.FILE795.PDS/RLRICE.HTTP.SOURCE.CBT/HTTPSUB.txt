          MACRO
          SUBTRACE &ID=,                                               +
               &DATA1=,&RDATA1=,                                       +
               &DATA2=,&RDATA2=,                                       +
               &DATA3=,&RDATA3=
          AIF    (T'&ID EQ 'O').SUBNOID
          AIF    (T'&RDATA1 NE 'O' AND T'&DATA1 NE 'O').SUBDUP1
          AIF    (T'&RDATA2 NE 'O' AND T'&DATA2 NE 'O').SUBDUP2
          AIF    (T'&RDATA2 EQ 'O' AND T'&DATA2 EQ 'O').SUB001
          AIF    (T'&RDATA1 NE 'O' AND T'&DATA1 NE 'O').SUBNO1
.SUB001   ANOP
          AIF    (T'&DATA1 EQ 'O').SUB002
          MVC    WORK_TRACE_DATA1,&DATA1
.SUB002   ANOP
          AIF    (T'&RDATA1 EQ 'O').SUB003
          ST     &RDATA1,WORK_TRACE_DATA1
.SUB003   ANOP
          AIF    (T'&DATA2 EQ 'O').SUB004
          MVC    WORK_TRACE_DATA2,&DATA2
.SUB004   ANOP
          AIF    (T'&RDATA2 EQ 'O').SUB005
          ST     &RDATA2,WORK_TRACE_DATA2
.SUB005   ANOP
          AIF    (T'&DATA3 EQ 'O').SUB006
          MVC    WORK_TRACE_DATA3,&DATA3
          AGO    .SUB007
.SUB006   ANOP
          AIF    (T'&RDATA3 EQ 'O').SUB007
          ST     &RDATA3,WORK_TRACE_DATA3
.SUB007   ANOP
          BAL    R14,TRACE000
          DC     CL8'&ID'
          MEXIT
.SUBDUP1  ANOP
          MNOTE  12,'DO NOT USE BOTH DATA1 AND RDATA1'
          MEXIT
.SUBDUP2  ANOP
          MNOTE  12,'DO NOT USE BOTH DATA2 AND RDATA2'
          MEXIT
.SUBNO1   ANOP
          MNOTE  12,'(R)DATA2 NOT VALID WITHOUT (R)DATA1'
          MEXIT
.SUBNOID  ANOP
          MNOTE  12,'ID IS REQUIRED'
          MEND
*---------------------------------------------------------------------*
*                                                                     *
*  Module name: HTTPSUB                                               *
*                                                                     *
*                                                                     *
*     PARMLIST:      Mapped by SUBPARM macro                          *
*                                                                     *
* ------------------------------------------------------------------- *
HTTPSUB  CSECT
HTTPSUB  AMODE 31
HTTPSUB  RMODE ANY
         USING HTTPSUB,R12,R11                DEFINE BASE
         STM   R14,R12,12(R13)                SAVE CALLER'S REGISTERS
         LR    R12,R15                        SET BASE
         B     INIT0000
MODID    DC    CL8'HTTPSUB'
         DC    CL8'&SYSDATE'
         DC    CL6'&SYSTIME'
INIT0000 DS    0H
         LA    R11,2048(R12)
         LA    R11,2048(R11)
         LR    R10,R1                         COPY PARM BLOCK ADDRESS
         USING HTTP_PARMS,R10
         MVI   HTTP_RETURN_CODE,$HTTP_OK
         MVC   HTTP_MESSAGE_1,BLANKS
         MVC   HTTP_MESSAGE_2,BLANKS
         MVC   HTTP_MESSAGE_3,BLANKS
         MVC   HTTP_MESSAGE_4,BLANKS
         MVC   HTTP_MESSAGE_5,BLANKS
         CLI   HTTP_FUNCTION,$HTTP_INIT       INITIALIZE?
         BE    INIT0010                       YES
         L     R1,HTTP_WORKAREA
         ST    R13,4(R1)
         ST    R1,8(R13)
         LR    R13,R1
         USING WORKAREA,R13
         SUBTRACE ID=FUNCTION,                                         +
               DATA1=HTTP_FUNCTION,                                    +
               RDATA2=R13,                                             +
               Rdata3=R14
         CLI   HTTP_FUNCTION,$HTTP_ALLOCATE   ALLOCATE?
         BE    ALLOC000                       YES
         CLI   HTTP_FUNCTION,$HTTP_CLEAN_UP   CLEAN-UP
         BE    CLEAN000                       YES
         CLI   HTTP_FUNCTION,$HTTP_CLOSE      CLOSE THE SOCKET?
         BE    CLOSE000                       YES
         CLI   HTTP_FUNCTION,$HTTP_CONNECT    CONNECT TO MAIN?
         BE    CONN0000                       YES
         CLI   HTTP_FUNCTION,$HTTP_FREE       FREE A DD?
         BE    FREE0000                       YES
         CLI   HTTP_FUNCTION,$HTTP_GET_URL    GET URL?
         BE    GETURL00                       YES
         CLI   HTTP_FUNCTION,$HTTP_LOG        SEND DATA TO LOG
         BE    LOG0000                        YES
         CLI   HTTP_FUNCTION,$HTTP_DEFINE_PRFX
         BE    DEFURL00                       YES
         CLI   HTTP_FUNCTION,$HTTP_DEFINE_SPEC
         BE    DEFURL10                       YES
         CLI   HTTP_FUNCTION,$HTTP_WRITE      WRITE?
         BE    WRITE100                       YES
         CLI   HTTP_FUNCTION,$HTTP_WRITE_RAW  WRITE 'RAW'?
         BE    WRITE200                       YES
         MVI   HTTP_RETURN_CODE,$HTTP_OTHER
         MVC   HTTP_MESSAGE_1(MSG01_L),MSG01
         B     EXIT0000                       EXIT
* ------------------------------------------------------------------- *
*        Initialize environment                                       *
* ------------------------------------------------------------------- *
INIT0010 DS    0H
         GETMAIN RU,                          GETMAIN WORK AREA        +
               LV=WORKAREAL,                                           +
               LOC=BELOW
         ST    R1,HTTP_WORKAREA               CHAIN PREVIOUS SAVE AREA
         ST    R13,4(R1)                      CHAIN PREVIOUS TO OURS
         ST    R1,8(R13)
         LR    R13,R1                         SET SAVE AREA ADDRESS
         MVI   WORK_FLAGS,0
         GETMAIN RU,                                                   +
               LV=502*TRACE_ENTRYL,                                    +
               LOC=BELOW
         ST    R1,WORK_TRACE_AREA
         SRL   R1,5
         SLL   R1,5
         LA    R1,TRACE_ENTRYL(R1)
         ST    R1,WORK_TRACE_1ST
         MVC   WORK_TRACE_1ST-4(4),=C'1STX'
         ST    R1,WORK_TRACE_CURR
         MVC   WORK_TRACE_CURR-4(4),=C'CURR'
         AH    R1,=Y(499*TRACE_ENTRYL)
         ST    R1,WORK_TRACE_LAST
         MVC   WORK_TRACE_LAST-4(4),=C'LAST'
         SUBTRACE ID=INIT,                                             +
               RDATA1=R13
* ------------------------------------------------------------------- *
*        Initialize TCP/IP environment                                *
* ------------------------------------------------------------------- *
         XC    WORK_TCPIP_TASK(TIELENTH),WORK_TCPIP_TASK
         MVC   WORK_FUNCTION,=CL16'INITAPI'
         SUBTRACE ID=INITAPI
         EZASMI TYPE=INITAPI,                                          +
               MAXSNO=WORK_TCPIP_MAX_SOCKET,                           +
               SUBTASK=C_HTTPSUB,                                      +
               RETCODE=WORK_TCPIP_RETURN_CODE,                         +
               ERRNO=WORK_TCPIP_ERROR_NUMBER,                          +
               ASYNC='NO',                                             +
               TASK=WORK_TCPIP_TASK
         MVC   HTTP_TCPIP_RC,WORK_TCPIP_RETURN_CODE
         MVC   HTTP_TCPIP_ERROR,WORK_TCPIP_ERROR_NUMBER
         SUBTRACE ID=RC,                                               +
               DATA1=WORK_TCPIP_RETURN_CODE,                           +
               DATA2=WORK_TCPIP_ERROR_NUMBER
         TM    WORK_TCPIP_RETURN_CODE,X'80'
         BO    ERR0010
* ------------------------------------------------------------------- *
*        Create socket                                                *
* ------------------------------------------------------------------- *
         SUBTRACE ID=SOCKET
         MVC   WORK_FUNCTION,=CL16'SOCKET'
         EZASMI TYPE=SOCKET,                                           +
               AF='INET',                                              +
               SOCTYPE='STREAM',                                       +
               RETCODE=WORK_TCPIP_RETURN_CODE,                         +
               ERRNO=WORK_TCPIP_ERROR_NUMBER,                          +
               TASK=WORK_TCPIP_TASK
         MVC   HTTP_TCPIP_RC,WORK_TCPIP_RETURN_CODE
         MVC   HTTP_TCPIP_ERROR,WORK_TCPIP_ERROR_NUMBER
         SUBTRACE ID=RC,                                               +
               DATA1=WORK_TCPIP_RETURN_CODE,                           +
               DATA2=WORK_TCPIP_ERROR_NUMBER
         TM    WORK_TCPIP_RETURN_CODE,X'80'
         BO    ERR0010
         MVC   WORK_SOCKET,WORK_TCPIP_RETURN_CODE+2
* ------------------------------------------------------------------- *
*        BIND to a port                                               *
* ------------------------------------------------------------------- *
         MVC   WORK_FUNCTION,=CL16'BIND'
         XC    WORK_PORT,WORK_PORT
         XC    WORK_PEER(WORK_PEER_L),WORK_PEER
         MVC   WORK_PEER_AF,=X'0002'          SET ADDRESS FAMILY
         SUBTRACE ID=BIND,                                             +
               DATA1=WORK_PEER+0,                                      +
               DATA2=WORK_PEER+8
         EZASMI TYPE=BIND,                                             +
               S=WORK_SOCKET,                                          +
               NAME=WORK_PEER,                                         +
               RETCODE=WORK_TCPIP_RETURN_CODE,                         +
               ERRNO=WORK_TCPIP_ERROR_NUMBER,                          +
               TASK=WORK_TCPIP_TASK
         MVC   HTTP_TCPIP_RC,WORK_TCPIP_RETURN_CODE
         MVC   HTTP_TCPIP_ERROR,WORK_TCPIP_ERROR_NUMBER
         SUBTRACE ID=RC,                                               +
               DATA1=WORK_TCPIP_RETURN_CODE,                           +
               DATA2=WORK_TCPIP_ERROR_NUMBER
         TM    WORK_TCPIP_RETURN_CODE,X'80'
         BO    ERR0010
* ------------------------------------------------------------------- *
*        Get our CLIENT ID                                            *
* ------------------------------------------------------------------- *
         SUBTRACE ID=GETID
         MVC   WORK_FUNCTION,=CL16'GETCLIENTID'
         EZASMI TYPE=GETCLIENTID,                                      +
               CLIENT=WORK_OUR_CLIENT,                                 +
               RETCODE=WORK_TCPIP_RETURN_CODE,                         +
               ERRNO=WORK_TCPIP_ERROR_NUMBER,                          +
               TASK=WORK_TCPIP_TASK
         MVC   HTTP_TCPIP_RC,WORK_TCPIP_RETURN_CODE
         MVC   HTTP_TCPIP_ERROR,WORK_TCPIP_ERROR_NUMBER
         SUBTRACE ID=CLIENTID,                                         +
               DATA1=WORK_OUR_CLIENT+0,                                +
               DATA2=WORK_OUR_CLIENT+8,                                +
               DATA3=WORK_OUR_CLIENT+16
         TM    WORK_TCPIP_RETURN_CODE,X'80'
         BO    ERR0010
         B     EXIT0000                       EXIT
* ------------------------------------------------------------------- *
*        CONNECT to HTTPWEB task in MAIN                              *
* ------------------------------------------------------------------- *
CONN0000 DS    0H
         MVC   WORK_FUNCTION,=CL16'CONNECT'
         XC    WORK_PEER(WORK_PEER_L),WORK_PEER
         MVC   WORK_PEER_AF,=X'0002'          SET ADDRESS FAMILY
         MVC   WORK_PEER_PORT,HTTP_PORT       SET PORT NUMBER
         MVC   WORK_PEER_IP,LOCAL_HOST_IP     SET IP ADDRESS
         SUBTRACE ID=CONNECT,                                          +
               DATA1=WORK_SOCKET,                                      +
               DATA2=WORK_PEER+0,                                      +
               DATA3=WORK_PEER+8
         EZASMI TYPE=CONNECT,                                          +
               S=WORK_SOCKET,                                          +
               NAME=WORK_PEER,                                         +
               RETCODE=WORK_TCPIP_RETURN_CODE,                         +
               ERRNO=WORK_TCPIP_ERROR_NUMBER
         MVC   HTTP_TCPIP_RC,WORK_TCPIP_RETURN_CODE
         MVC   HTTP_TCPIP_ERROR,WORK_TCPIP_ERROR_NUMBER
         SUBTRACE ID=RC,                                               +
               DATA1=WORK_TCPIP_RETURN_CODE,                           +
               DATA2=WORK_TCPIP_ERROR_NUMBER
         TM    WORK_TCPIP_RETURN_CODE,X'80'   SUCCESSFUL?
         BO    ERR0010                        NO
* ------------------------------------------------------------------- *
*        Identify ourselves, get MAIN's client ID                     *
* ------------------------------------------------------------------- *
         SUBTRACE ID=IDENTIFY
         MVI   NET_FUNCTION,$NET_IDENTIFY     IDENTIFYING OURSELVES
         MVC   NET_DATA(CLIENTL),WORK_OUR_CLIENT
         LA    R0,NET_PREFIX_LENGTH+CLIENTL
         ST    R0,NET_LENGTH
         BAL   R9,WRITE000                    SEND ID TO HTTPMAIN
         BAL   R9,READ0000                    READ REPLY
         CLI   NET_RETURN_CODE,$NET_OK        SUCCESSFUL?
         BNE   ERR0020                        NO
         MVC   WORK_HTTPWEB_CLIENT(CLIENTL),NET_DATA
         MVI   HTTP_RETURN_CODE,$HTTP_OK      SET RETURN CODE
         SUBTRACE ID=W_CLIENT,                                         +
               DATA1=WORK_HTTPWEB_CLIENT+0,                            +
               DATA2=WORK_HTTPWEB_CLIENT+8
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
*        "Get a URL".                                                 *
*                                                                     *
*        We should have previously defined URLs that we want to       *
*        process by doing "Send URLs".                                *
*                                                                     *
*        This subroutine (and therefore the calling program)          *
*        will be WAITed until the HTTP main address space             *
*        sends us a URL.                                              *
*                                                                     *
* ------------------------------------------------------------------- *
GETURL00 DS    0H
         SUBTRACE ID=GETURL
         MVI   NET_FUNCTION,$NET_GET_URL      SET FUNCTION
         LA    R1,NET_PREFIX_LENGTH           LENGTH TO WRITE
         ST    R1,NET_LENGTH                  SET LENGTH
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         BAL   R9,WRITE000                    ASK FOR A URL
         BAL   R9,READ0000                    RECEIVE THE URL
         CLI   NET_RETURN_CODE,$NET_SHUTDOWN  MAIN SPACE SHUTTING DOWN?
         BE    GETURL10                       YES
         SUBTRACE ID=WEBURL,                                           +
               DATA1=WEBURL,                                           +
               DATA2=URL_TEXT+0,                                       +
               DATA3=URL_TEXT+8
         MVC   HTTP_LENGTH,WEBURL_LENGTH      PASS LENGTH TO CALLER
         LA    R0,WEBURL+WEBURLL+URL_PREFIX_L URL ADDRESS
         ST    R0,HTTP_DATA                   PASS LENGTH TO CALLER
         MVC   HTTP_PARM_LENGTH,WEBURL_PARM_LENGTH
         AH    R0,WEBURL_PARM_DISP
         ST    R0,HTTP_PARM
         MVC   HTTP_URL_ID,WEBURL_ID
* ------------------------------------------------------------------- *
*        Take the socket                                              *
* ------------------------------------------------------------------- *
         MVC   WORK_FUNCTION,=CL16'TAKESOCKET'
         SUBTRACE ID=TAKESOCK,                                         +
               DATA1=WEBURL_ID,                                        +
               DATA2=WORK_HTTPWEB_CLIENT+4,                            +
               DATA3=WORK_HTTPWEB_CLIENT+12
         EZASMI TYPE=TAKESOCKET,              TAKE THE SOCKET          +
               SOCRECV=WEBURL_SOCKET,         .. ID AND SOCKET NBR     +
               CLIENT=WORK_HTTPWEB_CLIENT,                             +
               RETCODE=WORK_TCPIP_RETURN_CODE,                         +
               ERRNO=WORK_TCPIP_ERROR_NUMBER,                          +
               TASK=WORK_TCPIP_TASK
         MVC   HTTP_TCPIP_RC,WORK_TCPIP_RETURN_CODE
         MVC   HTTP_TCPIP_ERROR,WORK_TCPIP_ERROR_NUMBER
         SUBTRACE ID=TAKE_RC,                 TRACE RETURN CODE        +
               DATA1=WORK_TCPIP_RETURN_CODE,                           +
               DATA2=WORK_TCPIP_ERROR_NUMBER
         TM    WORK_TCPIP_RETURN_CODE,X'80'   SUCCESSFUL?
         BO    ERR0010                        NO
         SUBTRACE ID=SOCKET,                                           +
               DATA1=WORK_TCPIP_RETURN_CODE+2
         MVC   HTTP_SOCKET,WORK_TCPIP_RETURN_CODE+2
* ------------------------------------------------------------------- *
*        Inform HTTPSERV the TAKESOCKET is complete                   *
* ------------------------------------------------------------------- *
         LA    R1,NET_PREFIX_LENGTH
         ST    R1,NET_LENGTH
         BAL   R9,WRITE000                    TAKESOCKET IS COMPLETE
         BAL   R9,READ0000
         MVI   HTTP_RETURN_CODE,$HTTP_OK      SET RETURN CODE
         B     EXIT0000                       EXIT
GETURL10 DS    0H
         SUBTRACE ID=GETURL10
         MVI   HTTP_RETURN_CODE,$HTTP_MAIN_SHUTDOWN
         BE    EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
*        Define a URL                                                 *
*                                                                     *
* ------------------------------------------------------------------- *
DEFURL00 DS    0H
         SUBTRACE ID=DEF_PRFX,                                         +
               DATA1=HTTP_DATA,                                        +
               DATA2=HTTP_LENGTH
         XC    WEBURL(WEBURLL),WEBURL         INITIALIZE URL
         MVI   NET_FUNCTION,$NET_DEFINE_PREFIX
         MVI   WEBURL_STATUS,$URL_DEFINE_PREFIX
         B     DEFURL20
DEFURL10 DS    0H
         SUBTRACE ID=DEF_SPEC,                                         +
               DATA1=HTTP_DATA,                                        +
               DATA2=HTTP_LENGTH
         XC    WEBURL(WEBURLL),WEBURL         INITIALIZE URL
         MVI   NET_FUNCTION,$NET_DEFINE_SPECIFIC
         MVI   WEBURL_STATUS,$URL_DEFINE_SPECIFIC
DEFURL20 DS    0H
         MVC   WEBURL_EYE,C_WEBURL
         MVC   URL_EYE,C_URL
         L     R0,HTTP_DATA                   DATA ADDRESS
         L     R1,HTTP_LENGTH                 DATA LENGTH
         LA    R14,URL_TEXT                   I/O AREA DATA ADDRESS
         LR    R15,R1                         SET LENGTH 2 = LENGTH 1
         MVCL  R14,R0                         COPY TO I/O AREA
         L     R1,HTTP_LENGTH                 DATA LENGTH
         ST    R1,WEBURL_LENGTH               SET URL LENGTH
         STH   R1,URL_LENGTH                  SET URL LENGTH
         LA    R1,NET_PREFIX_LENGTH+WEBURLL+URL_PREFIX_L(R1)
         ST    R1,NET_LENGTH                  SET TOTAL LENGTH
         BAL   R9,WRITE000                    SEND TO MAIN
         BAL   R9,READ0000                    RESPONSE FROM MAIN
         CLI   NET_RETURN_CODE,$NET_OK        SUCCESSFUL?
         BNE   ERR0020                        NO
         B     EXIT0000                       AND EXIT
* ------------------------------------------------------------------- *
*                                                                     *
*        Write data to LOG                                            *
*                                                                     *
* ------------------------------------------------------------------- *
LOG0000  DS    0H
         BAL   R8,LOG1000
         B     EXIT0000
LOG1000  DS    0H
         LH    R1,LOG_DATA_LENGTH
         XC    LOG_DATA_LENGTH+2(2),LOG_DATA_LENGTH+2
         LA    R1,NET_PREFIX_LENGTH+LOGBLOKL(R1)
         ST    R1,NET_LENGTH
         SUBTRACE ID=LOG,                                              +
               DATA1=NET_LENGTH,                                       +
               DATA2=NET_DATA,                                         +
               DATA3=NET_DATA+8
         MVI   NET_FUNCTION,$NET_LOG
         BAL   R9,WRITE000
         BAL   R9,READ0000
         MVI   HTTP_RETURN_CODE,$HTTP_OK
         BR    R8
* ------------------------------------------------------------------- *
*                                                                     *
*        Close a socket.                                              *
*                                                                     *
*        A "SHUTDOWN" is issued prior to closing the socket to force  *
*        TCP/IP to flush any data buffered before the socket is       *
*        closed.                                                      *
*                                                                     *
* ------------------------------------------------------------------- *
CLOSE000 DS    0H
         SUBTRACE ID=SHUTDOWN,                                         +
               DATA1=HTTP_SOCKET
         MVC   WORK_FUNCTION,=CL16'SHUTDOWN'
         EZASMI TYPE=SHUTDOWN,                                         +
               HOW=2,                                                  +
               S=HTTP_SOCKET,                                          +
               RETCODE=WORK_TCPIP_RETURN_CODE,                         +
               ERRNO=WORK_TCPIP_ERROR_NUMBER
         MVC   HTTP_TCPIP_RC,WORK_TCPIP_RETURN_CODE
         MVC   HTTP_TCPIP_ERROR,WORK_TCPIP_ERROR_NUMBER
         CLC   H1027,WORK_TCPIP_ERROR_NUMBER+2
         BE    EXIT0000                         YES.. ACCEPTABLE
         TM    WORK_TCPIP_RETURN_CODE,X'80'
         BO    ERR0010
         SUBTRACE ID=CLOSE,                                            +
               DATA1=HTTP_SOCKET
         MVC   WORK_FUNCTION,=CL16'CLOSE SOCKET'
         EZASMI TYPE=CLOSE,                                            +
               S=HTTP_SOCKET,                                          +
               RETCODE=WORK_TCPIP_RETURN_CODE,                         +
               ERRNO=WORK_TCPIP_ERROR_NUMBER
         MVC   HTTP_TCPIP_RC,WORK_TCPIP_RETURN_CODE
         MVC   HTTP_TCPIP_ERROR,WORK_TCPIP_ERROR_NUMBER
         TM    WORK_TCPIP_RETURN_CODE,X'80'
         BO    ERR0020
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
*        READ response from HTTPMAIN                                  *
*                                                                     *
*        Note that we never READ from the WEB user                    *
*                                                                     *
* ------------------------------------------------------------------- *
READ0000 DS    0H
         SUBTRACE ID=READ0000,                                         +
               DATA1=WORK_SOCKET
         MVC   WORK_FUNCTION,=CL16'READ'
         SR    R2,R2
         LA    R3,NETBLOK
         MVC   WORK_LENGTH,=A(URL_BUFFER_SIZE)
READ0010 DS    0H
         SUBTRACE ID=READ0010,                                         +
               RDATA1=R2,                                              +
               RDATA2=R3
         EZASMI TYPE=READ,                                             +
               S=WORK_SOCKET,                                          +
               BUF=(R3),                                               +
               NBYTE=WORK_LENGTH,                                      +
               RETCODE=WORK_TCPIP_RETURN_CODE,                         +
               ERRNO=WORK_TCPIP_ERROR_NUMBER
         MVC   HTTP_TCPIP_RC,WORK_TCPIP_RETURN_CODE
         MVC   HTTP_TCPIP_ERROR,WORK_TCPIP_ERROR_NUMBER
         SUBTRACE ID=RC,                                               +
               DATA1=WORK_TCPIP_RETURN_CODE,                           +
               DATA2=WORK_TCPIP_ERROR_NUMBER
         TM    WORK_TCPIP_RETURN_CODE,X'80'   SUCCESSFUL?
         BO    ERR0010                        NO
         A     R3,WORK_TCPIP_RETURN_CODE      PLUS BYTES READ
         L     R0,WORK_LENGTH                 LENGTH REMAINING
         S     R0,WORK_TCPIP_RETURN_CODE      MINUS BYTES READ
         ST    R0,WORK_LENGTH                 LENGTH REMAINING
         A     R2,WORK_TCPIP_RETURN_CODE      TOTAL BYTES READ
         CLM   R2,15,NET_LENGTH               ALL READ?
         BNE   READ0010                       NO
         BR    R9
* ------------------------------------------------------------------- *
*        Send data to HTTPMAIN                                        *
* ------------------------------------------------------------------- *
WRITE000 DS    0H
         SUBTRACE ID=WRITE000,                                         +
               DATA1=WORK_SOCKET,                                      +
               DATA2=NET_LENGTH,                                       +
               DATA3=NET_DATA
         MVC   WORK_FUNCTION,=CL16'WRITE TO HTTPMAIN'
         EZASMI TYPE=WRITE,                                            +
               S=WORK_SOCKET,                                          +
               NBYTE=NET_LENGTH,                                       +
               BUF=NETBLOK,                                            +
               RETCODE=WORK_TCPIP_RETURN_CODE,                         +
               ERRNO=WORK_TCPIP_ERROR_NUMBER
         MVC   HTTP_TCPIP_RC,WORK_TCPIP_RETURN_CODE
         MVC   HTTP_TCPIP_ERROR,WORK_TCPIP_ERROR_NUMBER
         SUBTRACE ID=RC,                                               +
               DATA1=WORK_TCPIP_RETURN_CODE,                           +
               DATA2=WORK_TCPIP_ERROR_NUMBER
         TM    WORK_TCPIP_RETURN_CODE,X'80'   SUCCESSFUL?
         BO    ERR0010                        NO
         BR    R9
* ------------------------------------------------------------------- *
*                                                                     *
*        Send data to the web user.                                   *
*                                                                     *
*        For some reason IBM and the rest of the computing world      *
*        have to be different.  Text data on MVS is usually stored    *
*        in EBCDIC.  Everyone else uses ASCII.  Still some data       *
*        (like GIF or JPEG files) is just binary data and needs       *
*        to be sent 'as is'.  Therefore there are two types of        *
*        WRITE commands.  One for text, one for binary data.          *
*        Text data will be translated to ASCII before being           *
*        pass to TCP/IP.  The data is translated in the user's        *
*        I/O area!                                                    *
*                                                                     *
* ------------------------------------------------------------------- *
WRITE100 DS    0H
         SUBTRACE ID=WRITE100
         CLI   HTTP_FUNCTION,$HTTP_WRITE_RAW  SEND RAW?
         BE    WRITE110                       YES
         MVC   WORK_FUNCTION,=CL16'WRITE TO WEBUSER'
         L     R2,HTTP_DATA                   DATA'S ADDRESS
         L     R3,HTTP_LENGTH                 DATA'S LENGTH
         SUBTRACE ID=XLATE,                                            +
               RDATA1=R2,                                              +
               RDATA2=R3,                                              +
               DATA3=HTTP_ALET
         LAM   R2,R2,HTTP_ALET                DATA'S ALET
         SAC   512
         ICM   R0,15,XFF
         LA    R1,EBCDIC_TO_ASCII             TRANSLATE TABLE
         TRE   R2,R1                          TRANSLATE TO ASCII
         LAM   R2,R2,F0                       RESET AR
         SAC   0                              TURN OFF AR MODE
WRITE110 DS    0H
         OC    HTTP_TRACE_DCB,HTTP_TRACE_DCB  TRACE DCB PRESENT?
         BZ    WRITE120                       NO
         L     R2,HTTP_TRACE_IOAREA           TRACE I/O AREA
         L     R3,HTTP_LENGTH                 DATA LENGTH
         LA    R3,4(R3)                       PLUS 4
         STCM  R3,3,0(R2)                     SET LENGTH
         LA    R2,4(R2)                       SKIP LENGTH
         L     R3,HTTP_LENGTH                 RESET LENGTH
         L     R14,HTTP_DATA                  DATA ADDRESS
         L     R15,HTTP_LENGTH                SET LENGTH 2
         LAM   R14,R14,HTTP_ALET              SET AR
         SAC   512                            TURN ON AR MODE
         MVCL  R2,R14                         COPY DATA
         SAC   0                              TURN OFF AR MODE
         LAM   R14,R14,F0                     RESET AR
         L     R2,HTTP_TRACE_DCB              DCB ADDRESS
         L     R3,HTTP_TRACE_IOAREA           I/O AREA
         PUT   (R2),(R3)                      WRITE TO TRACE
WRITE120 DS    0H
         L     R2,HTTP_DATA                   DATA'S ADDRESS
         L     R3,HTTP_LENGTH                 LENGTH TO WRITE
WRITE130 DS    0H
         ST    R3,WORK_LENGTH
         SUBTRACE ID=WRITE130,                                         +
               DATA1=HTTP_SOCKET,                                      +
               RDATA2=R2,                                              +
               RDATA3=R3
         EZASMI TYPE=WRITE,                   SEND DATA TO WEB USER    +
               S=HTTP_SOCKET,                                          +
               BUF=(R2),                                               +
               NBYTE=WORK_LENGTH,                                      +
               ALET=HTTP_ALET,                                         +
               RETCODE=WORK_TCPIP_RETURN_CODE,                         +
               ERRNO=WORK_TCPIP_ERROR_NUMBER
         MVC   HTTP_TCPIP_RC,WORK_TCPIP_RETURN_CODE
         MVC   HTTP_TCPIP_ERROR,WORK_TCPIP_ERROR_NUMBER
         SUBTRACE ID=WRITERC,                                          +
               DATA1=WORK_TCPIP_RETURN_CODE,                           +
               DATA2=WORK_TCPIP_ERROR_NUMBER
         TM    WORK_TCPIP_RETURN_CODE,X'80'   SUCCESSFUL?
         BO    ERR0010                        NO
         A     R2,WORK_TCPIP_RETURN_CODE      PLUS DATA SENT
         S     R3,WORK_TCPIP_RETURN_CODE      MINUS DATA SENT
         BH    WRITE130                       KEEP WRITING
         B     EXIT0000
* ------------------------------------------------------------------- *
*        Send data 'as is' to web user                                *
* ------------------------------------------------------------------- *
WRITE200 DS    0H
         MVC   WORK_FUNCTION,=CL16'WRITE AS-IS'
         L     R2,HTTP_DATA                   DATA ADDRESS
         L     R3,HTTP_LENGTH                 DATA LENGTH
WRITE210 DS    0H
         ST    R3,WORK_LENGTH
         SUBTRACE ID=WRITE200,                                         +
               DATA1=HTTP_SOCKET,                                      +
               DATA2=HTTP_DATA,                                        +
               DATA3=HTTP_LENGTH
         EZASMI TYPE=WRITE,                                            +
               S=HTTP_SOCKET,                                          +
               BUF=0(R2),                                              +
               NBYTE=WORK_LENGTH,                                      +
               RETCODE=WORK_TCPIP_RETURN_CODE,                         +
               ERRNO=WORK_TCPIP_ERROR_NUMBER
         MVC   HTTP_TCPIP_RC,WORK_TCPIP_RETURN_CODE
         MVC   HTTP_TCPIP_ERROR,WORK_TCPIP_ERROR_NUMBER
         SUBTRACE ID=RC,                                               +
               DATA1=WORK_TCPIP_RETURN_CODE,                           +
               DATA2=WORK_TCPIP_ERROR_NUMBER
         TM    WORK_TCPIP_RETURN_CODE,X'80'   SUCCESSFUL?
         BO    ERR0010                        NO
         A     R2,WORK_TCPIP_RETURN_CODE      PLUS DATA SENT
         S     R3,WORK_TCPIP_RETURN_CODE      MINUS DATA SENT
         BH    WRITE210                       KEEP WRITING
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
CLEAN000 DS    0H
         SUBTRACE ID=CLEANUP
         MVC   WORK_FUNCTION,=CL16'TERMAPI'
         EZASMI TYPE=TERMAPI,                                          +
               TASK=WORK_TCPIP_TASK
         LTR   R15,R15
         BNZ   ERR0010
         L     R1,WORK_TRACE_AREA
         FREEMAIN RU,                                                  +
               LV=102*TRACE_ENTRYL,                                    +
               A=(1)
         LR    R1,R13
         L     R13,4(R13)
         FREEMAIN RU,                                                  +
               LV=WORKAREAL,                                           +
               A=(1)
         XC    HTTP_WORKAREA,HTTP_WORKAREA
         MVI   HTTP_RETURN_CODE,$HTTP_OK
         B     EXIT0010
* ------------------------------------------------------------------- *
*        Allocate a data set or PDS member  (must be cataloged)       *
* ------------------------------------------------------------------- *
ALLOC000 DS    0H
         SUBTRACE ID=ALLOC,                                            +
               DATA1=HTTP_DD
         LA    R5,WORK_RB
         ST    R5,WORK_RBA
         USING S99RB,R5
         OI    WORK_RBA,X'80'
         XC    WORK_RB(S99RBEND-S99RB),WORK_RB
         MVI   S99VERB,S99VRBAL
         LA    R1,WORK_DD_PARMS
         ST    R1,WORK_TEXT_ADDRESS_1
         LA    R1,WORK_DS_PARMS
         ST    R1,WORK_TEXT_ADDRESS_2
         LA    R1,WORK_DISP_PARMS
         ST    R1,WORK_TEXT_ADDRESS_3
         LA    R1,WORK_MEMBER_PARMS
         ST    R1,WORK_TEXT_ADDRESS_4
         OI    WORK_TEXT_ADDRESS_4,X'80'
         MVC   WORK_DD_PARMS(DAIRPL),DAIRPI
         MVC   WORK_DD_NAME,HTTP_DD
         MVC   WORK_DS_NAME,HTTP_DSN
         MVC   WORK_MEMBER_NAME,HTTP_MEMBER
         CLC   WORK_MEMBER_NAME,BLANKS
         BNE   SVC0000
         OI    WORK_TEXT_ADDRESS_3,X'80'
         B     SVC0000
* ------------------------------------------------------------------- *
*        FREE (deallocate) a DD                                       *
* ------------------------------------------------------------------- *
FREE0000 DS    0H
         SUBTRACE ID=FREE,                                             +
               DATA1=HTTP_DD
         LA    R5,WORK_RB
         ST    R5,WORK_RBA
         OI    WORK_RBA,X'80'
         XC    WORK_RB(S99RBEND-S99RB),WORK_RB
         MVI   S99VERB,S99VRBUN
         LA    R1,WORK_DD_PARMS
         ST    R1,WORK_TEXT_ADDRESS_1
         OI    WORK_TEXT_ADDRESS_1,X'80'
         MVC   WORK_DD_NAME,HTTP_DD
* ------------------------------------------------------------------- *
*        Issue DYNALLOC SVC                                           *
* ------------------------------------------------------------------- *
SVC0000  DS    0H
         MVI   S99RBLN,S99RBEND-S99RB
         LA    R1,WORK_TEXT_ADDRESS_1
         ST    R1,S99TXTPP
         LA    R1,WORK_RBA
         SUBTRACE ID=SVC99,                                            +
               RDATA1=R1
         SVC   99
         SUBTRACE ID=SVC99RC,                                          +
               RDATA1=R15
         STCM  R15,3,WORK_DWORD
         MVC   HTTP_MESSAGE_1(MSG02L),MSG02
         UNPK  HTTP_MESSAGE_1+(MSG02_R15-MSG02)(5),WORK_DWORD+3(3)
         TR    HTTP_MESSAGE_1+(MSG02_R15-MSG02)(4),HEXCHAR
         MVI   HTTP_MESSAGE_1+(MSG02_R15-MSG02)+4,C' '
         UNPK  HTTP_MESSAGE_1+(MSG02_ERR-MSG02)(5),S99ERROR(3)
         TR    HTTP_MESSAGE_1+(MSG02_ERR-MSG02)(4),HEXCHAR
         MVI   HTTP_MESSAGE_1+(MSG02_ERR-MSG02)+4,C' '
         UNPK  HTTP_MESSAGE_1+(MSG02_INFO-MSG02)(5),S99INFO(3)
         TR    HTTP_MESSAGE_1+(MSG02_INFO-MSG02)(4),HEXCHAR
         MVI   HTTP_MESSAGE_1+(MSG02_INFO-MSG02)+4,C' '
         LTR   R15,R15
         BNZ   SVC0010
         MVI   HTTP_RETURN_CODE,$HTTP_OK
         B     EXIT0000
SVC0010  DS    0H
         MVI   HTTP_RETURN_CODE,$HTTP_OTHER
* ------------------------------------------------------------------- *
*        EXIT                                                         *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         L     R1,4(,R13)
         L     R1,12(,R1)
         SUBTRACE ID=EXIT0000,                                         +
               RDATA1=R1
         L     R13,4(,R13)
EXIT0010 DS    0H
         LM    R14,R12,12(R13)                RESTORE  REGISTERS        ASE01680
         SR    R15,R15                        RETURN CODE               ASE01690
         BR    R14                            RETURN TO CALLER          ASE01700
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0010  DS    0H
         MVC   HTTP_MESSAGE_1(MSG03_L),MSG03
         MVC   HTTP_MESSAGE_1+(MSG03_FUNCTION-MSG03)(L'MSG03_FUNCTION),+
               WORK_FUNCTION
         L     R1,WORK_TCPIP_ERROR_NUMBER
         CVD   R1,WORK_DWORD
         ED    HTTP_MESSAGE_1+(MSG03_ERROR_NUMBER-MSG03)(L'MSG03_ERROR_+
               NUMBER),WORK_DWORD+5
         MVI   HTTP_RETURN_CODE,$HTTP_TCPIP_ERROR
         B     EXIT0000
ERR0020  DS    0H
         DC    H'0'
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
TRACE000 DS    0H
         ST    R15,WORK_TRACE_R15
         L     R15,WORK_TRACE_CURR
         C     R15,WORK_TRACE_LAST
         BE    TRACE010
         LA    R15,TRACE_ENTRYL(R15)
         B     TRACE020
TRACE010 DS    0H
         L     R15,WORK_TRACE_1ST
TRACE020 DS    0H
         ST    R15,WORK_TRACE_CURR
         USING TRACE_ENTRY,R15
         MVC   TRACE_ID,0(R14)
         MVC   TRACE_DATA1,WORK_TRACE_DATA1
         MVC   TRACE_DATA2,WORK_TRACE_DATA2
         MVC   TRACE_DATA3,WORK_TRACE_DATA3
         XC    WORK_TRACE_DATA1,WORK_TRACE_DATA1
         XC    WORK_TRACE_DATA2,WORK_TRACE_DATA2
         XC    WORK_TRACE_DATA3,WORK_TRACE_DATA3
         L     R15,WORK_TRACE_R15
         B     8(R14)
* ------------------------------------------------------------------ *
*                                                                    *
*        CONSTANTS                                                   *
*                                                                    *
* ------------------------------------------------------------------ *
         LTORG
H1027    DC    H'1027'
F0       DC    F'0'
BLANKS   DC    CL80' '

HEXCHAR  EQU   *-C'0'
         DC    C'0123456789ABCDEF'

XFF      DC    4X'FF'

C_HTTPSUB      DC   CL8'HTTPSUB'
C_URL          DC   CL8'URL    '
C_WEBURL       DC   CL8'WEBURL '
LOCAL_HOST_IP  DC   AL1(127),AL1(0),AL1(0),AL1(1)
         ETOA
* ------------------------------------------------------------------ *
*                                                                    *
* ------------------------------------------------------------------ *
DAIRPI    DC   AL2(DALDDNAM)         DDNAME PARMS
          DC   AL2(1)
          DC   AL2(8)
          DC   CL8' '                DDNAME
          DC   AL2(DALDSNAM)         DATASET NAME PARMS
          DC   AL2(1)
          DC   AL2(44)
          DC   CL44' '               DATASET NAME
          DC   AL2(DALSTATS)         INITIAL DISPOSITION PARMS
          DC   AL2(1)
          DC   AL2(1)
          DC   X'08'                 INITIAL DISPOSITION (SHR)
          DC   AL2(DALMEMBR)         MEMBER NAME PARMS
          DC   AL2(1)
          DC   AL2(8)
          DC   CL8' '                DATASET NAME
DAIRPL    EQU  *-DAIRPI
* ------------------------------------------------------------------ *
*                                                                    *
* ------------------------------------------------------------------ *
MSG01              DS   0C
                   DC   C'HTTPSUB01E '
                   DC   C'Invalid function code'
MSG01_L            EQU  *-MSG01
MSG02              DS   0C
                   DC   C'HTTPSUB02I  SVC 99  R15='
MSG02_R15          DC   CL4' '
                   DC   C'   ERR='
MSG02_ERR          DC   CL4' '
                   DC   C'   INFO='
MSG02_INFO         DC   CL4' '
                   DC   C' '
MSG02L             EQU  *-MSG02
MSG03              DS   0C
                   DC   C'HTTPSUB03E '
                   DC   C'TCPIP FUNCTION: '
MSG03_FUNCTION     DC   CL16' '
                   DC   C' failed. Error number'
MSG03_ERROR_NUMBER DC   X'402020202120'
MSG03_L            EQU  *-MSG03
* ------------------------------------------------------------------ *
*                                                                    *
*        WORK AREAS                                                  *
*                                                                    *
* ------------------------------------------------------------------ *
WORKAREA                DSECT
                        DS    18F

WORK_DWORD              DS    D

WORK_RBA                DS    A
WORK_RB                 DS    0F,(S99RBEND-S99RB)X

WORK_TEXT_ADDRESS_1     DS    A
WORK_TEXT_ADDRESS_2     DS    A
WORK_TEXT_ADDRESS_3     DS    A
WORK_TEXT_ADDRESS_4     DS    A
WORK_TEXT_ADDRESS_5     DS    A

WORK_LENGTH             DS    F

WORK_DD_PARMS           DS    AL2,AL2,AL2
WORK_DD_NAME            DS    CL8
WORK_DS_PARMS           DS    AL2,AL2,AL2
WORK_DS_NAME            DS    CL44
WORK_DISP_PARMS         DS    AL2,AL2,AL2
WORK_DISP               DS    X
WORK_MEMBER_PARMS       DS    AL2,AL2,AL2
WORK_MEMBER_NAME        DS    CL8

WORK_TRACE_R15          DS    F
WORK_ID                 DS    CL8
WORK_TRACE_AREA         DS    F
                        DS    CL4
WORK_TRACE_1ST          DS    F
                        DS    CL4
WORK_TRACE_LAST         DS    F
                        DS    CL4
WORK_TRACE_CURR         DS    F
WORK_TRACE_DATA1        DS    CL8
WORK_TRACE_DATA2        DS    CL8
WORK_TRACE_DATA3        DS    CL8

WORK_PEER               DS    0F
WORK_PEER_AF            DS    XL2
WORK_PEER_PORT          DS    XL2
WORK_PEER_IP            DS    XL4
WORK_PEER_RESERVED      DS    XL8
WORK_PEER_L             EQU   *-WORK_PEER

WORK_PORT               DS    XL2
WORK_SOCKET             DS    XL2

                        CLIENT PREFIX=WORK_OUR
CLIENTL                 EQU   *-WORK_OUR_CLIENT

                        CLIENT PREFIX=WORK_HTTPWEB

                        CLIENT PREFIX=WORK_PEER

WORK_TCPIP_RETURN_CODE  DS    F
WORK_TCPIP_ERROR_NUMBER DS    F
WORK_TCPIP_MAX_SOCKET   DS    F
WORK_TCPIP_TASK         EZASMI TYPE=TASK,STORAGE=CSECT

WORK_FUNCTION           DS    CL16

WORK_FLAGS              DS    X
$WORK_URL_LOGGING       EQU   X'80'

                        NETBLOK DSECT=NO
                        DS      CL32768
                        ORG     NET_DATA
                        WEBURL  DSECT=NO
                        URL     DSECT=NO
                        ORG     NET_DATA
                        LOGBLOK DSECT=NO
                        ORG
WORKAREAL               EQU     *-WORKAREA
* ------------------------------------------------------------------ *
*                                                                    *
* ------------------------------------------------------------------ *
TRACE_ENTRY     DSECT
TRACE_ID        DS        CL8
TRACE_DATA1     DS        CL8
TRACE_DATA2     DS        CL8
TRACE_DATA3     DS        CL8
TRACE_ENTRYL    EQU       *-TRACE_ENTRY
* ------------------------------------------------------------------ *
*                                                                    *
* ------------------------------------------------------------------ *
                SUBPARM   TYPE=DSECT
* ------------------------------------------------------------------ *
*                                                                    *
* ------------------------------------------------------------------ *
                IEFZB4D0
                IEFZB4D2
* ------------------------------------------------------------------ *
*                                                                    *
* ------------------------------------------------------------------ *
                COPY     REGEQU
$LF             EQU      X'0A'
URL_BUFFER_SIZE EQU      32000
                END      HTTPSUB
