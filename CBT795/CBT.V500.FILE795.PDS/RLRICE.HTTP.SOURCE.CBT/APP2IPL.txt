         MACRO
         PDE   &A
         DC    AL2(IPA&A-IPA)        DISP TO PDE DATA
         DC    CL8'&A'               DESCRIPTION
         MEND
* ------------------------------------------------------------------- *
*                                                                     *
*   This application displays IPL info                                *
*                                                                     *
* ------------------------------------------------------------------- *
APP2IPL  CSECT
APP2IPL  AMODE 31
APP2IPL  RMODE ANY
         STM   R14,R12,12(R13)       SAVE REGISTERS
         LR    R12,R15               SET BASE REGISTER
         USING APP2IPL,R12           DEFINE PROGRAM BASE
         B     INIT0000
MODID    DC    CL8'APP2IPL'
         DC    CL8'&SYSDATE'
         DC    CL6'&SYSTIME'
INIT0000 DS    0H
         LR    R11,R1                COPY PARM BLOCK ADDRESS
         USING HTTP_PARMS,R11
         GETMAIN RU,                                                   +
               LV=WORKL,                                               +
               LOC=ANY
         ST    R1,8(R13)
         ST    R13,4(R1)
         LR    R13,R1
         USING WORK,R13
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
INIT0010 DS    0H
         XC    HTTP_ALET,HTTP_ALET
         MVI   HTTP_FUNCTION,$HTTP_WRITE
         L     R10,CVTPTR                CVT ADDRESS
         USING CVT,R10
         L     R9,CVTECVT                ECVT ADDRESS
         USING ECVT,R9
         L     R8,ECVTIPA                IPA ADDRESS
         USING IPA,R8

         MVC   WORK_BUFFER(HTML_HEADERL),HTML_HEADER
         LA    R1,WORK_BUFFER
         ST    R1,HTTP_DATA
         LA    R1,HTML_HEADERL
         ST    R1,HTTP_LENGTH
         LA    R1,HTTP_PARMS
         L     R15,HTTP_HTTPSUB
         BALR  R14,R15

         MVC   WORK_BUFFER(SYSTEM_HEADERL),SYSTEM_HEADER
         MVC   WORK_BUFFER+(SYSTEM_NAME-SYSTEM_HEADER)(8),CVTSNAME
         LA    R1,SYSTEM_HEADERL
         ST    R1,HTTP_LENGTH
         LA    R1,HTTP_PARMS
         L     R15,HTTP_HTTPSUB
         BALR  R14,R15

         LA    R1,MSG_01_L
         ST    R1,HTTP_LENGTH
         MVC   WORK_BUFFER(MSG_01_L),MSG_01
BUFF     USING MSG_01,WORK_BUFFER

         MVC   BUFF.MSG_01_NAME(L'C_HARDWARE_NAME),C_HARDWARE_NAME
         MVC   BUFF.MSG_01_DATA(L'ECVTHDNM),ECVTHDNM
         BAL   R5,SEND0000

         CLC   ECVTLPNM,BLANKS
         BE    IPL0010
         MVC   BUFF.MSG_01_NAME(L'C_LPAR_NAME),C_LPAR_NAME
         MVC   BUFF.MSG_01_DATA(L'ECVTLPNM),ECVTLPNM
         BAL   R5,SEND0000

IPL0010  DS    0H
         CLC   ECVTVMNM,BLANKS
         BE    IPL0020
         MVC   BUFF.MSG_01_NAME(L'C_VM_NAME),C_VM_NAME
         MVC   BUFF.MSG_01_DATA(L'ECVTVMNM),ECVTVMNM
         BAL   R5,SEND0000

IPL0020  DS    0H
         MVC   BUFF.MSG_01_NAME(L'C_PRODUCT_OWNER),C_PRODUCT_OWNER
         MVC   BUFF.MSG_01_DATA(L'ECVTPOWN),ECVTPOWN
         BAL   R5,SEND0000

         MVC   BUFF.MSG_01_NAME(L'C_PRODUCT_NAME),C_PRODUCT_NAME
         MVC   BUFF.MSG_01_DATA(8),ECVTPNAM
         MVC   BUFF.MSG_01_DATA+10(2),ECVTPVER
         MVI   BUFF.MSG_01_DATA+12,C'.'
         MVC   BUFF.MSG_01_DATA+13(2),ECVTPREL
         MVI   BUFF.MSG_01_DATA+15,C'.'
         MVC   BUFF.MSG_01_DATA+16(2),ECVTPMOD
         BAL   R5,SEND0000

         MVC   BUFF.MSG_01_NAME(L'C_IPLPARM),C_IPLPARM
         MVC   BUFF.MSG_01_DATA(L'IPALPARM),IPALPARM
         BAL   R5,SEND0000

         MVC   BUFF.MSG_01_NAME(L'C_IODF_ADDRESS),C_IODF_ADDRESS
         MVC   BUFF.MSG_01_DATA(L'IPAIODFU),IPAIODFU
         BAL   R5,SEND0000

         MVC   BUFF.MSG_01_NAME(L'C_NUCLEUS_ID),C_NUCLEUS_ID
         MVC   BUFF.MSG_01_DATA(L'IPANUCID),IPANUCID
         BAL   R5,SEND0000

         MVC   BUFF.MSG_01_NAME(L'C_IPLPARM_DSN),C_IPLPARM_DSN
         MVC   BUFF.MSG_01_DATA(L'IPALPDSN),IPALPDSN
         BAL   R5,SEND0000

         MVC   BUFF.MSG_01_NAME(L'C_IPLPARM_UNIT),C_IPLPARM_UNIT
         MVC   BUFF.MSG_01_DATA(L'IPALPDDV),IPALPDDV
         BAL   R5,SEND0000

         MVC   BUFF.MSG_01_NAME(L'C_IODF_DSN_SUFFIX),C_IODF_DSN_SUFFIX
         MVC   BUFF.MSG_01_DATA(L'IPAIOSUF),IPAIOSUF
         BAL   R5,SEND0000

         MVC   BUFF.MSG_01_NAME(L'C_IODF_DSN_HLQ),C_IODF_DSN_HLQ
         MVC   BUFF.MSG_01_DATA(L'IPAIOHLQ),IPAIOHLQ
         BAL   R5,SEND0000

         MVC   BUFF.MSG_01_NAME(L'C_MASTER_DSN),C_MASTER_DSN
         MVC   BUFF.MSG_01_DATA(L'IPASCDSN),IPASCDSN
         BAL   R5,SEND0000

         MVC   BUFF.MSG_01_NAME(L'C_MASTER_VOL),C_MASTER_VOL
         MVC   BUFF.MSG_01_DATA(L'IPASCVOL),IPASCVOL
         BAL   R5,SEND0000

         MVC   BUFF.MSG_01_NAME(L'C_OS_CONFIG_ID),C_OS_CONFIG_ID
         MVC   BUFF.MSG_01_DATA(L'IPAIOCFG),IPAIOCFG
         BAL   R5,SEND0000

         MVC   BUFF.MSG_01_NAME(L'C_EDT_ID),C_EDT_ID
         MVC   BUFF.MSG_01_DATA(L'IPAIOEDT),IPAIOEDT
         BAL   R5,SEND0000

         MVC   BUFF.MSG_01_NAME(L'C_SYSPLEX),C_SYSPLEX
         MVC   BUFF.MSG_01_DATA(L'IPASXNAM),IPASXNAM
         BAL   R5,SEND0000

         MVC   BUFF.MSG_01_NAME(L'C_PARMLIB_DSN),C_PARMLIB_DSN
         MVC   BUFF.MSG_01_DATA(L'IPAPLDSN),IPAPLDSN
         BAL   R5,SEND0000

         MVC   BUFF.MSG_01_NAME(L'C_PARMLIB_VOLSER),C_PARMLIB_VOLSER
         MVC   BUFF.MSG_01_DATA(L'IPAPLVOL),IPAPLVOL
         BAL   R5,SEND0000

         MVC   BUFF.MSG_01_NAME(L'C_MTLSHARE),C_MTLSHARE
         MVC   BUFF.MSG_01_DATA(L'IPAMTLSH),IPAMTLSH
         BAL   R5,SEND0000

         MVC   BUFF.MSG_01_NAME(L'C_ARCH_LEVEL),C_ARCH_LEVEL
         MVC   BUFF.MSG_01_DATA(L'IPAARCHL),IPAARCHL
         BAL   R5,SEND0000

         MVC   BUFF.MSG_01_NAME(L'C_NUC_LIST),C_NUC_LIST
         MVC   BUFF.MSG_01_DATA(L'IPANUCL),IPANUCL
         BAL   R5,SEND0000

         MVC   BUFF.MSG_01_NAME(L'C_NUC_SUFFIX),C_NUC_SUFFIX
         MVC   BUFF.MSG_01_DATA(L'IPANLID),IPANLID
         BAL   R5,SEND0000

* ------------------------------------------------------------------- *
*        Process Parameter descriptors                                *
* ------------------------------------------------------------------- *
         LA    R7,PDE_TABLE
         USING PDE_DSECT,R7
PDE0010  DS    0H
         SR    R6,R6
         ICM   R6,3,PDE_DISP             DISP INTO IPA
         AR    R6,R8                     PLUS BASE
         USING IPAPDE,R6
         MVC   BUFF.MSG_01_NAME(L'PDE_NAME),PDE_NAME
         SR    R1,R1
         ICM   R1,3,IPAPDESL             PARAMETER LENGTH
         BZ    PDE0050                   ZERO..
         CH    R1,=Y(L'MSG_01_DATA-10)
         BNH   PDE0020
         LH    R1,=Y(L'MSG_01_DATA-10)
PDE0020  DS    0H
         BCTR  R1,0
         L     R2,IPAPDESA               PARAMETER ADDRESS
         EX    R1,PDE_MVC                COPY DATA
         CLI   IPAPDEDO+1,0              DEFAULT?
         BE    PDE0030                   YES
         CLI   IPAPDEDO+1,X'FF'          OPERATOR SUPPLIED?
         BE    PDE0040                   YES
         MVC   BUFF.MSG_01_DATA(2),IPAPDESS COPY IEASYS SUFFIX
         B     PDE0050
PDE0030  DS    0H
         MVC   BUFF.MSG_01_DATA(L'C_DEFAULT),C_DEFAULT
         B     PDE0050
PDE0040  DS    0H
         MVC   BUFF.MSG_01_DATA(L'C_OPERATOR),C_OPERATOR
PDE0050  DS    0H
         BAL   R5,SEND0000               SEND
         LA    R7,PDE_L(,R7)             NEXT PDE
         CLI   0(R7),X'FF'               END OF TABLE?
         BNE   PDE0010                   NO
         B     TRAIL000
PDE_MVC  MVC   BUFF.MSG_01_DATA+10(0),0(R2)
* ------------------------------------------------------------------- *
*        Send trailer info                                            *
* ------------------------------------------------------------------- *
TRAIL000 DS    0H
         MVC   WORK_BUFFER(HTML_TRAILERL),HTML_TRAILER
         LA    R1,HTML_TRAILERL
         ST    R1,HTTP_LENGTH
         LA    R1,HTTP_PARMS
         L     R15,HTTP_HTTPSUB
         BALR  R14,R15
         B     EXIT0000

* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
SEND0000 DS    0H
         LA    R1,HTTP_PARMS
         L     R15,HTTP_HTTPSUB
         BALR  R14,R15
         MVC   WORK_BUFFER(MSG_01_L),MSG_01
         BR    R5

* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         LR    R1,R13                  COPY WORK AREA ADDRESS
         L     R13,4(R13)              RESTORE CALLER'S R13
         FREEMAIN RU,                  FREE THE WORK AREA              +
               A=(R1),                                                 +
               LV=WORKL
         L     R14,12(R13)             RESTORE R14
         SR    R15,R15
         LM    R0,R12,20(R13)
         BR    R14
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *

BLANKS              DC  CL80' '
HEXCHAR             EQU *-C'0'
                    DC  C'0123456789ABCDEF'

HTML_HEADER         DS  0C
                    DC  C'<HTML>'
                    DC  AL1($LF)
                    DC  C'<BODY>'
                    DC  AL1($LF)
                    DC  C'<CENTER>'
                    DC  AL1($LF)
HTML_HEADERL        EQU *-HTML_HEADER
SYSTEM_HEADER       DS  0C
                    DC  C'<H1>IPLINFO FOR '
SYSTEM_NAME         DC  CL8' '
                    DC  C'</H1>'
                    DC  AL1($LF)
                    DC  C'<PRE>'
                    DC  AL1($LF)
SYSTEM_HEADERL      EQU *-SYSTEM_HEADER

HTML_TRAILER        DS  0C
                    DC  C'</PRE>'
                    DC  AL1($LF)
                    DC  C'</CENTER>'
                    DC  AL1($LF)
                    DC  C'</BODY>'
                    DC  AL1($LF)
                    DC  C'</HTML>'
                    DC  AL1($LF)
HTML_TRAILERL       EQU *-HTML_TRAILER

C_HARDWARE_NAME     DC  C'HARWARE NAME'
C_LPAR_NAME         DC  C'LPAR NAME'
C_VM_NAME           DC  C'VM user id'
C_PRODUCT_OWNER     DC  C'Product owner'
C_PRODUCT_NAME      DC  C'Product name'
C_IPLPARM           DC  C'IPL PARM'
C_IODF_ADDRESS      DC  C'IODF unit nbr'
C_NUCLEUS_ID        DC  C'Nucleus ID'
C_IPLPARM_DSN       DC  C'IPLPARM DSN'
C_IPLPARM_UNIT      DC  C'IPLPARM unit nbr'
C_IODF_DSN_SUFFIX   DC  C'IODF DSN suffix'
C_IODF_DSN_HLQ      DC  C'IODF DSN HLQ'
C_OS_CONFIG_ID      DC  C'OS config ID'
C_MASTER_DSN        DC  C'Master catalog DSN'
C_MASTER_VOL        DC  C'Master catalog VOL'
C_EDT_ID            DC  C'EDT ID'
C_SYSPLEX           DC  C'Sysplex name'
C_PARMLIB_DSN       DC  C'PARMLIB DSN'
C_PARMLIB_VOLSER    DC  C'PARMLIB VOLSER'
C_MTLSHARE          DC  C'MTL share'
C_ARCH_LEVEL        DC  C'Architecture level'
C_NUC_LIST          DC  C'NUC LIST info'
C_NUC_SUFFIX        DC  C'NUCLEUS suffix'
C_DEFAULT           DC  C'Default'
C_OPERATOR          DC  C'Operator'

PDE_TABLE           DS  0C
                    PDE ALLOC
                    PDE APF
                    PDE APG
                    PDE BLDL
                    PDE BLDLF
                    PDE CLOCK
                    PDE CLPA
                    PDE CMB
                    PDE CMD
                    PDE CON
                    PDE CONT
                    PDE COUPL
                    PDE CPQE
                    PDE CSA
                    PDE CSCBL
                    PDE CVIO
                    PDE DEVSU
                    PDE DIAG
                    PDE DUMP
                    PDE DUPLE
                    PDE EXIT
                    PDE FIX
                    PDE GRS
                    PDE GRSCN
                    PDE GRSRN
                    PDE ICS
                    PDE IOS
                    PDE IPS
                    PDE LNK
                    PDE LNKAU
                    PDE LOGCL
                    PDE LOGLM
                    PDE LOGRE
                    PDE LPA
                    PDE MAXCA
                    PDE MAXUS
                    PDE MLPA
                    PDE MSTRJ
                    PDE NONVI
                    PDE NSYSL
                    PDE NUCMA
                    PDE OMVS
                    PDE OPI
                    PDE OPT
                    PDE PAGEO
                    PDE PAGEP
                    PDE PAGNU
                    PDE PAGTO
                    PDE PAK
                    PDE PLEXC
                    PDE PRODP
                    PDE PROG
                    PDE PURGE
                    PDE RDE
                    PDE REAL
                    PDE RER
                    PDE RSU
                    PDE RSVNO
                    PDE RSVST
                    PDE SCH
                    PDE SMF
                    PDE SMS
                    PDE SQA
                    PDE SSN
                    PDE SVC
                    PDE SWAP
                    PDE SYSNA
                    PDE SYSP
                    PDE VAL
                    PDE VIODS
                    PDE VRREG
                    PDE RTLSP
                    PDE UNIP
                    PDE ILML
                    PDE ILMOD
                    PDE TSO
                    PDE LIC
                    PDE HVSHARE
                    PDE ILM
                    PDE DRMOD
                    PDE CEE
                    PDE PRCPU
                    DC  X'FF'

                    LTORG

MSG_01              DS  0C
MSG_01_NAME         DC  CL20' '
                    DC  CL3' '
MSG_01_DATA         DC  CL54' '
                    DC  AL1($LF)
MSG_01_L            EQU *-MSG_01
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
WORK                DSECT
                    DS    18F
WORK_DWORD          DS    D
WORK_BUFFER         DS    CL100
WORKL               EQU   *-WORK
                    SUBPARM  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
PDE_DSECT           DSECT
PDE_DISP            DS    AL2
PDE_NAME            DS    CL8
PDE_L               EQU   *-PDE_DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                    CVT      DSECT=YES,LIST=NO
                    IHAECVT  DSECT=YES,LIST=NO
                    IHAIPA   DSECT=YES,LIST=NO,IPAPDE=YES
         COPY REGEQU
$LF      EQU  X'0A'
         END  APP2IPL
