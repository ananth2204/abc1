*---------------------------------------------------------------------*
*                                                                     *
*  Module name: HTTPDAIR                                              *
*                                                                     *
*   Interface with MVS's dynamic allocation interface routine (DAIR)  *
*   to allocate or de-allocate data sets.                             *
*                                                                     *
*   The interface block is DAIRBLOK.                                  *
*                                                                     *
* ------------------------------------------------------------------- *
HTTPDAIR CSECT
HTTPDAIR AMODE 31
HTTPDAIR RMODE ANY
         USING HTTPDAIR,R12                   DEFINE BASE
         STM   R14,R12,12(R13)                CHAIN REGISTERS
         LR    R12,R15                        COPY ENTRY POINT ADDRESS
         B     DAIR0000
         DC    CL8'HTTPDAIR'
         DC    CL8'&SYSDATE'
         DC    CL6'&SYSTIME'
DAIR0000 DS    0H
         LR    R10,R1                         COPY PARM BLOCK ADDRESS
         USING DAIRBLOK,R10                   DEFINE BASE
         GETMAIN RU,                          STORAGE FOR WORK AREA    +
               LV=WKL,                        .. ACQUIRE WORK AREA     +
               LOC=ANY                        .. FROM ANY AREA
         ST    R13,4(R1)                      CHAIN CALLER'S AREA
         ST    R1,8(R13)                      CHAIN OUR AREA
         LR    R13,R1                         COPY WORK AREA ADDRESS
         USING WK,R13                         DEFINE BASE
         USING S99RB,R2                       DEFINE BASE
         LA    R2,WKRB                        REQUEST BLOCK'S ADDRESS
         ST    R2,WKRBA                       SET POINTER
         OI    WKRBA,X'80'                    SET END OF LIST
         MVC   WKDDP(DAIRPL),DAIRPI           INITIALIZE PARAMETERS
         XC    WKRB(S99RBEND-S99RB),WKRB
         CLI   DAIRCMD,$DAIRALE               EXISTING DATA SET?
         BE    DAIR0010                       YES
         CLI   DAIRCMD,$DAIRALN               NEW DATA SET?
         BE    DAIR0010                       YES
         CLI   DAIRCMD,$DAIRALS               SNAP (SPUN SYSOUT)
         BE    DAIR0050                       YES
         CLI   DAIRCMD,$DAIRREL               RELEASE?
         BE    DAIR0060                       YES
         CLI   DAIRCMD,$DAIRDEL               DELETE?
         BE    DAIR0070                       YES
         ABEND 1,DUMP,,USER                   SORRY
*---------------------------------------------------------------------*
*                                                                     *
*     Allocate DASD data sets                                         *
*                                                                     *
*---------------------------------------------------------------------*
DAIR0010 DS    0H
         MVI   S99VERB,S99VRBAL               SET VERB
         LA    R15,WKDDP                      DDNAME PARAMETER
         ST    R15,WKTXTP1                    SET PARAMETER 1 ADDRESS
         MVC   WKDD,DAIRDDN                   SET DD NAME
         LA    R15,WKDSNP                     DATASET NAME PARAMETER
         ST    R15,WKTXTP2                    SET PARAMETER 2 ADDRESS
         MVC   WKDSN,DAIRDSN                  COPY DATA SET NAME
         LA    R15,WKDSPIP                    INITIAL DISPOSITION
         ST    R15,WKTXTP3                    SET PARAMETER 3 ADDRESS
         MVI   WKDSPI,X'08'                   SET DISP TO 'SHR'
         CLI   DAIRCMD,$DAIRALN               ALLOCATE NEW?
         BE    DAIR0030                       YES
         CLC   DAIRMEM,BLANKS                 MEMBER NAME?
         BE    DAIR0020                       NO
         LA    R15,WKMEMP                     MEMBER NAME PARAMETERS
         ST    R15,WKTXTP4                    SET PARAMETER 4 ADDRESS
         MVC   WKMEM,DAIRMEM                  COPY MEMBER NAME
         OI    WKTXTP4,X'80'                  FLAG END-OF-LIST
         B     DAIR0040
DAIR0020 DS    0H
         OI    WKTXTP3,X'80'                  FLAG END-OF-LIST
         B     DAIR0040
DAIR0030 DS    0H
         MVI   WKDSPI,X'04'                   SET INITIAL DISP 'NEW'
         LA    R15,WKDSPNP                    NORMAL DISPOSITION
         ST    R15,WKTXTP4                    SET PARAMETER 4 ADDRESS
         MVI   WKDSPN,X'02'                   SET NORMAL DISP 'CATLG'
         LA    R15,WKUNITP                    UNIT PARAMETERS
         ST    R15,WKTXTP5                    SET PARAMETER 5 ADDRESS
         MVC   WKUNIT,DAIRUNIT                SET UNIT NAME
         LA    R15,WKBLKNP                    BLOCK LENGTH PARAMETER
         ST    R15,WKTXTP6                    SET PARAMETER 6 ADDRESS
         MVC   WKBLKN,DAIRBLKN+1              SET BLOCK LENGTH
         LA    R15,WKPRIP                     PRIMARY SPACE PARAMETER
         ST    R15,WKTXTP7                    SET PARAMETER 7 ADDRESS
         MVC   WKPRI,DAIRPRI+1                SET PRIMARY QUANTITY
         LA    R15,WKSECP                     SECONDARY SPACE PARAMETER
         ST    R15,WKTXTP8                    SET PARAMETER 8 ADDRESS
         MVC   WKSEC,DAIRSEC+1                SET SECONDARY QUANTITY
         LA    R15,WKRELP                     RELEASE UNUSED SPACE
         ST    R15,WKTXTP9                    SET PARAMETER 9 ADDRESS
         OI    WKTXTP9,X'80'                  FLAG END OF LIST
DAIR0040 DS    0H
         BAL   R8,DAIR0080                    GO ISSUE SVC 99
         B     EXIT0000                       EXIT
*---------------------------------------------------------------------*
*                                                                     *
*     Allocate SNAP (spun SYSOUT)                                     *
*                                                                     *
*---------------------------------------------------------------------*
DAIR0050 DS    0H
         MVI   S99VERB,S99VRBAL               SET VERB
         MVC   WKDD,DAIRDDN                   SET DDNAME
         MVI   WKPRTC,C'Z'                    SET SYSOUT CLASS
         LA    R1,WKDDP                       DDNAME PARAMETERS
         ST    R1,WKTXTP1                     SET PARAMETER 1 ADDRESS
         LA    R1,WKPRTP                      SYSOUT PARAMETERS
         ST    R1,WKTXTP2                     SET PARAMETER 2 ADDRESS
         LA    R1,WKFREEP                     FREE ON CLOSE PARAMETERS
         ST    R1,WKTXTP3                     SET PARAMETER 3 ADDRESS
         OI    WKTXTP3,X'80'                  SET 'END-OF-LIST'
         BAL   R8,DAIR0080                    ALLOCATE SNAP
         B     EXIT0000                       EXIT
*---------------------------------------------------------------------*
*                                                                     *
*     Release (un-allocate)                                           *
*                                                                     *
*---------------------------------------------------------------------*
DAIR0060 DS    0H
         LA    R2,WKRB                        REQUEST BLOCK'S ADDRESS
         MVC   WKDD,DAIRDDN                   SET DDNAME TO RELEASE
         MVI   S99VERB,S99VRBUN               SET VERB
         LA    R15,WKDDP                      DDNAME PARAMETER
         ST    R15,WKTXTP1                    SET PARAMETER 1 ADDRESS
         OI    WKTXTP1,X'80'                  FLAG END OF LIST
         BAL   R8,DAIR0080                    GO ISSUE SVC 99
         B     EXIT0000                       EXIT
*---------------------------------------------------------------------*
*                                                                     *
*    Delete a dataset.  This is a two step process.                   *
*    The dataset is allocated with 'DISP=(OLD,DELETE)'                *
*    then unallocated.                                                *
*                                                                     *
*---------------------------------------------------------------------*
DAIR0070 DS    0H
         MVC   WKDD,DAIRDDN                   SET DD DDNAME
         MVC   WKDSN,DAIRDSN                  SET DATA SET NAME
         MVI   WKDSPI,X'01'                   SET DISPOSITION 'OLD'
         MVI   WKDSPN,X'04'                   SET DISPOSITION 'DELETE'
         XC    WKRB(S99RBEND-S99RB),WKRB
         MVI   S99VERB,S99VRBAL               SET VERB
         LA    R15,WKDDP                      DDNAME PARAMETER
         ST    R15,WKTXTP1                    SET PARAMETER 1 ADDRESS
         LA    R15,WKDSNP                     DSN PARAMETER
         ST    R15,WKTXTP2                    SET PARAMETER 2 ADDRESS
         LA    R15,WKDSPIP                    INITIAL DISP PARAMETER
         ST    R15,WKTXTP3                    SET PARAMETER 3 ADDRESS
         LA    R15,WKDSPNP                    NORMAL DISP PARAMETER
         ST    R15,WKTXTP4                    SET PARAMETER 4 ADDRESS
         OI    WKTXTP4,X'80'                  FLAG END OF LIST
         BAL   R8,DAIR0080                    GO ISSUE SVC 99
         LTR   R15,R15                        ALLOCATE SUCCESSFUL?
         BNZ   EXIT0000                       NO.  PROCESS NEXT REQUEST
         MVI   S99VERB,S99VRBUN               SET VERB
         LA    R15,WKDDP                      DDNAME PARAMETER
         ST    R15,WKTXTP1                    SET PARAMETER 1 ADDRESS
         OI    WKTXTP1,X'80'                  FLAG END OF LIST
         BAL   R8,DAIR0080                    ISSUE SVC 99
         B     EXIT0000                       PROCESS NEXT REQUEST
*---------------------------------------------------------------------*
*                                                                     *
*     Issue the SVC                                                   *
*                                                                     *
*---------------------------------------------------------------------*
DAIR0080 DS    0H
         MVI   S99RBLN,S99RBEND-S99RB
         LA    R1,WKTXTP1                     FIRST TEXT UNIT POINTER
         ST    R1,S99TXTPP                    SET TEXT ADDRESS
         LA    R1,WKRBA                       REQUEST BLOCK POINTER
         SVC   99                             ISSUE SVC
         STCM  R15,3,DAIRR15                  PASS RETURN CODE
         MVC   DAIRERR,S99ERROR               PASS ERROR CODE
         MVC   DAIRINFO,S99INFO               PASS INFO CODE
         BR    R8                             RETURN
* ------------------------------------------------------------------- *
*             EXIT                                                    *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         LR    R1,R13                         COPY WORK AREA ADDRESS
         L     R13,4(R13)                     RESTORE REGISTER 13
         FREEMAIN RU,                         FREE WORK AREA           +
               A=(1),                                                  +
               LV=WKL
         LM    R14,R12,12(R13)                RESTORE REGISTERS
         SR    R15,R15                        RETURN CODE
         BR    R14                            RETURN TO CALLER
* ------------------------------------------------------------------ *
*        CONSTANTS                                                   *
* ------------------------------------------------------------------ *
BLANKS   DC    CL8' '
DAIRPI   DC    AL2(DALDDNAM)         DDNAME PARMS
         DC    AL2(1)
         DC    AL2(8)
         DC    CL8' '                DDNAME
         DC    AL2(DALDSNAM)         DATASET NAME PARMS
         DC    AL2(1)
         DC    AL2(44)
         DC    CL44' '               DATASET NAME
         DC    AL2(DALMEMBR)         MEMBER NAME PARMS
         DC    AL2(1)
         DC    AL2(8)
         DC    CL8' '                DATASET NAME
         DC    AL2(DALSTATS)         INITIAL DISPOSITION PARMS
         DC    AL2(1)
         DC    AL2(1)
         DC    X'00'                 INITIAL DISPOSITION
         DC    AL2(DALNDISP)         NORMAL DISPOSITION PARMS
         DC    AL2(1)
         DC    AL2(1)
         DC    X'00'                 NORMAL DISPOSITION
         DC    AL2(DALUNIT)          UNIT PARAMETERS
         DC    AL2(1)
         DC    AL2(8)
         DC    CL8'        '         UNIT NAME
         DC    AL2(DALBLKLN)         ALLOCATION AVERAGE BLOCK LENGTH
         DC    AL2(1)
         DC    AL2(3)
         DC    XL3'00'               BLOCK LENGTH
         DC    AL2(DALPRIME)         PRIMARY SPACE PARAMETERS
         DC    AL2(1)
         DC    AL2(3)
         DC    XL3'0'                PRIMARY SPACE (IN TRACKS)
         DC    AL2(DALSECND)         SECONDARY SPACE PARAMETERS
         DC    AL2(1)
         DC    AL2(3)
         DC    XL3'0'                SECONDARY SPACE (IN TRACKS)
         DC    AL2(DALRLSE)          RELEASE UNUSED SPACE
         DC    AL2(0)
         DC    AL2(DALSYSOU)         ALLOCATE SYSOUT
         DC    AL2(1)
         DC    AL2(1)
         DC    C'O'                  SYSOUT CLASS
         DC    AL2(DALCLOSE)         SAME AS FREE=CLOSE ON A DD STMT
         DC    AL2(0)
DAIRPL   EQU   *-DAIRPI
         LTORG
* ------------------------------------------------------------------ *
*        WORK AREAS                                                  *
* ------------------------------------------------------------------ *
WK       DSECT
         STDSAVE DSECT=NO
WKDWORD  DS    D
WKRBA    DS    A                     REQUEST BLOCK'S ADDRESS
WKTXTP1  DS    A                     TEXT UNIT 1'S ADDRESS
WKTXTP2  DS    A                     TEXT UNIT 2'S ADDRESS
WKTXTP3  DS    A                     TEXT UNIT 3'S ADDRESS
WKTXTP4  DS    A                     TEXT UNIT 4'S ADDRESS
WKTXTP5  DS    A                     TEXT UNIT 5'S ADDRESS
WKTXTP6  DS    A                     TEXT UNIT 6'S ADDRESS
WKTXTP7  DS    A                     TEXT UNIT 7'S ADDRESS
WKTXTP8  DS    A                     TEXT UNIT 8'S ADDRESS
WKTXTP9  DS    A                     TEXT UNIT 9'S ADDRESS
WKALIST  DS    A                     RETURNED UCB LIST ADDRESS
WKRB     DS    (S99RBEND-S99RB)X     RESERVE SPACE FOR REQUEST BLOCK
WKDDP    DS    AL2,AL2,AL2           DDNAME PARMS
WKDD     DS    CL8                   DDNAME
WKDSNP   DS    AL2,AL2,AL2           DATASET NAME PARMS
WKDSN    DS    CL44                  DATASET NAME
WKMEMP   DS    AL2,AL2,AL2           MEMBER NAME PARMS
WKMEM    DS    CL8                   MEMBER NAME
WKDSPIP  DS    AL2,AL2,AL2           INITIAL DISPOSITION PARMS
WKDSPI   DS    X                     INITIAL DISPOSITION
WKDSPNP  DS    AL2,AL2,AL2           NORMAL DISPOSITION PARMS
WKDSPN   DS    X                     NORMAL DISPOSITION
WKUNITP  DS    AL2,AL2,AL2           UNIT PARAMETERS
WKUNIT   DS    CL8                   UNIT NAME
WKBLKNP  DS    AL2,AL2,AL2           ALLOCATION UNITS (BLOCK LENGTH)
WKBLKN   DS    XL3                   BLOCK LENGTH
WKPRIP   DS    AL2,AL2,AL2           PRIMARY SPACE PARAMETERS
WKPRI    DS    XL3                   PRIMARY SPACE (IN TRACKS)
WKSECP   DS    AL2,AL2,AL2           SECONDARY SPACE PARAMETERS
WKSEC    DS    XL3                   SECONDARY SPACE (IN TRACKS)
WKRELP   DS    AL2,AL2               RELEASE UNUSED SPACE (RLSE)
WKPRTP   DS    AL2,AL2,AL2           SYSOUT PARAMETERS
WKPRTC   DS    C                     SYSOUT CLASS
WKFREEP  DS    AL2,AL2               FREE AT DE-ALLOCATION
WKPL     EQU   *-WKDDP
WKL      EQU   *-WK
* ------------------------------------------------------------------ *
*              DAIR INTEFACE BLOCK                                   *
* ------------------------------------------------------------------ *
         DAIRBLOK DSECT=YES
* ------------------------------------------------------------------ *
*              EQUATES                                               *
* ------------------------------------------------------------------ *
         COPY REGEQU
         IEFZB4D0
         IEFZB4D2
         END  HTTPDAIR
