         MACRO
         TASKINFO &NAME,&ID,&ECB,&WORK
         DC    CL8'&NAME'
         DC    AL1(&ID)
         DC    AL2(&ECB-HTTPCOMM)
         DC    AL2(&WORK-HTTPCOMM)
         MEND
         TITLE 'HTTPLOCL    Process local URLs'
*---------------------------------------------------------------------*
*                                                                     *
*  Module name: HTTPLOCL                                              *
*                                                                     *
*   This subtask processes local URLs                                 *
*                                                                     *
*   As of now there are two URLs                                      *
*      status     returns a list of the URLs defined                  *
*      trace      returns a formatted display of the trace table      *
*                                                                     *
*                                                                     *
*   Note that this module processes URLs like remote servers but      *
*   there is one major difference.  When a remote server space starts *
*   up, it must define the URL(s) it wants to process.  Remote        *
*   server spaces communicate with the main space via TCP/IP.         *
*   This module runs as a subtask of the main task in the main        *
*   space.  Communication is done by 'queueing' work using the        *
*   compare-and-swap (CS) instruction and ECBs for serialization.     *
*                                                                     *
* ------------------------------------------------------------------- *
HTTPLOCL CSECT
HTTPLOCL AMODE 31
HTTPLOCL RMODE ANY
         USING HTTPLOCL,R12,R10               DEFINE BASE
         USING HTTPCOMM,R11                   DEFINE BASE
         STM   R14,R12,12(R13)                CHAIN REGISTERS
         LR    R12,R15                        COPY ENTRY POINT ADDRESS
         B     INIT0000
         DC    CL8'HTTPLOCL'
         DC    CL8'&SYSDATE'
         DC    CL6'&SYSTIME'
INIT0000 DS    0H
         L     R11,0(R1)                      LOAD HTTPCOMM'S ADDRESS
         ST    R12,COMM_HTTPLOCL_ADDRESS
         LA    R10,2048(R12)
         LA    R10,2048(R10)
         GETMAIN RU,                          STORAGE FOR WORK AREA    +
               LV=WORK_AREA_LENGTH,                                    +
               LOC=ANY
         ST    R13,4(R1)                      CHAIN PREVIOUS AREA
         ST    R1,8(R13)                      CHAIN OUR AREA
         LR    R13,R1                         COPY WORK AREA ADDRESS
         ST    R13,COMM_LOCAL_WORK_AREA
         USING WORKAREA,R13
         ITRACE ID=INIT
         MVC   PRT_CC(PRT_LRECL),COMM_BLANKS
         XC    WORK_TCPIP_TASK(TIELENTH),WORK_TCPIP_TASK
         XC    WORK_TCPIP_ECB(104),WORK_TCPIP_ECB
         ITRACE ID=INITAPI
         EZASMI TYPE=INITAPI,                                          +
               RETCODE=WORK_TCPIP_RETURN_CODE,                         +
               ERRNO=WORK_TCPIP_ERROR_NUMBER,                          +
               SUBTASK=C_HTTPLOCL,                                     +
               ASYNC='ECB',                                            +
               MAXSNO=WORK_MAX_SOCKET,                                 +
               TASK=WORK_TCPIP_TASK
         ITRACE ID=GETID
         XC    WORK_TCPIP_ECB,WORK_TCPIP_ECB
         EZASMI TYPE=GETCLIENTID,                                      +
               CLIENT=COMM_HTTPLOCL_CLIENT,                            +
               ECB=WORK_TCPIP_ECB,                                     +
               ERRNO=WORK_TCPIP_ERROR_NUMBER,                          +
               RETCODE=WORK_TCPIP_RETURN_CODE
         WAIT  ECB=WORK_TCPIP_ECB
* ------------------------------------------------------------------- *
*        Define "local" URLs                                          *
* ------------------------------------------------------------------- *
         GETMAIN RU,                                                   +
               LV=WEBURLL+URL_PREFIX_L+100,                            +
               LOC=ANY
         LR    R9,R1
         USING WEBURL,R9
         USING URL,WEBURL_URL
         XC    WEBURL(WEBURLL+URL_PREFIX_L),WEBURL
         MVC   WEBURL_EYE,C_WEBURL
         MVC   WEBURL_SERVBLOK,=F'-1'
         MVI   WEBURL_STATUS,$URL_DEFINE_SPECIFIC
         LA    R2,LOCAL_URLS
INIT0010 DS    0H
         SR    R3,R3
         ICM   R3,3,0(R2)
         STCM  R3,3,URL_LENGTH                SET LENGTH
         BCTR  R3,0
         EX    R3,INIT_MVC
         XC    WEBURL_ECB,WEBURL_ECB          RESET ECB
         ITRACE ID=DEF_URL,                   DEFINING URL             +
               RDATA1=R9,                                              +
               DATA2=(URL_TEXT,8)
INIT0020 DS    0H
         L     R1,COMM_URL_QUEUE              1ST ON QUEUE NOW
         ST    R1,WEBURL_NEXT                 CHAIN TO OUR URL
         CS    R1,R9,COMM_URL_QUEUE           ADD TO QUEUE
         BC    4,INIT0020                     TRY AGAIN
         ITRACE ID=POST_URL
         POST  COMM_URL_WORK_ECB              WAKE UP URL TASK
         LA    R1,WEBURL_ECB
         ITRACE ID=WAIT_URL,                                           +
               RDATA1=R1,                                              +
               DATA2=(0(R1),8)
         WAIT  ECB=WEBURL_ECB                 WAIT
         XC    WEBURL_NEXT,WEBURL_NEXT
         LA    R2,3(R3,R2)                    NEXT URL
         CLI   0(R2),X'FF'                    END OF LOCAL URLS?
         BNE   INIT0010                       NO
         ITRACE ID=FREE_URL,                                           +
               RDATA1=R9
         FREEMAIN RU,                         FREE URL WORK AREA       +
               A=(R9),                                                 +
               LV=WEBURLL+URL_PREFIX_L+100
         POST  COMM_LOCAL_INIT_ECB            POST ECB
         B     LOCL0010
INIT_MVC MVC   URL_TEXT(0),2(R2)              COPY TEXT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
LOCL0010 DS    0H
         TM    COMM_EOJ,$EOJ_PHASE_1          SHUTDOWN PHASE 1?
         BO    EXIT0000                       YES.. IT'S MILLER TIME
         TM    COMM_LOCAL_WORK_ECB,X'40'      ECB ALREADY POSTED?
         BO    LOCL0020                       GET BUSY
         ITRACE ID=WORKWAIT,                  WAITING FOR WORK         +
               RDATA1=R1                      .. ECB'S ADDRESS
         WAIT  ECB=COMM_LOCAL_WORK_ECB        WAIT FOR WORK TO DO ECB
         ITRACE ID=WOKEUP
LOCL0020 DS    0H
         XC    COMM_LOCAL_WORK_ECB,COMM_LOCAL_WORK_ECB
LOCL0030 DS    0H
         SR    R1,R1
LOCL0040 DS    0H
         ICM   R2,15,COMM_LOCAL_URL_QUEUE
         BZ    LOCL0010
         CS    R2,R1,COMM_LOCAL_URL_QUEUE
         BC    4,LOCL0040
         ITRACE ID=HAVEQ,                     WE HAVE THE QUEUE        +
               RDATA1=R2                      .. FIRST (LAST) BLOCK
         LR    R3,R2                          COPY ADDRESS
         LA    R1,4                           INITIALIZE GETMAIN LENGTH
LOCL0050 DS    0H
         ICM   R3,15,WEBURL_NEXT-WEBURL(R3)
         BZ    LOCL0060                       NEXT BLOCK'S ADDRESS
         ITRACE ID=VEC1,                                               +
               RDATA1=R3,                                              +
               RDATA2=R1
         LA    R1,4(R1)                       ADD TO VECTOR LENGTH
         B     LOCL0050                       LOOP
LOCL0060 DS    0H
         ITRACE ID=GETVEC,                                             +
               RDATA1=R1                      .. SIZE OF VECTOR LIST
         ST    R1,WORK_VECTOR_SIZE            SAVE VECTOR SIZE
         GETMAIN RU,                          GETMAIN STORAGE          +
               LV=(1),                                                 +
               LOC=ANY
         ST    R1,WORK_VECTOR_ADDRESS         SAVE VECTOR ADDRESS
         ITRACE ID=VECADDR,                                            +
               RDATA1=R1                      .. VECTOR ADDRESS
LOCL0070 DS    0H
         ITRACE ID=BLDVEC,                                             +
               RDATA1=R1,                     .. CURRENT IN VECTOR     +
               RDATA2=R2                      .. WEBURL'S ADDRESS
         ST    R2,0(R1)                       SET ADDRESS
         ST    R1,WORK_VECTOR_CURRENT
         LA    R1,4(R1)                       NEXT
         ICM   R2,15,WEBURL_NEXT-WEBURL(R2)
         BNZ   LOCL0070
*---------------------------------------------------------------------*
*                                                                     *
*        Process the URLs                                             *
*                                                                     *
*     1) TAKESOCKET                                                   *
*     2) Determine which URL was entered                              *
*     3) send output to user                                          *
*     4) SHUTDOWN the SOCKET                                          *
*     5) CLOSE the SOCKET                                             *
*     6) Flag the URL complete                                        *
*     7) Queue the completed URL back to HTTPWEB                      *
*     8) Check for more work                                          *
*                                                                     *
*---------------------------------------------------------------------*
LOCL0080 DS    0H
         L     R1,WORK_VECTOR_CURRENT
         L     R9,0(R1)                       WEBURL'S ADDRESS
         USING WEBURL,R9                      DEFINE BASE
         USING URL,WEBURL_URL
         ITRACE ID=TAKESOCK,                                           +
               DATA1=(WEBURL_SOCKET,8),                                +
               DATA2=(WEBURL_ID,8)
         MVC   WORK_TCPIP_FUNCTION,C_TAKESOCKET
         XC    WORK_TCPIP_ECB,WORK_TCPIP_ECB
         EZASMI TYPE=TAKESOCKET,                                       +
               SOCRECV=WEBURL_SOCKET,                                  +
               CLIENT=WEBURL_FROM_CLIENT,                              +
               ECB=WORK_TCPIP_ECB,                                     +
               TASK=WORK_TCPIP_TASK,                                   +
               ERRNO=WORK_TCPIP_ERROR_NUMBER,                          +
               RETCODE=WORK_TCPIP_RETURN_CODE
         WAIT  ECB=WORK_TCPIP_ECB
         ITRACE ID=TAKE_RC,                                            +
               DATA1=(WORK_TCPIP_RETURN_CODE,8),                       +
               DATA2=(WORK_TCPIP_ERROR_NUMBER,8)
         TM    WORK_TCPIP_RETURN_CODE,X'80'
         BO    ERR0010
         MVC   WORK_SOCKET,WORK_TCPIP_RETURN_CODE+2
         ITRACE ID=HAVESOCK,                                           +
               DATA1=WORK_SOCKET
         OI    WEBURL_STATUS,$URL_PROCESS
         CLI   URL_TEXT,C'S'                  STATUS?
         BE    LOCL0200
*---------------------------------------------------------------------*
*                                                                     *
*                                                                     *
*     Process "TRACE" requests.                                       *
*                                                                     *
*     Send the user the internal trace table                          *
*                                                                     *
*                                                                     *
*---------------------------------------------------------------------*
LOCL0110 DS    0H
         ITRACE ID=TRACE
         MVC   WORK_BUFFER(HTTP_200L),HTTP_200
         MVC   WORK_BUFFER+HTTP_200L(HTML_STARTL),HTML_START
         MVC   WORK_BUFFER+HTTP_200L+HTML_STARTL(HTML_CENTER_STARTL),HT+
               ML_CENTER_START
         MVC   WORK_BUFFER+HTTP_200L+HTML_STARTL+HTML_CENTER_STARTL(HTM+
               L_TABLE_STARTL),HTML_TABLE_START
         MVC   WORK_BUFFER+HTTP_200L+HTML_STARTL+HTML_CENTER_STARTL+HTM+
               L_TABLE_STARTL(HTML_TRACE_HEADINGL),HTML_TRACE_HEADING
         LA    R3,HTTP_200L+HTML_STARTL+HTML_CENTER_STARTL+HTML_TABLE_S+
               TARTL+HTML_TRACE_HEADINGL
         BAL   R8,WRITE000
         L     R4,TRACE_FIRST                 FIRST TRACE ENTRY
         USING TRACE_ENTRY,R4                 DEFINE BASE
         USING WORK_TRACE,R2
LOCL0120 DS    0H
         C     R4,TRACE_LAST                  ALL ENTRIES PROCESSED?
         BE    LOCL0130                       YES
         OC    0(TRACE_ENTRY_LENGTH,R4),0(R4)
         BZ    LOCL0130
         MVI   WORK_TRACE,X'40'
         MVC   WORK_TRACE+1(WORK_TRACEL-1),WORK_TRACE
         MVC   WORK_TRACE_TR,HTML_TR
         MVC   WORK_TRACE_TD_1,HTML_TD
         MVC   WORK_TRACE_TD_2,HTML_TD
         MVC   WORK_TRACE_TD_3,HTML_TD
         MVC   WORK_TRACE_TD_4,HTML_TD
         MVC   WORK_TRACE_TD_5,HTML_TD
         MVC   WORK_TRACE_TD_6,HTML_TD
         MVC   WORK_TRACE_TD_7,HTML_TD
         MVC   WORK_TRACE_TD_END_1,HTML_TD_END
         MVC   WORK_TRACE_TD_END_2,HTML_TD_END
         MVC   WORK_TRACE_TD_END_3,HTML_TD_END
         MVC   WORK_TRACE_TD_END_4,HTML_TD_END
         MVC   WORK_TRACE_TD_END_5,HTML_TD_END
         MVC   WORK_TRACE_TD_END_6,HTML_TD_END
         MVC   WORK_TRACE_TD_END_7,HTML_TD_END
         UNPK  WORK_UNPACK(7),TRACE_ENTRY_TCB(4)
         TR    WORK_UNPACK(6),C_HEXCHAR
         MVC   WORK_TRACE_TCB,WORK_UNPACK
         MVC   WORK_TRACE_MODULE_NAME,TRACE_ENTRY_MODULE_NAME
         MVC   WORK_TRACE_ID,TRACE_ENTRY_TRACE_ID
         MVC   WORK_TRACE_DATA_1,TRACE_ENTRY_DATA_1
         TR    WORK_TRACE_DATA_1,PRINTABLE
         UNPK  WORK_UNPACK+0(9),TRACE_ENTRY_DATA_1+0(5)
         UNPK  WORK_UNPACK+8(9),TRACE_ENTRY_DATA_1+4(5)
         TR    WORK_UNPACK(16),C_HEXCHAR
         MVC   WORK_TRACE_HEX_1,WORK_UNPACK
         MVC   WORK_TRACE_DATA_2,TRACE_ENTRY_DATA_2
         TR    WORK_TRACE_DATA_2,PRINTABLE
         UNPK  WORK_UNPACK+0(9),TRACE_ENTRY_DATA_2+0(5)
         UNPK  WORK_UNPACK+8(9),TRACE_ENTRY_DATA_2+4(5)
         TR    WORK_UNPACK(16),C_HEXCHAR
         MVC   WORK_TRACE_HEX_2,WORK_UNPACK
         MVC   WORK_TRACE_TR_END,HTML_TR_END
         LA    R3,WORK_TRACEL
         BAL   R8,WRITE000
         LA    R4,TRACE_ENTRY_LENGTH(R4)
         B     LOCL0120
LOCL0130 DS    0H
         MVC   WORK_BUFFER(HTML_CENTER_ENDL),HTML_CENTER_END
         MVC   WORK_BUFFER+HTML_CENTER_ENDL(HTML_ENDL),HTML_END
         LA    R3,HTML_CENTER_ENDL+HTML_ENDL
         BAL   R8,WRITE000
         B     LOCL0400
         DROP  R2,R4
*---------------------------------------------------------------------*
*                                                                     *
*      Process "STATUS" requests.                                     *
*                                                                     *
*      Information returned is:                                       *
*         Status of each server space                                 *
*                                                                     *
*---------------------------------------------------------------------*
LOCL0200 DS    0H
         ITRACE ID=STATUS
         MVC   WORK_BUFFER(HTTP_200L),HTTP_200
         MVC   WORK_BUFFER+HTTP_200L(HTML_STARTL),HTML_START
         MVC   WORK_BUFFER+HTTP_200L+HTML_STARTL(HTML_CENTER_STARTL),HT+
               ML_CENTER_START
         LA    R3,HTTP_200L+HTML_STARTL+HTML_CENTER_STARTL
         BAL   R8,WRITE000
         MVC   WORK_BUFFER(HTML_SYSTEM_HEADINGL),HTML_SYSTEM_HEADING
         L     R1,CVTPTR
         USING CVT,R1
         MVC   WORK_BUFFER+(HTML_SYSTEM_NAME-HTML_SYSTEM_HEADING)(8),CV+
               TSNAME
         LA    R3,HTML_SYSTEM_HEADINGL
         BAL   R8,WRITE000

         MVC   WORK_BUFFER(HTML_TABLE_STARTL),HTML_TABLE_START
         LA    R3,HTML_TABLE_STARTL
         BAL   R8,WRITE000

         MVC   WORK_BUFFER(HTML_STATUS_2L),HTML_STATUS_2
         MVC   WORK_BUFFER+(HTML_STATUS_2_ITEM_1-HTML_STATUS_2)(8),=C'W+
               EB PORT'
         MVC   WORK_BUFFER+(HTML_STATUS_2_VALUE_1-HTML_STATUS_2)(5),COM+
               M_WEB_PORT_CHAR
         MVC   WORK_BUFFER+(HTML_STATUS_2_ITEM_2-HTML_STATUS_2)(8),=C'T+
               CP PORT'
         MVC   WORK_BUFFER+(HTML_STATUS_2_VALUE_2-HTML_STATUS_2)(5),COM+
               M_SERVER_PORT_CHAR
         LA    R3,HTML_STATUS_2L
         BAL   R8,WRITE000

         LA    R6,TASK_TABLE
         USING TASK_DSECT,R6
LOCL0210 DS    0H
         MVC   WORK_BUFFER(HTML_STATUS_3L),HTML_STATUS_3
         MVC   WORK_BUFFER+(HTML_STATUS_3_ITEM_1-HTML_STATUS_3)(8),TASK+
               _NAME
         MVC   WORK_BUFFER+(HTML_STATUS_3_VALUE_1-HTML_STATUS_3)(8),=C'+
               ........'
         MVC   WORK_BUFFER+(HTML_STATUS_3_ITEM_2-HTML_STATUS_3)(8),=C'T+
               erm ECB'
         MVC   WORK_BUFFER+(HTML_STATUS_3_VALUE_2-HTML_STATUS_3)(8),=C'+
               ........'
         SR    R1,R1
         IC    R1,TASK_ID
         EX    R1,TASK_TM
         BNO   LOCL0230
         MVC   WORK_BUFFER+(HTML_STATUS_3_VALUE_1-HTML_STATUS_3)(8),=C'+
               Attached'
         SR    R1,R1
         ICM   R1,3,TASK_ECB_OFFSET
         AR    R1,R11
         TM    0(R1),X'40'
         BNO   LOCL0220
         MVC   WORK_BUFFER+(HTML_STATUS_3_VALUE_1-HTML_STATUS_3)(10),=C+
               'Terminated'
LOCL0220 DS    0H
         UNPK  WORK_UNPACK(9),0(5,R1)
         TR    WORK_UNPACK,C_HEXCHAR
         MVC   WORK_BUFFER+(HTML_STATUS_3_VALUE_2-HTML_STATUS_3)(8),WOR+
               K_UNPACK
LOCL0230 DS    0H
         SR    R1,R1
         ICM   R1,3,TASK_WORK_OFFSET
         AR    R1,R11
         L     R1,0(R1)
         MVC   WORK_BUFFER+(HTML_STATUS_3_ITEM_3-HTML_STATUS_3)(13),=C'+
               Last TRACE ID'
         MVC   WORK_BUFFER+(HTML_STATUS_3_VALUE_3-HTML_STATUS_3)(8),TRA+
               CE_ID-WORKAREA(R1)
         LA    R3,HTML_STATUS_3L
         BAL   R8,WRITE000

         LA    R6,TASK_DSECTL(R6)
         CLI   0(R6),X'FF'
         BNE   LOCL0210

         MVC   WORK_BUFFER(HTML_TABLE_ENDL),HTML_TABLE_END
         LA    R3,HTML_TABLE_ENDL
         BAL   R8,WRITE000

         MVC   WORK_BUFFER(HTML_TRACE_STATUSL),HTML_TRACE_STATUS
         MVC   WORK_BUFFER+(HTML_TRACE_STATUS_STATUS-HTML_TRACE_STATUS)+
               (3),=C'Off'
         TM    X_TRACE_FLAG,$X_TRACE_ON
         BNO   LOCL0240
         MVC   WORK_BUFFER+(HTML_TRACE_STATUS_STATUS-HTML_TRACE_STATUS)+
               (3),=C'On '
LOCL0240 DS    0H
         LA    R3,HTML_TRACE_STATUSL
         BAL   R8,WRITE000
*---------------------------------------------------------------------*
*     Process remote servers                                          *
*---------------------------------------------------------------------*
         ICM   R7,15,COMM_SERVERS
         BZ    LOCL0330
         USING SERVBLOK,R7
LOCL0300 DS    0H
         MVC   WORK_BUFFER(HTML_SERVER_HEADINGL),HTML_SERVER_HEADING
         MVC   WORK_BUFFER+(HTML_SERVER_HEADING_NAME-HTML_SERVER_HEADIN+
               G)(L'HTML_SERVER_HEADING_NAME),PARTNER_CLIENT_NAME
         MVC   WORK_BUFFER+(HTML_SERVER_HEADING_TASK-HTML_SERVER_HEADIN+
               G)(L'HTML_SERVER_HEADING_TASK),PARTNER_CLIENT_TASK
         MVI   WORK_BUFFER+HTML_SERVER_HEADINGL,$LF
         LA    R3,HTML_SERVER_HEADINGL+1
         BAL   R8,WRITE000
         MVC   WORK_BUFFER(HTML_TABLE_STARTL),HTML_TABLE_START
         MVI   WORK_BUFFER+(HTML_TABLE_BORDER-HTML_TABLE_START),C'1'
         LA    R3,HTML_TABLE_STARTL
         BAL   R8,WRITE000

         MVC   WORK_BUFFER(HTML_STATUS_2L),HTML_STATUS_2
         MVC   WORK_BUFFER+(HTML_STATUS_2_ITEM_1-HTML_STATUS_2)(12),=C'+
               Connect date'
         MVC   WORK_DWORD(4),SERVER_CONNECT_DATE
         AP    WORK_DWORD(4),P_1900000
         MVC   WORK_BUFFER+(HTML_STATUS_2_VALUE_1-HTML_STATUS_2)(9),=X'+
               40202020204B202020'
         ED    WORK_BUFFER+(HTML_STATUS_2_VALUE_1-HTML_STATUS_2)(9),WOR+
               k_DWORD
         MVC   WORK_BUFFER+(HTML_STATUS_2_ITEM_2-HTML_STATUS_2)(12),=C'+
               Connect time'
         UNPK  WORK_UNPACK+0(3),SERVER_CONNECT_TIME+0(2)
         UNPK  WORK_UNPACK+3(3),SERVER_CONNECT_TIME+1(2)
         UNPK  WORK_UNPACK+6(3),SERVER_CONNECT_TIME+2(2)
         TR    WORK_UNPACK+0(2),C_HEXCHAR
         TR    WORK_UNPACK+3(2),C_HEXCHAR
         TR    WORK_UNPACK+6(2),C_HEXCHAR
         MVI   WORK_UNPACK+2,C':'
         MVI   WORK_UNPACK+5,C':'
         MVC   WORK_BUFFER+(HTML_STATUS_2_VALUE_2-HTML_STATUS_2)(8),WOR+
               K_UNPACK
         LA    R3,HTML_STATUS_2L
         BAL   R8,WRITE000

         MVC   WORK_BUFFER(HTML_STATUS_2L),HTML_STATUS_2
         MVC   WORK_BUFFER+(HTML_STATUS_2_ITEM_1-HTML_STATUS_2)(12),=C'+
               Last URL date'
         MVC   WORK_DWORD(4),SERVER_LAST_DATE
         AP    WORK_DWORD(4),P_1900000
         MVC   WORK_BUFFER+(HTML_STATUS_2_VALUE_1-HTML_STATUS_2)(9),=X'+
               40202020204B202020'
         ED    WORK_BUFFER+(HTML_STATUS_2_VALUE_1-HTML_STATUS_2)(9),WOR+
               k_DWORD
         MVC   WORK_BUFFER+(HTML_STATUS_2_ITEM_2-HTML_STATUS_2)(12),=C'+
               LAST URL time'
         UNPK  WORK_UNPACK+0(3),SERVER_LAST_TIME+0(2)
         UNPK  WORK_UNPACK+3(3),SERVER_LAST_TIME+1(2)
         UNPK  WORK_UNPACK+6(3),SERVER_LAST_TIME+2(2)
         TR    WORK_UNPACK+0(2),C_HEXCHAR
         TR    WORK_UNPACK+3(2),C_HEXCHAR
         TR    WORK_UNPACK+6(2),C_HEXCHAR
         MVI   WORK_UNPACK+2,C':'
         MVI   WORK_UNPACK+5,C':'
         MVC   WORK_BUFFER+(HTML_STATUS_2_VALUE_2-HTML_STATUS_2)(8),WOR+
               K_UNPACK
         LA    R3,HTML_STATUS_2L
         BAL   R8,WRITE000

         MVC   WORK_BUFFER(HTML_STATUS_2L),HTML_STATUS_2
         MVC   WORK_BUFFER+(HTML_STATUS_2_ITEM_1-HTML_STATUS_2)(14),=C'+
               URLs processed'
         ICM   R1,15,SERVER_URLS
         BZ    LOCL0310
         CVD   R1,WORK_DWORD
         MVC   WORK_BUFFER+(HTML_STATUS_2_VALUE_1-HTML_STATUS_2)(12),=X+
               '402020206B2020206B202020'
         ED    WORK_BUFFER+(HTML_STATUS_2_VALUE_1-HTML_STATUS_2)(12),WO+
               RK_DWORD+3
         B     LOCL0320
LOCL0310 DS    0H
         MVC   WORK_BUFFER+(HTML_STATUS_2_VALUE_1-HTML_STATUS_2)(4),=C'+
               NONE'
LOCL0320 DS    0H
         MVC   WORK_BUFFER+(HTML_STATUS_2_ITEM_2-HTML_STATUS_2)(8),=C'T+
               ERM ECB'
         UNPK  WORK_UNPACK(9),SERVER_TERM_ECB(5)
         TR    WORK_UNPACK(8),C_HEXCHAR
         MVC   WORK_BUFFER+(HTML_STATUS_2_VALUE_2-HTML_STATUS_2)(8),WOR+
               K_UNPACK
         LA    R3,HTML_STATUS_2L
         BAL   R8,WRITE000

         MVC   WORK_BUFFER(HTML_TABLE_ENDL),HTML_TABLE_END
         LA    R3,HTML_TABLE_ENDL
         BAL   R8,WRITE000

         MVC   WORK_BUFFER(HTML_SPACEL),HTML_SPACE
         LA    R3,HTML_SPACEL
         BAL   R8,WRITE000

         ICM   R7,15,SERVER_NEXT
         BNZ   LOCL0300
         B     LOCL0340
LOCL0330 DS    0H
         ITRACE ID=NOSERVER
         MVC   WORK_BUFFER(MSG02L),MSG02
         LA    R3,MSG02L
         BAL   R8,WRITE000

*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
LOCL0340 DS    0H
         MVC   WORK_BUFFER(HTML_CENTER_ENDL),HTML_CENTER_END
         MVC   WORK_BUFFER+HTML_CENTER_ENDL(HTML_ENDL),HTML_END
         LA    R3,HTML_CENTER_ENDL+HTML_ENDL
         BAL   R8,WRITE000
         B     LOCL0400
*---------------------------------------------------------------------*
*                                                                     *
*     Output has been sent                                            *
*     SHUTDOWN the SOCKET                                             *
*     CLOSE the socket                                                *
*     Flag the URL's status (complete)                                *
*     Put the WEBURL on the HTTPWEB's work queue for final clean-up   *
*                                                                     *
*---------------------------------------------------------------------*
LOCL0400 DS    0H
         MVC   WORK_TCPIP_FUNCTION,C_SHUTDOWN
         XC    WORK_TCPIP_ECB,WORK_TCPIP_ECB
         ITRACE ID=SHUTDOWN,                                           +
               RDATA1=R9,                                              +
               DATA2=(WEBURL_ID,8)
         EZASMI TYPE=SHUTDOWN,                                         +
               S=WORK_SOCKET,                                          +
               HOW=2,                                                  +
               RETCODE=WORK_TCPIP_RETURN_CODE,                         +
               ERRNO=WORK_TCPIP_ERROR_NUMBER,                          +
               ECB=WORK_TCPIP_ECB
         TM    WORK_TCPIP_RETURN_CODE,X'80'
         BO    ERR0010
         MVC   WORK_TCPIP_FUNCTION,C_CLOSE
         XC    WORK_TCPIP_ECB,WORK_TCPIP_ECB
         ITRACE ID=CLOSE
         EZASMI TYPE=CLOSE,                                            +
               S=WORK_SOCKET,                                          +
               RETCODE=WORK_TCPIP_RETURN_CODE,                         +
               ERRNO=WORK_TCPIP_ERROR_NUMBER,                          +
               ECB=WORK_TCPIP_ECB
         WAIT  ECB=WORK_TCPIP_ECB
         TM    WORK_TCPIP_RETURN_CODE,X'80'
         BO    ERR0010
         OI    WEBURL_STATUS,$URL_COMPLETE
         ITRACE ID=COMPLETE,                                           +
               RDATA1=R9,                                              +
               DATA2=(WEBURL_ID,8)
LOCL0410 DS    0H
         L     R1,COMM_WEB_COMPLETED
         ST    R1,WEBURL_NEXT
         CS    R1,R9,COMM_WEB_COMPLETED
         BC    4,LOCL0410
         ITRACE ID=POST_WEB                   WAKE UP HTTPWEB
         POST  COMM_WEB_WORK_ECB
* ------------------------------------------------------------------- *
*                                                                     *
*      If there are more URLs..                                       *
*         process the next URL                                        *
*      If there are no more URLs..                                    *
*         FREEMAIN this vector                                        *
*         check for new work on the queue                             *
*                                                                     *
* ------------------------------------------------------------------- *
         L     R1,WORK_VECTOR_CURRENT         CURRENT ADDRESS IN VECTOR
         C     R1,WORK_VECTOR_ADDRESS         AT FIRST ENTRY?
         BE    LOCL0510                       YES
         SH    R1,C_H4                        BACK UP 1 ENTRY
         ST    R1,WORK_VECTOR_CURRENT         SAVE CURRENT ADDRESS
         B     LOCL0080                       PROCESS THIS URL
LOCL0510 DS    0H
         L     R0,WORK_VECTOR_SIZE            VECTOR SIZE
         ITRACE ID=FREEVEC,                                            +
               RDATA1=R1,                                              +
               RDATA2=R0
         FREEMAIN RU,                         FREE THE VECTOR          +
               A=(1),                                                  +
               LV=(0)
         B     LOCL0020
* ------------------------------------------------------------------- *
*                                                                     *
*  At entry                                                           *
*     R3   Length of data to write                                    *
*                                                                     *
*  At exit                                                            *
*     R2   Address of WORK_BUFFER                                     *
*     R3   Zero                                                       *
*                                                                     *
* ------------------------------------------------------------------- *
WRITE000 DS    0H
*        ITRACE ID=WRITE,                     UNCOMMENTING THIS
*              RDATA1=R3,                     WILL RESULT IN A LOT
*              DATA2=WORK_BUFFER              OF TRACE ENTRIES
         ST    R3,WORK_WRITE_LENGTH
WRITE010 DS    0H
         SR    R0,R0
         LA    R1,EBCDIC_TO_ASCII
         LA    R2,WORK_BUFFER
         L     R3,WORK_WRITE_LENGTH
         TRE   R2,R1                          TRANSLATE TO ASCII
         BC    1,WRITE010

         LA    R2,WORK_BUFFER
         L     R3,WORK_WRITE_LENGTH
WRITE020 DS    0H
         MVC   WORK_TCPIP_FUNCTION,C_WRITE
         XC    WORK_TCPIP_ECB,WORK_TCPIP_ECB
         ST    R3,WORK_WRITE_LENGTH
         EZASMI TYPE=WRITE,                                            +
               S=WORK_SOCKET,                                          +
               NBYTE=WORK_WRITE_LENGTH,                                +
               BUF=(R2),                                               +
               RETCODE=WORK_TCPIP_RETURN_CODE,                         +
               ERRNO=WORK_TCPIP_ERROR_NUMBER,                          +
               ECB=WORK_TCPIP_ECB
         WAIT  ECB=WORK_TCPIP_ECB
         TM    WORK_TCPIP_RETURN_CODE,x'80'
         BO    ERR0010
         A     R2,WORK_TCPIP_RETURN_CODE
         S     R3,WORK_TCPIP_RETURN_CODE
         BH    WRITE020
         LA    R2,WORK_BUFFER
         BR    R8
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
PRT0000  DS    0H
         ITRACE ID=PRINT
         GETMAIN RU,                                                   +
               LV=PRTBLOKL,                                            +
               LOC=ANY
         MVC   0(PRTBLOKL,R1),PRTBLOK
         MVC   PRT_CC(PRT_LRECL),COMM_BLANKS
PRT0010  DS    0H
         L     R14,COMM_PRINT_QUEUE           FIRST BLOCK ON QUEUE
         ST    R14,PRT_NEXT-PRTBLOK(R1)
         CS    R14,R1,COMM_PRINT_QUEUE        ADD NEW BLOCK TO CHAIN
         BC    4,PRT0010                      TRY AGAIN
         POST  COMM_PRINT_WORK_ECB            WAKE UP PRINT SUBTASK
         BR    R8
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0010  DS    0H
         ITRACE ID=TCP_ERR,                                            +
               DATA1=(WORK_TCPIP_FUNCTION,8),                          +
               DATA2=(WORK_TCPIP_ERROR_NUMBER,8)
         MVC   PRT_MESSAGE_ID(MSG01_L),MSG01
         MVC   PRT_MESSAGE_ID+(MSG01_FUNCTION-MSG01)(L'MSG01_FUNCTION),+
               WORK_TCPIP_FUNCTION
         UNPK  PRT_MESSAGE_ID+(MSG01_RETURN_CODE-MSG01)(9),WORK_TCPIP_R+
               ETURN_CODE(5)
         TR    PRT_MESSAGE_ID+(MSG01_RETURN_CODE-MSG01)(8),C_HEXCHAR
         MVI   PRT_MESSAGE_ID+(MSG01_RETURN_CODE-MSG01)+8,C' '
         L     R1,WORK_TCPIP_ERROR_NUMBER
         CVD   R1,WORK_DWORD
         ED    PRT_MESSAGE_ID+(MSG01_ERROR_NUMBER-MSG01)(L'MSG01_ERROR_+
               NUMBER),WORK_DWORD+5
         BAL   R8,PRT0000
         ABEND 1,DUMP,,USER
* ------------------------------------------------------------------- *
*        EXIT                                                         *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=EXIT
         LR    R1,R13                         COPY WORK AREA ADDRESS
         L     R13,4(R13)                     RESTORE REGISTER 13       ASE01670
         FREEMAIN RU,                                                  +
               A=(1),                                                  +
               LV=WORK_AREA_LENGTH
         LM    R14,R12,12(R13)                RESTORE REGISTERS         ASE01680
         SR    R15,R15                        RETURN CODE               ASE01690
         BR    R14                            RETURN TO CALLER          ASE01700
* ------------------------------------------------------------------- *
TASK_TM  TM    COMM_SUBTASKS,X'00'
* ------------------------------------------------------------------- *
*                                                                     *
*        CONSTANTS                                                    *
*                                                                     *
* ------------------------------------------------------------------- *
         LTORG

TASK_TABLE    DS    0C
         TASKINFO PRINT,                                               +
               $PRINT_SUBTASK,                                         +
               COMM_PRINT_TERM_ECB,                                    +
               COMM_PRINT_WORK_AREA
         TASKINFO CONSOLE,                                             +
               $CONSOLE_SUBTASK,                                       +
               COMM_CONSOLE_TERM_ECB,                                  +
               COMM_CONSOLE_WORK_AREA
         TASKINFO LOGGING,                                             +
               $LOGGING_SUBTASK,                                       +
               COMM_LOG_TERM_ECB,                                      +
               COMM_LOG_WORK_AREA
         TASKINFO WEB,                                                 +
               $WEB_SUBTASK,                                           +
               COMM_WEB_TERM_ECB,                                      +
               COMM_WEB_WORK_AREA
         TASKINFO TCP,                                                 +
               $TCP_SUBTASK,                                           +
               COMM_HTTPTCP_TERM_ECB,                                  +
               COMM_HTTPTCP_WORK_AREA
         TASKINFO LOCAL,                                               +
               $LOCAL_SUBTASK,                                         +
               COMM_LOCAL_TERM_ECB,                                    +
               COMM_LOCAL_WORK_AREA
         DC    X'FF'

C_H4                     DC    H'4'
P_1900000                DC    P'1900000'

C_HEXCHAR                EQU   *-C'0'
                         DC    C'0123456789ABCDEF'

C_CLOSE                  DC    CL16'CLOSE'
C_HTTPLOCL               DC    C'HTTPLOCL'
C_HTTPMAIN               DC    C'HTTPMAIN'
C_SHUTDOWN               DC    CL16'SHUTDOWN'
C_STATUS_URL             DC    C'STATUS'
C_TAKESOCKET             DC    CL16'TAKESOCKET'
C_TRACE_URL              DC    C'TRACE.HTML'
C_WRITE                  DC    CL16'WRITE'

C_WEBURL                 DC    CL8'WEBURL'

LOCAL_URLS               DS    0H
                         DC    AL2(5),C'TRACE'
                         DC    AL2(6),C'STATUS'
                         DC    X'FF'

HTTP_200                 DS    0C
                         DC    C'HTTP/1.0 200 OK'
                         DC    AL1($LF)
                         DC    C'Content-type: text/html'
                         DC    AL1($LF)
                         DC    AL1($LF)
                         DC    AL1($LF)
HTTP_200L                EQU   *-HTTP_200
HTML_START               DS    0C

                         DC    C'<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTM+
                 3.2 Final//EN">'
                         DC    AL1($LF)
                         DC    C'<HTML><BODY BACKGROUND=/GIF1/BACKGND>'
                         DC    AL1($LF)
HTML_STARTL              EQU   *-HTML_START
HTML_SYSTEM_HEADING      DS    0C
                         DC    C'<H1>'
                         DC    C'This server is running on '
HTML_SYSTEM_NAME         DC    CL8' '
                         DC    C'</H1>'
HTML_SYSTEM_HEADINGL     EQU   *-HTML_SYSTEM_HEADING

HTML_STATUS_1            DS    0C
                         DC    C'<TR>'
                         DC    C'<TD>'
HTML_STATUS_1_ITEM       DC    CL20' '
                         DC    C'</TD>'
                         DC    C'<TD>'
HTML_STATUS_1_VALUE      DC    CL20' '
                         DC    C'</TD>'
                         DC    C'</TR>'
HTML_STATUS_1L           EQU   *-HTML_STATUS_1

HTML_STATUS_2            DS    0C
                         DC    C'<TR>'
                         DC    C'<TD>'
HTML_STATUS_2_ITEM_1     DC    CL20' '
                         DC    C'</TD>'
                         DC    C'<TD>'
HTML_STATUS_2_VALUE_1    DC    CL20' '
                         DC    C'</TD>'
                         DC    C'<TD>'
HTML_STATUS_2_ITEM_2     DC    CL20' '
                         DC    C'</TD>'
                         DC    C'<TD>'
HTML_STATUS_2_VALUE_2    DC    CL20' '
                         DC    C'</TD>'
                         DC    C'</TR>'
HTML_STATUS_2L           EQU   *-HTML_STATUS_2

HTML_STATUS_3            DS    0C
                         DC    C'<TR>'
                         DC    C'<TD>'
HTML_STATUS_3_ITEM_1     DC    CL20' '
                         DC    C'</TD>'
                         DC    C'<TD>'
HTML_STATUS_3_VALUE_1    DC    CL20' '
                         DC    C'</TD>'
                         DC    C'<TD>'
HTML_STATUS_3_ITEM_2     DC    CL20' '
                         DC    C'</TD>'
                         DC    C'<TD>'
HTML_STATUS_3_VALUE_2    DC    CL20' '
                         DC    C'</TD>'
                         DC    C'<TD>'
HTML_STATUS_3_ITEM_3     DC    CL20' '
                         DC    C'</TD>'
                         DC    C'<TD>'
HTML_STATUS_3_VALUE_3    DC    CL20' '
                         DC    C'</TD>'
                         DC    C'</TR>'
HTML_STATUS_3L           EQU   *-HTML_STATUS_3

HTML_TRACE_STATUS        DS    0C
                         DC    C'<BR>'
                         DC    C'<H3>External trace is '
HTML_TRACE_STATUS_STATUS DC    CL3' '
                         DC    C'</H3>'
                         DC    C'<BR>'
                         DC    C'<h3>External servers</h3>'
HTML_TRACE_STATUSL       EQU   *-HTML_TRACE_STATUS

HTML_CENTER_START        DS    0C
                         DC    C'<CENTER>'
HTML_CENTER_STARTL       EQU   *-HTML_CENTER_START
HTML_CENTER_END          DS    0C
                         DC    C'</CENTER>'
HTML_CENTER_ENDL         EQU   *-HTML_CENTER_END
HTML_END                 DS    0C
                         DC    C'</BODY>'
                         DC    AL1($LF)
                         DC    C'</HTML>'
HTML_ENDL                EQU   *-HTML_END
HTML_TRACE_HEADING       DS    0C
                         DC    C'<TH>'
                         DC    C'<TR>'
                         DC    C'<TD>TCB ADDR</TD>'
                         DC    C'<TD>MODULE</TD>'
                         DC    C'<TD>ID</TD>'
                         DC    C'<TD>DATA1</TD>'
                         DC    C'<TD>DATA2</TD>'
                         DC    C'<TD>HEX1</TD>'
                         DC    C'<TD>HEX2</TD>'
                         DC    C'</TR>'
                         DC    C'</TH>'
                         DC    AL1($LF)
HTML_TRACE_HEADINGL      EQU   *-HTML_TRACE_HEADING

HTML_SERVER_HEADING      DS    0C
                         DC    C'Server with partner name '
HTML_SERVER_HEADING_NAME DC    CL8' '
                         DC    C'  task '
HTML_SERVER_HEADING_TASK DC    CL8' '
                         DC    C'<BR>'
HTML_SERVER_HEADINGL     EQU   *-HTML_SERVER_HEADING

HTML_TABLE_START         DS    0C
                         DC    C'<TABLE BORDER='
HTML_TABLE_BORDER        DC    C'0'
                         DC    C'>'
                         DC    AL1($LF)
HTML_TABLE_STARTL        EQU   *-HTML_TABLE_START
HTML_TABLE_END           DS    0C
                         DC    C'</TABLE>'
                         DC    AL1($LF)
HTML_TABLE_ENDL          EQU   *-HTML_TABLE_END
HTML_TD                  DC    C'<TD>'
HTML_TD_END              DC    C'</TD>'
HTML_TR                  DC    C'<TR>'
HTML_TR_END              DC    C'</TR>'

HTML_SPACE               DS    0C
                         DC    C'<BR>'
HTML_SPACEL              EQU   *-HTML_SPACE

MSG01                    DS    0C
                         DC    CL15'HTTPTCP01I'
                         DC    C'TCP/IP function '
MSG01_FUNCTION           DC    CL16' '
                         DC    C'  return code: '
MSG01_RETURN_CODE        DC    CL8' '
                         DC    C'  error number:'
MSG01_ERROR_NUMBER       DC    X'402020202120'
MSG01_L                  EQU   *-MSG01
MSG02                    DC    C'No servers are active'
                         DC    AL1($LF)
MSG02L                   EQU   *-MSG02

PRINTABLE                DS    0C   0 1 2 3 4 5 6 7 8 9 A B C D E F
                         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B' 0
                         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B' 1
                         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B' 2
                         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B' 3
                         DC    X'404B4B4B4B4B4B4B4B4B4A4B4C4D4E4F' 4
                         DC    X'4B4B4B4B4B4B4B4B4B4B5A5B5C5D5E5F' 5
                         DC    X'60614B4B4B4B4B4B4B4B4B6B6C6D6E6F' 6
                         DC    X'4B4B4B4B4B4B4B4B4B797A7B7C7D7E7F' 7
                         DC    X'4B8182838485868788894B4B4B4B4B4B' 8
                         DC    X'4B9192939495969798994B4B4B4B4B4B' 9
                         DC    X'4B4BA2A3A4A5A6A7A8A94B4B4B4B4B4B' A
                         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B' B
                         DC    X'4BC1C2C3C4C5C6C7C8C94B4B4B4B4B4B' C
                         DC    X'4BD1D2D3D4D5D6D7D8D94B4B4B4B4B4B' D
                         DC    X'4BE1E2E3E4E5E6E7E8E94B4B4B4B4B4B' E
                         DC    X'F0F1F2F3F4F5F6F7F8F94B4B4B4B4B4B' F
* ------------------------------------------------------------------- *
*                                                                     *
*        WORK AREAS                                                   *
*                                                                     *
* ------------------------------------------------------------------- *
WORKAREA                 DSECT
                         STDSAVE DSECT=NO
WORK_DWORD               DS   D
WORK_WRITE_LENGTH        DS   F
WORK_VECTOR_SIZE         DS   F       ADDRESS VECTOR SIZE
WORK_VECTOR_ADDRESS      DS   A       ADDRESS OF ADDRESS VECTOR
WORK_VECTOR_CURRENT      DS   A       CURRENT ADDRESS IN ADDRESS VECTOR

WORK_MAX_SOCKET          DS   F
WORK_TCPIP_RETURN_CODE   DS   F
WORK_TCPIP_ERROR_NUMBER  DS   F
WORK_TCPIP_ECB           DS   F
                         DS   XL100

WORK_SOCKET              DS   H

WORK_RC                  DS   H

WORK_TCPIP_FUNCTION      DS   CL16

WORK_UNPACK              DS   CL16
                         DS   X

* ------------------------------------------------------------------- *
WORK_TRACE            DS  0C

WORK_TRACE_TR         DS  CL4

WORK_TRACE_TD_1       DS  CL4
WORK_TRACE_TCB        DS  CL6
WORK_TRACE_TD_END_1   DS  CL5

WORK_TRACE_TD_2       DS  CL4
WORK_TRACE_MODULE_NAME DS CL5
WORK_TRACE_TD_END_2   DS  CL5

WORK_TRACE_TD_3       DS  CL4
WORK_TRACE_ID         DS  CL8
WORK_TRACE_TD_END_3   DS  CL5

WORK_TRACE_TD_4       DS  CL4
WORK_TRACE_DATA_1     DS  CL8
WORK_TRACE_TD_END_4   DS  CL5

WORK_TRACE_TD_5       DS  CL4
WORK_TRACE_DATA_2     DS  CL8
WORK_TRACE_TD_END_5   DS  CL5

WORK_TRACE_TD_6       DS  CL4
WORK_TRACE_HEX_1      DS  CL16
WORK_TRACE_TD_END_6   DS  CL5

WORK_TRACE_TD_7       DS  CL4
WORK_TRACE_HEX_2      DS  CL16
WORK_TRACE_TD_END_7   DS  CL5

WORK_TRACE_TR_END     DS  CL5
WORK_TRACEL           EQU *-WORK_TRACE
* ------------------------------------------------------------------- *

                      PRTBLOK DSECT=NO
WORK_TCPIP_TASK       EZASMI TYPE=TASK,STORAGE=CSECT


WORK_BUFFER           DS  (URL_BUFFER_SIZE)X

WORK_AREA_LENGTH      EQU *-WORKAREA
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                      COMMON MAIN,TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                      WEBURL
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                      URL
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                      SERVBLOK DSECT=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DETAIL           DSECT
DETAIL_TR        DS    CL4
DETAIL_TD_1      DS    CL4
DETAIL_FIELD_1   DS    CL18
                 DS    CL2
DETAIL_VALUE_1   DS    CL18
DETAIL_TD_1E     DS    CL5
DETAIL_TD_2      DS    CL4
DETAIL_FIELD_2   DS    CL18
                 DS    CL2
DETAIL_VALUE_2   DS    CL18
DETAIL_TD_2E     DS    CL5
DETAIL_TRE       DS    CL4
DETAIL_BR        DS    CL4
DETAILL          EQU   *-DETAIL
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
TASK_DSECT       DSECT
TASK_NAME        DS    CL8
TASK_ID          DS    X
TASK_ECB_OFFSET  DS    AL2
TASK_WORK_OFFSET DS    AL2
TASK_DSECTL      EQU   *-TASK_DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 CVT   DSECT=YES,LIST=NO
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
$CR              EQU   X'0D'
$LF              EQU   X'0A'
                 COPY  REGEQU
         END  HTTPLOCL
