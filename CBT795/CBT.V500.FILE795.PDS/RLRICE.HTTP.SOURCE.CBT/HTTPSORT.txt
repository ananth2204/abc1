*---------------------------------------------------------------------*
*                                                                     *
*  Module name: HTTPSORT                                              *
*                                                                     *
*  Sort the URLs in the URL DATASPACE into descending EBCDIC sequence *
*                                                                     *
* ------------------------------------------------------------------- *
HTTPSORT CSECT
HTTPSORT AMODE 31
HTTPSORT RMODE ANY
         USING HTTPSORT,R12                   DEFINE BASE
         USING HTTPCOMM,R11                   DEFINE BASE
         STM   R14,R12,12(R13)                SAVE ENTRY REGISTERS
         LR    R12,R15                        COPY BASE
         B     INIT0000                       BRANCH AROUND EYECATCHER
MODID    DC    CL8'HTTPSORT'                  MODULE NAME
         DC    CL8'&SYSDATE'                  DATE OF ASSEMBLY
         DC    CL6'&SYSTIME'                  TIME OF ASSEMBLY
INIT0000 DS    0H
         GETMAIN RU,                          OBTAIN WORK AREA STORAGE +
               LOC=ANY,                                                +
               LV=WORKL
         ST    R1,8(R13)
         ST    R13,4(R1)
         LR    R13,R1
         USING WORK,R13
         USING SERVURL,R9
* ------------------------------------------------------------------- *
*                                                                     *
*      Sort the SERVURL blocks into descending (EBCDIC) sequence.     *
*                                                                     *
*      All SERVURL blocks are the same length.                        *
*      The SERVURL blocks are kept in a DATASPACE.                    *
*      The DATASPACE has already been added to the access list.       *
*      The DATASPACE ALET is in COMM_SORT_ALET.                       *
*                                                                     *
*      The program is in AR mode before, during, and after sorting.   *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
SORT0000 DS    0H
         L     R3,COMM_URLS
         ITRACE ID=SORT,RDATA1=R3
         XC    WORK_SORT_TOP,WORK_SORT_TOP
         LAM   R9,R9,COMM_SORT_ALET
         SAC   512
         SR    R9,R9
         LA    R14,WORK_JUNK
         LA    R15,16
SORT0005 DS    0H
         MVC   0(256,R14),0(R9)
         LA    R14,256(R14)
         LA    R9,256(R9)
         BCT   R15,SORT0005
SORT0010 DS    0H
         CH    R3,H1                          MORE THAN 1?
         BNH   EXIT0000                       NO
         L     R9,WORK_SORT_TOP               FIRST URL
         ITRACE ID=SORTLOOP,                                           +
               RDATA1=R9,                                              +
               RDATA2=R3
         MVC   WORK_URL_LENGTH,SERVURL_URL_LENGTH
         MVC   WORK_URL,SERVURL_URL           INITIALIZE 'HIGH'
         MVC   WORK_SERVER,SERVURL_SERVER
         ST    R9,WORK_URL_ADDRESS
         LR    R2,R3                          COPY NUMBER OF ENTRIES
SORT0020 DS    0H
         L     R1,SERVURL_URL_LENGTH
         C     R1,WORK_URL_LENGTH             SHORTER URL?
         BL    SORT0030                       NO
         L     R1,WORK_URL_LENGTH             USE SHORTEST LENGTH
SORT0030 DS    0H
         BCTR  R1,0
         EX    R1,SORTCLC
         BNH   SORT0040                       NO
         ITRACE ID=NEWHIGH,RDATA1=R9
         MVC   WORK_URL_LENGTH,SERVURL_URL_LENGTH
         MVC   WORK_URL,SERVURL_URL           SAVE 'HIGHEST'
         MVC   WORK_SERVER,SERVURL_SERVER
         ST    R9,WORK_URL_ADDRESS            SAVE ADDRESS
SORT0040 DS    0H
         AH    R9,=Y(SERVURLL)                NEXT URL
         BCT   R2,SORT0020                    LOOP
         L     R9,WORK_SORT_TOP               1ST URL
         L     R1,WORK_URL_ADDRESS
         LAM   R1,R1,COMM_SORT_ALET
         ITRACE ID=SORTSWAP,                                           +
               RDATA1=R1,                                              +
               RDATA2=R9
TEMP     USING SERVURL,R1
         MVC   TEMP.SERVURL_URL_LENGTH,SERVURL_URL_LENGTH
         MVC   TEMP.SERVURL_URL,SERVURL_URL
         MVC   TEMP.SERVURL_SERVER,SERVURL_SERVER
         MVC   SERVURL_URL_LENGTH,WORK_URL_LENGTH
         MVC   SERVURL_URL,WORK_URL
         MVC   SERVURL_SERVER,WORK_SERVER
         LAM   R1,R1,F0
         AH    R9,=Y(SERVURLL)
         ST    R9,WORK_SORT_TOP
         BCT   R3,SORT0010
* ------------------------------------------------------------------- *
*        Exit                                                         *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         LAM   R9,R9,F0
         SAC   0
EXIT0010 DS    0H
         LR    R1,R13
         L     R13,4(R13)                     CALLER'S SAVE AREA
         FREEMAIN RU,                         FREE WORK AREA           +
               A=(1),                                                  +
               LV=WORKL
         LM    R14,R12,12(R13)                RESTORE REGISTERS
         SR    R15,R15                        RETURN CODE
         BR    R14                            RETURN TO CALLER
* ------------------------------------------------------------------- *
SORTCLC  CLC   SERVURL_URL(0),WORK_URL        HIGHER?
* ------------------------------------------------------------------- *
*                                                                     *
*        CONSTANTS                                                    *
*                                                                     *
*-------------------------------------------------------------------- *
F0       DC    F'0'
H1       DC    H'1'
         LTORG
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
WORK                DSECT
                    STDSAVE DSECT=NO
WORK_SERVER         DS      A
WORK_SORT_TOP       DS      A
WORK_URL_ADDRESS    DS      A
WORK_URL_LENGTH     DS      A
WORK_URL            DS      CL256
WORK_JUNK           DS      XL4096
WORKL               EQU     *-WORK
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COMMON  MAIN,TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         WEBURL
         SERVURL
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY REGEQU
         END  HTTPSORT
