         MACRO
         FDEF  &NAME,&TYPE,&LEN,&BASE=
         LCLC  &T
         LCLA  &L
         GBLC  &BASENAME
         AIF   (T'&BASE EQ 'O').NOBASE
.BASE    ANOP
&BASENAME SETC  '&BASE'
         DC    CL8'&BASE'            CONTROL BLOCK NAME
         MEXIT
.NOBASE  ANOP
         AIF   (T'&TYPE EQ 'O').TYPE1
&T       SETC  '&TYPE'
         AGO   .TYPE2
.TYPE1   ANOP
&T       SETC  T'&NAME
.TYPE2   ANOP
         AIF   (T'&LEN EQ 'O').LEN1
&L       SETA  &LEN
         AGO   .LEN2
.LEN1    ANOP
&L       SETA  L'&NAME
.LEN2    ANOP
         DC    CL32'&NAME'           FIELD NAME
         DC    AL2(&NAME-&BASENAME)  DISP WITHIN CONTROL BLOCK
         DC    C'&T'                 FIELD TYPE
         DC    AL2(&L)               SOURCE FIELD LENGTH
         MEND
* ------------------------------------------------------------------- *
*              COMMON DATA MAP                                        *
* ------------------------------------------------------------------- *
         USING HTTPCOMM,R11                   DEFINE BASE
         COMMON   MAIN,TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         SERVBLOK
         URL
*---------------------------------------------------------------------*
*                                                                     *
*  Module name: HTTPSNAP                                              *
*                                                                     *
*  PROCESS SNAP commands entered via the console.                     *
*                                                                     *
*  At entry:                                                          *
*    R1 command address                                               *
*    R2 command length                                                *
*    R3 address of console id                                         *
*                                                                     *
*                                                                     *
*  SNAP dumps can be requested via a "F xxxxx,SNAP" console command   *
*  where xxxxx is the name of the HTTPMAIN STC or JOB address space.  *
*                                                                     *
*  When a SNAP command is entered, HTTPCONS 1) LOADs this module,     *
*  2) links to this code via a BALR, 3) DELETEs this so the storage   *
*  it would occupy can be used for other purposes.  The trade-off is  *
*  the overhead of a LOAD and DELETE each time a SNAP dump is         *
*  requested.                                                         *
*                                                                     *
*                                                                     *
*  The SYSOUT class used for the SNAP dumps is specified on the       *
*  SNAPCLASS control statement in HTTPMAIN control statements.        *
*                                                                     *
*  One possible improvement may be to allow a SYSOUT class to be      *
*  specified on the SNAP command: "F xxxxx,SNAP,c" where c would      *
*  an optional operand.  If "c" is specified it would over-ride       *
*  the SNAPCLASS.                                                     *
*                                                                     *
*                                                                     *
*  The HTTPSNAP SYSOUT files are dynamically allocated with           *
*  FREE=CLOSE.  This "spins off" each SNAP dump so it can be          *
*  printed or deleted from the queue without having to stop the       *
*  HTTPMAIN address space.                                            *
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
HTTPSNAP CSECT
HTTPSNAP AMODE 31
HTTPSNAP RMODE ANY
         USING HTTPSNAP,R12                   DEFINE BASE
         STM   R14,R12,12(R13)                SAVE ENTRY REGISTERS
         LR    R12,R15                        COPY BASE
         B     INIT0000                       BRANCH AROUND EYECATCHER
MODID    DC    CL8'HTTPSNAP'                  MODULE NAME
         DC    CL8'&SYSDATE'                  DATE OF ASSEMBLY
         DC    CL6'&SYSTIME'                  TIME OF ASSEMBLY
INIT0000 DS    0H
         GETMAIN RU,                          WORK AREA STORAGE        +
               LV=WORKL,                      .. SIZE                  +
               LOC=BELOW                      .. MUST BE BELOW
         ST    R13,4(R1)
         ST    R1,8(R13)
         LR    R13,R1                         SAVE WORK AREA ADDRESS
         USING WORK_AREA,R13                  DEFINE BASE
         ITRACE ID=INIT
         MVC   WORK_CID,0(R3)                 COPY CONSOLE ID
         MVC   WORK_WTO(WTOL),WTOI            INITIALIZE WTO
         LA    R7,WORK_WTO
         USING WPL,R7                         DEFINE BASE
         MVI   WORK_HEADING,C' '
         MVC   WORK_HEADING+1(L'WORK_HEADING-1),WORK_HEADING
         MVI   WORK_PRINT_SPACE,C' '          INITIALIZE SPACE
         MVC   WORK_PRINT,WORK_PRINT_SPACE
         MVC   DAIRDDN,SNAPDDN                SET DDNAME
         MVI   DAIRCMD,$DAIRALS               ALLOCATE SNAP SYSOUT
         MVC   DAIRCLS,COMM_SNAP_CLASS
         LA    R1,DAIRBLOK                    REQUEST BLOCK'S ADDRESS
         L     R15,COMM_HTTPDAIR_ADDRESS
         BALR  R14,R15                        LINK TO HTTPDAIR
         OC    DAIRR15,DAIRR15                ALLOCATION SUCCESSFUL?
         BNZ   ERR0010                        NO
* ------------------------------------------------------------------- *
*                                                                     *
*     The output DD has been allocated.                               *
*                                                                     *
*     Open the DCB.                                                   *
*                                                                     *
* ------------------------------------------------------------------- *
         MVC   WORK_DCB(DCBL),DCBI            INITIALIZE THE DCB
         MVC   WORK_OPEN(OPENL),OPENI
         OPEN  WORK_DCB,                                               +
               MODE=31,                                                +
               MF=(E,WORK_OPEN)
         ZAP   WORK_PAGE,P0                   INITIALIZE PAGE NUMBER
         ZAP   WORK_LINE,P0                   INITIALIZE LINE NUMBER
         AP    COMM_SNAP_NUMBER,P1            ADD 1 TO NUMBER DUMPS
         TIME  DEC                            GET DATE AND TIME
         STM   R0,R1,WORK_TIME_DATE           SAVE TIME AND DATE
         AP    WORK_DATE,P1900000             ADD CENTURY TO DATE
         BAL   R10,HEAD0000                   PRINT HEADING
* ------------------------------------------------------------------- *
*     Format fields from the common area                              *
* ------------------------------------------------------------------- *
         ST    R11,WORK_FMT_BASE              SET BASE
         MVC   WORK_FMT_FDEF,A_HTTPCOMM_FDEF
         BAL   R8,FMT0000                     FORMAT THE SCOMM FIELDS
* ------------------------------------------------------------------- *
*      List the active URLs                                           *
* ------------------------------------------------------------------- *
         ITRACE ID=URLS
         XC    WEBURL(WEBURLL),WEBURL         INITIALIZE WEBURL BLOCK
         MVC   WEBURL_EYE,C_WEBURL
         LA    R0,WEBURLL
         ST    R0,WEBURL_LENGTH               SET LENGTH
         LA    R0,WORK_DCB                    DCB ADDRESS
         ST    R0,WEBURL_SERVBLOK             PASS TO HTTPURL
         MVI   WEBURL_STATUS,$URL_SNAP
         LA    R1,WEBURL
SNAP0010 DS    0H
         L     R2,COMM_URL_QUEUE              1ST ON QUEUE
         ST    R2,WEBURL_NEXT
         CS    R2,R1,COMM_URL_QUEUE           ADD NEW BLOCK
         POST  COMM_URL_WORK_ECB              WAKE UP URL SUBTASK
         WAIT  ECB=WEBURL_ECB                 WAIT FOR URL SUBTASK
* ------------------------------------------------------------------- *
*                                                                     *
*      Print the trace table                                          *
*                                                                     *
* ------------------------------------------------------------------- *
SNAP0100 DS    0H
         ITRACE ID=TRACE
         MVI   WORK_PRINT_CC,C'1'             FORCE TOP OF PAGE
         MVC   WORK_PRINT_DATA(MSG05L),MSG05
         BAL   R9,PRT0000
         ICM   R2,15,TRACE_FIRST              FIRST TRACE TABLE ENTRY
         BZ    CLOSE000                       NO TRACE TABLE
         USING TRACE_ENTRY,R2                 DEFINE BASE
SNAP0110 DS    0H
         OC    TRACE_ENTRY(TRACE_ENTRY_LENGTH),TRACE_ENTRY
         BZ    CLOSE000                       YES
         MVC   WORK_PRINT_DATA(MSG06L),MSG06
         C     R2,TRACE_CURRENT               CURRENT ENTRY?
         BNE   SNAP0120                       NO
         MVC   WORK_PRINT_DATA+(MSG06_CUR-MSG06)(L'MSG06_CUR),=C'CURREN+=>'
               T -->'
SNAP0120 DS    0H
         UNPK  WORK_PRINT_DATA+(MSG06_TCB-MSG06)(7),TRACE_ENTRY_TCB(5)   ADDR
         TR    WORK_PRINT_DATA+(MSG06_TCB-MSG06)(6),HEXCHAR
         MVI   WORK_PRINT_DATA+(MSG06_TCB-MSG06)+6,C' '
         MVC   WORK_PRINT_DATA+(MSG06_MOD-MSG06)(L'MSG06_MOD),TRACE_ENT+
               RY_MODULE_NAME
         MVC   WORK_PRINT_DATA+(MSG06_ID-MSG06)(L'MSG06_ID),TRACE_ENTRY+
               _TRACE_ID
         MVC   WORK_PRINT_DATA+(MSG06_D1-MSG06)(L'MSG06_D1),TRACE_ENTRY+
               _DATA_1
         TR    WORK_PRINT_DATA+(MSG06_D1-MSG06)(L'MSG06_D1),PRINTABLE
         UNPK  WORK_PRINT_DATA+(MSG06_D1X-MSG06)(9),TRACE_ENTRY_DATA_1(+
               5)
         TR    WORK_PRINT_DATA+(MSG06_D1X-MSG06)(8),HEXCHAR
         UNPK  WORK_PRINT_DATA+(MSG06_D1X-MSG06)+8(9),TRACE_ENTRY_DATA_+
               1+4(5)
         TR    WORK_PRINT_DATA+(MSG06_D1X-MSG06)+8(8),HEXCHAR
         MVI   WORK_PRINT_DATA+(MSG06_D1X-MSG06)+8+8,C' '
         MVC   WORK_PRINT_DATA+(MSG06_D2-MSG06)(L'MSG06_D2),TRACE_ENTRY+
               _DATA_2
         TR    WORK_PRINT_DATA+(MSG06_D2-MSG06)(L'MSG06_D2),PRINTABLE
         UNPK  WORK_PRINT_DATA+(MSG06_D2X-MSG06)(9),TRACE_ENTRY_DATA_2(+
               5)
         TR    WORK_PRINT_DATA+(MSG06_D2X-MSG06)(8),HEXCHAR
         UNPK  WORK_PRINT_DATA+(MSG06_D2X-MSG06)+8(9),TRACE_ENTRY_DATA_+
               2+4(5)
         TR    WORK_PRINT_DATA+(MSG06_D2X-MSG06)+8(8),HEXCHAR
         MVI   WORK_PRINT_DATA+(MSG06_D2X-MSG06)+8+8,C' '
         BAL   R9,PRT0000
         C     R2,TRACE_LAST                  END OF TABLE?
         BE    CLOSE000                       YES
         LA    R2,TRACE_ENTRY_LENGTH(R2)
         B     SNAP0110
         DROP  R2
* ------------------------------------------------------------------- *
*     Close the DCB, issue WTO and exit                               *
* ------------------------------------------------------------------- *
CLOSE000 DS    0H
         MVC   WORK_CLOSE(CLOSEL),CLOSEI
         CLOSE (WORK_DCB),                                             +
               MODE=31,                                                +
               MF=(E,WORK_CLOSE)
         MVC   WPLTXT(MSG01L),MSG01           COPY MESSAGE
         BAL   R10,WTO0000                    ISSUE WTO
         B     EXIT0000                       AND EXIT
* ------------------------------------------------------------------- *
*                                                                     *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
FMT0000  DS    0H
         STM   R0,R15,WORK_FMT_SAVE
         L     R2,WORK_FMT_BASE
         L     R3,WORK_FMT_FDEF
         USING FDEF,R3
         MVC   WORK_PRINT_DATA(MSG03L),MSG03
         MVC   WORK_PRINT_DATA+(MSG03_BLOCK-MSG03)(L'MSG03_BLOCK),0(R3)
         UNPK  WORK_UNPACK(9),WORK_FMT_BASE(5)
         TR    WORK_UNPACK,HEXCHAR
         MVC   WORK_PRINT_DATA+(MSG03_ADDR-MSG03)(L'MSG03_ADDR),WORK_UN+
               PACK
         BAL   R9,PRT0000                     PRINT
         LA    R3,8(R3)                       FIRST FIELD DEFINITION
FMT0010  DS    0H
         MVC   WORK_PRINT_DATA(MSG04L),MSG04
         LA    R4,WORK_PRINT_DATA+(MSG04_DATA-MSG04)
         USING FLD,R4
         LA    R5,((L'WORK_PRINT_DATA-MSG04L)/(FLDL+5))
FMT0020  DS    0H
         MVC   FLD_LABEL,FDEF_NAME            COPY FIELD NAME
         MVC   FLD_TYPE+1(1),FDEF_TYPE        FIELD TYPE
         SR    R1,R1                          CLEAR REGISTER
         ICM   R1,3,FDEF_LENGTH               LENGTH
         CVD   R1,WORK_DWORD                  CONVERT TO DECIMAL
         MVC   FLD_LENGTH,=X'40202120'        INITIALIZE WITH EDIT WORD
         ED    FLD_LENGTH,WORK_DWORD+6        EDIT LENGTH
         UNPK  WORK_UNPACK(5),FDEF_DISP(3)    UNPACK DISPLACEMENT
         TR    WORK_UNPACK(4),HEXCHAR         CONVERT TO PRINTABLE
         MVC   FLD_DISP,WORK_UNPACK           COPY DISPLACEMENT
         SR    R14,R14                        CLEAR REGISTER
         ICM   R14,3,FDEF_LENGTH              FIELD LENGTH
         SR    R15,R15                        CLEAR REGISTER
         ICM   R15,3,FDEF_DISP                DISPLACEMENT
         AR    R15,R2                         DATA ADDRESS
         CLI   FDEF_TYPE,C'A'                 ADDRESS?
         BE    FMT0050                        YES
         CLI   FDEF_TYPE,C'C'                 CHARACTER?
         BE    FMT0030                        YES
         CLI   FDEF_TYPE,C'F'                 FULLWORD?
         BE    FMT0060                        YES
         CLI   FDEF_TYPE,C'H'                 HALFWORD?
         BE    FMT0070                        YES
         CLI   FDEF_TYPE,C'P'                 PACKED DECIMAL?
         BE    FMT0080                        YES
         CLI   FDEF_TYPE,C'X'                 HEX?
         BE    FMT0050                        YES
         B     FMT0090
* ----------------------  CHARACTER FIELDS  ------------------------- *
FMT0030  DS    0H
         CH    R14,H8                         LONGER THAN 8 CHARACTERS?
         BNH   FMT0040                        NO
         LH    R14,H8                         LIMIT TO 8
FMT0040  DS    0H
         BCTR  R14,0
         EX    R14,FMTMVC01                   COPY DATA
         B     FMT0090
* ----------------------  ADDRESS FIELDS  --------------------------- *
FMT0050  DS    0H
         UNPK  WORK_UNPACK(9),0(5,R15)        UNPACK THE DATA
         TR    WORK_UNPACK,HEXCHAR            TRANSLATE TO PRINTABLE
         SLL   R14,1                          MULTIPLY BY 2
         BCTR  R14,0                          MINUS 1
         LA    R1,FLD_VALUE+7
         SR    R1,R14
         EX    R14,FMTMVC02                   COPY TO PRINT AREA
         B     FMT0090
* ----------------------  FULLWORD FIELDS  -------------------------- *
FMT0060  DS    0H
         L     R1,0(R15)                      LOAD VALUE
         CVD   R1,WORK_DWORD                  CONVERT TO DECIMAL
         MVC   FLD_VALUE,=X'4020202020202120'
         ED    FLD_VALUE,WORK_DWORD+4         EDIT VALUE
         B     FMT0090
* ----------------------  FULLWORD FIELDS  -------------------------- *
FMT0070  DS    0H
         LH    R1,0(R15)                      LOAD VALUE
         CVD   R1,WORK_DWORD                  CONVERT TO DECIMAL
         MVC   FLD_VALUE,=X'4020202020202120'
         ED    FLD_VALUE,WORK_DWORD+4         EDIT VALUE
         B     FMT0090
* ----------------------  PACKED DECIMAL ---------------------------- *
FMT0080  DS    0H
         BCTR  R14,0
         EX    R14,FMTZAP                     ZAP IT INTO DWORD
         MVC   FLD_VALUE,=X'4020202020202120'
         ED    FLD_VALUE,WORK_DWORD+4         EDIT VALUE
FMT0090  DS    0H
         LA    R4,FLDL+5(R4)                  NEXT IN OUTPUT
         LA    R3,FDEFL(R3)                   NEXT FIELD DEFINITION
         CLI   0(R3),X'FF'                    END OF FIELDS?
         BE    FMT0100                        YES
         BCT   R5,FMT0020                     FORMAT NEXT FIELD
         BAL   R9,PRT0000                     PRINT
         B     FMT0010
FMT0100  DS    0H
         BAL   R9,PRT0000                     PRINT LAST LINE
         BAL   R9,PRT0000                     PRINT BLANK LINE
         LM    R0,R15,WORK_FMT_SAVE           RESTORE REGISTERS
         BR    R8
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
FMTMVC01 MVC   FLD_VALUE(0),0(R15)            COPY DATA
FMTMVC02 MVC   0(0,R1),WORK_UNPACK            COPY DATA
FMTZAP   ZAP   WORK_DWORD,0(0,R15)            ZAP PACKED DECIMAL
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
PRT0000  DS    0H
         CP    WORK_LINE,MAXLINE
         BL    PRT0010
         BAL   R10,HEAD0000                   PRINT NEXT HEADING
PRT0010  DS    0H
         PUT   WORK_DCB,WORK_PRINT            PRINT
         AP    WORK_LINE,P1                   ADD TO LINE COUNT
         MVC   WORK_PRINT,WORK_PRINT_SPACE
         BR    R9
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
HEAD0000 DS    0H
         MVC   WORK_HEADING(HEADINGL),HEADING
         ED    WORK_HEADING+(HEAD_DATE-HEADING)(L'HEAD_DATE),WORK_DATE
         UNPK  WORK_UNPACK(7),WORK_TIME(4)
         MVC   WORK_HEADING+(HEAD_TIME-HEADING)+0(2),WORK_UNPACK+0
         MVC   WORK_HEADING+(HEAD_TIME-HEADING)+3(2),WORK_UNPACK+2
         MVC   WORK_HEADING+(HEAD_TIME-HEADING)+6(2),WORK_UNPACK+4
         MVI   WORK_HEADING+(HEAD_TIME-HEADING)+2,C':'
         MVI   WORK_HEADING+(HEAD_TIME-HEADING)+5,C':'
         ED    WORK_HEADING+(HEAD_DUMP-HEADING)(L'HEAD_DUMP),COMM_SNAP_+
               NUMBER
         AP    WORK_PAGE,P1
         ED    WORK_HEADING+(HEAD_PAGE-HEADING)(L'HEAD_PAGE),WORK_PAGE
         PUT   WORK_DCB,WORK_HEADING
         MVI   WORK_HEADING,C' '
         MVC   WORK_HEADING+1(L'WORK_HEADING-1),WORK_HEADING
         LA    R1,((L'WORK_PRINT_DATA-MSG04L)/(FLDL+5))
         LA    R15,WORK_HEADING+(MSG04_DATA-MSG04)+1
HEAD0010 DS    0H
         MVC   0(FLD_HEADL,R15),FLD_HEAD
         LA    R15,FLD_HEADL+5(R15)
         BCT   R1,HEAD0010
         PUT   WORK_DCB,WORK_HEADING
         MVI   WORK_HEADING,C' '
         MVC   WORK_HEADING+1(L'WORK_HEADING-1),WORK_HEADING
         ZAP   WORK_LINE,P1
         BR    R10
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0010  DS    0H
         MVC   WPLTXT(MSG02L),MSG02
         UNPK  WPLTXT+(MSG02_R15-MSG02)(5),DAIRR15(3)
         TR    WPLTXT+(MSG02_R15-MSG02)(4),HEXCHAR
         MVI   WPLTXT+(MSG02_R15-MSG02)+4,C' '
         UNPK  WPLTXT+(MSG02_ERR-MSG02)(5),DAIRERR(3)
         TR    WPLTXT+(MSG02_ERR-MSG02)(4),HEXCHAR
         MVI   WPLTXT+(MSG02_ERR-MSG02)+4,C' '
         UNPK  WPLTXT+(MSG02_INFO-MSG02)(5),DAIRINFO(3)
         TR    WPLTXT+(MSG02_INFO-MSG02)(4),HEXCHAR
         MVI   WPLTXT+(MSG02_INFO-MSG02)+4,C' '
         BAL   R10,WTO0000
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
WTO0000  DS    0H
         ITRACE ID=WTO
         WTO   MF=(E,WORK_WTO),               ISSUE WTO                +
               CONSID=WORK_CID                .. TO SAME CONSOLE
         MVI   WPLTXT,C' '
         MVC   WPLTXT+1(99),WPLTXT
         BR    R10
* ------------------------------------------------------------------- *
*        Exit                                                         *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=EXIT
         LR    R1,R13                         COPY SAVE AREA ADDRESS
         L     R13,4(R13)                     CALLER'S SAVE AREA
         FREEMAIN RU,                         FREE WORK AREA           +
               A=(1),                                                  +
               LV=WORKL
         LM    R14,R12,12(R13)                RESTORE REGISTERS
         SR    R15,R15                        RETURN CODE
         BR    R14                            RETURN TO CALLER
* ------------------------------------------------------------------- *
*                                                                     *
*        CONSTANTS                                                    *
*                                                                     *
* ------------------------------------------------------------------- *
DCBI     DCB   DDNAME=HTTPSNAP,                                        +
               DSORG=PS,                                               +
               LRECL=133,                                              +
               RECFM=FBA,                                              +
               MACRF=PM
DCBL     EQU   *-DCBI
OPENI    OPEN  (*,OUTPUT),MF=L,MODE=31
OPENL    EQU   *-OPENI
CLOSEI   CLOSE (*),MF=L,MODE=31
CLOSEL   EQU   *-CLOSEI

A_HTTPCOMM_FDEF DC  A(HTTPCOMM_FDEF)
A_SERVBLOK_FDEF DC  A(SERVBLOK_FDEF)

WTOI     WTO   '                                                       +
                                                                     ',+
               CONSID=,                                                +
               MF=L
WTOL     EQU   *-WTOI

F0          DC    F'0'
H8          DC    H'8'

P0          DC    P'0'
P1          DC    P'1'
P1900000    DC    P'1900000'
MAXLINE     DC    P'65'
SNAPDDN     DC    CL8'HTTPSNAP'
C_WEBURL    DC    CL8'WEBURL  '

HEXCHAR     EQU   *-C'0'
            DC    C'0123456789ABCDEF'

HEADING     DS    0C
            DC    C'1'
HEAD_DATE   DC    X'40212020204B202020'
            DC    CL2' '
HEAD_TIME   DS    CL8' '
            DC    CL29' '
            DC    C'HTTP MAIN SNAP DUMP'
HEAD_DUMP   DC    X'40206B2020206B202120'
            DC    CL40' '
HEAD_PAGE   DC    X'402020202120'
HEADINGL    EQU   *-HEADING

FLD_HEAD    DC    CL32'LABEL'
            DC    CL2' '
            DC    CL4'TYPE'
            DC    CL3' '
            DC    CL3'LEN'
            DC    CL2' '
            DC    CL4'DISP'
            DC    CL3' '
            DC    CL7'VALUE'
FLD_HEADL   EQU   *-FLD_HEAD

MSG01       DS    0C
            DC    C'HTTPSNAP01I'
            DC    C'SNAP DUMP complete'
MSG01L      EQU   *-MSG01

MSG02       DS    0C
            DC    C'HTTPSNAP02E '
            DC    C'Allocation fro SNAP DUMP failed.  R15='
MSG02_R15   DC    CL4' '
            DC    C'  ERROR='
MSG02_ERR   DC    CL4' '
            DC    C' INFO='
MSG02_INFO  DC    CL4' '
MSG02L      EQU   *-MSG02

MSG03       DC    C'HTTPSNAP03I'
            DC    C' '
MSG03_BLOCK DC    CL8' '
            DC    C' AT '
MSG03_ADDR  DC    CL8' '
MSG03L      EQU   *-MSG03

MSG04       DC    C'HTTPSNAP04I'
            DC    C' '
MSG04_DATA  DS    C
MSG04L      EQU   *-MSG04

MSG05       DC    C'HTTPSNAP05I'
            DC    CL10' '
            DC    C'-------------  TRACE TABLE ------------'
MSG05L      EQU   *-MSG05

MSG06       DS    0C
            DC    C'          '
MSG06_CUR   DC    CL11' '
            DC    CL03' '
MSG06_TCB   DC    CL06' '
            DC    CL03' '
MSG06_MOD   DC    CL05' '
            DC    CL03' '
MSG06_ID    DC    CL08' '
            DC    CL03' '
MSG06_D1    DC    CL08' '
            DC    C' '
MSG06_D1X   DC    CL16' '
            DC    CL03' '
MSG06_D2    DC    CL08' '
            DC    C' '
MSG06_D2X   DS    CL16' '
            DC    C' '
MSG06L      EQU   *-MSG06

*-------------------  0 1 2 3 4 5 6 7 8 9 A B C D E F  --------------*
PRINTABLE DS    0C
          DC    XL16'40404040404040404040404040404040'   x'00' - X'0F'
          DC    XL16'40404040404040404040404040404040'   X'10' - X'1F'
          DC    XL16'40404040404040404040404040404040'   X'20' - X'2F'
          DC    XL16'40404040404040404040404040404040'   X'30' - X'3F'
          DC    XL16'404040404040404040404A4B4C4D4E4F'   X'40' - X'4F'
          DC    XL16'504040404040404040405A5B5C5D5E5F'   X'50' - X'5F'
          DC    XL16'60614040404040404040406B6C6D6E6F'   X'60' - X'6F'
          DC    XL16'404040404040404040407A7B7C7D7E7F'   X'70' - X'7F'
          DC    XL16'40404040404040404040408B8C8D8E8F'   X'80' - X'8F'
          DC    XL16'40404040404040404040409B9C9D9E9F'   X'90' - X'9F'
          DC    XL16'4040404040404040404040ABACADAEAF'   X'A0' - X'AF'
          DC    XL16'4040404040404040404040BBBCBDBEBF'   X'B0' - X'BF'
          DC    XL16'40C1C2C3C4C5C6C7C8C9404040404040'   X'C0' - X'CF'
          DC    XL16'40D1D2D3D4D5D6D7D8D9404040404040'   X'D0' - X'DF'
          DC    XL16'4040E2E3E4E5E6E7E8E9404040404040'   X'E0' - X'EF'
          DC    XL16'F0F1F2F3F4F5F6F7F8F9404040404040'   X'F0' - X'FF'

          LTORG

* ------------------------------------------------------------------- *
*              Common area FDEFs                                      *
* ------------------------------------------------------------------- *
HTTPCOMM_FDEF  DS    0C
               FDEF  BASE=HTTPCOMM
               FDEF  TRACE_FIRST
               FDEF  TRACE_LAST
               FDEF  TRACE_CURRENT
               FDEF  TRACE_TABLE_ADDRESS
               FDEF  TRACE_TABLE_SIZE
               FDEF  X_TRACE_FLAG
               FDEF  COMM_DXD_ADDRESS
               FDEF  COMM_HTTPMAIN_ADDRESS
               FDEF  COMM_HTTPCONS_ADDRESS
               FDEF  COMM_HTTPDAIR_ADDRESS
               FDEF  COMM_HTTPLOCL_ADDRESS
               FDEF  COMM_HTTPLOG_ADDRESS
               FDEF  COMM_HTTPPRT_ADDRESS
               FDEF  COMM_HTTPSERV_ADDRESS
               FDEF  COMM_HTTPTCP_ADDRESS
               FDEF  COMM_HTTPURL_ADDRESS
               FDEF  COMM_HTTPWEB_ADDRESS
               FDEF  COMM_TERMINATION_ECB
               FDEF  COMM_PRINT_TCB
               FDEF  COMM_PRINT_QUEUE
               FDEF  COMM_PRINT_TERM_ECB
               FDEF  COMM_PRINT_WORK_ECB
               FDEF  COMM_CONSOLE_TCB
               FDEF  COMM_CONSOLE_TERM_ECB
               FDEF  COMM_CONSOLE_WORK_ECB
               FDEF  COMM_LOG_TCB
               FDEF  COMM_LOG_TERM_ECB
               FDEF  COMM_LOG_WORK_ECB
               FDEF  COMM_LOG_QUEUE
               FDEF  COMM_URL_TCB
               FDEF  COMM_URL_TERM_ECB
               FDEF  COMM_URL_WORK_ECB
               FDEF  COMM_URL_QUEUE
               FDEF  COMM_WEB_TCB
               FDEF  COMM_WEB_TERM_ECB
               FDEF  COMM_WEB_WORK_ECB
               FDEF  COMM_WEB_COMPLETED
               FDEF  COMM_HTTPTCP_TCB
               FDEF  COMM_HTTPTCP_TERM_ECB
               FDEF  COMM_HTTPTCP_WORK_ECB
               FDEF  COMM_LOCAL_TCB
               FDEF  COMM_LOCAL_TERM_ECB
               FDEF  COMM_LOCAL_WORK_ECB
               FDEF  COMM_LOCAL_URL_QUEUE
               FDEF  COMM_SERVERS
               FDEF  COMM_NEW_SERVERS
               FDEF  COMM_LOST_SERVERS
               FDEF  COMM_NBR_URLS
               FDEF  COMM_URL_SPACE_TOKEN,X,4
               FDEF  COMM_URLS_RECEIVED
               FDEF  COMM_WEB_PORT
               FDEF  COMM_WEB_PORT_CHAR
               FDEF  COMM_SERVER_PORT
               FDEF  COMM_SERVER_PORT_CHAR
               FDEF  COMM_MAX_LINES
               FDEF  COMM_SNAP_CLASS
               FDEF  COMM_SNAP_NUMBER
               FDEF  COMM_SUBTASKS
               FDEF  COMM_FLAGS
               FDEF  COMM_TRACE_FLAGS
               FDEF  COMM_EOJ
               FDEF  COMM_HTTPTCP_CLIENT_NAME
               FDEF  COMM_HTTPTCP_CLIENT_TASK
               FDEF  COMM_HTTPWEB_CLIENT_NAME
               FDEF  COMM_HTTPWEB_CLIENT_TASK
               FDEF  COMM_HTTPLOCL_CLIENT_NAME
               FDEF  COMM_HTTPLOCL_CLIENT_TASK
               DC    X'FF'
* ------------------------------------------------------------------- *
*              SERVBLOK FDEF's                                        *
* ------------------------------------------------------------------- *
SERVBLOK_FDEF  DS    0C
               FDEF  BASE=SERVER_EYE
               FDEF  SERVER_EYE
               FDEF  SERVER_NEXT
               FDEF  SERVER_TCB
               FDEF  SERVER_HTTP_COMMON
               FDEF  SERVER_WORK_ADDRESS
               FDEF  SERVER_URL
               FDEF  SERVER_TCPIP_RETURN_CODE
               FDEF  SERVER_TCPIP_ERROR_NUMBER
               FDEF  SERVER_GIVESOCKET_ECB
               FDEF  SERVER_TERM_ECB
               FDEF  SERVER_WORK_ECB
               FDEF  SERVER_CONNECT_DATE
               FDEF  SERVER_CONNECT_TIME
               FDEF  SERVER_LAST_DATE
               FDEF  SERVER_LAST_TIME
               FDEF  SERVER_URLS
               FDEF  SERVER_SENDS
               FDEF  SERVER_RECEIVES
               FDEF  SERVER_BYTES_RECEIVED
               FDEF  SERVER_BYTES_SENT
               FDEF  SERVER_SOCKET
               FDEF  SERVER_MAX_SOCKET
               FDEF  SERVER_SUBTASK
               FDEF  SERVER_TCPIP_FUNCTION
               DC    X'FF'
* ------------------------------------------------------------------- *
*              WORK AREA                                              *
* ------------------------------------------------------------------- *
WORK_AREA           DSECT
                    STDSAVE  DSECT=NO
WORK_DWORD          DS   D
WORK_FMT_SAVE       DS   16F
WORK_CID            DS   F
WORK_WTO            DS   0A,(WTOL)X
WORK_DCB            DS   0F,(DCBL)X
WORK_OPEN           DS   0F,(OPENL)X
WORK_CLOSE          DS   0F,(CLOSEL)X

WORK_FMT_BASE       DS   F
WORK_FMT_FDEF       DS   F

WORK_TIME_DATE      DS   0F
WORK_TIME           DS   XL4
WORK_DATE           DS   PL4

WORK_UNPACK         DS   CL10
                    DS   X

WORK_PAGE           DS   PL3
WORK_LINE           DS   PL3

WORK_PRINT_SPACE    DS   C
WORK_PRINT          DS   0CL133
WORK_PRINT_CC       DS   C
WORK_PRINT_DATA     DS   CL132

WORK_HEADING        DS   CL133
                    DAIRBLOK DSECT=NO
                    WEBURL   DSECT=NO
WORKL               EQU  *-WORK_AREA
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
FDEF        DSECT
FDEF_NAME   DS    CL32                 FIELD NAME
FDEF_DISP   DS    AL2                  DISPLACEMENT TO FIELD
FDEF_TYPE   DS    C                    FIELD TYPE
FDEF_LENGTH DS    AL2                  FIELD LENGTH
FDEFL       EQU   *-FDEF
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
FLD        DSECT
FLD_LABEL  DS    CL32'LABEL'
           DS    CL2' '
FLD_TYPE   DS    CL4'TYPE'
           DS    CL2' '
FLD_LENGTH DS    CL4'LEN'
           DS    CL2' '
FLD_DISP   DS    CL4' '
           DS    CL2' '
FLD_VALUE  DS    CL8'VALUE'
FLDL       EQU   *-FLD
* ------------------------------------------------------------------- *
*              WTO PARM LIST                                          *
* ------------------------------------------------------------------- *
         IEZWPL DSECT=YES
* ------------------------------------------------------------------- *
*              EQUATES                                                *
* ------------------------------------------------------------------- *
         COPY REGEQU
         END  HTTPSNAP
