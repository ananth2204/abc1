          MACRO
          ITRACE &ID=,                                                 +
               &DATA1=,&RDATA1=,                                       +
               &DATA2=,&RDATA2=,                                       +
               &DATA3=,&RDATA3=
          AIF    (T'&ID EQ 'O').SUBNOID
          AIF    (T'&RDATA1 NE 'O' AND T'&DATA1 NE 'O').SUBDUP1
          AIF    (T'&RDATA2 NE 'O' AND T'&DATA2 NE 'O').SUBDUP2
          AIF    (T'&RDATA2 EQ 'O' AND T'&DATA2 EQ 'O').SUB001
          AIF    (T'&RDATA1 NE 'O' AND T'&DATA1 NE 'O').SUBNO1
.SUB001   ANOP
          AIF    (T'&DATA1 EQ 'O').SUB002
          MVC    WORK_TRACE_DATA1,&DATA1
.SUB002   ANOP
          AIF    (T'&RDATA1 EQ 'O').SUB003
          ST     &RDATA1,WORK_TRACE_DATA1
.SUB003   ANOP
          AIF    (T'&DATA2 EQ 'O').SUB004
          MVC    WORK_TRACE_DATA2,&DATA2
.SUB004   ANOP
          AIF    (T'&RDATA2 EQ 'O').SUB005
          ST     &RDATA2,WORK_TRACE_DATA2
.SUB005   ANOP
          AIF    (T'&DATA3 EQ 'O').SUB006
          MVC    WORK_TRACE_DATA3,&DATA3
          AGO    .SUB007
.SUB006   ANOP
          AIF    (T'&RDATA3 EQ 'O').SUB007
          ST     &RDATA3,WORK_TRACE_DATA3
.SUB007   ANOP
          BAL    R14,TRACE000
          DC     CL8'&ID'
          MEXIT
.SUBDUP1  ANOP
          MNOTE  12,'DO NOT USE BOTH DATA1 AND RDATA1'
          MEXIT
.SUBDUP2  ANOP
          MNOTE  12,'DO NOT USE BOTH DATA2 AND RDATA2'
          MEXIT
.SUBNO1   ANOP
          MNOTE  12,'(R)DATA2 NOT VALID WITHOUT (R)DATA1'
          MEXIT
.SUBNOID  ANOP
          MNOTE  12,'ID IS REQUIRED'
          MEND
* ------------------------------------------------------------------- *
*                                                                     *
*                                                                     *
*    This HTTP application makes PDS members available for viewing    *
*    via a web-browser such as Internet Explorer.                     *
*                                                                     *
*    PDSes that are to have their members made accessible must        *
*    be defined via a DD, HTML, or RAW control statement.  PDSes      *
*    are not dynamically allocated so there must be a JCL statement   *
*    for each PDS defined.                                            *
*                                                                     *
*                                                                     *
*    Control statements are:                                          *
*       PORT       Specifies the PORT number for communicating        *
*                  with the HTTP main address space.                  *
*                                                                     *
*       DD         Defines a PDS whose members will be viewable.      *
*                                                                     *
*       HTML       Defines a PDS whose members will be viewable and   *
*                  are HTML statements.                               *
*                                                                     *
*       RAW        Defines a PDS whose members whose contents are     *
*                  binary data and will be viewable.                  *
*                                                                     *
*       TRACE      If present in the start up statements, the data    *
*                  written to the web users will also be written      *
*                  to SYSPRINT.                                       *
*                                                                     *
*                                                                     *
*                                                                     *
*   The difference between PDSes defined via DD and HTML statements   *
*   is that PDSes defined by "DD" statements contain EBCDIC data      *
*   that is not HTML.  For example you may want to make members of    *
*   a documentation data set viewable via the web.  Members of PDSes  *
*   defined via "DD" will have some HTML appended before and after    *
*   the contents of the file.  This HTML will instruct the web        *
*   browser to present the data as "preformatted" data.  The data is  *
*   translated from EBCDIC to ASCII.  PDSes defined by "HTML"         *
*   statements are assumed to be HTML files in EBCDIC.  The data is   *
*   translated from EBCDIC to ASCII but no HTML is added before or    *
*   after the contents of the member(s).  PDSes defined by "RAW"      *
*   statements are considered binary and sent completely as is.       *
*   Examples of this type of PDSes would be GIF, JPEG, etc data.      *
*   All PDSes may be in either fixed (F or FB) or variable (V or VB)  *
*   record formats.  For variable length record files the length      *
*   data is removed in all cases.                                     *
*                                                                     *
*                                                                     *
*                                                                     *
*   This program does not do any sort of buffering other than what    *
*   is automatic by MVS QSAM and BSAM access methods.  There is no    *
*   provision for any dynamic data.  All files are considered static  *
*   data.  If the PDSes are allocated with DISP=SHR on the JCL DD     *
*   statements, the members can be updated while this utility is      *
*   running.  This can cause the server to ABEND if the update        *
*   causes the library to 'go into a new extent'.                     *
*                                                                     *
*                                                                     *
*   As is there is no security checking.  Any member that is readable *
*   by this utility will be viewable to any web user that has access  *
*   to the URLs served by this utility.                               *
*                                                                     *
*                                                                     *
*   The utility defines each DD defined (by DD, HTML, or RAW)         *
*   statements as a URLs to the HTTP main address space.              *
*   You may want to read more about how the HTTP main address         *
*   space works.                                                      *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
HTTPAPP1 CSECT
HTTPAPP1 AMODE 31
HTTPAPP1 RMODE 24
         STM   R14,R12,12(R13)                SAVE REGISTERS
         LR    R12,R15                        SET BASE REGISTER
         USING HTTPAPP1,R12,R11               DEFINE PROGRAM BASE
         B     INIT0000
MODID    DC    CL8'HTTPAPP1'
         DC    CL8'&SYSDATE'
         DC    CL6'&SYSTIME'
INIT0000 DS    0H
         LA    R11,2048(R12)                  SET
         LA    R11,2048(R11)                    2ND BASE
         GETMAIN RU,                          GETMAIN WORK AREA        +
               LV=WORKL,                                               +
               LOC=BELOW
         ST    R1,8(R13)
         ST    R13,4(R1)
         LR    R13,R1
         USING WORK,R13                       DEFINE BASE
         MVI   WORK_FLAGS,0                   INITIALIZE FLAGS
         MVI   WORK_RC,0                      INITIALIZE RETURN CODE
         LOAD  EP=HTTPSUB                     LOAD HTTPSUB
         ST    R0,HTTP_HTTPSUB                SAVE HTTPSUB ENTRY POINT
         XC    HTTP_WORKAREA,HTTP_WORKAREA    INITIALIZE WORKAREA ADDR
         MVC   WORK_SYSIN(SYSINL),SYSINI      INIITALIZE SYSIN DD
         MVC   WORK_OCPL(OPENL),OPENI         INITIALIZE OPEN PARM LIST

         MVC   WORK_ID,MODID
         GETMAIN RU,                                                   +
               LV=1002*TRACE_ENTRYL,                                   +
               LOC=BELOW
         ST    R1,WORK_TRACE_AREA
         SRL   R1,5
         SLL   R1,5
         LA    R1,TRACE_ENTRYL(R1)
         ST    R1,WORK_TRACE_1ST
         MVC   WORK_TRACE_1ST-4(4),=C'1STX'
         ST    R1,WORK_TRACE_CURR
         MVC   WORK_TRACE_CURR-4(4),=C'CURR'
         AH    R1,=Y(499*TRACE_ENTRYL)
         ST    R1,WORK_TRACE_LAST
         MVC   WORK_TRACE_LAST-4(4),=C'LAST'
         ITRACE ID=INIT
         OPEN  (WORK_SYSIN,INPUT),            OPEN SYSIN               +
               MODE=31,                                                +
               MF=(E,WORK_OCPL)
         MVC   WORK_SYSPRINT(SYSPRINTL),SYSPRINTI
         OPEN  (WORK_SYSPRINT,OUTPUT),        OPEN SYSPRINT            +
               MODE=31,                                                +
               MF=(E,WORK_OCPL)
         MVI   PR_CC,C' '                     INITIALIZE
         MVC   PR_LINE,PR_CC                    PRINT I/O AREA
         ZAP   PAGE_NBR,P0                    INITIALIZE PAGE NBR
         MVC   WORK_INDEX_NAME,BLANKS         INITIALIZE NAME
         ST    R13,WORK_ESTAE_WORK            PASS R13 TO ESTAE EXIT
         LA    R1,ERR0060                     RETRY POINT
         ST    R1,WORK_ESTAE_RETRY            PASS TO ESTAE EXIT
         BAL   R9,HEAD0000                    PRINT HEADING
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
INIT0010 DS    0H
         GET   WORK_SYSIN,WORKCARD            READ A CONTROL STATEMENT
         MVC   PR_LINE(L'WORKCARD),WORKCARD
         BAL   R9,PRT0000                     PRINT
         CLI   WORKCARD,C'*'                  COMMENT?
         BE    INIT0010                       YES
         CLC   PORTSTMT,WORKCARD              PORT STATEMENT?
         BE    INIT0020                       YES
         CLC   INDXSTMT,WORKCARD              INDEX STATEMENT
         BE    INIT0030                       YES
         CLC   HTMLSTMT,WORKCARD              DD STATEMENT?
         BE    INIT0100                       YES
         CLC   DDSTMT,WORKCARD                DD STATEMENT?
         BE    INIT0100                       YES
         CLC   RAWSTMT,WORKCARD               RAW STATEMENT?
         BE    INIT0100                       YES
         CLC   TRACESTMT,WORKCARD             TRACE STATEMENT?
         BE    INIT0200                       YES
         OI    WORK_FLAGS,$ERROR              SET ERROR INDICATOR
         MVC   PR_LINE(MSG01L),MSG01
         BAL   R9,PRT0000
         B     INIT0010
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
INIT0020 DS    0H
         OI    WORK_FLAGS,$PORT               SET PORT NBR INDICATOR
         PACK  WORK_DWORD,WORKCARD+9(5)       PACK PORT NBR
         CVB   R1,WORK_DWORD                  CONVERT TO BINARY
         STH   R1,HTTP_PORT                   SET PORT NBR
         ITRACE ID=PORT,                                               +
               DATA1=WORKCARD+9,                                       +
               DATA2=HTTP_PORT
         B     INIT0010
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
INIT0030 DS    0H
         MVC   WORK_INDEX_NAME,WORKCARD+9     SET PDS INDEX MEMBER NAME
         ITRACE ID=INDEX,                                              +
               DATA1=WORKCARD+9
         B     INIT0010
* ------------------------------------------------------------------- *
*                                                                     *
*        Process RAW statements                                       *
*                                                                     *
* ------------------------------------------------------------------- *
INIT0100 DS    0H
         ITRACE ID=DD,                                                 +
               DATA1=WORKCARD+0,                                       +
               DATA2=WORKCARD+9
         CLI   WORKCARD+09,C' '               NAME START WITH A BLANK?
         BE    INIT0150                       YES
         LA    R2,WORKCARD+71                 END OF DATA
         LA    R3,72-9                        MAX LENGTH
INIT0120 DS    0H
         CLI   0(R2),C' '                     BLANK?
         BNE   INIT0130                       NO
         BCTR  R2,0                           MINUS 1
         BCT   R3,INIT0120                    LOOP
         B     INIT0010
* --------------- Verify DD name is not too long -------------------- *
INIT0130 DS    0H
         CH    R3,H8                          DD NAME TOO LONG?
         BH    INIT0160                       YES
* --------------- Obtain storage for the DD ------------------------- *
         GETMAIN RU,                          GETMAIN A DD_BLOK        +
               LV=DD_BLOKL,                                            +
               LOC=BELOW
         LR    R8,R1                          COPY ADDRESS
         USING DD_BLOK,R8                     DEFINE BASE
         MVC   DD_NEXT,WORK_DD_CHAIN
         ST    R8,WORK_DD_CHAIN               ADD NEW BLOCK
         ST    R3,DD_DDNAME_LENGTH            SET DDNAME'S LENGTH
         XC    DD_DEPTH,DD_DEPTH              INITIALIZE DEPTH
         MVC   DD_DDNAME,WORKCARD+9           COPY DD NAME
         LA    R7,DD_DCB_INFO                 DCB INFO
         USING DCB_DSECT,R7
         LA    R6,$NEST_DEPTH
         MVI   DD_TYPE,$DD_NORMAL
         CLI   WORKCARD,C'D'                  NORMAL DD?
         BE    INIT0140                       YES
         MVI   DD_TYPE,$DD_HTML
         CLI   WORKCARD,C'H'                  HTML DD?
         BE    INIT0140                       YES
         MVI   DD_TYPE,$DD_RAW                SET TYPE TO 'RAW'
INIT0140 DS    0H
         MVC   DCB_DCB(MEMDCBL),MEMDCBI       COPY DCB
         USING IHADCB,DCB_DCB                 DEFINE BASE
         MVC   DCBDDNAM,WORKCARD+9            COPY DD NAME
         OPEN  (DCB_DCB,INPUT),               OPEN DCB                 +
               MODE=31,                                                +
               MF=(E,WORK_OCPL)

         LA    R7,DCB_DSECTL(,R7)             NEXT DCB
         BCT   R6,INIT0140                    LOOP
         B     INIT0010
* ----------------- DD NAME BLANK ----------------------------------- *
INIT0150 DS    0H
         OI    WORK_FLAGS,$ERROR              SET ERROR INDICATOR
         MVC   PR_LINE(MSG05L),MSG05
         BAL   R9,PRT0000
         MVI   WORK_RC,4
         B     EXIT0000
* ----------------- DD name too long--------------------------------- *
INIT0160 DS    0H
         OI    WORK_FLAGS,$ERROR              SET ERROR INDICATOR
         MVC   PR_LINE(MSG10L),MSG10
         BAL   R9,PRT0000
         MVI   WORK_RC,4
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
INIT0200 DS    0H
         ITRACE ID=TRACE
         OI    TRACE_FLAGS,$TRACE             SET INDICATOR
         MVC   WORK_TRACE(TRACEL),TRACEI      COPY DCB
         OPEN  (WORK_TRACE,OUTPUT),           OPEN TRACE DCB           +
               MODE=31,                                                +
               MF=(E,WORK_OCPL)
         LA    R1,WORK_TRACE                  ADDRESS OF DCB
         ST    R1,HTTP_TRACE_DCB              PASS TO HTTPSUB
         GETMAIN RU,                          GETMAIN TRACE I/O AREA   +
               LV=32768,                                               +
               LOC=BELOW
         ST    R1,HTTP_TRACE_IOAREA           PASS ADDRESS TO HTTPSUB
         B     INIT0010
* ------------------------------------------------------------------- *
*                                                                     *
*                                                                     *
*                                                                     *
*     All control statements have been processed.                     *
*                                                                     *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0000 DS    0H
         ITRACE ID=MAIN0000,                                           +
               DATA1=WORK_FLAGS
         TM    WORK_FLAGS,$PORT               PORT NUMBER GIVEN?
         BNO   ERR0030                        NO
* -------------------- Initialize environment ----------------------- *
         ITRACE ID=HTTPINIT
         OI    WORK_FLAGS,$INIT               SET FLAG
         LA    R1,WORK_OUTPUT_BUFFER
         ST    R1,HTTP_DATA
         MVI   HTTP_FUNCTION,$HTTP_INIT       SET COMMAND
         LA    R1,HTTP_PARMS                  HTTPSUB PARM LIST
         L     R15,HTTP_HTTPSUB               HTTPSUB ENTRY POINT
         BALR  R14,R15                        LINK TO HTTPSUB
         ITRACE ID=INIT_RC,                                            +
               DATA1=HTTP_RETURN_CODE
         CLI   HTTP_RETURN_CODE,$HTTP_OK      SUCCESSFUL?
         BNE   ERR0020                        NO
* -------------------- Connect to MAIN ------------------------------ *
         ITRACE ID=CONNECT
         MVI   HTTP_FUNCTION,$HTTP_CONNECT    SET COMMAND
         LA    R1,HTTP_PARMS                  HTTPSUB PARM LIST
         L     R15,HTTP_HTTPSUB               HTTPSUB ENTRY POINT
         BALR  R14,R15                        LINK TO HTTPSUB
         ITRACE ID=CONN_RC,                                            +
               DATA1=HTTP_RETURN_CODE
         CLI   HTTP_RETURN_CODE,$HTTP_OK      SUCCESSFUL?
         BNE   ERR0020                        NO

* ------------------ Register URLs with MAIN ------------------------ *
         ICM   R8,15,WORK_DD_CHAIN            1ST DD_BLOK
         BZ    ERR0040                        NONE.. ERROR
MAIN0010 DS    0H
         ITRACE ID=REGISTER,                                           +
               DATA1=DD_DDNAME,                                        +
               DATA2=DD_DDNAME_LENGTH
         MVI   HTTP_FUNCTION,$HTTP_DEFINE_PRFX   SET COMMAND
         MVC   WORK_OUTPUT_BUFFER(L'DD_DDNAME),DD_DDNAME
         LA    R1,WORK_OUTPUT_BUFFER
         ST    R1,HTTP_DATA
         MVC   HTTP_LENGTH,DD_DDNAME_LENGTH   SET LENGTH OF DDNAME
         LA    R1,HTTP_PARMS                  HTTPSUB PARMS
         L     R15,HTTP_HTTPSUB               HTTPSUB ENTRY POINT
         BALR  R14,R15                        CALL HTTPSUB
         ITRACE ID=REGDD_RC,                                           +
               DATA1=HTTP_RETURN_CODE
         CLI   HTTP_RETURN_CODE,$HTTP_OK      SUCCESSFUL?
         BNE   ERR0020                        NO
         ICM   R8,15,DD_NEXT                  NEXT DD_BLOK
         BNZ   MAIN0010                       LOOP
* ------------------------------------------------------------------- *
         TM    WORK_FLAGS,$ERROR              ERRORS DETECTED?
         BO    ERR0010                        YES..

* ------------------------------------------------------------------- *
*                                                                     *
*     No initialization errors detected.                              *
*                                                                     *
* ------------------------------------------------------------------- *
         ITRACE ID=GET_NEST
         GETMAIN RU,                                                   +
               LV=$NEST_DEPTH*NESTL,                                   +
               LOC=BELOW
         ST    R1,WORK_NEST_TABLE             SAVE NEST TABLE ADDRESS
         LR    R5,R1
         ITRACE ID=NESTADDR,                                           +
               RDATA1=R5
         USING NEST_DSECT,R5
         LA    R6,$NEST_DEPTH
MAIN0020 DS    0H
         MVC   NEST_DDNAME,BLANKS             INITIALIZE DD NAME
         MVC   NEST_MEMBER,BLANKS             INITIALIZE MEMBER NAME
         XC    NEST_DCB,NEST_DCB              INITIALIZE DCB BLOCK ADDR
         A     R5,=A(NESTL)                   NEXT NEST INFO
         BCT   R6,MAIN0020                    LOOP
* ------------------------------------------------------------------- *
*                                                                     *
*     Wait for the main address space to get a reference to           *
*     one of the URLs.                                                *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0030 DS    0H
         ITRACE ID=GET_URL
         MVI   HTTP_FUNCTION,$HTTP_GET_URL    SET COMMAND
         LA    R1,WORK_OUTPUT_BUFFER
         ST    R1,HTTP_DATA
         LA    R1,HTTP_PARMS                  HTTPSUB PARM LIST
         L     R15,HTTP_HTTPSUB               HTTPSUB ENTRY POINT
         BALR  R14,R15                        CALL HTTPSUB
         ITRACE ID=GET_RC,                                             +
               DATA1=HTTP_RETURN_CODE
         CLI   HTTP_RETURN_CODE,$HTTP_CONSOLE COMMAND FROM CONSOLE?
         BE    CONS0000                       YES
         CLI   HTTP_RETURN_CODE,$HTTP_MAIN_SHUTDOWN
         BE    SHUT0000                       YES
         CLI   HTTP_RETURN_CODE,$HTTP_OK      ERROR?
         BNE   ERR0050                        YES
         ITRACE ID=HAVE_URL,                                           +
               DATA1=HTTP_URL_ID,                                      +
               DATA2=HTTP_LENGTH,                                      +
               DATA3=HTTP_DATA
* ------------------------------------------------------------------- *
*                                                                     *
*     We have a URL                                                   *
*                                                                     *
*     Log the URL                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
         NI    WORK_FLAGS,255-$HTML_HEADER    RESET INDICATOR
         MVC   PR_LINE(MSG04L),MSG04
         LH    R1,HTTP_SOCKET                 SOCKET FOR THIS URL
         CVD   R1,WORK_DWORD
         ED    PR_LINE+(MSG04_SOCKET-MSG04)(L'MSG04_SOCKET),WORK_DWORD++
               5
         L     R1,HTTP_URL_ID                 URL ID
         CVD   R1,WORK_DWORD                  CONVERT TO DECIMAL
         ED    PR_LINE+(MSG04_URL_ID-MSG04)(L'MSG04_URL_ID),WORK_DWORD++
               3
         L     R2,HTTP_DATA                   URL'S ADDRESS
         L     R3,HTTP_LENGTH
         CH    R3,=Y(L'MSG04_URL)             TOO LONG?
         BNH   MAIN0040                       NO
         LH    R3,=Y(L'MSG04_URL)             LIMIT LENGTH
MAIN0040 DS    0H
         BCTR  R3,0
         EX    R3,MVC_01                      COPY URL TEXT
         BAL   R9,PRT0000                     PRINT
         L     R5,WORK_NEST_TABLE
         USING NEST_DSECT,R5
         XC    WORK_NEST_DEPTH,WORK_NEST_DEPTH
* ------------------------------------------------------------------- *
*     Parse out the DD name and member name                           *
* ------------------------------------------------------------------- *
         L     R3,HTTP_LENGTH                 URL LENGTH
MAIN0050 DS    0H
         MVC   NEST_DDNAME,BLANKS
         MVC   NEST_MEMBER,BLANKS
         LA    R4,NEST_DDNAME                 OUTPUT DD NAME
         NI    WORK_FLAGS,255-$PARSED_MEMBER
MAIN0060 DS    0H
         SR    R15,R15
MAIN0070 DS    0H
         CLI   0(R2),C'/'                     DELIMITER?
         BE    MAIN0080
         CLI   0(R2),X'00'                    END OF URL?
         BE    MAIN0080
         CLI   0(R2),C' '                     END OF URL?
         BE    MAIN0080
         CH    R15,H8                         TOO LONG?
         BH    MAIN0300
         MVC   0(1,R4),0(R2)                  COPY TO DD OR MEMBER NAME
         LA    R2,1(,R2)                      NEXT SOURCE
         LA    R4,1(,R4)                      NEXT OUTPUT
         LA    R15,1(,R15)                    PLUS 1 TO LENGTH
         BCT   R3,MAIN0070                    LOOP
         B     MAIN0090                       END OF DATA
MAIN0080 DS    0H
         TM    WORK_FLAGS,$PARSED_MEMBER      HAVE MEMBER NAME?
         BO    MAIN0090                       YES
         OI    WORK_FLAGS,$PARSED_MEMBER      SET INDICATOR
         LA    R2,1(R2)                       SKIP DELIMITER
         LA    R4,NEST_MEMBER                 SET TO MEMBER NAME
         B     MAIN0060
* ------------------------------------------------------------------- *
*                                                                     *
*     Locate the DD_BLOK for this DD                                  *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0090 DS    0H
         ITRACE ID=LOC_DD,                                             +
               DATA1=NEST_DDNAME
         ICM   R8,15,WORK_DD_CHAIN            FIRST DD_BLOK
MAIN0100 DS    0H
         CLC   DD_DDNAME,NEST_DDNAME          FOUND THE DCB?
         BE    MAIN0110                       YES
         ICM   R8,15,DD_NEXT
         BNZ   MAIN0100
         B     MAIN0400
* ------------------------------------------------------------------- *
*                                                                     *
*     DD_BLOK has been located.                                       *
*     Issue FIND for the member                                       *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0110 DS    0H
         ITRACE ID=DD_FOUND,                                           +
               RDATA1=R8,                                              +
               DATA2=DD_DEPTH
         L     R7,DD_DEPTH                    DD NEST DEPTH
         MH    R7,=Y(DCB_DSECTL)              MULTIPLY BY LENGTH EACH
         LA    R7,DD_DCB_INFO(R7)             DCB INFO ADDRESS
         ST    R7,NEST_DCB                    SET DCB INFO ADDRESS
         L     R1,DD_DEPTH                    CURRENT DEPTH
         LA    R1,1(,R1)                      ADD 1
         ST    R1,DD_DEPTH                    UPDATE DEPTH
         CLC   NEST_MEMBER,BLANKS             MEMBER NAME GIVEN?
         BNE   MAIN0120                       YES
         MVC   NEST_MEMBER,WORK_INDEX_NAME    SET TO INDEX MEMBER NAME
MAIN0120 DS    0H
         ITRACE ID=FIND_MEM,                                           +
               DATA1=NEST_MEMBER
         FIND  DCB_DCB,                       'FIND' THE MEMBER        +
               NEST_MEMBER,                                            +
               D
         ITRACE ID=FIND_RC,                                            +
               RDATA1=R15
         LTR   R15,R15                        MEMBER FOUND?
         BNZ   MAIN0410                       NO
* ------------------------------------------------------------------- *
*                                                                     *
*        WRITE HTTP HEADER                                            *
*                                                                     *
* ------------------------------------------------------------------- *
         CLC   NEST_MEMBER,WORK_INDEX_NAME    INDEX MEMBER?
         BE    MAIN0130                       YES
         CLI   DD_TYPE,$DD_HTML               HTML FILE?
         BE    MAIN0140                       YES
         CLI   DD_TYPE,$DD_RAW                RAW FILE?
         BE    MAIN0140                       YES
MAIN0130 DS    0H
         ITRACE ID=MAIN0130
         XC    HTTP_ALET,HTTP_ALET            DATA IN OUR ADDRESS SPACE
         BAL   R9,HTTP0000                    SEND HTTP HEADING
         BAL   R9,HTML0000                    SEND HTML HEADING
* ------------------------------------------------------------------- *
*      READ first block of the member                                 *
* ------------------------------------------------------------------- *
MAIN0140 DS    0H
         ITRACE ID=READ_1ST
         BAL   R10,READ0010
         B     MAIN0160
* ------------------------------------------------------------------- *
*                                                                     *
*      READ/SEND the requested member                                 *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0150 DS    0H
         ITRACE ID=MAIN0150
         BAL   R10,READ0000                    READ A BLOCK
MAIN0160 DS    0H
         OC    DCB_DATA_LENGTH,DCB_DATA_LENGTH EOF?
         BNZ   MAIN0180                        NO
MAIN0170 DS    0H
         ITRACE ID=LOGICEOF,                                           +
               DATA1=WORK_NEST_DEPTH,                                  +
               DATA2=DD_DEPTH
         MVC   NEST_DDNAME,BLANKS             RESET DD NAME
         MVC   NEST_MEMBER,BLANKS             RESET MEMBER NAME
         XC    NEST_DCB,NEST_DCB              RESET DCB BLOCK ADDRESS
         L     R1,DD_DEPTH                    DEPTH ON THIS DD
         BCTR  R1,0                           MINUS 1
         ST    R1,DD_DEPTH                    UPDATE DEPTH
         ICM   R1,15,WORK_NEST_DEPTH          NEST DEPTH
         BZ    MAIN0320                       END OF ALL DATA
         BCTR  R1,0                           MINUS 1
         ST    R1,WORK_NEST_DEPTH
         S     R5,=A(NESTL)                   BACK UP 1 IN NEST TABLE
         L     R7,NEST_DCB                    RESET DCB INFO
         B     MAIN0150                       PROCESS NEXT RECORD
MAIN0180 DS    0H
         ITRACE ID=MAIN0180
         L     R2,DCB_DATA_ADDRESS
         L     R0,DCB_DATA_ADDRESS            DATA ADDRESS
         LH    R1,DCB_DATA_LENGTH             DATA LENGTH
         LA    R14,WORK_OUTPUT_BUFFER
         LR    R15,R1                         SET LENGTH
         MVCL  R14,R0                         COPY TO BUFFER
         LA    R1,WORK_OUTPUT_BUFFER
         LH    R2,DCB_DATA_LENGTH             DATA LENGTH
         CH    R2,=Y(L'INCLUDE_STATEMENT)     COULD IT BE AN INCLUDE?
         BL    MAIN0190                       NO
         CLC   INCLUDE_STATEMENT,WORK_OUTPUT_BUFFER
         BNE   MAIN0190                       NOT AN INCLUDE
         ITRACE ID=INCLUDE,                                            +
               DATA1=WORK_NEST_DEPTH
         L     R4,WORK_NEST_DEPTH             NEST DEPTH
         CH    R4,=Y($NEST_DEPTH)             AT MAX DEPTH
         BE    ERR0090                        YES
         LA    R4,1(,R4)                      ADD 1
         ST    R4,WORK_NEST_DEPTH             UPDATE NEST DEPTH
         A     R5,=A(NESTL)                   NEXT IN NEST TABLE
         LA    R2,WORK_OUTPUT_BUFFER+L'INCLUDE_STATEMENT
         LH    R3,DCB_DATA_LENGTH             DATA LENGTH
         SH    R3,=Y(L'INCLUDE_STATEMENT)     MINUS INCLUDE LENGTH
         BH    MAIN0050                       PARSE DD AND MEMBER
         B     MAIN0170                       TREAT SAME AS EOF
MAIN0190 DS    0H
         CLC   NEST_MEMBER,WORK_INDEX_NAME    INDEX MEMBER?
         BE    MAIN0200                       YES
         CLI   DD_TYPE,$DD_RAW                RAW DATA?
         BE    MAIN0210                       YES
MAIN0200 DS    0H
         AR    R1,R2
         MVI   0(R1),$LF
         LA    R2,1(,R2)
         ST    R2,HTTP_LENGTH
         BAL   R10,WRITE000
         B     MAIN0150
MAIN0210 DS    0H
         LA    R1,WORK_OUTPUT_BUFFER
         ST    R1,HTTP_DATA
         ST    R2,HTTP_LENGTH
         ITRACE ID=WRT_RAW,                                            +
               RDATA1=R1,                                              +
               RDATA2=R2
         MVI   HTTP_FUNCTION,$HTTP_WRITE_RAW
         XC    HTTP_ALET,HTTP_ALET
         LA    R1,HTTP_PARMS
         L     R15,HTTP_HTTPSUB
         BALR  R14,R15
         CLI   HTTP_RETURN_CODE,$HTTP_OK
         BNE   ERR0020
         B     MAIN0150
* ------------------------------------------------------------------- *
*                                                                     *
*      DDNAME in URL is too long                                      *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0300 DS    0H
         TM    WORK_FLAGS,$PARSED_MEMBER      PARSING MEMBER NAME?
         BO    MAIN0310                       YES
         ITRACE ID=DD_LONG
         BAL   R9,HTTP0000
         BAL   R9,HTML0000
         MVC   WORK_OUTPUT_BUFFER(MSG06L),MSG06
         LA    R1,MSG06L
         ST    R1,HTTP_LENGTH
         BAL   R10,WRITE000
         B     MAIN0320
* ------------------------------------------------------------------- *
*                                                                     *
*      Member in URL is too long                                      *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0310 DS    0H
         ITRACE ID=MEM_LONG
         BAL   R9,HTTP0000
         BAL   R9,HTML0000
         MVC   WORK_OUTPUT_BUFFER(MSG07L),MSG07
         LA    R1,HTML_HEADERL+MSG07L
         ST    R1,HTTP_LENGTH
         BAL   R10,WRITE000
* ------------------------------------------------------------------- *
*                                                                     *
*      Send HTML trailer data                                         *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0320 DS    0H
         TM    WORK_FLAGS,$HTML_HEADER
         BNO   MAIN1000
         ITRACE ID=TRAILER
         LA    R1,HTML_TRAILERL
         ST    R1,HTTP_LENGTH
         BAL   R10,WRITE000
         B     MAIN1000
* ------------------------------------------------------------------- *
*                                                                     *
*      DD not found                                                   *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0400 DS    0H
         ITRACE ID=BAD_DD,                                             +
               DATA1=DD_DDNAME
         BAL   R9,HTTP0000
         BAL   R9,HTML0000
         MVC   WORK_OUTPUT_BUFFER(MSG08L),MSG08
         MVC   WORK_OUTPUT_BUFFER+(MSG08DD-MSG08)(L'MSG08DD),DD_DDNAME
         LA    R1,MSG08L
         ST    R1,HTTP_LENGTH
         BAL   R10,WRITE000
         B     MAIN1000
* ------------------------------------------------------------------- *
*                                                                     *
*      Member not found                                               *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0410 DS    0H
         ITRACE ID=BAD_MEM,                                            +
               DATA1=NEST_MEMBER
         BAL   R9,HTTP0000                    SEND HTTP HEADING
         BAL   R9,HTML0000                    SEND HTML HEADING
         MVC   WORK_OUTPUT_BUFFER(MSG09L),MSG09
         LA    R1,MSG09L
         ST    R1,HTTP_LENGTH                 SEND MESSAGE
         BAL   R10,WRITE000
         B     MAIN1000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0420 DS    0H
         ITRACE ID=LOST
         MVC   PR_LINE(MSG16L),MSG16
         LH    R0,HTTP_SOCKET
         CVD   R0,WORK_DWORD
         ED    PR_LINE+(MSG16_SOCKET-MSG16)(L'MSG16_SOCKET),WORK_DWORD++
               4
         BAL   R9,PRT0000
* ------------------------------------------------------------------- *
*                                                                     *
*        CLOSE this connection                                        *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN1000 DS    0H
         ITRACE ID=MAIN1000
         BAL   R9,HTML0100                    SEND HTML TRAILER
         ITRACE ID=CLOSE
         MVI   HTTP_FUNCTION,$HTTP_CLOSE
         LA    R1,HTTP_PARMS
         L     R15,HTTP_HTTPSUB
         BALR  R14,R15
         XC    WORK_NEST_DEPTH,WORK_NEST_DEPTH
         LA    R6,$NEST_DEPTH
MAIN1010 DS    0H
         XC    DD_DEPTH,DD_DEPTH              RESET DEPTH
         A     R5,=A(NESTL)                   NEXT NEST INFO
         BCT   R6,MAIN1010                    LOOP
         B     MAIN0030
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
HTTP0000 DS    0H
         TM    WORK_FLAGS,$HTTP_HEADER        HTTP HEADING SENT?
         BOR   R9                             YES
         ITRACE ID=HTTPHEAD
         OI    WORK_FLAGS,$HTTP_HEADER        SET FLAG
         MVC   WORK_OUTPUT_BUFFER(HTTP_HEADERL),HTTP_HEADER
         LA    R1,HTTP_HEADERL
         ST    R1,HTTP_LENGTH
         BAL   R10,WRITE000                   WRITE HTTP HEADING
         BR    R9
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
HTML0000 DS    0H
         TM    WORK_FLAGS,$HTML_HEADER        HTML HEADING SENT?
         BOR   R9                             YES
         ITRACE ID=HTMLHEAD
         OI    WORK_FLAGS,$HTML_HEADER        SET FLAG
         MVC   WORK_OUTPUT_BUFFER(HTML_HEADERL),HTML_HEADER
         LA    R1,HTML_HEADERL
         ST    R1,HTTP_LENGTH
         BAL   R10,WRITE000                   WRITE HTML HEADING
         BR    R9
HTML0100 DS    0H
         TM    WORK_FLAGS,$HTML_HEADER        HTML HEADING SENT?
         BNOR  R9                             NO
         ITRACE ID=TRAIL2
         NI    WORK_FLAGS,255-$HTML_HEADER    RESET FLAG
         MVC   WORK_OUTPUT_BUFFER(HTML_TRAILERL),HTML_TRAILER
         LA    R1,HTML_TRAILERL
         ST    R1,HTTP_LENGTH
         BAL   R10,WRITE000                   WRITE HTML TRAILER
         BR    R9
* ------------------------------------------------------------------- *
*                                                                     *
*      READ member of one of the DDs                                  *
*                                                                     *
* ------------------------------------------------------------------- *
READ0000 DS    0H
         ITRACE ID=READ0000,                                           +
               RDATA1=R5,                                              +
               RDATA2=R8
         LH    R1,DCB_LENGTH_LEFT             DATA LEFT IN BLOCK
         SH    R1,DCB_RECORD_LENGTH           MINUS RECORD'S LENGTH
         BZ    READ0010                       NONE LEFT...
         STH   R1,DCB_LENGTH_LEFT             SAVE LENGTH LEFT NOW
         L     R1,DCB_RECORD_ADDRESS          CURRENT RECORD'S ADDRESS
         AH    R1,DCB_RECORD_LENGTH           NEXT RECORD'S ADDRESS
         ST    R1,DCB_RECORD_ADDRESS          SET ADDRESS
         ST    R1,DCB_DATA_ADDRESS            SET ADDRESS
         TM    DCBRECFM,DCBRECF               FIXED LENGTH RECORDS?
         BOR   R10                            YES..
         LH    R0,0(R1)                       THIS RECORD'S LENGTH
         STH   R0,DCB_RECORD_LENGTH           SAVE SIZE
         LA    R1,4(R1)                       DATA'S ADDRESS
         SH    R0,H4                          DATA'S LENGTH
         ST    R1,DCB_DATA_ADDRESS            SET DATA ADDRESS
         STH   R0,DCB_DATA_LENGTH             SET DATA LENGTH
         BR    R10
READ0010 DS    0H
         ITRACE ID=READ0010,                                           +
               RDATA1=R5,                                              +
               RDATA2=R8
         XC    DCB_ECB,DCB_ECB                CLEAR THE DECB
         READ  DCB_ECB,                       READ BLOCK OF MEMBER     +
               SF,                            .. SEQUENTIALLY FORWARD  +
               DCB_DCB,                       .. DCB                   +
               NEST_IO_AREA,                  .. I/O AREA ADDRESS      +
               ,                              .. NO SPECIFIED LENGTH   +
               S,                             .. BLOCK SIZE FROM DCB   +
               MF=(E,DCB_READ)                ..
         CHECK DCB_ECB                        WAIT FOR I/O COMPLETION
         LA    R1,NEST_IO_AREA                DATA ADDRESS
         TM    DCBRECFM,DCBRECV               VARIABLE LENGTH RECORDS?
         BO    READ0020                       YES
         ST    R1,DCB_RECORD_ADDRESS          SET RECORD ADDRESS
         ST    R1,DCB_DATA_ADDRESS            SET DATA ADDRESS
         LH    R2,DCBBLKSI                    BLOCK SIZE FROM DCB
         L     R15,DCBIOBA                    IOB ADDRESS FROM DCB
         SH    R15,H8                         DCB POINTS AT IOB+8
         USING IOB,R15                        DEFINE BASE
         SH    R2,IOBSTDRD+14                 COMPUTE LENGTH READ
         STH   R2,DCB_LENGTH_LEFT
         MVC   DCB_DATA_LENGTH,DCBLRECL
         MVC   DCB_RECORD_LENGTH,DCBLRECL
         BR    R10
READ0020 DS    0H
         LH    R0,0(R1)                       BLOCK LENGTH
         SH    R0,H4                          MINUS LENGTH OF BDW
         STH   R0,DCB_LENGTH_LEFT             SET LENGTH OF DATA LEFT
         LA    R1,4(R1)                       SKIP BDW
         ST    R1,DCB_RECORD_ADDRESS          SET RECORD ADDRESS
         LH    R0,0(R1)                       RECORD LENGTH
         STH   R0,DCB_RECORD_LENGTH           SET RECORD LENGTH
         SH    R0,H4                          DATA LENGTH
         STH   R0,DCB_DATA_LENGTH             SET DATA LENGTH
         LA    R1,4(R1)                       DATA ADDRESS
         ST    R1,DCB_DATA_ADDRESS            SET DATA ADDRESS
         ITRACE ID=READ0020,                                           +
               DATA1=DCB_RECORD_ADDRESS,                               +
               DATA2=DCB_RECORD_LENGTH,                                +
               DATA3=DCB_LENGTH_LEFT
         BR    R10
* ------------------------------------------------------------------- *
*          EOF reached on current member                              *
* ------------------------------------------------------------------- *
READ0030 DS    0H
         SAM31
         ITRACE ID=EOF
         XC    DCB_DATA_ADDRESS,DCB_DATA_ADDRESS
         XC    DCB_DATA_LENGTH,DCB_DATA_LENGTH
         XC    DCB_RECORD_ADDRESS,DCB_RECORD_ADDRESS
         XC    DCB_RECORD_LENGTH,DCB_RECORD_LENGTH
         XC    DCB_LENGTH_LEFT,DCB_LENGTH_LEFT
         BR    R10
* ------------------------------------------------------------------- *
*                                                                     *
*          Write to web user (EBCDIC data)                            *
*                                                                     *
* ------------------------------------------------------------------- *
WRITE000 DS    0H
         ITRACE ID=WRT_WEB,                                            +
               DATA1=HTTP_DATA,                                        +
               DATA2=HTTP_LENGTH
         MVI   HTTP_FUNCTION,$HTTP_WRITE
         LA    R1,WORK_OUTPUT_BUFFER
         ST    R1,HTTP_DATA
         LA    R1,HTTP_PARMS
         L     R15,HTTP_HTTPSUB
         BALR  R14,R15
         CLI   HTTP_RETURN_CODE,$HTTP_OK
         BNE   ERR0020
         BR    R10
* ------------------------------------------------------------------- *
*                                                                     *
*          Operator has entered a command at the console              *
*                                                                     *
* ------------------------------------------------------------------- *
CONS0000 DS    0H
         ITRACE ID=CONSOLE
         MVC   PR_LINE(MSG12L),MSG12
         MVC   PR_LINE+(MSG12_CMD-MSG12)(L'MSG12_CMD),HTTP_MESSAGE_1
         BAL   R9,PRT0000
         CLC   =C'STOP ',HTTP_MESSAGE_1
         BE    EXIT0000
         MVC   PR_LINE(MSG13L),MSG13
         BAL   R9,PRT0000
         B     MAIN0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0010  DS    0H
         ITRACE ID=ERR0010
         MVC   PR_LINE(MSG02L),MSG02
         BAL   R9,PRT0000
         MVI   WORK_RC,4
         B     EXIT0000
ERR0020  DS    0H
         ITRACE ID=ERR0020,                                            +
               DATA1=HTTP_TCPIP_ERROR
         MVC   PR_LINE(MSG03L),MSG03
         BAL   R9,PRT0000
         MVC   PR_LINE(L'HTTP_MESSAGE_1),HTTP_MESSAGE_1
         BAL   R9,PRT0000
         MVC   PR_LINE(L'HTTP_MESSAGE_2),HTTP_MESSAGE_2
         BAL   R9,PRT0000
         CLC   HTTP_TCPIP_ERROR,X_00000020    CONNECTION CLOSED?
         BE    MAIN0420                       YES
         MVI   WORK_RC,4
         B     EXIT0000
ERR0030  DS    0H
         ITRACE ID=ERR0030
         MVC   PR_LINE(MSG11L),MSG11
         BAL   R9,PRT0000
         MVI   WORK_RC,4
         B     EXIT0000
ERR0040  DS    0H
         ITRACE ID=ERR0040
         MVC   PR_LINE(MSG15L),MSG15
         BAL   R9,PRT0000
         MVI   WORK_RC,4
         B     EXIT0000
ERR0050  DS    0H
         ITRACE ID=ERR0050,                                            +
               DATA1=HTTP_TCPIP_RC,                                    +
               DATA2=HTTP_TCPIP_ERROR
         DC    H'5'
ERR0060  DS    0H
         ITRACE ID=ERR0060
         ESTAE 0
         MVI   HTTP_FUNCTION,$HTTP_FREE
         LA    R1,HTTP_PARMS
         L     R15,HTTP_HTTPSUB
         BALR  R14,R15
         MVC   WORK_OUTPUT_BUFFER(MSG14L),MSG14
         UNPK  WORK_DWORD(7),WORK_ABEND_CODE(4)
         TR    WORK_DWORD(6),HEXCHAR
         MVC   WORK_OUTPUT_BUFFER+(MSG14_SYSTEM-MSG14)(3),WORK_DWORD
         MVC   WORK_OUTPUT_BUFFER+(MSG14_USER-MSG14)(3),WORK_DWORD+3
         LA    R1,MSG14L
         ST    R1,HTTP_LENGTH
         BAL   R10,WRITE000
         B     MAIN1000
ERR0090  DS    0H
         DC    H'9'
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
PRT0000  DS    0H
         ITRACE ID=PRT0000
         ST    R9,WORK_PRT_R9
         CP    LINE_COUNT,MAX_LINE            TIME FOR HEADING?
         BL    PRT0010                        NO
         BAL   R9,HEAD0000                    PRINT HEADING
PRT0010  DS    0H
         PUT   WORK_SYSPRINT,PR_CC
         MVI   PR_CC,C' '
         MVC   PR_LINE,PR_CC
         AP    LINE_COUNT,P1
         L     R9,WORK_PRT_R9
         BR    R9
HEAD0000 DS    0H
         ITRACE ID=HEAD0000
         MVC   PR_SAVE,PR_CC
         AP    PAGE_NBR,P1
         MVC   PR_CC(HEADINGL),HEADING
         ED    PR_CC+(HEADPAGE-HEADING)(L'HEADPAGE),PAGE_NBR
         PUT   WORK_SYSPRINT,PR_CC
         MVC   PR_CC(121),PR_SAVE
         ZAP   LINE_COUNT,P1
         BR    R9
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
SHUT0000 DS    0H
         ITRACE ID=SHUTDOWN
         MVC   PR_LINE(MSG89L),MSG89
         BAL   R9,PRT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         OC    HTTP_WORKAREA,HTTP_WORKAREA
         BZ    EXIT0010
         MVI   HTTP_FUNCTION,$HTTP_CLEAN_UP
         LA    R1,HTTP_PARMS
         L     R15,HTTP_HTTPSUB
         BALR  R14,R15
         CLI   HTTP_RETURN_CODE,$HTTP_OK
         BE    EXIT0010
         MVC   PR_LINE(MSG90L),MSG90
         MVC   PR_LINE+(MSG90_MSG-MSG90)(L'MSG90_MSG),HTTP_MESSAGE_1
         BAL   R9,PRT0000
EXIT0010 DS    0H
         TM    TRACE_FLAGS,$TRACE
         BNO   EXIT0015
         CLOSE WORK_TRACE,                                             +
               MODE=31,                                                +
               MF=(E,WORK_OCPL)
         MVI   TRACE_FLAGS,0
EXIT0015 DS    0H
         ICM   R8,15,WORK_DD_CHAIN
         BZ    EXIT0040
EXIT0020 DS    0H
         LA    R7,DD_DCB_INFO
         LA    R6,$NEST_DEPTH
EXIT0030 DS    0H
         CLOSE DCB_DCB,                                                +
               MODE=31,                                                +
               MF=(E,WORK_OCPL)
         LA    R7,DCB_DSECTL(,R7)
         BCT   R6,EXIT0030
         L     R2,DD_NEXT
         FREEMAIN RU,                                                  +
               A=(R8),                                                 +
               LV=DD_BLOKL
         LTR   R8,R2
         BNZ   EXIT0020
EXIT0040 DS    0H
         ICM   R1,15,WORK_NEST_TABLE   NEST TABLE
         BZ    EXIT0050                NOT GETMAIN'D
         FREEMAIN RU,                                                  +
               A=(R1),                                                 +
               LV=$NEST_DEPTH*NESTL
EXIT0050 DS    0H
         MVC   PR_LINE(MSG91L),MSG91
         BAL   R9,PRT0000
         MVC   WORK_OCPL(CLOSEL),CLOSEI
         CLOSE WORK_SYSIN,                                             +
               MODE=31,                                                +
               MF=(E,WORK_OCPL)
         CLOSE WORK_SYSPRINT,                                          +
               MODE=31,                                                +
               MF=(E,WORK_OCPL)
         DELETE EP=HTTPSUB
         SR    R2,R2                   CLEAR REGISTER
         IC    R2,WORK_RC
         LR    R1,R13                  COPY WORK AREA ADDRESS
         L     R13,4(R13)              RESTORE CALLER'S R13
         FREEMAIN RU,                  FREE THE WORK AREA              +
               A=(1),                                                  +
               LV=WORKL
         L     R14,12(R13)             RESTORE R14
         LR    R15,R2                  INSERT RETURN CODE
         LM    R0,R12,20(R13)
         BR    R14
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
TRACE000 DS    0H
         ST    R15,WORK_TRACE_R15
         L     R15,WORK_TRACE_CURR
         C     R15,WORK_TRACE_LAST
         BE    TRACE010
         LA    R15,TRACE_ENTRYL(R15)
         B     TRACE020
TRACE010 DS    0H
         L     R15,WORK_TRACE_1ST
TRACE020 DS    0H
         ST    R15,WORK_TRACE_CURR
         USING TRACE_ENTRY,R15
         MVC   TRACE_ID,0(R14)
         MVC   TRACE_DATA1,WORK_TRACE_DATA1
         MVC   TRACE_DATA2,WORK_TRACE_DATA2
         MVC   TRACE_DATA3,WORK_TRACE_DATA3
         XC    WORK_TRACE_DATA1,WORK_TRACE_DATA1
         XC    WORK_TRACE_DATA2,WORK_TRACE_DATA2
         XC    WORK_TRACE_DATA3,WORK_TRACE_DATA3
         L     R15,WORK_TRACE_R15
         B     8(R14)
*-------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MVC_01   MVC   PR_LINE+(MSG04_URL-MSG04)(0),0(R2)
*-------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ESTAE000 DS    0H
         STM   R14,R12,12(R13)
         LR    R12,R15
         USING ESTAE000,R12
         USING SDWA,R1
         L     R2,SDWAPARM
         L     R3,0(R2)
         USING WORK,R3
         MVC   WORK_ABEND_CODE,SDWACMPC
         L     R4,WORK_ESTAE_RETRY
         SETRP RETADDR=(R4),                                           +
               RETREGS=YES,                                            +
               REGS=(14,12),                                           +
               FRESDWA=YES,                                            +
               RC=4
* ------------------------------------------------------------------- *
SYSINI    DCB  DSORG=PS,                                               +
               DDNAME=SYSIN,                                           +
               LRECL=80,                                               +
               RECFM=FB,                                               +
               EODAD=MAIN0000,                                         +
               MACRF=GM
SYSINL    EQU  *-SYSINI
SYSPRINTI DCB  DSORG=PS,                                               +
               DDNAME=SYSPRINT,                                        +
               LRECL=121,                                              +
               RECFM=FBA,                                              +
               MACRF=PM
SYSPRINTL EQU  *-SYSPRINTI
TRACEI    DCB  DSORG=PS,                                               +
               DDNAME=TRACE,                                           +
               RECFM=VB,                                               +
               MACRF=PM
TRACEL    EQU  *-TRACEI
MEMDCBI   DCB  DSORG=PO,                                               +
               DDNAME=XXXXXXXX,                                        +
               EODAD=READ0030,                                         +
               MACRF=R
MEMDCBL   EQU  *-MEMDCBI
READI     READ READIECB,             READ BLOCK OF MEMBER              +
               SF,                   .. SEQUENTIALLY FORWARD           +
               ,                     .. DCB                            +
               ,                     .. I/O AREA ADDRESS               +
               ,                     .. NO SPECIFIED LENGTH            +
               S,                    .. BLOCK SIZE FROM DCB            +
               MF=L                  ..
READL     EQU  *-READI
OPENI     OPEN (*,INPUT),                                              +
               MODE=31,                                                +
               MF=L
OPENL     EQU  *-OPENI
CLOSEI    CLOSE (*),                                                   +
               MODE=31,                                                +
               MF=L
CLOSEL    EQU  *-CLOSEI
ESTAEI   ESTAE ESTAE000,                                               +
               CT,                                                     +
               MF=L
ESTAEL   EQU   *-ESTAEI

HEXCHAR        EQU   *-C'0'
               DC    C'0123456789ABCDEF'

H4             DC    H'4'
H8             DC    H'8'
H100           DC    H'100'
P0             DC    P'0'
P1             DC    P'1'
MAX_LINE       DC    P'65'
X_00000020     DC    X'00000020'          <-- CONNECTION CLOSED
BLANKS         DC    CL8' '
HEADING        DS    0C
               DC    C'1'
               DC    CL30' '
               DC    C'HTTP SAMPLE APPLICATION'
               DC    CL30' '
               DC    C'PAGE'
HEADPAGE       DC    X'402020202120'
HEADINGL       EQU   *-HEADING

PORTSTMT       DC    CL09'PORT'
DDSTMT         DC    CL09'DD'
HTMLSTMT       DC    CL09'HTML'
INDXSTMT       DC    CL09'INDEX'
RAWSTMT        DC    CL09'RAWDD'
TRACESTMT      DC    CL09'TRACE'

INCLUDE_STATEMENT DC C'<--INCLUDE-->'

MSG01          DS    0C
               DC    CL15'HTTPAPP101E'
               DC    C'INVALID CONTROL STATEMENT'
MSG01L         EQU   *-MSG01
MSG02          DS    0C
               DC    CL15'HTTPAPP102E'
               DC    C'Error(s) detected, execution aborted'
MSG02L         EQU   *-MSG02
MSG03          DS    0C
               DC    CL15'HTTPAPP103E'
               DC    C'Error from HTTPSUB: '
MSG03_MSG      DC    CL80' '
MSG03L         EQU   *-MSG03
MSG04          DS    0C
               DC    CL15'HTTPAPP104I'
               DC    C'Socket'
MSG04_SOCKET   DC    X'402020202120'
               DC    C'    URL '
MSG04_URL_ID   DC    X'40202020202020202120'
               DC    C'  '
MSG04_URL      DC    CL80' '
MSG04L         EQU   *-MSG04
MSG05          DS    0C
               DC    CL15'HTTPAPP105E'
               DC    C'DDNAME may not start with a blank'
MSG05L         EQU   *-MSG05

MSG06          DS    0C
               DC    C'HTTPAPP106E  DDNAME IN URL IS TOO LONG'
MSG06L         EQU   *-MSG06
MSG07          DS    0C
               DC    C'HTTPAPP107E  MEMBER IN URL IS TOO LONG'
MSG07L         EQU   *-MSG07
MSG08          DS    0C
               DC    C'HTTPAPP108E  DDNAME '
MSG08DD        DC    CL8' '
               DC    C' not found'
MSG08L         EQU   *-MSG08
MSG09          DS    0C
               DC    C'HTTPAPP109E  Member '
MSG09MEM       DC    CL8' '
               DC    C' not found'
MSG09L         EQU   *-MSG09
MSG10          DS    0C
               DC    CL15'HTTPAPP110E'
               DC    C'DD NAME IS TOO LONG'
MSG10L         EQU   *-MSG10
MSG11          DS    0C
               DC    CL15'HTTPAPP111E'
               DC    C'PORT not specified'
MSG11L         EQU   *-MSG11
MSG12          DS    0C
               DC    CL15'HTTPAPP112I'
               DC    C'Command from console: '
MSG12_CMD      DC    CL80' '
MSG12L         EQU   *-MSG12
MSG13          DS    0C
               DC    CL15'HTTPAPP113E'
               DC    C'Invalid console command'
MSG13L         EQU   *-MSG13
MSG14          DS    0C
               DC    CL15'HTTPAPP114E'
               DC    C'Error occurred during OPEN.  SYSTEM CODE '
MSG14_SYSTEM   DS    CL3
               DC    C'     USER CODE '
MSG14_USER     DS    CL3
MSG14L         EQU   *-MSG14
MSG15          DS    0C
               DC    CL15'HTTPAPP115E'
               DC    C'No DD''s defined.'
MSG15L         EQU   *-MSG15
MSG16          DS    0C
               DC    CL15'HTTPAPP116W'
               DC    C'Connection lost.  Closing port'
MSG16_SOCKET   DC    X'402020202120'
MSG16L         EQU   *-MSG16
MSG89          DS    0C
               DC    CL15'HTTPAPP189I'
               DC    C'Received shutdown from HTTPMAIN'
MSG89L         EQU   *-MSG89
MSG90          DS    0C
               DC    CL15'HTTPAPP190E'
               DC    C'Error from HTTPSUB in shutdown: '
MSG90_MSG      DC    CL80' '
MSG90L         EQU   *-MSG90
MSG91          DS    0C
               DC    CL15'HTTPAPP191I'
               DC    C'Shutting down'
MSG91L         EQU   *-MSG91

HTTP_HEADER    DS    0C
               DC    C'HTTP/1.1 200 OK'
               DC    AL1($LF)
               DC    C'Content-Type: text/html'
               DC    AL1($LF)
               DC    AL1($LF)
HTTP_HEADERL   EQU   *-HTTP_HEADER
HTML_HEADER    DS    0C
               DC    C'<HTML>'
               DC    C'<BODY>'
               DC    C'<PRE>'
               DC    AL1($LF)
HTML_HEADERL   EQU   *-HTML_HEADER
HTML_TRAILER   DS    0C
               DC    C'</PRE>'
               DC    C'</BODY>'
               DC    C'</HTML>'
HTML_TRAILERL  EQU   *-HTML_TRAILER

* ------------------------------------------------------------------- *
*        WORK AREAS                                                   *
* ------------------------------------------------------------------- *
WORK                DSECT
                    DS    18F
WORK_DWORD          DS    D

WORK_TRACE_R15      DS    F
WORK_ID             DS    CL8
WORK_TRACE_AREA     DS    F
                    DS    CL4
WORK_TRACE_1ST      DS    F
                    DS    CL4
WORK_TRACE_LAST     DS    F
                    DS    CL4
WORK_TRACE_CURR     DS    F
WORK_TRACE_DATA1    DS    CL8
WORK_TRACE_DATA2    DS    CL8
WORK_TRACE_DATA3    DS    CL8

WORK_NEST_TABLE     DS    A
WORK_NEST_DEPTH     DS    F
WORK_DD_CHAIN       DS    A

WORK_SYSIN          DS    0F,(SYSINL)X
WORK_SYSPRINT       DS    0F,(SYSPRINTL)X
WORK_TRACE          DS    0F,(TRACEL)X

WORK_OCPL           DS    0F,(OPENL)X

WORK_ESTAE_PARMS    DS    0A
WORK_ESTAE_WORK     DS    A
WORK_ESTAE_RETRY    DS    A

WORK_INDEX_NAME     DS    CL8

WORK_FLAGS          DS    X
$ERROR              EQU   X'80'
$INIT               EQU   X'40'
$PORT               EQU   X'20'
$PARSED_MEMBER      EQU   X'10'
$HTTP_HEADER        EQU   X'08'
$HTML_HEADER        EQU   X'04'

TRACE_FLAGS         DS    X
$TRACE              EQU   X'80'

WORK_RC             DS    X

WORKCARD            DS    CL80

PAGE_NBR            DS    PL3
LINE_COUNT          DS    PL3

PR_CC               DS    C
PR_LINE             DS    CL120
                    ORG   PR_LINE
PR_LENGTH           DS    CL6
                    DS    CL4
PR_DATA             DS    CL100
                    ORG
PR_SAVE             DS    CL121
WORK_PRT_R9         DS    F

WORK_ABEND_CODE     DS    XL3

                    SUBPARM TYPE=CSECT

WORK_OUTPUT_BUFFER  DS    32768C
WORKL               EQU   *-WORK
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
$NEST_DEPTH         EQU  10
NEST_DSECT          DSECT
NEST_DDNAME         DS   CL8                  DD NAME
NEST_MEMBER         DS   CL8                  MEMBER NAME
NEST_DCB            DS   A                    ADDRESS OF DCB INFO
NEST_IO_AREA        DS   CL32768              I/O AREA
NESTL               EQU  *-NEST_DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DD_BLOK             DSECT
DD_NEXT             DS   A                    NEXT DDBLOK
DD_DDNAME_LENGTH    DS   F                    LENGTH OF DD NAME
DD_DEPTH            DS   F                    DEPTH IN THIS DD/DCB
DD_DDNAME           DS   CL8                  DDNAME
DD_TYPE             DS   C                    DD TYPE
$DD_HTML            EQU  C'H'                 .. HTML
$DD_NORMAL          EQU  C'N'                 .. PLAIN FILE
$DD_RAW             EQU  C'R'                 .. RAW DATA
                    DS   0F                   FORCE ALIGNMENT
DD_DCB_INFO         DS   ($NEST_DEPTH)XL(DCB_DSECTL)
DD_BLOKL            EQU  *-DD_BLOK
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DCB_DSECT           DSECT
DCB_DCB             DS   0F,(MEMDCBL)X        DCB
DCB_READ            DS   0F,(READL)X          READ (LIST FORM)
DCB_ECB             DS   F                    ECB
DCB_READS           DS   F                    NBR OF READS
DCB_RECORD_ADDRESS  DS   A                    CURRENT RECORD ADDRESS
DCB_DATA_ADDRESS    DS   A                    DATA IN CURRENT RECORD
DCB_RECORD_LENGTH   DS   H                    CURRENT RECORD LENGTH
DCB_DATA_LENGTH     DS   H                    LENGTH OF DATA
DCB_LENGTH_LEFT     DS   H                    LENGTH LEFT IN BLOCK
                    DS   0F                   FORCE FULLWORD ALIGNMENT
DCB_DSECTL          EQU  *-DCB_DSECT
* ------------------------------------------------------------------ *
*                                                                    *
* ------------------------------------------------------------------ *
TRACE_ENTRY         DSECT
TRACE_ID            DS   CL8
TRACE_DATA1         DS   CL8
TRACE_DATA2         DS   CL8
TRACE_DATA3         DS   CL8
TRACE_ENTRYL        EQU  *-TRACE_ENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                    IHASDWA
                    IEZIOB
                    IEZDEB LIST=YES
                    DCBD DSORG=PO
                    COPY REGEQU
$LF                 EQU  X'0A'
                    END  HTTPAPP1
