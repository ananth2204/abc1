* ------------------------------------------------------------------- *
*                                                                     *
*  Name: HTTPTERM                                                     *
*                                                                     *
*  PURPOSE: HTTP Termination/clean-up                                 *
*                                                                     *
* ------------------------------------------------------------------- *
HTTPTERM CSECT
HTTPTERM AMODE 31
HTTPTERM RMODE ANY
         USING HTTPTERM,R12                   DEFINE BASE
         USING HTTPCOMM,R11
         STM   R14,R12,12(R13)                SAVE ENTRY REGISTERS
         LR    R12,R15                        COPY BASE
         B     INIT0000                       BRANCH AROUND EYECATCHER
MODID    DC    CL8'HTTPTERM'                  MODULE NAME
         DC    CL8'&SYSDATE'                  DATE OF ASSEMBLY
         DC    CL6'&SYSTIME'                  TIME OF ASSEMBLY
INIT0000 DS    0H
         GETMAIN RU,                                                   +
               LV=WORKL,                                               +
               LOC=BELOW
         ST    R13,4(R1)
         ST    R1,8(R13)
         LR    R13,R1
         USING WORK,R13
         MVI   PRT_COMMAND,$PRT_PRINT
         MVC   PRT_CC(PRT_LRECL),COMM_BLANKS
* ------------------------------------------------------------------- *
*                                                                     *
*                       Phase 1                                       *
*   Set the EOJ1 flag.                                                *
*   No further server space connections or web requests accepted.     *
*   The TCP/IP subtask will shutdown.                                 *
*   The local URL subtask is shutdown.                                *
*   THE WEB contact subtask is shutdown.                              *
*   The URL manager subtask is shutdown.                              *
*                                                                     *
* ------------------------------------------------------------------- *
TERM0000 DS    0H
         ITRACE ID=EOJ1
         MVC   PRT_MESSAGE_ID(MSG02L),MSG02
         BAL   R8,PRT0000
         OI    COMM_EOJ,$EOJ_PHASE_1          SIGNAL SHUTDOWN..

* ------------------------------------------------------------------- *
*        Shutdown the main-to-server contact subtask                  *
* ------------------------------------------------------------------- *
         MVC   WORK_SUBTASK,HTTPTCP
         POST  COMM_HTTPTCP_WORK_ECB          POST THE ECB
         WAIT  ECB=COMM_HTTPTCP_TERM_ECB      WAIT FOR SUBTASK
         ITRACE ID=TCP_ECB,                   SUBTASK HAS TERMINATED   + +
               DATA1=(COMM_HTTPTCP_TERM_ECB,4) .. ECB'S STATUS
         DETACH COMM_HTTPTCP_TCB
         STH   R15,WORK_RC                    SAVE RETURN CODE
         LTR   R15,R15
         BNZ   ERR0010
         MVC   PRT_MESSAGE_ID(MSG03L),MSG03
         UNPK  PRT_MESSAGE_ID+(MSG03ECB-MSG03)(9),COMM_HTTPTCP_TERM_ECB+) 5)
               (5)
         TR    PRT_MESSAGE_ID+(MSG03ECB-MSG03)(8),HEXCHAR
         MVI   PRT_MESSAGE_ID+(MSG03ECB-MSG03)+8,C' '
         BAL   R8,PRT0000

* ------------------------------------------------------------------- *
*        Shutdown the local URL processing subtask                    *
* ------------------------------------------------------------------- *
         MVC   WORK_SUBTASK,HTTPLOCL
         POST  COMM_LOCAL_WORK_ECB
         WAIT  ECB=COMM_LOCAL_TERM_ECB
         ITRACE ID=LOCL_ECB,                  SUBTASK HAS TERMINATED   + +
               DATA1=(COMM_LOCAL_TERM_ECB,4)  .. ECB'S STATUS
         DETACH COMM_LOCAL_TCB
         STH   R15,WORK_RC                    SAVE RETURN CODE
         LTR   R15,R15
         BNZ   ERR0010
         MVC   PRT_MESSAGE_ID(MSG04L),MSG04
        UNPK  PRT_MESSAGE_ID+(MSG04ECB-MSG04)(9),COMM_LOCAL_TERM_ECB(5) 5)
         TR    PRT_MESSAGE_ID+(MSG04ECB-MSG04)(8),HEXCHAR
         MVI   PRT_MESSAGE_ID+(MSG04ECB-MSG04)+8,C' '
         BAL   R8,PRT0000

* ------------------------------------------------------------------- *
*        Shutdown the web user contact subtask                        *
* ------------------------------------------------------------------- *
         MVC   WORK_SUBTASK,HTTPWEB
         POST  COMM_WEB_WORK_ECB
         WAIT  ECB=COMM_WEB_TERM_ECB
         ITRACE ID=WEB_ECB,                   SUBTASK HAS TERMINATED   + +
               DATA1=(COMM_WEB_TERM_ECB,4)    .. ECB'S STATUS
         DETACH COMM_WEB_TCB
         STH   R15,WORK_RC                    SAVE RETURN CODE
         LTR   R15,R15
         BNZ   ERR0010
         MVC   PRT_MESSAGE_ID(MSG05L),MSG05
         UNPK  PRT_MESSAGE_ID+(MSG05ECB-MSG05)(9),COMM_WEB_TERM_ECB(5)  5)
         TR    PRT_MESSAGE_ID+(MSG05ECB-MSG05)(8),HEXCHAR
         MVI   PRT_MESSAGE_ID+(MSG05ECB-MSG05)+8,C' '
         BAL   R8,PRT0000

* ------------------------------------------------------------------- *
*        Shutdown the URL subtask                                     *
* ------------------------------------------------------------------- *
         MVC   WORK_SUBTASK,HTTPURL
         POST  COMM_URL_WORK_ECB
         WAIT  ECB=COMM_URL_TERM_ECB
         ITRACE ID=URL_ECB,                   SUBTASK HAS TERMINATED   + +
               DATA1=(COMM_URL_TERM_ECB,4)    .. ECB'S STATUS
         DETACH COMM_URL_TCB
         STH   R15,WORK_RC                    SAVE RETURN CODE
         LTR   R15,R15
         BNZ   ERR0010
         MVC   PRT_MESSAGE_ID(MSG06L),MSG06
         UNPK  PRT_MESSAGE_ID+(MSG06ECB-MSG06)(9),COMM_WEB_TERM_ECB(5)  5)
         TR    PRT_MESSAGE_ID+(MSG06ECB-MSG06)(8),HEXCHAR
         MVI   PRT_MESSAGE_ID+(MSG06ECB-MSG06)+8,C' '
         BAL   R8,PRT0000
* ------------------------------------------------------------------- *
*                                                                     *
*                START OF PHASE 2 OF SHUTDOWN                         *
*   Set the EOJ2 flag.                                                *
*   Terminate the server connections.                                 *
*                                                                     *
* ------------------------------------------------------------------- *
         ITRACE ID=EOJ2
         OI    COMM_EOJ,$EOJ_PHASE_2          SIGNAL SHUTDOWN..
         MVC   PRT_MESSAGE_ID(MSG07L),MSG07
         BAL   R8,PRT0000                     PRINT MESSAGE
         MVC   WORK_SUBTASK,HTTPSERV
         ICM   R7,15,COMM_SERVERS
         USING SERVBLOK,R7
         BZ    TERM0030
TERM0010 DS    0H
         TM    SERVER_FLAGS,$SERVER_LOST
         BO    TERM0020
         POST  SERVER_WORK_ECB
         WAIT  ECB=SERVER_TERM_ECB
TERM0020 DS    0H
         ITRACE ID=DTCHSRV,                                            +
               DATA1=(SERVER_TERM_ECB,4)
         DETACH SERVER_TCB
         LTR   R15,R15
         BNZ   ERR0010
         MVC   PRT_MESSAGE_ID(MSG08L),MSG08
         MVC   PRT_MESSAGE_ID+(MSG08TASK-MSG08)(L'MSG08TASK),SERVER_SUB+
               TASK
         UNPK  PRT_MESSAGE_ID+(MSG08ECB-MSG08)(9),SERVER_TERM_ECB(5)    5)
         TR    PRT_MESSAGE_ID+(MSG08ECB-MSG08)(8),HEXCHAR
         MVI   PRT_MESSAGE_ID+(MSG08ECB-MSG08)+8,C' '
         BAL   R8,PRT0000
         ITRACE ID=FREESERV,                                           +
               RDATA1=R7
         LR    R1,R7
         ICM   R7,15,SERVER_NEXT
         FREEMAIN RU,                                                  +
               A=(1),                                                  +
               LV=SERVBLOKL
         LTR   R7,R7
         BNZ   TERM0010
* ------------------------------------------------------------------- *
*                                                                     *
*                START OF PHASE 3 OF SHUTDOWN                         *
*                                                                     *
*     Terminate the CONSOLE subtask                                   *
*     Terminate the LOGGING subtask                                   *
*                                                                     *
* ------------------------------------------------------------------- *
TERM0030 DS    0H
         ITRACE ID=EOJ3
         OI    COMM_EOJ,$EOJ_PHASE_3          SIGNAL PHASE 3
         MVC   PRT_MESSAGE_ID(MSG10L),MSG10
         BAL   R8,PRT0000                     PRINT MESSAGE
         OC    COMM_CONSOLE_TCB,COMM_CONSOLE_TCB                        ?
         BZ    TERM0110
         ITRACE ID=CONSTERM
         POST  COMM_CONSOLE_WORK_ECB
         WAIT  ECB=COMM_CONSOLE_TERM_ECB      WAIT FOR TERMINATION
         DETACH COMM_CONSOLE_TCB              DETACH CONSOLE SUBTASK
         ITRACE ID=COMM_CONSOLE_TERM_ECB,     SUBTASK HAS TERMINATED   + +
               DATA1=(COMM_CONSOLE_TERM_ECB,4) .. ECB'S STATUS
         MVC   PRT_MESSAGE_ID(MSG11L),MSG11
         UNPK  PRT_MESSAGE_ID+(MSG11ECB-MSG11)(9),COMM_CONSOLE_TERM_ECB+5)
               (5)
         TR    PRT_MESSAGE_ID+(MSG11ECB-MSG11)(8),HEXCHAR
         MVI   PRT_MESSAGE_ID+(MSG11ECB-MSG11)+8,C' '
         BAL   R8,PRT0000
TERM0110 DS    0H
         OC    COMM_LOG_TERM_ECB+1(3),COMM_LOG_TERM_ECB+1
         BZ    TERM0120
TERM0120 DS    0H
         OC    COMM_LOG_TCB,COMM_LOG_TCB                                ?
         BZ    TERM0130
         ITRACE ID=LOGTERM
         POST  COMM_LOG_WORK_ECB
         WAIT  ECB=COMM_LOG_TERM_ECB          WAIT FOR TERMINATION
         DETACH COMM_LOG_TCB                  DETACH LOGGING SUBTASK
         ITRACE ID=COMM_LOG_TERM_ECB,         SUBTASK HAS TERMINATED   + +
               DATA1=(COMM_LOG_TERM_ECB,4)    .. ECB'S STATUS
         MVC   PRT_MESSAGE_ID(MSG13L),MSG13
         UNPK  PRT_MESSAGE_ID+(MSG13ECB-MSG13)(9),COMM_LOG_TERM_ECB(5)  5)
         TR    PRT_MESSAGE_ID+(MSG13ECB-MSG13)(8),HEXCHAR
         MVI   PRT_MESSAGE_ID+(MSG13ECB-MSG13)+8,C' '
         BAL   R8,PRT0000
* ------------------------------------------------------------------- *
*                Start phase 4 of shutdown                            *
* ------------------------------------------------------------------- *
TERM0130 DS    0H
         OI    COMM_EOJ,$EOJ_PHASE_4          SIGNAL PHASE 4
         MVC   PRT_MESSAGE_ID(MSG15L),MSG15
         BAL   R8,PRT0000
         ITRACE ID=EOJ4
         OI    COMM_EOJ,$EOJ_PHASE_4
         POST  COMM_PRINT_WORK_ECB
         WAIT  1,ECB=COMM_PRINT_TERM_ECB                                ION
         DETACH COMM_PRINT_TCB
         ITRACE ID=COMM_PRINT_TERM_ECB,                                +
               DATA1=(COMM_PRINT_TERM_ECB,4)
         B     EXIT0000
* ------------------------------------------------------------------- *
*        ERROR ROUTINES                                               *
* ------------------------------------------------------------------- *
ERR0010  DS    0H
         ITRACE ID=DTCHFAIL                   SUBTASK DETACH FAILED
         STH   R15,WORK_RC                    SAVE RETURN CODE
         MVC   PRT_MESSAGE_ID(MSG20L),MSG20
         MVC   PRT_MESSAGE_ID+(MSG20TSK-MSG20)(L'MSG20TSK),WORK_SUBTASK
         UNPK  PRT_MESSAGE_ID+(MSG20R15-MSG20)(5),WORK_RC(3)
         TR    PRT_MESSAGE_ID+(MSG20R15-MSG20)(L'MSG20R15),HEXCHAR
         MVI   PRT_MESSAGE_ID+(MSG20R15-MSG20)+4,C' '
         BAL   R8,PRT0000                     PRINT MESSAGE
         DC    H'0'                           TIME TO FIX THIS THING
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
PRT0000  DS    0H
         ITRACE ID=PRINT
         GETMAIN RU,                                                   +
               LV=PRTBLOKL,                                            +
               LOC=ANY
         MVC   0(PRTBLOKL,R1),PRTBLOK
         MVC   PRT_CC(PRT_LRECL),COMM_BLANKS
PRT0010  DS    0H
         L     R14,COMM_PRINT_QUEUE           FIRST BLOCK ON QUEUE
         ST    R14,PRT_NEXT-PRTBLOK(R1)
         CS    R14,R1,COMM_PRINT_QUEUE        ADD NEW BLOCK TO CHAIN
         BC    4,PRT0010                      TRY AGAIN
         POST  COMM_PRINT_WORK_ECB            WAKE UP THE PRINT SUBTASK
         BR    R8                             COMPLETE
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=EXIT
         LR    R2,R13
         L     R13,4(R13)
         FREEMAIN RU,                         FREEMAIN WORK AREA       +
               A=(R2),                                                 +
               LV=WORKL
         LM    R14,R12,12(R13)                RESTORE REGISTERS
         SR    R15,R15                        CLEAR REGISTER
         BR    R14                            EXIT
* ------------------------------------------------------------------- *
*                                                                     *
*        CONSTANTS                                                    *
*                                                                     *
* ------------------------------------------------------------------- *

HEXCHAR     EQU   *-C'0'
            DC    C'0123456789ABCDEF'

HTTPCONS    DC    CL8'HTTPCONS'
HTTPLOG     DC    CL8'HTTPLOG'
HTTPLOCL    DC    CL8'HTTPLOCL'
HTTPPRT     DC    CL8'HTTPPRT'
HTTPSERV    DC    CL8'HTTPSERV'
HTTPTCP     DC    CL8'HTTPTCP'
HTTPURL     DC    CL8'HTTPURL'
HTTPWEB     DC    CL8'HTTPWEB'
PRT_ID      DC    CL8'PRTBLOK'

MSG02    DS    0C
         DC    CL15'HTTPTERM02I'
         DC    C'Termination phase 1 beginning.'
MSG02L   EQU   *-MSG02
MSG03    DS    0C
         DC    CL15'HTTPTERM03I'
         DC    C'TCP/IP subtask terminated, ECB='
MSG03ECB DC    CL8' '
         DC    C' '
MSG03L   EQU   *-MSG03
MSG04    DS    0C
         DC    CL15'HTTPTERM04I'
         DC    C'Local URL subtask terminated, ECB='
MSG04ECB DC    CL8' '
         DC    C' '
MSG04L   EQU   *-MSG04
MSG05    DS    0C
         DC    CL15'HTTPTERM05I'
         DC    C'WEB subtask terminated, ECB='
MSG05ECB DC    CL8' '
         DC    C' '
MSG05L   EQU   *-MSG05
MSG06    DS    0C
         DC    CL15'HTTPTERM06I'
         DC    C'URL subtask terminated, ECB='
MSG06ECB DC    CL8' '
         DC    C' '
MSG06L   EQU   *-MSG06
MSG07    DS    0C
         DC    CL15'HTTPTERM07I'
         DC    C'Termination phase 2 beginning.'
MSG07L   EQU   *-MSG07
MSG08    DS    0C
         DC    CL15'HTTPTERM08I'
         DC    C'SERVER subtask '
MSG08TASK DC   CL8' '
         DC    C' terminated, ECB='
MSG08ECB DC    CL8' '
         DC    C' '
MSG08L   EQU   *-MSG08
MSG10    DS    0C
         DC    CL15'HTTPTERM10I'
         DC    C'Termination phase 3 beginning.'
MSG10L   EQU   *-MSG10
MSG11    DS    0C
         DC    CL15'HTTPTERM11I'
         DC    C'Console communcation subtask terminated, ECB='
MSG11ECB DC    CL8' '
         DC    C' '
MSG11L   EQU   *-MSG11
MSG13    DS    0C
         DC    CL15'HTTPTERM13I'
         DC    C'Logging subtask terminated, ECB='
MSG13ECB DC    CL8' '
         DC    C' '
MSG13L   EQU   *-MSG13
MSG15    DS    0C
         DC    CL15'HTTPTERM15I'
         DC    C'Termination complete.'
MSG15L   EQU   *-MSG15
MSG20    DS    0C
         DC    CL15'HTTPTERM20E'
         DC    C'DETACH failed.  Subtask '
MSG20TSK DC    CL8' '
         DC    C', R15='
MSG20R15 DC    CL4' '
         DC    C' '
MSG20L   EQU   *-MSG20
         LTORG
* ------------------------------------------------------------------- *
*                                                                     *
*        WORK AREA                                                    *
*                                                                     *
* ------------------------------------------------------------------- *
WORK             DSECT
                 STDSAVE DSECT=NO
WORK_ID          DS       CL8
WORK_DWORD       DS       D

WORK_RC          DS       H
WORK_SUBTASK     DS       CL8              SUBTASK BEING DETACHED
                 PRTBLOK  DSECT=NO
WORKL            EQU      *-WORK
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COMMON   MAIN,TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         SERVBLOK DSECT=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                  IKJTCB   LIST=NO
                  IEFTIOT1
SERVER_IO_SIZE    EQU      32760
                  COPY     REGEQU
         END   HTTPTERM
