A64STUB CSECT
A64STUB AMODE 31
A64STUB RMODE 31
         PRINT OFF
         IEABRCX DEFINE      LOAD MACRO BRANCHES
         IEABRCX ENABLE
         PRINT ON
********************************************************
*
* CALLED FROM COBOL TO ALLOW AMODE64
*
*-------------------------------------------------------
* NEEDS/USAGE
* 1) LOAD EP=TEST64    CHANGE TO YOUR AMODE64 MODULE
* 2) CHANGE AMODE IF NECESSARY (AMODE31 CALLS DEFAULTED)
*    - ?MODE 31     <----LINE TO ?MODE 24
*    - SAM31        <----LINE TO SAM24
* 3) CHECK RETURN CODES SECTION IF THIS MODULE ERROR CODE IS OK
* 4) CALL THIS MODULE AS YOU WOULD CALL DESIRED 64B MODULE
*-------------------------------------------------------
* MODULE IS REUSABLE, NOT REENTRANT
* FIRST RUN LOADS AND SAVES ENTRY POINT OF AMODE 64 MODUL
* NEXT RUNS REUSE THIS ADDRESS <-- RENT FROM THIS POINT
*
* THIS AVOIDS EXCESSIVE IO USING LOAD/DELETE OR LINKX
* AND ALSO ABENDS OVERFLOWING RESPONSIBILITY COUNT AFTER 32K
* LOAD ITERATIONS
*-------------------------------------------------------
* RETURN CODES:
* IN CASE MODUL ENDS IN ERROR (LOAD MACRO CANNOT LOAD)
* CC IS SET TO X'FFFFFFF...'
* OTHERWISE CC IS COPIED FROM AMODE64 MODULES'S CC
* PLEASE MAKE SURE X'FFFF...' IS NOT IN CONFLICT WITH SOME ERROR
* FROM AMODE 64 MODULE!!!
*-------------------------------------------------------
* AUTHOR: PETR SVOBODA
* EMAIL:  PETR.SVOBODA@CZ.IBM.COM
*         PSVO@CENTRUM.CZ
*****************************
         SYSSTATE ASCENV=P
         BAKR  14,0                SAVE CALLERS ENV
         XGR   5,5                 CLEAR ALL 64B
         LR    5,1                 LOAD 32BITS OF PARAMETERS ADDRESS
         LARL  12,C1               ADRESABILITY CONSTS
         USING (C1,C2),12
         LG    15,EPA              LOADED ALREADY?
         LTR   15,15
         JNZ   BRANCH              YES JUST BRANCH
         LOAD  EP=TEST64           NO LOAD
         LTR   15,15
         JNZ   ENDERR              CC=FFFF
         LR     6,0
         BCTR   6,0                BIT64=1 FOR AMODE64 MODULE CLEAR IT
         LLGTR  15,6               SET VALID 64B ADDRESS
         STG   15,EPA              AND SAVE IT FOR NEXT RUN
BRANCH   DS    0H
         SAM64 ,                   AMODE SWITCH AND ASM INFORM
         SYSSTATE ASCENV=P,AMODE64=YES
         LGR   1,5                 PARAMETERS POINTER RESET
         BASR  14,15               CALL MODULE
         SAM31 ,                   SWITCH BACK
END      PR    ,                   END USING MODULE CC
ENDERR   LA   15,0                 STUB ERROR FFFFFFF
         LA   1,1
         SR   15,1
         PR
C1       EQU  *
EPA      DC    D'0'                FIRST RUN=0, OTHERS EP
         LTORG
C2       EQU *
         END
