/* REXX
 *
 * Sample REXX EXEC for/from IBM Systems Magazine Article
 *   "Digging Into the Bag of ISPF Tricks"
 *    http://tinyurl.com/yyghzr
 *
 * To allocate an ISPF Profile for which ever system you are on
 * in a multiple TSO environment
 * Add to your default logon processing, before entering ISPF
 *
 * NOTE:
 *  Contrary to what my article said, you can only logon once
 *  on any single z/OS image with the same USERID
 *
 *  Also, the profiles will DRIFT over time, so that the contents will
 *  differ from each system you sign on to. That never bothered me.
 *
 */

trace

    /*
     * Cannot re-allocate the ISPF profile data set while in ISPF
     */
    If sysvar("SYSISPF") = "ACTIVE" then do
       zedsmsg = "Invalid under ISPF"
       zedlmsg = "Cannot de-allocate ISPPROF under ISPF"
       address "ISPEXEC" "SETMSG MSG(ISRZ000)"
       exit 8
       end

    /*
     * build the name of the profile data set
     * either userid.<profile> or prefix.userid.<profile>
     * ISPF.PROFILE could be different at other shops
     */
    system = mvsvar("SYSNAME")
    pref = sysvar("SYSPREF")
    user = userid()
    if user ¬= pref then pref = pref || "." || user
    oldprof = "'" || pref || ".ISPF.ISPPROF'"
    pref    = pref || "." || system
    profile = "'" || pref || ".ISPF.PROFILE'"

    /*
     * Does the profile dataset exist?
     */
    if sysdsn(profile) = "OK" then do
       "ALLOC F(ISPPROF) OLD REUSE DA(" || profile || ")"
       exit 0
       end

   /*
    * allocate the new profile dataset first time on this system
    */
   drop sysin.
   address "TSO"
     "ALLOC F(IN) DA("  || oldprof || ") SHR"
     "ALLOC F(OUT) DA(" || profile || ") NEW LIKE(" || oldprof || ")"
     "ALLOC F(SYSPRINT) DUMMY REUSE"
     "ALLOC F(SYSIN)    UNIT(DASD) SPACE(1 1) TRACKS REUSE"

   sysin.0 = 1
   sysin.1 = " C I=IN,O=OUT"
     "EXECIO * DISKW SYSIN (FINIS STEM SYSIN."
   drop sysin.

     "TSOEXEC CALL 'SYS1.LINKLIB(IEBCOPY)'"
     code = rc

     "ALLOC F(SYSIN)    DA(*) REUSE"
     "ALLOC F(SYSPRINT) DA(*) REUSE"
     "FREE F(IN,OUT)"

     if code ¬= 0 then do
       say "Unsuccesful allocation of" profile "DataSet"
       say "Allocation unchanged from default"
       end

     "ALLOC F(ISPPROF) OLD REUSE DA(" || profile || ")"

   exit
