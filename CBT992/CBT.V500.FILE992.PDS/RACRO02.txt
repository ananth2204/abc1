***********************************************************************
*
*   This code was taken from a batch program which changes the volser
*   identity in Record 3 of Track 0 of a disk pack, while the pack
*   remains online.  There is a definite risk in this, since the system
*   judges the volume id of a pack from its UCB entry, rather than
*   from the actual identity that is written on Record 3 of Track 0
*   on the actual pack.  The UCB volid is only changed when the pack is
*   varied offline and then varied online.
*
*   The RACROUTE condition chosen here, was to ask for READ access to
*   a FACILITY class profile called TBCXTUL.  That name was made up
*   from CBT backwards, X, and Tools.  But it can be any profile at
*   all, in the FACILITY class.
*
*   Make sure you have enough base register space, to make room for
*   RACWORK and FRACWORK data areas.  You can GETMAIN these as well.
*
***********************************************************************
*                                                                     *
*  CHECK THE AUTHORITY OF THE USER TO SAF CLASS "FACILITY' FOR        *
*  ENTITY "TBCXTUL".  NOTE THAT FASTAUTH IS USED.  TO AVOID THE       *
*  SITUATION WHERE SPECIAL ATTRIBUTES OF THE USER MAY ALLOW THE       *
*  ACCESS WITH LOGGING, A CHECK IS MADE FOR A NON-ZERO REASON         *
*  CODE.  THIS CONDITION WILL ALSO BE CONSIDERED A FAILURE.           *
*                                                                     *
***********************************************************************
*                                  Convert to an unconditional branch
         NOP   TESTEND             to bypass all of this checking.
*                                  This is nice to put in for
*                                  convenience, but don't publicize.
TEST0    RACROUTE REQUEST=LIST,WORKA=RACWORK,CLASS=FACILITY,           X
               ENVIR=CREATE,                                           X
               GLOBAL=YES,                                             X
               RELEASE=2.2,                                            X
               MF=S
***********************************************************************
*        TEST  PROFILE TBCXTUL OF FACILITY CLASS                      *
***********************************************************************
TEST1    MVC   RESOURCE,=CL44'TBCXTUL  '  (CBT BACKWARDS, X, TOOLS)
         RACROUTE REQUEST=FASTAUTH,WORKA=RACWORK,WKAREA=FRACWORK,      X
               ENTITY=RESOURCE,CLASS=FACILITY,ATTR=READ,               X
               MF=(E,RACROUTE)
         LTR   R15,R15             TEST THE RETURN CODE
         BNZ   TESTBAD             NOT ZERO, NOT AUTHORIZED
         CLC   RACROUTE+4(4),=F'0' TEST THE REASON CODE
         BNE   TESTBAD             NOT ZERO, NOT AUTHORIZED
         B     TESTOK
***********************************************************************
TESTBAD  DS    0H
TESTFAIL MVC   RESOURCE,=CL44' '   BLANK THE PROFILE NAME FOR "FAIL"
         OI    FLAG0,X'02'         TEST WAS NOT OK,
*                                  THEN FAIL AFTER RACF CLEANUP.
***********************************************************************
***********************************************************************
*        CLEANUP AFTER RACROUTE                                       *
***********************************************************************
TESTOK   RACROUTE REQUEST=LIST,WORKA=RACWORK,CLASS=FACILITY,           X
               ENVIR=DELETE,                                           X
               RELEASE=2.2,                                            X
               MF=S
         TM    FLAG0,X'02'
         BO    NOTAUTHR
TESTEND  DS    0H
*                      GO TO NORMAL END OF CHECKING (TEST WAS OK)
         EJECT
***********************************************************************
NOTAUTHR DS    0H                  If no PARM coded, then leave.
         MVI   OUTLINE-1,X'40'
         MVC   OUTLINE,OUTLINE-1
         MVC   OUTLINE+1(37),=C' You have not been authorized by RACF'
         PUT   SYSPRINT,OUTLINE
         MVC   OUTLINE,OUTLINE-1
         MVC   OUTLINE+1(21),=C' Nothing was changed.'
         PUT   SYSPRINT,OUTLINE
OPMSG3   WTO   'ONLC003 Not authorized by RACF--Nothing Changed',      +
               ROUTCDE=11
***********************************************************************
         DC    X'40'
OUTLINE  DS    CL133
SYSPRINT DCB   MACRF=PM,DDNAME=SYSPRINT,DSORG=PS,RECFM=FBA,LRECL=133
***********************************************************************
FLAG0    DC    X'00'
FACILITY DC    CL8'FACILITY'       RACROUTE CLASS NAME
RESOURCE DC    CL44' '             RACROUTE RESOURCE NAME
RACROUTE RACROUTE REQUEST=FASTAUTH,MF=L RACROUTE LIST FORM
RACWORK  DS    CL512               RACROUTE WORK AREA 1
FRACWORK DS    16F                 RACROUTE WORK AREA 2
***********************************************************************

