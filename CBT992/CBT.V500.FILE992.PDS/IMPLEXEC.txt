*  -----------------------------------------------------------------
*  THIS CODE, WHEN INSERTED INTO A TSO COMMAND SOURCE, WILL SIMULATE
*  A "IKJ56500I COMMAND XXXXXXXX NOT FOUND" MESSAGE, MAKING THE USER
*  WHO IS NOT AUTHORIZED, THINK THAT THE COMMAND DOESN'T EXIST.
*  -----------------------------------------------------------------
*  THIS CODE NEEDS TO BE MODIFIED TO ACCOMMODATE 8-CHARACTER USERIDS.
*  IF 8-CHARACTER USERIDS ARE IN EFFECT, AND YOU ARE RUNNING UNDER
*  ONE OF THEM, THEN THE CONTENTS OF THE PSCBUSER FIELD IS '>7BYTES',
*  THE PSCBUSRL FIELD (THE PSCBUSER LENGTH FIELD) IS X'07', AND
*  THE CORRECT USERID IS AT PSCB+X'54' PSCBUID8 FOR 8 BYTES, AND THE
*  CORRECT PSCB USER LENGTH IS AT PSCB+X'20' PSCBU8L WHICH IS X'08'.
*  SO FROM Z/OS 2.3 AND UP, IF 8-CHARACTER USERIDS ARE IN EFFECT,
*  YOU HAVE TO CODE FOR THIS. THE SWITCH TO SHOW WHETHER 8-CHARACTER
*  USERID SUPPORT IS TURNED ON, IS LOCATED IN THE TSVT (MACRO IKJTSVT)
*  CONTROL BLOCK, AT TSVT+6.  X'00' MEANS PRE-Z/OS 2.3. X'07' MEANS
*  THAT 8-CHARACTER USERID IS OF, AND X'08' MEANS 8-CHARACTER USERID
*  SUPPORT HAS BEEN TURNED ON.
*  -----------------------------------------------------------------
         L     R1,548              PSAAOLD
         L     R15,172(,R1)        ASCBJBNI
         LTR   R15,R15             IS THIS A JOB
         BNZ   IMPLEXEC            YES, QUIT
         L     R15,60(,R1)         ASCBTSB
         LTR   R15,R15             IS THIS A TSO SESSION
         BZ    PROCEED             NO, BRANCH IF STARTED TASK
.SKIP1   ANOP
AUTHID   L     R1,16               CVTPTR
         L     R15,0(,R1)          TCB WORDS CVTTCBP
         L     R15,4(,R15)         CURRENT TCB
         L     R1,180(,R15)        TCBJSCB
         L     R1,264(,R1)         JSCBPSCB
         LTR   R1,R1               ANY PSCB?
         BZ    IMPLEXEC            NO - NOT A TSO SESSION
         USING PSCB,R1
*        TM    PSCBATR1,PSCBCTRL   OPERATOR                       FEB99
*        BO    PROCEED             YES - PROCEED                  FEB99
*        AGO   .IMPLEX                                            FEB99
* ATTENTION:                       PERSONALLY, I THINK THAT IF YOU HAVE
* =========                        TSO OPER AND RACF UPDATE TO DASDVOL,
*                                  YOU'RE PROBABLY OKAY TO PROCEED.  IF
*                                  YOU WANT TO HARD-CODE VALID USERIDS,
*                                  THEN DELETE THE 3 FEB99 LINES ABOVE.
*                                  - GREG PRICE                   FEB99
*
*        TM    PSCBATR1,PSCBCTRL   OPERATOR
*        BZ    IMPLEXEC            NO - BRANCH
*        CLC   PSCBUSER(2),SAMURAI SYSTEMS SUPPORT USERID
*        BE    PROCEED             YES, BRANCH
         LA    R15,USERIDS
AUTHLOOP CLI   0(R15),0            END OF LIST?
         BE    IMPLEXEC            YES, NOT AUTHORIZED
         CLC   PSCBUSER(7),0(R15)
         BE    PROCEED
         LA    R15,8(,R15)         POINT TO NEXT USERID
         B     AUTHLOOP            GO CHECK IT
         DROP  R1                  PSCB
*SAMURAI DC    C'SP  '
USERIDS  DC    0D'0'               ALIGN FOR EASY ZAPS
         DC    CL8'MYUSRID '
         DC    CL8'IBMUSER '
         DC    CL8'USERID1 '
         DC    CL8'USERID2 '
         DC    CL8'        '
         DC    CL8'        '
         DC    CL8'        '
         DC    CL8'        '
         DC    H'0'                END OF USERID LIST
.IMPLEX  ANOP
         SPACE
IMPLEXEC EQU   *
         L     R1,CPPLCBUF
         XC    2(2,R1),2(R1)       SET CBUF TO IMPLICIT EXEC
         L     R1,CPPLECT          GET ECT ADDRESS
         USING ECT,R1
         CLI   ECTSCMD,C' '        IS THIS A SUBCOMMAND
         BNE   *+10                YES - SAY SUBCOMMAND NOT FOUND
         MVC   ECTPCMD,=CL8'EXEC'  NO  - SAY COMMAND NOT FOUND
         DROP  R1                  ECT
         LR    R1,R13              FREE GETMAINED AREA
         L     R0,@SIZE
         L     R13,4(,R13)
         FREEMAIN R,A=(1),LV=(0)
         L     R1,24(,R13)         RESTORE CPPL POINTER
         LA    R15,12(,R13)        POINT TO 2-WORD XCTL PARM
         XC    0(8,R15),0(R15)     CLEAR IT
         XCTL  (2,12),EP=EXEC,SF=(E,(15))
         EJECT
************************************************************
*                                                          *
*        SET UP IOPL FOR PUTLINE                           *
*                                                          *
************************************************************
         SPACE
PROCEED  LA    R15,MYIOPL
*    .  .  .  .  .  .  .  .  .  .  .  .  .  .  .  .  .
         IKJCPPL
         IKJPSCB
         IKJECT
