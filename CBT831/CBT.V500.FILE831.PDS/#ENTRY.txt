         MACRO
&NAME    #ENTRY &MAIN=NO,&LMD=
         LCLA  &DISP,&NFW
         AIF   ('&NAME' NE '').MAINVAL
         MNOTE 8,'AN ENTRY NAME MUST BE SUPPLIED(I.E. CSECTNM #NTRY)'
         MEXIT
.MAINVAL ANOP
         AIF   ('&MAIN' EQ 'NO').MNENTRY
         AIF   ('&MAIN' EQ 'YES').MNCSECT
         MNOTE 8,'&MAIN IS AN UNKNOWN VALUE OF MAIN. ONLY YES OR NO IS X
                 ALLOWED'
         MEXIT
.MNCSECT ANOP
&NAME    CSECT
         STM   R14,R12,12(R13)                  SAVE CALLER'S REGS
         LR    R12,R15                          SET BASE ON NTRY POINT
         USING &NAME,R12                        SET PGM ADDRESSABILITY
         B     *+34                             SKIP ENTRY STATTISTICS
         DC    CL8'&NAME'                       CSECT NAME
         DC    CL8'&SYSDATE'                    ASSEMBLY DATE
         DC    CL6'&SYSTIME'                    ASSEMBLY TIME
         DC    CL8'&LMD '                       USER MOD ID
         ST    R13,#SVEAREA+4                   CHAIN SAVE AREA
         MVC   8(4,R13),=A(#SVEAREA)            CHAIN SAVE AREA
         LA    R13,#SVEAREA                     POINT TO OUR SAVE AREA
         USING #GBLAREA,R13                     SET GLOBAL AREA ADRBLT
         MEXIT
.MNENTRY ANOP
&NAME    DS    0F
&NFW     SETA  0
&DISP    SETA  8
&NFW     SETA  13
&DISP    SETA  8+(&NFW*4)
         USING *,R15                            SET ADDRESSABILITY
         STM   R2,R14,16(R15)                   SAVE CALLER'S REGISTERS
         B     *+4+&DISP                        SKIP ENTRY STATTISTICS
         DC    CL8'&NAME'                       SKIP ENTRY STATTISTICS
         DS    &NFW.F                           #ENTRY SAVE AREA
         DROP  R15                              KILL R15 ADDRESSABILITY
         LR    R12,R15                          SET BASE ON NTRY POINT
         USING &NAME,R12                        SET PGM ADDRESSABILITY
         MEND
