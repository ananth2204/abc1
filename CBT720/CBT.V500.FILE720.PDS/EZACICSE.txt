         TITLE 'EZACICSE - CICS Sockets Listener Exit'
*********************************************************************
* AUTHOR - BILL SWEENEY                                             *
*     SSC, INC.                                                     *
*     WHSWEENEY@SSCMAINFRAME.COM                                    *
*     WWW.SSCMAINFRAME.COM                                          *
* DATE - (C) COPYRIGHT 2004                                         *
*                                                                   *
* DISCLAIMER - IT IS THE RESPONSIBILITY OF THE INIDIVIDUAL          *
*         TO VALIDATE THAT THIS SOFTWARE WILL NOT HARM YOUR         *
*         SYSTEM. INSTALL THIS SOFTWARE AT YOUR OWN RISK.           *
*         SSC, INC. IS NOT RESPONSIBLE FOR ANY PROBLEMS             *
*         CAUSED WHILE USING THIS SOFTWARE. GOOD LUCK.              *
*                                                                   *
* LINKAGE INFORMATION - AMODE 31                                    *
*                  RMODE 24                                         *
*                  REENTRANT                                        *
*                  CICS Command Level                               *
*                                                                   *
*  DESCRIPTION - CICS Listener security exit. CICS Command Level    *
*                Assembler.                                         *
*                                                                   *
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
***********************************************************************
         DFHEISTG
COMPTR   EQU   4                   POINTER TO COMMAREA
RESP_1   DS    F
RESP_2   DS    F
JOBNAME  DS    CL8
EZACICSE CSECT
*
*        THE LENGTH OF THE "COMMAREA" IS TESTED. IF ZERO, WHICH IT
*        SHOULDN'T BE, THEN WE'LL EXIT; OTHERWISE, PROCESS.
*
         LH    COMPTR,EIBCALEN     HAS A COMMAREA BEEN RETURNED?
         LTR   COMPTR,COMPTR
         BZ    RETURN              ...NO, THEN JUST EXIT
*
*
         L     COMPTR,DFHEICAP     GET ADRESSABILITY TO COMMAREA
         USING EXITDATA,COMPTR
*
         MVI   CLIENT_SWITCH2,C'1'    CICS LISTENER SENDS MSG TO CLIENT
*
         EXEC  CICS INQUIRE SYSTEM JOBNAME(JOBNAME)                    X
               RESP(RESP_1) RESP2(RESP_2)
*
         CLC   JOBNAME(8),PRODCICS
         BNE   CHECK_IPDEVL
         CLC   CLIENT_IPADDRESS(4),PROD_SERVER_IPADDRESS
         BNE   FAILIT
         B     CHECK_TRANS
CHECK_IPDEVL   EQU  *
         CLC   CLIENT_IPADDRESS(4),DEVL_SERVER_IPADDRESS
* The next 2 instructions were to test their production IP address
*  Commented out for production implementation  WHS  23 May 2001
*        BE    CHECK_TRANS
*        CLC   CLIENT_IPADDRESS(4),PROD_SERVER_IPADDRESS
         BNE   FAILIT
CHECK_TRANS    EQU  *
*  We're going to verify all four bytes
         CLC   CLIENT_TRANSACTION(4),SERVER_TRANSACTION
*        CLC   CLIENT_TRANSACTION(3),SERVER_TRANSACTION
         BNE   FAILIT
         MVI   CLIENT_SWITCH,C'1'     PERMIT THE TRANSACTION
         MVC   CLIENT_TERMID(4),SERVER_TERMID
         B     RETURN
*
FAILIT   MVI   CLIENT_SWITCH,C'0'     DO NOT PERMIT THE TRANSACTION
*
*        THE PROGRAM TERMINATES BY RETURNING TO CICS.
*
RETURN   EQU  *
         EXEC CICS RETURN
*
PRODCICS               DC   CL8'PRODCICS'
* I changed these IP addresses to distribute this source
* Have them match what you are doing
PROD_SERVER_IPADDRESS  DC   X'64646464'          100.100.100.100
DEVL_SERVER_IPADDRESS  DC   X'64646465'          100.100.100.101
* Next IP was used to test application talking w/in OS/390
*WDEVL_IPADDRESS   DC   X'64646466'          100.100.100.102
SERVER_TRANSACTION DC  CL4'ZLIS'            TRANSACTION
SERVER_TERMID     DC   CL4'ZZZZ'            PICKED ONE FOR NOW
*
         LTORG
EXITDATA DSECT
CLIENT_TRANSACTION    DS  CL4
CLIENT_DATAAREA       DS  CL40
CLIENT_ACTION         DS  CL2
CLIENT_INTERVALCNTL   DS  CL6
CLIENT_ADDRESS        DS  H
CLIENT_PORT           DS  H
CLIENT_IPADDRESS      DS  F
CLIENT_SWITCH         DS  X
CLIENT_SWITCH2        DS  X
CLIENT_TERMID         DS  CL4
CLIENT_SOCKET_DESC    DS  H
CLIENT_USERID         DS  CL8
         END
