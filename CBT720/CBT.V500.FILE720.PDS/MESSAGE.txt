         MACRO
         MESSAGE  &MSGID=,&FUNCTION=,&COMMAND=,&SEARCH=0,&SELECT=0,    X
               &MULTLINE=0,&COLOR=0
         LCLA  &FUNC1,&FUNC2
&FUNC1   SETA  0
&FUNC2   SETA  0
&SINGQUOT SETC  ''''
         AIF   ('&FUNCTION' EQ 'COMMAND').CMD,                         X
               ('&FUNCTION' EQ 'REPLY').RPLY,                          X
               ('&FUNCTION' EQ 'EXEC').EXEC,                           X
               ('&FUNCTION' EQ 'HIGHLITE').HILITE,                     X
               ('&FUNCTION' EQ 'EVENT').EVNT,                          x
               ('&FUNCTION' EQ 'SUPPRESS').SPRSS
        MNOTE  8,'AL001 FUNCTION MUST BE COMMAND, REPLY, EVENT OR EXEC'
         AGO    .END
.CMD     ANOP
&FUNC1   SETA   1
         AGO    .CONT1
.RPLY    ANOP
&FUNC1   SETA   2
         AGO    .CONT1
.EXEC    ANOP
&FUNC1   SETA   4
         AGO    .CONT1
.HILITE  ANOP
&FUNC1   SETA   128
         AIF   ('&COLOR' EQ 'RED').RED
         AGO    .CONT1
.RED     ANOP
&FUNC2   SETA   1
         AGO    .CONT1
.EVNT    ANOP
&FUNC1   SETA   8
         AGO    .CONT1
.SPRSS   ANOP
&FUNC2   SETA   4
.CONT1   ANOP
&LMSGID  SETC  'L''MSG&SYSNDX'
&QUOTEM  SETC  '&MSGID'(1,1)
&LCMD    SETC  'L''CMD&SYSNDX'
&ENTLNG  SETC  'ENTL&SYSNDX'
&QUOTE   SETC  '&COMMAND'(1,1)
.*
         AIF   ('&SEARCH' EQ '0').CONT2
&LSRCH   SETC  'L''SCH&SYSNDX'
&SCHLNG  SETC  'SCHL&SYSNDX'
&QUOTEL  SETC  '&SEARCH'(1,1)
&FUNC1   SETA  &FUNC1+16
         AIF   ('&SELECT' EQ '0').CONT2,                               X
               ('&SELECT' EQ 'SELECT').CONT2,                          X
               ('&SELECT' EQ 'JOBNAME').JBNME
  MNOTE  8,'AL002 SELECT OR JOBNAME ARE THE ONLY VALID SELECT KEYWORDS'
         AGO    .END
.JBNME   ANOP
&FUNC1   SETA   &FUNC1+32
&LSRCH   SETC   '8'
.*
.CONT2   ANOP
         AIF   ('&MULTLINE' EQ '0').CONT3,                             X
               ('&MULTLINE' EQ 'NO').CONT3,                            X
               ('&MULTLINE' EQ 'YES').MULTIL,                          X
               ('&MULTLINE' EQ 'ALL').MULTALL
        MNOTE  8,'AL002 YES OR NO ARE THE ONLY VALID MUTLLINE KEYWORDS'
         AGO    .END
.MULTIL  ANOP
&FUNC1   SETA   &FUNC1+64
         AGO    .CONT3
.MULTALL ANOP
&FUNC1   SETA   &FUNC1+64
&FUNC2   SETA   &FUNC2+2
.CONT3   ANOP
ENT&SYSNDX  DC    AL2(&ENTLNG)
            DC    AL1(&LMSGID)
            AIF   ('&QUOTEM' EQ '&SINGQUOT').NOQUOTM
MSG&SYSNDX  DC    C'&MSGID'             MESSAGE ID
            AGO   .ENDMSGID
.NOQUOTM ANOP
MSG&SYSNDX  DC    C&MSGID               MESSAGE ID
.ENDMSGID   ANOP
            DC    AL1(&FUNC1)           FUNCTION TO PERFORM
            DC    AL1(&FUNC2)           RESERVED
.*
            DC    AL1(&LCMD)
.*
         AIF   ('&QUOTE' EQ '&SINGQUOT').NOQUOTE
CMD&SYSNDX  DC    C'&COMMAND'         COMMAND, REPLY OR FIELD TO PASS
         AGO   .ENDCMD
.NOQUOTE ANOP
CMD&SYSNDX  DC    C&COMMAND           COMMAND, REPLY OR FIELD TO PASS
.*
.*
.ENDCMD  ANOP
         AIF   ('&SEARCH' EQ '0').ENDSRCH,
.*
            DC    AL1(&LSRCH)
.*
         AIF   ('&QUOTEL' EQ '&SINGQUOT').NOQUOTS
         AIF   ('&SELECT' EQ 'JOBNAME').JBNME8
SCH&SYSNDX  DC    C'&SEARCH'          SEARCH ARGUMENT
         AGO   .ENDSRCH
.JBNME8  ANOP
SCH&SYSNDX  DC    CL8'&SEARCH'          SEARCH ARGUMENT
         AGO   .ENDSRCH
.NOQUOTS ANOP
         AIF   ('&SELECT' EQ 'JOBNAME').NOQJBN8
SCH&SYSNDX  DC    C&SEARCH            SEARCH ARGUMENT
         AGO   .ENDSRCH
.NOQJBN8 ANOP
SCH&SYSNDX  DC    CL8&SEARCH            SEARCH ARGUMENT
.*
.ENDSRCH ANOP
ENTL&SYSNDX  EQU  *-ENT&SYSNDX
.END     ANOP
         MEND
