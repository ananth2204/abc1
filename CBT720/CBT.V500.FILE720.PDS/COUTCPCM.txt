********************************************************************
* AUTHOR - BILL SWEENEY                                            *
*     SSC, INC.                                                    *
*     WHSWEENEY@SSCMAINFRAME.COM                                   *
*     WWW.SSCMAINFRAME.COM                                         *
* DATE - (C) COPYRIGHT 2004                                        *
*                                                                  *
* DISCLAIMER - IT IS THE RESPONSIBILITY OF THE INIDIVIDUAL         *
*         TO VALIDATE THAT THIS SOFTWARE WILL NOT HARM YOUR        *
*         SYSTEM. INSTALL THIS SOFTWARE AT YOUR OWN RISK.          *
*         SSC, INC. IS NOT RESPONSIBLE FOR ANY PROBLEMS            *
*         CAUSED WHILE USING THIS SOFTWARE. GOOD LUCK.             *
*                                                                  *
* LINKAGE INFORMATION - AMODE 24                                   *
*                  RMODE 24                                        *
*                  NON-REENTRANT                                   *
*                  *** APF AUTHORIZED ***                          *
*                                                                  *
* DESCRIPTION -                                                    *
* THIS PROGRAM WILL ALLOW YOU TO ENTER UP TO 100 CP COMMANDS WHILE *
* MVS   RUNS AS A GUEST UNDER VM. IT USES THE FACILITIES OF THE    *
* DIAGNOSE INSTRUCTION AND READS THE COMMANDS AS SYSIN INPUT.      *
********************************************************************
COUTCPCM CSECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         B     72(R15)                    SAVE LOGIC
         DS    17F
         STM   R14,R12,12(R13)
         ST    R13,4(R15)
         ST    R15,8(R13)
         LR    R13,R15
         USING COUTCPCM,R13
********************************************************************
* GET TWO PAGES OF STORAGE FOR UP TO 100 80 BYTE RECORDS.          *
********************************************************************
         GETMAIN EU,LV=8196,A=STRADDR1
         L     R12,STRADDR1           LET R12 POINT TO STORAGE
         OPEN  SYSIN
         TM    SYSIN+48,X'10'         GOOD OPEN OF SYSIN
         BO    GOODOPEN               YES, THEN CONTINUE
         ABEND 400
GOODOPEN LA    R11,100                SET A LIMIT OF 100 MEMBERS
         SLR   R6,R6                  CLEAR R6 FOR USE AS COUNTER
READNEXT GET   SYSIN,(12)             READ A REACORD
         LA    R12,80(R12)            POINT TO NEXT POSITION
         LA    R6,1(R6)               KEEP TRACK OF HOW MANY READ
         BCT   R11,READNEXT           DO UP TO 100 COMMANDS
EODGET   CLOSE SYSIN                  CLOSE HIM
         LTR   R6,R6                  WERE THERE ANY COMMANDS
         BZ    FREESTOR               NO, THEN JUST QUIT
*********************************************************************
* SORRY FOR THE MODESET PEOPLE BUT WE NEED TO ISSUE PRIVELEGED      *
* INSTRUCTIONS, SO WE WILL TRY TO BE QUICK, AND ONLY SET IT ONCE.   *
*********************************************************************
         MODESET MODE=SUP,KEY=ZERO
         STIDP DOUBLE           STORE CPUID
         CLI   DOUBLE,X'FF'     IF FIRST CHARACTER FF THEN UNDER VM
         BNE   CHGMODE          NO, THEN DO NOT DIAGNOSE
         L     R12,STRADDR1     R12 POINT BACK TO FIRST RECORD
ENDREC   LA    R11,70(R12)      POINT TO END OF COMMAND RECORD
         LA    R9,71            TOTAL LENGTH OF 71
LOOPEND  CLI   0(R11),C' '      IS IT A NON BLANK OR LESS ENTRY ?
         BH    GOTEND           IF GT THEN WE GOT END OF RECORD
         BCTR  R11,0            NO, DECREMENT R11 BY 1
         BCT   R9,LOOPEND       LOOP THRU RECORD
         B     NEXTREC          IF WE FALL THRU THEN NO CMD TO ISSUE
GOTEND   SR    R11,R12          GET LENGTH OF COMMAND-1 INTO R11
********************************************************************
* PLEASE DO NOT LET THIS NEXT MOVE CONFUSE YOU, BUT WHEN TRYING TO *
* ISSUE A DIAGNOSE INSTR THAT DOES A LOAD REAL ADDR (LRA) TO       *
* GETMAINED STORAGE NOTHING HAPPENS. I THEREFORE TOOK THE COMMAND  *
* AND MOVED IT INTO STORAGE LOCATED WITHIN THE BOUNDS OF THIS      *
* PROGRAM. GO FIGURE.                                              *
********************************************************************
         EX    R11,MVCVMCM      MVC  VMCMD(0),O(R120
VERDEV   LRA   R8,VMCMD         LOAD R8 WITH REAL ADDR OF CMD
         LA    R10,1(R11)       GET THE LENGTH IN R10
         DC    X'838A0008'      ISSUE DIAGNOSE COMMAND
*
*  WAIT ONESEC CPU BETWEEN COMMANDS
*
         STIMER WAIT,,BINTVL=ONESEC
NEXTREC  LA    R12,80(R12)       POINT TO NEXT RECORD
         BCT   R6,ENDREC         LOOP THRU TIL DONE
CHGMODE  EQU   *
         MODESET MODE=PROB,KEY=NZERO
FREESTOR EQU   *
         FREEMAIN EC,LV=8196,A=STRADDR1
RETURN   EQU   *
         L     R13,4(R13)
         LM    R14,R12,12(R13)
         SLR   R15,R15          CLEAR R15 TO SHOW GOOD RC
         BR    R14
SYSIN    DCB   DDNAME=SYSIN,MACRF=GM,EODAD=EODGET,DSORG=PS
MVCVMCM  MVC   VMCMD(0),0(R12)
DOUBLE   DC    D'0'
ONESEC   DC    F'100'
STRADDR1 DC    F'0'
VMCMD    DC    CL71' '
         END
