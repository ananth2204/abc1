/**rexx**/
/*  PROGRAM-ID           EDTDSPLY.                                 */
/*  Displays the esoteric entries in the current EDT.              */
/*trace i*/
save_prompt = prompt("off")
/* --------------------------------------------------------------- */
/* Change the next line to point to your panel dataset in ISPPLIB. */
/* --------------------------------------------------------------- */
address "ISPEXEC" "LIBDEF ISPPLIB DATASET ID('SYS1.U.PANELS')"
edtscan:
ADDRESS "ISPEXEC" "CONTROL DISPLAY LOCK" /* LOCK THE TERMINAL      */
if (rc > 8) then do                      /* error?                 */
   say 'Lock error    rc = 'rc''         /* yes- output message    */
   ADDRESS "ISPEXEC" "LIBDEF ISPPLIB"    /* remove allocation      */
   exit(0)                               /* and quit               */
end
ADDRESS "ISPEXEC" "ADDPOP ROW(6) COLUMN(5)" /* pop up position    */
ADDRESS "ISPEXEC" "DISPLAY PANEL(EDTPAN02)"
   if (rc > 8) then do                      /* error?              */
      say 'Display error    rc = 'rc''      /* yes- output message */
      ADDRESS "ISPEXEC" "LIBDEF ISPPLIB"    /* remove allocation   */
      exit(0)                               /* and quit            */
   end                                                             */
EDTINF                                   /* Obtain EDT Info        */
if (rc ¬= 0) then do                     /* error?                 */
   edtrc= rc                             /* save the return code   */
   say 'EDTINF Error    rc = 'rc''       /* yes- output message    */
   ADDRESS "ISPEXEC" "LIBDEF ISPPLIB"    /* remove allocation      */
   exit(edtrc)                           /* and quit               */
end
call edt_esoteric_build                  /* build esoteric info    */
ADDRESS "ISPEXEC" "REMPOP"               /* remove popup           */
ADDRESS "ISPEXEC" "ADDPOP ROW(2) COLUMN(1)"  /* pop up position    */
ADDRESS "ISPEXEC" "TBDISPL EDTTAB PANEL(EDTPAN01)"
if (rc > 8) then do                      /* error?                 */
   say 'tbdispl error    rc = 'rc''      /* yes- output message    */
   ADDRESS "ISPEXEC" "LIBDEF ISPPLIB"    /* remove allocation      */
   exit(0)                               /* and quit               */
end
ADDRESS "ISPEXEC" "REMPOP"               /* remove menu popup      */
ADDRESS "ISPEXEC" "LIBDEF ISPPLIB"       /* remove allocation      */
exit(0)                                  /* quit                   */
edt_esoteric_build:
ADDRESS "ISPEXEC" "TBCREATE EDTTAB NOWRITE REPLACE"
if (rc >  4) then do                     /* call ok?               */
   say 'tbcreate error    rc = 'rc''     /* no- inform the user    */
   ADDRESS "ISPEXEC" "LIBDEF ISPPLIB"    /* remove allocation      */
   exit(0)                               /* lets quit              */
end
ztdmark=    '-------------------------------------------------------------------
--------'
ADDRESS  "ISPEXEC" "VPUT    (esoteric,vio,devnums,ZTDMARK) SHARED"
tabrows=  5000                           /* max table rows         */
no_edt_table_entries= x2d(c2x(substr(edttab,1,4))) /*no of table ents*/
displ= 5                                 /* starting displacement  */
do i = 1 to no_edt_table_entries         /* loop through all tab ents*/
   esoteric= substr(edttab,displ,8)      /* esoteric name          */
   vio=      substr(edttab,displ+8,1)    /* vio eligible           */
   no_of_device_nums= x2d(c2x(substr(edttab,displ+9,4)))
   displ= (displ + 13)                   /* position onto dev nums */
   devnums_displ= displ                  /* device number start    */
   devnums= ''                           /* init                   */
   devnums_per_line= 0                   /* init                   */
   do x= 1 to no_of_device_nums          /* print the device nums  */
      devnums= devnums||' '||substr(edttab,devnums_displ,4)
      devnums_displ= (devnums_displ + 4) /* next entry             */
      devnums_per_line= (devnums_per_line + 1)
      if (devnums_per_line = 10) then do /* ten device nos per line*/
         ADDRESS "ISPEXEC" "TBADD EDTTAB  /* add the entries       */
                 SAVE(esoteric,vio,devnums)
                 MULT("TABROWS")"
         if (rc ¬= 0) then do           /* call ok?                */
            say 'tbadd error    rc = 'rc'' /* no- inform the user  */
            ADDRESS "ISPEXEC" "LIBDEF ISPPLIB" /* remove allocation*/
            exit(0)                      /* let's quit             */
         end
         else do
            esoteric= ''                 /* init                   */
            vio=      ''                 /* init                   */
            devnums=  ''                 /* init                   */
            devnums_per_line= 0          /* init                   */
         end
      end
   end
  if (devnums_per_line ¬= 0) then do     /* anything left to print?*/
     ADDRESS "ISPEXEC" "TBADD EDTTAB     /* yes- add the entries   */
              SAVE(esoteric,vio,devnums)
              MULT("TABROWS")"
     if (rc ¬= 0) then do               /* call ok?                */
        say 'tbadd error    rc = 'rc' ' /* no- inform the user  */
        ADDRESS "ISPEXEC" "LIBDEF ISPPLIB" /* remove allocation*/
        exit(0)                         /* let's quit              */
     end
  end
  displ= devnums_displ                  /* next edt entry position */
end
ADDRESS "ISPEXEC" "TBTOP EDTTAB"         /* position to top of tab */
if (rc ¬= 0) then do                     /* call ok?               */
   say 'tbtop error    rc = 'rc''        /* no- inform the user    */
   ADDRESS "ISPEXEC" "LIBDEF ISPPLIB"    /* remove allocation      */
   exit(0)                               /* let's quit             */
end
return
