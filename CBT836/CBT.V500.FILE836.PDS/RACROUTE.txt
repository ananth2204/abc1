RACROUTE TITLE 'RACROUTE STATUS=ACCESS'
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
*
*        Author:   David Spiegel
*
*        Update:   Sam Golob  - June 17, 2002
*                  Return words instead of only the reason code.
*
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
*
*  This program is meant to be used with the following REXX:
*
*  /*   rexx   */
*  Arg dsn
*  Address TSO "Call *(Racrout) '"dsn"'"
*
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
RACROUTE CSECT ,
RACROUTE AMODE 31
RACROUTE RMODE ANY
         SAVE  (14,12),,RACROUTE.&SYSDATE..&SYSTIME Save caller's regs.
         LR    R12,R15            Load address of entry point
         USING RACROUTE,R12       Give assembler program's base reg.
*
         LR    R11,R1             Save Pointer to Parameter Address Blk
         USING ARGS,R11
*
         GETMAIN R,LV=SAVESIZE,LOC=BELOW
         XC    0(CLEARSIZ,R1),0(R1)  Clear the save/work area
         ST    R1,8(,R13)         Save new save area addr.in old area
         ST    R13,4(,R1)         Save old save area addr.in new area
         LR    R13,R1             Replace old save area with new one
         USING SAVEAREA,R13       Tell assembler about save area
*
*
*
         L     R11,0(,R11)
         LH    R5,0(,R11)
         C     R5,=F'0'            PARM?
         BE    RETURN              No, RETURN
*
         BCTR  R5,0
         EX    R5,MVC
         B     CONTINUE
*
MVC      MVC   THEENT(*-*),2(R11)
*
*
*
CONTINUE DS    0F
         RACROUTE REQUEST=AUTH,                                        x
               RELEASE=1.9,                                            x
               STATUS=ACCESS,                                          x
               CLASS='DATASET',                                        x
               ATTR=UPDATE,                                            x
               ENTITY=THEENT,VOLSER=THEVOL,                            x
               WORKA=SAFWORKA
         LM    R3,R4,CONTINUE+4    Save Return Code, Reason Code
         ST    R4,SAVER4A
*
*
*
RETURN   DS    0H
*        WTO   'RACROUTE About to Exit'
         L     R4,SAVER4A
         B     *+4(R4)
         B     NONE
         B     READ
         B     UPDATE
         B     CONTROL
         B     ALTER
         B     OUTFREE
         B     OUTFREE
OUTFREE  DS    0H
         LR    R1,R13             Save old save area addr. for FREEMAIN
         L     R13,4(,R13)        Restore old save area address
         FREEMAIN R,LV=SAVESIZE,A=(1)
EXIT     DS    0H
*        SLR   R15,R15            Set a return code of zero
         LR    R15,R4             Return with Reason Code
         RETURN (14,12),RC=(15)   Return to caller, return code zero
*
NONE     DS    0H
         MVI   OUTLINE-1,X'40'
         MVC   OUTLINE,OUTLINE-1
         MVC   OUTLINE,=CL17'Access is NONE   '
         TPUT  OUTLINE,17
         B     OUTFREE
READ     DS    0H
         MVI   OUTLINE-1,X'40'
         MVC   OUTLINE,OUTLINE-1
         MVC   OUTLINE,=CL8'READ'
         MVC   OUTLINE,=CL17'Access is READ   '
         TPUT  OUTLINE,17
         B     OUTFREE
UPDATE   DS    0H
         MVI   OUTLINE-1,X'40'
         MVC   OUTLINE,OUTLINE-1
         MVC   OUTLINE,=CL17'Access is UPDATE '
         TPUT  OUTLINE,17
         B     OUTFREE
CONTROL  DS    0H
         MVI   OUTLINE-1,X'40'
         MVC   OUTLINE,OUTLINE-1
         MVC   OUTLINE,=CL17'Access is CONTROL'
         TPUT  OUTLINE,17
         B     OUTFREE
ALTER    DS    0H
         MVI   OUTLINE-1,X'40'
         MVC   OUTLINE,OUTLINE-1
         MVC   OUTLINE,=CL17'Access is ALTER  '
         TPUT  OUTLINE,17
         B     OUTFREE
*
*
*
         DS    0D
SAFWORKA DS    CL512
THEENT   DC    CL44' '
THEVOL   DC    CL6'THEVOL'
*
*
*
SAVEAREA DSECT ,                  Register save area and work area
         DS    18F                Register save area
CLEARSIZ EQU   *-SAVEAREA         Save and work area size
         DS    X
OUTLINE  DS    CL19
SAVER4A  DS    F
RECORD   DS    0CL16
USERID   DS    CL8
PASSWORD DS    CL8
RECSIZE  EQU   *-RECORD
WTO0     WTO   '                                                     ',X
               MF=L
WTO0L    EQU   *-WTO0
SAVESIZE EQU   *-SAVEAREA         Save and work area size
*
*
*
ARGS DSECT
          DS   0D
LENGTH    DS   H
DSNAME    DS   CL44               Dataset Name to Check
          YREGS
          END
