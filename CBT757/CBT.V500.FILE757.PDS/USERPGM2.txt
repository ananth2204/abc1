***********************************************************************
*+-------------------------------------------------------------------+*
*|                                                                   |*
*| PROGRAM NAME:   USERPGM2                                          |*
*|                                                                   |*
*| FUNCTION:       Program Example. It opens a data set, prints a    |*
*|                 test message, calls programs, and closes the data |*
*|                 set. Shows an example of defined log points and   |*
*|                 the log point information written to output.      |*
*|                                                                   |*
*|                 At program end an intentional 'abend' is done to  |*
*|                 show an example of the diagnostic abend report    |*
*|                 written to output.                                |*
*|                                                                   |*
*| ATTRIBUTES:     REENTRANT, REUSABLE                               |*
*|                 AMODE 31, RMODE 31, AC=0                          |*
*|                                                                   |*
*+-------------------------------------------------------------------+*
***********************************************************************
*
         COPY  LGCPLOGR            ==>Include Logger CopyBook First<==
*
USERPGM2 LPGMNTRY TYPE=MAIN,       Define Main Program Entry (Required)X
               BASE=(12),          Establish Base Register (Default)   X
               AMOD=31,RMOD=31,    Addressing and Residency (Default)  X
               STORAGE=32,         Define LCA Storage Size (Default)   X
               LOG=ON,             Turn On Logging (Default)           X
               LOGOUT=PRNT,        Print Records When Gen'ed (Default) X
               LCAPFX=USR,         Name the LCA Prefix (Required)      X
               MSG='Main User Program 2 Example' Set Any Msg (Optional)
*
***********************************************************************
* Get the parm address for parameters passed to sample user program.  *
* The caller's save area address is in the LCA. If you had specified  *
* LCAPFX=XYZ, then the label references would need to use that prefix *
* as in:                                                              *
*  LG    R1,XYZSAVE+(XYZS64BCK-XYZSAVE) Get Callers Save Area Adr     *
***********************************************************************
*
USER0100 DS    0H
         LG    R1,USRSAVE+(USRS64BCK-USRSAVE) Get Callers Save Area Adr
         L     R2,24(,R1)          Get the Parameter List Address
         ST    R2,USR$PARM         Save the Parameter List Address
*
***********************************************************************
* Open a data set and show an example for displaying storage areas    *
***********************************************************************
*
USER0200 DS    0H
         MVC   SYSPRINT(SYSPRTLN),SYS$PRNT ...Fill SYSPRINT DCB List
         MVC   SYSOPENL,SYSLOPEN   Build the Open Parameter List
         LA    R2,SYSPRINT         Get Address of DCB
         ST    R2,SYSOPENL+4       Put Address in Work Area in LCA
         LA    R2,SYSOPENL         Address of DCB List
*
USER0300 #LGPOINT  SHOW=('SYSPRINT',SYSPRINT,SYSPRTLN),                X
               MSG=('Show the SYSPRINT DCB before Open')
*
         OPEN  MF=(E,(R2)),MODE=31 Execute Form of Open
*
USER0400 #LGPOINT  SHOW=('SYSPRINT',SYSPRINT,SYSPRTLN),                X
               MSG=('Show the SYSPRINT DCB after Open')
*
         PUT   SYSPRINT,SOMEMSG    Now Print a Test Message to Output
*
***********************************************************************
* Call a Program                                                      *
***********************************************************************
*
USER0500 DS    0H
         MVC   ANY$DATA,BLANKS     Fill an Area with Blanks
         LH    R2,=Y(L'ANY$DATA)   Indicate Length of Our Area
         CALL  ABCD0000,           Perform Call Processing             X
               ((R2),              ..Pass the Length of Area           X
               ANY$DATA),          ..Pass Address of Some Area         X
               VL,MF=(E,USRPLIST)  ..Remote Program Parameter List
*
USER0600 DS    0H
         MVC   SYSCLOSL,SYSLCLOS   Build the Close Parameter List
         LA    R2,SYSPRINT         Get Address of DCB
         ST    R2,SYSCLOSL+4       Put Address in List Area in LCA
         LA    R2,SYSCLOSL         Address of DCB List
         CLOSE MF=(E,(2)),MODE=31  Execute Form of Close
*
USER0700 #LGPOINT  SHOW=(('SYSPRINT',SYSPRINT,SYSPRTLN),               X
               ('ANY$DATA',ANY$DATA,L'ANY$DATA))
USER0800 DS    0H
         LA    R2,BAD$DATA         Address of Where Our Data Resides
         LA    R3,L'BAD$DATA       Get the Length of Data to Process
         LGR   R4,R3               From Data Leng in Bytes to Work Reg
         SLLG  R4,R4,1             Multiply * 2 to Expand Receive Field
         LGR   R5,R4               Recieve Field Leng to Work Register
         SLLG  R5,R5,4             Shift Receive Length to 1st Position
         OGR   R5,R3               Put the Two Pack Lengths Together
         EX    R5,PACKWORK         Pack the Bad Data and **0C7 Abend**
*
***********************************************************************
*     Return To Caller                                                *
***********************************************************************
*
USEREXIT DS    0H
         LPGMEXIT                  Define Program Exit
*
***********************************************************************
*     Area for Data Constants                                         *
***********************************************************************
*
PACKWORK ZAP   USR$WORK(0),0(0,R2) Indicated Target of the Execute
BLANKS   DC    CL256' '            Area with Blanks
SOMEMSG  DC    CL121' This is a test message.'
BAD$DATA DC    CL7'BADDATA'        This is some test data.
*
         DC    AL2(SYSPRTLN)       Data Control Block Length
SYS$PRNT DCB   DSORG=PS,           Physical Sequential Organization    X
               DDNAME=SYSPRINT,    Data Definition Name                X
               MACRF=(PM),         Put Move Mode                       X
               RECFM=FBA,          Data Set Record Format              X
               LRECL=121           Record Length
SYSPRTLN EQU   *-SYS$PRNT          Length Definition for SYSPRINT DCB
*
SYSLOPEN OPEN  (,OUTPUT),MF=L,MODE=31  DCB Open Parameter List
SYSLOPLN EQU   *-SYSLOPEN          Length Definition for DCB Open List
SYSLCLOS CLOSE (*-*),MF=L,MODE=31  CLOSE Macro Parameter List
SYSLCLLN EQU   *-SYSLCLOS          Length Definition for DCB Close List
*
***********************************************************************
*     Program RENT Work Area for USERPGM2 RSECT                       *
***********************************************************************
*
         LCA   DEFINE              Define Program RENT Work Area
*
USR$WORK DS    10D                 General Work Area
USRPLIST DS    10D                 Call Parameter List Area
USR$PARM DS    F                   User Parameter List Area
ANY$DATA DS    CL256               An Area to Put Any Data
SYSPRINT DS    XL(SYSPRTLN)        SYSPRINT DCB Area
SYSOPENL DS    XL(SYSLOPLN)        Open Parameter List
SYSCLOSL DS    XL(SYSLCLLN)        Close Parameter List
*
         LCA   DEFEND              Define End of Program RENT Work Area
*
***********************************************************************
*                    End of Main Program                              *
***********************************************************************
         EJECT ,
*
***********************************************************************
*+-------------------------------------------------------------------+*
*|   Called Program ABCD0000                                         |*
*+-------------------------------------------------------------------+*
***********************************************************************
*
ABCD0000 LPGMNTRY TYPE=SUB,        Define Sub-Program Entry (Required) X
               BASE=(12,11),       Establish Mutiple Base Registers    X
               LCAPFX=ABC,         Name the LCA Prefix (Required)      X
               MSG='Called Program 1 Example' ...Message is Optional
*
***********************************************************************
*Get the parm address for parameters passed to called program example.*
***********************************************************************
*
ABCD0100 #LGPOINT
         LG    R1,ABCSAVE+(ABCF64R01-ABCSAVE) Parameter List Address
         LM    R1,R2,0(R1)         Get the Input Parameters
         ST    R1,ABC$LENG         Save Length of Parm Area Passed
         O     R2,=A(X'80000000')  Clear Parameter List VL Indicator
         ST    R2,ABC$ADDR         Save the Parm Area Address
*
***********************************************************************
* Call Another Program                                                *
***********************************************************************
*
ABCD0200 #LGPOINT REG=(R1,R3,R5)
         CALL  WXYZ0000,           Perform Call Processing             X
               ABC$PARM,           ..Pass Common Message Parameters    X
               VL,MF=(E,ABCPLIST)  ..Remote Program Parameter List
*
         SLR   R0,R0
         LA    R15,256
ABCD0300 #LGPOINT  SHOW=('DATATABL',DATATABL,(R15)),REG=ALL,           X
               MSG='Show Contents of Data Table and Regs'
ABCD0400 #LGPOINT
         BAS   R14,ASUB0000        Branch to Subroutine
*
ABCD$END #LGPOINT
         SLR   R15,R15             Indicate No Errors in Processing
*
***********************************************************************
*     Return To Caller of ABCD0000                                    *
***********************************************************************
*
ABCDEXIT #LGPOINT
         ST    R15,ABCRETCD        Set the Return Code
         LPGMEXIT RC=ABCRETCD      Define Program Exit Protocol
*
***********************************************************************
*     Subroutine                                                      *
***********************************************************************
*
ASUB0000 LPGMSUBE                  Define Subroutine Entry
         SLR   R0,R0               * Some Code *
ASUBEXIT LPGMSUBX                  Define Subroutine Exit
*
***********************************************************************
*     Area for Data Constants                                         *
***********************************************************************
*
DATATABL DC    256AL1(*-DATATABL)  Define a Table
*
***********************************************************************
*     Program RENT Work Area for ABCD0000 RSECT                       *
***********************************************************************
*
         LCA   DEFINE              Define Program RENT Work Area
*
ABC1WORK DS    10D                 General Work Area
ABCPLIST DS    10D                 Call Parameter List Area
ABC$PARM DS    F                   User Parameter List Area
ABC$ADDR DS    F                   Area to Save Parameter Area Address
ABC$LENG DS    F                   Area to Save Parameter Area Length
ABCRETCD DS    F                   Area to Save Return Code
*
         LCA   DEFEND              Define End of Program RENT Work Area
         EJECT ,
*
***********************************************************************
*+-------------------------------------------------------------------+*
*|   Called Program WXYZ0000                                         |*
*+-------------------------------------------------------------------+*
***********************************************************************
*
WXYZ0000 LPGMNTRY TYPE=SUB,        Define Sub-Program Entry (Required) X
               LCAPFX=WXY,         Name the LCA Prefix (Required)      X
               MSG='Called Program 2 Example' ...Message is Optional
*
WXYZ0100 #LGPOINT MSG='I am here.'
         LA    R6,4                Set a Return Code
         LA    R7,5                Set a Reason Code
WXYZ0200 #LGPOINT PVTAREA=YES
         SLR   R15,R15             Clear a Register
*
***********************************************************************
*     Return To Caller of WXYZ0000                                    *
***********************************************************************
*
WXYZEXIT DS    0H
         LPGMEXIT RC=(R6),RS=(R7)  Define Program Exit Protocol
*
***********************************************************************
*     Program RENT Work Area for WXYZ0000 RSECT                       *
***********************************************************************
*
         LCA   DEFINE              Define Program RENT Work Area
WXY2WORK DS    10D                 General Work Area
         LCA   DEFEND              Define End of Program RENT Work Area
*
         END
