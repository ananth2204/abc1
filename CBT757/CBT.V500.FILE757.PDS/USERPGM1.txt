***********************************************************************
*                                                                     *
* PROGRAM NAME:    USERPGM1                                           *
*                                                                     *
* FUNCTION:        Program Example. It opens a data set, prints a test*
*                  message, closes the data set, and ends.            *
*                                                                     *
* ATTRIBUTES:      REENTRANT, REUSABLE                                *
*                  AMODE 24, RMODE 24, AC=0                           *
*                                                                     *
***********************************************************************
*
         COPY  LGCPLOGR            ==>Include Logger CopyBook First<==
*
USERPGM1 LPGMNTRY TYPE=MAIN,       Define Main Program Entry (Required)X
               AMOD=24,RMOD=24,    Addressing and Residency            X
               LCAPFX=USR          Name the LCA Prefix (Required)
*
USER0100 DS    0H
         MVC   SYSPRINT(SYSPRTLN),SYS$PRNT ...Fill SYSPRINT DCB List
         LA    R2,SYSPRINT         Get Address of our DCB
         ST    R2,USR$WORK         Put Address in Work Area in LCA
         OI    USR$WORK,X'8F'      Set VL Bit and Indicate Output
         LA    R2,USR$WORK         Address of DCB List
         OPEN  MF=(E,(R2))         Open Using DCB Copied to Rent Area
USER0200 DS    0H
         PUT   SYSPRINT,SOMEMSG    Now Print a Test Message to Output
USER0300 DS    0H
         LA    R2,SYSPRINT         Get Address of DCB
         ST    R2,USR$WORK         Put Address in Work Area in PCA
         OI    USR$WORK,X'80'      Set VL Bit Indicator
         LA    R2,USR$WORK         Address of DCB List
         CLOSE MF=(E,(R2))         Close It
*
***********************************************************************
*     Return To Caller                                                *
***********************************************************************
*
USEREXIT DS    0H
         LPGMEXIT                  Define Program Exit
*
***********************************************************************
*     Area for Data Constants                                         *
***********************************************************************
*
SOMEMSG  DC    CL121' This is a test message.'
*
         DC    AL2(SYSPRTLN)       Data Control Block Length
SYS$PRNT DCB   DSORG=PS,           Physical Sequential Organization    X
               DDNAME=SYSPRINT,    Data Definition Name                X
               MACRF=(PM),         Put Move Mode                       X
               RECFM=FBA,          Data Set Record Format              X
               LRECL=121           Record Length
SYSPRTLN EQU   *-SYS$PRNT          Length Definition for SYSPRINT DCB
*
***********************************************************************
*    Program RENT Work Area, Logger saves your registers and makes    *
*    progran reentrant for you. This area pointed to by R13.          *
***********************************************************************
*
         LCA   DEFINE              Define Program RENT Work Area
*
USR$WORK DS    10D                 General Work Area
USRPLIST DS    10D                 Call Parameter List Area
ANY$DATA DS    CL256               An Area to Put Any Data
SYSPRINT DS    XL(SYSPRTLN)        SYSPRINT DCB Area
*
         LCA   DEFEND              Define End of Program RENT Work Area
*
***********************************************************************
*                    End of Program Example                           *
***********************************************************************
*
         END
