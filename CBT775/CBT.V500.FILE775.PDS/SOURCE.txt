./ ADD NAME=$README  0105-07348-07351-1031-00013-00002-00000-DOTCWS
This dataset contains the source code for the SHARE session 8247 -
Writing a web-enabled CICS/COBOL program, presented at SHARE 109 in
San Diego in August of 2007 and SHARE 110 in Orlando in February
of 2008.

Contact information: cschneidpublic@yahoo.com

Member
HTTPSAMP  the sample web-enabled CICS/COBOL application
J7200501  translates EIBRESP value to its mnemonic
J7200521  encapsulate the CICS WRITE OPERATOR function
J7200524  encapsulate LE services to obtain and format time/date

./ ADD NAME=HTTPSAMP 0199-06355-07348-1609-02118-04786-00000-DOTCWS
       Identification Division.
       Program-ID.    HTTPSAMP.
       Author.        Craig Schneiderwent.
      *
      * The intent of this program is to provide both proof of concept
      * and example code for CICS applications that wish to be invoked
      * via an http or https request.
      *
      * Invoke via
      * https://your.url.here:####/CICS/CWBA/HTTPSAMP?t=<rplyTy>
      * Where #### is your designated port number.
      *
      *
      * Where <rplyTy> is
      * html - HTML page
      * xml  - XML artificially inflated to > 32K for demo purposes
      * imag - image (jpeg) to demonstrate it can be done
      * form - HTML form which can be sent back via HTTP POST to
      *        demo HTTP POST processing
      *
      * One other optional parameter that can be passed
      * c=y  - include a Content-Type header
      * c=n  - do not include a Content-Type header
      *
      * Multiple parameters are passed as follows:
      * HTTPSAMP?t=html&c=n
      *
      * This program also demonstrates multiple methods of passing
      * data from the web side to the CICS/COBOL side.  One can pass
      * a parameter on an HTTP GET method as indicated above, or one
      * can pass data on an HTTP POST method as is done with HTML form
      * processing.  Note that an HTTP POST is not limited to HTML form
      * processing.  One could also pass e.g. XML, and the CICS COBOL
      * program would then do a WEB RECEIVE to obtain the XML.
      *
      * Much of what is in here comes from the RFCs (Requests for
      * Comment) for the http protocol.  The RFCs are available at
      * http://www.ietf.org/rfc.html.
      *
      * Some RFCs of note:
      *
      * Hypertext Transfer Protocol -- HTTP/1.0
      * http://www.ietf.org/rfc/rfc1945.txt?number=1945
      *
      * Hypertext Transfer Protocol -- HTTP/1.1
      * http://www.ietf.org/rfc/rfc2616.txt?number=2616
      *
      * HTTP Header Field Registrations
      * http://www.ietf.org/rfc/rfc4229.txt?number=4229
      *
      * HTTP Authentication: Basic and Digest Access Authentication
      * http://www.ietf.org/rfc/rfc2617.txt?number=2617
      *
      *
      * This program seeks to make judicious use of the WRITE OPERATOR
      * API, logging errors of note while working hard to avoid a
      * situation where the CICS JESMSGLG would be flooded with
      * messages.  Thus the messages written to the JESMSGLG are _not_
      * the result of "business logic" errors, but actual application
      * bugs or CICS errors that may require some action be taken.
      *
      * Note that the WEB * CICS APIs are not threadsafe in CICS
      * TS 2.2, but they are threadsafe in CICS TS 3.1.  In order to
      * be a good CICS citizen, this program restricts itself to
      * threadsafe CICS APIs.  The J7200521 subroutine executes the
      * WRITE OPERATOR API on our behalf, as it is not threadsafe.
      * By LINKing to this subroutine, we avoid the TCB switching
      * problems that would result from dynamically or statically
      * calling J7200521.
      *
      * The CICS TS Application Programming Guide has an extensive
      * discussion of threadsafe and its implications.
      *
      * Debugging code left in for illustrative purposes is marked
      * with a '#debug' beginning in column 1.
      *
      *
       Environment Division.
       Configuration Section.
#debug COPY '$DEBUG01'.
       Data Division.
       Working-Storage Section.
       01  CONSTANTS.
           05  MYNAME                  PIC X(008) VALUE 'HTTPSAMP'.
           05  CICS-ERR-PGM            PIC X(008) VALUE 'J7200501'.
           05  CICS-WTO-PGM            PIC X(008) VALUE 'J7200521'.
           05  HTTP-DT-TM-PGM          PIC X(008) VALUE 'J7200524'.
      *
      *        This is used to get a date/time string in an HTTP-
      *        approved format.
      *
           05  DT-PIC-STRN.
               10                      PIC S9(004) COMP-5 VALUE +80.
               10                      PIC X(080)
                   VALUE 'Www, DD Mmm YYYY HH:MI:SS GMT'.
      *
      *        This is used to indicate we want GMT time from J7200524.
      *
           05  GMT-TM-FL               PIC X(001) VALUE 'N'.
      *
      *        This is used to retrieve the client code page.
      *
           05  HTTP-CHARSET-HDR        PIC X(014)
               VALUE 'Accept-Charset'.
      *
      *        The following HTTP-CNTE-TY-* constants are for the
      *        Content-Type HTTP protocol header.  Unsurprisingly,
      *        they specify what content type the message body
      *        consists of.
      *
           05  HTTP-CNTE-TY-HTML       PIC X(009) VALUE 'text/html'.
           05  HTTP-CNTE-TY-XML        PIC X(008) VALUE 'text/xml'.
           05  HTTP-CNTE-TY-IMAG       PIC X(010) VALUE 'image/jpeg'.
      *
      *        The host code page is used in translating to/from
      *        the client code page.  The problem here is that the
      *        host code page is set at compile time for each
      *        program.  The current default at WisDOT is
      *        CODEPAGE(037).
      *
      *        This constant _must_ reflect the correct code page.
      *
           05  HOST-CD-PG              PIC X(008) VALUE '037     '.
      *
      *        These are the names of the input fields in the HTML
      *        form defined in FORM-RPLY.  They are used to retrieve
      *        the values of the fields with WEB READ FORMFIELD.
      *
           05  FORM-FLD-NM-TY-LIT      PIC X(004) VALUE 'type'.
           05  FORM-FLD-NM-CNTE-LIT    PIC X(007) VALUE 'cntehdr'.
      *
      *        These constants are used in the HTTP protocol header.
      *
           05  HTTP-PTCL-HDR-SRVR-LIT  PIC X(006)
               VALUE 'Server'.
           05  HTTP-PTCL-HDR-SRVR      PIC X(004)
               VALUE 'CICS'.
           05  HTTP-PTCL-HDR-DT-LIT    PIC X(004)
               VALUE 'Date'.
           05  HTTP-PTCL-HDR-CNTE-TY-LIT PIC X(012)
               VALUE 'Content-Type'.
           05  HTTP-PTCL-HDR-CACHE-CNTL-LIT PIC X(013)
               VALUE 'Cache-Control'.
           05  HTTP-PTCL-HDR-CACHE-CNTL PIC X(008)
               VALUE 'no-cache'.
           05  PTCL-LIT                 PIC X(004)
               VALUE 'http'.
      *
      *        These are application-specific status codes returned
      *        to the caller.
      *
           05  RPLY-STUS-CD-LITS.
               10  STUS-0001           PIC X(050)
                   VALUE '0001 Success'.
               10  STUS-0002           PIC X(050)
                   VALUE '0002 Internal Application Error'.
               10  STUS-0003           PIC X(050)
                   VALUE '0003 Bad Request Received From Client'.
               10  STUS-0004           PIC X(050)
                   VALUE '0004 Request URI Too Long'.
               10  STUS-0005           PIC X(050)
                   VALUE '0005 Request Query String Too Long'.

      *    Due to its size, this buffer is defined such that
      *    it can be displayed in a minimum number of lines
      *    in a core dump.  Not that I expect any...
       01  WS-RPLY-BUFR.
           05  PIC X(050) OCCURS 40960.

#debug COPY '$DEBUG02'.

       01  SWITCHES.
           05  NON-HTTP-RQST-DONE-SW   PIC X(001)  VALUE SPACE.
               88  NON-HTTP-RQST-DONE              VALUE 'Y'.
           05  HAVE-ERR-MSG-SUFX-SW    PIC X(001)  VALUE SPACE.
               88  HAVE-ERR-MSG-SUFX               VALUE 'Y'.
           05  HTTP-HDR-NOT-FND-SW     PIC X(001)  VALUE SPACE.
               88  HTTP-HDR-NOT-FND                VALUE 'Y'.
           05  FORM-FLD-NOT-FND-SW     PIC X(001)  VALUE SPACE.
               88  FORM-FLD-NOT-FND                VALUE 'Y'.
           05  CICS-ERR-SW             PIC X(001)  VALUE SPACE.
               88  CICS-ERR                        VALUE 'Y'.
           05  BINARY-CONTENT-SW       PIC X(001)  VALUE SPACE.
               88  BINARY-CONTENT                  VALUE 'Y'.

       01  WORK-AREAS.
           05  WTO-LN                  PIC 9(008)  COMP-5 VALUE 1.
           05  WTO-SUFX-LN             PIC 9(008)  COMP-5 VALUE 1.
           05  WS-RESP                 PIC S9(008) COMP-5 VALUE +0.
           05  WS-RESP2                PIC S9(008) COMP-5 VALUE +0.
           05  WS-RPLY-BUFR-LN         PIC S9(008) COMP-5 VALUE +0.
           05  WS-HTTP-HDR-TO-RTV-LN   PIC S9(008) COMP-5 VALUE +0.
           05  WS-HTTP-HDR-BUFR-LN     PIC S9(008) COMP-5 VALUE +0.
           05  WS-HTTP-MTHD-LN         PIC S9(008) COMP-5 VALUE +0.
           05  WS-HTTP-VERS-LN         PIC S9(008) COMP-5 VALUE +0.
           05  WS-HTTP-PATH-LN         PIC S9(008) COMP-5 VALUE +0.
           05  WS-HTTP-QRY-STRN-LN     PIC S9(008) COMP-5 VALUE +0.
           05  WS-AUTH-CVDA            PIC S9(008) COMP-5 VALUE +0.
           05  WS-CLNT-NM-LN           PIC S9(008) COMP-5 VALUE +0.
           05  WS-CLNT-ADDR-LN         PIC S9(008) COMP-5 VALUE +0.
           05  WS-SRVR-NM-LN           PIC S9(008) COMP-5 VALUE +0.
           05  WS-SRVR-ADDR-LN         PIC S9(008) COMP-5 VALUE +0.
           05  WS-FORM-FLD-NM-LN       PIC S9(008) COMP-5 VALUE +0.
           05  WS-FORM-FLD-VAL-LN      PIC S9(008) COMP-5 VALUE +0.
           05  WS-SSL-TY-CVDA          PIC S9(008) COMP-5 VALUE +0.
           05  WTO-CA-LN               PIC S9(008) COMP-5 VALUE +0.
           05  FORM-RPLY-URL-PTR       PIC S9(008) COMP-5 VALUE +1.
           05  WS-HTTP-MTHD            PIC X(080) VALUE SPACES.
           05  WS-HTTP-VERS            PIC X(080) VALUE SPACES.
           05  WS-HTTP-PATH.
               10                      PIC X(050) OCCURS 10.
           05  WS-HTTP-QRY-STRN.
               10                      PIC X(050) OCCURS 10.
           05  WS-HTTP-QRY-STRN-PARMS  PIC X(050) OCCURS 10
               INDEXED QRY-STRN-INDX.
           05  WS-RPLY-TY              PIC X(008) VALUE SPACES.
           05  WS-FORM-FLD-NM          PIC X(016) VALUE SPACES.
           05  WS-MSNG-FORM-FLD-NM     PIC X(016) VALUE SPACES.
           05  WS-FORM-FLD-VAL         PIC X(004) VALUE SPACES.
           05  WS-RPLY-CNTE-HDR-FL     PIC X(001) VALUE SPACES.
           05  WS-CICS-RESP-MNEMONIC   PIC X(025) VALUE SPACES.
           05  WS-TRUNC-ITEM           PIC X(025) VALUE SPACES.
           05  WS-HTTP-HDR-TO-RTV      PIC X(050) VALUE SPACES.
           05  WS-CICS-APPLID          PIC X(008) VALUE SPACES.
           05  THIS-USERID             PIC X(008) VALUE SPACES.
           05  THIS-USERNAME           PIC X(020) VALUE SPACES.
           05  WS-DOC-TOKN             PIC X(016) VALUE SPACES.
           05  MSG-NB                  PIC X(004) VALUE SPACES.
               88  MSG-NB-CICS-ERR                VALUE '0004'.
           05  WTO-TXT.
               10                      PIC X(050) OCCURS 13.
               10                      PIC X(040).
           05  WTO-SUFX.
               10                      PIC X(050) OCCURS 13.
               10                      PIC X(040).
           05  CICS-API-FAILED         PIC X(030) VALUE SPACES.
           05  CICS-API-FAILED-LOC     PIC X(004) VALUE SPACES.
           05  CICS-RESP-DSPL          PIC 9(010) VALUE ZEROES.
           05  CICS-RESP-DSPL-X
               REDEFINES
               CICS-RESP-DSPL          PIC X(010).
           05  CICS-RESP2-DSPL         PIC 9(010) VALUE ZEROES.
           05  CICS-RESP2-DSPL-X
               REDEFINES
               CICS-RESP2-DSPL         PIC X(010).
           05  WS-HTTP-HDR-BUFR.
               10                      PIC X(050) OCCURS 5.
           05  WS-HTTP-CLNT-CHARSET    PIC X(040) VALUE SPACES.
           05  WS-DUMMY-BUFR.
               10                      PIC X(050) OCCURS 10.
           05  WS-CLNT-NM              PIC X(080) VALUE SPACES.
           05  WS-CLNT-ADDR            PIC X(015) VALUE SPACES.
           05  WS-SRVR-NM              PIC X(080) VALUE SPACES.
           05  WS-SRVR-ADDR            PIC X(015) VALUE SPACES.
           05  WS-TCPIP-SRVC-NM        PIC X(008) VALUE SPACES.
           05  WS-PORT-NB              PIC X(005) VALUE SPACES.
           05  WS-RPLY-STUS-CD         PIC X(050) VALUE SPACES.

       01  WTO-CA.
           COPY J7200521 REPLACING ==:PRFX:== BY ==WTO-CA-==.

      * These are used in contructing and sending the HTTP
      * protocol headers.
       01  HTTP-PTCL-HDR-AREAS.
           05  HTTP-PTCL-HDR-DT        PIC X(080) VALUE SPACES.
           05  HTTP-PTCL-HDR-CNTE-TY   PIC X(025) VALUE SPACES.
           05  HTTP-PTCL-HDR-NM        PIC X(080) VALUE SPACES.
           05  HTTP-PTCL-HDR-NM-LN     PIC S9(008) COMP-5 VALUE +0.
           05  HTTP-PTCL-HDR-VAL       PIC X(080) VALUE SPACES.
           05  HTTP-PTCL-HDR-VAL-LN    PIC S9(008) COMP-5 VALUE +0.

      * You'll see a lot of x'0D25' constants in some of the *-RPLY
      * structures that follow.  That value translates into the
      * familiar CRLF (Carriage Return, Line Feed) ASCII sequence.
       01  HTML-RPLY.
           05                    PIC X(006)  VALUE '<html>'.
           05                    PIC X(002)  VALUE X'0D25'.
           05                    PIC X(017)  VALUE '<h1 align=center>'.
           05                    PIC X(002)  VALUE X'0D25'.
           05  RPLY-STUS-CD      PIC X(050)  VALUE SPACES.
           05                    PIC X(002)  VALUE X'0D25'.
           05                    PIC X(005)  VALUE '</h1>'.
           05                    PIC X(017)  VALUE '<h2 align=center>'.
           05                    PIC X(002)  VALUE X'0D25'.
           05  RPLY-TRANID       PIC X(004)  VALUE SPACES.
           05                    PIC X(001)  VALUE '-'.
           05  RPLY-PGM          PIC X(008)  VALUE SPACES.
           05                    PIC X(002)  VALUE X'0D25'.
           05                    PIC X(005)  VALUE '</h2>'.
           05                    PIC X(002)  VALUE X'0D25'.
           05                    PIC X(004)  VALUE '<br>'.
           05                    PIC X(002)  VALUE X'0D25'.
           05                    PIC X(006)  VALUE '<body>'.
           05                    PIC X(002)  VALUE X'0D25'.
           05                    PIC X(004)  VALUE '<ul>'.
           05                    PIC X(002)  VALUE X'0D25'.
           05                    PIC X(004)  VALUE '<li>'.
           05                    PIC X(004)  VALUE '<b> '.
           05                    PIC X(008)  VALUE 'Region: '.
           05                    PIC X(004)  VALUE '</b>'.
           05  RPLY-APPL-ID      PIC X(008)  VALUE SPACES.
           05                    PIC X(005)  VALUE '</li>'.
           05                    PIC X(002)  VALUE X'0D25'.
           05                    PIC X(004)  VALUE '<li>'.
           05                    PIC X(004)  VALUE '<b> '.
           05                    PIC X(008)  VALUE 'Method: '.
           05                    PIC X(004)  VALUE '</b>'.
           05  RPLY-HTTP-MTHD    PIC X(080)  VALUE SPACES.
           05                    PIC X(005)  VALUE '</li>'.
           05                    PIC X(002)  VALUE X'0D25'.
           05                    PIC X(004)  VALUE '<li>'.
           05                    PIC X(004)  VALUE '<b> '.
           05                    PIC X(009)  VALUE 'Version: '.
           05                    PIC X(004)  VALUE '</b>'.
           05  RPLY-HTTP-VERS    PIC X(080)  VALUE SPACES.
           05                    PIC X(005)  VALUE '</li>'.
           05                    PIC X(002)  VALUE X'0D25'.
           05                    PIC X(004)  VALUE '<li>'.
           05                    PIC X(004)  VALUE '<b> '.
           05                    PIC X(006)  VALUE 'Path: '.
           05                    PIC X(004)  VALUE '</b>'.
           05  RPLY-HTTP-PATH    PIC X(512)  VALUE SPACES.
           05                    PIC X(005)  VALUE '</li>'.
           05                    PIC X(002)  VALUE X'0D25'.
           05                    PIC X(004)  VALUE '<li>'.
           05                    PIC X(004)  VALUE '<b> '.
           05                    PIC X(014)  VALUE 'Query String: '.
           05                    PIC X(004)  VALUE '</b>'.
           05  RPLY-HTTP-QRY-STRN PIC X(512)  VALUE SPACES.
           05                    PIC X(005)  VALUE '</li>'.
           05                    PIC X(002)  VALUE X'0D25'.
           05                    PIC X(004)  VALUE '<li>'.
           05                    PIC X(004)  VALUE '<b> '.
           05                    PIC X(016)  VALUE 'Authentication: '.
           05                    PIC X(004)  VALUE '</b>'.
           05  RPLY-AUTH-TY      PIC X(012)  VALUE SPACES.
           05                    PIC X(005)  VALUE '</li>'.
           05                    PIC X(002)  VALUE X'0D25'.
           05                    PIC X(004)  VALUE '<li>'.
           05                    PIC X(004)  VALUE '<b> '.
           05                    PIC X(010)  VALUE 'SSL Type: '.
           05                    PIC X(004)  VALUE '</b>'.
           05  RPLY-SSL-TY       PIC X(012)  VALUE SPACES.
           05                    PIC X(005)  VALUE '</li>'.
           05                    PIC X(002)  VALUE X'0D25'.
           05                    PIC X(004)  VALUE '<li>'.
           05                    PIC X(004)  VALUE '<b> '.
           05                    PIC X(016)  VALUE 'Client Address: '.
           05                    PIC X(004)  VALUE '</b>'.
           05  RPLY-CLNT-ADDR    PIC X(015)  VALUE SPACES.
           05                    PIC X(005)  VALUE '</li>'.
           05                    PIC X(002)  VALUE X'0D25'.
           05                    PIC X(004)  VALUE '<li>'.
           05                    PIC X(004)  VALUE '<b> '.
           05                    PIC X(013)  VALUE 'Client Name: '.
           05                    PIC X(004)  VALUE '</b>'.
           05  RPLY-CLNT-NM      PIC X(080)  VALUE SPACES.
           05                    PIC X(005)  VALUE '</li>'.
           05                    PIC X(002)  VALUE X'0D25'.
           05                    PIC X(004)  VALUE '<li>'.
           05                    PIC X(004)  VALUE '<b> '.
           05                    PIC X(014)  VALUE 'TCPIPSERVICE: '.
           05                    PIC X(004)  VALUE '</b>'.
           05  RPLY-TCPIPSERVICE PIC X(008)  VALUE SPACES.
           05                    PIC X(005)  VALUE '</li>'.
           05                    PIC X(002)  VALUE X'0D25'.
           05                    PIC X(004)  VALUE '<li>'.
           05                    PIC X(004)  VALUE '<b> '.
           05                    PIC X(016)  VALUE 'Server Address: '.
           05                    PIC X(004)  VALUE '</b>'.
           05  RPLY-SRVR-ADDR    PIC X(015)  VALUE SPACES.
           05                    PIC X(005)  VALUE '</li>'.
           05                    PIC X(002)  VALUE X'0D25'.
           05                    PIC X(004)  VALUE '<li>'.
           05                    PIC X(004)  VALUE '<b> '.
           05                    PIC X(013)  VALUE 'Server Name: '.
           05                    PIC X(004)  VALUE '</b>'.
           05  RPLY-SRVR-NM      PIC X(080)  VALUE SPACES.
           05                    PIC X(005)  VALUE '</li>'.
           05                    PIC X(002)  VALUE X'0D25'.
           05                    PIC X(004)  VALUE '<li>'.
           05                    PIC X(004)  VALUE '<b> '.
           05                    PIC X(013)  VALUE 'Server Port: '.
           05                    PIC X(004)  VALUE '</b>'.
           05  RPLY-SRVR-PORT    PIC X(005)  VALUE SPACES.
           05                    PIC X(005)  VALUE '</li>'.
           05                    PIC X(002)  VALUE X'0D25'.
           05                    PIC X(004)  VALUE '<li>'.
           05                    PIC X(004)  VALUE '<b> '.
           05                    PIC X(009)  VALUE 'User ID: '.
           05                    PIC X(004)  VALUE '</b>'.
           05  RPLY-USER-ID      PIC X(008)  VALUE SPACES.
           05                    PIC X(005)  VALUE '</li>'.
           05                    PIC X(002)  VALUE X'0D25'.
           05                    PIC X(004)  VALUE '<li>'.
           05                    PIC X(004)  VALUE '<b> '.
           05                    PIC X(011)  VALUE 'User Name: '.
           05                    PIC X(004)  VALUE '</b>'.
           05  RPLY-USER-NM      PIC X(020)  VALUE SPACES.
           05                    PIC X(005)  VALUE '</li>'.
           05                    PIC X(002)  VALUE X'0D25'.
           05                    PIC X(004)  VALUE '<li>'.
           05                    PIC X(004)  VALUE '<b> '.
           05                    PIC X(005)  VALUE 'GMT: '.
           05                    PIC X(004)  VALUE '</b>'.
           05  RPLY-GMT          PIC X(080)  VALUE SPACES.
           05                    PIC X(005)  VALUE '</li>'.
           05                    PIC X(002)  VALUE X'0D25'.
           05                    PIC X(005)  VALUE '</ul>'.
           05                    PIC X(002)  VALUE X'0D25'.
           05                    PIC X(004)  VALUE '<br>'.
           05                    PIC X(004)  VALUE '<br>'.
           05  RPLY-INVD-HINT    PIC X(690)  VALUE SPACES.
           05                    PIC X(004)  VALUE '<br>'.
           05                    PIC X(007)  VALUE '</body>'.
           05                    PIC X(002)  VALUE X'0D25'.
           05                    PIC X(007)  VALUE '</html>'.

       01  FORM-RPLY.
           05                    PIC X(006)  VALUE '<html>'.
           05                    PIC X(002)  VALUE X'0D25'.
           05                    PIC X(017)  VALUE '<h1 align=center>'.
           05                    PIC X(002)  VALUE X'0D25'.
           05  RPLY-STUS-CD      PIC X(050)  VALUE SPACES.
           05                    PIC X(002)  VALUE X'0D25'.
           05                    PIC X(005)  VALUE '</h1>'.
           05                    PIC X(017)  VALUE '<h2 align=center>'.
           05                    PIC X(002)  VALUE X'0D25'.
           05  RPLY-TRANID       PIC X(004)  VALUE SPACES.
           05                    PIC X(001)  VALUE '-'.
           05  RPLY-PGM          PIC X(008)  VALUE SPACES.
           05                    PIC X(002)  VALUE X'0D25'.
           05                    PIC X(005)  VALUE '</h2>'.
           05                    PIC X(002)  VALUE X'0D25'.
           05                    PIC X(004)  VALUE '<br>'.
           05                    PIC X(002)  VALUE X'0D25'.
           05                    PIC X(006)  VALUE '<body>'.
           05                    PIC X(006)  VALUE '<form '.
           05                    PIC X(007)  VALUE 'action='.
           05  FORM-RPLY-URL     PIC X(080)  VALUE SPACES.
           05                    PIC X(014)  VALUE 'method="POST" '.
           05                    PIC X(013)  VALUE 'name="form1">'.
           05                    PIC X(002)  VALUE X'0D25'.
           05                    PIC X(003)  VALUE '<b>'.
           05                    PIC X(012)  VALUE 'Reply Type: '.
           05                    PIC X(004)  VALUE '</b>'.
           05                    PIC X(002)  VALUE X'0D25'.
           05                    PIC X(007)  VALUE '<input '.
           05                    PIC X(012)  VALUE 'type="text" '.
           05                    PIC X(009)  VALUE 'size="4" '.
           05                    PIC X(012)  VALUE 'name="type" '.
           05                    PIC X(014)  VALUE 'maxlength="4">'.
           05                    PIC X(002)  VALUE X'0D25'.
           05                    PIC X(004)  VALUE '<br>'.
           05                    PIC X(002)  VALUE X'0D25'.
           05                    PIC X(003)  VALUE '<b>'.
           05                    PIC X(004)  VALUE '<br>'.
           05                    PIC X(002)  VALUE X'0D25'.
           05                    PIC X(003)  VALUE '<b>'.
           05                    PIC X(013)  VALUE 'Content-type '.
           05                    PIC X(008)  VALUE 'Header: '.
           05                    PIC X(004)  VALUE '</b>'.
           05                    PIC X(002)  VALUE X'0D25'.
           05                    PIC X(007)  VALUE '<input '.
           05                    PIC X(016)  VALUE 'type="checkbox" '.
           05                    PIC X(016)  VALUE 'name="cntehdr">'.
           05                    PIC X(002)  VALUE X'0D25'.
           05                    PIC X(004)  VALUE '<br>'.
           05                    PIC X(002)  VALUE X'0D25'.
           05                    PIC X(007)  VALUE '<input '.
           05                    PIC X(014)  VALUE 'type="submit" '.
           05                    PIC X(016)  VALUE 'value="Request" '.
           05                    PIC X(015)  VALUE 'name="Button1" '.
           05                    PIC X(015)  VALUE 'class="button">'.
           05                    PIC X(002)  VALUE X'0D25'.
           05                    PIC X(007)  VALUE '</form>'.
           05                    PIC X(002)  VALUE X'0D25'.
           05                    PIC X(007)  VALUE '</body>'.
           05                    PIC X(002)  VALUE X'0D25'.
           05                    PIC X(007)  VALUE '</html>'.

       01  INVD-RPLY.
           05                    PIC X(006)  VALUE '<html>'.
           05                    PIC X(002)  VALUE X'0D25'.
           05                    PIC X(017)  VALUE '<h1 align=center>'.
           05                    PIC X(002)  VALUE X'0D25'.
           05  RPLY-STUS-CD      PIC X(050)  VALUE SPACES.
           05                    PIC X(002)  VALUE X'0D25'.
           05                    PIC X(005)  VALUE '</h1>'.
           05                    PIC X(017)  VALUE '<h2 align=center>'.
           05                    PIC X(002)  VALUE X'0D25'.
           05  RPLY-TRANID       PIC X(004)  VALUE SPACES.
           05                    PIC X(001)  VALUE '-'.
           05  RPLY-PGM          PIC X(008)  VALUE SPACES.
           05                    PIC X(002)  VALUE X'0D25'.
           05                    PIC X(005)  VALUE '</h2>'.
           05                    PIC X(002)  VALUE X'0D25'.
           05                    PIC X(004)  VALUE '<br>'.
           05                    PIC X(002)  VALUE X'0D25'.
           05                    PIC X(006)  VALUE '<body>'.
           05                    PIC X(002)  VALUE X'0D25'.
           05                    PIC X(014)  VALUE 'Query String: '.
           05  RPLY-HTTP-QRY-STRN PIC X(512)  VALUE SPACES.
           05                    PIC X(004)  VALUE '<br>'.
           05                    PIC X(042)
               VALUE 'Must contain a t=&lt reply type &gt where '.
           05                    PIC X(034)
               VALUE '&lt reply type &gt is html or xml '.
           05                    PIC X(016)
               VALUE 'or form or imag '.
           05                    PIC X(002)  VALUE X'0D25'.
           05                    PIC X(004)  VALUE '<br>'.
           05  RPLY-INVD-HINT    PIC X(690)  VALUE SPACES.
           05                    PIC X(004)  VALUE '<br>'.
           05                    PIC X(005)  VALUE 'GMT: '.
           05  RPLY-GMT          PIC X(080)  VALUE SPACES.
           05                    PIC X(007)  VALUE '</body>'.
           05                    PIC X(002)  VALUE X'0D25'.
           05                    PIC X(007)  VALUE '</html>'.

       01  XML-RPLY.
           05  XML-OCCURS        PIC 9(004)  VALUE 100.
           05  XML-RPLY-TBL
               OCCURS 1 TO 2000
               DEPENDING XML-OCCURS
               INDEXED XML-RPLY-INDX.
               10  RPLY-STUS-CD      PIC X(050)  VALUE SPACES.
               10  RPLY-TRANID       PIC X(004)  VALUE SPACES.
               10  RPLY-PGM          PIC X(008)  VALUE SPACES.
               10  RPLY-APPL-ID      PIC X(008)  VALUE SPACES.
               10  RPLY-HTTP-MTHD    PIC X(080)  VALUE SPACES.
               10  RPLY-HTTP-VERS    PIC X(080)  VALUE SPACES.
               10  RPLY-HTTP-PATH    PIC X(512)  VALUE SPACES.
               10  RPLY-HTTP-QRY-STRN PIC X(512)  VALUE SPACES.
               10  RPLY-AUTH-TY      PIC X(012)  VALUE SPACES.
               10  RPLY-SSL-TY       PIC X(012)  VALUE SPACES.
               10  RPLY-CLNT-ADDR    PIC X(015)  VALUE SPACES.
               10  RPLY-CLNT-NM      PIC X(080)  VALUE SPACES.
               10  RPLY-TCPIPSERVICE PIC X(008)  VALUE SPACES.
               10  RPLY-SRVR-ADDR    PIC X(015)  VALUE SPACES.
               10  RPLY-SRVR-NM      PIC X(080)  VALUE SPACES.
               10  RPLY-SRVR-PORT    PIC X(005)  VALUE SPACES.
               10  RPLY-USER-ID      PIC X(008)  VALUE SPACES.
               10  RPLY-USER-NM      PIC X(020)  VALUE SPACES.
               10  RPLY-GMT          PIC X(080)  VALUE SPACES.

      * This is the hex representation of the SHARE logo.  Its primary
      * purpose is to show that a JPEG graphic can be sent from a
      * CICS COBOL program.  It's just showing off, really.
       01  IMAG-RPLY.
           COPY IMAG.

      * This area is used in creating a core dump.  It is sometimes
      * useful to have such a thing when an error occurs, even if
      * an abend is inappropriate.
       01  LCL-APLC-DEBUG-AREA.
           05  CEE3DMP-TITL.
               10                       PIC X(010) VALUE '+=+=+=+=+='.
               10  CEE3DMP-TITL-SPFC    PIC X(060) VALUE SPACES.
               10                       PIC X(010) VALUE '=+=+=+=+=+'.
           05  CEE3DMP-OPTIONS.
               10  PIC X(020) VALUE 'TRACEBACK'.
               10  PIC X(020) VALUE 'THREAD(CURRENT)'.
               10  PIC X(020) VALUE 'VARIABLES'.
               10  PIC X(020) VALUE 'PAGESIZE(60)'.
               10  PIC X(020) VALUE 'NOCONDITION'.
               10  PIC X(020) VALUE 'ENCLAVE(CURRENT)'.
               10  CEE3DMP-OPTIONS-PAD-TO-255 PIC X(135).
           05  CEE3DMP-LEFB-CD          PIC X(012).

       Linkage Section.
       01  DFHCOMMAREA PIC X(001).

       Procedure Division.
#debug     COPY '$DEBUG03'.

#debug     DISPLAY
#debug         MYNAME
#debug         ' Begin'

           PERFORM 0100-INIT

      *    Presume everything will go okay - move in a
      *    "success" status message
           MOVE STUS-0001 TO WS-RPLY-STUS-CD

      *    Obtain information about our current environment,
      *    this is going to be sent back as a demonstration
      *    of these API calls.  They may also be of use,
      *    particularly the HTTP-QRY-STRN, in providing a key
      *    such as a DID# for an inquiry.
           PERFORM 8000-WHO-IS-CALLING
           PERFORM 8010-GET-HTTP-MTHD
           PERFORM 8020-GET-HTTP-VERS
           PERFORM 8030-GET-HTTP-PATH

      *    We need this for translating from our code page to the
      *    client's code page.  Think EBCDIC to ASCII but more
      *    complicated.
           PERFORM 1040-GET-CLNT-CD-PG

           IF WS-HTTP-MTHD(1:WS-HTTP-MTHD-LN) = 'POST'
      *        We were invoked via an HTTP POST.  For purposes of this
      *        application, that implies that an HTML form was sent.
      *        We will now retrieve the fields from the form and
      *        proceed accordingly.
      *        The http method was retrieved in 8010-GET-HTTP-MTHD.
               PERFORM 1050-GET-FORM-FLDS
           ELSE
      *        Presume we were invoked with an HTTP GET, and that the
      *        query string contains information instructing us on how
      *        to proceed.
      *        A complete list of HTTP methods can be found in section
      *        9 "Method Definitions" of RFC 2616 "Hypertext Transfer
      *        Protocol -- HTTP/1.1"
               PERFORM 8040-GET-HTTP-QRY-STRN
               PERFORM 1030-PARSE-QRY-STRN
           END-IF

      *    An application could also be coded to WEB RECEIVE an
      *    XML datastream instead of WEB READing FORMFIELDs or
      *    processing the query string.

      *    Create the document which will contain the reply text.  The
      *    document is just a place for CICS to store our reply.
           PERFORM 8050-DOC-CRTE

      *    We need GMT time for the http protocol header and
      *    some of our reply parameters.
           PERFORM 1010-GET-GMT-TM

      *    Build the reply to send back to the requestor, note that
      *    the Content-type protocol header is also set in the
      *    paragraphs that create the different content types.
           PERFORM 1000-BLD-RPLY

      *    Send the http protocol header to the requestor.
           PERFORM 1020-SEND-HTTP-PTCL-HDR

      *    Finally, send the reply.
           PERFORM 8090-WEB-SEND

#debug     DISPLAY
#debug         MYNAME
#debug         ' End'

           EXEC CICS RETURN END-EXEC
           .

       0100-INIT.
      *
      * I got some strange behavior when I tried using a Local-Storage
      * section to force reinitialization of the VALUE clauses.  I
      * suspect this has something to do with how the CICS Web
      * Interface invokes application programs.  After some puzzling
      * over storage dumps, I decided the clearest solution was to
      * do "old school" initialization logic by hand.
      *
           INITIALIZE
               WORK-AREAS
               HTTP-PTCL-HDR-AREAS
               HTML-RPLY
               FORM-RPLY
               INVD-RPLY
               SWITCHES
               WS-RPLY-BUFR
               LCL-APLC-DEBUG-AREA

           MOVE 1 TO WTO-LN
                     WTO-SUFX-LN

           MOVE +1 TO FORM-RPLY-URL-PTR
           .

       1000-BLD-RPLY.
      *
      * In a real application, you'd be calling subroutines that
      * implement business logic and probably building an XML data
      * stream in reply.
      *

           EVALUATE WS-RPLY-TY
               WHEN 'HTML    '
                    PERFORM 2010-BLD-HTML-RPLY
               WHEN 'XML     '
                    PERFORM 2020-BLD-XML-RPLY
               WHEN 'FORM    '
                    PERFORM 2060-BLD-FORM-RPLY
               WHEN 'IMAG    '
                    PERFORM 2070-BLD-IMAG-RPLY
               WHEN OTHER
                    PERFORM 2030-BLD-INVD-RPLY
           END-EVALUATE
           .

       1010-GET-GMT-TM.
      *
      * Obtain current GMT date/time for http protocol header.
      *
           CALL HTTP-DT-TM-PGM USING
               GMT-TM-FL
               DT-PIC-STRN
               HTTP-PTCL-HDR-DT
           END-CALL
           .

       1020-SEND-HTTP-PTCL-HDR.
      *
      * The RFCs indicate that one should include an http protocol
      * header in order to be a good internet citizen.  It's not that
      * hard, so here it is.
      *
      * A definitive list of HTTP headers can be found in section 14
      * "Header Field Definitions" of RFC 2616 "Hypertext Transfer
      * Protocol -- HTTP/1.1"
      *
      * CICS does not allow direct modification of the HTTP status
      * code in the protocol header, and only allows certain HTTP
      * status codes to be set.  This is per the "CICS Internet
      * Guide."  So, rather than using the HTTP status code, I have
      * invented, purely for demonstration purposes, an
      * application status code.  This is what you see in the
      * RPLY-STUS-CD field.
      *

      *    Server
           MOVE HTTP-PTCL-HDR-SRVR-LIT TO HTTP-PTCL-HDR-NM
           MOVE LENGTH OF HTTP-PTCL-HDR-SRVR-LIT
             TO HTTP-PTCL-HDR-NM-LN
           MOVE HTTP-PTCL-HDR-SRVR TO HTTP-PTCL-HDR-VAL
           MOVE LENGTH OF HTTP-PTCL-HDR-SRVR
             TO HTTP-PTCL-HDR-VAL-LN
           PERFORM 8100-WEB-WRITE

      *    Date
           MOVE HTTP-PTCL-HDR-DT-LIT TO HTTP-PTCL-HDR-NM
           MOVE LENGTH OF HTTP-PTCL-HDR-DT-LIT
             TO HTTP-PTCL-HDR-NM-LN
           MOVE HTTP-PTCL-HDR-DT TO HTTP-PTCL-HDR-VAL
           MOVE LENGTH OF HTTP-PTCL-HDR-DT
             TO HTTP-PTCL-HDR-VAL-LN
           PERFORM 8100-WEB-WRITE

           IF WS-RPLY-CNTE-HDR-FL = 'N'
      *        Request from client to NOT send the Content-type
      *        http protocol header.  This is to demonstrate
      *        what happens if this header is absent.
      *        In a real application you should just send this header.
               CONTINUE
           ELSE
      *        Content-Type
               MOVE HTTP-PTCL-HDR-CNTE-TY-LIT TO HTTP-PTCL-HDR-NM
               MOVE LENGTH OF HTTP-PTCL-HDR-CNTE-TY-LIT
                 TO HTTP-PTCL-HDR-NM-LN
               MOVE HTTP-PTCL-HDR-CNTE-TY TO HTTP-PTCL-HDR-VAL
               MOVE LENGTH OF HTTP-PTCL-HDR-CNTE-TY
                 TO HTTP-PTCL-HDR-VAL-LN
               PERFORM 8100-WEB-WRITE
           END-IF

      *    This header isn't defined for HTTP 1.0, but it works under
      *    CICS TS 2.2 and doesn't seem to cause any harm.
      *    Cache-Control
           MOVE HTTP-PTCL-HDR-CACHE-CNTL-LIT TO HTTP-PTCL-HDR-NM
           MOVE LENGTH OF HTTP-PTCL-HDR-CACHE-CNTL-LIT
             TO HTTP-PTCL-HDR-NM-LN
           MOVE HTTP-PTCL-HDR-CACHE-CNTL TO HTTP-PTCL-HDR-VAL
           MOVE LENGTH OF HTTP-PTCL-HDR-CACHE-CNTL
             TO HTTP-PTCL-HDR-VAL-LN
           PERFORM 8100-WEB-WRITE
           .

       1030-PARSE-QRY-STRN.
      *
      * The query string is everything that comes after the ? in the
      * URL.  This is a common way to pass parameters to server
      * applications.  Multiple parameters are typically presented in
      * "keyword=value" format, separated by the ampersand ('&')
      * character.
      *
           UNSTRING
               WS-HTTP-QRY-STRN
                 DELIMITED '&'
             INTO
      *        We don't accept this many parms, but coding it this
      *        way allows illustration of multiple parm processing.
               WS-HTTP-QRY-STRN-PARMS(1)
               WS-HTTP-QRY-STRN-PARMS(2)
               WS-HTTP-QRY-STRN-PARMS(3)
               WS-HTTP-QRY-STRN-PARMS(4)
               WS-HTTP-QRY-STRN-PARMS(5)
               WS-HTTP-QRY-STRN-PARMS(6)
               WS-HTTP-QRY-STRN-PARMS(7)
               WS-HTTP-QRY-STRN-PARMS(8)
               WS-HTTP-QRY-STRN-PARMS(9)
               WS-HTTP-QRY-STRN-PARMS(10)
           END-UNSTRING

      *    Convert to upper case for convenience of processing.
           PERFORM VARYING QRY-STRN-INDX FROM 1 BY 1
           UNTIL QRY-STRN-INDX > 10
               INSPECT WS-HTTP-QRY-STRN-PARMS(QRY-STRN-INDX)
                 REPLACING ALL LOW-VALUE BY SPACE
               MOVE FUNCTION UPPER-CASE(WS-HTTP-QRY-STRN-PARMS
                                        (QRY-STRN-INDX) )
                 TO WS-HTTP-QRY-STRN-PARMS(QRY-STRN-INDX)
           END-PERFORM

           PERFORM VARYING QRY-STRN-INDX FROM 1 BY 1
           UNTIL QRY-STRN-INDX > 10
           OR WS-HTTP-QRY-STRN-PARMS(QRY-STRN-INDX) = SPACES
               EVALUATE WS-HTTP-QRY-STRN-PARMS(QRY-STRN-INDX)(1:2)
                   WHEN 'C='
                        MOVE WS-HTTP-QRY-STRN-PARMS(QRY-STRN-INDX)(3:1)
                          TO WS-RPLY-CNTE-HDR-FL
                   WHEN 'T='
                        MOVE WS-HTTP-QRY-STRN-PARMS(QRY-STRN-INDX)(3:8)
                          TO WS-RPLY-TY
                   WHEN OTHER
      *                 Unrecognized parameter, force return of the
      *                 "invalid input" html page.
                        MOVE 'INVD    '  TO WS-RPLY-TY
                        STRING
                            'Invalid query string parm = '
                              DELIMITED SIZE
                            WS-HTTP-QRY-STRN-PARMS(QRY-STRN-INDX)(1:2)
                              DELIMITED SIZE
                          INTO
                            RPLY-INVD-HINT OF INVD-RPLY
                        END-STRING
               END-EVALUATE
           END-PERFORM
           .

       1040-GET-CLNT-CD-PG.
      *
      *    We must specify the client's code page when we send
      *    a response.  The code page is present in the http
      *    protocol header, so we will retrieve it from there.
      *
           INITIALIZE
               WS-HTTP-HDR-BUFR
               WS-HTTP-HDR-TO-RTV
           MOVE HTTP-CHARSET-HDR TO WS-HTTP-HDR-TO-RTV
           MOVE LENGTH OF HTTP-CHARSET-HDR TO WS-HTTP-HDR-TO-RTV-LN
           MOVE LENGTH OF WS-HTTP-HDR-BUFR TO WS-HTTP-HDR-BUFR-LN
           PERFORM 8070-WEB-READ-HDR
           IF HTTP-HDR-NOT-FND
      *        Since there doesn't appear to be a client code page
      *        in the protocol header, we'll use an innocuous default.
      *        This default comes from section 3.7.1 "Canonicalization
      *        and Text Defaults" of RFC 2616 "Hypertext Transfer
      *        Protocol -- HTTP/1.1"
               INITIALIZE WS-HTTP-CLNT-CHARSET
               MOVE 'ISO-8859-1' TO WS-HTTP-CLNT-CHARSET
           ELSE
               PERFORM 8080-UNSTRN-CHARSET
           END-IF
           .

       1050-GET-FORM-FLDS.
      *
      * Retrieve the form fields present in the FORM-RPLY HTML stream.
      *
      * This demonstrates an alternate way to provide input from a
      * browser to a server program.
      *
           MOVE FORM-FLD-NM-TY-LIT TO WS-FORM-FLD-NM
           MOVE LENGTH OF FORM-FLD-NM-TY-LIT TO WS-FORM-FLD-NM-LN
           PERFORM 8110-WEB-READ-FORMFIELD
           IF FORM-FLD-NOT-FND
      *        Force a return of the INVD-RPLY page.
               MOVE 'INVD' TO WS-RPLY-TY
           ELSE
               MOVE WS-FORM-FLD-VAL(1:WS-FORM-FLD-VAL-LN)
                 TO WS-RPLY-TY
           END-IF

           MOVE FORM-FLD-NM-CNTE-LIT TO WS-FORM-FLD-NM
           MOVE LENGTH OF FORM-FLD-NM-CNTE-LIT TO WS-FORM-FLD-NM-LN
           PERFORM 8110-WEB-READ-FORMFIELD
           IF FORM-FLD-NOT-FND
               CONTINUE
           ELSE
               IF WS-FORM-FLD-VAL(1:WS-FORM-FLD-VAL-LN) = 'ON'
                   MOVE 'Y' TO WS-RPLY-CNTE-HDR-FL
               END-IF
           END-IF
           .

       2010-BLD-HTML-RPLY.
      *
      * This is just building a reply that shows we got here and
      * what information we can get via standard APIs.
      *
           MOVE EIBTRNID         TO RPLY-TRANID
                                 OF HTML-RPLY
           MOVE MYNAME           TO RPLY-PGM
                                 OF HTML-RPLY
           MOVE WS-CICS-APPLID   TO RPLY-APPL-ID
                                 OF HTML-RPLY
      *    Reference modification is used for MTHD, VERS, PATH and
      *    QRY-STRN because they are padded with x'00' by the API
      *    that retrieved them.
           MOVE WS-HTTP-MTHD(1:WS-HTTP-MTHD-LN)
                                 TO RPLY-HTTP-MTHD
                                 OF HTML-RPLY
           MOVE WS-HTTP-VERS(1:WS-HTTP-VERS-LN)
                                 TO RPLY-HTTP-VERS
                                 OF HTML-RPLY
           MOVE WS-HTTP-PATH(1:WS-HTTP-PATH-LN)
                                 TO RPLY-HTTP-PATH
                                 OF HTML-RPLY
           MOVE WS-HTTP-QRY-STRN(1:WS-HTTP-QRY-STRN-LN)
                                 TO RPLY-HTTP-QRY-STRN
                                 OF HTML-RPLY
           MOVE WS-CLNT-NM       TO RPLY-CLNT-NM
                                 OF HTML-RPLY
           MOVE WS-CLNT-ADDR     TO RPLY-CLNT-ADDR
                                 OF HTML-RPLY
           MOVE WS-SRVR-NM       TO RPLY-SRVR-NM
                                 OF HTML-RPLY
           MOVE WS-SRVR-ADDR     TO RPLY-SRVR-ADDR
                                 OF HTML-RPLY
           MOVE WS-PORT-NB       TO RPLY-SRVR-PORT
                                 OF HTML-RPLY
           MOVE WS-TCPIP-SRVC-NM TO RPLY-TCPIPSERVICE
                                 OF HTML-RPLY
           MOVE THIS-USERID      TO RPLY-USER-ID
                                 OF HTML-RPLY
           MOVE THIS-USERNAME    TO RPLY-USER-NM
                                 OF HTML-RPLY
           MOVE HTTP-PTCL-HDR-DT TO RPLY-GMT
                                 OF HTML-RPLY

           EVALUATE WS-AUTH-CVDA
      *        These may deserve to be factored out into their own
      *        subroutine, ala J7200501.
               WHEN DFHVALUE(AUTOAUTH)
                    MOVE 'AUTOAUTH    ' TO RPLY-AUTH-TY
                                        OF HTML-RPLY
               WHEN DFHVALUE(AUTOREGISTER)
                    MOVE 'AUTOREGISTER' TO RPLY-AUTH-TY
                                        OF HTML-RPLY
               WHEN DFHVALUE(BASICAUTH)
                    MOVE 'BASICAUTH   ' TO RPLY-AUTH-TY
                                        OF HTML-RPLY
               WHEN DFHVALUE(CERTIFICAUTH)
                    MOVE 'CERTIFICAUTH' TO RPLY-AUTH-TY
                                        OF HTML-RPLY
               WHEN DFHVALUE(NOAUTHENTIC)
                    MOVE 'NOAUTHENTIC ' TO RPLY-AUTH-TY
                                        OF HTML-RPLY
               WHEN OTHER
                    MOVE 'UNKNOWN     ' TO RPLY-AUTH-TY
                                        OF HTML-RPLY
           END-EVALUATE
           EVALUATE WS-SSL-TY-CVDA
      *        These may deserve to be factored out into their own
      *        subroutine, ala J7200501.
               WHEN DFHVALUE(SSL)
                    MOVE 'SSL         ' TO RPLY-SSL-TY
                                        OF HTML-RPLY
               WHEN DFHVALUE(NOSSL)
                    MOVE 'NOSSL       ' TO RPLY-SSL-TY
                                        OF HTML-RPLY
               WHEN DFHVALUE(CLIENTAUTH)
                    MOVE 'CLIENTAUTH  ' TO RPLY-SSL-TY
                                        OF HTML-RPLY
               WHEN OTHER
                    MOVE 'UNKNOWN     ' TO RPLY-SSL-TY
                                        OF HTML-RPLY
           END-EVALUATE

           IF CICS-ERR
               IF WS-RPLY-STUS-CD = STUS-0001
                   MOVE STUS-0003 TO WS-RPLY-STUS-CD
               END-IF
               MOVE WTO-TXT(1:WTO-LN)
                 TO RPLY-INVD-HINT OF HTML-RPLY
           END-IF

           MOVE WS-RPLY-STUS-CD         TO RPLY-STUS-CD
                                        OF HTML-RPLY

           MOVE LENGTH OF HTML-RPLY TO WS-RPLY-BUFR-LN
           MOVE HTML-RPLY TO WS-RPLY-BUFR
      *    Insert the reply text into the reply document
           PERFORM 8060-DOC-ISRT

           MOVE HTTP-CNTE-TY-HTML   TO HTTP-PTCL-HDR-CNTE-TY
           .

       2020-BLD-XML-RPLY.
      *
      * This is just building a reply that shows we got here and
      * what information we can get via standard APIs.
      *
      * This is contrived to end up being larger than 32K.
      *
           PERFORM VARYING XML-RPLY-INDX FROM 1 BY 1
           UNTIL XML-RPLY-INDX > XML-OCCURS
               INITIALIZE XML-RPLY-TBL(XML-RPLY-INDX)
               MOVE EIBTRNID         TO RPLY-TRANID
                                     OF XML-RPLY(XML-RPLY-INDX)
               MOVE MYNAME           TO RPLY-PGM
                                     OF XML-RPLY(XML-RPLY-INDX)
               MOVE WS-CICS-APPLID   TO RPLY-APPL-ID
                                     OF XML-RPLY(XML-RPLY-INDX)
      *    Reference modification is used for MTHD, VERS, PATH and
      *    QRY-STRN because they are padded with x'00' by the API
      *    that retrieved them.  That makes for messy XML.
               MOVE WS-HTTP-MTHD(1:WS-HTTP-MTHD-LN)
                                     TO RPLY-HTTP-MTHD
                                     OF XML-RPLY(XML-RPLY-INDX)
               MOVE WS-HTTP-VERS(1:WS-HTTP-VERS-LN)
                                     TO RPLY-HTTP-VERS
                                     OF XML-RPLY(XML-RPLY-INDX)
               MOVE WS-HTTP-PATH(1:WS-HTTP-PATH-LN)
                                     TO RPLY-HTTP-PATH
                                     OF XML-RPLY(XML-RPLY-INDX)
               MOVE WS-HTTP-QRY-STRN(1:WS-HTTP-QRY-STRN-LN)
                                     TO RPLY-HTTP-QRY-STRN
                                     OF XML-RPLY(XML-RPLY-INDX)
               MOVE WS-CLNT-NM       TO RPLY-CLNT-NM
                                     OF XML-RPLY(XML-RPLY-INDX)
               MOVE WS-CLNT-ADDR     TO RPLY-CLNT-ADDR
                                     OF XML-RPLY(XML-RPLY-INDX)
               MOVE WS-SRVR-NM       TO RPLY-SRVR-NM
                                     OF XML-RPLY(XML-RPLY-INDX)
               MOVE WS-SRVR-ADDR     TO RPLY-SRVR-ADDR
                                     OF XML-RPLY(XML-RPLY-INDX)
               MOVE WS-PORT-NB       TO RPLY-SRVR-PORT
                                     OF XML-RPLY(XML-RPLY-INDX)
               MOVE WS-TCPIP-SRVC-NM TO RPLY-TCPIPSERVICE
                                     OF XML-RPLY(XML-RPLY-INDX)
               MOVE THIS-USERID      TO RPLY-USER-ID
                                     OF XML-RPLY(XML-RPLY-INDX)
               MOVE THIS-USERNAME    TO RPLY-USER-NM
                                     OF XML-RPLY(XML-RPLY-INDX)
               MOVE HTTP-PTCL-HDR-DT TO RPLY-GMT
                                     OF XML-RPLY(XML-RPLY-INDX)
               EVALUATE WS-AUTH-CVDA
      *            These may deserve to be factored out into their own
      *            subroutine, ala J7200501.
                   WHEN DFHVALUE(AUTOAUTH)
                        MOVE 'AUTOAUTH    ' TO RPLY-AUTH-TY
                                            OF XML-RPLY(XML-RPLY-INDX)
                   WHEN DFHVALUE(AUTOREGISTER)
                        MOVE 'AUTOREGISTER' TO RPLY-AUTH-TY
                                            OF XML-RPLY(XML-RPLY-INDX)
                   WHEN DFHVALUE(BASICAUTH)
                        MOVE 'BASICAUTH   ' TO RPLY-AUTH-TY
                                            OF XML-RPLY(XML-RPLY-INDX)
                   WHEN DFHVALUE(CERTIFICAUTH)
                        MOVE 'CERTIFICAUTH' TO RPLY-AUTH-TY
                                            OF XML-RPLY(XML-RPLY-INDX)
                   WHEN DFHVALUE(NOAUTHENTIC)
                        MOVE 'NOAUTHENTIC ' TO RPLY-AUTH-TY
                                            OF XML-RPLY(XML-RPLY-INDX)
                   WHEN OTHER
                        MOVE 'UNKNOWN     ' TO RPLY-AUTH-TY
                                            OF XML-RPLY(XML-RPLY-INDX)
               END-EVALUATE
               EVALUATE WS-SSL-TY-CVDA
      *            These may deserve to be factored out into their own
      *            subroutine, ala J7200501.
                   WHEN DFHVALUE(SSL)
                        MOVE 'SSL         ' TO RPLY-SSL-TY
                                            OF XML-RPLY(XML-RPLY-INDX)
                   WHEN DFHVALUE(NOSSL)
                        MOVE 'NOSSL       ' TO RPLY-SSL-TY
                                            OF XML-RPLY(XML-RPLY-INDX)
                   WHEN DFHVALUE(CLIENTAUTH)
                        MOVE 'CLIENTAUTH  ' TO RPLY-SSL-TY
                                            OF XML-RPLY(XML-RPLY-INDX)
                   WHEN OTHER
                        MOVE 'UNKNOWN     ' TO RPLY-SSL-TY
                                            OF XML-RPLY(XML-RPLY-INDX)
               END-EVALUATE
               MOVE WS-RPLY-STUS-CD         TO RPLY-STUS-CD
                                            OF XML-RPLY(XML-RPLY-INDX)
           END-PERFORM

      *    I tested sending a 1 megabyte XML data stream.  This program
      *    completed processing in short order (< 1 second, which is
      *    the granularity of the clock I was using.  MS Internet
      *    Explorer took 10+ seconds to display the results.  I don't
      *    know how much of that was network traversal time and how
      *    much of that was MSIE rendering the XML.
           XML GENERATE WS-RPLY-BUFR
               FROM XML-RPLY
               COUNT IN WS-RPLY-BUFR-LN
           END-XML

      *    Insert the reply text into the reply document
           PERFORM 8060-DOC-ISRT

           MOVE HTTP-CNTE-TY-XML    TO HTTP-PTCL-HDR-CNTE-TY
           .

       2030-BLD-INVD-RPLY.
      *
      * This is just building an HTML reply indicating something
      * is wrong.
      *
           MOVE EIBTRNID         TO RPLY-TRANID
                                 OF INVD-RPLY
           MOVE MYNAME           TO RPLY-PGM
                                 OF INVD-RPLY
           MOVE WS-HTTP-QRY-STRN TO RPLY-HTTP-QRY-STRN
                                 OF INVD-RPLY
           MOVE HTTP-PTCL-HDR-DT TO RPLY-GMT
                                 OF INVD-RPLY

           EVALUATE TRUE
               WHEN CICS-ERR
                    MOVE WTO-TXT(1:WTO-LN)
                      TO RPLY-INVD-HINT OF INVD-RPLY
               WHEN FORM-FLD-NOT-FND
                    STRING
                        'You must send a form with a field named "'
                          DELIMITED SIZE
                        WS-FORM-FLD-NM(1:WS-FORM-FLD-NM-LN)
                          DELIMITED SIZE
                        '" if invoking '
                          DELIMITED SIZE
                        MYNAME
                          DELIMITED SIZE
                        ' with an HTTP POST.'
                          DELIMITED SIZE
                      INTO
                        RPLY-INVD-HINT OF INVD-RPLY
                    END-STRING
           END-EVALUATE

           IF WS-RPLY-STUS-CD = STUS-0001
               MOVE STUS-0003 TO WS-RPLY-STUS-CD
           END-IF

           MOVE WS-RPLY-STUS-CD
             TO RPLY-STUS-CD
             OF INVD-RPLY

      *    Insert the reply text into the reply document
           MOVE LENGTH OF INVD-RPLY TO WS-RPLY-BUFR-LN
           MOVE INVD-RPLY TO WS-RPLY-BUFR
           PERFORM 8060-DOC-ISRT

           MOVE HTTP-CNTE-TY-HTML   TO HTTP-PTCL-HDR-CNTE-TY
           .

       2060-BLD-FORM-RPLY.
      *
      * Populate the fields on the HTML page containing the form to
      * be sent back to the browser, add it to the document.
      *
      * In true pseudo-conversational style, we expect the form to
      * be populated by the user sitting in front of the browser
      * and then sent back to us.  That processing is handled in
      * the mainline code, where we check to see if we were invoked
      * via an HTTP POST.
      *
           MOVE EIBTRNID         TO RPLY-TRANID
                                 OF FORM-RPLY
           MOVE MYNAME           TO RPLY-PGM
                                 OF FORM-RPLY

      *
      *    The reply-to URL for the HTML form is constructed here.  The
      *    pieces of the original URL are used, so if the URL used to
      *    invoke this program changes, this code need not change.
      *
           STRING PTCL-LIT DELIMITED SIZE
             INTO FORM-RPLY-URL
             POINTER FORM-RPLY-URL-PTR
           END-STRING

           IF WS-SSL-TY-CVDA = DFHVALUE(SSL)
               STRING 's' DELIMITED SIZE
                 INTO FORM-RPLY-URL POINTER FORM-RPLY-URL-PTR
               END-STRING
           END-IF

           STRING
               '://'        DELIMITED SIZE
               WS-SRVR-NM(1:WS-SRVR-NM-LN) DELIMITED SIZE
               ':'          DELIMITED SIZE
               WS-PORT-NB   DELIMITED SIZE
               WS-HTTP-PATH(1:WS-HTTP-PATH-LN) DELIMITED SIZE
               '?'          DELIMITED SIZE
               WS-HTTP-QRY-STRN(1:WS-HTTP-QRY-STRN-LN) DELIMITED SIZE
             INTO FORM-RPLY-URL
             POINTER FORM-RPLY-URL-PTR
             OVERFLOW
               PERFORM 2061-FORM-URL-OVRF-ERR
             NOT OVERFLOW
               PERFORM 2062-FORM-OK
           END-STRING

      *    Insert the reply text into the reply document
           PERFORM 8060-DOC-ISRT

           MOVE HTTP-CNTE-TY-HTML   TO HTTP-PTCL-HDR-CNTE-TY
           .

       2061-FORM-URL-OVRF-ERR.
           MOVE 'Query string is too long' TO RPLY-INVD-HINT
                                           OF INVD-RPLY

           MOVE EIBTRNID         TO RPLY-TRANID
                                 OF INVD-RPLY
           MOVE MYNAME           TO RPLY-PGM
                                 OF INVD-RPLY
           MOVE WS-HTTP-QRY-STRN TO RPLY-HTTP-QRY-STRN
                                 OF INVD-RPLY
           MOVE HTTP-PTCL-HDR-DT TO RPLY-GMT
                                 OF INVD-RPLY

           MOVE STUS-0005               TO WS-RPLY-STUS-CD
           MOVE WS-RPLY-STUS-CD         TO RPLY-STUS-CD
                                        OF INVD-RPLY
           MOVE LENGTH OF INVD-RPLY TO WS-RPLY-BUFR-LN
           MOVE INVD-RPLY TO WS-RPLY-BUFR
           .

       2062-FORM-OK.
           MOVE WS-RPLY-STUS-CD         TO RPLY-STUS-CD
                                        OF FORM-RPLY
           MOVE LENGTH OF FORM-RPLY TO WS-RPLY-BUFR-LN
           MOVE FORM-RPLY TO WS-RPLY-BUFR
           .

       2070-BLD-IMAG-RPLY.
      *
      * Send an image back to the requestor.
      *
      *
           SET BINARY-CONTENT TO TRUE
           MOVE LENGTH OF IMAG-RPLY TO WS-RPLY-BUFR-LN
           MOVE IMAG-RPLY TO WS-RPLY-BUFR
           PERFORM 8060-DOC-ISRT

           MOVE HTTP-CNTE-TY-IMAG   TO HTTP-PTCL-HDR-CNTE-TY
           .

       8000-WHO-IS-CALLING.
      *
      * Obtain information about the current invocation
      * environment - region name, who is executing this transaction,
      * etc.
      *
      * Significant points here, if your application is supposed
      * to be executed via https, it would be a good idea to check
      * the value returned in SSLTYPE to ensure SSL is in effect.
      * Also, some of the information obtained from the EXTRACT TCPIP
      * API is included in the error messages written to the log.  This
      * information is often helpful in debugging.
      *
           EXEC CICS ASSIGN
               APPLID(WS-CICS-APPLID)
               USERID(THIS-USERID)
               USERNAME(THIS-USERNAME)
           END-EXEC

           MOVE LENGTH OF WS-CLNT-NM   TO WS-CLNT-NM-LN
           MOVE LENGTH OF WS-CLNT-ADDR TO WS-CLNT-ADDR-LN
           MOVE LENGTH OF WS-SRVR-NM   TO WS-SRVR-NM-LN
           MOVE LENGTH OF WS-SRVR-ADDR TO WS-SRVR-ADDR-LN

           EXEC CICS
               EXTRACT TCPIP
               AUTHENTICATE(WS-AUTH-CVDA)
               CLIENTNAME(WS-CLNT-NM)
               CNAMELENGTH(WS-CLNT-NM-LN)
               CLIENTADDR(WS-CLNT-ADDR)
               CADDRLENGTH(WS-CLNT-ADDR-LN)
               SERVERNAME(WS-SRVR-NM)
               SNAMELENGTH(WS-SRVR-NM-LN)
               SERVERADDR(WS-SRVR-ADDR)
               SADDRLENGTH(WS-SRVR-ADDR-LN)
               SSLTYPE(WS-SSL-TY-CVDA)
               TCPIPSERVICE(WS-TCPIP-SRVC-NM)
               PORTNUMBER(WS-PORT-NB)
               RESP(WS-RESP)
               RESP2(WS-RESP2)
           END-EXEC

           EVALUATE WS-RESP
               WHEN DFHRESP( NORMAL )
                    CONTINUE
               WHEN OTHER
                    INITIALIZE CICS-API-FAILED
                    MOVE 'EXTRACT TCPIP' TO CICS-API-FAILED
                    MOVE '8000'        TO CICS-API-FAILED-LOC
                    PERFORM 8900-GET-CICS-RESP-MNEMONIC
           END-EVALUATE
           EVALUATE WS-RESP
               WHEN DFHRESP( NORMAL )
                    CONTINUE
               WHEN DFHRESP( INVREQ )
                    PERFORM 8910-MAKE-CICS-ERR-MSG
                    PERFORM 9100-WTO
               WHEN DFHRESP( LENGERR )
                    IF WS-RESP2 = 1
      *                 Other values of RESP2 indicate that one of
      *                 the address or name fields was truncated, and
      *                 that's not significant enough for me to write
      *                 code to deal with it right now. The address
      *                 fields are all the right length, so it would
      *                 just be a name field.
                        PERFORM 8910-MAKE-CICS-ERR-MSG
                        PERFORM 9100-WTO
                    END-IF
           END-EVALUATE

           EVALUATE TRUE
      *        This code makes the later uses of reference
      *        modification on the client name work correctly.
               WHEN WS-CLNT-NM-LN > LENGTH OF WS-CLNT-NM
      *             The client name was truncated, but for our
      *             purposes what we got was sufficient.
                    MOVE LENGTH OF WS-CLNT-NM   TO WS-CLNT-NM-LN
               WHEN WS-CLNT-NM-LN = 0
      *             The client name was not found.
                    MOVE 1                      TO WS-CLNT-NM-LN
           END-EVALUATE

           EVALUATE TRUE
      *        This code makes the later uses of reference
      *        modification on the server name work correctly.
               WHEN WS-SRVR-NM-LN > LENGTH OF WS-SRVR-NM
      *             The server name was truncated, but for our
      *             purposes what we got was sufficient.
                    MOVE LENGTH OF WS-SRVR-NM   TO WS-SRVR-NM-LN
               WHEN WS-SRVR-NM-LN = 0
      *             The server name was not found.
                    MOVE 1                      TO WS-SRVR-NM-LN
           END-EVALUATE

#debug     DISPLAY
#debug         MYNAME
#debug         SPACE
#debug         THIS-USERID
#debug         SPACE
#debug         THIS-USERNAME
#debug         SPACE
#debug         WS-CLNT-NM
#debug         SPACE
#debug         WS-CLNT-ADDR
#debug         SPACE
#debug         WS-SRVR-NM
#debug         SPACE
#debug         WS-SRVR-ADDR
#debug         ':'
#debug         WS-PORT-NB
           .

       8010-GET-HTTP-MTHD.
      *
      * Obtain the http method - GET, POST, etc.
      *
           MOVE LENGTH OF WS-HTTP-MTHD TO WS-HTTP-MTHD-LN
           EXEC CICS
               WEB EXTRACT
               HTTPMETHOD(WS-HTTP-MTHD)
               METHODLENGTH(WS-HTTP-MTHD-LN)
               RESP(WS-RESP)
               RESP2(WS-RESP2)
           END-EXEC
           INITIALIZE HAVE-ERR-MSG-SUFX-SW
           EVALUATE WS-RESP
               WHEN DFHRESP( NORMAL )
                    CONTINUE
               WHEN DFHRESP( INVREQ )
                    SET NON-HTTP-RQST-DONE TO TRUE
               WHEN DFHRESP( LENGERR )
                    INITIALIZE WS-TRUNC-ITEM
                    MOVE 'http-method' TO WS-TRUNC-ITEM
                    PERFORM 8920-MAKE-TRUNC-SUFX
      *             This was truncated, but we want the length set
      *             so that we can use it in reference modification
      *             later.
                    MOVE LENGTH OF WS-HTTP-MTHD TO WS-HTTP-MTHD-LN
           END-EVALUATE
           EVALUATE WS-RESP
               WHEN DFHRESP( NORMAL )
                    CONTINUE
               WHEN OTHER
                    IF NON-HTTP-RQST-DONE
      *                 This is an interesting situation.  I would
      *                 suggest that a new error message be defined to
      *                 indicate this condition.  Such an error message
      *                 could be modeled on the existing CICS error
      *                 message UDOT0004E.
                        CONTINUE
                    ELSE
                        INITIALIZE CICS-API-FAILED
                        MOVE 'WEB-EXTRACT' TO CICS-API-FAILED
                        MOVE '8010'        TO CICS-API-FAILED-LOC
                        PERFORM 8900-GET-CICS-RESP-MNEMONIC
                        PERFORM 8910-MAKE-CICS-ERR-MSG
                        PERFORM 9100-WTO
                    END-IF
           END-EVALUATE
           .

       8020-GET-HTTP-VERS.
      *
      * Obtain the http version string.  This is from the http
      * headers that accompany the request.
      *
           MOVE LENGTH OF WS-HTTP-VERS TO WS-HTTP-VERS-LN
           EXEC CICS
               WEB EXTRACT
               HTTPVERSION(WS-HTTP-VERS)
               VERSIONLEN(WS-HTTP-VERS-LN)
               RESP(WS-RESP)
               RESP2(WS-RESP2)
           END-EXEC
           INITIALIZE HAVE-ERR-MSG-SUFX-SW
           EVALUATE WS-RESP
               WHEN DFHRESP( NORMAL )
                    CONTINUE
               WHEN DFHRESP( INVREQ )
                    SET NON-HTTP-RQST-DONE TO TRUE
               WHEN DFHRESP( LENGERR )
                    INITIALIZE WS-TRUNC-ITEM
                    MOVE 'http-version' TO WS-TRUNC-ITEM
                    PERFORM 8920-MAKE-TRUNC-SUFX
      *             This was truncated, but we want the length set
      *             so that we can use it in reference modification
      *             later.
                    MOVE LENGTH OF WS-HTTP-VERS TO WS-HTTP-VERS-LN
           END-EVALUATE
           EVALUATE WS-RESP
               WHEN DFHRESP( NORMAL )
                    CONTINUE
               WHEN OTHER
                    IF NON-HTTP-RQST-DONE
      *                 This is an interesting situation.  I would
      *                 suggest that a new error message be defined to
      *                 indicate this condition.  Such an error message
      *                 could be modeled on the existing CICS error
      *                 message UDOT0004E.
                        CONTINUE
                    ELSE
                        INITIALIZE CICS-API-FAILED
                        MOVE 'WEB-EXTRACT' TO CICS-API-FAILED
                        MOVE '8020'        TO CICS-API-FAILED-LOC
                        PERFORM 8900-GET-CICS-RESP-MNEMONIC
                        PERFORM 8910-MAKE-CICS-ERR-MSG
                        PERFORM 9100-WTO
                    END-IF
           END-EVALUATE
           .

       8030-GET-HTTP-PATH.
      *
      * Obtain the http path - the bit that comes between the first
      * slash after "http://" and the question mark.
      *
           MOVE LENGTH OF WS-HTTP-PATH TO WS-HTTP-PATH-LN
           EXEC CICS
               WEB EXTRACT
               PATH(WS-HTTP-PATH)
               PATHLENGTH(WS-HTTP-PATH-LN)
               RESP(WS-RESP)
               RESP2(WS-RESP2)
           END-EXEC
           INITIALIZE HAVE-ERR-MSG-SUFX-SW
           EVALUATE WS-RESP
               WHEN DFHRESP( NORMAL )
                    CONTINUE
               WHEN DFHRESP( INVREQ )
                    SET NON-HTTP-RQST-DONE TO TRUE
               WHEN DFHRESP( LENGERR )
                    MOVE STUS-0004 TO WS-RPLY-STUS-CD
                    INITIALIZE WS-TRUNC-ITEM
                    MOVE 'http-path' TO WS-TRUNC-ITEM
                    PERFORM 8920-MAKE-TRUNC-SUFX
      *             This was truncated, but we want the length set
      *             so that we can use it in reference modification
      *             later.
                    MOVE LENGTH OF WS-HTTP-PATH TO WS-HTTP-PATH-LN
           END-EVALUATE
           EVALUATE WS-RESP
               WHEN DFHRESP( NORMAL )
                    CONTINUE
               WHEN OTHER
                    IF NON-HTTP-RQST-DONE
      *                 This is an interesting situation.  I would
      *                 suggest that a new error message be defined to
      *                 indicate this condition.  Such an error message
      *                 could be modeled on the existing CICS error
      *                 message UDOT0004E.
                        CONTINUE
                    ELSE
                        INITIALIZE CICS-API-FAILED
                        MOVE 'WEB-EXTRACT' TO CICS-API-FAILED
                        MOVE '8030'        TO CICS-API-FAILED-LOC
                        PERFORM 8900-GET-CICS-RESP-MNEMONIC
                        PERFORM 8910-MAKE-CICS-ERR-MSG
                        PERFORM 9100-WTO
                    END-IF
           END-EVALUATE
           .

       8040-GET-HTTP-QRY-STRN.
      *
      * Obtain the http query string - the bit that comes after
      * the question mark.
      *
           MOVE LENGTH OF WS-HTTP-QRY-STRN TO WS-HTTP-QRY-STRN-LN
           EXEC CICS
               WEB EXTRACT
               QUERYSTRING(WS-HTTP-QRY-STRN)
               QUERYSTRLEN(WS-HTTP-QRY-STRN-LN)
               RESP(WS-RESP)
               RESP2(WS-RESP2)
           END-EXEC
           INITIALIZE HAVE-ERR-MSG-SUFX-SW
           EVALUATE WS-RESP
               WHEN DFHRESP( NORMAL )
                    CONTINUE
               WHEN DFHRESP( INVREQ )
                    SET NON-HTTP-RQST-DONE TO TRUE
               WHEN DFHRESP( LENGERR )
                    INITIALIZE WS-TRUNC-ITEM
                    MOVE 'http-query-string' TO WS-TRUNC-ITEM
                    PERFORM 8920-MAKE-TRUNC-SUFX
      *             This was truncated, but we want the length set
      *             so that we can use it in reference modification
      *             later.
                    MOVE LENGTH OF WS-HTTP-QRY-STRN
                      TO WS-HTTP-QRY-STRN-LN
           END-EVALUATE
           EVALUATE WS-RESP
               WHEN DFHRESP( NORMAL )
                    CONTINUE
               WHEN OTHER
                    IF NON-HTTP-RQST-DONE
      *                 This is an interesting situation.  I would
      *                 suggest that a new error message be defined to
      *                 indicate this condition.  Such an error message
      *                 could be modeled on the existing CICS error
      *                 message UDOT0004E.
                        CONTINUE
                    ELSE
                        INITIALIZE CICS-API-FAILED
                        MOVE 'WEB-EXTRACT' TO CICS-API-FAILED
                        MOVE '8040'        TO CICS-API-FAILED-LOC
                        PERFORM 8900-GET-CICS-RESP-MNEMONIC
                        PERFORM 8910-MAKE-CICS-ERR-MSG
                        PERFORM 9100-WTO
                    END-IF
           END-EVALUATE
           .

       8050-DOC-CRTE.
      *
      * Create the document into which we will insert the http
      * protocol header(s) and html/text/xml that constitue
      * the reply to be sent to the requestor.
      *
      * If you take a look at 8090-WEB-SEND, you'll see that we use
      * the WS-DOC-TOKN to indicate which document we want to send
      * as a reply.
      *
           EXEC CICS
               DOCUMENT CREATE
               DOCTOKEN(WS-DOC-TOKN)
               RESP(WS-RESP)
               RESP2(WS-RESP2)
           END-EXEC
           INITIALIZE HAVE-ERR-MSG-SUFX-SW
           EVALUATE WS-RESP
               WHEN DFHRESP( NORMAL )
                    CONTINUE
               WHEN OTHER
                    INITIALIZE CICS-API-FAILED
                    MOVE 'DOCUMENT-CREATE' TO CICS-API-FAILED
                    MOVE '8050'            TO CICS-API-FAILED-LOC
                    PERFORM 8900-GET-CICS-RESP-MNEMONIC
                    PERFORM 8910-MAKE-CICS-ERR-MSG
                    PERFORM 9100-WTO
           END-EVALUATE
           .

       8060-DOC-ISRT.
      *
      * Insert the content of WS-RPLY-BUFR into the reply document.
      *
      * This example application only does one insert per reply.  The
      * CICS documentation indicates one can do multiple inserts, with
      * each being added to the "bottom" of the document.  One can
      * also create "bookmarks" within the document and insert at a
      * bookmark location.
      *
           IF BINARY-CONTENT
               EXEC CICS
                   DOCUMENT INSERT
                   DOCTOKEN(WS-DOC-TOKN)
                   BINARY(WS-RPLY-BUFR)
                   LENGTH(WS-RPLY-BUFR-LN)
                   RESP(WS-RESP)
                   RESP2(WS-RESP2)
               END-EXEC
           ELSE
               EXEC CICS
                   DOCUMENT INSERT
                   DOCTOKEN(WS-DOC-TOKN)
                   TEXT(WS-RPLY-BUFR)
                   LENGTH(WS-RPLY-BUFR-LN)
                   HOSTCODEPAGE(HOST-CD-PG)
                   RESP(WS-RESP)
                   RESP2(WS-RESP2)
               END-EXEC
           END-IF

           INITIALIZE HAVE-ERR-MSG-SUFX-SW
           EVALUATE WS-RESP
               WHEN DFHRESP( NORMAL )
                    CONTINUE
               WHEN OTHER
                    INITIALIZE CICS-API-FAILED
                    MOVE 'DOCUMENT-INSERT' TO CICS-API-FAILED
                    MOVE '8060'            TO CICS-API-FAILED-LOC
                    PERFORM 8900-GET-CICS-RESP-MNEMONIC
                    PERFORM 8910-MAKE-CICS-ERR-MSG
                    PERFORM 9100-WTO
           END-EVALUATE
           .

       8070-WEB-READ-HDR.
      *
      * Obtain the http protocol header item specified in
      * WS-HTTP-HDR-TO-RTV.
      *
           INITIALIZE HTTP-HDR-NOT-FND-SW
           EXEC CICS
               WEB READ
               HTTPHEADER(WS-HTTP-HDR-TO-RTV)
               NAMELENGTH(WS-HTTP-HDR-TO-RTV-LN)
               VALUE(WS-HTTP-HDR-BUFR)
               VALUELENGTH(WS-HTTP-HDR-BUFR-LN)
               RESP(WS-RESP)
               RESP2(WS-RESP2)
           END-EXEC
           INITIALIZE HAVE-ERR-MSG-SUFX-SW
           EVALUATE WS-RESP
               WHEN DFHRESP( NORMAL )
                    CONTINUE
               WHEN OTHER
                    INITIALIZE CICS-API-FAILED
                    MOVE 'WEB-READ-HTTPHEADER' TO CICS-API-FAILED
                    MOVE '8070'            TO CICS-API-FAILED-LOC
                    PERFORM 8900-GET-CICS-RESP-MNEMONIC
                    PERFORM 8930-MAKE-HTTP-HDR-ERR-SUFX
           END-EVALUATE
           EVALUATE WS-RESP
               WHEN DFHRESP( NORMAL )
                    CONTINUE
               WHEN DFHRESP( NOTFND )
                    IF WS-RESP2 = 1
      *                 Requested header was not found
                        SET HTTP-HDR-NOT-FND TO TRUE
                    ELSE
                        PERFORM 8910-MAKE-CICS-ERR-MSG
                        PERFORM 9100-WTO
                    END-IF
               WHEN OTHER
                    PERFORM 8910-MAKE-CICS-ERR-MSG
                    PERFORM 9100-WTO
           END-EVALUATE
           .

       8080-UNSTRN-CHARSET.
      *
      * Get the first client code page sent on the http header.  This
      * is used on the WEB SEND API call to convert from the host
      * code page (probably 037 EBCDIC) to the client code page (which
      * we don't know, probably ISO-8859-1).
      *
      * This isn't very robust, and could probably benefit from its
      * own subroutine for parsing out the preferred code page instead
      * of just arbitrarily using the first code page listed.
      *
      * See http://www.ietf.org/rfc/rfc2616.txt?number=2616, section
      * 14.2 for the full syntax of the Accept-Charset protocol
      * header.
      *
           UNSTRING WS-HTTP-HDR-BUFR
             DELIMITED ',' OR ';' OR SPACE
             INTO
               WS-HTTP-CLNT-CHARSET
               WS-DUMMY-BUFR
           END-UNSTRING
           .

       8090-WEB-SEND.
      *
      * Send the reply constructed in the document referenced by
      * WS-DOC-TOKN back to the requester.
      *
           EXEC CICS
               WEB SEND
               DOCTOKEN(WS-DOC-TOKN)
               CLNTCODEPAGE(WS-HTTP-CLNT-CHARSET)
               RESP(WS-RESP)
               RESP2(WS-RESP2)
           END-EXEC
           INITIALIZE HAVE-ERR-MSG-SUFX-SW
           EVALUATE WS-RESP
               WHEN DFHRESP( NORMAL )
                    CONTINUE
               WHEN OTHER
                    INITIALIZE CICS-API-FAILED
                    MOVE 'WEB-SEND'        TO CICS-API-FAILED
                    MOVE '8090'            TO CICS-API-FAILED-LOC
                    PERFORM 8900-GET-CICS-RESP-MNEMONIC
           END-EVALUATE
           EVALUATE WS-RESP
               WHEN DFHRESP( NORMAL )
                    CONTINUE
               WHEN DFHRESP( NOTFND )
                    IF WS-RESP2 = 7
      *                 Requested client code page is bad
                        PERFORM 8940-MAKE-CLNT-CD-PG-ERR-SUFX
                    END-IF
                    PERFORM 8910-MAKE-CICS-ERR-MSG
                    PERFORM 9100-WTO
               WHEN DFHRESP( NOTFND )
                    PERFORM 8910-MAKE-CICS-ERR-MSG
                    PERFORM 9100-WTO
           END-EVALUATE
           .

       8100-WEB-WRITE.
      *
      * Send the reply constructed in the document referenced by
      * WS-DOC-TOKN back to the requester.
      *
           EXEC CICS
               WEB WRITE
               HTTPHEADER(HTTP-PTCL-HDR-NM)
               NAMELENGTH(HTTP-PTCL-HDR-NM-LN)
               VALUE(HTTP-PTCL-HDR-VAL)
               VALUELENGTH(HTTP-PTCL-HDR-VAL-LN)
               RESP(WS-RESP)
               RESP2(WS-RESP2)
           END-EXEC
           INITIALIZE HAVE-ERR-MSG-SUFX-SW
           EVALUATE WS-RESP
               WHEN DFHRESP( NORMAL )
                    CONTINUE
               WHEN OTHER
                    INITIALIZE CICS-API-FAILED
                    MOVE 'WEB-WRITE'       TO CICS-API-FAILED
                    MOVE '8100'            TO CICS-API-FAILED-LOC
                    PERFORM 8900-GET-CICS-RESP-MNEMONIC
                    PERFORM 8910-MAKE-CICS-ERR-MSG
                    PERFORM 9100-WTO
           END-EVALUATE
           .

       8110-WEB-READ-FORMFIELD.
      *
      * Read the specified field from the HTML form with which this
      * program was invoked.
      *
           MOVE LENGTH OF WS-FORM-FLD-VAL TO WS-FORM-FLD-VAL-LN
           INITIALIZE FORM-FLD-NOT-FND-SW
           EXEC CICS
               WEB READ
               FORMFIELD(WS-FORM-FLD-NM)
               NAMELENGTH(WS-FORM-FLD-NM-LN)
               VALUE(WS-FORM-FLD-VAL)
               VALUELENGTH(WS-FORM-FLD-VAL-LN)
               CLNTCODEPAGE(WS-HTTP-CLNT-CHARSET)
               HOSTCODEPAGE(HOST-CD-PG)
               RESP(WS-RESP)
               RESP2(WS-RESP2)
           END-EXEC
           INITIALIZE HAVE-ERR-MSG-SUFX-SW
           EVALUATE WS-RESP
               WHEN DFHRESP( NORMAL )
                    MOVE FUNCTION UPPER-CASE(WS-FORM-FLD-VAL)
                      TO WS-FORM-FLD-VAL
               WHEN OTHER
      *             Provide some human-readable info for debugging.
                    INITIALIZE CICS-API-FAILED
                    MOVE 'WEB-READ-FORMFIELD' TO CICS-API-FAILED
                    MOVE '8110'            TO CICS-API-FAILED-LOC
                    PERFORM 8900-GET-CICS-RESP-MNEMONIC
           END-EVALUATE
           EVALUATE WS-RESP
               WHEN DFHRESP( NORMAL )
                    CONTINUE
               WHEN DFHRESP( NOTFND )
                    IF WS-RESP2 = 1
      *                 Form field was not found.
                        SET FORM-FLD-NOT-FND TO TRUE
                        MOVE WS-FORM-FLD-NM(1:WS-FORM-FLD-NM-LN)
                          TO WS-MSNG-FORM-FLD-NM
                    ELSE
                        PERFORM 8910-MAKE-CICS-ERR-MSG
                        PERFORM 9100-WTO
                    END-IF
               WHEN DFHRESP( INVREQ )
                    EVALUATE WS-RESP2
                        WHEN 11
      *                      Requested client code page is bad
                             PERFORM 8940-MAKE-CLNT-CD-PG-ERR-SUFX
                        WHEN 12
      *                      Requested server code page is bad
                             PERFORM 8950-MAKE-SRVR-CD-PG-ERR-SUFX
                        WHEN 14
      *                      Requested client and server code page
      *                      combination is bad.
                             PERFORM 8960-MAKE-C-S-CD-PG-ERR-SUFX
                    END-EVALUATE
                    IF WS-RESP2 = 13
      *                 The POST request didn't provide a form.
                        SET FORM-FLD-NOT-FND TO TRUE
                    ELSE
                        PERFORM 8910-MAKE-CICS-ERR-MSG
                        PERFORM 9100-WTO
                    END-IF
               WHEN OTHER
                    PERFORM 8910-MAKE-CICS-ERR-MSG
                    PERFORM 9100-WTO
           END-EVALUATE
           .

       8900-GET-CICS-RESP-MNEMONIC.
      *
      * Obtain the mnemonic text for the RESP code that resulted
      * from the most recent CICS API call.
      *
           CALL CICS-ERR-PGM USING
               WS-RESP
               WS-CICS-RESP-MNEMONIC
           END-CALL
           .

       8910-MAKE-CICS-ERR-MSG.
      *
      * Concatenate fields to create a meaningful error message
      * to be written to the JESMSGLG.  This should assist in
      * debugging any problems that occur.
      *
           MOVE SPACES   TO WTO-TXT
           MOVE WS-RESP  TO CICS-RESP-DSPL
           MOVE WS-RESP2 TO CICS-RESP2-DSPL
           SET MSG-NB-CICS-ERR TO TRUE
           SET WTO-CA-MSG-TY-ERR      TO TRUE
           STRING
               CICS-API-FAILED
                 DELIMITED BY SPACE
               SPACE
                 DELIMITED BY SIZE
               CICS-RESP-DSPL-X
                 DELIMITED BY SPACE
               SPACE
                 DELIMITED BY SIZE
               WS-CICS-RESP-MNEMONIC
                 DELIMITED BY SPACE
               SPACE
                 DELIMITED BY SIZE
               CICS-RESP2-DSPL-X
                 DELIMITED BY SPACE
               SPACE
                 DELIMITED BY SIZE
               CICS-API-FAILED-LOC
                 DELIMITED BY SIZE
             INTO
               WTO-TXT
             WITH POINTER
               WTO-LN
           END-STRING

           IF HAVE-ERR-MSG-SUFX
               STRING
                   SPACE
                     DELIMITED BY SIZE
                   WTO-SUFX(1:WTO-SUFX-LN)
                     DELIMITED BY SIZE
                 INTO
                   WTO-TXT
                 WITH POINTER
                   WTO-LN
               END-STRING
           END-IF

           INITIALIZE HAVE-ERR-MSG-SUFX-SW
           .

       8920-MAKE-TRUNC-SUFX.
      *
      * Construct operator message suffix indicating which item
      * was truncated.
      *
           SET HAVE-ERR-MSG-SUFX TO TRUE
           INITIALIZE WTO-SUFX
           MOVE 1 TO WTO-SUFX-LN
           STRING
               WS-TRUNC-ITEM
                 DELIMITED SPACE
               ' was truncated'
                 DELIMITED SIZE
               ' client IP: '
                 DELIMITED SIZE
               WS-CLNT-ADDR
                 DELIMITED SIZE
               ' client name: '
                 DELIMITED SIZE
               WS-CLNT-NM(1:WS-CLNT-NM-LN)
                 DELIMITED SIZE
             INTO
               WTO-SUFX
             POINTER
               WTO-SUFX-LN
           END-STRING
           .

       8930-MAKE-HTTP-HDR-ERR-SUFX.
      *
      * Construct operator message suffix indicating which http
      * header item was being processed when the error occurred.
      *
           SET HAVE-ERR-MSG-SUFX TO TRUE
           INITIALIZE WTO-SUFX
           MOVE 1 TO WTO-SUFX-LN
           STRING
               WS-HTTP-HDR-TO-RTV(1:WS-HTTP-HDR-TO-RTV-LN)
                 DELIMITED SIZE
               ' was being procesed'
                 DELIMITED SIZE
               ' client IP: '
                 DELIMITED SIZE
               WS-CLNT-ADDR
                 DELIMITED SIZE
               ' client name: '
                 DELIMITED SIZE
               WS-CLNT-NM(1:WS-CLNT-NM-LN)
                 DELIMITED SIZE
             INTO
               WTO-SUFX
             POINTER
               WTO-SUFX-LN
           END-STRING
           .

       8940-MAKE-CLNT-CD-PG-ERR-SUFX.
      *
      * Construct operator message suffix indicating the client
      * code page that was invalid.
      *
           SET HAVE-ERR-MSG-SUFX TO TRUE
           INITIALIZE WTO-SUFX
           MOVE 1 TO WTO-SUFX-LN
           STRING
               ' client code page is '
                 DELIMITED SIZE
               WS-HTTP-CLNT-CHARSET
                 DELIMITED SIZE
               ' client IP: '
                 DELIMITED SIZE
               WS-CLNT-ADDR
                 DELIMITED SIZE
               ' client name: '
                 DELIMITED SIZE
               WS-CLNT-NM(1:WS-CLNT-NM-LN)
                 DELIMITED SIZE
             INTO
               WTO-SUFX
             POINTER
               WTO-SUFX-LN
           END-STRING
           .

       8950-MAKE-SRVR-CD-PG-ERR-SUFX.
      *
      * Construct operator message suffix indicating the server
      * code page that was invalid.
      *
           SET HAVE-ERR-MSG-SUFX TO TRUE
           INITIALIZE WTO-SUFX
           MOVE 1 TO WTO-SUFX-LN
           STRING
               ' server code page is '
                 DELIMITED SIZE
               HOST-CD-PG
                 DELIMITED SIZE
               ' server IP: '
                 DELIMITED SIZE
               WS-SRVR-ADDR
                 DELIMITED SIZE
               ' port: '
                 DELIMITED SIZE
               WS-PORT-NB
                 DELIMITED SIZE
               ' server name: '
                 DELIMITED SIZE
               WS-SRVR-NM(1:WS-SRVR-NM-LN)
                 DELIMITED SIZE
             INTO
               WTO-SUFX
             POINTER
               WTO-SUFX-LN
           END-STRING
           .

       8960-MAKE-C-S-CD-PG-ERR-SUFX.
      *
      * Construct operator message suffix indicating the client and
      * server code page combination that was invalid.
      *
           SET HAVE-ERR-MSG-SUFX TO TRUE
           INITIALIZE WTO-SUFX
           MOVE 1 TO WTO-SUFX-LN
           STRING
               ' client code page is '
                 DELIMITED SIZE
               WS-HTTP-CLNT-CHARSET
                 DELIMITED SIZE
               ' client IP: '
                 DELIMITED SIZE
               WS-CLNT-ADDR
                 DELIMITED SIZE
               ' client name: '
                 DELIMITED SIZE
               WS-CLNT-NM(1:WS-CLNT-NM-LN)
                 DELIMITED SIZE
               ' server code page is '
                 DELIMITED SIZE
               HOST-CD-PG
                 DELIMITED SIZE
               ' server IP: '
                 DELIMITED SIZE
               WS-SRVR-ADDR
                 DELIMITED SIZE
               ' port: '
                 DELIMITED SIZE
               WS-PORT-NB
                 DELIMITED SIZE
               ' server name: '
                 DELIMITED SIZE
               WS-SRVR-NM(1:WS-SRVR-NM-LN)
                 DELIMITED SIZE
             INTO
               WTO-SUFX
             POINTER
               WTO-SUFX-LN
           END-STRING
           .

       9100-WTO.
      *
      * In order to avoid issues with the WRITE OPERATOR CICS API
      * not being threadsafe, it is encapsulated in its own program
      * object and we LINK to it instead of dynamically CALLing it.
      * The CICS Programming Guide has a good explanation of
      * threadsafe and its implications.
      *
      * And just look at how messy life gets if you encounter an
      * error within error handling code.
      *
           SET CICS-ERR TO TRUE
           MOVE WTO-LN  TO WTO-CA-TXT-LN
           MOVE WTO-TXT TO WTO-CA-TXT
           MOVE MSG-NB  TO WTO-CA-MSG-NB
           MOVE MYNAME  TO WTO-CA-CALLER
           MOVE LENGTH OF WTO-CA TO WTO-CA-LN

           EXEC CICS
               LINK
               PROGRAM(CICS-WTO-PGM)
               COMMAREA(WTO-CA)
               LENGTH(WTO-CA-LN)
               RESP(WS-RESP)
               RESP2(WS-RESP2)
           END-EXEC

           IF WTO-CA-RC-NORMAL AND WS-RESP = DFHRESP(NORMAL)
               CONTINUE
           ELSE
               PERFORM 8900-GET-CICS-RESP-MNEMONIC
               DISPLAY
                 MYNAME
                 SPACE
                 CICS-WTO-PGM
                 ' RC = '
                 WTO-CA-RC
                 ' RESP = '
                 WS-RESP
                 ' CICS RESP MNEMONIC = '
                 WS-CICS-RESP-MNEMONIC
                 ' RESP2 = '
                 WS-RESP2
                 ' attempting to write operator '
                 WTO-CA-TXT(1:WTO-CA-TXT-LN)
               STRING
                   MYNAME DELIMITED SIZE
                   ' State dump in 9100-WTO ' DELIMITED SIZE
                 INTO
                   CEE3DMP-TITL-SPFC OF LCL-APLC-DEBUG-AREA
               END-STRING
               CALL 'CEE3DMP' USING
                   CEE3DMP-TITL    OF LCL-APLC-DEBUG-AREA
                   CEE3DMP-OPTIONS OF LCL-APLC-DEBUG-AREA
                   CEE3DMP-LEFB-CD OF LCL-APLC-DEBUG-AREA
               END-CALL
           END-IF
           .

       End Program HTTPSAMP.
./ ADD NAME=J7200501 0206-01276-07150-1111-00242-00181-00060-DOTCWS
       PROCESS XOPTS(FLAG(I),COBOL3,NOLINKAGE)
       Identification Division.
       Program-ID. J7200501.
       Author. Craig Schneiderwent.
      *
      * Return the mnemonic for a given CICS EIBRESP value.
      *
       Data Division.
       Linkage Section.
       01  RESP-CD  PIC 9(008) COMP-5.
       01  RESP-TXT PIC X(025).
       Procedure Division Using
           RESP-CD
           RESP-TXT
           .

           EVALUATE RESP-CD
               WHEN DFHRESP( NORMAL )
                    MOVE 'NORMAL' TO RESP-TXT
               WHEN DFHRESP( ERROR  )
                    MOVE 'ERROR' TO RESP-TXT
               WHEN DFHRESP( RDATT )
                    MOVE 'RDATT' TO RESP-TXT
               WHEN DFHRESP( WRBRK )
                    MOVE 'WRBRK' TO RESP-TXT
               WHEN DFHRESP( EOF )
                    MOVE 'EOF' TO RESP-TXT
               WHEN DFHRESP( EODS )
                    MOVE 'EODS' TO RESP-TXT
               WHEN DFHRESP( EOC )
                    MOVE 'EOC' TO RESP-TXT
               WHEN DFHRESP( INBFMH )
                    MOVE 'INBFMH' TO RESP-TXT
               WHEN DFHRESP( ENDINPT )
                    MOVE 'ENDINPT' TO RESP-TXT
               WHEN DFHRESP( NONVAL )
                    MOVE 'NONVAL' TO RESP-TXT
               WHEN DFHRESP( NOSTART )
                    MOVE 'NOSTART' TO RESP-TXT
               WHEN DFHRESP( TERMIDERR )
                    MOVE 'TERMIDERR' TO RESP-TXT
               WHEN DFHRESP( FILENOTFOUND )
                    MOVE 'FILENOTFOUND' TO RESP-TXT
               WHEN DFHRESP( NOTFND )
                    MOVE 'NOTFND' TO RESP-TXT
               WHEN DFHRESP( DUPREC )
                    MOVE 'DUPREC' TO RESP-TXT
               WHEN DFHRESP( DUPKEY )
                    MOVE 'DUPKEY' TO RESP-TXT
               WHEN DFHRESP( INVREQ )
                    MOVE 'INVREQ' TO RESP-TXT
               WHEN DFHRESP( IOERR )
                    MOVE 'IOERR' TO RESP-TXT
               WHEN DFHRESP( NOSPACE )
                    MOVE 'NOSPACE' TO RESP-TXT
               WHEN DFHRESP( NOTOPEN )
                    MOVE 'NOTOPEN' TO RESP-TXT
               WHEN DFHRESP( ENDFILE )
                    MOVE 'ENDFILE' TO RESP-TXT
               WHEN DFHRESP( ILLOGIC )
                    MOVE 'ILLOGIC' TO RESP-TXT
               WHEN DFHRESP( LENGERR )
                    MOVE 'LENGERR' TO RESP-TXT
               WHEN DFHRESP( QZERO )
                    MOVE 'QZERO' TO RESP-TXT
               WHEN DFHRESP( SIGNAL )
                    MOVE 'SIGNAL' TO RESP-TXT
               WHEN DFHRESP( QBUSY )
                    MOVE 'QBUSY' TO RESP-TXT
               WHEN DFHRESP( ITEMERR )
                    MOVE 'ITEMERR' TO RESP-TXT
               WHEN DFHRESP( PGMIDERR )
                    MOVE 'PGMIDERR' TO RESP-TXT
               WHEN DFHRESP( TRANSIDERR )
                    MOVE 'TRANSIDERR' TO RESP-TXT
               WHEN DFHRESP( ENDDATA )
                    MOVE 'ENDDATA' TO RESP-TXT
               WHEN DFHRESP( EXPIRED )
                    MOVE 'EXPIRED' TO RESP-TXT
               WHEN DFHRESP( RETPAGE )
                    MOVE 'RETPAGE' TO RESP-TXT
               WHEN DFHRESP( RTEFAIL )
                    MOVE 'RTEFAIL' TO RESP-TXT
               WHEN DFHRESP( RTESOME )
                    MOVE 'RTESOME' TO RESP-TXT
               WHEN DFHRESP( TSIOERR )
                    MOVE 'TSIOERR' TO RESP-TXT
               WHEN DFHRESP( MAPFAIL )
                    MOVE 'MAPFAIL' TO RESP-TXT
               WHEN DFHRESP( INVERRTERM )
                    MOVE 'INVERRTERM' TO RESP-TXT
               WHEN DFHRESP( INVMPSZ )
                    MOVE 'INVMPSZ' TO RESP-TXT
               WHEN DFHRESP( IGREQID )
                    MOVE 'IGREQID' TO RESP-TXT
               WHEN DFHRESP( OVERFLOW )
                    MOVE 'OVERFLOW' TO RESP-TXT
               WHEN DFHRESP( INVLDC )
                    MOVE 'INVLDC' TO RESP-TXT
               WHEN DFHRESP( NOSTG )
                    MOVE 'NOSTG' TO RESP-TXT
               WHEN DFHRESP( JIDERR )
                    MOVE 'JIDERR' TO RESP-TXT
               WHEN DFHRESP( QIDERR )
                    MOVE 'QIDERR' TO RESP-TXT
               WHEN DFHRESP( NOJBUFSP )
                    MOVE 'NOJBUFSP' TO RESP-TXT
               WHEN DFHRESP( DSSTAT )
                    MOVE 'DSSTAT' TO RESP-TXT
               WHEN DFHRESP( SELNERR )
                    MOVE 'SELNERR' TO RESP-TXT
               WHEN DFHRESP( FUNCERR )
                    MOVE 'FUNCERR' TO RESP-TXT
               WHEN DFHRESP( UNEXPIN )
                    MOVE 'UNEXPIN' TO RESP-TXT
               WHEN DFHRESP( NOPASSBKRD )
                    MOVE 'NOPASSBKRD' TO RESP-TXT
               WHEN DFHRESP( NOPASSBKWR )
                    MOVE 'NOPASSBKWR' TO RESP-TXT
               WHEN DFHRESP( SYSIDERR )
                    MOVE 'SYSIDERR' TO RESP-TXT
               WHEN DFHRESP( ISCINVREQ )
                    MOVE 'ISCINVREQ' TO RESP-TXT
               WHEN DFHRESP( ENQBUSY )
                    MOVE 'ENQBUSY' TO RESP-TXT
               WHEN DFHRESP( ENVDEFERR )
                    MOVE 'ENVDEFERR' TO RESP-TXT
               WHEN DFHRESP( IGREQCD )
                    MOVE 'IGREQCD' TO RESP-TXT
               WHEN DFHRESP( SESSIONERR )
                    MOVE 'SESSIONERR' TO RESP-TXT
               WHEN DFHRESP( SYSBUSY )
                    MOVE 'SYSBUSY' TO RESP-TXT
               WHEN DFHRESP( SESSBUSY )
                    MOVE 'SESSBUSY' TO RESP-TXT
               WHEN DFHRESP( NOTALLOC )
                    MOVE 'NOTALLOC' TO RESP-TXT
               WHEN DFHRESP( CBIDERR )
                    MOVE 'CBIDERR' TO RESP-TXT
               WHEN DFHRESP( INVEXITREQ )
                    MOVE 'INVEXITREQ' TO RESP-TXT
               WHEN DFHRESP( INVPARTNSET )
                    MOVE 'INVPARTNSET' TO RESP-TXT
               WHEN DFHRESP( INVPARTN )
                    MOVE 'INVPARTN' TO RESP-TXT
               WHEN DFHRESP( PARTNFAIL )
                    MOVE 'PARTNFAIL' TO RESP-TXT
               WHEN DFHRESP( USERIDERR )
                    MOVE 'USERIDERR' TO RESP-TXT
               WHEN DFHRESP( NOTAUTH )
                    MOVE 'NOTAUTH' TO RESP-TXT
               WHEN DFHRESP( SUPPRESSED )
                    MOVE 'SUPPRESSED' TO RESP-TXT
               WHEN DFHRESP( NOSPOOL )
                    MOVE 'NOSPOOL' TO RESP-TXT
               WHEN DFHRESP( TERMERR )
                    MOVE 'TERMERR' TO RESP-TXT
               WHEN DFHRESP( ROLLEDBACK )
                    MOVE 'ROLLEDBACK' TO RESP-TXT
               WHEN DFHRESP( END )
                    MOVE 'END' TO RESP-TXT
               WHEN DFHRESP( DISABLED )
                    MOVE 'DISABLED' TO RESP-TXT
               WHEN DFHRESP( ALLOCERR )
                    MOVE 'ALLOCERR' TO RESP-TXT
               WHEN DFHRESP( STRELERR )
                    MOVE 'STRELERR' TO RESP-TXT
               WHEN DFHRESP( OPENERR )
                    MOVE 'OPENERR' TO RESP-TXT
               WHEN DFHRESP( SPOLBUSY )
                    MOVE 'SPOLBUSY' TO RESP-TXT
               WHEN DFHRESP( SPOLERR )
                    MOVE 'SPOLERR' TO RESP-TXT
               WHEN DFHRESP( NODEIDERR )
                    MOVE 'NODEIDERR' TO RESP-TXT
               WHEN DFHRESP( TASKIDERR )
                    MOVE 'TASKIDERR' TO RESP-TXT
               WHEN DFHRESP( TCIDERR )
                    MOVE 'TCIDERR' TO RESP-TXT
               WHEN DFHRESP( DSNNOTFOUND )
                    MOVE 'DSNNOTFOUND' TO RESP-TXT
               WHEN DFHRESP( LOADING )
                    MOVE 'LOADING' TO RESP-TXT
               WHEN DFHRESP( MODELIDERR )
                    MOVE 'MODELIDERR' TO RESP-TXT
               WHEN DFHRESP( OUTDESCRERR )
                    MOVE 'OUTDESCRERR' TO RESP-TXT
               WHEN DFHRESP( PARTNERIDERR )
                    MOVE 'PARTNERIDERR' TO RESP-TXT
               WHEN DFHRESP( PROFILEIDERR )
                    MOVE 'PROFILEIDERR' TO RESP-TXT
               WHEN DFHRESP( NETNAMEIDERR )
                    MOVE 'NETNAMEIDERR' TO RESP-TXT
               WHEN DFHRESP( LOCKED )
                    MOVE 'LOCKED' TO RESP-TXT
               WHEN DFHRESP( RECORDBUSY )
                    MOVE 'RECORDBUSY' TO RESP-TXT
               WHEN DFHRESP( UOWNOTFOUND )
                    MOVE 'UOWNOTFOUND' TO RESP-TXT
               WHEN DFHRESP( UOWLNOTFOUND )
                    MOVE 'UOWLNOTFOUND' TO RESP-TXT
               WHEN DFHRESP( LINKABEND )
                    MOVE 'LINKABEND' TO RESP-TXT
               WHEN DFHRESP( CHANGED )
                    MOVE 'CHANGED' TO RESP-TXT
               WHEN DFHRESP( PROCESSBUSY )
                    MOVE 'PROCESSBUSY' TO RESP-TXT
               WHEN DFHRESP( ACTIVITYBUSY )
                    MOVE 'ACTIVITYBUSY' TO RESP-TXT
               WHEN DFHRESP( PROCESSERR )
                    MOVE 'PROCESSERR' TO RESP-TXT
               WHEN DFHRESP( ACTIVITYERR )
                    MOVE 'ACTIVITYERR' TO RESP-TXT
               WHEN DFHRESP( CONTAINERERR )
                    MOVE 'CONTAINERERR' TO RESP-TXT
               WHEN DFHRESP( EVENTERR )
                    MOVE 'EVENTERR' TO RESP-TXT
               WHEN DFHRESP( TOKENERR )
                    MOVE 'TOKENERR' TO RESP-TXT
               WHEN DFHRESP( NOTFINISHED )
                    MOVE 'NOTFINISHED' TO RESP-TXT
               WHEN DFHRESP( POOLERR )
                    MOVE 'POOLERR' TO RESP-TXT
               WHEN DFHRESP( TIMERERR )
                    MOVE 'TIMERERR' TO RESP-TXT
               WHEN DFHRESP( SYMBOLERR )
                    MOVE 'SYMBOLERR' TO RESP-TXT
               WHEN DFHRESP( TEMPLATERR )
                    MOVE 'TEMPLATERR' TO RESP-TXT
               WHEN DFHRESP( RESUNAVAIL )
                    MOVE 'RESUNAVAIL' TO RESP-TXT
               WHEN DFHRESP( CHANNELERR )
                    MOVE 'CHANNELERR' TO RESP-TXT
               WHEN DFHRESP( CCSIDERR )
                    MOVE 'CCSIDERR' TO RESP-TXT
               WHEN OTHER
                    MOVE 'UNKNOWN' TO RESP-TXT
           END-EVALUATE

           GOBACK
           .

./ ADD NAME=J7200521 0211-06103-06286-1305-00271-00155-00061-DOTCWS
       Identification Division.
       Program-ID.    J7200521.
       Author.        Craig Schneiderwent.
      *
      * Encapsulate the CICS WRITE OPERATOR function.  Caller must
      * specify a message number, which will be prefixed with 'UDOT'
      * and a message type of 'I' (Informational), 'W' (Warning),
      * 'E' (Error), or 'S' (Severe).
      *
      * Good practice is to document the message, currently the place
      * to do that is in MVS/QuickRef.  These messages will be used
      * by on-call and operations staff in problem resolution
      * situations - do your best to help them out with meaningful
      * diagnostics in the message and good documentation of the
      * message.
      *
      * In order to avoid issues with the WRITE OPERATOR CICS API
      * not being threadsafe, it is encapsulated here in its own
      * program object and we LINK to it instead of dynamically
      * CALLing it.
      *
      * The CICS Programming Guide has a good explanation of
      * threadsafe and its implications.
      *
      *
      * Sample Use:
      *
      *Working-Storage Section.
      *01  CONSTANTS.
      *    05  MYNAME                  PIC X(008) VALUE 'J8675309'.
      *    05  CICS-ERR-TXT-PGM        PIC X(008) VALUE 'J7200501'.
      *    05  CICS-WTO-PGM            PIC X(008) VALUE 'J7200521'.
      *    05  MSG-NB                  PIC X(004) VALUE '0005'.
      *
      *01  WORK-AREAS.
      *    05  CICS-RESP               PIC S9(008) COMP-5.
      *    05  CICS-RESP2              PIC S9(008) COMP-5.
      *    05  WTO-CA-LN               PIC 9(008)  COMP-5.
      *    05  DATA-TXT                PIC X(025)
      *        VALUE 'Test message'.
      *
      *01  WTO-CA.
      *    COPY J7200521 REPLACING ==:PRFX:== BY ==WTO-==.
      *
      *Procedure Division.
      *    MOVE LENGTH OF DATA-TXT TO WTO-TXT-LN
      *    MOVE DATA-TXT TO WTO-TXT
      *    MOVE '0005' TO WTO-MSG-NB
      *    SET WTO-MSG-TY-ERR TO TRUE
      *    MOVE MYNAME TO WTO-CALLER
      *    MOVE LENGTH OF WTO-CA TO WTO-CA-LN
      *    EXEC CICS LINK
      *        PROGRAM(CICS-WTO-PGM)
      *        COMMAREA(WTO-CA)
      *        LENGTH(WTO-CA-LN)
      *        RESP(CICS-RESP)
      *        RESP2(CICS-RESP2)
      *    END-EXEC
      *
      *    IF WTO-RC-NORMAL AND CICS-RESP = DFHRESP(NORMAL)
      *        CONTINUE
      *    ELSE
      *        ...error handling
      *    END-IF
      *
      * Return Codes:
      * 0  - Success
      * 4  - Invalid parameter(s)
      *      An error message will be DISPLAYed
      * 16 - Failure
      *
      *
       Environment Division.
       Configuration Section.
       Data Division.
       Working-Storage Section.
       01  CONSTANTS.
           05  MYNAME                  PIC X(008) VALUE 'J7200521'.
           05  CICS-ERR-PGM            PIC X(008) VALUE 'J7200501'.
           05  ERR-MSG-PRFX            PIC X(004) VALUE 'UDOT'.

       01  WORK-AREAS.
           05  ERR-MSG-LN              PIC 9(008) COMP-5 VALUE 1.
           05  CICS-APPL-ID            PIC X(008) VALUE SPACES.
           05  THIS-USERID             PIC X(008) VALUE SPACES.
           05  ERR-MSG-WTO.
               10                      PIC X(050) OCCURS 13.
               10                      PIC X(040).
           05  CICS-API-FAILED         PIC X(030) VALUE SPACES.
           05  CICS-API-FAILED-LOC     PIC X(004) VALUE SPACES.
           05  CICS-RESP-TXT           PIC X(025) VALUE SPACES.
           05  CICS-RESP-DSPL          PIC 9(010) VALUE ZEROES.
           05  CICS-RESP-DSPL-X
               REDEFINES
               CICS-RESP-DSPL          PIC X(010).
           05  CICS-RESP2-DSPL         PIC 9(010) VALUE ZEROES.
           05  CICS-RESP2-DSPL-X
               REDEFINES
               CICS-RESP2-DSPL         PIC X(010).


       Linkage Section.
       01  DFHCOMMAREA.
           COPY J7200521 REPLACING ==:PRFX:== BY ==LS-==.

       Procedure Division Using
           DFHCOMMAREA
           .


           SET LS-RC-NORMAL TO TRUE
           INITIALIZE ERR-MSG-WTO
           PERFORM 0100-EDIT-PARMS

           IF LS-RC-NORMAL
               CONTINUE
           ELSE
               EXEC CICS RETURN END-EXEC
           END-IF

           EXEC CICS ASSIGN
               APPLID(CICS-APPL-ID)
               USERID(THIS-USERID)
               NOHANDLE
           END-EXEC

           MOVE EIBRESP  TO LS-RESP
           MOVE EIBRESP2 TO LS-RESP2

           IF EIBRESP = DFHRESP( NORMAL )
               PERFORM 9040-FMT-MSG-01
               PERFORM 9100-WTO
           ELSE
      *        The documentation for CICS TS 2.2 doesn't list any
      *        way for the above CICS ASSIGN call to fail, but this
      *        routine is likely to be called as an application is
      *        failing.  Hence the "belt _and_ suspenders" technique.
               MOVE 'ASSIGN' TO CICS-API-FAILED
               MOVE 'MAIN'   TO CICS-API-FAILED-LOC
               PERFORM 9200-DSPL-CICS-ERR
               SET LS-RC-BAD TO TRUE
           END-IF

           EXEC CICS RETURN END-EXEC
           .

       0100-EDIT-PARMS.
           IF LS-MSG-TY-VALID
               CONTINUE
           ELSE
               DISPLAY
                   MYNAME
                   ' message type ('
                   LS-MSG-TY
                   ') is invalid.  Valid values are I W E S.'
               SET LS-RC-INVD-PARM TO TRUE
           END-IF

           IF LS-TXT-LN-VALID
               CONTINUE
           ELSE
               DISPLAY
                   MYNAME
                   ' message length ('
                   LS-TXT-LN
                   ') is invalid.  0 < length <= 690.'
               SET LS-RC-INVD-PARM TO TRUE
           END-IF
           .

       9040-FMT-MSG-01.

           MOVE SPACES TO ERR-MSG-WTO
           MOVE +1     TO ERR-MSG-LN
           STRING
               ERR-MSG-PRFX
                 DELIMITED BY SIZE
               LS-MSG-NB
                 DELIMITED BY SIZE
               LS-MSG-TY
                 DELIMITED BY SIZE
               SPACE
                 DELIMITED BY SIZE
               CICS-APPL-ID
                 DELIMITED BY SIZE
               SPACE
                 DELIMITED BY SIZE
               EIBTRNID
                 DELIMITED BY SIZE
               SPACE
                 DELIMITED BY SIZE
               LS-CALLER
                 DELIMITED BY SIZE
               SPACE
                 DELIMITED BY SIZE
               THIS-USERID
                 DELIMITED BY SIZE
               SPACE
                 DELIMITED BY SIZE
               LS-TXT(1:LS-TXT-LN)
                 DELIMITED BY SIZE
             INTO
               ERR-MSG-WTO
             WITH POINTER
               ERR-MSG-LN
           END-STRING
           .

       9100-WTO.
      *
      *    The NOHANDLE is here because if an error occurs during
      *    error processing, an abend is too likely to create an
      *    abend loop.
      *
           EXEC CICS
                WRITE OPERATOR
                TEXT(ERR-MSG-WTO)
                TEXTLENGTH(ERR-MSG-LN)
                NOHANDLE
           END-EXEC

           MOVE EIBRESP  TO LS-RESP
           MOVE EIBRESP2 TO LS-RESP2
           IF EIBRESP = DFHRESP( NORMAL )
               CONTINUE
           ELSE
               MOVE 'WRITE OPERATOR' TO CICS-API-FAILED
               MOVE '9100'           TO CICS-API-FAILED-LOC
               SET LS-RC-BAD TO TRUE
               PERFORM 9200-DSPL-CICS-ERR
           END-IF
           .

       9200-DSPL-CICS-ERR.
      *
      * This is really a last ditch effort at error determination.  If
      * the ASSIGN or WRITE OPERATOR calls fail, this paragraph will
      * provide at least some indication of the problem.
      *
           CALL CICS-ERR-PGM USING
               LS-RESP
               CICS-RESP-TXT
           END-CALL
           MOVE LS-RESP    TO CICS-RESP-DSPL
           MOVE LS-RESP2   TO CICS-RESP2-DSPL

           DISPLAY
               MYNAME
               SPACE
               CICS-API-FAILED
               ' FAILED AT '
               CICS-API-FAILED-LOC
               SPACE
               ' RESP = '
               CICS-RESP-DSPL
               ' RESP TEXT = '
               CICS-RESP-TXT
               ' RESP2 = '
               CICS-RESP2-DSPL
               ' CALLER = '
               LS-CALLER
               ' MESSAGE # = '
               LS-MSG-NB
               ' MESSAGE TYPE = '
               LS-MSG-TY
               ' WTO TEXT LENGTH = '
               LS-TXT-LN
               ' WTO TEXT = '
               LS-TXT(1:LS-TXT-LN)
           .

./ ADD NAME=J7200524 0201-06248-07165-1238-00123-00032-00004-DOTCWS
       Identification Division.
       Program-ID.    J7200524.
      *
      * This program wraps up the LE callable services necessary to
      * obtain the current date and time (GMT or local) and format
      * that date and/or time according to a format string.  This
      * is conceptually similar to the C function strftime().
      *
      * Valid values for the format string can be found by looking
      * up the CEEDATM callable service in the Language Environment
      * Programming Reference.
      *
      * If errors are encountered in any of the LE callable services,
      * the service will display a message.  It appears that CEEMSG
      * is used for this purpose.
      *
      * Under systems other than CICS, Language Environment writes
      * the message to the ddname of the file specified in the MSGFILE
      * run-time option. Under CICS, the message is written to a
      * transient data queue named CESE.
      *
      * Currently, this means that in batch the message is likely to
      * be found in SYSOUT and in CICS it's likely to be found in
      * CEEMSG.
      *
      *Working-Storage Section.
      *01  CONSTANTS.
      *    05  MYNAME                  PIC X(008) VALUE 'J8675309'.
      *    05  FMT-DT-TM-PGM           PIC X(008) VALUE 'J7200524'.
      *
      *01  LCL-TM-SW               PIC X(001).
      *    88  LCL-TM                         VALUE 'Y'.
      *    88  GMT-TM                         VALUE 'N'.
      *
      *01  DT-PIC-STRN.
      *    05  DT-PIC-STRN-LEN     PIC S9(004) COMP-5 VALUE +80.
      *    05  DT-PIC-STRN-VAL     PIC X(080)
      *        VALUE 'Www, DD Mmm YYYY HH:MI:SS GMT'.
      *
      *01  FMT-DT-TM               PIC X(080) VALUE SPACES.
      *
      *Procedure Division.
      *    SET GMT-TM TO TRUE
      *    CALL J7200524-PGM USING
      *        LCL-TM-SW
      *        DT-PIC-STRN
      *        FMT-DT-TM
      *    END-CALL
      *

       Environment Division.
       Data Division.
       Working-Storage Section.
       01  CONSTANTS.
           05  MYNAME                  PIC X(008) VALUE 'J7200524'.

       01  WORK-AREAS.
           05  GMT-LILIAN              PIC 9(008) COMP-5 VALUE 0.
           05  GMT-FLOT                           COMP-2 VALUE 0.0.
           05  OSET-HRS                           COMP-2 VALUE 0.0.
           05  OSET-MIN                           COMP-2 VALUE 0.0.
           05  OSET-SEC                           COMP-2 VALUE 0.0.

       Linkage Section.
      * Input -  Switch indicating whether to pass back local time
      *          or GMT.  A value of 'Y' in this field indicates
      *          local time should be used.  Any value other than 'Y'
      *          indicates GMT should be used.
       01  LCL-TM-SW               PIC X(001).
           88  LCL-TM                         VALUE 'Y'.

      * Input -  Date picture string prefixed by its length.  Valid
      *          picture string formatting is documented in the
      *          Language Environment Programming Reference.
       01  DT-PIC-STRN.
           05  DT-PIC-STRN-LEN     PIC S9(004) COMP-5.
           05  DT-PIC-STRN-VAL
               OCCURS 1 TO 80
               DEPENDING DT-PIC-STRN-LEN
                                   PIC X(001).

      * Output - GMT date/time formatted according to the content
      *          of DT-PIC-STRN-VAL.
       01  FMT-DT-TM               PIC X(080).

       Procedure Division Using
           LCL-TM-SW
           DT-PIC-STRN
           FMT-DT-TM
           .

           MOVE SPACES TO FMT-DT-TM

      *    Obtain GMT time.
           CALL 'CEEGMT' USING
               GMT-LILIAN
               GMT-FLOT
               OMITTED
           END-CALL

           IF LCL-TM
      *        If local time requested, obtain its offset from GMT
      *        and compute local time.
               CALL 'CEEGMTO' USING
                   OSET-HRS
                   OSET-MIN
                   OSET-SEC
                   OMITTED
               END-CALL
               ADD OSET-SEC TO GMT-FLOT
           END-IF

      *    Format the date according to the passed picture string.
           CALL 'CEEDATM' USING
               GMT-FLOT
               DT-PIC-STRN
               FMT-DT-TM
               OMITTED
           END-CALL

           MOVE 0       TO RETURN-CODE
           GOBACK.

