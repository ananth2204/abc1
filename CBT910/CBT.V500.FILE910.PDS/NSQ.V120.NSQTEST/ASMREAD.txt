* NOSQLZ DIRECT READ ON DATABASE
*
ASMREAD  CSECT
ASMREAD  AMODE 31
ASMREAD  RMODE ANY
         YREGS  ,
         BAKR  R14,0
         LR    R12,R15
         USING ASMREAD,R12
* READ ACCESS
         MVI   NSQ_VRSN,C'1'
         MVC   NSQ_ACT,=CL8'READ'
         MVI   NSQ_OPT,C'N'
         MVC   NSQ_TBLNM,=CL8'DBSTATES'
         MVC   NSQ_INLEN,=A(L'OUTZONE)
         XC    NSQ_OUTLN,NSQ_OUTLN
         MVC   NSQ_KYLEN,=AL2(L'INSTATE)
         MVC   NSQ_KYVAL(L'INSTATE),INSTATE
         CALL  NSQTCAPI,(NSQ_BLOCK,OUTZONE),VL
* PROCESS RESULT
         CLC   NSQ_RETCD,=F'0'                NOSQLZ RC=0 ?
         BNE   ERROR
         WTO   TEXT=INMSG                     DISPLAY INPUT KEY
         MVC   OUTLGTH,NSQ_OUTLN+2
         WTO   TEXT=OUTLGTH                   DISPLAY RECORD DATA
         XR    R15,R15
         PR    ,
ERROR    DS    0H
         MVC   ERRORTXT,NSQ_MESSG             COPY ERROR MESSAGE
         WTO   TEXT=ERRORMSG                  DISPLAY ERROR MESSAGE
         ICM   R15,15,NSQ_RETCD
         PR    ,
*
INMSG    DC    H'57'                      USED FOR DISPLAY WTO
INSTATE  DS    0CL75
         DC    XL5'D481899585',CL52' '                   "MAINE"
         DC    XL5'D481899585',CL13' '                   "MAINE"
OUTLGTH  DS    H
OUTZONE  DS    CL100
*
ERRORMSG DC    H'100'
ERRORTXT DS    CL100
* PARAMETER BLOCK
NSQ_BLOCK DS   0H
NSQ_VRSN  DS   CL1           VERSION OF PARAMETER BLOCK: C'1'
NSQ_OPT   DS   CL1           READ OPTION: 'N'EWEST (RECOMMENDED)
NSQ_ACT   DS   CL8        ACTION ("START", "CREATE", "READ", "UPDATE",
*                                 "DELETE", "COMMIT", "ROLLBACK")
NSQ_TBLNM DS   CL8           NAME OF TABLE DATA SET TO BE ACCESSED
* DATA ZONE INFORMATION
NSQ_INLEN DS   CL4   IN      LENGTH OF DATA ZONE SUPPLIED BY CALLER
*                            (FOR ALL OPERATIONS)
NSQ_OUTLN DS   CL4   OUT     LENGTH OF DATA COPIED INTO ZONE BY NOSQLZ
*                            (ONLY FOR "READ" OPERATIONS)
* ERROR INFORMATION
NSQ_RETCD DS   CL4           RETURN CODE
NSQ_MESSG DS   CL100         ERROR MESSAGE (IF RETURN CODE NOT ZERO)
* KEY PARAMETER
NSQ_KYLEN DS   CL2           LENGTH OF KEY (1-250)
NSQ_KYVAL DC   XL250'00'     VALUE OF KEY
*
         LTORG
         END
