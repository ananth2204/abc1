LPALSTxx (LPA library list)

Use the LPALSTxx member to add your installation's read-only
reenterable user programs to the pageable link pack area (PLPA).
Placing programs in the PLPA allows them to be shared among users
of the system.

Use one or more LPALSTxx members to concatenate your
installation's program library data sets to SYS1.LPALIB.  The
system uses this concatenation, which is referred to as the
LPALST concatenation, to build the PLPA.

The modules in the data sets that you define in LPALSTxx must
meet the same requirements as the modules defined in SYS1.LPALIB.
For example, these modules must be reentrant and executable.

During NIP, the system opens and concatenates each data set
specified in LPALSTxx in the order in which the data set names
are listed, starting with the first-specified LPALSTxx member.

The LPALST concatenation can have up to 255 extents.  If you
specify more data sets than the concatenation can contain, the
system truncates the LPALST concatenation and issues messages
that indicate which data sets were not included in the
concatenation.  If one or more LPALSTxx members exist, and the
system can open the specified data sets successfully, the system
uses the LPALST concatenation to build the PLPA (during cold
starts and IPLs that include the CLPA option).  If a library
named in LPALSTxx cannot be opened, message IEA712I is issued for
that library and the system continues to build the LPA list
concatenation with the other libraries named in LPALSTxx.  If no
LPALSTxx members exist, or none of the libraries named in
LPALSTxx can be opened, the system builds the PLPA from only the
modules named in SYS1.LPALIB.

As of MVS Version 4.3, it is no longer necessary for the data
sets in the LPALST to be APF-authorized.  However, any module in
the pageable link pack area will be treated by the system as
though it came from an APF-authorized library.  Ensure that you
have properly protected any data set in the LPALST to avoid
system security and integrity exposures, just as you would
protect any APF-authorized library.

Data sets to be concatenated to SYS1.LPALIB must be cataloged in
the system master catalog or a user catalog identified in the
LPALSTxx member.  (The system does not check OS CVOLs and only
checks user catalogs when a data set name and pack VOLSER are
listed in LPALSTxx.)

It seems that if one copy of a dataset is cataloged in the master
catalog, and a separate copy of that dataset is cataloged in a user
catalog, then the system uses the copy that was cataloged in the
master catalog to create the PLPA.

You cannot use PDSEs in the LPALST concatenation.

To identify which LPALSTxx members the system is to use, specify
the member suffixes on the LPA parameter in IEASYSxx or as a
parameter entered during IPL.


Control Block Information:

         MACRO
         IHALLT &DSECT=YES         LNKLST/LPALST tables
LLT      DSECT
LLTID    DS    C'LLT '             BLOCK ACRONYM
LLTCOUNT DS    F                   NUMBER OF ENTRIES
LLTENTRY DS    0CL45               DSNAME ENTRY
LLTDSLTH DS    FL1                 DSNAME LENGTH AFTER TRUNCATION
LLTDSN   DS    CL44                DATA SET NAME
LLTNEXT  EQU   *                   NEXT ENTRY
         MEND

After the end of the "dataset portion" of the LPA table, there is a
corresponding list of VOLSERs that corresponds to the dataset name
entries.  It is appropriate to bump through both tables at the same
time, to display both "dataset name" and the corresponding "volser".
See the code sample below.


Code to display LPA table entries - DSNAME plus VOLSER:

FINDLPAT DS    0H                  DISPLAY LPA TABLE AND VOLSERS
         L     R2,16               POINT TO THE CVT
         L     R2,X'4AC'(,R2)      CVTSMEXT A(CVTVSTGX) - ADDRESS OF
*                                  VIRTUAL STORAGE MAP EXTENSION.
         L     R3,X'38'(,R2)
         CLC   =C'LPAT',0(R3)      Are we really there?
         BNE   ERROR1
         LA    R3,4(,R3)           BYPASS 'LPAT'
         L     R4,0(,R3)           GET NUMBER OF ENTRIES
         LA    R5,4(,R3)           POINT AT ENTRIES
*--------------------------------------------------------------------*
*        GET  VOLSER FROM SECOND LPA TABLE                           *
*--------------------------------------------------------------------*
         XR    R6,R6               CLEAR REGISTER
         XR    R7,R7               CLEAR REGISTER
         LR    R7,R4               LOAD NUMBER OF ENTRIES TO MULTIPLY
         M     R6,=F'45'           MULTIPLY BY SIZE OF EACH ENTRY
         AR    R7,R5               POINT TO VOLSER TABLE THAT IS
*                                    PAST THE DSN TABLE IN LPAT.
*--------------------------------------------------------------------*
*              LOOP THRU LLT TO GET EACH DATASET                     *
*--------------------------------------------------------------------*
LOOP     DS    0H
         SLR   R2,R2
         IC    R2,0(,R5)           ENTRY LENGTH
         MVC   REC,BLANKS
         S     R2,=F'1'
         MVC   REC+$DSN(0),1(R5)   MOVE IN DSN
         EX    R2,*-6               FOR REAL
         MVC   REC+$VOL1(6),0(R7)  MOVE IN VOLSER FROM 2ND TABLE
*--------------------------------------------------------------------*
*        ISSUE LOCATE TO FIND VOLSER OF DATASET - (OPTIONAL)         *
*--------------------------------------------------------------------*
         MVI   CAMWORK2,X'FF'
         MVC   CAMWORK2+1(255),CAMWORK2
         XC    CAMLIST,CAMLIST     ZERO CAMLIST
         MVC   CAMLIST(4),=XL4'44000000' LOCATE PARMS
         LA    R0,REC+$DSN
         ST    R0,CAMLIST+4        @ OF DATASET
         LA    R0,CAMWORK
         ST    R0,CAMLIST+12       @ OF CAMLIST WORK AREA
         SPACE 1
         LA    R1,CAMLIST
LOCATE   SVC   26                  LOCATE DATASET
         SPACE 1
         MVC   REC+$OFF(4),=X'F0212020'
         ED    REC+$OFF(4),DSNNUM
         MVI   REC+$OFF,C'+'
         MVC   REC+$VOL2(6),CAMWORK+6
*
         CLC   REC+$VOL1(6),REC+$VOL2
         BE    VOLCHK1
         MVI   REC+$VOL1+7,C'*'
VOLCHK1  DS    0H
*
         TPUT  REC,L'REC
         LA    R5,1+44(,R5)        POINT AT NEXT ENTRY
         LA    R7,6(,R7)           POINT AT NEXT VOLSER
         AP    DSNNUM,=P'1'
         BCT   R4,LOOP


References:


1.  Please see the LPALIST display program on CBT File 566.


