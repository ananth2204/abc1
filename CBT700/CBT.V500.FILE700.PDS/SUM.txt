/************************** REXX   ************************************/
/*                                                                    */
/*   COMMAND NAME: SUM                                                */
/*   WRITTEN BY:   JIM HAIRE                                          */
/*                                                                    */
/*   FORMAT:                                                          */
/*       THIS COMMAND WILL TOTAL ALL ITEMS STORED IN THE PROFILE.     */
/*       THESE ARE ITEMS THAT HAVE BEEN PLACED ON THE PROFILE BY      */
/*       THE "SNIP" COMMAND.                                          */
/*                                                                    */
/*                                                                    */
/*   DESCRIPTION:                                                     */
/*       THIS PROGRAM WILL TOTAL THE VALUES THAT YOU HAVE SNIPPED     */
/*       TO THE PROFILE WITH THE SNIP COMMAND.                        */
/*                                                                    */
/**********************************************************************/

ADDRESS ISPEXEC
"ISREDIT MACRO (FORMAT DECIMALS)"
UPPER FORMAT

"VGET (LINNBR) PROFILE"
IF RC = 0 THEN NOP
ELSE
    DO
      SAY 'THERE HAVE BEEN NO VALUES SNIPPED TO THE PROFILE'
      SIGNAL STOPIT
    END

CALL 0000_DETERMINE_AMOUNT_FORMAT

AT_LEAST_ONE_RECORD_SW = 'N'
TOTAL_AMOUNT = 0
DO I = 1 TO LINNBR
    CALL 1000_TOTAL_AMOUNTS
END

IF AT_LEAST_ONE_RECORD_SW = 'Y' THEN
    SAY 'THE TOTAL IS 'TOTAL_AMOUNT
ELSE
    SAY 'THERE WERE NO RECORDS TO BE TOTALED'

CALL 1300_ERASE_THE_PROFILE

STOPIT:
EXIT
/**/
/**/
/**/
/**/
/**/
0000_DETERMINE_AMOUNT_FORMAT:
/*--------------------------------------------------------------------*/
/*                                                                    */
/*   THIS PARAGRAPH WILL DETERMINE WHAT THE AMOUNT FORMAT IS GOING    */
/*   TO BE BASED ON THE FORMAT INFORMATION THE USER PROVIDES.         */
/*   THE FORMAT CAN BE EITHER DISPLAY, BINARY, OR PACKED.             */
/*                                                                    */
/*--------------------------------------------------------------------*/
SELECT
    WHEN FORMAT = 'N'                  THEN NOP
    WHEN FORMAT = 'B'                  THEN NOP
    WHEN FORMAT = 'P'                  THEN NOP
    WHEN FORMAT = 'DISPLAY'            THEN FORMAT = 'N'
    WHEN FORMAT = ' '                  THEN FORMAT = 'N'
    WHEN FORMAT = 'FORMAT'             THEN FORMAT = 'N'
    WHEN FORMAT = 'BINARY'             THEN FORMAT = 'B'
    WHEN FORMAT = 'COMP'               THEN FORMAT = 'B'
    WHEN FORMAT = 'COMPUTATIONAL'      THEN FORMAT = 'B'
    WHEN FORMAT = 'COMP-4'             THEN FORMAT = 'B'
    WHEN FORMAT = 'COMPUTATIONAL-4'    THEN FORMAT = 'B'
    WHEN FORMAT = 'PACKED'             THEN FORMAT = 'P'
    WHEN FORMAT = 'COMP-3'             THEN FORMAT = 'P'
    WHEN FORMAT = 'COMPUTATIONAL-3'    THEN FORMAT = 'P'
    WHEN FORMAT = 'PACKED-DECIMAL'     THEN FORMAT = 'P'
    OTHERWISE
        ADDRESS ISPEXEC
        RC = 44
        ZEDSMSG = 'PF1 FOR ERROR'
        ZEDLMSG = 'THE PARAMETER SUPPLIED WITH THE SUM COMMAND ',
                  'CAN BE THE WORD "DISPLAY", "BINARY", OR "PACKED".'
        'SETMSG MSG(ISRZ001)'
        EXIT
END

CALL 0001_HOW_MANY_DECIMAL_POSITIONS

RETURN
/**/
/**/
/**/
/**/
/**/
0001_HOW_MANY_DECIMAL_POSITIONS:
/**********************************************************************/
/*                                                                    */
/*    THIS PARAGRAPH ASKS A QUESTION ABOUT THE NUMBER OF DECIMAL      */
/*    POSITIONS IN THE NUMBER YOU ARE CONVERTING.                     */
/*                                                                    */
/**********************************************************************/
IF FORMAT = 'B' | FORMAT = 'P' THEN
    IF DECIMALS = '' THEN
        DO
          SAY 'HOW MANY IMPLIED DECIMAL POSITIONS ARE THERE IN THE',
              'NUMBERS YOU ARE ADDING?'
          PULL DECIMALS
        END

RETURN
/**/
/**/
/**/
/**/
/**/
1000_TOTAL_AMOUNTS:
/*--------------------------------------------------------------------*/
/*    THIS PARAGRAPH IS THE DRIVER FOR CALCULATING THE TOTAL AMOUNT.  */
/*--------------------------------------------------------------------*/
ADDRESS ISPEXEC
"VGET (SNIP"I") PROFILE"

IF RC = 0 THEN
    DO
      ADDRESS TSO

      INTERPRET "AMOUNT = SNIP"I

      CALL 1100_CONVERT_TO_PROPER_FORMAT
      IF VALID_NUMERIC = '1' THEN
          DO
            AT_LEAST_ONE_RECORD_SW = 'Y'
            TOTAL_AMOUNT = TOTAL_AMOUNT + AMOUNT
          END
    END

RETURN
/**/
/**/
/**/
/**/
/**/
1100_CONVERT_TO_PROPER_FORMAT:
/*--------------------------------------------------------------------*/
/*                                                                    */
/*    THIS PARAGRAPH WILL CONVERT THE AMOUNT TO AN AMOUNT REXX CAN    */
/*    HANDLE DEPENDING ON THE FORMAT PROVIDED BY THE USER.  IT WILL   */
/*    THEN BE ADDED TO THE AMOUNT TOTAL.                              */
/*                                                                    */
/*--------------------------------------------------------------------*/
AMOUNT = STRIP(AMOUNT)        /* REMOVES LEADING AND TRAILING BLANKS */

CN = 1
DO UNTIL CN > AMOUNT_LENGTH
    AMOUNT_LENGTH = LENGTH(AMOUNT)
    IF CN > AMOUNT_LENGTH THEN
        LEAVE
    ELSE
        DO
          CALL 1110_REMOVE_SPECIAL_CHARACTERS
          IF SPECIAL_CHARACTER = 'Y' THEN
              AMOUNT = DELSTR(AMOUNT,CN,1)
          ELSE
              CN = CN + 1
        END
END

AMOUNT = STRIP(AMOUNT,'B')
CALL 1120_CHECK_FOR_VALID_NUMERIC

IF VALID_NUMERIC = '1' THEN
    DO
      CALL @CONVERT AMOUNT FORMAT DECIMALS
      IF CONV_ERROR = 'Y' THEN SIGNAL STOPIT
      AMOUNT = WORD(RESULT,1)
      /*----------------------------------------------------------*/
      /*  IF AMOUNT EQUALS THE WORD RESULT, THEN BAD CONVERSION.  */
      /*----------------------------------------------------------*/
      IF AMOUNT = 'RESULT' THEN EXIT
    END
ELSE
    IF AMOUNT <= ' ' THEN NOP
    ELSE
        DO
          SAY 'THE DATA VALUE 'AMOUNT' DID NOT GET INCLUDED IN THE'
          SAY 'TOTAL BECAUSE IT IS AN INVALID NUMERIC VALUE.'
          SAY ' '
        END

RETURN
/**/
/**/
/**/
/**/
/**/
1110_REMOVE_SPECIAL_CHARACTERS:
/*--------------------------------------------------------------------*/
/*                                                                    */
/*    THIS PARAGRAPH WILL REMOVE DOLLAR SIGNS, COMMAS, AND PERCENT    */
/*    SIGNS FROM A NUMBER ON A REPORT.                                */
/*                                                                    */
/*--------------------------------------------------------------------*/
SPECIAL_CHARACTER = 'N'
SELECT
  WHEN SUBSTR(AMOUNT,CN,1) <= ' ' THEN SPECIAL_CHARACTER = 'Y'
  WHEN SUBSTR(AMOUNT,CN,1) = '$'  THEN SPECIAL_CHARACTER = 'Y'
  WHEN SUBSTR(AMOUNT,CN,1) = ','  THEN SPECIAL_CHARACTER = 'Y'
  WHEN SUBSTR(AMOUNT,CN,1) = '%'  THEN SPECIAL_CHARACTER = 'Y'
  OTHERWISE
      NOP
END

RETURN
/**/
/**/
/**/
/**/
/**/
1120_CHECK_FOR_VALID_NUMERIC:
/*--------------------------------------------------------------------*/
/*                                                                    */
/*    THIS PARAGRAPH WILL CHECK THAT ONCE ALL SPECIAL CHARACTERS      */
/*    HAVE BEEN REMOVED THAT THE NUMBER IS A VALID NUMBER.            */
/*    A '1' WILL BE RETURNED IF THE AMOUNT IS A VALID NUMBER.         */
/*                                                                    */
/*--------------------------------------------------------------------*/
VALID_NUMERIC = DATATYPE(AMOUNT,'N')

RETURN
/**/
/**/
/**/
/**/
/**/
1300_ERASE_THE_PROFILE:
/*--------------------------------------------------------------------*/
/*                                                                    */
/*    THIS PARAGRAPH WILL ERASE THE PROFILE FOR ALL SNIPPED FIELDS.   */
/*                                                                    */
/*--------------------------------------------------------------------*/
DO J = 1 TO LINNBR
    "VERASE (SNIP"J") PROFILE"
END

"VERASE (LINNBR) PROFILE"

RETURN
/**/
/**/
/**/
/**/
/**/
