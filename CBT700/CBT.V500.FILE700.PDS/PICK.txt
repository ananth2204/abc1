/***************************** REXX  **********************************/
/*                                                                    */
/*    COMMAND NAME: PICK <MOVE>                                       */
/*    WRITTEN BY:   JIM HAIRE                                         */
/*                                                                    */
/*    FORMAT:                                                         */
/*        ENTER "PICK" ON THE COMMAND LINE.                           */
/*        PLACE THE CURSOR ON ANY WORD YOU WISH TO PUT TO THE         */
/*        PROFILE.  THE CURSOR CAN BE ON ANY PART OF THE WORD.        */
/*                                                                    */
/*        THIS WILL PUT THE WORD ON THE PROFILE WHERE IT CAN BE       */
/*        "GLUED" AT A LATER TIME.                                    */
/*                                                                    */
/*        YOU CAN USE THE "MOVE" PARAMETER TO ERASE THE WORD FROM     */
/*        ITS CURRENT POSITION.                                       */
/*                                                                    */
/*                                                                    */
/*    ARGUMENT LIST:                                                  */
/*        <MOVE> - IF YOU TYPE "M" OR "MOVE" AFTER THE "PICK"         */
/*                 COMMAND, THE WORD THAT YOU "PICK" WILL BE          */
/*                 ERASED.                                            */
/*                                                                    */
/*                                                                    */
/*    DESCRIPTION:                                                    */
/*       THIS EDIT MACRO WILL ALLOW YOU TO TAKE A WORD FROM A MEMBER  */
/*       BEING EDITED.  YOU CAN THEN ATTACH THIS WORD INTO SOME       */
/*       OTHER MEMBER BEING EDITED.                                   */
/*                                                                    */
/**********************************************************************/
ADDRESS ISPEXEC
"ISREDIT MACRO (MOVESW)"

CALL 00000_INITIALIZATION
CALL 10000_DETERMINE_CURSOR_POSITION
CALL 20000_LOAD_WORDS_TO_STEM
CALL 30000_DETERMINE_WORD_CHOSEN
CALL 40000_WRITE_WORD_TO_PROFILE

UPPER MOVESW
IF MOVESW = 'MOVE' | MOVESW = 'M' THEN
    DO
      CALL 50000_REMOVE_WORD
      "ISREDIT CURSOR = &PICKLIN &PICKCOL"
    END
ELSE
    "ISREDIT CURSOR = &PICKLIN &PICKCOL"

EXIT
/**/
/**/
/**/
/**/
/**/
00000_INITIALIZATION:
/*--------------------------------------------------------------------*/
/*    THIS PARAGRAPH WILL INITIALIZE ANY VALUES OR STEMS THAT NEED    */
/*    TO BE INITIALIZED.                                              */
/*--------------------------------------------------------------------*/
DROP DATA_WORD. START_POS. END_POS. DATA_LENGTH.

RETURN
/**/
/**/
/**/
/**/
/**/
10000_DETERMINE_CURSOR_POSITION:
/*--------------------------------------------------------------------*/
/*    THIS PARAGRAPH WILL DETERMINE WHERE THE CURSOR HAS BEEN         */
/*    PLACED AND WILL DETERMINE WHAT DATA IS ON THE LINE WHERE THE    */
/*    CURSOR HAS BEEN PLACED.                                         */
/*--------------------------------------------------------------------*/
ADDRESS ISPEXEC
CALL @CURSOR
PICKLIN = WORD(RESULT,1)
PICKCOL = WORD(RESULT,2)

IF PICKLIN > 0 & PICKCOL > 0 THEN
    DO
      CALL @DATA
      DATALINE = RESULT
      CALL 11000_CHECK_CURSOR_ON_WORD
    END
ELSE
    DO
      ADDRESS ISPEXEC
      ZEDSMSG = 'PF1 FOR ERROR'
      ZEDLMSG = 'CURSOR MUST BE PLACED ON A WORD IN THE FILE '
      ZEDLMSG = ZEDLMSG || 'BEING EDITED.'
      'SETMSG MSG(ISRZ001)'
      EXIT 12
    END

RETURN
/**/
/**/
/**/
/**/
/**/
11000_CHECK_CURSOR_ON_WORD:
/*--------------------------------------------------------------------*/
/*    THIS PARAGRAPH WILL CHECK THAT THE CURSOR HAS BEEN PLACED ON A  */
/*    WORD AND NOT ON A SPACE.                                        */
/*--------------------------------------------------------------------*/
CURSOR_CHARACTER = SUBSTR(DATALINE,PICKCOL,1)
IF CURSOR_CHARACTER = '' | CURSOR_CHARACTER = ' ' THEN
    DO
      ADDRESS ISPEXEC
      ZEDSMSG = 'PF1 FOR ERROR'
      ZEDLMSG = 'CURSOR MUST BE PLACED ON A WORD IN THE FILE '
      ZEDLMSG = ZEDLMSG || 'BEING EDITED.  IT CANNOT BE PLACED '
      ZEDLMSG = ZEDLMSG || 'ON A SPACE OR LOW VALUES.'
      'SETMSG MSG(ISRZ001)'
      EXIT 12
    END

RETURN
/**/
/**/
/**/
/**/
/**/
20000_LOAD_WORDS_TO_STEM:
/*--------------------------------------------------------------------*/
/*    THIS PARAGRAPH WILL LOAD THE WORDS FROM THE DATA LINE TO A STEM */
/*    ALONG WITH THEIR BEGINNING AND ENDING POSITIONS.                */
/*--------------------------------------------------------------------*/
NUMBER_OF_WORDS = WORDS(DATALINE)

DO I = 1 TO NUMBER_OF_WORDS
    /*-------------------------------------------------------------*/
    /*-  FIND THE BEGINNING POSITION OF EACH WORD IN THE STRING.  -*/
    /*-------------------------------------------------------------*/
    DATA_WORD.I   = WORD(DATALINE,I)
    DATA_LENGTH.I = LENGTH(DATA_WORD.I)
    START_POS.I   = WORDINDEX(DATALINE,I)
    END_POS.I     = START_POS.I + DATA_LENGTH.I - 1
END

RETURN
/**/
/**/
/**/
/**/
/**/
30000_DETERMINE_WORD_CHOSEN:
/*--------------------------------------------------------------------*/
/*    THIS PARAGRAPH WILL DETERMINE THE WORD CHOSEN BASED ON CURSOR   */
/*    POSITION.                                                       */
/*--------------------------------------------------------------------*/
WORD_PICKED = 0
DO I = 1 TO NUMBER_OF_WORDS
    /*-------------------------------------------------------------*/
    /*-  FIND OUT WHICH WORD IN THE STRING THE CURSOR IS IN.      -*/
    /*-  STORE THE NUMBER OF THE WORD IN WORD_PICKED.             -*/
    /*-------------------------------------------------------------*/
    IF PICKCOL >= START_POS.I & PICKCOL <= END_POS.I THEN
    DO
      WORD_PICKED = I
      I = NUMBER_OF_WORDS
    END
END

/*--------------------------------------------------------------------*/
/*    IF THE WORD HAS NOT BEEN DETERMINED AFTER ALL PROCESSING HAS    */
/*    BEEN PERFORMED, CAUSE AN ERROR TO DISPLAY.                      */
/*--------------------------------------------------------------------*/
IF WORD_PICKED = 0 THEN
    DO
      ADDRESS ISPEXEC
      ZEDSMSG = 'PF1 FOR ERROR'
      ZEDLMSG = 'THE PROGRAM COULD NOT DETERMINE WHICH WORD WAS '
      ZEDLMSG = ZEDLMSG || 'BEING CHOSEN.  LOGIC ERROR IN THE '
      ZEDLMSG = ZEDLMSG || 'PROGRAM.'
      'SETMSG MSG(ISRZ001)'
      EXIT 12
    END

RETURN
/**/
/**/
/**/
/**/
/**/
40000_WRITE_WORD_TO_PROFILE:
/*--------------------------------------------------------------------*/
/*    THIS PARAGRAPH WILL PUT THE WORD ON THE PROFILE.                */
/*--------------------------------------------------------------------*/
"VGET (LINNBR) PROFILE"
IF RC = 0 THEN
    LINNBR = LINNBR + 1
ELSE
    LINNBR = 1
INTERPRET "SNIP"LINNBR" = DATA_WORD.WORD_PICKED"

"VPUT (LINNBR) PROFILE"
"VPUT (SNIP"LINNBR") PROFILE"

RETURN
/**/
/**/
/**/
/**/
/**/
50000_REMOVE_WORD:
/*--------------------------------------------------------------------*/
/*    THIS PARAGRAPH WILL REMOVE THE WORD FROM THE MEMBER BEING       */
/*    EDITED IF THAT ARGUMENT HAS BEEN PROVIDED.                      */
/*--------------------------------------------------------------------*/
/**********************************/
/*  CREATE A STRING OF SPACES AS  */
/*  LONG AS THE LENGTH OF THE     */
/*  WORD BEING "PICKED".          */
/**********************************/
"ISREDIT (OLDDATA) = LINE &PICKLIN"
OLDSTRG = SUBSTR(OLDDATA,START_POS.WORD_PICKED,DATA_LENGTH.WORD_PICKED)
OLDSTRG = "'"OLDSTRG"'"
NEWSTRG = COPIES(' ',DATA_LENGTH.WORD_PICKED)
NEWSTRG = "'"NEWSTRG"'"
WRDSTART = START_POS.WORD_PICKED
"ISREDIT CURSOR = &PICKLIN &WRDSTART"
"ISREDIT CHANGE &OLDSTRG &NEWSTRG"


RETURN
/**/
/**/
/**/
/**/
/**/
