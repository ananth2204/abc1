//SOMITCWI JOB (????????,????,1439,9999),REPLYM-SOMITCW,
//             CLASS=?,MSGCLASS=?,
//             NOTIFY=SOMITCW,COND=(0,NE)
//REPLYM  EXEC HLASMCL,PARM.C=(NODECK,OBJECT,RENT),
//             PARM.L='REFR,RENT,REUS,AC=1,LIST,MAP'
//SYSLIB   DD  DSN=SYS1.MACLIB,DISP=SHR,UNIT=SYSDA,VOL=SER=??????
//         DD  DSN=SYS1.MODGEN,DISP=SHR,UNIT=SYSDA,VOL=SER=??????
PASS     TITLE 'REPLYM - Reply ''M'' to unload wrong tapes mounted'
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
*   Title: REPLYM ( originally was REPLYU )
*
*   Author: Murphy from CBT325 tape file 25 member AUTOWTOR
*
*   Re-written by: I ain't saying.
*
*   Date re-written:   5-1-98
*
*   Change Log:
*
* xx-xx-xx x
*  x.x.x.  x
*
*   Remarks:
*
*        This MPF exit to automatically give reply of 'M' to messages
*   IEC514D, IEC534D, and IEC701D.
*
*      Could not read label on output tape:
*  *IEC514D DCK OR LBL ERR 0523,ABC123,JOBNAME,STEPNAME,OUTPUT.DSNAME
*  *08 IEC514D REPLY 'M'-UNLOAD OR 'A'-ABEND
*
*
*      Wrong output vol ser for 3480 ( wrong label type for others ):
*  *IEC534D A 0180,ABC001,SL,JOBNAME,JOBSTEP,PROCSTEP
*  *01 IEC534D REPLY 'U'-USE OR 'M'-UNLOAD
*
*      Tape being initialized ( IEHINITT ):
*  * IEC701D M cua,VOLUME TO BE LABELED volser
*
*  ( Message IEC507D has it's own exit because of special processing )
*
*   The tricky thing is that messages IEC514D and IEC534D are
*   really two messages each.
*   The first message gives the tape volume serial number and the
*   second message asks the operator to re-label and write over or
*   not to re-label and not write over wrong vol ser tape.
*   The first message is ignored.  'M' is replied to the second.
* noIEC514D,SUP(NO),USEREXIT(REPLYM)no Reply 'M', DCK or label error
* noIEC534D,SUP(NO),USEREXIT(REPLYM)no Reply 'M', wrong vol ser mounted
* noIEC701D,SUP(NO),USEREXIT(REPLYM)no Reply 'M', to initialize a tape
*   IEC704A,SUP(NO),USEREXIT(REPLYM)   Reply 'M' to unload NL tapes
*   goes in the MPFLSTxx member of SYS1.PARMLIB, then use the
*   SET MPF=XX console command.
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
REPLYM   CSECT ,
REPLYM   AMODE 31
REPLYM   RMODE ANY
         LCLA  &N                 Declare arithmetic local variable
&N       SETA  0                  Change &N from zero to zero
.REGS    ANOP  ,
         AIF   (&N GT 15).FINREGS Only define 16 registers
R&N      EQU   &N                 A register equate
&N       SETA  &N+1               Add one for mthe next register number
         AGO   .REGS              Go back to generate the next equate
.FINREGS ANOP  ,
         SAVE  (14,12),,REPLYM.&SYSDATE..&SYSTIME  Save caller's regs.
         LR    R12,R15            Load address of entry point
         USING REPLYM,R12         Give assembler program's base reg.
         L     R11,0(,R1)         Load the address of the CTXT
         USING CTXT,R11           Addressing CTXT
         L     R10,CTXTTXPJ       Load address of major line text
         USING CTXTATTR,R10       Addressing message attributes & text
         CLC   0(2,R11),CT        See if a real CTXT is found
         BNE   RETURN             If not a CTXT, get out quick
         CLI   CTXTRPID,C'0'      See if a reply id number is present
         BL    RETURN             No reply id, go exit this exit
         GETMAIN RU,LV=WORKLEN,LOC=BELOW,SP=229  Get MYDSECT storage
         LR    R9,R1              Address of work area for SVC 34 & WTO
         USING MYDSECT,R9         Addressability to my DSECT
         MVC   CMD(15),REPLYCMD   Move model command to SVC 34 buffer
         MVC   CMDNUM,CTXTRPID    Insert reply id number
         SLR   R0,R0              Set SVC 34 full authorization
         MGCR  CMD                Issue SVC 34 with cmd address in R1
         MVC   WTOEXEC,WTOLIST    Prime Write-To-Operator message buff.
         MVC   WTOEXEC+38(2),CTXTRPID  Put replyid in WTO message
         WTO   MF=(E,WTOEXEC)     Send message to the operator
         FREEMAIN RU,LV=WORKLEN,A=(R9),SP=229  Release MYDSECT area
         DROP  R9,R10,R11
RETURN   DS    0H
         SLR   R15,R15            Set a return code of zero
         RETURN (14,12),RC=(15)   Return to caller, return code zero
*
CT       DC    CL2'CT'            Constant for compare
REPLYCMD DC    AL2(15),AL2(0),CL11'REPLY XX,M '  Model reply command
WTOLIST  WTO 'MPF exit ''REPLYM'' replied ''M'' to *XX',ROUTCDE=11,MF=L
CNCLMSG  EQU   *-WTOLIST
*
MYDSECT  DSECT ,
CMD      DS    XL2                Length of the command with header
         DS    XL2                always binary zeroes
         DS    CL6                Will be 'REPLY '
CMDNUM   DS    CL2                Will be the reply id number
         DS    CL3                Will be ',M '
WTOEXEC  DS    CL(CNCLMSG)
WORKLEN  EQU   *-MYDSECT          Length of my DSECT
*
CTXT     DSECT ,
         DS    CL8
CTXTTXPJ DS    CL4                Pointer to major line text
CTXTTXPN DS    CL4                Pointer to minor line text
         DS    CL8
CTXTRPID DS    CL2                Reply id
CTXTATTR DSECT ,
         DS    CL6
CTXTTMSG DS    CL126
*
         END   ,
//L.SYSLMOD DD DSN=????.LOAD????,DISP=SHR,SPACE=,
//             UNIT=SYSDA,VOL=SER=??????
 NAME REPLYM
