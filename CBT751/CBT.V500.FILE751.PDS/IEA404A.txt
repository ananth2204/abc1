//SOMITCWI JOB (????????,????,1439,9999,9999),IEA404A-SOMITCW,
//             CLASS=?,MSGCLASS=?,
//             NOTIFY=SOMITCW,COND=(0,NE)
//IEA404A  EXEC HLASMCL,PARM.C=(NODECK,OBJECT,RENT),
//             PARM.L='REFR,RENT,REUS,AC=1,LIST,MAP'
//SYSLIB   DD  DISP=SHR,DSN=SYS1.MACLIB    ,UNIT=SYSDA,VOL=SER=??????
//         DD  DISP=SHR,DSN=SYS1.MODGEN    ,UNIT=SYSDA,VOL=SER=??????
PASS     TITLE 'IEA404A - Clear console buffers if full'
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
*   Title: IEA404A
*
*   Author: I ain't saying.
*
*   Date-written: 2001-07-12
*
*   Change Log:
*
* xx-xx-xx xxxxxxx
*  x.x.x.
*
*
*   Remarks:
*
*    This MPF routine intercepts message IEA404A and issues
*  operator command 'K Q,R=HC,L=1' whenever the message occurs.
*  The control command will route all messages pending for console
*  number 1 to go to hardcpy on disk and free the buffers.
*  A potential problem is that tape mounts messages and WTOR
*  messages may also be rerouted, so the operator could miss them.
*  To activate this exit:
*    link to a Link-List data set
*    issue operator command: F LLA,REFRESH
*    code the following line in member MPFLST00 of SYS1.PARMLIB:
*    IEA404A,SUP(NO),USEREXIT(IEA404A)
*    and issue operator command: SET MPF=00
*
*    SET MPF=NO will disable MPF if needed due to errors.
*
*   Sample message:
*
* *IEA404A SEVERE WTO BUFFER SHORTAGE - 100% FULL
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
IEA404A  CSECT ,
IEA404A  AMODE 31
IEA404A  RMODE ANY
         SAVE  (14,12),,IEA404A.&SYSDATE..&SYSTIME  Save caller's regs.
         LR    R12,R15            Load address of entry point
         USING IEA404A,R12        Give assembler program's base reg.
         L     R11,0(,R1)         Load the address of the CTXT
         USING CTXT,R11           Addressing CTXT
         L     R10,CTXTTXPJ       Load address of major line text
         USING CTXTATTR,R10       Addressing message attributes & text
         CLC   0(2,R11),CT        See if a real CTXT is found
         BNE   RETURN             If not a CTXT, get out quick
*
*        LA    R9,CTXTTMSG+8      Start hunt for data after msgid
* Label CTXTTMSG has:
* IEA404A SEVERE WTO BUFFER SHORTAGE - 100% FULL
*
         DROP  R10,R11            Don't need DSECT registers anymore
         SLR   R0,R0              Set SVC 34 full authorization
         MGCR  KCMD               Issue SVC 34 with cmd address in R1
RETURN   DS    0H
         SLR   R15,R15            Set a return code of zero
         RETURN (14,12),RC=(15)   Return to caller, return code zero
         DROP  R12                Don't need base register anymore
*
CT       DC    CL2'CT'            Constant for compare
KCMD     DC    AL2(40),AL2(0),CL36'CONTROL Q,R=HC,L=1 MPF EXIT IEA404A'
*                                  123456789 123456789 123456789 12345
*
         LTORG ,                  In case someone adds literals
*
*  Following mapped by SYS1.MODGEN(IEZVX100) ???????
CTXT     DSECT ,
         DS    CL8
CTXTTXPJ DS    CL4                Pointer to major line text
CTXTTXPN DS    CL4                Pointer to minor line text
         DS    CL8
CTXTRPID DS    CL2                Reply id, if any
CTXTATTR DSECT ,
         DS    CL6
CTXTTMSG DS    CL126
*
         CVT   DSECT=YES    MVS's Communications Vector Table
*
R0       EQU   0
R1       EQU   1                  R
R2       EQU   2                    E
R3       EQU   3                      G
R4       EQU   4                        I
R5       EQU   5                          S
R6       EQU   6                            T
R7       EQU   7                              E
R8       EQU   8                                R
R9       EQU   9                  E
R10      EQU   10                   Q
R11      EQU   11                     U
R12      EQU   12                       A
R13      EQU   13                         T
R14      EQU   14                           E
R15      EQU   15                             S
         END   ,
//L.SYSLMOD DD DISP=SHR,DSN=????.LOAD????,SPACE= ,
//*            UNIT=SYSDA,VOL=SER=??????
 NAME IEA404A
