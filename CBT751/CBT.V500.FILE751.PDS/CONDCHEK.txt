//SOMITCWC JOB (????????,????,1439,9999),CONDCHEK-SOMITCW,
//         CLASS=?,MSGCLASS=?,
//         NOTIFY=SOMITCW,COND=(0,NE)
//ASMHCL   EXEC HLASMCL
//SYSLIB   DD  DSN=SYS1.MACLIB,DISP=SHR,UNIT=SYSDA,VOL=SER=------
//         DD  DSN=SYS1.AMODGEN,DISP=SHR,UNIT=SYSDA,VOL=SER=------
PASS     TITLE 'Utility to LINK other programs and check the Cond Code'
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
*      This program LINKs other program and checks the condition code
*    for zero.  If the returning condition is not zero, the step is
*    abended with the abend code of the return code.
*
*      The name of the program to be LINKed is supplied from the
*    PARM field of an EXEC statement.  An additional PARM may also
*    be specified to be passed to the program being LINKed.
*
*      The first eight characters of the PARM field is used for the
*    name of the program to be linked.  If any additional parameters
*    are placed in the PARM field, they are passed to the linked
*    program.  If no parm field is on the EXEC statement at all,
*    the default program of IDCAMS is LINKed.
*
*    Examples:
*
*      To LINK IDCAMS:
*    //IDCAMS   EXEC PGM=CONDCHEK,RD=R
*    //SYSPRINT DD  SYSOUT=*
*    //SYSIN    DD  DSN=P.CTLCARD(REPRO),DISP=SHR
*    //SYSUT1   DD  DSN=INPUT.TAPE,DISP=SHR,DCB=BUFNO=5
*    //SYSUT2   DD  DSN=COPY.OF.TAPE,DISP=(,KEEP),UNIT=TAPE,
*    //             DCB=(RECFM=FB,LRECL=100,BLKSIZE=32700)
*
*      To LINK IEBDG:
*    //IEBDG    EXEC PGM=CONDCHEK,PARM=IEBDG,RD=R
*    //SYSPRINT DD  SYSOUT=*
*     DSD OUTPUT=(SYSUT2)
*     CREATE QUANTITY=1500,FILL=X'00'
*    //SYSUT2   DD  DSN=NEW.BDAM.FILE,DISP=(,CATLG,DELETE),
*    //             UNIT=SYSDA,SPACE=(CYL,50,RLSE),DCB=(RECFM=F,
*    //             LRECL=19069,BLKSIZE=19069,BUFNO=3)
*
*      To LINK IEBISAM:
*    //IEBISAM  EXEC PGM=CONDCHEK,PARM='IEBISAM PRINTL',RD=R
*    //SYSPRINT DD  SYSOUT=*
*    //SYSUT1   DD  DSN=ISAM.OR.VSAM.KEYED.FILE,DISP=SHR
*    //SYSUT2   DD  SYSOUT=A,OUTLIM=500
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SPACE 2
CONDCHEK CSECT ,
         STM   R14,R12,12(R13)    Save caller's registers
         BALR  R12,R0             Set base register
         USING *,R12              Give system the base register
         ST    R13,SAVE+4         Save the address of the old save area
         LA    R0,SAVE            Load address of the new save area
         ST    R0,8(,R13)         Connect save area chain
         LR    R13,R0             Move in new save area
         L     R1,0(,R1)          Load the address of the PARM field
         LH    R11,0(,R1)         Load the length of the PARM
         LTR   R11,R11            See if the PARM has a length
         BZ    DEFAULT            LINK to the default program if zero
         SH    R11,EIGHT          See if the PARM is greater than eight
         BNP   MOVEPROG           No extra PARM to pass to LINKed pgm
         STH   R11,PARMSIZE       Save PARM size to pass to LINKed pgm
         BCTR  R11,R0             Subtract one for MVC length
         EX    R11,MVC1           Move PARM to be passed to LINKed pgm
         MVC   PGMNAME,2(R1)      Move program for LINK
*
LINK     DS    0H
         LINK  EPLOC=PGMNAME,PARAM=PARMSIZE,VL=1  LINK to program
         LTR   R10,R15            See if Condition/Return Code is zero
         BNZ   ABEND              Abend the program if Return not zero
*
         L     R13,SAVE+4         Restore the old register thirteen
         L     R14,12(,R13)       Restore the return address
         LM    R0,R12,20(R13)     Restore registers zero to twelve
         BR    R14                Return to caller
*
DEFAULT  DS    0H
         MVC   PGMNAME,IDCAMS     Use default program to LINK
         B     LINK               Go to LINK program
*
MOVEPROG DS    0H
         LA    R11,7(,R11)        Set PARM length to move program name
         EX    R11,MVC2           Move program name for LINK
         B     LINK               Go to LINK the program
*
ABEND    DS    0H
         WTO   'Program error, check listing',ROUTCDE=(1,11)
*                                 Tell the operator of the error
         LR    R1,R10             Load abend code for SVC 13
         SVC   13                 Abend this task, no dump
         SPACE 2
MVC1     MVC   PARMS(1),10(R1)    Move PARM for LINKed program
MVC2     MVC   PGMNAME(1),2(R1)   Move program name
         EJECT
SAVE     DC    18F'0'             Register save area
*
PGMNAME  DC    CL8' '             Program to be LINKed
IDCAMS   DC    CL8'IDCAMS '       Default program to LINK
*
EIGHT    DC    H'8'               Constant for subtraction
*
         CNOP  2,4                Align following DC and DS
PARMSIZE DC    X'0000'            Size of PARM to be passed
PARMS    DS    XL92               PARM field to be passed
         SPACE
R0       EQU   0                  Register equates
R1       EQU   1                  Register equates
R2       EQU   2                  Register equates
R3       EQU   3                  Register equates
R4       EQU   4                  Register equates
R5       EQU   5                  Register equates
R6       EQU   6                  Register equates
R7       EQU   7                  Register equates
R8       EQU   8                  Register equates
R9       EQU   9                  Register equates
R10      EQU   10                 Register equates
R11      EQU   11                 Register equates
R12      EQU   12                 Register equates
R13      EQU   13                 Register equates
R14      EQU   14                 Register equates
R15      EQU   15                 Register equates
         END   ,
//L.SYSLMOD DD DSN=????.LOAD????,DISP=SHR,SPACE=,
//             UNIT=SYSDA,VOL=SER=------
   NAME CONDCHEK
