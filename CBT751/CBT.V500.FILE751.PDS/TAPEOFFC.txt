        /* Rexx EXEC TAPEOFF to read the TAPEOFF ( tape offsite )
           report and create an MS-DOS .bat file and SCO UnixWare
           scripts.

           .bat files are never used.  There are better ways to
           scratch tapes.  DDNAME BAT should be dummied out.

           The .bat file is to create empty .aws tape images
           to replace scratched tapes.  DDNAME BAT should be
           dummied out.

           The .cp script is to copy tapes for off site data
           transmission.

           .bat file records look like:
       hetinit -d C01234.aws C01234 ?.?.?.?.
           with one record for each tape image.

          .cp has a header record that looks like:
       echo 'About to copy ????/???? tapes'; date

          .cp has a trailer record that looks like:
       echo 'All ????/???? tapes copied'; date

          .cp has two records for each tape image like:
       cp /tapeC/C01/C01234.aws /tapeA/offsite/C/C01234.aws
       echo 'C01234 copied from /tapeC/C01 to /tapeA/offsite/C'
                                                                 */


        "EXECIO * DISKR VOLS (FINIS STEM in."

        cp.   = ""
        cp.0  = 0
        c     = 1
        cp.c  = "echo 'About to copy ????/???? tapes'; date"
        bat.  = ""
        bat.0 = 0
        b     = 0

        DO i = 1 TO in.0

          /* see if position 1 blank */
          IF SUBSTR(in.i,1,1) = ' ' THEN DO
            /* See if position 2 non-blank */
            IF SUBSTR(in.i,2,1) <> ' ' THEN DO
              vol = SUBSTR(in.i,2,6)
              vo = SUBSTR(in.i,2,3)
              v = SUBSTR(in.i,2,1)
              c = c + 1
  cp.c = "cp /tape"v"/"vo"/"vol".aws /tapeA/offsite/"v"/"vol".aws"
              c = c + 1
  cp.c = "echo '"vol "copied from /tape"v"/"vo" to /tapeA/offsite/"v"'"
              b = b + 1
              bat.b = 'hetinit -d' vol'.aws' vol '?.?.?.?.'
            END
          END
        END

          c = c + 1
          cp.c  = "echo 'All ????/???? tapes copied'; date"
          cp.0  = c
        "EXECIO * DISKW CP (FINIS STEM cp."
          bat.0  = b
        "EXECIO * DISKW BAT (FINIS STEM bat."
        EXIT
