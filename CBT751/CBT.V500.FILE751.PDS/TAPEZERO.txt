//SOMITCWT JOB (????????,????,1439,9999,9999),TAPEZERO-SOMITCW,
//             CLASS=?,MSGCLASS=?,
//             NOTIFY=SOMITCW,COND=(0,NE)
//HLASMCL  EXEC HLASMCL,PARM.C=(NORENT,OBJECT,NODECK,TERM),
//             PARM.L=NORENT
//SYSLIB   DD  DISP=SHR,DSN=SYS1.MACLIB
PASS TITLE 'TAPEZERO - Clears expired tapes to save disk space'
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
*    This program reads SYSIN records and uses the first
*  six bytes for a vol ser of a tape to erase.
*
*    Input SYSIN must have a valid vol ser in
*  columns 1 through 6 or this program will abend.
*
*     Two sample JCLs:
*
* //xxxxxxxT JOB (xxxxxxxx,xxxx,1439,9999,9999),TAPEZERJ-xxxxxxxx,
* //         CLASS=x,MSGCLASS=x,
* //         NOTIFY=SOMITCW,COND=(0,NE)
* //TAPEZERO EXEC PGM=TAPEZERO
* A00482 SYSIN is forced to RECFM=FB and needs a LRECL 6 or greater.
* A00483 Only the first six bytes of each record are used.
* A00486 Columns 7 through the end of record can be comments.
* //TAPE     DD  UNIT=(TAPE,,DEFER)
* //* RECFM=FB,LRECL=20,BLKSIZE=20 Forced by program
* //SYSUDUMP DD  SYSOUT=*
*
* //xxxxxxxT JOB (xxxxxxxx,xxxx,1439,9999,9999),TAPEZERJ-xxxxxxxx,
* //         CLASS=x,MSGCLASS=x,
* //         NOTIFY=SOMITCW,COND=(0,NE)
* //*
* //TAPESTAT EXEC PGM=TAPESTAT
* //STEPLIB  DD  DISP=SHR,DSN=xxxx.APF.LOADxxxx
* C00000-C02999
* //TAPESTAT DD  UNIT=(TAPE,32,DEFER)   Tie up all tape drives
* //STATS    DD  DISP=(,CATLG,DELETE),DSN=SYSx.TAPES.STATS(+1),
* //             UNIT=SYSDA,SPACE=(TRK,(15,15),RLSE),
* //             DCB=$,RECFM=FB,LRECL=53
* //SYSUDUMP DD  SYSOUT=*
* //LOCK     DD  DISP=OLD,DSN='TAPESTAT is running',VOL=REF=$
* //*
* //SORT    EXEC PGM=SORT
* //SYSOUT   DD  SYSOUT=*
*  SORT FIELDS=COPY
*  INCLUDE COND=(8,4,CH,EQ,C'Expi')  Copy expired tape records
* //SORTIN   DD  DISP=SHR,DSN=SYSx.TAPES.STATS(+1)
* //SORTOUT  DD  UNIT=SYSDA,SPACE=(TRK,(15,15)),DISP=(,PASS)
* //*
* //TAPEZERO EXEC PGM=TAPEZERO
* //SYSIN    DD  DISP=(SHR,DELETE),DSN=*.SORT.SORTOUT
* //TAPE     DD  UNIT=(TAPE,,DEFER)
* //* RECFM=FB,LRECL=20,BLKSIZE=20 Forced by program
* //SYSUDUMP DD  SYSOUT=*
* //LOCK     DD  DISP=OLD,DSN='TAPEZERO is running',VOL=REF=$
* //LOCK     DD  DISP=SHR,DSN='DAYBKUP is running',VOL=REF=$
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SPACE 1
TAPEZERO CSECT ,
TAPEZERO AMODE 24                 Addressing mode of 24 for GET MACRO
TAPEZERO RMODE 24                 Residency mode of 24 because of AMODE
         USING *,R15              Set temporary base register
         SAVE  (14,12),,'TAPEZERO.&SYSDATE..&SYSTIME' Save Registers
         DROP  R15                Drop the temporary base register
         LR    R12,R15            Load the program's base register
         USING TAPEZERO,R12       Give the assembler the new base
         LA    R0,WORKSIZE        Load work area size for GETMAIN
         GETMAIN R,LV=(0)         Get storage for work space
         XC    0(WORKSIZE,R1),0(R1)  Clear the work area
         ST    R1,8(,R13)         Connect old and new save areas
         ST    R13,4(,R1)         Connect old and new save areas
         LR    R13,R1             Connect old and new save areas
         USING SAVEAREA,R13
* OPEN SYSIN
         OPEN  (SYSIN,INPUT)      Open SYSIN file
* Read tape JOB File Control Block
         RDJFCB TAPE              Read tape JFCB into storage
         USING THEJFCB,JFCB       Tell assembler where the JFCB is
         MVC   JFCBDSNM,DSNAME    Move 'EMPTY.TAPE ' to tape DSNAME
         MVI   JFCBNVOL,1         Number of vol sers in JFCB
*
GET      DS    0H
         GET   SYSIN              Get volume record
         LR    R11,R1             Save address of input volume record
         CLI   0(R11),X'40'       See if vol ser starts blank
         BNH   BADVOL             Volser starts blank or less, go abend
         TRT   1(5,R11),TABLE     See if volser ends with five numerics
         BNZ   BADVOL             Volser last 5 bytes not numeric,abend
         MVC   JFCBVOLS,0(R11)    Move vol ser to clear for OPENJ
         OPEN  (TAPE,OUTPUT),TYPE=J  OPEN the tape as OUTPUT to clear
         PUT   TAPE,OUTREC        Write 'TAPEZERO empty tape.'
         CLOSE TAPE               CLOSE the tape
         B     GET                Go do the next tape
*
* The end
EODAD    DS    0H
         CLOSE SYSIN              Close input SYSIN file
         LA    R0,WORKSIZE        Load work area size for FREEMAIN
         LR    R1,R13             Save work area location for FREEMAIN
         L     R13,4(,R13)        Restore caller's save area address
         FREEMAIN R,LV=(0),A=(1)  Free work area's storage
         RETURN (14,12),RC=(15)   Return to caller with FREEMAIN retcde
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
* Error routines
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
BADVOL   DS    0H
         WTO 'TAPEZERO - Received a bad SYSIN record',ROUTCDE=11
         WTO 'TAPEZERO - First six bytes are not a vol ser',ROUTCDE=11
         WTO 'TAPEZERO - First byte must not be blank',ROUTCDE=11
         WTO 'TAPEZERO - Last five bytes must be numeric',ROUTCDE=11
         WTO 'TAPEZERO - Bad record starts with:',ROUTCDE=11
         MVC  BADWTO+19(33),0(R11)  Put bad record in WTO message
BADWTO   WTO  'TAPEZERO - 123456789 123456789 123456789 123',ROUTCDE=11
         ABEND 123                Abend without a dump
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
* Constants
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
         DS    0D                 Align Constants area
*
DSNAME   DC    CL44'EMPTY.TAPE '  Data set name for cleared tapes
*
OUTREC   DC    CL20'TAPEZERO empty tape.'  Data written to tape
*
*
TABLE    DC    256X'1'            Table to check for numerics in volser
         ORG   TABLE+240          Back up location counter to numbers
         DC    10X'0'             Overlay numbers for TRT to not find
         ORG   ,                  Return location counter to normal
*
JFCB     DS    XL176              Area for JFCB for RDJFCB and OPENJ
*
SYSIN    DCB   DDNAME=SYSIN,DSORG=PS,MACRF=GL,RECFM=FB,EODAD=EODAD
*
TAPE     DCB   DDNAME=TAPE,DSORG=PS,MACRF=PM,RECFM=FB,LRECL=20,        C
               BLKSIZE=20,EXLST=EXLST
*
EXLST    DC    X'87',AL3(JFCB)    Where RDJFCB puts the JFCB
*
         EJECT ,
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
*      DSECTs Follow
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SPACE 1
SAVEAREA DSECT ,
         DS    9D                 Register save area
*
WORKSIZE EQU   *-SAVEAREA
*
THEJFCB  DSECT ,
         IEFJFCBN LIST=YES        Map the JOB File Control Block
*
*
TAPEZERO CSECT ,
         LTORG ,                  In case someone adds literals
         EJECT ,
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
*     REGISTER EQUATES
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SPACE
R0       EQU   0                  Register equate for register 0
R1       EQU   1                  Register equate for register 1
R2       EQU   2                  Register equate for register 2
R3       EQU   3                  Register equate for register 3
R4       EQU   4                  Register equate for register 4
R5       EQU   5                  Register equate for register 5
R6       EQU   6                  Register equate for register 6
R7       EQU   7                  Register equate for register 7
R8       EQU   8                  Register equate for register 8
R9       EQU   9                  Register equate for register 9
R10      EQU   10                 Register equate for register 10
R11      EQU   11                 Register equate for register 11
R12      EQU   12                 Register equate for register 12
R13      EQU   13                 Register equate for register 13
R14      EQU   14                 Register equate for register 14
R15      EQU   15                 Register equate for register 15
         END   ,                  The end of the program
//L.SYSLMOD DD DSN=????.LOAD????,DISP=SHR,SPACE=   ,
   NAME TAPEZERO
