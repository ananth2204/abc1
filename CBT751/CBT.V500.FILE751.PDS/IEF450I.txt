//SOMITCWI JOB (????????,????,1439,9999,9999),IEF450I-SOMITCW,
//             CLASS=?,MSGCLASS=?,
//             NOTIFY=SOMITCW,COND=(0,NE)
//IEF450I  EXEC HLASMCL,PARM.C=(TERM,NODECK,OBJECT,RENT),
//             PARM.L='REFR,RENT,REUS,AC=1,LIST,MAP'
//SYSLIB   DD  DISP=SHR,DSN=SYS1.MACLIB
//         DD  DISP=SHR,DSN=????.SORC????
PASS     TITLE 'IEF450I - MPF exit to E-Mail JOB abends'
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
*       When a JOB abends during nightly or week-end production,
*   this MPF exit will start a started-task to send E-Mail.
*
*   MPF exit name: IEF450I
*
*   Date-Written: 2007-06-15
*
*   Blame: somitcw@yahoo.com
*
*   Dependencies:
*   ????.PROCLIB(ERR450I)  for: S ERR450I,M='JOBNAME message'
*   ????.???????(ERR450I)  for: Rexx EXEC to issue XMITIP
*   SYS1.PARMLIB(MPFLST??) for: MPF list to call exit
*
*  123456789 123456789 123456789 123456789 123456789 123456789 12345678
* IEF450I SRRUNKPA SELECT RADSS - ABEND=S222 U0000 REASON=00000000
*
* If a JOB ( not TSU or STC ), Night or Week-end ( not week day )
* s err450i,m='J01234 SRRUNKPA SELECT RADSS - ABEND=S222 U0000
*    REASON=00000000'
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         PRINT ON,GEN,DATA
IEF450I  CSECT ,
IEF450I  AMODE 31
IEF450I  RMODE ANY
         SAVE  (14,12),,IEF450I.&SYSDATE..&SYSTIME
*        Saves registers and inserts program-name, date, and time
         LR    R12,R15            Load address of entry point
         USING IEF450I,R12        Give assembler the base register
         L     R11,PSAAOLD-PSA    Load ASCB current address
         USING ASCB,R11           Addressing for ASCB
         ICM   R11,B'1111',ASCBJBNI See if a JOB ( Not TSU,STC,other )
         BZ    RETURN             Not a JOB, get out
         L     R11,FLCCVT-PSA     Load Communication Vector Table addr
         USING CVT,R11            Addressing for CVT
         L     R11,CVTUSER        Load CVT user address
         USING XCVTUSER,R11       Addressing for CVT user area
         CLI   XCVTWHEN,C'D'      See if weekday daytime
         BE    RETURN             Day time on week day, get out
         L     R11,0(,R1)         Load the CTXT address
         USING CTXT,R11           Addressing for CTXT
         CLC   0(2,R11),CT        Insure that we have a CTXT
         BNE   RETURN             I'm lost, get out
         GETMAIN RU,LV=WORKL,LOC=BELOW,SP=229
         LR    R10,R1             Address of save and work area
         USING MYDSECT,R10        Addressing the save and work area
         L     R11,CTXTTXPJ       Load address of message text DSECT
         USING CTXTATTR,R11       Message attributes
*
* Label CTXTTMSG in DSECT CTXTATTR has:
* IEF450I job-name procstep job-step - ABEND=S222 U0000 REASON=00000000
*
         MVC   STARTCMD,STARTDUM  Prime MGCR PARM list with template
         MVC   WTOLIST,WTOLDUM    Prime WTO PARM list with template
         MVC   JOBMSG1,CTXTTMSG+8 Put error message in start command
         MVC   JOBMSG2,CTXTTMSG+8 Put error message in WTO
         DROP  R11                Don't need addr. of CTXTATTR anymore
*
         SLR   R0,R0              Clear MGCR authorization codes
         LA    R1,STARTCMD        Load address of start command
*        MGCR  (1)                Issue the start command
         SVC   34                 Issue the start command
         LA    R1,WTOLIST         Load address of WTO list
*        WTO   MF=(E,(1))         Issue "Issued: START" WTO
         SVC   35                 Issue "Issued: START" WTO
FREEMAIN DS    0H
         FREEMAIN RU,LV=WORKL,A=(R10),SP=229
         DROP  R10                Save area gone, tell assembler
RETURN   DS    0H
         SLR   R15,R15            Set a return code of zero
         RETURN (14,12),RC=(15)   Return to caller
*
         DROP  R12                Don't need base register anymore
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
*  Constants
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
CT       DC    CL2'CT'            Constant for compare
* Start to E-Mail
STARTDUM DC    AL2(83),AL2(0)     Start command
         DC    CL45'START ERR450I,M=''job-name procstep job-step -'
         DC    CL34' abend=s222 u0000 reason=00000000'''
STARTLEN EQU   *-STARTDUM         Length of start command
*
         DS    0F                 Insure some alignment        5
WTOLDUM  WTO   'MPF exit IEF450I - issued: START ERR450I,M=''job-name p+
               rocstep job-step - abend=s222 u0000 reason=00000000''', +
               ROUTCDE=11,MF=L
WTOLEN   EQU   *-WTOLDUM
*
         LTORG ,                  Put literal pool here
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
*  DSECTs
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
MYDSECT  DSECT ,
         DS    0D                 Insure double word boundary
STARTCMD DS    CL(STARTLEN)       Place to put start command
JOBMSG1  EQU   STARTCMD+21,61,C'C' Location for command error message
*
WTOLIST  DS    XL(WTOLEN)         WTO parameter list
JOBMSG2  EQU   WTOLIST+48,61,C'C' Location for WTO error message
WORKL    EQU   *-MYDSECT          Length of the DSECT
*
*        COPY  IEZVX100           Copy DSECT
CTXT     DSECT ,
         DS    CL8
CTXTTXPJ DS    CL4                Pointer to major line text
CTXTTXPN DS    CL4                Pointer to minor line text
*
* Many other including:
* CTXTJBNM DS    CL8  NAME OF JOB THAT ISSUED MESSAGE
* CTXTSYSN DS    CL8  NAME OF SYSTEM ON WHICH WTO ORIGINATED
*
CTXTATTR DSECT ,
CTXTTLEN DS    H                  Text length
CTXTTLMX DS    H                  Maximum length of text
CTXTTFLG DS    XL2                Message type flags
CTXTTMSG DS    CL126
*
* Following DSECT name is PSA
         IHAPSA DSECT=YES,LIST=YES  Prefixed Save Area
* PSATOLD  DC    A                Pointer to the home (current) TCB
* PSAAOLD  DC    A                Pointer to the home (current) ASCB
*
* Following DSECT name is CVT
         CVT   DSECT=YES,LIST=YES Communication Vector Table
* CVTUSER DC   A                  Points to XCVTUSER
*
* Following DSECT name is ASCB
         IHAASCB DSECT=YES,LIST=YES  Address Space Control Block
* ASCBJBNI DS    A -              Pointer to JOBname field for
* *                               initiated programs or zero
*
* Following DSECT name is TCB
*        IKJTCB DSECT=YES,LIST=YES  Task Control Block
* TCBJSCB    DS    0A
* TCBJSCBB   DS    AL3
*
* Following DSECT name is IEZJSCB
*        IEZJSCB ,                JOB Step Control Block
* JSCBSSIB   DS    A
*
* Following DSECT name is SSIB
*        IEFJSSIB ,               Sub-System Indentification block
* SSIBJBID DS    CL8              Job Identifier or Subsystem name
*
         COPY CVTUSER             Copy the CVTUSER area DSECT
* XCVTUSER DSECT ,
* XCVTUEYE DS    CL8              7 bytes of 'CVTUSER' and 1 byte X'01'
* XCVTUVOL DS    XL6              VOLSER from message used by IEC507D
* XCVTWHEN DS    CL1              D-N-W Day, Night, Week-end indicator
*          DS    XL1              Space available for expansion
* XCVTUTIM DS    XL6              CYYDDDHHMMSS Century, Year,Day, Hour,
* *                               Minute, and Second, used by IOS000I
*          DS    XL2              Space available for expansion
* XCVTPSWD DS    F                ACF2 LGNIXIT anchor, used by SAVEPSWD
* XCVTJOUR DS    F                DFHJC4508 Journal PRIM or SECO
* XCVTTAPE DS    XL4              TAPE, TAPESAVE, TAPESYNC, & IGXMSGEX
* * TAPESKIP DS    CL6            TAPESKIP was old name for XCVTSKIP
* XCVTSKIP DS    CL6              IEC512I tape vol ser to ignore
*          DS    XL22             Space available for expansion
* XCVTUSIZ EQU   *-XCVTUSER       Length of area pointed to by CVTUSER
*
R0       EQU   0
R1       EQU   1                  R
R2       EQU   2                    e
R3       EQU   3                      g
R4       EQU   4                        i
R5       EQU   5                          s
R6       EQU   6                            t
R7       EQU   7                              e
R8       EQU   8                                r
R9       EQU   9                  E
R10      EQU   10                   q
R11      EQU   11                     u
R12      EQU   12                       a
R13      EQU   13                         t
R14      EQU   14                           e
R15      EQU   15                             s
         END   ,
//L.SYSLMOD  DD  DSN=????.LOAD????,DISP=SHR,SPACE=
 NAME IEF450I
