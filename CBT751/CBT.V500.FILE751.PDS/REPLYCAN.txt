//SOMITCWR JOB (????????,????,1,1),REPLYCAN-SOMITCW,
//             CLASS=?,MSGCLASS=?,
//             NOTIFY=SOMITCW,COND=(0,NE)
//REPLYCAN EXEC HLASMCL,PARM.C=(NODECK,OBJECT,RENT),
//             PARM.L='REFR,RENT,REUS,AC=1,LIST,MAP'
//SYSLIB   DD
//         DD  DSN=SYS1.MODGEN,DISP=SHR
REPLYCAN TITLE '* * * Automatic reply ''CANCEL'' to WTORs * * *'
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
* Title: REPLYCAN
*
* Original Author: Murphy from CBT325 Tape file 25 member AUTOWTOR
*
* Current Blame: SOMITCW@yahoo.com
*
* Date-Rewritten: 2005-10-27
*
* Remarks:
*
*     Automatically give reply of CANCEL to message IEF238D.
* IEF238D,SUP(NO),USEREXIT(REPLYCAN) needs to go in MPFLSTxx in
* SYS1.PARMLIB, then use SET MPF=xx whatever member you set it to.
*
*  IEF244I TAPEZERO TAPESTAT - UNABLE TO ALLOCATE 1 UNIT(S)
*          AT LEAST 1 OFFLINE UNIT(S) NEEDED.
*  IEF877E TAPEZERO NEEDS 1 UNIT(S)
*  FOR TAPESTAT TAPESTAT
*  FOR VOLUME SCRTCH-   1
*  OFFLINE, NOT ACCESSIBLE
*  0500-0501 05A0-05A1
*  :
*  IEF878I END OF IEF877E FOR TAPEZERO TAPESTAT TAPESTAT
* *49 IEF238D TAPEZERO - REPLY DEVICE NAME OR 'CANCEL'.
*  IEE600I REPLY TO 49 IS;CANCEL
*  IEF251I TAPEZERO JOB CANCELLED
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
REPLYCAN CSECT ,
REPLYCAN AMODE 31
REPLYCAN RMODE ANY
         SAVE  (14,12),,REPLYCAN.&SYSDATE..&SYSTIME
*        Saves registers and inserts program, date, and time
         LR    R12,R15            Load address of entry point
         USING REPLYCAN,R12
         LR    R9,R1
         L     R5,0(,R1)
         USING CTXT,R5            Addressing CTXT
         CLC   0(2,R5),=C'CT'
         BNE   RETURN
         GETMAIN RU,LV=WORKL,LOC=BELOW,SP=229
         LR    R3,R1              Address of work area/savearea
         USING SAVDSECT,R3        Addressability
         ST    R13,SAVEAREA+4     Store caller savearea address (HSA)
         ST    R3,8(,R13)         Add of program save in caller (LSA)
         LR    R13,R3             Address of savearea in R13
         L     R2,CTXTTXPJ
         USING CTXTATTR,R2        Msg attributes
         LA    R4,CTXTTMSG        Address of text area
         USING MSGTEXT,R4
         MVC   WTOEXEC,WTOLIST
         MVC   WTOEXEC+42(8),MSGID   Message ID 'IEF238D '
         XC    MGCRPL(MGCRLTH),MGCRPL  Clear parameter list
         MVC   MGCRTEXT(L'TXTINSRT),TXTINSRT Move in reply buffer
         MVC   REPLY,CTXTRPID     Insert reply id
         LA    R1,(MGCRTEXT-MGCRPL)+L'TXTINSRT  Get MGCRPL length
         STC   R1,MGCRLGTH        Save length in MGCRPL
         SR    R0,R0
         MGCR  MGCRPL
         DROP  R2,R4
         WTO   MF=(E,WTOEXEC)
FREERET  L     R13,4(,R13)        Address of calling program savearea
         FREEMAIN RU,LV=WORKL,A=(3),SP=229
RETURN   SR    R15,R15
         RETURN (14,12),RC=(15)
*
TXTINSRT DC    CL16'REPLY xx,CANCEL '  Reply
*               456789 123456789 123456789 '123456'789 123456789
WTOLIST  WTO   'MPF exit REPLYCAN Replied ''CANCEL'' to xxxxxxxx ',    C
               ROUTCDE=11,MF=L
CNCLMSG  EQU   *-WTOLIST
R0       EQU   0
R1       EQU   1                  R
R2       EQU   2                    e
R3       EQU   3                      g
R4       EQU   4                        i
R5       EQU   5                          s
R6       EQU   6                            t
R7       EQU   7                              e
R8       EQU   8                                r
R9       EQU   9                  E
R10      EQU   10                   q
R11      EQU   11                     u
R12      EQU   12                       a
R13      EQU   13                         t
R14      EQU   14                           e
R15      EQU   15                             s
SAVDSECT DSECT ,
SAVEAREA DS    18F
         DS    0F
WTOEXEC  DS    CL(CNCLMSG)
MGCR     IEZMGCR DSECT=NO
         ORG   MGCRTEXT
COMMAND  DS    CL6
REPLY    DS    CL2
REPLYMSG DS    CL8
         ORG   ,
WORKL    EQU   *-SAVEAREA
*
*        IEZVX100 DSECT=YES       CTXT DSECT
*
CTXT     DSECT ,
         DS    CL8
CTXTTXPJ DS    CL4                Pointer to major line text
CTXTTXPN DS    CL4                Pointer to minor line text
         DS    CL8
CTXTRPID DS    CL2                Reply id
*
CTXTATTR DSECT ,
         DS    CL6                Six byte attribute header before msg
CTXTTMSG DS    CL126
*
MSGTEXT  DSECT ,
MSGID    DS    CL8
JOBDATA  DS    CL8
         ORG   MSGTEXT
         DS    CL4
WTORID   DS    CL8
*
         END   ,
//L.SYSLMOD  DD  DSN=????.LOAD????,DISP=SHR,SPACE=
 NAME REPLYCAN
