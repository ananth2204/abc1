//SOMITCWI JOB (????????,????,1439,9999),LLAFRESH-SOMITCW,
//             CLASS=?,MSGCLASS=?,
//             NOTIFY=SOMITCW,COND=(0,NE)
//LLAFRESH EXEC HLASMCL,PARM.C=(NODECK,OBJECT,RENT),
//             PARM.L='REFR,RENT,REUS,AC=1,LIST,MAP'
//SYSLIB   DD  DSN=SYS1.MACLIB,DISP=SHR,UNIT=SYSDA,VOL=SER=??????
//         DD  DSN=SYS1.MODGEN,DISP=SHR,UNIT=SYSDA,VOL=SER=??????
LLAFRESH TITLE '* * * Automatic Refresh of LLA * * *'
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
*   Title: LLAFRESH
*
*   Author: I ain't saying.
*
*   Date-written: 1999-10-30
*
*   Change Log:
*
* xx-xx-xx xxxxxxx
*  x.x.x.
*
*   Remarks:
*
*     When a LinkList data set was compressed on one SYSRES that was
*   shared with another system, I didn't remember to refresh Library
*   LookAside on the second.  68 production JOBs abended trying to
*   start before the operator caught it in one and a half minutes.
*
*        This MPF routine intercepts message IEW4009I and issues
*   operator command 'MODIFY LLA,REFRESH' whenever the message occurs.
*   This exit will refresh LLA on the first abend.
*
*   To activate this exit:
*       Link to an LPA-List data set
*       ( MPF exits are normally in LinkList, but this
*       exit is used to recover from LinkList errors. )
*       If it were LinkList, issue operator command: F LLA,REFRESH
*       Since it is LPA-List, IPL to pick up the exit
*       Code the following line in member MPFLST00 of SYS1.PARMLIB:
*       IEW4009I,SUP(NO),USEREXIT(LLAFRESH)
*       and issue operator command: SET MPF=00
*
*       SET MPF=NO will disable all MPF exits, if needed due to errors.
*
*    Three samples of the messages:
*
*CSV031I LIBRARY ACCESS FAILED FOR MODULE QWIKREF1, RETURN CODE 24,
*REASON CODE 26080021, DDNAME *LNKLST*
*LLAFRESH FETCH FAILED FOR MODULE QWIKREF1 FROM DDNAME -LNKLST- BECAUSE
*OF AN I/O ERROR.
*IEW4005I FETCH FOR MODULE QWIKREF1 FROM DDNAME -LNKLST- FAILED BECAUSE
*IEWFETCH ISSUED RC 0F AND REASON 40
*
*CSV031I LIBRARY ACCESS FAILED FOR MODULE SAVEPSWD, RETURN CODE 24,
*REASON CODE 26080021, DDNAME *LNKLST*
*CSV028I ABEND106-28  JOBNAME=SAVEPSWD  STEPNAME=SAVEPSWD
*IEW4009I FETCH FAILED FOR MODULE SAVEPSWD FROM DDNAME -LNKLST- BECAUSE
*OF AN I/O ERROR.
*IEW4005I FETCH FOR MODULE SAVEPSWD FROM DDNAME -LNKLST- FAILED BECAUSE
*IEWFETCH ISSUED RC 0F AND REASON 40
*
*IEW4009I FETCH FAILED FOR MODULE WTOR     FROM DDNAME -LNKLST- BECAUSE
*OF AN I/O ERROR.
*IEW4005I FETCH FOR MODULE WTOR     FROM DDNAME -LNKLST- FAILED BECAUSE
*IEWFETCH ISSUED RC 0F AND REASON 40
*CSV031I LIBRARY ACCESS FAILED FOR MODULE WTOR    , RETURN CODE 24,
*REASON CODE 26080021, DDNAME *LNKLST*
*CSV028I ABEND106-28  JOBNAME=DLREG378  STEPNAME=GOSIS
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         MACRO
         REGS  ,                  MACRO to define register equates
         LCLA  REG                Define variable
&REG     SETA  0                  Insure that variable is Initialized
.LOOP    ANOP
         AIF   (&REG GT 15).END   Get out if all done
R&REG    EQU   &REG               Register equate
&REG     SETA  &REG+1             Bump register number
         AGO   .LOOP              Go back to do the next register
.END     ANOP
         MEND
LLAFRESH CSECT ,
LLAFRESH AMODE 31
LLAFRESH RMODE ANY
         SAVE  (14,12),,LLAFRESH.&SYSDATE..&SYSTIME Save caller's regs.
         LR    R12,R15            Load address of entry point
         USING LLAFRESH,R12       Give assembler program's base reg.
*
         SLR   R0,R0              Set SVC 34 full authorization
         MGCR  MODIFY             Issue SVC 34 with cmd address in R1
RETURN   DS    0H
         SLR   R15,R15            Set a return code of zero
         RETURN (14,12),RC=(15)   Return to caller, return code zero
         DROP  R12                Don't need base register anymore
*
MODIFY   DC  AL2(42),AL2(0),CL38'MODIFY LLA,REFRESH  MPF EXIT LLAFRESH'
*                                123456789 123456789 123456789 1234567
*
         LTORG ,                  In case someone adds literals
*
         REGS  ,                  Generate register equates
         END   ,
//L.SYSLMOD DD DSN=SYS1.LPALIB,DISP=SHR,SPACE=,
//             UNIT=SYSDA,VOL=SER=??????
 NAME LLAFRESH
