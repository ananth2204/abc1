//SOMITCWR JOB (????????,????,1,1),REPLYU-SOMITCW,
//             CLASS=?,MSGCLASS=?,
//             NOTIFY=SOMITCW,COND=(0,NE)
//REPLYU   EXEC HLASMCL,PARM.C=(NODECK,OBJECT,RENT),
//             PARM.L='REFR,RENT,REUS,AC=1,LIST,MAP'
//SYSLIB   DD
//         DD  DSN=SYS1.MODGEN,DISP=SHR
//         DD  DSN=SYS1.AMODGEN,DISP=SHR
REPLYU   TITLE '* * * AUTOMATIC REPLY TO WTORS * * *'
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
*   TITLE: REPLYU                                                     *
*                                                                     *
*   AUTHOR: MURPHY FROM CBT325 TAPE FILE 25 MEMBER AUTOWTOR           *
*                                                                     *
*   DATE-WRITTEN: 12/17/85                                            *
*                                                                     *
*   DATE-MODIFIED: 5-24-91                                            *
*                                                                     *
*   REMARKS:                                                          *
*                                                                     *
*        AUTOMATICALLY GIVE REPLY OF U TO MESSAGE ICK003D.            *
*   ICK003D,SUP(NO),USEREXIT(REPLYU) NEED TO GO IN MPF MEMBER IN      *
*   SYS1.PARMLIB, THEN USE SET MPF=XX WHATEVER MEMBER YOU SET IT TO.  *
*   SEE MPFLST03 FOR EXAMPLE.                                         *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
REPLYU   CSECT ,
REPLYU   AMODE 31
REPLYU   RMODE ANY
         SAVE  (14,12),,REPLYU.&SYSDATE..&SYSTIME
*        SAVES REGISTERS AND INSERTS PROGRAM, DATE, AND TIME
         LR    R12,R15            LOAD ADDRESS OF ENTRY POINT
         USING REPLYU,R12
         LR    R9,R1
         L     R5,0(,R1)
         USING CTXT,R5            ADDRESSING CTXT
         CLC   0(2,R5),=C'CT'
         BNE   RETURN
         GETMAIN RU,LV=WORKL,LOC=BELOW,SP=229
         LR    R3,R1              ADDRESS OF WORK AREA/SAVEAREA
         USING SAVDSECT,R3        ADDRESSABILITY
         ST    R13,SAVEAREA+4     STORE CALLER SAVEAREA ADDRESS (HSA)
         ST    R3,8(,R13)         ADD OF PROGRAM SAVE IN CALLER(LSA)
         LR    R13,R3             ADDRESS OF SAVEAREA IN R13
         L     R2,CTXTTXPJ
         USING CTXTATTR,R2        MSG ATTRIBUTES
         LA    R4,CTXTTMSG        ADDRESS OF TEXT AREA
         USING MSGTEXT,R4
         MVC   WTOEXEC,WTOLIST
         MVC   WTOEXEC+22(8),MSGID   MESSAGE NUMBER
         XC    MGCRPL(MGCRLTH),MGCRPL  CLEAR PARAMETER LIST
         MVC   MGCRTEXT(L'TXTINSRT),TXTINSRT MOVE IN REPLY BUFFER
         MVC   REPLY,CTXTRPID     INSERT REPLY ID
         LA    R1,(MGCRTEXT-MGCRPL)+L'TXTINSRT  GET MGCRPL LENGTH
         STC   R1,MGCRLGTH        SAVE LENGTH IN MGCRPL
         SR    R0,R0
         MGCR  MGCRPL
         DROP  R2,R4
         WTO   MF=(E,WTOEXEC)
FREERET  L     R13,4(,R13)        ADDRESS OF CALLING PROGRAM SAVEAREA
         FREEMAIN RU,LV=WORKL,A=(3),SP=229
RETURN   SR    R15,R15
         RETURN (14,12),RC=(15)
TXTINSRT DC    CL11'REPLY XX,U '  REPLY
WTOLIST  WTO   'REPLYU REPLIED TO MSGNUMB - AS SELECTED BY MPF',       X
               ROUTCDE=11,MF=L
CNCLMSG  EQU   *-WTOLIST
R0       EQU   0
R1       EQU   1                  R
R2       EQU   2                    E
R3       EQU   3                      G
R4       EQU   4                        I
R5       EQU   5                          S
R6       EQU   6                            T
R7       EQU   7                              E
R8       EQU   8                                R
R9       EQU   9                  E
R10      EQU   10                   Q
R11      EQU   11                     U
R12      EQU   12                       A
R13      EQU   13                         T
R14      EQU   14                           E
R15      EQU   15                             S
SAVDSECT DSECT ,
SAVEAREA DS    18F
         DS    0F
WTOEXEC  DS    CL(CNCLMSG)
MGCR     IEZMGCR DSECT=NO
         ORG   MGCRTEXT
COMMAND  DS    CL6
REPLY    DS    CL2
REPLYMSG DS    CL3
         ORG   ,
WORKL    EQU   *-SAVEAREA
MSGTEXT  DSECT ,
MSGID    DS    CL8
JOBDATA  DS    CL8
         ORG   MSGTEXT
         DS    CL4
WTORID   DS    CL8
CTXTATTR DSECT ,
         DS    CL6
CTXTTMSG DS    CL126
CTXT     DSECT ,
         DS    CL8
CTXTTXPJ DS    CL4          POINTER TO MAJOR LINE TEXT
CTXTTXPN DS    CL4          POINTER TO MINOR LINE TEXT
         DS    CL8
CTXTRPID DS    CL2          REPLY ID
         END   ,
//L.SYSLMOD  DD  DSN=????.LOAD????(REPLYU),DISP=OLD,SPACE=,
//             UNIT=SYSDA,VOL=SER=------
//CHECKCC  EXEC PGM=IEFBR14
