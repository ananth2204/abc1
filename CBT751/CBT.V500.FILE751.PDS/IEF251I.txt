//SOMITCWI JOB (????????,????,1439,9999,9999),IEF251I-SOMITCW,
//             CLASS=?,MSGCLASS=?,
//             NOTIFY=SOMITCW,COND=(0,NE)
//IEF251I  EXEC HLASMCL,PARM.C=(TERM,NODECK,OBJECT,RENT),
//             PARM.L='REFR,RENT,REUS,AC=1,LIST,MAP'
//SYSLIB   DD  DISP=SHR,DSN=SYS1.MACLIB
//         DD  DISP=SHR,DSN=SYS1.MODGEN
PASS     TITLE 'IEF251I - MPF exit to E-Mail device allocation failure'
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
*       When a JOB or started task fails due to device
*   allocation, this exit will E-Mail a note.
*
*   MPF exit name: IEF251I
*
*   Date-Written: 2007-06-13
*
*   Blame: somitcw@yahoo.com
*
*   Dependencies:
*   ????.PROCLIB(ERR251I)  for: S ERR251,JOB=????????
*   ????.???????(ERR251I)  for: Rexx EXEC to issue XMITIP
*   SYS1.PARMLIB(MPFLST??) for: MPF list to call exit
*
*  IEF251I jjobname {procstep} stepname - JOB CANCELLED (in SYSOUT)
*  Explanation:  During device allocation for a job,
*  one of the following occurred:
*  o   The operator entered a CANCEL command.
*  o   The operator replied CANCEL to message IEF238D.
*  o   A subsystem requested cancellation in response
*     to a request to allocate a subsystem data set.
*
*  IEF244I FRSDOWNL FRSDOWN STEPYY - UNABLE TO ALLOCATE 2 UNIT(S) 381
*          AT LEAST 2 OFFLINE UNIT(S) NEEDED.
*  IEF877E FRSDOWNL NEEDS 1 UNIT(S) 382
*  FOR FRSDOWN STEPYY FOCSTACK
*  FOR VOLUME SCRTCH-   1
*  OFFLINE
*  0100-013C 013E-0147 0210-022F
*  :
*  IEF878I END OF IEF877E FOR FRSDOWNL FRSDOWN STEPYY FOCSTACK
*  REPLY 53,CANCEL
*  MPF exit REPLYCAN Replied 'CANCEL' to IEF238D
* *53 IEF238D FRSDOWNL - REPLY DEVICE NAME OR 'CANCEL'.
*  IEE600I REPLY TO 53 IS;CANCEL
*  IEF251I FRSDOWNL JOB CANCELLED
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         PRINT ON,GEN,DATA
IEF251I  CSECT ,
IEF251I  AMODE 31
IEF251I  RMODE ANY
         SAVE  (14,12),,IEF251I.&SYSDATE..&SYSTIME
*        Saves registers and inserts program-name, date, and time
         LR    R12,R15            Load address of entry point
         USING IEF251I,R12        Give assembler the base register
         L     R11,0(,R1)         Load the CTXT address
         USING CTXT,R11           Addressing CTXT
         CLC   0(2,R11),CT        Insure that we have a CTXT
         BNE   RETURN             I'm lost, get out
         GETMAIN RU,LV=WORKL,LOC=BELOW,SP=229
         LR    R10,R1             Address of save and work area
         USING MYDSECT,R10        Addressing the save and work area
         L     R11,CTXTTXPJ       Load address of message text DSECT
         USING CTXTATTR,R11       Message attributes
*
* Label CTXTTMSG in DSECT CTXTATTR has:
* IEF251I SMFDUMPW JOB CANCELLED
*
         MVC   STARTCMD,STARTDUM  Prime MGCR PARM list with template
         MVC   WTOLIST,WTOLDUM    Prime WTO PARM list with template
         MVC   JOBNAME1,CTXTTMSG+8  Put JOB name in start command
         MVC   JOBNAME2,CTXTTMSG+8  Put JOB name in WTO
         DROP  R11                Don't need addr. of CTXTATTR anymore
*
         SLR   R0,R0              Clear MGCR authorization codes
         LA    R1,STARTCMD        Load address of start command
*        MGCR  (1)                Issue the start command
         SVC   34                 Issue the start command
         LA    R1,WTOLIST         Load address of WTO list
*        WTO   MF=(E,(1))         Issue "Issued: START" WTO
         SVC   35                 Issue "Issued: START" WTO
FREEMAIN DS    0H
         FREEMAIN RU,LV=WORKL,A=(R10),SP=229
         DROP  R10                Save area gone, tell assembler
RETURN   DS    0H
         SLR   R15,R15            Set a return code of zero
         RETURN (14,12),RC=(15)   Return to caller
*
         DROP  R12                Don't need base register anymore
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
*  Constants
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
CT       DC    CL2'CT'            Constant for compare
* Start to E-Mail  01     23       456789 123456789 123456
STARTDUM DC    AL2(30),AL2(0),CL26'START ERR251I,JOB=???????? '
STARTLEN EQU   *-STARTDUM         Length of start command
*
         DS    0F                 Insure some alignment
*               456789 123456789 123456789 123456789 123456789
WTOLDUM  WTO   'MPF exit IEF251I - Issued: START ERR251I,JOB=????????',C
               ROUTCDE=11,MF=L
WTOLEN   EQU   *-WTOLDUM
*
         LTORG ,                  Put literal pool here
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
*  DSECTs
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
MYDSECT  DSECT ,
         DS    0D                 Insure double word boundary
STARTCMD DS    CL(STARTLEN)       Place to put start command
JOBNAME1 EQU   STARTCMD+22,8,C'C' Name of JOB that failed
*
WTOLIST  DS    XL(WTOLEN)         WTO parameter list
JOBNAME2 EQU   WTOLIST+49,8,C'C'  Name of JOB that failed
WORKL    EQU   *-MYDSECT          Length of the DSECT
*
*        COPY  IEZVX100           Copy DSECT
CTXT     DSECT ,
         DS    CL8
CTXTTXPJ DS    CL4          Pointer to major line text
CTXTTXPN DS    CL4          Pointer to minor line text
*
* Many other including:
* CTXTJBNM DS    CL8  NAME OF JOB THAT ISSUED MESSAGE
* CTXTSYSN DS    CL8  NAME OF SYSTEM ON WHICH WTO ORIGINATED
*
CTXTATTR DSECT ,
         DS    CL6
CTXTTMSG DS    CL126
*
R0       EQU   0
R1       EQU   1                  R
R2       EQU   2                    e
R3       EQU   3                      g
R4       EQU   4                        i
R5       EQU   5                          s
R6       EQU   6                            t
R7       EQU   7                              e
R8       EQU   8                                r
R9       EQU   9                  E
R10      EQU   10                   q
R11      EQU   11                     u
R12      EQU   12                       a
R13      EQU   13                         t
R14      EQU   14                           e
R15      EQU   15                             s
         END   ,
//L.SYSLMOD  DD  DSN=????.LOAD????,DISP=SHR,SPACE=
 NAME IEF251I
