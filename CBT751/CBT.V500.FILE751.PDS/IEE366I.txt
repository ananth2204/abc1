//SOMITCWI JOB (????????,????,1439,9999,9999),IEE366I-SOMITCW,
//             CLASS=?,MSGCLASS=?,
//             NOTIFY=SOMITCW,COND=(0,NE)
//IEE366I  EXEC HLASMCL,PARM.C=(NODECK,OBJECT,RENT),
//             PARM.L='REFR,RENT,REUS,AC=1,LIST,MAP'
//SYSLIB   DD  DISP=SHR,DSN=SYS1.MACLIB   ,UNIT=SYSDA,VOL=SER=??????
//         DD  DISP=SHR,DSN=SYS1.MODGEN   ,UNIT=SYSDA,VOL=SER=??????
PASS     TITLE 'IEE366I - Start SMF DUMP if data is being buffered'
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
*   Title: IEE366I
*
*   Author: I ain't saying.
*
*   Date-written: 4-28-97
*
*   Change Log:
*
* xx-xx-xx xxxxxxx
*  x.x.x.
*
*
*   Remarks:
*
*        This MPF routine intercepts message IEE366I and issues
*   operator command 'START R,Q=SMFDUMP' whenever the message occurs.
*   To activate this exit:
*       link to a Link-List data set
*       issue operator command: F LLA,REFRESH
*       code the following line in member MPFLST00 of SYS1.PARMLIB:
*       IEE366I,SUP(NO),USEREXIT(IEE366I)
*       and issue operator command: SET MPF=00
*
*       SET MPF=NO will disable MPF if needed due to errors.
*
*   Sample message:
*
* IEE366I NO SMF DATA SETS AVAILABLE--DATA BEING BUFFERED TIME=01:16:42
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
IEE366I  CSECT ,
IEE366I  AMODE 31
IEE366I  RMODE ANY
         SAVE  (14,12),,IEE366I.&SYSDATE..&SYSTIME  Save caller's regs.
         LR    R12,R15            Load address of entry point
         USING IEE366I,R12        Give assembler program's base reg.
         L     R11,0(,R1)         Load the address of the CTXT
         USING CTXT,R11           Addressing CTXT
         L     R10,CTXTTXPJ       Load address of major line text
         USING CTXTATTR,R10       Addressing message attributes & text
         CLC   0(2,R11),CT        See if a real CTXT is found
         BNE   RETURN             If not a CTXT, get out quick
*
*        LA    R9,CTXTTMSG+8      Start hunt for data after msgid
* Label CTXTTMSG has:
* IEE366I NO SMF DATA SETS AVAILABLE--DATA BEING BUFFERED TIME=01:16:42
*
         DROP  R10,R11            Don't need DSECT registers anymore
         SLR   R0,R0              Set SVC 34 full authorization
         MGCR  STARTCMD           Issue SVC 34 with cmd address in R1
RETURN   DS    0H
         SLR   R15,R15            Set a return code of zero
         RETURN (14,12),RC=(15)   Return to caller, return code zero
         DROP  R12                Don't need base register anymore
*
CT       DC    CL2'CT'            Constant for compare
STARTCMD DC    AL2(40),AL2(0),CL36'START R,Q=SMFDUMP  MPF EXIT IEE366I'
*                                  123456789 123456789 123456789 12345
*
         LTORG ,                  In case someone adds literals
*
*  Following mapped by SYS1.MODGEN(IEZVX100) ???????
CTXT     DSECT ,
         DS    CL8
CTXTTXPJ DS    CL4                Pointer to major line text
CTXTTXPN DS    CL4                Pointer to minor line text
         DS    CL8
CTXTRPID DS    CL2                Reply id, if any
CTXTATTR DSECT ,
         DS    CL6
CTXTTMSG DS    CL126
*
         CVT   DSECT=YES    MVS's Communications Vector Table
*
R0       EQU   0
R1       EQU   1                  R
R2       EQU   2                    E
R3       EQU   3                      G
R4       EQU   4                        I
R5       EQU   5                          S
R6       EQU   6                            T
R7       EQU   7                              E
R8       EQU   8                                R
R9       EQU   9                  E
R10      EQU   10                   Q
R11      EQU   11                     U
R12      EQU   12                       A
R13      EQU   13                         T
R14      EQU   14                           E
R15      EQU   15                             S
         END   ,
//L.SYSLMOD DD DISP=SHR,DSN=????.LOAD????,SPACE=  ,
//*            UNIT=SYSDA,VOL=SER=??????
 NAME IEE366I
