SMFLOG   TITLE 'DISPLAY SOME INFO FROM TYPE 4, 20, 35 SMF RECORDS'
* ------------------------------------------------------------------- *
*  WRITTEN:     MAY 9 2013                                            *
*                                                                     *
*  FUNCTION:    READS VSAM SMF FILE FROM DDNAME 'SMFIN' AND           *
*               DISPLAYS MINIMAL INFO FROM SMF TYPE 4, 20, AND 35.    *
*                                                                     *
*               YOU SHOULD POINT THIS PROGRAM TO THE ACTIVE SMF       *
*               DATA SET, BY ALLOCATING DD NAME SMFIN TO IT, ADDING   *
*               PARAMETERS SH AND REUSE, EXAMPLE AS FOLLOWS:          *
*                                                                     *
*               ALLOC F(SMFIN) DA('SYS1.MAN1') SH REUSE               *
* ------------------------------------------------------------------- *
         MACRO
&LABEL   APUT  &MSG,                    @MESSAGE TEXT                  &
               &LEN,                    @LENGTH OF MESSAGE             &
               &RSA=,                   OPTIONAL 2 WORD SAVE AREA      &
               &EPA=                    EPUTL EPA
.*
.* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
.*
.*  --> E@PUT
.*
.*  THIS MACRO SETS UP A PARAMETERS AND INVOKES THE EPUTL ROUTINE.
.*
.*
.*  ARGUMENTS: -----------------------------------------------------
.*
.*      &MSG : ADDRESS OF MESSAGE TEXT OR A LITERAL
.*
.*      &LEN : LENGTH OF MESSAGE TEXT (DEFAULT IS L'&MSG)
.*
.*      &RSA :(OPTIONAL) 1 WORD SAVE AREA TO PRESERVE R14 FOR TPUT
.*            COMPATIBILITY.
.*
.*      &EPA :(OPTIONAL) EPA FOR EPUTL ROUTINE
.*
.*
.*   EXAMPLES: -----------------------------------------------------
.*
.*      1) APUT  'LITERAL'
.*
.*      2) APUT  (R3),80
.*
.*      3) APUT  LABEL
.*
.*      4) APUT  LABEL,40
.*
.*
.* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
.*
.*
.*  LOCAL SET SYMBOLS
.*
         LCLC  &SMA                     MESSAGE ADDRESS (REG)
         LCLC  &SML                     MESSAGE LENGTH (REG)
         LCLC  &SEP                     EPA (REGISTER)
.*
         LCLC  &L1,&L2,&L3              LOCAL LABELS
.*
&L1      SETC  'EP'.'&SYSNDX'.'L1'      LOCAL LABELS
&L2      SETC  'EP'.'&SYSNDX'.'L2'      LOCAL LABELS
&L3      SETC  'EP'.'&SYSNDX'.'L3'      LOCAL LABELS
.*
.*
.*  MESSAGE ADDRESS
.*
         AIF   (T'&LABEL EQ 'O').A0500                      *HMD 11/86*
.*                                                          *HMD 11/86*
&LABEL   DS    0H                                           *HMD 11/86*
.*                                                          *HMD 11/86*
.A0500   ANOP                                               *HMD 11/86*
         AIF   ('&MSG' NE '').A2000
         MNOTE 8,'''&MSG='' MISSING / REQUIRED'
         MEXIT
.*
.A2000   ANOP
         AIF   ('&MSG'(1,1) NE '(').A2400
&SMA     SETC  '&MSG'(2,K'&MSG-2)
         LR    R1,&SMA                  @MESSAGE TEXT
         AGO   .A2600
.*
.A2400   ANOP
         AIF   ('&MSG'(1,1) NE '''').A2500
         B     &L3                      BRANCH AROUND MESSAGE
&L2      DC    C&MSG
&L3      DS    0H
         LA    R1,&L2                   @MESSAGE
         AGO   .A2600
.*
.A2500   ANOP
         LA    R1,&MSG                  @MESSAGE TEXT
.A2600   ANOP
.*
.*  MESSAGE LENGTH
.*
         AIF   ('&LEN' NE '').A3000
         AIF   ('&MSG'(1,1) NE '''').A2800
         LA    R0,L'&L2                 MESSAGE LENGTH
         AGO   .A3600
.*
.A2800   ANOP
         AIF   ('&MSG'(1,1) EQ '(').A2900
         LA    R0,L'&MSG                MESSAGE LENGTH
         AGO   .A3600
.*
.A2900   ANOP
         MNOTE 8,'''LEN'' MISSING / REQUIRED'
         MEXIT
.*
.A3000   ANOP
         AIF   ('&LEN'(1,1) NE '(').A3400
&SML     SETC  '&LEN'(2,K'&LEN-2)
         LR    R0,&SML                  MESSAGE LENGTH
         AGO   .A3600
.*
.A3400   ANOP
         LA    R0,&LEN                  MESSAGE LENGTH
.A3600   ANOP
.*
.*  RSA
.*
         AIF   ('&RSA' EQ '').A4900
         ST    R14,&RSA                 SAVE REGS 14
.A4900   ANOP
.*
.*  ENTRY POINT
.*
         AIF   ('&EPA' NE '').A5000
         L     R15,=V(EPUTL)            EPA
         AGO   .A5600
.*
.A5000   AIF   ('&EPA'(1,1) NE '(').A5400
&SEP     SETC  '&EPA'(2,K'&EPA-2)
         LR    R15,&SEP                 EPA
         AGO   .A5600
.*
.A5400   ANOP
         L     R15,&EPA                 EPA
.*
.A5600   ANOP
.*
.*  CALL LINKAGE
.*
.ACALL   BALR  R14,R15                  CALL PUTLINE ROUTINE
.*
         AIF   ('&RSA' EQ '').A6900
         L     R14,&RSA                 RESTORE R14
.A6900   ANOP
.*
.AMEND   MEND
* ------------------------------------------------------------------- *
SMFLOG   CSECT
         USING *,10
ENTRY0   B     BEGIN-*(,R15)
         DC    AL1(7),CL7'SMFLOG'
         DC    CL16' &SYSDATE &SYSTIME '
         DC    CL12'Version 1.1 '
WORKSIZ  DC    0F'0',AL1(0),AL3(WORKLEN)
BEGIN    STM   14,12,12(13)
         LR    R10,R15
         LR    R2,R1
         L     R0,WORKSIZ
         GETMAIN R,LV=(0)
         LR    R9,R1
         USING WORKD,R9
         XC    0(WORKINI,R1),0(R1)
         ST    R13,4(,R9)          OLD ADDRESS IN NEW SAVE AREA
         ST    R9,8(,R13)          NEW ADDRESS IN OLD SAVE AREA
         LR    R13,R9
         LA    R15,12
         ST    R15,RETCODE
         SPACE
INITCON  DS    0H
         MVI   BLANKS-1,X'40'
         MVC   BLANKS,BLANKS-1
         MVC   OBUF,BLANKS
WTITLE   DS    0H
         APUT  TITLE0,L'TITLE0
         APUT  TITL0U,L'TITL0U
         APUT  TITLE1,L'TITLE1
         APUT  TITLE2,L'TITLE2
         APUT  TITLEU,L'TITLEU
*
         LA    R3,WACB
         LA    R4,WRPL
         LA    R5,IEXL
         LA    R6,IBUF
         MVC   WACB(IACS),IACB
         MVC   WRPL(IRPS),IRPL
*        MODCB RPL=(R4),ACB=(R3),AREA=(R6)
         ST    R3,RPLDACB-IFGRPL(,R4)
         ST    R6,RPLAREA-IFGRPL(,R4)
*        MODCB ACB=(R3),EXLST=(R5)
         ST    R5,ACBEXLST-IFGACB(,R3)
         MVI   OPEN,X'80'
         MVI   CLOSE,X'80'
         OPEN  ((R3)),MF=(E,OPEN)
         TM    ACBOFLGS-IFGACB(R3),ACBOPEN
         BO    OKOPEN
         ST    R15,WORD
         MVC   OBUF(5),=C'OPEN '
         LA    R15,OBUF+5
         LA    R1,WORD
         LA    R0,1
         BAL   R14,TOHEX4
         LA    R1,ACBERFLG-IFGACB(,R3)
         LA    R0,1
         BAL   R14,TOHEX4
         BAL   R14,APUTL
         B     EXIT0
OKOPEN   SR    R7,R7
RLOOP    EQU   *
         GET   RPL=(R4)
         LTR   R15,R15
         BZ    OKGET
         ST    R15,WORD
         MVC   OBUF(4),=C'GET '
         LA    R1,WORD
         LA    R0,1
         LA    R15,OBUF+4
         BAL   R14,TOHEX4
         MVC   1(9,R15),=C'RPLRTNCD '
         LA    R15,10(,R15)
         LA    R1,RPLRTNCD-IFGRPL(,R4)
         LA    R0,1
         BAL   R14,TOHEX4
         BAL   R14,APUTL
         B     EXITC
OKGET    A     R7,=F'1'
         CLI   5(R6),4             STEP TERMINATION
         BE    TYPE4
         CLI   5(R6),20            JOB INITIATION
         BE    TYPE20
         CLI   5(R6),35            LOGOFF
         BE    TYPE35
         B     RLOOP
TYPE4    DS    0H
         MVI   OBUF-1,X'40'
         MVC   OBUF,OBUF-1
         CVD   R7,DOUBLE
         MVC   WBUF(8),=X'4020202020202120'
         ED    WBUF(8),DOUBLE+4
         MVC   OBUF(6),WBUF+2
         MVI   OBUF+6,C' '
         MVC   OBUF+7(3),=C' 4 '
         MVC   OBUF+10(8),18(R6)    JOBNAME
         MVI   OBUF+18,C' '
         LA    R15,OBUF+19
         LA    R1,10(,R6)          DATE
         BAL   R14,UDATE
         MVC   WORD(4),6(R6)       TIME
         BAL   R14,UTIME
         MVC   WORD(4),26(R6)      TIME RDR
         BAL   R14,UTIME
         MVC   WORD(4),43(R6)      TIME INIT
         BAL   R14,UTIME
         MVC   WORD(4),94(R6)      TIME PGM
         BAL   R14,UTIME
         MVC   0(8,R15),58(R6)     PGM
         LA    R15,9(,R15)
         BAL   R14,APUTL
         B     RLOOP
TYPE20   DS    0H
         MVI   OBUF-1,X'40'
         MVC   OBUF,OBUF-1
         CVD   R7,DOUBLE
         MVC   WBUF(8),=X'4020202020202120'
         ED    WBUF(8),DOUBLE+4
         MVC   OBUF(6),WBUF+2
         MVI   OBUF+6,C' '
         MVC   OBUF+7(3),=C'20 '
         MVC   OBUF+10(8),18(R6)    JOBNAME
         MVI   OBUF+18,C' '
         LA    R15,OBUF+19
         LA    R1,10(,R6)          DATE
         BAL   R14,UDATE
         MVC   WORD(4),6(R6)       TIME
         BAL   R14,UTIME
*        MVC   0(10,R15),=C'(JOB INIT)'
         MVC   27(10,R15),=C'(JOB INIT)'
         LA    R15,10(,R15)
         BAL   R14,APUTL
         MVC   OBUF+59(10),BLANKS
         B     RLOOP
TYPE35   DS    0H
         MVI   OBUF-1,X'40'
         MVC   OBUF,OBUF-1
         CVD   R7,DOUBLE
         MVC   WBUF(8),=X'4020202020202120'
         ED    WBUF(8),DOUBLE+4
         MVC   OBUF(6),WBUF+2
         MVI   OBUF+6,C' '
         MVC   OBUF+7(3),=C'35 '
         MVC   OBUF+10(8),18(R6)    JOBNAME
         MVI   OBUF+18,C' '
         LA    R15,OBUF+19
         LA    R1,10(,R6)          DATE
         BAL   R14,UDATE
         MVC   WORD(4),6(R6)       TIME OFF
         BAL   R14,UTIME
         MVC   0(6,R15),=C'SINCE '
         LA    R15,6(,R15)
         MVC   WORD(4),26(R6)      TIME ON
         BAL   R14,UTIME
         BAL   R14,APUTL
         B     RLOOP
UDATE    UNPK  WBUF(7),0(4,R1)
         MVC   0(2,R15),WBUF+2
         MVI   2(R15),C'.'
         MVC   3(3,R15),WBUF+4
         MVI   6(R15),C' '
         LA    R15,7(,R15)
         BR    R14
UTIME    L     R1,WORD
         SR    R0,R0
         D     R0,=F'100'          COMPUTE SECONDS, REM 100THS
         SR    R0,R0
         D     R0,=F'60'           COMPUTE MINUTES, REM SECONDS
         SR    R0,R0
         D     R0,=F'60'           COMPUTE HOURS, REM MINUTES
         CVD   R1,DOUBLE           HOURS
         OI    DOUBLE+7,X'0F'
         UNPK  WBUF(3),DOUBLE+6(2)
         MVC   0(2,R15),WBUF+1
         MVI   2(R15),C':'
         CVD   R0,DOUBLE           MINUTES
         OI    DOUBLE+7,X'0F'
         UNPK  WBUF(3),DOUBLE+6(2)
         MVC   3(2,R15),WBUF+1
         MVI   5(R15),C' '
         LA    R15,6(,R15)
         BR    R14
EOF      EQU   *
EXITC    MVI   CLOSE,X'80'
         CLOSE ((R3)),MF=(E,CLOSE)
DONE     B     EXIT0
         SPACE
TOHEX1   MVC   1(1,R15),0(R1)
         UNPK  0(3,R15),1(2,R15)
         TR    0(2,R15),HEXTAB-240
         LA    R15,2(,R15)
         LA    R1,1(,R1)
         BCT   R0,TOHEX1
         MVI   0(R15),C' '
         BR    R14
         SPACE
TOHEX4   MVC   1(4,R15),0(R1)
         UNPK  0(9,R15),1(5,R15)
         TR    0(8,R15),HEXTAB-240
         MVI   8(R15),C' '
         LA    R15,9(,R15)
         LA    R1,4(,R1)
         BCT   R0,TOHEX4
         BR    R14
         SPACE
APUTL    ST    R14,RET14
         APUT  OBUF,L'OBUF
         L     R14,RET14
         BR    R14
TPUTL    ST    R14,RET14
         LA    R1,OBUF
         LR    R0,R15
         SR    R0,R1
         SVC   93 TPUT
         L     R14,RET14
         BR    R14
         SPACE
EXIT0    MVI   RETCODE+3,0
EXIT     L     R2,RETCODE
         LR    R1,R13             POINT TO ADDRESS TO FREEMAIN
         L     R0,WORKSIZ         LENGTH TO FREEMAIN
         L     R13,4(,R13)        RESTORE ORIGINAL R13
         FREEMAIN R,A=(1),LV=(0)
         DROP  R9
         LR    R15,R2
         LM    0,12,20(R13)
         L     R14,12(,R13)
         BR    R14
         SPACE
         LTORG
         DC    0D'0'
DACB     EQU   0
DBUF     EQU   0
IOPEN    OPEN  (DACB),MF=L
ICLOSE   CLOSE (DACB),MF=L
IACB     ACB   DDNAME=SMFIN,MACRF=(ADR,SEQ,IN)
IACS     EQU   *-IACB
IRPL     RPL   ACB=DACB,OPTCD=(ADR,SEQ),AREA=DBUF,AREALEN=32767
IRPS     EQU   *-IRPL
IEXL     EXLST EODAD=EOF
IEXS     EQU   *-IEXL
HEXTAB   DC    C'0123456789ABCDEF'
         DC    CL8' '
TITLE0   DC    C'                  SMFLOG - Version 1.1 '
TITL0U   DC    C'                  ------ - ------- --- '
TITLE1   DC    C' Line  Ty Jobname   Date  Time  Time  Time  Time  Pgm X
               Name'
TITLE2   DC    C'Number pe                        Rdr  Init   Pgm '
TITLEU   DC    C'------ -- -------- ------ ----- ----- ----- ----- ----X
               ----'
         DC    0D'0'
WORKD    DSECT
         DS    18F
DOUBLE   DS    D
RETCODE  DS    F
WORD     DS    F
OPEN     DS    F
CLOSE    DS    F
RET14    DS    F
WORKINI  EQU   *-WORKD
WACB     DS    0F,(IACS)X
WRPL     DS    0F,(IRPS)X
         DC    C' '
OBUF     DS    CL80
         DC    C' '
BLANKS   DC    CL80' '
WBUF     DS    CL16
IBUF     DS    4096D
         DS    0D
WORKLEN  EQU   *-WORKD
         SPACE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE
         PRINT NOGEN
         IFGACB
         IFGRPL
         END   ENTRY0
EPUTL    TITLE 'PUTLINE FOR FEWER THAN 83 ARGUMENTS IN THE KEY OF C MAJ&
               OR'
EPUTL    CSECT                                              *HMD 10/85*
         LCLB  &XA                                          *HMD 10/85*
&XA      SETB  1                  SET TO ZERO ON NON/XA     *HMD 10/85*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
*  MODULE NAME  . . . . EPUTL
*
*  ALIASES  . . . . . . NONE
*
*  DATE WRITTEN . . . . 06/85
*  UPDATED      . . . . 04/88                                  HD APR88
*
*  AUTHOR . . . . . . . JIM ELSWORTH
*  UPDATOR  . . . . . . HOWARD M. DEAN                         HD APR88
*
*  ENVIRONMENT  . . . . TSO COMMAND-PROCESSOR
*
*  PURPOSE:
*    ALLOW AN EASY INTERFACE TO PUTLINE ROUTINE
*
*
*  DESCRIPTION:
*    BUILDS PUTLINE PARAMETER LIST BASED ON INPUT ARGUMENTS.
*    ISSUES SINGLE LINE PUTLINE.
*
*
*  ATTRIBUTES:
*    REENTRANT
*
*  FEATURES:
*    WILL RUN UNDER BATCH TMP SINCE PUTLINE USED INSTEAD OF TPUT.
*
*  CALLED MODULES:
*    PUTLINE
*
*  ASSUMPTIONS:
*    CALLED BY A TSO-CP THAT IS OBSERVING STANDARD LINKAGE
*
*  ABENDS:
*    POSSIBLE 0CX IF BAD ARGUMENTS
*
*  CAVEATS:
*    UNPREDICTABLE RESULTS IF FIRST PROGRAM RUNNING UNDER THIS TCB
*    DID NOT SAVE (14,12) BEFORE R1 WAS MODIFIED.
*
*
*  INPUT ARGUMENTS:
*    1)  R1 : ADDRESS OF MESSAGE TEXT
*    2)  R0 : LENGTH OF MESSAGE TEXT
*    3) R13 : STANDARD OS SAVE AREA
*    4) R14 : RETURN ADDRESS
*    5) R15 : ENTRY ADDRESS
*
*               ( CONTINUED ... )
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
*
*  OUTPUT ARGUMENT: RETURN CODE FROM PUTLINE OR
*
RCBA     EQU   100                 BAD MESSAGE ADDRESS
RCBL     EQU   104                 MESSAGE LENGTH <1 OR >MAXMSG
*
MAXMSG   EQU   255                 MAXIMUM MESSAGE LENGTH
*
*
*  REGISTERS USED:
*
R0       EQU   0                   MESSAGE LENGTH AT ENTRY
R1       EQU   1                   MESSAGE ADDRESS AT ENTRY
R2       EQU   2                   WORK
RMA      EQU   4                   @MESSAGE TEXT
RML      EQU   5                   MESSAGE LENGTH
RUPT     EQU   6                   @UPT (USER PROFILE TABLE)
RECT     EQU   7                   @ECT
R12      EQU   12                  CSECT BASE
R13      EQU   13                  @SAVE AREA AND DYNAMIC WORK AREA
R14      EQU   14                  WORK/LINKAGE
R15      EQU   15                  WORK/LINKAGE
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         EJECT
         IKJCPPL
         EJECT
         IKJTCB   LIST=YES                                  *HMD 10/85*
         EJECT                                              *HMD 10/85*
         CVT      DSECT=YES,LIST=YES                        *HMD 10/85*
         EJECT
         IHAPSA   LIST=YES                                     HD APR88
         EJECT                                                 HD APR88
PUTLDS   DSECT
         DS    18F                      OS SAVE AREA
PUTAAA   EQU   *
PUTECB   DS    F                        ECB
PUTIOPL  DS    4F                       IOPLADS
PUTBLOK  PUTLINE MF=L
PUTLEN   DS    H                        LENGTH
PUTZZZ   DS    H                        RESERVED
PUTMSG   DS    (MAXMSG)C                MESSAGE TEXT AREA
PUTSIZE  EQU   *-PUTLDS                 SIZE OF PUTLINE PARMS
         EJECT
***********************************************************************
*  SET UP LINKAGE
***********************************************************************
         SPACE 1
EPUTL    CSECT
         AIF  (NOT &XA).X001                                *HMD 10/85*
EPUTL    AMODE 31
.X001    ANOP                                               *HMD 10/85*
         STM   14,12,12(13)       SAVE CALLERS REGS
         LR    R12,R15            COPY EPA
         USING EPUTL,R12          ADDRESSABILITY
         AIF   (NOT &XA).X002                               *HMD 10/85*
         GETMAIN RU,              LV=LENGTH                            &
               LV=PUTSIZE,        STANDARD OS SA PLUS ANY EXTRA        &
               LOC=RES            RELATIVE TO 16MB LINE
         AGO   .X003                                        *HMD 10/85*
.X002    ANOP                                               *HMD 10/85*
         GETMAIN RU,              LV=LENGTH                            &
               LV=PUTSIZE         STANDARD OS SA PLUS ANY EXTRA
.X003    ANOP                                               *HMD 10/85*
         ST    1,8(13)            OUR SAVE AREA POINTER
         ST    13,4(1)            POINTER TO CALLERS SAVE AREA
         LR    13,1               @OUR SAVE AREA
         USING PUTLDS,R13         MAP SA AND WORK AREA
         L     R14,4(R13)         POINT TO CALLERS RSA
         LM    R0,R1,20(R14)      RESTORE REGS CHANGED BY GETMAIN
         SPACE 2
***********************************************************************
*  UNLOAD ARGUMENTS AND VALIDATE WHERE POSSIBLE
***********************************************************************
         SPACE 1
         LA    R15,RCBA                 ASSUME BAD ADDRESS
         LTR   RMA,R1                   MESSAGE ADDRESS
         BNP   RETURN                   GOOD ASSUMPTION
         LA    R15,RCBL                 ASSUME BAD LENGTH
         LTR   RML,R0                   POSITIVE?
         BNP   RETURN                   -NO
         C     RML,=A(MAXMSG)           MORE THAN MAX?
         BH    RETURN                   -YES
         SPACE 2
***********************************************************************
*  BUILD PARAMETER LIST
***********************************************************************
         SPACE 1
         LA    R15,4(RML)               MESSAGE LENGTH
         STH   R15,PUTLEN                 PLUS HEADER
         XC    PUTZZZ,PUTZZZ            CLEAR 2ND HWORD OF RDW
         BCTR  RML,0                    REDUCE FOR MOVE
         EX    RML,PUTMOVE
         EJECT
***********************************************************************
*  DIG UP ADDRESS OF UPT AND ECT
***********************************************************************
         SPACE 1
         L     R2,PSATOLD-PSA           @TCB                   HD APR88
         L     R2,TCBFSA-TCB(R2)        @1ST PROBLEM PGM SAVE AREA
         L     R2,24(R2)                REG1 THAT WAS PASSED TO CP
         USING CPPL,R2                  MAP CPPL
         L     RUPT,CPPLUPT             @UPT
         L     RECT,CPPLECT             @ECT
         SPACE 2
***********************************************************************
*  ISSUE PUTLINE CALL
***********************************************************************
         SPACE 1
         XC    PUTECB,PUTECB            CLEAR ECB
         XC    PUTBLOK(12),PUTBLOK      CLEAR PARM BLOCK       HD APR88
         PUTLINE PARM=PUTBLOK,          PARAMETER BLOCK                &
               UPT=(RUPT),              @UPT                           &
               ECT=(RECT),              @ECT                           &
               ECB=PUTECB,              @ECB                           &
               OUTPUT=(PUTLEN,TERM,SINGLE,DATA),                       &
               MF=(E,PUTIOPL)
         EJECT
***********************************************************************
*  RETURN TO CALLER
***********************************************************************
         SPACE 1
RETURN   EQU   *
         LR    2,R13                    @OUR SAVE AREA
         LR    3,R15                    SAVE RETURN CODE
         L     R13,4(R13)               @CALLERS SAVE AREA
         FREEMAIN RU,LV=PUTSIZE,A=(R2),SP=0
         LR    R15,3                    RESTORE RC
         RETURN (14,12),RC=(15)         RESTORE CALLERS REGS
         SPACE 5
***********************************************************************
*  CONSTANTS AND NON-REENTRANT WORK AREAS
***********************************************************************
         SPACE 1
PUTMOVE  MVC   PUTMSG(*-*),0(RMA)       MOVE MESSAGE TO BUFFER
         SPACE 2
         LTORG
         SPACE 2
         END   EPUTL
