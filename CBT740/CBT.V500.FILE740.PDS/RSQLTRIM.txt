/* REXX */
/* =====================================================================
 * Language   : REXX
 * Name       : RSQLTRIM
 * Category   : Rexx exec
 * Description: Rexx program to remove unneccessary spaces from a
 *            : string, leaving characters enclosed in Quotes exactly as
 *            : they appear.
 * Rexx       :
 * Panels     :
 * Skeltons   : none
 * Messages   : none
 * Procedure  : none
 * Author     : Alan Wynne.
 * Owner      : Alan Wynne.
 * Created    : August 2004
 * Modified   :
 * ================================================================== */
  Parse Arg SQLStmnt

  SQLStmnt = STRIP(SQLStmnt, B)


  StmntLen = Length(SQLStmnt)

  Do i = 1 until i >= StmntLen
    FirstChar = SubStr(SQLStmnt,i,1)
    FirstTwoChar = SubStr(SQLStmnt,i,2)
    Select
      When FirstTwoChar         = '""'  Then i = i + 1
      When FirstTwoChar         = "''"  Then i = i + 1
      When FirstChar            = '"'   Then
        Do
          Literal    = Substr(SQLStmnt,i+1)
          LiteralLen = Length(Literal)
          Do j = 1 Until  j >= LiteralLen
            Select
              When SubStr(Literal,j,2) = '""'  Then j = j + 1
              When SubStr(Literal,j,1) = '"'   Then
                Do
                  SQLStmntEnd  = Substr(Literal,j+1)
                  Literal      = Substr(Literal,1,j)
                  LiteralLen   = j
                End
              Otherwise Nop
            End
          End
          SQLStmnt = SubStr(SQLStmnt,1,i)||Literal||SQLStmntEnd
          i = i + j
        End
      When FirstChar            = "'"   Then
        Do
          Literal  = Substr(SQLStmnt,i+1)
          LiteralLen = Length(Literal)
          Do j = 1 Until  j >= LiteralLen
            Select
              When SubStr(Literal,j,2) = "''"  Then j = j + 1
              When SubStr(Literal,j,1) = "'"   Then
                Do
                  SQLStmntEnd  = Substr(Literal,j+1)
                  Literal      = Substr(Literal,1,j)
                  LiteralLen   = j
               End
              Otherwise Nop
            End
          End
          SQLStmnt = SubStr(SQLStmnt,1,i)||Literal||SQLStmntEnd
          i = i + j
        End
      When FirstTwoChar         = "  " Then
        Do
          j = i + 2
          Do  while (Substr(SQLStmnt,j,1) = " " & j < StmntLen)
            j = j + 1
          End
          SQLStmnt = SubStr(SQLStmnt,1,i)||SubStr(SQLStmnt,j)
          StmntLen = Length(SQLStmnt)
        End
      Otherwise Nop
    End
  End
  Return SQLStmnt
