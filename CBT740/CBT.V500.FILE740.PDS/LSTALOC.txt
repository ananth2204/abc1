/* REXX */
  Ver      = "1.00"
  ReturnCode = 0
/* ==================================================================
 * Author: Alan Wynne
 *
 * Rexx Exec to List all datasets allocated with their DDname and
 * Disposition.
 *
 * ================================================================== */
/* TRACE ?R */
/* ================================================================== */
/* Recieve arguments                                                  */
/*                                                                    */

/*no arguments                                                        */
/*Parse ARG                                                           */

/* ================================================================== */
/* Test the Alloc commands.                                           */
/*                                                                    */

  Drop DatasetInfo.
  DatasetInfo.0 = 0

  BlankString = '                                                     '

  x = OUTTRAP('ListAlc.')

  "LISTALC STATUS"

  if ListAlc.0 = 0 Then NOP
  Else
    Do i = 1 to listAlc.0
      Select
        When SubStr(ListAlc.i,1,2) = '--'    then NOP
        When SubStr(ListAlc.i,1,2) = '  '    then
          Do
            DatasetInfo.j.DD   = Substr(ListAlc.i,3,8)
            DatasetInfo.j.Disp = Strip(Substr(ListAlc.i,12))
          End
        Otherwise
          Do
            DatasetInfo.0      = DatasetInfo.0 + 1
            j                  = DatasetInfo.0
            DatasetInfo.j.DSN  = Strip(ListAlc.i)
            DatasetInfo.j.DD   = ''
            DatasetInfo.j.Disp = ''
            k =  POS(' ',DatasetInfo.j.DSN,1)
            If k > 0 then
              Do
                DatasetInfo.j.DD   =                                  ,
                  Strip(Substr(DatasetInfo.j.DSN,k,))
                DatasetInfo.j.DSN  =                                  ,
                  Strip(Substr(DatasetInfo.j.DSN,1,k))
              End
          End
      End
    End

  if DatasetInfo.0 = 0 Then Nop
  Else
    Do
      Say "List dataset allocated."
      Say "                                                 "
      Say "The number of Datasets Allocated is:"DatasetInfo.0
      Say "                                                 "
      Say "DDname   DSN                                     "         ,
        "          Disp"
      Say "----+----1----+----2----+----3----+----4----+----5" ||     ,
        "----+----6----+----7----+----"
      Do i = 1 to DatasetInfo.0
        Say SUBSTR(DatasetInfo.i.DD  ||BlankString,1,8),
            SUBSTR(DatasetInfo.i.DSN ||BlankString,1,50),
            DatasetInfo.i.Disp
      End
    End

  Return

