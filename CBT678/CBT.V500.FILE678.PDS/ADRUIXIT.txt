//SOMITCWA JOB (xxxxxxxx,xxxx,1439,9999,9999),ADRUIXIT-SOMITCW,
//             CLASS=x,MSGCLASS=x,
//             NOTIFY=SOMITCW,COND=(4,LT)
//*
//* This must assemble and link before AWSDSSU links.
//* If using LLA data sets, don't forget to update or
//* refresh after JOBs.
//*
//* This is an exit to ADRDSSU to allow program AWSADR
//* to convert ADRDSSU DUMP output to AWS format and to
//* allow ADRDSSU RESTORE to read an AWS file and give
//* ADRDSSU the ADRDSSU format data.
//*
//* This exit must be linked into ADRDSSU to work.
//* See JCL AWSDSSU to link this exit, ADRDSSU and
//* create a new program called AWSDSSU that is
//* called by AWSADR.
//*
//HLASMCL  EXEC HLASMCL,PARM.C=(RENT,OBJECT,NODECK,TERM),
//             PARM.L=RENT
//SYSLIB   DD
//         DD  DSN=SYS1.MODGEN,DISP=SHR
* * * * * * * * * * * * * * * * * * * * * * * * * * *
* ADRUIXIT User Exit.
*
* If a DUMP operation starting, turns on the UFNOOUT bit flag
*   which will allow output DUMP records to not be written to
*   the normal output when written to the output AWS file.
*
* If a RESTORE operation starting, turns on UFNOIN bit flag
*   which allows records to be supplied from the AWS input
*   file instead of an ADRDSSU DUMP format data set.
*
* If either bit is set, a return code of four tells ADRDSSU.
*
* Written: 2003-6-11
* Problems: somitcw@erols.com
*
* * * * * * * * * * * * * * * * * * * * * * * * * * *
ADRUIXIT CSECT ,
ADRUIXIT AMODE 31                 ADRDSSU requires 31 bit addressing
ADRUIXIT RMODE ANY                ADRDSSU doesn't care about RMODE
         SAVE  (14,12),,'ADRUIXIT.&SYSDATE..&SYSTIME'  Save registers
         LR    R12,R15            Set a base register
         USING ADRUIXIT,R12       Set addressability to the exit
         USING ADRUFOB,R1         Set addressability to ADRUFO
         TM    UFFUNCT1,UFFUDUMP  See if a DUMP function
         BO    DUMP               Is DUMP, go set delete allow bit
         TM    UFFUNCT1,UFFUREST  See if a RESTORE function
         BZ    NOTSET             Is not RESTORE, go skip change
         OI    UFAIFLGS,UFNOIN    Turn on "No RESTORE input tape" bit
         WTO   'ADRUIXIT set UFNOIN bit to allow RESTORE' Debug
         B     SET                Go indicate change and exit
DUMP     DS    0H
         OI    UFAIFLGS,UFNOOUT   Turn on "No DUMP output tape" bit
         WTO   'ADRUIXIT set UFNOOUT bit to allow DUMP' Debug
SET      DS    0H
         LM    R14,R12,12(R13)    Restore other registers
         LA    R15,4              Indicate that a bit was changed
         BR    R14                Return to AWSDSSU
NOTSET   DS    0H
         LM    R14,R12,12(R13)    Restore other registers
         SLR   R15,R15            Indicate that nothing was touched
         BR    R14                Return to AWSDSSU
         LTORG  ,                 Keep literals in their own CSECT
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
* DSECT for ADRUIXIT
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
         ADRUFO ,                 Include the ADRUFO mapping DSECT
*
ADRUIXIT CSECT ,                  Resume the main CSECT
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
* Register equates
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
R0       EQU   0                  Register equate for register 0
R1       EQU   1                  Register equate for register 1
R2       EQU   2                  Register equate for register 2
R3       EQU   3                  Register equate for register 3
R4       EQU   4                  Register equate for register 4
R5       EQU   5                  Register equate for register 5
R6       EQU   6                  Register equate for register 6
R7       EQU   7                  Register equate for register 7
R8       EQU   8                  Register equate for register 8
R9       EQU   9                  Register equate for register 9
R10      EQU   10                 Register equate for register 10
R11      EQU   11                 Register equate for register 11
R12      EQU   12                 Register equate for register 12
R13      EQU   13                 Register equate for register 13
R14      EQU   14                 Register equate for register 14
R15      EQU   15                 Register equate for register 15
         END   ,
//L.SYSLMOD DD DSN=USER.LINKLIB,DISP=SHR,SPACE=
   NAME ADRUIXIT(R)
//
