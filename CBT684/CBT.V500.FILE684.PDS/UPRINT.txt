         MACRO
&NAME    UPRINT &A,&B,&SKIPAFT=0,&LINELEN=133
         GBLA  &W
         LCLA  &N,&LL
         LCLC  &INTO,&FROM,&LL2
&W       SETA  &W+11
&LL      SETA  &LINELEN-1
&LL2     SETC  '&LL'
&INTO    SETC  'PRINTER'
&FROM    SETC  'LINE'
         AIF   ('&A' EQ '').ONE
&INTO    SETC  '&A'
.ONE     AIF   ('&B' EQ '').TWO
&FROM    SETC  '&B'
.TWO     ANOP
&NAME    PUT   &INTO,&FROM
         MVI   &FROM,X'40'
         MVC   &FROM+1(&LL2),&FROM
         AIF   (T'&SKIPAFT EQ 'N').NUMERIC
PRN0&W   CP    &SKIPAFT,=P'2'
         BL    PRN&W
         BH    PRN3&W
         MVI   &FROM,X'F0'
         SP    &SKIPAFT,=P'2'
         PUT   &INTO,&FROM
         MVI   &FROM,X'40'
         MVC   &FROM+1(&LL2),&FROM
         B      PRN4&W
PRN&W    CP    &SKIPAFT,=P'1'
         BL    PRN4&W
         PUT   &INTO,&FROM
         MVI   &FROM,X'40'
         MVC   &FROM+1(&LL2),&FROM
         SP    &SKIPAFT,=P'1'
         B      PRN4&W
PRN3&W   MVI   &FROM,X'60'
         PUT   &INTO,&FROM
         SP    &SKIPAFT,=P'3'
         MVI   &FROM,X'40'
         MVC   &FROM+1(&LL2),&FROM
         B     PRN0&W
PRN4&W   EQU   *
         MEXIT
.NUMERIC ANOP
&N       SETA  &SKIPAFT
.LOOP    AIF   (&N LE 0).FIN
         AIF   (&N GE 2).DUO
         PUT   &INTO,&FROM
         MVI   &FROM,X'40'
         MVC   &FROM+1(&LL2),&FROM
         AGO   .FIN
.DUO     AIF   (&N GT 2).TRES
         MVI   &FROM,X'F0'
         PUT   &INTO,&FROM
         MVI   &FROM,X'40'
         MVC   &FROM+1(&LL2),&FROM
         AGO   .FIN
.TRES    MVI   &FROM,X'60'
&N       SETA  &N-3
         PUT   &INTO,&FROM
         MVI   &FROM,X'40'
         MVC   &FROM+1(&LL2),&FROM
         AGO   .LOOP
.FIN     ANOP
         MEND
