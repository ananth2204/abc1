MSGFLUSH TITLE 'MPF EXIT TO DELETE WTO MESSAGE FROM CONSOLES AND LOGS'
*
*        PURPOSE: TO SUPPRESS WTO MESSAGE DISPLAY ON EVERY
*                 CONSOLE AS WELL AS THE SYSTEM LOG AND JOB LOG.
*                 THIS ALLOWS INSTALLATION CODE SUCH AS SMF EXITS
*                 LIKE IEFU83 TO SUPPLY NUMEROUS MESSAGES WHICH
*                 CAN BE SELECTIVELY DISCARDED TO REDUCE CONSOLE
*                 AND LOG MESSAGE CLUTTER.  THESE DISCARDED MESSAGES
*                 WILL ONLY APPEAR IN THE MASTER TRACE TABLE, AND
*                 WTP MESSAGES (ROUTCDE=11) WILL NOT BE TPUT UNDER
*                 TSO EVEN IF 'PROFILE WTPMSG' IS ACTIVE.
*
*       FUNCTION: TURN ON THE "DELETE THE MESSAGE" BIT AS WELL AS
*                 THE "SUPPRESS FROM JOBLOG" BIT.
*                 SELECTIVITY IS PROVIDED BY THE ACTIVE MPFLST__
*                 MEMBER OF SYS1.PARMLIB.
*
*        WRITTEN: GREG PRICE OF FERNTREE COMPUTER SERVICES 08 OCT 1992.
*
*     ATTRIBUTES: REUS,RENT,REFR,AMODE=31,RMODE=ANY.
*
*         MACROS: IEZVX100 MAPPING MACRO FROM SYS1.MODGEN.
*
         SPACE 2
MSGFLUSH CSECT
MSGFLUSH AMODE 31                 31 BIT ADDRESSING MODE
MSGFLUSH RMODE ANY                31 BIT RESIDENCY MODE
         SPACE
         USING MSGFLUSH,R12
         STM   R14,R12,12(R13)    SAVE CALLER'S REGISTERS
         LR    R12,R15            ESTABLISH MODULE'S BASE REGISTER
         L     R9,0(R1)           R9==> WTO EXIT PARAMETER LIST
         USING CTXT,R9            ESTABLISH ADDRESSABLITY TO IT
         OI    CTXTRFB2,CTXTRDTM  DELETE THE MESSAGE
         OI    CTXTERF3,CTXTESJL  SUPPRESS MESSAGE FROM THE JOBLOG
         DROP  R12,R9             MSGFLUSH, CTXT
         LM    R14,R12,12(R13)    RESTORE CALLER'S REGISTERS
         BR    R14                RETURN TO CALLER
         SPACE 2
         LTORG
         SPACE 2
         DS    0D                 END OF CSECT
         TITLE 'EQUATES AND DSECTS'
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE 2
         IEZVX100
         SPACE 2
         END   MSGFLUSH
