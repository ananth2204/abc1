TCPU83   TITLE 'SMF RECORD SVC ENTRY CONFIRMATION USER EXIT'
***********************************************************************
*                                                                     *
* MODULE: TCPU83 - SMF RECORD WRITE CONFIRMATION EXIT (SVC ENTRY)     *
*                                                                     *
* FUNCTION: TO INTERCEPT SMF-118 RECORDS AND DISPLAY KEY FIELDS       *
*           IN SYSLOG.  THE ULTIMATE USE IS TO VERIFY FTP             *
*           PROCESSING.                                               *
*                                                                     *
* MACROS: FROM SYS1.MACLIB AND SYS1.MODGEN.                           *
*                                                                     *
*         REGISTERS AT ENTRY:                                         *
*         R1  -  ADDRESS OF SMF RECORD RDW                            *
*         R13 -  ADDRESS OF 72 BYTE SAVEAREA                          *
*         R14 -  RETURN ADDRESS                                       *
*         R15 -  ADDRESS OF TCPU83 (ENTRY POINT)                      *
*                                                                     *
* EXIT:   THIS MODULE MUST RESTORE ALL REGISTERS EXCEPT R15 WHICH     *
*         CONTAINS A RETURN CODE.  THE SMF RECORD MAY BE MODIFIED     *
*         BY THIS EXIT.                                               *
*                                                                     *
*         RETURN CODES:                                               *
*         0   -  WRITE THE SMF RECORD TO THE SMF DATASET.             *
*         4   -  SUPPRESS THE WRITING OF THE SMF RECORD.              *
*                                                                     *
* ATTRIBUTES: REUS,RENT,REFR, KEY 0, SRB MODE AMODE=31, RMODE=ANY.    *
*                                                                     *
***********************************************************************
         EJECT
TCPU83   CSECT
TCPU83   AMODE 31
TCPU83   RMODE ANY
***********************************************************************
*                                                                     *
*  REGISTER USAGE                                                     *
*                                                                     *
R0       EQU   0                                                      *
R1       EQU   1                                                      *
R2       EQU   2                    SMF RECORD BASE                   *
R3       EQU   3                    WORK (MESSAGE BUFFER POINTER)     *
R4       EQU   4                    WORK                              *
R5       EQU   5                    WORK                              *
R6       EQU   6                                                      *
R7       EQU   7                                                      *
R8       EQU   8                                                      *
R9       EQU   9                    WORK AREA BASE                    *
R10      EQU   10                                                     *
R11      EQU   11                                                     *
R12      EQU   12                   PROGRAM BASE                      *
R13      EQU   13                   CALLER'S SAVE AREA BASE           *
R14      EQU   14                   RETURN ADDRESS                    *
R15      EQU   15                   ENTRY POINT, WORK, RETURN CODE    *
*                                                                     *
***********************************************************************
         TITLE 'MAINLINE - GLOBAL PROCESSING AND RECORD TYPE CHECK'
         J     U84$
         DC    AL1(15),CL15'TCPU83 &SYSDATE'
U84$     DS    0H
         STM   R14,R12,12(R13)      SAVE REGISTERS
         LR    R12,R15              R12 - PROGRAM BASE REGISTER
         USING TCPU83,R12
         ICM   R2,15,0(R1)          R2  - SMF RECORD BASE REGISTER
         JZ    WRITEREC             ZERO ADDRESS - CAN'T FIND RECORD
         CLI   5(R2),118            TYPE TCPIP RECORD ??
         JE    REC118$              YES
         SPACE
WRITEREC DS    0H
         LM    R14,R12,12(R13)      EXIT AND WRITE RECORD
         SR    R15,R15                       RC=0
         BR    R14
         TITLE 'TCP/IP FTP STATISTICS RECORDS'
*---------------------------------------------------------------------*
*---     THIS MODIFICATION INSERTED BY RICK FOCHTMAN, BOARD OF     ---*
*---     TRADE CLEARING CORP., TO PROVIDE FTP STATISTICS ON        ---*
*---     SYSLOG IN REAL TIME.  THE DSECT SMFFTP76 WAS EXTRACTED    ---*
*---     FROM MACRO EZASMF76, FOUND IN THE SEZACMAC DATASET FOR    ---*
*---     TCP/IP.  LOGIC IS FAIRLY SIMPLE AND REASONABLY WELL       ---*
*---     DOCUMENTED.                                               ---*
*---------------------------------------------------------------------*
REC118$  DS    0H
         STORAGE OBTAIN,LENGTH=WK118LEN,LOC=24,SP=31
         LR    R8,R1               COPY STORAGE ADDRESS
         LA    R9,WK118LEN         AND LENGTH
         LA    R14,0               SOURCE TO ZEROS
         LA    R15,0               LENGTH/PAD TO ZEROS
         MVCL  R8,R14              CLEAR THE AREA
         LR    R9,R1               COPY STORAGE ADDRESS
         USING WK118,R9             AND BASE MY DSECT
         USING SMFFTP76,R2         RECORD DSECT BASE
         CLC   22(2,R2),=H'3'      FTP SUBTYPE ??
         JE    DOIT                YES -----
         CLC   22(2,R2),=H'70'     FTP RANGE ??
         JL    ENDIT               NOPE ---
         CLC   22(2,R2),=H'75'     FTP RANGE ??
         JH    ENDIT               NOPE ---
DOIT     DS    0H
         LH    R3,0(,R2)           LOAD RECORD LENGTH
         BCTR  R3,R0               DECR BY ONE (POINT TO LAST BYTE)
         LA    R4,INWTO            POINT TO MESSAGE INIT'S
         LA    R5,INWTOL           AND LENGTH
         LR    R15,R5              COPY FOR OUTPUT LEN
         LA    R14,HDRL            OUTPUT POINTER
         MVCL  R14,R4              INIT THE MESSAGES
         MVC   FTPID,SMFFTPSU      USERID
         MVC   FTPDSN,SMFFTDSN     DSNAME
         #CVIP FTPIPO,SMFFTPSA,EDWK1     FROM ADDRESS
         #CVIP FTPIPI,SMFFTPSL,EDWK1     TO ADDRESS
         $CVTIME FTPSTT,SMFFTTRS         START TIME
         $CVTIME FTPEDT,SMFFTTRE         END TIME
         MVC   FTPEDT-3(3),=C' / '
         MVC   FTPCMD,SMFFTPCM     FTP SUBCOMMAND
         MVC   FTPTYP,SMFFTPFM     FORMAT, MODE AND STRUCTURE
         EDIT  FTPBCT,SMFFTTBC,'Z,ZZZ,ZZZ,ZZZ',                        X
               WRKA=EDWK1,WRKB=EDWK2
         LA    R0,0
         WTO   TEXT=((HDRL,),(DL1,),(DL2,),(DL2A,),(DL3,),(DL4,)),     X
               MCSFLAG=HRDCPY,MF=(E,SMF76MSG),                         X
               DESC=12
ENDIT    DS    0H
         STORAGE RELEASE,LENGTH=WK118LEN,ADDR=(9),SP=31
         J     WRITEREC             ALL DONE, GO WRITE TYPE 64 RECORD
         DROP  R2,R9
         TITLE 'STATIC AREA AND LITERAL POOL'
*-------------------------------------------------------------------
*---     DATA TO INITIALIZE MLWTO AREA
*------------------------------------------------------------------
         DS    0F
INWTO  DC  AL2(L'HDR)
       DC  CL35'U83FTPI ----- FTP STATISTICS -----'
       DC  AL2(L'LINE1)
       DC  CL58'ID: XXXXXXXX   TIME START/STOP XX:XX:XX / XX:XX:XX'
       DC  AL2(L'LINE2)
       DC  CL58'IP ADDRESS TO/FROM: XXX.XXX.XXX.XXX / XXX.XXX.XXX.XXX'
       DC  AL2(L'LINE2A)
       DC  CL47'SUBCOMMAND: XXXX  FORMAT/XFER/STRUCTURE: XXXX '
       DC  AL2(L'LINE3)
       DC  CL31'BYTE COUNT: X,XXX,XXX,XXX'
       DC  AL2(L'LINE4)
       DC  CL54'DSNAME: XXXXXXXX.XXXXXXXX.XXXXXXXX.XXXXXXXX.XXXXXXXX'
SMF76WTO WTO   TEXT=((,L),(,D),(,D),(,D),(,D),(,DE)),DESC=12,          X
               MCSFLAG=HRDCPY,MF=L
INWTOL   EQU   *-INWTO
         SPACE 2
         LTORG
         SPACE 2
         TITLE 'DYNAMIC AREA DEFINITIONS'
WK118    DSECT
         DS    18F
STWTOS   WTO   'U83FTPI TYPE 118 RECORD LOCATED.  XX   ',              X
               MF=L LINKAGE=BRANCH
EDWK1    DS    D
EDWK2    DS    CL16
*----------------------------------------------------------
*---     MLWTO AREA IN THE WORK DSECT
*----------------------------------------------------------
HDRL   DC  AL2(L'HDR)
HDR    DC  CL35'U83FTPI ----- FTP STATISTICS -----'
DL1    DC  AL2(L'LINE1)
LINE1 DC CL58'ID: XXXXXXXX  TIME START/STOP XX:XX:XX.XX / XX:XX:XX.XX'
DL2    DC  AL2(L'LINE2)
LINE2  DC  CL58'IP ADDRESS TO/FROM: XXX.XXX.XXX.XXX / XXX.XXX.XXX.XXX'
DL2A   DC  AL2(L'LINE2A)
LINE2A DC  CL47'SUBCOMMAND: XXXX  FORMAT/XFER/STRUCTURE: XXXX '
DL3    DC  AL2(L'LINE3)
LINE3  DC  CL31'BYTE COUNT: X,XXX,XXX,XXX'
DL4    DC  AL2(L'LINE4)
LINE4  DC  CL54'DSNAME: XXXXXXXX.XXXXXXXX.XXXXXXXX.XXXXXXXX.XXXXXXXX'
FTPID    EQU   LINE1+4,8
FTPSTT   EQU   LINE1+31
FTPEDT   EQU   LINE1+45
FTPIPI   EQU   LINE2+20
FTPIPO   EQU   LINE2+38
FTPCMD   EQU   LINE2A+12,4
FTPTYP   EQU   LINE2A+41,4
FTPBCT   EQU   LINE3+12,13
FTPDSN   EQU   LINE4+8,44
SMF76MSG WTO   TEXT=((,L),(,D),(,D),(,D),(,D),(,DE)),DESC=12,          X
               MCSFLAG=HRDCPY,MF=L
END118   DS    0D
WK118LEN EQU   *-WK118             AREA LENGTH
         SPACE 3
*---------------------------------------------------------------------*
*                DSECT FOR FTP SMF RECORDS (SUBTYPE 75)               *
*---------------------------------------------------------------------*
SMFFTP76 DSECT                    DSECT TO DESCRIBE FTP SMF RECORDS
SMFFTPHD DS    CL24               STANDARD SMF HEADER WITH SUBTYPES
*
SMFFTPCM DS    CL4                FTP SUBCOMMAND
*
SMFFTPTY DS    CL4                FTP SERVER=FILE TYPE (SEQ, JES, SQL)
*                             OR  FTP CLIENT=VALUE OF REPLY TO FTP CMD
*
SMFFTPSA DS    F                  FTP SERVER REMOTE IP ADDRESS   OR
*                                 FTP CLIENT LOCAL  IP ADDRESS
*
SMFFTPSL DS    F                  FTP SERVER LOCAL   IP ADDRESS  OR
*                                 FTP CLIENT FOREIGN IP ADDRESS
*
SMFFTPCP DS    CL2                FTP CLIENT LOCAL   PORT
SMFFTPCF DS    CL2                FTP CLIENT FOREIGN PORT
SMFFTPR2 DS    CL4                FTP CLIENT RESERVED
*
SMFFTPSU DS    CL8                FTP SERVER LOCAL  USER ID      OR
*                                 FTP CLIENT REMOTE USER ID
*
SMFFTPFM DS    CL1                FTP DATA FORMAT
SMFFTPFA EQU   C'A'                   A = ASCII
SMFFTPFE EQU   C'E'                   E = EBCDIC
SMFFTPFI EQU   C'I'                   I = IMAGE
*
SMFFTPMO DS    CL1                FTP MODE
SMFFTPMB EQU   C'B'                   B = BLOCK
SMFFTPMC EQU   C'C'                   C = COMPRESSED
SMFFTPMS EQU   C'S'                   S = STREAM
*
SMFFTPST DS    CL1                FTP STRUCTURE
SMFFTPSF EQU   C'F'                   F = FILE
*
SMFFTPDT DS    CL1                FTP DATA SET TYPE
SMFFTPDB EQU   C' '                   BLANK = SEQUENTIAL
SMFFTPDP EQU   C'P'                   P     = PDS
*
SMFFTTRS DS    F                  FTP START TIME OF TRANSMISSION
SMFFTTRE DS    F                  FTP END   TIME OF TRANSMISSION
SMFFTTBC DS    F                  FTP BYTE COUNT OF TRANSMISSION
*
SMFFTPXD DS    CL1                FTP ID
SMFFTPXC EQU   C'C'                   C = CLIENT
SMFFTPXS EQU   C'S'                   S = SERVER
*
SMFFTSLR DS    CL3                FTP SERVER - LAST REPLY TO CLIENT
*
SMFFTDSN DS    CL44               FTP DATA SET NAME
SMFFTMEM DS    CL8                FTP MEMBER NAME FOR PDS
SMFFTRS2 DS    CL8                FTP RESERVED FOR ABNORMAL END INFO.
*
SMFFTDS2 DS    CL44               FTP SECOND DATA SET NAME (IF NEEDED)
*                                 OR  FTP CLIENT LOCAL DIRECTORY
*
SMFFTMM2 DS    CL8                FTP SECOND MEMBER NAME FOR PDS
*
SMFFTSTC DS    CL8                FTP SERVER STARTED TASK QUALIFIER OR
*                                 FTP CLIENT USER ID OF FTP USER
*
SMFFTHST DS    CL8                FTP TCP/IP HOST NAME
*
SMFFTSRP DS    CL2                FTP SERVER REMOTE PORT NUMBER
SMFFTSLP DS    CL2                FTP SERVER LOCAL  PORT NUMBER
SMFFTOF1 DS    AL2                OFFSET TO THE FIRST HFS FILE FIELD
SMFFTOF2 DS    AL2                OFFSET TO THE SECOND HFS FILE FIELD
SMFFTBYF DS    XL8                BYTES TRANSFERRED COUNTER IN DOUBLE
SMFFTGIG DS    AL4                4-GIGABYTE COUNTER FOR BYTEXFER
SMFFTVAR DS    0C                 VAR-LENGTH FIELDS
*
         END
