MSGNOLOG TITLE 'MPF EXIT TO DELETE WTO MESSAGE FROM SYSLOG AND JOBLOG'
*
*        PURPOSE: TO SUPPRESS WTO MESSAGE DISPLAY ON THE SYSTEM
*                 LOG AS WELL AS THE JOB LOG.
*                 THIS ALLOWS INSTALLATION CODE SUCH AS SMF EXITS
*                 LIKE IEFU83 TO SUPPLY NUMEROUS MESSAGES TO THE
*                 JOB MESSAGES DATA SET (ASSUMING ROUTCDE=11)
*                 WITHOUT CAUSING EXTRA LOG MESSAGE CLUTTER.
*
*       FUNCTION: TURN ON THE "FORCE NO HARDCOPY" BIT AS WELL AS
*                 THE "SUPPRESS FROM JOBLOG" BIT.
*                 SELECTIVITY IS PROVIDED BY THE ACTIVE MPFLST__
*                 MEMBER OF SYS1.PARMLIB.  IF 'SUP(YES)' IS SPECIFIED
*                 THEN THE MESSAGE WILL BE SHOWN ON THE SYSTEM LOG,
*                 UNLESS MCSFLAG=HRDCPY IS SPECIFIED WHEREUPON THE
*                 MESSAGE WILL BE DISPLAY ON THE SYSTEM CONSOLE(S).
*                 WTP MESSAGES CAN STILL BE TPUT UNDER TSO SUBJECT
*                 TO 'PROFILE WTPMSG' BEING ACTIVE.
*
*        WRITTEN: GREG PRICE OF FERNTREE COMPUTER SERVICES 08 OCT 1992.
*
*     ATTRIBUTES: REUS,RENT,REFR,AMODE=31,RMODE=ANY.
*
*         MACROS: IEZVX100 MAPPING MACRO FROM SYS1.MODGEN.
*
         SPACE 2
MSGNOLOG CSECT
MSGNOLOG AMODE 31                 31 BIT ADDRESSING MODE
MSGNOLOG RMODE ANY                31 BIT RESIDENCY MODE
         SPACE
         USING MSGNOLOG,R12
         STM   R14,R12,12(R13)    SAVE CALLER'S REGISTERS
         LR    R12,R15            ESTABLISH MODULE'S BASE REGISTER
         L     R9,0(R1)           R9==> WTO EXIT PARAMETER LIST
         USING CTXT,R9            ESTABLISH ADDRESSABLITY TO IT
         OI    CTXTRFB2,CTXTRNHC  FORCE NO HARDCOPY
         OI    CTXTERF3,CTXTESJL  SUPPRESS MESSAGE FROM THE JOBLOG
         DROP  R12,R9             MSGNOLOG, CTXT
         LM    R14,R12,12(R13)    RESTORE CALLER'S REGISTERS
         BR    R14                RETURN TO CALLER
         SPACE 2
         LTORG
         SPACE 2
         DS    0D                 END OF CSECT
         TITLE 'EQUATES AND DSECTS'
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE 2
         IEZVX100
         SPACE 2
         END   MSGNOLOG
