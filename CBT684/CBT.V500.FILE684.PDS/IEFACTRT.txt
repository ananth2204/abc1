ACTRT    TITLE 'JOB AND STEP TERMINATION STATISTICS ON SYSOUT'
*---------------------------------------------------------------------*
*---     THIS RELATIVELY POOR EXAMPLE OF THE PROGRAMMER'S ART      ---*
*---     GENERATES AND LISTS A REPORT OF SYSTEM RESOURCE USAGE     ---*
*---     FOR EACH STEP OF A JOB, STARTED TASK OR TSU AT STEP       ---*
*---     COMPLETION TIME.  FOR DOCUMENTATION ON THE INPUT          ---*
*---     PARAMETERS, SEE THE 'USER EXITS' SPL MANUAL.              ---*
*---                                                               ---*
*---     IN MVS4.3, LOOK IN MANUAL 'INSTALLATION EXITS',           ---*
*---     GC28-1637-05                                              ---*
*---                                                               ---*
*---     THIS EXIT CALLS MODULE IEFYS, WHICH IS LOCATED IN         ---*
*---     DISTRIBUTION LIBRARY SYS1.AOSB3. (LMOD IEFTB724)          ---*
*---------------------------------------------------------------------*
         MACRO
&LBL     $CLINE    &COMMENT
         AIF   ('&LBL' EQ '').NOLBL
&LBL     DS    0H
.NOLBL   ANOP
         MVI   LINE,C' '           START WITH A DASH
         MVC   LINE+1(119),LINE    FILL THE LINE
         MVI   LINE,C'¦'           MARK THE
         MVI   LINE+119,C'¦'         CORNERS
         MEND
         MACRO --- CONVERT BINARY TIME TO HH:MM:SS.TH
&LBL     $CVTIME  &OUT,&IN
         LCLC  &Z
         SPACE 1
&Z       SETC  '&LBL'
         AIF   ('&Z' EQ '').NOLBL
&Z       DS    0H                  DEFINE THE LABEL
.NOLBL   ANOP
         LA    15,&IN              LOAD POINTER TO INPUT FIELD
         ICM   0,15,0(15)          INSERT INPUT VALUE
         LA    14,&OUT
         XC    0(11,14),0(14)
         SRDA  0,32                AND SHIFT TO R1
         SR    0,0
         D     0,=F'10'            DIVIDE BY 10
         STC   0,10(,14)           STORE IN OUTPUT AREA
         SR    0,0
         D     0,=F'10'            DIVIDE BY 10
         STC   0,9(,14)            STORE IN OUTPUT AREA
         SR    0,0
         D     0,=F'10'            DIVIDE BY 10
         STC   0,7(,14)            STORE IN OUTPUT AREA
         SR    0,0
         D     0,=F'6'             DIVIDE BY 6
         STC   0,6(,14)            STORE IN OUTPUT AREA
         SR    0,0
         D     0,=F'10'            DIVIDE BY 10
         STC   0,4(,14)            STORE IN OUTPUT AREA
         SR    0,0
         D     0,=F'6'             DIVIDE BY 6
         STC   0,3(,14)            STORE IN OUTPUT AREA
         SR    0,0
         D     0,=F'10'            DIVIDE BY 10
         STC   0,1(,14)            STORE IN OUTPUT AREA
         STC   1,0(,14)            STORE IN OUTPUT AREA
         OC    0(11,14),=C'00:00:00.00' INSERT NUMERIC ZONES
         SPACE 1
         MEND
         MACRO
&LBL     $EDIT &TO,&FROM
         AIF   ('&LBL' EQ '').NOLBL
&LBL     DS    0H
.NOLBL   ANOP
         L     R0,&FROM            LOAD INPUT VALUE
         LA    R1,&TO              POINT TO OUTPUT AREA
         BAL   R14,EDIT
         MEND
         MACRO
&LBL     $EDITK &TO,&FROM
         AIF   ('&LBL' EQ '').NOLBL
&LBL     DS    0H
.NOLBL   ANOP
         L     R0,&FROM            LOAD INPUT VALUE
         LA    R1,&TO              POINT TO OUTPUT AREA
         BAL   R14,EDITK
         MEND
         MACRO --- PUT TO JCL MESSAGE DATA SET ---
&LBL     $PUTYS &LINE,&LEN
         LCLC  &LLL
&LLL     SETC  '&LBL'
         AIF   ('&LLL' EQ '').NOLBL
&LLL     DS    0H
.NOLBL   ANOP
         LA    R0,&LINE         POINT TO OUTPUT LINE
         ST    R0,36(,R12)      STORE OUTPUT AREA ADDRESS
         LA    R0,&LEN          LOAD OUTPUT LENGTH
         STH   R0,42(,R12)      STORE IN PUT PARM LIST
         L     R15,=V(IEFYS)    LOAD ROUTINE ADDRESS
         BALR  R14,R15          CALL MSG WRITER ROUTINE
         MEND
         TITLE 'JOB AND STEP TERMINATION STATISTICS ON SYSOUT'
IEFACTRT CSECT
IEFACTRT RMODE ANY
IEFACTRT AMODE 31
R0       EQU   0                   R
R1       EQU   1                    E
R2       EQU   2                     G
R3       EQU   3                      I
R4       EQU   4                       S
R5       EQU   5                        T
R6       EQU   6                         E
R7       EQU   7                          R
R8       EQU   8
R9       EQU   9                   E
R10      EQU   10                   Q
R11      EQU   11                    U
R12      EQU   12                     A
R13      EQU   13                      T
R14      EQU   14                       E
R15      EQU   15                        S
         USING IEFACTRT,R15        INITIAL BASE REGISTER
         B     PROLOG              BRANCH PAST ID STUFF
         DC    CL8'IEFACTRT'
         DC    CL8'&SYSTIME'
         DC    CL8'&SYSDATE'
PROLOG   DS    0H
         STM   R14,R12,12(R13)     SAVE ENTRY REGISTERS
         LR    R11,R15             COPY BASE ADDRESS
         LA    R10,2048
         LA    R10,2048(R10,R11)
         DROP  R15                 DROP THE SETUP BASE
         USING IEFACTRT,R11,R10    DEFINE THE TRUE BASE
         LR    R9,R1               COPY THE PARMLIST POINTER
         USING ACTRTPRM,R9         AND BASE ITS DSECT
         LR    R3,R0               AND ENTRY CODE
         GETMAIN RU,LV=WKLEN       GRAB WORK-AREA STORAGE
         LR    R4,R1               COPY ITS ADDRESS
         LR    R5,R0               AND LENGTH
         N     R5,=A(X'FFFFFF')    CLOBBER SUBPOOL NUMBER
         LA    R14,0               SOURCE ADDRESS = 0
         LA    R15,0               SOURCE LENGTH/PAD = ZEROS
         MVCL  R4,R14              CLEAR THE AREA TO ZEROS
         ST    R1,8(,R13)          ADD TO SAVE-AREA CHAIN
         ST    R13,4(,R1)          SAVE THE BACK POINTER
         LR    R13,R1              NEW SAVE AREA/WORK AREA
         USING WKAREA,R13          AND BASE ITS DSECT
         CH    R3,=H'20'           TYPE-30 RECORD COMING ??
         BNE   ENDITALL            NOPE ---
         L     R8,ACTADDR          POINT TO THE SMF RECORD
         USING SMFRCD30,R8         AND BASE THE FIRST DSECT
*---------------------------------------------------------------------*
*---     WE'RE STILL CONCERNED ABOUT THIS PARTICULAR AREA: WHAT    ---*
*---     IF THE JOB STEP HAS ENOUGH EXCP ENTRIES THAT THE RECORD   ---*
*---     OVERFLOWS TO A SECOND RECORD?  THIS AREA NEEDS            ---*
*---     ADDITIONAL RESEARCH AND TESTING.                          ---*
*---------------------------------------------------------------------*
         CLC   SMF30SON,=H'0'      ANY SUBSYSTEM SEGMENTS PRESENT ??
*        BE    IOSEG               NOPE; MUST BE JUST I/O STATS.
         L     R7,SMF30SOF         LOAD SUBSYSTEM OFFSET
         LA    R7,0(R7,R8)         POINT TO SUBSYSTEM SECTION
         MVI   JSIND,C'S'          SET TO DEFAULT OF STEP
         USING SMF30PSS,R7         AND BASE ITS SECTION
         CLC   SMF30TYP,=H'4'      STEP TOTALS ??
         BE    STEPE               YES ---
         CLC   SMF30TYP,=H'5'      JOB TERMINATION ??
         BE    JOBE                YES ---
         DROP  R7
         B     ENDITALL            NOTHING TO DO ---
*---------------------------------------------------------------------*
*---     START OF A  JOB-TERMINATION RECORD. PROCESS THE           ---*
*---     COMPLETION SEGMENT OF THE TYPE-30 SMF RECORD.             ---*
*---------------------------------------------------------------------*
JOBE     DS    0H
         MVI   JSIND,C'J'          INDICATE A JOB TERM RECORD
*---------------------------------------------------------------------*
*---     START OF A STEP-TERMINATION RECORD. PROCESS THE           ---*
*---     COMPLETION SEGMENT OF THE TYPE-30 SMF RECORD.             ---*
*---------------------------------------------------------------------*
STEPE    DS    0H
         CLC   SMF30TON,=H'0'      COMPLETION SECTION PRESENT ??
         BE    CEND                NOPE; SKIP THIS LINE
         L     R7,SMF30TOF         LOAD COMPLETION SECTION OFFSET
         LA    R7,0(R7,R8)         POINT TO COMPLETION SECTION
         USING SMF30CMP,R7         AND BASE THE FIELDS
*---------------------------------------------------------------------*
*---     BOX-TOP LINE                                              ---*
*---------------------------------------------------------------------*
         $CLINE                    CLEAR THE LINE AREA
         MVI   LINE+1,C'-'
         MVC   LINE+2(118),LINE+1
         MVI   LINE,C'+'
         MVI   LINE+119,C'+'
         $PUTYS LINE,120           OUTPUT THE CONSTRUCTED LINE
*---------------------------------------------------------------------*
*---     BOX TITLE LINE                                            ---*
*---------------------------------------------------------------------*
         $CLINE                    CLEAR THE LINE AREA
         MVC   LINE+4(L'M43),M43    MARK THE REPORT TYPE AS STEP
         CLI   JSIND,C'J'          IS THIS A JOB TERM RECORD?
         BNE   BTL1                NO - IT'S A STEP
         MVC   LINE+4(L'M44),M44    MARK THE REPORT TYPE AS JOB
BTL1     $PUTYS LINE,120           OUTPUT THE CONSTRUCTED LINE
*---------------------------------------------------------------------*
*---     SPACER LINE                                               ---*
*---------------------------------------------------------------------*
         $CLINE                    CLEAR THE LINE AREA
         $PUTYS LINE,120           OUTPUT THE CONSTRUCTED LINE
*---------------------------------------------------------------------*
*---     JOBNAME/STEPNAME/COMPLETION LINE                          ---*
*---------------------------------------------------------------------*
         CLC   SMF30TON,=H'0'      ANY COMPLETION SECTIONS ??
         BE    SKP0                NOPE ---
         MVC   LINE+7(L'M15),M15      MOVE IN THE JOB NAME/STEP NAME
         CLI   JSIND,C'J'          IS THIS A JOB TERM RECORD?
         BE    CEND                YES, DON'T NEED THE REST OF THIS
         MVC   LINE+28(L'M10),M10
         MVC   LINE+46(L'M37),M37
         TM    SMF30STI,X'20'      CANCELLED BY IEFUJI ??
         BNO   C1                  NOPE ---
         MVC   LINE+74(24),=CL24'CANCELLED BY IEFUJI EXIT'
         B     CEND                GO ADD JOB/STEP NAMES
C1       DS    0H
         TM    SMF30STI,X'10'      CANCELLED BY IEFUSI ??
         BNO   C2                  NOPE ---
         MVC   LINE+74(24),=CL24'CANCELLED BY IEFUSI EXIT'
         B     CEND                GO ADD JOB/STEP NAMES
C2       DS    0H
         TM    SMF30STI,X'08'      CANCELLED BY IEFACTRT ?
         BNO   C3                  NOPE ---
         MVC   LINE+74(26),=CL26'CANCELLED BY IEFACTRT EXIT'
         B     CEND                GO ADD JOB/STEP NAMES
C3       DS    0H
         TM    SMF30STI,X'01'      WAS STEP FLUSHED ??
         BZ    C3A                 NOPE ---
         MVC   LINE+74(22),=CL22'STEP WAS NOT EXECUTED.'
         B     CEND                AND SCAT ---
C3A      DS    0H
         TM    SMF30STI,X'02'      ABENDED STEP ??
         BO    C4                  YES ---
         LH    R0,SMF30SCC         LOAD THE COMPLETION/RETURN CODE
         CVD   R0,DWORK            DECIMALIZE IT
         OI    DWORK+7,X'0F'       FORCE LAST CHAR. NUMERIC
         UNPK  LINE+74(8),DWORK        UNPACK INTO THE LINE
         MVC   LINE+74(3),=CL3'RC '    SHOW IT'S A RETURN CODE
         B     CEND                GO ADD JOB/STEP NAMES
C4       DS    0H
         TM    SMF30SCC,X'80'      USER ABEND ??
         BO    C5                  YES ---
         MVC   LINE+74(11),=CL11'ABEND  S'
         ICM   R2,3,SMF30SCC       INSERT THE ABEND CODE
         SRDL  R2,4                SHIFT OFF A NIBBLE
         SRL   R3,4                INSERT SOME SPACE
         SRDL  R2,4                ANOTHER NIBBLE
         SRL   R3,4                AND ANOTHER SPACE
         SRDL  R2,4                ONE LAST
         SRL   R3,4                  TIME
         STCM  R3,14,LINE+74+8         STORE IN THE LINE
         TR    LINE+74+8(3),=C'0123456789ABCDEF'
         B     CEND                GO ADD JOB/STEP NAMES
C5       DS    0H
         LH    R0,SMF30SCC         LOAD THE CODE
         N     R0,=A(X'FFF')       TRIM OFF GARBAGE STUFF
         CVD   R0,DWORK            DECIMALIZE IT
         OI    DWORK+7,X'0F'       FINISH LAST ZONE
         UNPK  LINE+74(11),DWORK       ADD TO THE LINE
         MVC   LINE+74(7),=CL7'ABEND U' AND DATA LABEL
CEND     DS    0H
         DROP  R7
         CLC   SMF30ION,=H'0'      ANY ID SECTIONS ??
         BE    SKP0                NOPE ---
         L     R7,SMF30IOF         LOAD OFFSET OF ID SECTION
         LA    R7,0(R7,R8)         POINT TO ID SECTION
         USING SMF30ID,R7          AND BASE THE DSECT
         MVC   LINE+16(8),SMF30JBN     MOVE THE JOB NAME
         MVC   LINE+34(8),SMF30STM     AND STEP NAME
         $PUTYS LINE,120           OUTPUT THE CONSTRUCTED LINE
         $CLINE                    CLEAR THE LINE AREA
         $PUTYS LINE,120           OUTPUT THE CONSTRUCTED LINE
*---------------------------------------------------------------------*
*---     PROGRAM NAME AND STEP #                                  ---*
*---------------------------------------------------------------------*
         CLI   JSIND,C'J'          IS THIS A JOB TERM RECORD?
         BE    SKP0                YES, DON'T NEED THE REST OF THIS
         MVC   LINE+28(L'M54),M54     STEP #
         MVC   LINE+7(L'M55),M55      PROGRAM
         MVC   LINE+16(8),SMF30PGM    PROGRAM NAME
         LH    1,SMF30STN
         CVD   1,DWORK
         OI    DWORK+7,X'0F'        MAKE SURE SIGN IS 'F'
         UNPK  LINE+41(3),DWORK+6(2)
         CLI   LINE+41,X'F0'   BLANK
         BNE   BLANK1            OUT
         MVI   LINE+41,X'40'       ZERO
         CLI   LINE+42,X'F0'         DIGITS
         BNE   BLANK1                  FOR
         MVI   LINE+42,X'40'             READABILITY
BLANK1   $PUTYS LINE,120           OUTPUT THE CONSTRUCTED LINE
         $CLINE                    CLEAR THE LINE AREA
         $PUTYS LINE,120           OUTPUT THE CONSTRUCTED LINE
SKP0     DS    0H
         DROP  R7
*---------------------------------------------------------------------*
*---     START AND END TIMES AND STEP ELAPSED TIME                 ---*
*---------------------------------------------------------------------*
         CLC   SMF30ION,=H'0'      ANY ID SECTIONS ??
         BE    SKP1                NOPE ---
         L     R7,SMF30IOF         LOAD OFFSET OF ID SECTION
         LA    R7,0(R7,R8)         POINT TO ID SECTION
         USING SMF30ID,R7          AND BASE THE DSECT
         CLI   JSIND,C'J'          IS THIS A JOB TERM RECD?
         BE    JS7                 BUPASS THE NEXT TEST
         CLC   SMF30PPS,=F'0'      WAS STEP STARTED ??
         BE    EXCP6               NOPE ---
*
JS7      MVC   LINE+7(L'M21A),M21A     START DATE
         MVC   LINE+36(L'M19A),M19A    END DATE
         XC    WORK(L'WORK),WORK   CLEAR THE STORAGE
         MVC   WORK+8(4),SMF30STD  MOVE START DATE TO CONVERT AREA
         CONVTOD CONVVAL=WORK,TODVAL=TOD,TIMETYPE=BIN,                 X
               DATETYPE=YYDDD,MF=(E,CONVTOD)
         STCKCONV STCKVAL=TOD,CONVVAL=WORK,TIMETYPE=BIN,               X
               DATETYPE=MMDDYYYY,MF=(E,STCKCONV)
         MVC   TOD(4),WORK+8
         MVI   TOD+4,X'0F'
         UNPK  WORK(9),TOD(5)
         MVC   LINE+19(2),WORK
         MVI   LINE+21,C'/'
         MVC   LINE+22(2),WORK+2
         MVI   LINE+24,C'/'
         MVC   LINE+25(4),WORK+4
         XC    WORK(L'WORK),WORK   CLEAR THE STORAGE
*
         STCK  TOD
         STCKCONV STCKVAL=TOD,CONVVAL=WORK,TIMETYPE=BIN,               X
               DATETYPE=MMDDYYYY,MF=(E,STCKCONV)
         MVC   TOD(4),WORK+8
         MVI   TOD+4,X'0F'
         UNPK  WORK(9),TOD(5)
         MVC   LINE+47(2),WORK
         MVI   LINE+49,C'/'
         MVC   LINE+50(2),WORK+2
         MVI   LINE+52,C'/'
         MVC   LINE+53(4),WORK+4
         $PUTYS LINE,120           OUTPUT THE CONSTRUCTED LINE
         $CLINE                    CLEAR THE LINE AREA
*
         TIME  BIN                 GET THE CURRENT TIME
         STM   R0,R1,NOW           SAVE FOR FORMATTING
         MVC   LINE+7(L'M21),M21      START TIME
         MVC   LINE+36(L'M19),M19      STOP TIME
         MVC   LINE+60(L'M45),M45      ELAPSED TIME
         $CVTIME LINE+19,SMF30PPS      DO THE START TIME OF STEP
         CLI   JSIND,C'J'          IS THIS A JOB TERM RECORD?
         BNE   JS2                 NO - SKIP CONVERT
         $CVTIME LINE+19,SMF30SIT      DO THE START TIME OF JOB
JS2      $CVTIME LINE+47,NOW           END TIME IS SMF RECORD TIME
         ZAP   DWORK,SMF30STD      COPY DATE TO MY AREA
         CVB   R0,DWORK            CONVERT TO BINARY
         SRDA  R0,32               SHIFT TO DIVIDENT REGISTER
         D     R0,=F'1000'         SEPARATE YEARS FROM DAYS
         LR    R15,R0              SAVE THE NUMBER OF DAYS
         LR    R0,R1               COPY THE YEAR VALUE
         MH    R0,=H'365'          TIMES DAYS/YEAR
         SRL   R1,2                YEAR/4 = LEAP DAYS
         AR    R15,R1               ADD ---
         AR    R15,R0                   ADD AGAIN ---
         ZAP   DWORK,NOW+4(4)      COPY DATE TO MY AREA
         CVB   R0,DWORK            CONVERT TO BINARY
         SRDA  R0,32               SHIFT TO DIVIDEND REGISTER
         D     R0,=F'1000'         SEPARATE YEARS FROM DAYS
         LR    R14,R0              SAVE THE NUMBER OF DAYS
         LR    R0,R1               COPY THE YEAR VALUE
         MH    R0,=H'365'          TIMES DAYS/YEAR
         SRL   R1,2                YEAR/4 = LEAP DAYS
         AR    R14,R1              ADD ---
         AR    R14,R0                 ADD AGAIN ---
         SR    R14,R15             SUBTRACT THEN FROM NOW
         SRDA  R14,32              SHIFT FOR MULTIPLY
         M     R14,=F'8640000'     TIMES .01 SEC CHUNKS
         ICM   R0,15,NOW           LOAD END TIME
         CLI   JSIND,C'J'          IS THIS A JOB TERM RECORD?
         BNE   JS3                 NO USE STEP TIME
         S     R0,SMF30SIT         SUBTRACT JOB START TIME
         B     JS4                 GO AROUND NEXT INSTRUCTION
JS3      S     R0,SMF30PPS         SUBTRACT STEP START TIME
JS4      AR    R0,R15              ADD THE DAYS' WORTH
         ST    R0,DWORK            SAVE IN A WORK AREA
         $CVTIME LINE+69,DWORK         ELAPSED TIME
         $PUTYS LINE,120           OUTPUT THE CONSTRUCTED LINE
         $CLINE                    CLEAR THE LINE AREA
         $PUTYS LINE,120           OUTPUT THE CONSTRUCTED LINE
         DROP  R7
SKP1     DS    0H
*---------------------------------------------------------------------*
*---     CPU TIME FOR STEP TCB,SRB AND VECTOR FACILITY             ---*
*---------------------------------------------------------------------*
         CLC   SMF30CON,=H'0'      IS THERE A PROCESSOR SECTION ??
         BE    SKPPROC             NOPE ---
         L     R7,SMF30COF         LOAD SECTION OFFSET
         LA    R7,0(R7,R8)         POINT TO THE SECTION
         USING SMF30CAS,R7         AND BASE THE DSECT
         $CLINE                    CLEAR THE LINE AREA
         MVC   LINE+7(L'M25),M25
         MVC   LINE+18(L'M25A),M25A  STEP:
         CLI   JSIND,C'J'          IS THIS A JOB TERM RECORD?
         BNE   JS8                 YES- BYPASS A LOT OF STUFF
         MVC   LINE+18(L'M25B),M25B  JOB:
JS8      MVC   LINE+25(L'M2),M2
         MVC   LINE+49(L'M1),M1
         MVC   LINE+73(L'M11),M11
         $CVTIME LINE+30,SMF30CPT
         $CVTIME LINE+54,SMF30CPS
         $CVTIME LINE+81,SMF30JVU
         $PUTYS LINE,120           OUTPUT THE CONSTRUCTED LINE
         $CLINE                    CLEAR THE LINE AREA
         CLI   JSIND,C'J'          IS THIS A JOB TERM RECORD?
         BE    JS5                 YES- BYPASS A LOT OF STUFF
         MVC   LINE+18(L'M8),M8
         MVC   LINE+25(L'M2),M2
         MVC   LINE+49(L'M1),M1
         MVC   LINE+73(L'M11),M11
         $CVTIME LINE+30,SMF30ICU
         $CVTIME LINE+54,SMF30ISB
         $CVTIME LINE+81,SMF30IVU
         $PUTYS LINE,120           OUTPUT THE CONSTRUCTED LINE
         $CLINE                    CLEAR THE LINE AREA
         $PUTYS LINE,120           OUTPUT THE CONSTRUCTED LINE
         DROP  R7
SKPPROC  DS    0H
*---------------------------------------------------------------------*
*---     PAGING ACTIVITY: 4 LINES FOR NOW, MAY BECOME 5            ---*
*---------------------------------------------------------------------*
         CLC   SMF30RON,=H'0'      SECTION PRESENT ??
         BZ    SKPPAGE             NOPE ---
         L     R7,SMF30ROF         LOAD ITS OFFSET
         LA    R7,0(R7,R8)         POINT TO THE SECTION
         USING SMF30SAP,R7         AND BASE THE SECTION'S DSECT
         $CLINE                    CLEAR THE LINE AREA
         MVC   LINE+7(L'M29),M29      SECTION HEADER
         $PUTYS LINE,120           OUTPUT THE CONSTRUCTED LINE
         $CLINE                    CLEAR THE LINE AREA
         MVC   LINE+14(L'M23),M23      'PRIVATE AREA'
         MVC   LINE+44(L'M16),M16      'PAGEINS, '
         MVC   LINE+69(L'M18),M18      'PAGEOUTS, '
*        MVC   LINE+95(L'M22),M22      'PAGE RECLAIMS '
         $EDIT LINE+29,SMF30PGI        PAGEIN COUNT
         $EDIT LINE+54,SMF30PGO        PAGEOUT COUNT
*        $EDIT LINE+80,SMF30REC        RECLAIM COUNT
         $PUTYS LINE,120           OUTPUT THE CONSTRUCTED LINE
         $CLINE                    CLEAR THE LINE AREA
         MVC   LINE+14(L'M7),M7        'VIO:'
         MVC   LINE+44(L'M16),M16      'PAGEINS, '
         MVC   LINE+69(L'M18),M18      'PAGEOUTS, '
         MVC   LINE+95(L'M22),M22      'PAGE RECLAIMS '
         $EDIT LINE+29,SMF30VPI        PAGEIN COUNT
         $EDIT LINE+54,SMF30VPO        PAGEOUT COUNT
         $EDIT LINE+80,SMF30VPR        RECLAIM COUNT
         $PUTYS LINE,120           OUTPUT THE CONSTRUCTED LINE
         $CLINE                    CLEAR THE LINE AREA
         MVC   LINE+14(L'M13),M13      'COMMON:'
         MVC   LINE+44(L'M16),M16      'PAGEINS, '
*        MVC   LINE+95(L'M22),M22      'PAGE RECLAIMS '
         $EDIT LINE+29,SMF30CPI        PAGEIN COUNT
*        $EDIT LINE+80,SMF30CPR        PAGEOUT COUNT
         $PUTYS LINE,120           OUTPUT THE CONSTRUCTED LINE
         $CLINE                    CLEAR THE LINE AREA
         MVC   LINE+14(L'M12),M12      'SWAPS:'
         MVC   LINE+44(L'M30),M30      'PAGES IN,'
         MVC   LINE+69(L'M31),M31      'PAGES OUT,'
         MVC   LINE+95(L'M26),M26      'SWAP SEQUENCES'
         $EDIT LINE+29,SMF30PSI        PAGES SWAPPED IN
         $EDIT LINE+54,SMF30PSO        PAGES SWAPPED OUT
         $EDIT LINE+80,SMF30NSW        SWAP SEQUENCES
         $PUTYS LINE,120           OUTPUT THE CONSTRUCTED LINE
         $CLINE                    CLEAR THE LINE AREA
         MVC   LINE+44(L'M41),M41      'PAGE STEALS'
         $EDIT LINE+29,SMF30PST        PAGESTEAL COUNT
         $PUTYS LINE,120           OUTPUT THE CONSTRUCTED LINE
         $CLINE                    CLEAR THE LINE AREA
         $PUTYS LINE,120           OUTPUT THE CONSTRUCTED LINE
*---------------------------------------------------------------------*
*---     STORAGE USED/REQUESTED                                    ---*
*---------------------------------------------------------------------*
         MVC   LINE+7(L'M32),M32   SECTION HEADER
         $PUTYS LINE,120           OUTPUT THE CONSTRUCTED LINE
         $CLINE                    CLEAR THE LINE AREA
         MVC   LINE+15(L'M46),M46  'AVAILABLE:'
         MVC   LINE+27(L'M47),M47  'PRIVATE AREA:'
         MVC   LINE+57(L'M48),M48  'EXTENDED PRIVATE AREA:'
         L     R0,SMF30RGB         LOAD THE BYTE COUNT
         SRL   R0,10               DIVIDE BY 1K (1024)
         ST    R0,DWORK            SAVE FOR CONVERSION
         $EDITK LINE+40,DWORK      CONVERT INTO THE LINE
         L     R0,SMF30ERG         LOAD THE BYTE COUNT
         SRL   R0,10               DIVIDE BY 1K (1024)
         ST    R0,DWORK            SAVE FOR CONVERSION
         $EDITK LINE+80,DWORK      CONVERT INTO THE LINE
         MVC   LINE+49(2),=C'K,'   ADD THE
         MVC   LINE+89(2),=C'K '     UNIT TAGS
         $PUTYS LINE,120           OUTPUT THE CONSTRUCTED LINE
         $CLINE                    CLEAR THE LINE AREA
         MVC   LINE+15(L'M49),M49  'USED:'
         MVC   LINE+27(L'M47),M47  'PRIVATE AREA:'
         MVC   LINE+57(L'M48),M48  'EXTENDED PRIVATE AREA:'
         L     R0,SMF30URB         LOAD BYTE COUNT
         SRL   R0,10               DIVIDE BY 1K (1024)
         ST    R0,DWORK            SAVE FOR CONVERSION
         $EDITK LINE+40,DWORK      CONVERT INTO THE LINE
         L     R0,SMF30EUR         LOAD THE BYTE COUNT
         SRL   R0,10               DIVIDE BY 1K (1024)
         ST    R0,DWORK            SAVE FOR CONVERSION
         $EDITK LINE+80,DWORK      CONVERT INTO THE LINE
         MVC   LINE+49(2),=C'K,'   ADD THE
         MVC   LINE+89(2),=C'K '     UNIT TAGS
         $PUTYS LINE,120           OUTPUT THE CONSTRUCTED LINE
         $CLINE                    CLEAR THE LINE AREA
         MVC   LINE+27(L'M51),M51  'PRIVATE AREA:'
         MVC   LINE+57(L'M52),M52  'EXTENDED PRIVATE AREA:'
         L     R0,SMF30ARB         LOAD BYTE COUNT
         SRL   R0,10               DIVIDE BY 1K (1024)
         ST    R0,DWORK            SAVE FOR CONVERSION
         $EDITK LINE+40,DWORK      CONVERT INTO THE LINE
         L     R0,SMF30EAR         LOAD THE BYTE COUNT
         SRL   R0,10               DIVIDE BY 1K (1024)
         ST    R0,DWORK            SAVE FOR CONVERSION
         $EDITK LINE+80,DWORK      CONVERT INTO THE LINE
         MVC   LINE+49(2),=C'K,'   ADD THE
         MVC   LINE+89(2),=C'K '     UNIT TAGS
         $PUTYS LINE,120           OUTPUT THE CONSTRUCTED LINE
         $CLINE                    CLEAR THE LINE AREA
         MVC   LINE+15(L'M50),M50  'REGION REQUESTED:'
         $EDITK LINE+40,SMF30RGN   EDIT IN THE REGION SIZE
         MVC   LINE+49(2),=C'K '   UNIT TAG
         $PUTYS LINE,120           OUTPUT THE CONSTRUCTED LINE
         $CLINE                    CLEAR THE LINE AREA
         TM    SMF30SFL,X'40'      IEFUSI MODIFY REGION ??
         BZ    NOUSI               NOPE ---
         MVC   LINE+15(L'M53),M53  'REGION MODIFIED ...'
         $PUTYS LINE,120           OUTPUT THE CONSTRUCTED LINE
JS5      $CLINE                    CLEAR THE LINE AREA
NOUSI    DS    0H
         $PUTYS LINE,120           OUTPUT THE CONSTRUCTED LINE
         DROP  R7
SKPPAGE  DS    0H
*---------------------------------------------------------------------*
*---     SERVICE UNITS USED                                        ---*
*---------------------------------------------------------------------*
         CLC   SMF30PON,=H'0'
         BE    SKPPERF
         L     R7,SMF30POF
         LA    R7,0(R7,R8)
         USING SMF30PRF,R7
         MVC   LINE+7(L'M33),M33   STEP: MESSAGE
         CLI   JSIND,C'J'          IS THIS A JOB TERM RECD?
         BNE   JS10                NOPE
         MVC   LINE+7(L'M33),M33A  JOB: MESSAGE
JS10     $PUTYS LINE,120           OUTPUT THE CONSTRUCTED LINE
         $CLINE                    CLEAR THE LINE AREA
         MVC   LINE+25(L'M6),M6
         MVC   LINE+52(L'M5),M5
         MVC   LINE+79(L'M3),M3
         MVC   LINE+106(L'M4),M4
         $EDIT LINE+10,SMF30CSU
         $EDIT LINE+37,SMF30SRB
         $EDIT LINE+64,SMF30IO
         $EDIT LINE+91,SMF30MSO
         $PUTYS LINE,120           OUTPUT THE CONSTRUCTED LINE
         $CLINE                    CLEAR THE LINE AREA
         MVC   LINE+52(L'M40),M40
         $EDIT LINE+37,SMF30SRV
         $PUTYS LINE,120           OUTPUT THE CONSTRUCTED LINE
         $CLINE                    CLEAR THE LINE AREA
         $PUTYS LINE,120           OUTPUT THE CONSTRUCTED LINE
         DROP  R7
SKPPERF  DS    0H
*---------------------------------------------------------------------*
*---     OPERATOR SECTION                                          ---*
*---------------------------------------------------------------------*
         CLI   JSIND,C'J'          IS THIS A JOB TERM RECORD?
         BE    JS6                 YES - WE ARE DONE
         CLC   SMF30OON,=H'0'      ANY OPERATOR SECTIONS ??
         BE    SKPOPER             NOPE ---
         L     R7,SMF30OOF
         LA    R7,0(R7,R8)
         USING SMF30OPS,R7
         CLC   SMF30PDM(24),=6F'0' ALL ZEROS ??
         BE    SKPOPER
         MVC   LINE+7(L'M34),M34
         $PUTYS LINE,120           OUTPUT THE CONSTRUCTED LINE
         $CLINE                    CLEAR THE LINE AREA
         CLC   SMF30PTM(8),=2F'0'
         BE    NOTAPE
         MVC   LINE+11(L'M39),M39
         MVC   LINE+60(L'M36),M36
         $EDIT LINE+38,SMF30PTM
         $EDIT LINE+83,SMF30TPR
         $PUTYS LINE,120           OUTPUT THE CONSTRUCTED LINE
         $CLINE                    CLEAR THE LINE AREA
NOTAPE   DS    0H
         CLC   SMF30PDM(8),=2F'0'
         BE    NODISK
         MVC   LINE+11(L'M38),M38
         MVC   LINE+60(L'M35),M35
         $EDIT LINE+38,SMF30PDM
         $EDIT LINE+83,SMF30PRD
         $PUTYS LINE,120           OUTPUT THE CONSTRUCTED LINE
         $CLINE                    CLEAR THE LINE AREA
NODISK   DS    0H
         CLC   SMF30MTM(8),=2F'0'
         BE    NOMSS
         MVC   LINE+11(L'M38A),M38A
         MVC   LINE+60(L'M35A),M35A
         $EDIT LINE+38,SMF30MTM
         $EDIT LINE+83,SMF30MSR
         $PUTYS LINE,120           OUTPUT THE CONSTRUCTED LINE
         $CLINE                    CLEAR THE LINE AREA
NOMSS    DS    0H
         $PUTYS LINE,120           OUTPUT THE CONSTRUCTED LINE
         DROP  R7
SKPOPER  DS    0H
*---------------------------------------------------------------------*
*---     EXCP STATISTICS SECTION                                   ---*
*---                                                               ---*
*---     THIS GETS RATHER HAIRY HERE; WE HAVE TO WORK BETWEEN TWO  ---*
*---     'ARRAYS' WITHOUT OVERFLOWING EITHER ONE.  THE FIRST       ---*
*---     ARRAY IS THE EXCP SECTION OF THE SMF RECORD AND THE       ---*
*---     SECOND IS THE PRINT LINE.  IN ADDITION, WE NEED TO        ---*
*---     CONSIDER THE FACT THE THE EXCP SECTION MAY BE EXTENDED    ---*
*---     TO MULTIPLE FOLLOWING RECORDS AS WELL AND WE DON'T        ---*
*---     REALLY KNOW WHAT ELSE IS PRESENT IN THOSE RECORDS.  AND   ---*
*---     I HAVEN'T EVEN CONSIDERED THE JOB TERMINATION RECORDS,    ---*
*---     YET.                                                      ---*
*---------------------------------------------------------------------*
         CLC   SMF30EON,=H'0'      ANY EXCP SEGMENTS ??
         BE    EXCP6               NOPE ---
         MVC   LINE+7(L'M27),M27   SECTION HEADER
         MVC   LINE+25(L'M42),M42  DETAIL DESCRIPTOR
         $PUTYS LINE,120           OUTPUT THE CONSTRUCTED LINE
         $CLINE                    CLEAR THE LINE AREA
EXCPST   DS    0H
         L     R7,SMF30EOF         LOAD SECTION OFFSET
         LA    R7,0(R7,R8)         POINT TO THE SECTION
         USING SMF30EXP,R7         BASE THE SEGMENT'S DSECT
         LH    R4,SMF30ELN         LOAD SEGMENT LENGTH
         LR    R5,R4               COPY THE LENGTH
         MH    R5,SMF30EON         TIMES NUMBER OF SEGMENTS
         LA    R5,0(R5,R7)         POINT TO LAST SEGMENT
         SR    R5,R4               BACK UP TO LAST ENTRY
         LA    R3,LINE+4           POINT TO FIRST POSITION
         LA    R6,LINE+81          AND LAST POSITION
         $CLINE                    CLEAR THE LINE AREA
EXCP1    DS    0H
         CLC   SMF30DEV,=H'0'      NULL DEVTYPE ??
         BE    EXCP5               YES; VIO OR DUMMY
         MVC   15(8,R3),SMF30DDN   COPY THE DDNAME
         ICM   R0,3,SMF30CUA       INSERT UNIT ADDRESS
         SRDL  R0,4
         SRL   R1,4
         SRDL  R0,4
         SRL   R1,4
         SRDL  R0,4
         SRL   R1,4
         SRDL  R0,4
         SRL   R1,4
         STCM  R1,B'1111',24(R3)   STORE ADDRESS IN LINE
         TR    24(4,R3),=C'0123456789ABCDEF'
         $EDIT 0(,R3),SMF30BLK     EDIT IN THE BLOCK COUNT
         MVC   29(4,R3),=CL4'XXXX' STARTING DEVICE TYPE
         LA    R1,DEVTAB           POINT TO DEVICE TYPE TABLE
         LA    R14,6               AND ENTRY LENGTH
         LA    R15,DEVTABE         POINT TO LAST ENTRY
EXCP2    DS    0H
         CLC   SMF30DEV(2),0(R1)   IS THIS A MATCH ??
         BNE   EXCP3               NOPE ---
         MVC   29(4,R3),2(R1)      YES; TRANSLATE IT
         B     EXCP4               AND SCOOT ---
EXCP3    DS    0H
         BXLE  R1,R14,EXCP2        AND LOOP, SEARCHINGLY
EXCP4    DS    0H
         LA    R3,38(,R3)          POINT TO NEXT LINE AREA
         CR    R3,R6               FAR ENOUGH ??
         BNH   EXCP5               NOPE ---
         $PUTYS LINE,120           OUTPUT THE CONSTRUCTED LINE
         $CLINE                    CLEAR THE LINE AREA
         LA    R3,LINE+4           RELOAD STARTING POINTER
EXCP5    DS    0H
         BXLE  R7,R4,EXCP1         AND LOOP ---
         CLI   LINE+22,C' '        ANYTHING IN THIS LINE
         BE    EXCP6               NOPE ---
         $PUTYS LINE,120           OUTPUT THE CONSTRUCTED LINE
         $CLINE                    CLEAR THE LINE AREA
EXCP6    DS    0H
         CLC   SMF30EOR,=H'0'      ANY IN SUBSEQUENT RECORDS ??
         BNE   EXCP7               YES ---
JS6      MVI   LINE,C'+'
         MVI   LINE+1,C'-'
         MVC   LINE+2(117),LINE+1
         MVI   LINE+119,C'+'
         $PUTYS LINE,120           OUTPUT THE CONSTRUCTED LINE
         $CLINE                    CLEAR THE LINE AREA
EXCP7    DS    0H
ENDITALL DS    0H
         LR    R1,R13              COPY MY WORK-AREA ADDRESS
         L     R13,4(,R13)
         FREEMAIN RU,A=(1),LV=WKLEN RELEASE THE STORAGE
         LM    R14,R12,12(R13)     RESTORE THE REGISTERS
         LA    R15,0               SET ZERO RETURN CODE
         LA    R1,0                RETAIN THIS SMF RECORD
         BR    R14                 AND RETURN TO SMF
EDITK    DS    0H
         CVD   R0,DWORK
         MVC   EDWORK,MASKA        COPY THE EDIT MASK
         ED    EDWORK,DWORK        ACTUAL EDIT
         MVC   0(9,R1),EDWORK+11   MOVE TO OUTPUT FIELD
         BR    R14                 AND RETURN TO LOCAL CALLER
EDIT     DS    0H
         CVD   R0,DWORK
         MVC   EDWORK,MASKA        COPY THE EDIT MASK
         ED    EDWORK,DWORK        ACTUAL EDIT
         MVC   0(14,R1),EDWORK+6   MOVE TO OUTPUT FIELD
         BR    R14                 AND RETURN TO LOCAL CALLER
MASKA    DS    0X
         DC    C' '
         DC    X'202020'
         DC    C','
         DC    X'202020'
         DC    C','
         DC    X'202020'
         DC    C','
         DC    X'202020'
         DC    C','
         DC    X'202120'
M1       DC    CL03'SRB'
M2       DC    CL03'TCB'
M3       DC    CL04'IOC,'
M4       DC    CL03'MSO'
M5       DC    CL04'SRB,'
M6       DC    CL04'TCB,'
M7       DC    CL04'VIO:'
M8       DC    CL05'INIT:'
M9       DC    CL05'PLPA:'
M10      DC    CL05'STEP:'
M11      DC    CL06'VECTOR'
M12      DC    CL06'SWAPS:'
M13      DC    CL07'COMMON:'
M14      DC    CL07'SYSTEM:'
M15      DC    CL08'JOBNAME:'
M16      DC    CL08'PAGEINS,'
M17      DC    CL09'E/SYSTEM:'
M18      DC    CL09'PAGEOUTS,'
M19      DC    CL9'END TIME:'
M19A     DC    CL9'END DATE:'
M20      DC    CL11'E/PVT AREA:'
M21      DC    CL11'START TIME:'
M21A     DC    CL11'START DATE:'
M22      DC    CL13'PAGE RECLAIMS'
M23      DC    CL13'PRIVATE AREA:'
M24      DC    CL14'PVT AREA USED:'
M25      DC    CL9'CPU TIME:'
M25A     DC    CL5'STEP:'
M25B     DC    CL5' JOB:'
M26      DC    CL14'SWAP SEQUENCES'
M27      DC    CL16'DEVICE ACTIVITY:'
M28      DC    CL16'E/PVT AREA USED:'
M29      DC    CL30'PAGING AND SWAPPING ACTIVITY:'
M30      DC    CL9'PAGES IN,'
M31      DC    CL10'PAGES OUT,'
M32      DC    CL31'STORAGE AVAILABILITY AND USAGE:'
M33      DC    CL32'SERVICE UNITS USED BY THIS STEP:'
M33A     DC    CL32'SERVICE UNITS USED BY THIS JOB: '
M34      DC    CL21'DEVICE MOUNTS ISSUED:'
M35      DC    CL22'SPECIFIC DASD VOLUMES:'
M35A     DC    CL21'SPECIFIC MSS VOLUMES:'
M36      DC    CL22'SPECIFIC TAPE VOLUMES:'
M37      DC    CL26'CONDITION/COMPLETION CODE:'
M38      DC    CL26'NON-SPECIFIC DASD VOLUMES:'
M38A     DC    CL26'NON-SPECIFIC MSS VOLUMES:'
M39      DC    CL26'NON-SPECIFIC TAPE VOLUMES:'
M40      DC    CL19'TOTAL SERVICE UNITS'
M41      DC    CL35'PAGE STEALS FROM THIS ADDRESS SPACE'
M42      DC    CL42'(BLOCK COUNT, DDNAME, DEVICE, DEVICE TYPE)'
M43      DC    CL27'STEP TERMINATION STATISTICS'
M44      DC    CL27'JOB TERMINATION STATISTICS '
M45      DC    CL9'ELAPSED: '
M46      DC    CL10'AVAILABLE:'
M47      DC    CL13'PRIVATE AREA:'
M48      DC    CL22'EXTENDED PRIVATE AREA:'
M49      DC    CL5'USED:'
M50      DC    CL17'REGION REQUESTED:'
M51      DC    CL13'SYSTEM  AREA:'
M52      DC    CL22'EXTENDED SYSTEM  AREA:'
M53      DC    CL46'--- REGION REQUEST MODIFIED BY IEFUSI EXIT ---'
M54      DC    CL12'STEP NUMBER:'
M55      DC    CL08'PROGRAM:'
DEVTAB   DS    0H
         DC    X'200F',CL4'3390'
         DC    X'8081',CL4'3490'
         DC    X'8080',CL4'3480'
         DC    X'8003',CL4'3400'
         DC    X'200E',CL4'3380'
         DC    X'0400',CL4'OCR'
         DC    X'080A',CL4'1443'
         DC    X'080B',CL4'3203'
         DC    X'080C',CL4'3525'
         DC    X'080E',CL4'3800'
         DC    X'0801',CL4'2540'
         DC    X'0802',CL4'2540'
         DC    X'0804',CL4'2501'
         DC    X'0805',CL4'2520'
         DC    X'0806',CL4'3505'
         DC    X'0808',CL4'1403'
         DC    X'0809',CL4'3211'
         DC    X'081A',CL4'1285'
         DC    X'081B',CL4'1287'
         DC    X'081C',CL4'1288'
         DC    X'081D',CL4'1419'
         DC    X'081E',CL4'1419'
         DC    X'081F',CL4'1275'
         DC    X'0810',CL4'2671'
         DC    X'0811',CL4'4245'
         DC    X'0813',CL4'4248'
         DC    X'0816',CL4'3890'
         DC    X'0817',CL4'3886'
         DC    X'0818',CL4'2495'
         DC    X'0819',CL4'3895'
         DC    X'0820',CL4'1052'
         DC    X'0821',CL4'2150'
         DC    X'0822',CL4'3210'
         DC    X'0823',CL4'3215'
         DC    X'0830',CL4'3215'
         DC    X'084C',CL4'3838'
         DC    X'0842',CL4'3851'
         DC    X'0844',CL4'3540'
         DC    X'100A',CL4'3284'
         DC    X'100B',CL4'3286'
         DC    X'100C',CL4'3158'
         DC    X'100D',CL4'3036'
         DC    X'100E',CL4'3138'
         DC    X'100F',CL4'3148'
         DC    X'1002',CL4'2250'
         DC    X'1003',CL4'2260'
         DC    X'1004',CL4'1053'
         DC    X'1005',CL4'2280'
         DC    X'1006',CL4'2282'
         DC    X'1007',CL4'MD85'
         DC    X'1008',CL4'3066'
         DC    X'1009',CL4'327X'
         DC    X'200A',CL4'3340'
         DC    X'200B',CL4'3350'
         DC    X'200C',CL4'3375'
         DC    X'200D',CL4'3330'
         DC    X'2001',CL4'2311'
         DC    X'2002',CL4'2301'
         DC    X'2003',CL4'2303'
         DC    X'2004',CL4'2302'
         DC    X'2005',CL4'2321'
         DC    X'2006',CL4'2305'
         DC    X'2007',CL4'2305'
         DC    X'2008',CL4'2314'
         DC    X'2009',CL4'3330'
         DC    X'40F1',CL4'3791'
         DC    X'4001',CL4'2702'
         DC    X'4002',CL4'2701'
         DC    X'4003',CL4'2703'
         DC    X'4004',CL4'2955'
         DC    X'4005',CL4'3705'
         DC    X'4100',CL4'CTC'
         DC    X'8001',CL4'2400'
DEVTABE  DS    0H
         DC    X'8001',CL4'2400'
         LTORG
ACTRTPRM DSECT
ACTCXPA  DS    A                   COMMON EXIT PARM AREA
ACTSTEPN DS    A                   ADDRESS OF STEP NAME (CL8)
ACTPGMRN DS    A                   ADDRESS OF PGMR NAME (CL20)
ACTTCBAF DS    A                   ADDRESS OF CPUTIME/ACCT FLD COUNT
ACTACCT  DS    A                   ADDRESS OF JOB ACCT FIELDS
ACTTSBAF DS    A                   ADDRESS OF CPUTIME/STEP ACCT FLD CNT
ACTSACCT DS    A                   ADDRESS OF STEP ACCT FIELDS
ACTFLAG1 DS    A                   ADDRESS OF HALFWORD FLAGS
ACTTERM  DS    A                   ADDRESS OF TERM. STATUS (XL2)
ACTADDR  DS    A                   ADDRESS OF THE SMF RECORD
WKAREA   DSECT
SAVEAREA DS    18F
NOW      DS    D                   TIME AND DATE FROM 'TIME' MACRO
DWORK    DS    D
JSIND    DS    C                   JOB/STEP INDICATOR
EDWORK   DS    CL20
LINE     DS    CL133
         DS    0F                  ALIGN ON A WORD BOUNDARY
WORK     DS    XL16
TOD      DS    XL8
TODLEN   EQU   L'TOD+L'WORK
CONVTOD  CONVTOD MF=L
STCKCONV STCKCONV MF=L
ENDWK    DS    0D
WKLEN    EQU   ENDWK-WKAREA
         IFASMFR 30
         END
/*
//IEWL     EXEC PGM=IEWL,REGION=4M,COND=(8,LE),
//      PARM='LET,LIST,XREF,SIZE=(1400K,800K),NOTEST,RENT,REUS,AC(1)'
//SYSPRINT DD SYSOUT=*
//*YSLMOD  DD DISP=SHR,DSN=SYS0008.LINKLIB
//SYSLMOD  DD DISP=SHR,DSN=SYS1.PT.LINKLIB
//SYSUT1   DD UNIT=SYSALLDA,SPACE=(CYL,(1,2),RLSE)
//SYSLIN   DD DISP=(OLD,DELETE),DSN=&&OBJ
//         DD *
 INCLUDE SYSLIB(IEFTB724)
 NAME IEFACTRX(R)
//SYSLIB   DD DISP=SHR,DSN=SYS1.AOSB3
//*
