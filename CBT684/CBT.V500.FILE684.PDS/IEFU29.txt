IEFU29   TITLE 'SMF SWITCH EXIT ROUTINE                        '
*---------------------------------------------------------------------*
*---     NAME: IEFU29                                              ---*
*---                                                               ---*
*---     USAGE:  SMF EXIT ROUTINE TO START DUMP OF SMF WHEN SMF    ---*
*---           SWITCH COMMAND IS ISSUED.                           ---*
*---                                                               ---*
*---     METHOD:                                                   ---*
*---           1. ISSUE COMMAND 'START SMFDUMP,IMAGE=.........'    ---*
*---           2. ISSUE WTO THAT COMMAND HAS BEEN ISSUED.          ---*
*---                                                               ---*
*---     PATCH AREA: PATCH (UNUSED AND INTIALIZED TO               ---*
*---           BINARY ZEROES)                                      ---*
*---                                                               ---*
*---     ATTRIBUTES = KEY 0, REENTRANT, SUPERVISOR STATE, ENABLED, ---*
*---           NO LOCKS HELD                                       ---*
*---                                                               ---*
*---     ENTRY POINT:  IEFU29                                      ---*
*---                                                               ---*
*---     LINKAGE:  AT ENTRY, R1 POINTS TO FULLWORD ADDRESS OF THE  ---*
*---           SMF DATASET TO BE DUMPED.                           ---*
*---                                                               ---*
*---     EXIT: AT PROGRAM END VIA BSM 0,14                         ---*
*---                                                               ---*
*---     RETURN CODES: = ZERO EXIT NOPED JES NOT ACTIVE            ---*
*---           0 ZERO EXIT NO-OPED JES NOT ACTIVE                  ---*
*---           4 TO PREVENT SMF FROM ISSUING MESSAGE IEE362A OR    ---*
*---             IEE362I                                           ---*
*---                                                               ---*
*---     DATA AREAS: CVT, SMCA                                     ---*
*---                                                               ---*
*---     MACROS: SAVE, WTO, RETURN, GETMAIN, FREEMAIN              ---*
*---                                                               ---*
*---     MESSAGES: INFORM OPERATOR THAT COMMAND HAS BEEN ISSUED.   ---*
*---------------------------------------------------------------------*
         SPACE 3
IEFU29   CSECT ,                       CSECT NAME DECLARED
IEFU29   AMODE 31
IEFU29   RMODE ANY
*---------------------------------------------------------------------*
*---     REGISTER EQUATES                                          ---*
*---------------------------------------------------------------------*
R0       EQU   0                   R
R1       EQU   1                    E
R2       EQU   2                     G
R3       EQU   3                      I
R4       EQU   4                       S
R5       EQU   5                        T
R6       EQU   6                         E
R7       EQU   7                          R
R8       EQU   8
R9       EQU   9                   E
R10      EQU   10                   Q
R11      EQU   11                    U
R12      EQU   12                     A
R13      EQU   13                      T
R14      EQU   14                       E
R15      EQU   15                        S
         SPACE 3
*---------------------------------------------------------------------*
*---     STANDARD ENTRY LINKAGE (REENTRANT)                        ---*
*---------------------------------------------------------------------*
         B     28(,R15)            BRANCH AROUND
         DC    AL1(23),CL7'IEFU29'
         DC    CL8'&SYSTIME'
         DC    CL8'&SYSDATE'
         STM   R14,R12,12(R13)     SAVE ENTRY REGISTERS
         LR    R12,R15             COPY ENTRY POINT ADDRESS
         USING IEFU29,R12          DECLARE THE BASE REGISTER
         L     R8,0(R1)            SAVE INPUT PARM(DSNAME)
         GETMAIN RU,LV=DATALEN,SP=0,LOC=BELOW GET STORAGE
         ST    R13,4(,R1)          SAVE CALLER'S SAVE AREA ADDR
         ST    R1,8(,R13)          SAVE MY SAVE AREA ADDRESS
         LR    R13,R1              LOAD SAVE AREA ADDRESS
         USING DATA,R13            SET UP DATA AREA ADDRESSABILITY
*---------------------------------------------------------------------*
*---     LOCATE SYSTEM IDENTIFICATION VIA CVT AND SMCA             ---*
*---------------------------------------------------------------------*
         L     R5,CVTPTR               OBTAIN ADDRESS OF CVT
         USING CVTMAP,R5               TELL THE ASSEMBLER
         L     R5,CVTSMCA              @ OF SMF CONTROL AREA
         DROP  R5                      DONE USING THE CVT
         USING SMCABASE,R5             TELL THE ASSEMBLER
         MVC   SYSID,SMCASID           COPY SYSTEM IDENTIFIER
         DROP  R5                      DONE USING SMCA
*---------------------------------------------------------------------*
*---     ENQ TO SEE IF WE'RE ALREADY ACTIVE.                       ---*
*---------------------------------------------------------------------*
         MVC   ENQLIST(LENQLIST),ENQLSTX LOAD IN MODEL PARM LIST
         ENQ   MF=(E,ENQLIST)          TEST IF RESOURCE IN USE?
         LTR   R15,R15                 WAS THE RESOURCE AVAILABLE?
         BNZ   SKIPDUMP                NO, DO NOT START DUMP
*---------------------------------------------------------------------*
*---     CONVERT SYSID TO PARM VALUE FOR DDUMPSMF PROC             ---*
*---------------------------------------------------------------------*
         LM    R5,R7,SRCHPRM       LOAD BXLE PARMS
         B     CMPID1              BRANCH AROUND 'EYEBALLER'
         DC    CL42'ZAP TO 47F0.... TO NEGATE ID TRANSLATE -->'
CMPID1   NOP   CMPID3
         CLC   0(4,R5),SYSID       GOT A MATCH HERE ??
         BNE   CMPID2              NOPE ---
         MVC   SYSID,4(R5)         YES; TRANSLATE IT
         B     CMPID3              AND EXIT
CMPID2   DS    0H
         BXLE  R5,R6,CMPID1
CMPID3   DS    0H
*---------------------------------------------------------------------*
*---     BUILD COMMAND AND OPERATOR MESSAGE                        ---*
*---------------------------------------------------------------------*
         MVC   WTOAREA(WTOLEN),WTOL    MOVE IN WTO MESSAGE
         MVC   WTOAREA+IMGOFF(4),SYSID MOVE DSN IN MESSAGE
         MVC   CMDAREA(CMDLEN),CMDL    MOVE IN START COMMAND
         MVC   CMDADSN,SYSID           MOVE DSN IN START CMD
         SLR   R0,R0                   CLEAR REG ZERO FOR SVC 34
         MGCR  CMDAREA                 ISSUE START COMMAND
         B     *+4(R15)            CHECK RETURN CODE
         B     CNSLMSG             OK; STARTED MESSAGE
         B     RC4                 RETURN CODE OF 4
         B     RC8                 RETURN CODE OF 8
         B     RC12                RETURN CODE OF 12
         B     RC16                RETURN CODE OF 16
CNSLMSG  DS    0H
         WTO   MF=(E,WTOAREA)          ISSUE MSG
SKIPDUMP DS    0H
         LR    R1,R13                  LOAD GETMAINED AREA
         L     R13,4(R13)              POINT TO CALLER'S SAVE AREA
         FREEMAIN RU,LV=DATALEN,A=(1)  FREE GETMAINED STORAGE
         LM    14,12,12(13)            RESTORE REGISTERS
         LA    15,4                    SET RETURN CODE TO 4
         BSM   0,R14                   RESTORE CALLER'S MODE
         SPACE 3
RC4      DS    0H
         WTO   'IEFU29: MGCR RETURN CODE 4'
         B     SKIPDUMP            AND EXIT ---
RC8      DS    0H
         WTO   'IEFU29: MGCR RETURN CODE 8'
         B     SKIPDUMP            AND EXIT ---
RC12     DS    0H
         WTO   'IEFU29: MGCR RETURN CODE 12'
         B     SKIPDUMP            AND EXIT ---
RC16     DS    0H
         WTO   'IEFU29: MGCR RETURN CODE 16'
         B     SKIPDUMP            AND EXIT ---
         EJECT
*
*  DATA AREA
*
SMFQNAME DC    CL8'IPOSMF01'
SMFRNAME DC    CL7'DATASET'
*
* THIS TABLE DETERMINES THE IMAGE= PARM FOR THE DUMPSMF PROC,
* BASED ON THE SYSID IN THE PARM LIST.
*
SRCHPRM  DC    A(TABLE,8,TABLEEND)
TABLE    DC    CL4'PD  ',CL4'SMF1'
         DC    CL4'TD  ',CL4'SMF2'
         DC    CL4'SYSD',CL4'SMF3'
         DC    CL4'SUNG',CL4'SMF1'
         DC    CL4'MV  ',CL4'SMF4'
         DC    CL4'MVSD',CL4'SMF4'
         DC    CL4'????',CL4'????'
         DC    CL4'????',CL4'????'
         DC    CL4'????',CL4'????'
         DC    CL4'????',CL4'????'
         DC    CL4'????',CL4'????'
         DC    CL4'????',CL4'????'
         DC    CL4'????',CL4'????'
         DC    CL4'????',CL4'????'
         DC    CL4'????',CL4'????'
TABLEEND DC    CL4'????',CL4'????'
*
CMDL     DS    0F                  START COMMAND FORMAT FOR SVC
         DC    AL2(CMDLEN)         LENGTH OF STRING
         DC    AL2(00)
         DC    C'START DUMPSMF,IMAGE=' COMMAND
CMDLDSN  DC    CL4' '              SYSTEM IMAGE IDENTIFIER
         DC    C' '                BLANK AT END OF COMMAND
CMDLEN   EQU   *-CMDL              LENGTH OF COMMAND STRING
*
*  N O T E :  DO NOT CHANGE LENGTH OF THE FOLLOWING DSNAME  AREA
WTOL     WTO   'IEFU29 HAS ISSUED ''START DUMPSMF,IMAGE=1234'' ',      C
               ROUTCDE=(1,2,11),MF=L
WTOLEN   EQU   *-WTOL             LENGTH OF WTO STRING
IMGOFF   EQU   43                 OFFSET DSNAME FIELD IN WTO LIST
ENQLSTX  ENQ   (SMFQNAME,SMFRNAME,E,,SYSTEM),RET=TEST,MF=L
*
* DATA DSECT - AREA TO BE GETMAIN'ED
*
DATA     DSECT
SAVE     DS    18F                REGISTER SAVE AREA
SYSID    DS    CL4
CMDAREA  DS    0F,XL(CMDLEN)      AREA FOR COMMAND
         ORG   CMDAREA+(CMDLDSN-CMDL)
CMDADSN  DS    CL4' '
         ORG   ,
WTOAREA  DS    0F,XL(WTOLEN)      AREA FOR WTO PARM LIST
ENQLIST  ENQ   (SMFQNAME,SMFRNAME,E,,SYSTEM),RET=TEST,MF=L
LENQLIST EQU   *-ENQLIST          LENGTH OF WTO STRING
RESERVED DS    4D                 RESERVED
DATALEN  EQU   *-DATA             EQUATE FOR LENGTH OF DATA DSECT
*
IEFU29   CSECT                    GET BACK TO CSECT
         DS    0D                 FORCE TO DOUBLE WORD
*
* PATCH AREA
*
MODLEN   EQU   *-IEFU29           EQUATE FOR MODULE LENGTH(- PATCH)
PTCHLEN  EQU   ((MODLEN+7)/8+7)/8*8 PATCH AREA LENGTH EQUATE
PTCHLNTH DC    Y(PTCHLEN)          LENGTH OF PATCH AREA
PTCHBASE DC    S(PATCH)            BASE DISPLACED ADDRESS OF PATCH
PATCH    DC    XL(PTCHLEN)'00'     PATCH AREA
*
         PRINT OFF
         CVT   DSECT=YES               MAP THE CVT AREA
         IEESMCA
         PRINT ON
         END   IEFU29
