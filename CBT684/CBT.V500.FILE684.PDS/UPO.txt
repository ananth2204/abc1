         MACRO
&NAME    UPO   &A,&B,&T,&MAXLINE=57,&LINELEN=133
         GBLA  &UPOW
         LCLA  &W,&NT,&N,&LL,&LL2
         LCLC  &INTO,&FROM,&LINEA,&LINEB,&LINEC,&LINED
&UPOW    SETA  &UPOW+1
         AIF   (&UPOW GT 1).ERROR
&LL      SETA  &LINELEN-1
&W       SETA  &UPOW+2
&INTO    SETC  'PRINTER'
&FROM    SETC  'LINE'
&NT      SETA  1
&LINEA   SETC  'UPOLINE'
         AIF   ('&T(1)' EQ '').TFIN
&LINEA   SETC  '&T(1)'
         AIF   ('&T(2)' EQ '').TFIN
&NT      SETA  2
&LINEB   SETC  '&T(2)'
         AIF   ('&T(3)' EQ '').TFIN
&NT      SETA  3
&LINEC   SETC  '&T(3)'
         AIF   ('&T(4)' EQ '').TFIN
&NT      SETA  4
&LINED   SETC  '&T(4)'
.TFIN    ANOP
         AIF   ('&A' EQ '').BNEXT
&INTO    SETC  '&A'
.BNEXT   AIF   ('&B' EQ '').DCBFIN
&FROM    SETC  '&B'
.DCBFIN  ANOP
&NAME    ST    R6,UPOREG
         CLI   &FROM,X'F1'
         BE    UPON&W.&NT
         CLI   &FROM,X'5C'
         BE    UPOE&W.&NT
         MVC   UPOTEMP(1),&FROM
         BAL   R6,UPOTESTS
         AP    UPOLINEB(2),UPOTEMP(1)
         CP    UPOLINEA(2),UPOLINEB(2)
         BL    UPON&W.&NT
UPOL&W.&NT PUT &INTO,&FROM
         MVI   &FROM,X'40'
         MVC   &FROM+1(&LL),&FROM
         B     UPOFIN
UPOE&W.&NT MVI &FROM,X'F1'
         ZAP   UPOPAGE(3),=P'1'
         ZAP   UPOLINEB(2),=P'1'
         B     UPOL&W.&NT
UPON&W.&NT CP  UPOPAGE(3),=P'1'
         BE    UPONPAGE
         SP    UPOINCR(2),UPOLINEB(2)
         AP    UPOINCR(2),UPOTEMP(1)
         CP    UPOINCR(2),=P'2'
         BNH   UPOCON
         UPRINT &INTO,UPOLINE,SKIPAFT=UPOINCR,LINELEN=&LINELEN
         MVC   UPOINCR(2),UPOLINEA
&LL2     SETA  &LL-28
UPOCON   MVC   UPOLINE+&LL2.(20),=C'(CONTINUED ON PAGE  '
         MVI   UPOLINE,X'F0'
         LA    R6,UPOWORK+6
         MVC   UPOWORK(6),UPOEDIT
         EDMK  UPOWORK(6),UPOPAGE
         SR    R6,R1
         EX    R6,UPOEXECA
         PUT   &INTO,UPOLINE
         MVI   UPOLINE,X'40'
         MVC   UPOLINE+1(&LL),UPOLINE
&LL2     SETA  &LL-10
UPONPAGE MVC   &LINEA+&LL2.(6),=C'PAGE  '
         LA    R6,UPOWORK+5
         MVC   UPOWORK(6),UPOEDIT
         EDMK  UPOWORK(6),UPOPAGE
         SR    R6,R1
         EX    R6,UPOEXECB
         AP    UPOPAGE(3),=P'1'
         ZAP   UPOLINEB(2),=P'1'
         MVI   &LINEA,X'F1'
         PUT   &INTO,&LINEA
         AIF   (&NT LE 1).PUTFIN
         MVC   UPOTEMP(1),&LINEB
         BAL   R6,UPOTESTS
         AP    UPOLINEB(2),UPOTEMP(1)
         PUT   &INTO,&LINEB
         AIF   (&NT LE 2).PUTFIN
         MVC   UPOTEMP(1),&LINEC
         BAL   R6,UPOTESTS
         AP    UPOLINEB(2),UPOTEMP(1)
         PUT   &INTO,&LINEC
         AIF   (&NT LE 3).PUTFIN
         MVC   UPOTEMP(1),&LINED
         BAL   R6,UPOTESTS
         AP    UPOLINEB(2),UPOTEMP(1)
         PUT   &INTO,&LINED
.PUTFIN  ANOP
         MVC   UPOTEMP(1),&FROM
         BAL   R6,UPOTESTS
*
UPOADD   AP    UPOLINEB(2),UPOTEMP(1)
         AP    UPOLINEB(2),=P'2'
&LL2     SETA  &LL-2
         MVC   UPOLINE+2(&LL2),UPOLINE+1
         MVI   UPOLINE,X'F0'
         PUT   &INTO,UPOLINE
         B     UPOL&W.&NT
&LL2     SETA  &LL-9
UPOEXECA MVC   UPOLINE+&LL2.(0),0(R1)
&LL2     SETA  &LL-4
UPOEXECB MVC   &LINEA+&LL2.(0),0(R1)
UPOTESTS CLI   UPOTEMP,X'40'
         BE    UPO1
         CLI   UPOTEMP,X'F0'
         BE    UPO2
         CLI   UPOTEMP,X'60'
         BE    UPO3
         CLI   UPOTEMP,X'4E'
         BE    UPO0
         MVI   &FROM,X'40'
UPO1     MVI   UPOTEMP,X'1C'
         BR    R6
UPO0     MVI   UPOTEMP,X'0C'
         BR    R6
UPO2     MVI   UPOTEMP,X'2C'
         BR    R6
UPO3     MVI   UPOTEMP,X'3C'
         BR    R6
UPOREG   DC    F'0'
UPOTEMP  DC    X'00'
UPOPAGE  DC    PL3'1'
UPOLINEA DC    PL2'&MAXLINE'
UPOLINEB DC    PL2'&MAXLINE'
UPOEDIT  DC    X'402020202020'
UPOINCR  DC    PL2'&MAXLINE'
&LL2     SETA  &LL+1
UPOLINE  DS    0CL&LL2
         DC    X'F0'
         DC    CL&LL' '
UPOWORK  DC    CL6' ',CL1')'
         CNOP  0,4
UPOFIN   L     R6,UPOREG
         MEXIT
.ERROR   ANOP
         SPACE 3
         MNOTE *,'**** FATAL ERROR,'
         SPACE
         MNOTE *,'           **** THE UPO MACRO WAS CALLED MORE THAN   +
               ONCE IN THE PROGRAM'
         SPACE 2
         MNOTE *,'**** CONDITIONAL ASSEMBLY TERMINATED ****'
         MEND
