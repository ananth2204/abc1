IEFUTL   TITLE 'BOTCC USER TIME LIMIT EXIT '
*---------------------------------------------------------------------*
*---     FUNCTION - GRANT UNLIMITED EXTENSION FOR WAIT TIME TO     ---*
*---                ANY JOB OR STC.  ALLOW TSO SESSIONS TO BE      ---*
*---                CANCELLED AFTER 2 HOURS OF WAIT TIME.          ---*
*---------------------------------------------------------------------*
IEFUTL   CSECT
R0       EQU   0                   R
R1       EQU   1                    E
R2       EQU   2                     G
R3       EQU   3                      I
R4       EQU   4                       S
R5       EQU   5                        T
R6       EQU   6                         E
R7       EQU   7                          R
R8       EQU   8
R9       EQU   9                   E
R10      EQU   10                   Q
R11      EQU   11                    U
R12      EQU   12                     A
R13      EQU   13                      T
R14      EQU   14                       E
R15      EQU   15                        S
         J     *+12                BRANCH AROUND
         DC    AL1(7),CL7'IEFUTL'
         STM   R14,R12,12(R13)     SAVE REGISTERS
         LR    R12,R15             COPY EP ADDRESS
         USING IEFUTL,R12          DECLARE PROGRAM BASE REGISTER
*---------------------------------------------------------------------*
*---                                                               ---*
*---     ESTABLISH ADDRESSABILITY TO SMF PARAMETER AREA DSECT      ---*
*---     EVALUATE REGISTER 0 = 0 - JOB CPU TIME EXCEEDED           ---*
*---                         = 4 - STEP CPU TIME EXCEEDED          ---*
*---                         = 8 - JOB WAIT TIME EXCEEDED          ---*
*---------------------------------------------------------------------*
         L     R11,0(R1)           LOAD POINTER TO PARAMETER AREA
         USING SMFEPLA,R11         DECLARE THE MAP
         CHI   R0,4                SEE IF JOB/STEP TIME EXCEEDED
         JH    WAITTIME            MUST BE WAIT TIME
*---------------------------------------------------------------------*
*---     CPU TIME EXCEEDED - EXTEND FOR ETERNITY                   ---*
*---------------------------------------------------------------------*
         LHI   R2,8                INDICATE RETURN CODE OF 8 -
         ICM   R1,15,ETERNITY      LOAD TIME EXTENSION IN SECONDS
         J     RETURN              GO RETURN TO CALLER
*---------------------------------------------------------------------*
*---     WAIT TIME EXCEEDED                                        ---*
*---        - ALLOW TSO SESSION TO BE CANCELLED                    ---*
*---        - EXTEND JOB/STC INDEFINITELY                          ---*
*---------------------------------------------------------------------*
WAITTIME DS    0H
         TM    SMFOPT,SMFTSO       SEE IF TSO SESSION
         JO    CANCEL              YES...GO CANCEL TSO SESSION
*---------------------------------------------------------------------*
*---     EXTEND INDEFINITELY ---                                   ---*
*---------------------------------------------------------------------*
         LHI   R2,8                EXTEND CPU TIME IN SECONDS
         ICM   R1,15,ETERNITY      LOAD TIME EXTENSION IN SECONDS
         J     RETURN              GO RETURN TO CALLER
*---------------------------------------------------------------------*
*---     ZERO REGISTER 2 TO INDICATE CANCEL                        ---*
*---------------------------------------------------------------------*
CANCEL   DS    0H
         SLR   R2,R2               CANCEL RETURN CODE
*---------------------------------------------------------------------*
*---     NORMAL END PROCESSING                                     ---*
*---------------------------------------------------------------------*
RETURN   DS    0H
         L     R14,12(,R13)        LOAD ADDRESS FOR RETURN
         LR    R15,R2              LOAD RETURN CODE FROM REGISTER 2
         LM    R2,R12,28(R13)      RESTORE REGISTERS 2 TO 12
         BR    R14                 RETURN TO CALLER
         SPACE 2
ETERNITY DC    X'7FFFFFFF'
         SPACE 2
************************ DSECTS (MAPPING MACROS) **********************
SMFEPLA  DSECT
SMFJOBN  DS    CL8
SMFTIME  DS    CL4
SMFDATE  DS    CL4
SMFSID   DS    CL4
SMFUSID  DS    CL8
SMFSTEP  DS    CL1
SMFOPT   DS    CL1
SMFTSO   EQU   X'01'
SMFVS1   DS    CL1
SMFRESV  DS    CL1
SMFUSER  DS    CL4
         ORG   SMFUSER
SMFUSERC DS    CL1
SMFUSERW DS    CL1
         ORG
         SPACE 5
********************************* EQUATES *****************************
         SPACE 5
         END
