         MACRO
&LBL     #CVIP &TO,&FROM,&DWD
         LCLC  &A,&B,&C,&D,&E,&F
         LCLA  &Z,&Y,&X,&W,&V,&U
&B       SETC  '&DWD'
         AIF   ('&DWD' NE '').L0
         CNOP  4,8
&B       SETC  'CVIP&SYSNDX'
.L0      ANOP
&A       SETC  '&LBL'
         AIF   ('&A' EQ '').L1
&LBL     DS    0H
.L1      ANOP
         AIF   ('&DWD' NE '').L2
         J     *+12                BRANCH AROUND WORK AREA
&B       DS    D                   CVD/UNPACK WORK AREA
.L2      ANOP
         L     R0,&FROM            LOAD THE IP ADDRESS WORD
         SRDL  R0,32               SHIFT OVER TO R1
         D     R0,=F'256'          LAST 'TRIT' OF ADDRESS
         CVD   R0,&B               DECIMALIZE IT
         UNPK  &TO+12(3),&B+6(2)   GRAB THE VALUE
         SR    R0,R0
         D     R0,=F'256'          NEXT 'TRIT'
         CVD   R0,&B               DECIMALIZE IT
         UNPK  &TO+8(3),&B+6(2)    GRAB THE VALUE
         SR    R0,R0
         D     R0,=F'256'          NEXT 'TRIT'
         CVD   R0,&B               DECIMALIZE IT
         UNPK  &TO+4(3),&B+6(2)    GRAB THE VALUE
         CVD   R1,&B               DECIMALIZE LAST 'TRIT'
         UNPK  &TO.(3),&B+6(2)     GRAB THE VALUE
         OC    &TO.(15),=C'000.000.000.000'
         MEND
