ANCHOR   TITLE 'ALLOCATES CUSTOMER ANCHOR TABLE IN ECSA'
*       AUTHOR   :  A SYSTEMS PROGRAMMER
* INSTALLATION   :  CONCEALED
*     SECURITY   :  APF AUTHORIZATION IS REQUIRED
*         DATE   :  03/24/1997
*
*
*  NOTE: STORAGE IS OBTAINED FROM SUBPOOL 241. THE ATTRIBUTES OF THIS
*  SUBPOOL ARE:
*
*  LOCATION         : COMMON CSA/ECSA
*  FETCH PROTECTION : NO
*  TYPE             : PAGEABLE
*  OWNER            : SYSTEM
*  STORAGE KEY      : SELECTABLE - STORAGE KEY 0 HAS BEEN ASSIGNED
*
*  CONTACT SOMEONE AT WWW.IBM.COM FOR YOUR VERY OWN
*  OFFSET IN THE CUSTOMER ANCHOR TABLE. ONCE YOU'VE RECEIVED YOUR
*  TABLE ENTRY, CHANGE THE CONSTANT AT THE 'CHANGEME' LABEL BELOW
*  AND IN THE CACEMCKI PROGRAM.
*
*  MODIFICATION #1    08/20/2010
*
*  PROVIDE ANCHOR POINT FOR CA/OPTIMIZER REMOVAL PROJECT
*
*  MODIFICATION #2    08/23/2010
*
*  COMMENTED OUT AUTHORIZATION CHECK (TESTAUTH) SINCE IT DOESN'T
*  WORK AS EXPECTED DURING SUBSYSTEM INITIALZATION.
*
         TITLE 'INTERNAL MACROS'
         MACRO
         DOCPGM
         LCLC  &LABEL
.**********************************************************************
.****                                                             *****
.****    THIS MACRO DOCUMENTS THE CSECT.                          *****
.****                                                             *****
.****    CSECT NAME                                               *****
.****    ASSEMBLER NAME AND VERSION                               *****
.****    TIME AND DATE                                            *****
.****    OPERATING SYSTEM LEVEL                                   *****
.****                                                             *****
.**********************************************************************
&SKIPIT  SETC  '$$$$'.'&SYSNDX'
         USING &SYSECT,R15       TEMPORARY BASE REGISTER
         B     &SKIPIT           GO AROUND EYE CATCHER
         DC    C'&SYSSTYP &SYSECT WAS '
         DC    C'ASSEMBLED BY &SYSASM VERSION &SYSVER. '
         DC    C'AT &SYSTIME ON &SYSDATC. '
         DC    C'UNDER &SYSTEM_ID'
&SKIPIT  DS    0H
         MEND
*
*        INSTALLATION COMMUNICATIONS VECTOR TABLE
*
         MACRO
         USRCVT
USRCVT   DSECT
USREYC   DC   CL8'USR_CVT '
CAOPTREM DS   A
RESERVE2 DS   A
RESERVE3 DS   A
RESERVE4 DS   A
RESERVE5 DS   A
RESERVE6 DS   A
RESERVE7 DS   A
RESERVE8 DS   A
RESERVE9 DS   A
RESERVEA DS   A
USRCVTLN EQU *-USRCVT
         MEND
         TITLE 'MAIN PROGRAM'
         PUNCH ' SETCODE AC(1)'
ANCHOR   CSECT
ANCHOR   RMODE ANY
ANCHOR   AMODE 31
         YREGS
         SPLEVEL SET
         DOCPGM
         BAKR  R14,0            SAVE REGS ON LINKAGE STACK
         L     R12,LOADPT       LOAD BASE REGISTER
         DROP  R15              DROP TEMPORARY BASE REGISTER
         USING ANCHOR,R12       TELL ASSEMBLER ABOUT BASE REGISTER
         LA    R13,SAVEX        POINT R13 TO SAVE AREA
         MVC   4(4,R13),=C'F1SA' SET ACRONYM IN SAVE AREA
*        TESTAUTH FCTN=1        TEST JOBSTEP FOR APF AUTHORIZATION
*        LTR   R15,R15          TEST RETURN CODE
*        JNZ   NOTAUTH          IF RC NE 0 WE ARE NOT AUTHORIZED
         L     R1,CVTPTR        GET CVT ADDRESS
         USING CVT,R1           GET ADDRESSABILITY TO CVT
         L     R1,CVTECVT       GET ECVT ADDRESS
         DROP  R1               DROP ADDRESSABILITY TO CVT
         USING ECVT,R1          GET ADDRESSABILITY TO ECVT
         L     R1,ECVTCTBL      GET CUSTOMER ANCHOR TABLE ADDRESS
         DROP  R1               DROP ADDRESSABILITY TO ECVT
CHANGEME LA    R2,X'XX'(,R1)    POINT TO ASSIGNED CUSTOMER SLOT
         CLC   0(4,R2),=F'0'    BETTER BE ZERO
         JNE   NOTZERO          NO. GET OUT
         MODESET MODE=SUP,KEY=ZERO    OBTAIN SUPERVISOR/KEY ZERO
         STORAGE OBTAIN,LENGTH=USRCVTLN,ADDR=USRCVTAD,SP=241,KEY=0
         L     R3,USRCVTAD      GET ADDRESS OF OBTAINED STORAGE
         USING USRCVT,R3        POINT TO DSECT
         MVI   0(R3),X'00'      CLEAR OUT FIRST BYTE OF STORAGE
         MVC   1(USRCVTLN-1,R3),0(R3) CLEAR OUT REST OF STORAGE
         MVC   USREYC(8),=CL8'USR_CVT ' MOVE EYECATCHER INTO USRCVT
         ST    R3,0(,R2)        STORE ADDRESS OF OUR USR CVT
         CSVQUERY INEPNAME=USRANC01,SEARCH=LPA,OUTEPA=QUERYADDR
         LTR   R15,R15          IS RC=0?
         JNE   NOTZERO          NO. GET OUT
         L     R4,QUERYADDR     RETREIVE RETURNED ADDRESS
         ST    R4,CAOPTREM      STORE INTO FIRST SLOT IN THE USRCVT
         DROP  R3               DROP ADDRESSABILITY TO THE USRCVT
         MODESET KEY=NZERO,MODE=PROB  GET BACK INTO PROBLEM MODE
         LA    R15,0            SET RETURN CODE
         PR    ,                RETURN TO SYSTEM
NOTAUTH  DS    0H
NOTZERO  DS    0H
         WTO   'CUSTOMER ANCHOR TABLE CREATE ERROR....CONTACT TECHNICALX
                SUPPORT IMMEDIATELY',ROUTCDE=(1,11),DESC=1
         L     R15,16           SET RETURN CODE
         PR    ,                RETURN TO CALLER
LOADPT   DC    A(ANCHOR)        ADDRESS OF START OF PROGRAM
USRCVTAD DS    F                ADDRESS OF OBTAINED STORAGE
USRANC01 DC    CL8'USRANC01'    CA/OPTIMIZER REMOVAL PROJECT
QUERYADDR DS   F                CSVQUERY ADDRESS RETURN
SAVEX    DS    18F              SAVE AREA
         LTORG
         TITLE 'COMMUNICATIONS VECTOR TABLE'
         CVT   DSECT=YES,LIST=NO
         TITLE 'EXTENDED COMMUNICATIONS VECTOR TABLE'
         IHAECVT LIST=NO
         TITLE 'INSTALLATION COMMUNICATIONS VECTOR TABLE'
         USRCVT
         END   ANCHOR
