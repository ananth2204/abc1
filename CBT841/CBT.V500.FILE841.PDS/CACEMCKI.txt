CACEMCKI TITLE 'CA/OPTIMIZER REMOVAL PROJECT - STUB MODULE #1'
*
*       AUTHOR   :  A SYSTEMS PROGRAMMER
* INSTALLATION   :  CONCEALED
*     SECURITY   :  NONE
*         DATE   :  08/20/2010
*
* THIS IS THE FIRST PART OF THE CA/OPTIMIZER STUB PROGRAM. THIS MODULE
* IS LOADED BY THE OPTIMIZED COBOL PROGRAM AND IS THEN BRANCHED TO
* VIA A BALR INSTRUCTION.
*
* THIS PROGRAM RUNS THE CONTROL BLOCK CHAIN TO THE USR_CVT AND
* RETRIEVES THE FIRST VALUE IN THE TABLE WHICH IS THE SECOND PART
* OF THE STUB PROGRAM.
*
* BEFORE THIS PROGRAM BRANCHES TO THE SECOND PROGRAM IT MOVES THREE
* NOOP INSTRUCTIONS TO THE CALLING PROGRAM WHICH PREVENTS IT FROM BEING
* CALLED AGAIN.
*
* MODIFICATION # 1 08/23/2010
*
* ADDED MOVE NOOP'S TO CALLING PROGRAM TO FIX PERFORMANCE PROBLEM WITH
* THE PROCESS.
*
CAOPTSB1 CSECT
CAOPTSB1 RMODE 24
CAOPTSB1 AMODE 24
         SPLEVEL
         YREGS
         PRINT OFF
         COPY  IEABRC
         PRINT ON
         USING CAOPTSB1,R15     SET UP BASE REGISTER
         SAM31                  SET AMODE 31
         L     R1,CVTPTR        GET CVT ADDRESS
         USING CVT,R1           GET ADDRESSABILITY TO CVT
         L     R1,CVTECVT       GET ECVT ADDRESS
         DROP  R1               DROP ADDRESSABILITY TO CVT
         USING ECVT,R1          GET ADDRESSABILITY TO ECVT
         L     R1,ECVTCTBL      GET CUSTOMER ANCHOR TABLE ADDRESS
         DROP  R1               DROP ADDRESSABILITY TO ECVT
CHANGEME LA    R1,X'XX'(,R1)    POINT TO ASSIGNED CUSTOMER SLOT
         CLC   0(4,R1),=F'0'    BETTER NOT BE ZERO
         JE    YESZERO          YES...DANGER, WILL ROBINSON!
         L     R1,0(,R1)        LOAD POINTER TO USR_CVT
         USING USRCVT,R1        TELL ASSEMBLER
         L     R0,CAOPTREM      LOAD THE ADDRESS OF THE SECOND PROGRAM
         LR    R1,R14           SAVE ADDRESS OF RETURN ADDRESS
         SH    R1,=H'6'         POINT BACK THREE INSTRUCTIONS
         SAM24                  GO BACK INTO AMODE 24 FOR MOVE
         MVC   0(6,R1),NOPCODE  MODIFY CALLING PROGRAM
         SAM31                  RESET AMODE 31
         LR    15,R0            RESTORE STUB PROGRAM #2'S ADDRESS
         BR    R15              BRANCH TO STUB PROGRAM #2
         DROP  R15              DROP ADDRESSABILITY
YESZERO  WTO   'USR_CVT NOT BUILT...CONTACT TECHNICAL SUPPORT ONCALL', X
               ROUTCDE=(1,11),DESC=1
         BR    R14              RETURN TO CALLER
NOPCODE  NOPR  R0
         NOPR  R0
         NOPR  R0
         TITLE 'COMMUNICATIONS VECTOR TABLE'
         CVT   DSECT=YES,LIST=NO
         TITLE 'EXTENDED COMMUNICATIONS VECTOR TABLE'
         IHAECVT LIST=NO
         TITLE 'INSTALLATION COMMUNICATIONS VECTOR TABLE'
USRCVT   DSECT
USREYC   DC   CL8'USR_CVT '
CAOPTREM DS   A
RESERVE2 DS   A
RESERVE3 DS   A
RESERVE4 DS   A
RESERVE5 DS   A
RESERVE6 DS   A
RESERVE7 DS   A
RESERVE8 DS   A
RESERVE9 DS   A
RESERVEA DS   A
USRCVTLN EQU  *-USRCVT
         END  CAOPTSB1
