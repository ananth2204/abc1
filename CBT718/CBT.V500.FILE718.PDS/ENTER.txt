         MACRO
&LABEL   ENTER     &AMODE=31,&RMODE=ANY,&WSLEN=,&SP=0,&LOC=RES
.* SET UP MODULE ENTRY PROLOG; FOLLOWING MACRO, REGISTER CONTENTS ARE
.* AS AT ENTRY WITH TWO EXCEPTIONS:
.*    1) R12 @ BASE OF MODULE (BASE REGISTER)
.*    2) IF WSLEN SPECIFIED, STORAGE IS OBTAINED FOR A LENGTH OF WSLEN
.*       IN THE SUBPOOL SPECIFIED BY SP; FOLLOWING MACRO, R11@ STORAGE
         PUSH      PRINT                     SAVE PRINTING STATUS
         PRINT     OFF,NOPRINT               SUPRESS PRINTING REG EQ
         YREGS     ,                         REGISTER EQUATES
         POP       PRINT                     RESTORE PRINTING STATUS
&LABEL   CSECT     ,                         NAME OF PROGRAM
&LABEL   AMODE     &AMODE                    SPECIFY AMODE
&LABEL   RMODE     &RMODE                    SPECIFY RMODE
         B         @&SYSNDX-&LABEL.(R15)     BRANCH AROUND HEADER
         DC        C'*CSECT:'                EYECATCHER
         DC        C'&LABEL'                 MODULE NAME
         DC        C'*COMPILED:'             EYECATCHER
         DC        CL19'&SYSCLOCK'           DATE AND TIME STAMP
         ORG       *-9                       OVERLAY BLANK IN SYSCLOCK
         DC        C'*'                      CHANGE TO AN *
         ORG       ,                         STOP OVERLAYING
@&SYSNDX DS        0H                        BRANCH AROUND HEADER
         BAKR      R14,R0                    SAVE STATE
         LR        R12,R15                   LOAD CODE PAGE REGISTER
         USING     &LABEL,R12                MAP CODE PAGE REGISTER
         AIF       ('&WSLEN' EQ '').NOREFR   NO WORKING STORAGE REQ
         STORAGE   OBTAIN,LENGTH=&WSLEN,SP=&SP,LOC=(&LOC)
*                                            ... IN REQUESTED SUBPOOL
         LR        R11,R1                    COPY @ WORKING STORAGE
         EREG      R13,R10                   RESTORE REGS EXCL R11-12
.NOREFR  ANOP                                REFR NOT REQUIRED
         MEND
