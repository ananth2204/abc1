         ICTL      1,71,20

***********************************************************************
*                                                                     *
*                           I K J E F L D 2                           *
*                                                                     *
*                                                                     *
*F U T U R I T Y   S O F T W A R E   I N T E R N A T I O N A L   I N C*
*                                                                     *
*                                                                     *
*                   CHIEF ARCHITECT: Garry G. Green                   *
*                  ARCHITECT EN CHEF: Garry G. Green                  *
*                                                                     *
*                       COPYRIGHT MAY 2005 FSI                        *
*                                                                     *
***********************************************************************

***********************************************************************
*   CHANGE ACTIVITY                                                   *
*   $G00=050527 VERSION 1.0 BASE                                      *
***********************************************************************

***********************************************************************
*   ATTRIBUTES:                                                       *
*     REFRESHABLE, AMODE(24), RMODE(24), PLPA OR LINKLIST RESIDENT    *
*     ENTERED KEY8, SUPERVISOR STATE                                  *
*                                                                     *
*   OPERATION                                                         *
*                                                                     *
*     1) MSTR SUBSYSTEM DOES NOT SUPPORT RUNNING A SECOND JOB WHEN THE*
*        FIRST ONE ENDS (DESIGNED TO RUN STC'S ONLY); THE TSO LOGON   *
*        COMMAND LOGS OFF THE CURRENT JOB AND STARTS A SECOND TSO JOB *
*        IN THE SAME ADDESS SPACE, CAUSING ERRORS.                    *
*                                                                     *
*        THIS EXIT CHECKS IF THE TSO JOB BEING LOGGED OFF WAS EXEC.   *
*        UNDER THE MSTR SUBSYSTEM; IF SO, IT ISSUES AN ERROR MESSAGE  *
*        TO USER AND SET RETURN CODE 16 FORCING LOGOFF                *
*                                                                     *
*     2) IF TSO EXEC. UNDER MSTR S/S, DURING LOGOFF, MESSAGE IKJ56457I*
*        PROGRAM ERROR MESSAGE IS ISSUED DUE TO A NON-ZERO TERMINATION*
*        CODE WHICH IS NORMAL FOR THE MSTR SUBSYSTEM; WE ZERO THE     *
*        ONE BYTE FIELD AT OFFSET +2 INTO THE JSXL TO SUPPRESS MESSAGE*
***********************************************************************


***********************************************************************
*                     R E C O R D   L A Y O U T S                     *
***********************************************************************

         PRINT     ON,NOGEN                  SUPRESS EXPANSION
         IHAPSA    ,                         MAP PSA
         CVT       DSECT=YES                 MAP CVT
         IEFJESCT  ,                         MAP JESCT
         IKJTCB    ,                         MAP TCB
         IHAASCB   ,                         MAP ASCB
         IHAASXB   ,                         MAP ASXB
         IKJEFLWA  ,                         MAP LWA
         IEFVJSEL  ,                         MAP JSEL AND JSXL
         PRINT     ON,GEN                    GENERATE OUTPUT

***********************************************************************
*                     P R O G R A M   P R O L O G                     *
***********************************************************************

IKJEFLD2 ENTER     AMODE=24,RMODE=24         ESTABLISH STD ENTRY PROLOG
         B         START                     BRANCH AROUND CONSTANTS
*STATS
***********************************************************************
*                         C O N S T A N T S                           *
***********************************************************************

NOLOGON1 DC        C'TSO LOGON command (i.e. LOGON command executed fro*
                   m a logged on TSO terminal)'
NOLOGON2 DC        C'only supported for TSO jobs executing under the Pr*
                   imary JES Subsystem.  TSO'
NOLOGON3 DC        C'Exit IKJEFLD2 is forcing a LOGOFF.  Please press e*
                   nter to complete LOGOFF.'
***********************************************************************
*              L O G I C A L   P R O G R A M   S T A R T              *
***********************************************************************

START    DS        0H                        Logical program start
         LR        R3,R1                     Save parm @
         L         R4,20(R3)                 Get Exit to Exit Comm Word
         L         R5,FLCCVT-PSA(0)          Get @ CVT
         L         R6,CVTJESCT-CVTMAP(R5)    Get @ JESCT
         CLC       8(4,R4),JESPJESN-JESCT(R6) Q. User exec. under PJES
         BE        RC0                       A. Yes - branch
         L         R7,0(R3)                  Get 1st parm @
         L         R8,8(R7)                  Get @ Command Buffer
         CLC       0(5,R8),=CL5'LOGON'       Q. LOGON command entered
         BNE       LOGOFF                    A. No - branch
         TPUT      NOLOGON1,L'NOLOGON1       Issue error msg 1
         TPUT      NOLOGON2,L'NOLOGON2       Issue error msg 2
         TPUT      NOLOGON3,L'NOLOGON3       Issue error msg 3
         L         R9,PSATOLD-PSA(0)         Get @ current TCB
         IPK       ,                         Save PSW Key
         SPKA      X'00'                     Into PSW Key 0
         TGET      TCBUSER-TCB(R9),1         Issue TGET to ensure user
*                                            user sees message
         SPKA      0(R2)                     Restore PSW Key
LOGOFF   DS        0H                        LOGOFF command entered
         L         R4,PSAAOLD-PSA(0)         Get @ ASCB
         L         R5,ASCBASXB-ASCB(R4)      Get @ ASXB
         L         R6,ASXBLWA-ASXB(R5)       Get @ LWA
         L         R7,LWAJSEL-LWA(R6)        Get @ JSEL
         L         R8,JSELJSXL-JSEL(R7)      Get @ JSXL
         CLI       2(R8),36                  Q. Is Job Scheduling
*                                               error code = 36
         BNE       JSCODEOK                  A. No - branch
         IPK       ,                         Save PSW Key
         SPKA      X'00'                     Into Key 0 to update JSXL
         MVI       2(R8),0                   Set JSXL code to 0
         SPKA      0(R2)                     Restore PSW Key
JSCODEOK DS        0H                        Job scheduling code is ok
         LA        R15,16                    Set RC 16 to force logoff
         B         END                       Go to common end
RC0      DS        0H                        User executed under PJES
         XR        R15,R15                   Set RC 0 to perform
*                                            normal processing
END      DS        0H                        Common end
         PR        ,                         Return to caller
         LTORG     ,                         Assemble literals here
         END       IKJEFLD2
