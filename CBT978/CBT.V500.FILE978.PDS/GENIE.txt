/* rexx */
Rexx_GENIE:
  rexxpgm_version = "02.27"           /* ver lev */

  ADDRESS ISREDIT "MACRO (GENIEMBR) NOPROCESS"
  ADDRESS ISPEXEC "CONTROL ERRORS RETURN"

  sys_sysuid = SYSVAR("SYSUID")
  function_rc = Rcvt_Acee()

/* <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
 Here is where you can tailor GENIE to use different data sets.
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> */
  SELECT
    WHEN (user_special = "YES") THEN DO
      wish@dsn = "VENDOR.GENIE.WISHLIST" /* SET without Apostrophes */
    END
    WHEN (WORDPOS("GENIEAPP", saf_connect) > 0) THEN DO
      wish@dsn = "!HLQ!.APPLIC.WISHLIST"  /* SET without Apostrophes */
    END
    OTHERWISE DO
      wish@dsn = "!WISHLIST!"             /* SET without Apostrophes */
    END
  END

/* <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
 Here is where you can tailor GENIE to use a different
 configuration file.
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> */
  wish@cfg = "$$$$0501"

/* <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
=======================================================================
 This code drives Initialization, Process, and Termination routines.
=======================================================================
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> */
Main_Routine:
  return_code = Initialization_Routine()
  IF (return_code = 0) THEN DO
    return_code = Process_Routine()
  END
  return_code = Termination_Routine()
  ADDRESS ISREDIT "MEND"
  EXIT 1        /* EXIT 1 will place the cursor on the command line. */

/* <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
=======================================================================

 Dataset:  REXX(GENIE)
           Genuine Every Noteworthy Itemized Examplizer (GENIE).
 Contact:  "Kenneth Tomiak"<CBT_Ken@KTomiak.BIZ>
 Overview: Similar to MODEL, but expanded to use any TOPIC (manual)
           as defined in wish@cfg. The TOPIC is assigned a four
           character type. TOPICs are broken up into CHAPTERs and
           WISH members provide details that can include DATA lines.
 Syntax:   GENIE |wish|*|?|=keyword|
           wish    : The combination of a TOPIC, CHAPTER, and sequence.
           *       : Expand all TOPICs and CHAPTERs at startup.
           ?       : Pop-up the Primary Command help panel at startup.
           =keyword: Find keyword in WISH files.

-----------------------------------------------------------------------

 History of Modifications
 ------------------------
vv.mm WHEN     Who / What you did ... (Newest change at the top.)
----- -------- --------------------------------------------------------
02.27 20180523 KTOMIAK "Kenneth Tomiak"<CBT_Ken@KTomiak.BIZ>
         -     1) Added "B"ook when requesting INFO on a member.
      20180518 2) ZERRLM when variable substitution RC > 0.
----- -------- --------------------------------------------------------
02.26 20180517 KTOMIAK "Kenneth Tomiak"<CBT_Ken@KTomiak.BIZ>
         -     1) Added exclamation mark around substitution
      20180516    variable names.
               2) Docucmented ID@TYPE values:
                    ID@TYPE=@ Copyright
                    ID@TYPE=* Comment
                    ID@TYPE=B Book
                    ID@TYPE=D DATALINE
                    ID@TYPE=E Edited last by
                    ID@TYPE=G GENIE configuration (see 3 below)
                    ID@TYPE=I INFOLINE  ======
                    ID@TYPE=K Keywords
                    ID@TYPE=M MSGLINE   ==MSG>
                    ID@TYPE=N NOTELINE  =NOTE=
                    ID@TYPE=R Run a routine (see 5 below)
                    ID@TYPE=S Source member name
                    ID@TYPE=T Title (TOPIC, CHAPTER, Subject)
                    ID@TYPE=U URL of a Web Page
                    ID@TYPE=W URL of a Web Site
                    ID@TYPE=X eXchange variable with value (see 4 below)
                    ID@TYPE=Z Date and time last updated
               3) Docucmented configuration member
                  Column 1 = file_typ
                  ---------------------
                    file_typ=T TOPIC
                    file_typ=C CHAPTER
                    file_typ=W WISH
                    file_typ=K Keywords
               4) Docucmented variable = substitution-value:
                  GENIE reserved values
                  --------------------------------------------------
                    current_dataset = the current dataset name
                    current_member = the current member name
                    logon_acct    = from the logon PROCEDURE (ZACCTNUM)
                    mvsvar_sysname = MVSVAR("SYSNAME")
                    saf_dfltgrp   = User's default RACF group
                    saf_name      = RACF name field (20 characters)
                    sysvar_sysuid = SYSVAR("SYSUID")
                    zdate         = DATE("S") yyyymmdd
                    ztime         = TIME()     hh:mm:ss
                  User defined samples
                  --------------------
                    X!your_variable! = "your static value"
                    X!company-url! = "http://www.ktomiak.com/ORG"
                  Tested reserved samples
                  -----------------------
                    x!acct! = logon_acct
                    x!datasetid! = editdsn
                    x!group! = saf_dfltgrp
                    x!memberid! = editmbr
                    x!mvsname! = mvsvar_sysname
                    x!now-time! = ztime
                    x!programmer-name! = saf_name
                    x!sysuser! = sysvar_sysuid
                    x!todays-date! = zdate
               5) Documented Routines (ID@TYPE=R)
                  Known system routines inside GENIE
                  ----------------------------------
                    system_symbols
                  Sample user routines outside GENIE
                  ----------------------------------
                    genierta
               6) Added support for invoking a user routine.
               7) NOTELINE length limited to 72 characters.
                  Verification added to GENIE and GENIEBLD.
                  MSGLINE, too.
               8) IF NOTELINE inserts INFOLINE then add a space.
               9) Fixed a bad occurence of GENIESRT being set to "T".
----- -------- --------------------------------------------------------
02.25 20180513 KTOMIAK "Kenneth Tomiak"<CBT_Ken@KTomiak.BIZ>
               1) Vast renaming of 'item' to member so that I could
                  use WISH and wish where appropriate.
               2) Cleanup of TOPIC, CHAPTER, and WISH capitalization.
               3) Consistent help text for Expand and Unexpand.
               4) Fixed inserting to use variable newtext.
----- -------- --------------------------------------------------------
02.24 20180512 KTOMIAK "Kenneth Tomiak"<CBT_Ken@KTomiak.BIZ>
               1) Stylize using
                  Programming Using The Kenneth Tomiak Method.
                 a) Use PUTKTM comments on primary routines.
                 b) Use PUTKTM on user-defined routines.
                 c) Conform variable names to lowercase.
                 d) Remove WIDTH on panels.
                 e) Correct panel attribute characters (~ for |).
               2) Stylize using Edit Macro @putktm:
                  Programming Using The Kenneth Tomiak Method.
               3) Reenable "?" presenting Primary command help.
               4) Delete unnecessary VPUT of ZERR* variables.
               5) Transform "A"pplication to "T"OPIC and "T"OPIC to
                  "W"ISH.
----- -------- --------------------------------------------------------
02.23 20180224 KTOMIAK "Kenneth Tomiak"<CBT_Ken@KTomiak.BIZ>
               1) Alias FIND with KEYWORD.
               2) Add full text SEARCH, albeit a CPU consumer.
----- -------- --------------------------------------------------------
02.22 20180207 KTOMIAK "Kenneth Tomiak"<CBT_Ken@KTomiak.BIZ>
               1) Hmmm.... IF string <> prior_string and ENTER used
                  THEN DO a FIND.
               2) IF FIND turns up nothing then SETMSG.
----- -------- --------------------------------------------------------
02.21 20180207 KTOMIAK "Kenneth Tomiak"<CBT_Ken@KTomiak.BIZ>
               1) Moved default wish@dsn to the OTHERWISE block.
----- -------- --------------------------------------------------------
02.20 20180207 KTOMIAK "Kenneth Tomiak"<CBT_Ken@KTomiak.BIZ>
               1) Capture RC on LMINIT and LMPUT.
               2) Added TOPIC level full expansion based on CRDG.
               3) Made the table field GENIEDES scrollable.
----- -------- --------------------------------------------------------
02.19 20170919 KTOMIAK "Kenneth Tomiak"<CBT_Ken@KTomiak.BIZ>
               1) Added line selection Preview.
----- -------- --------------------------------------------------------
02.18 20170825 KTOMIAK "Kenneth Tomiak"<CBT_Ken@KTomiak.BIZ>
               1) Changed WHEN to use ABBREV in many lines.
               2) Change Expand(\\) to Expand(||) which led to
                  changing attribute (%|_) to (%~_).
               3) Removed DEBUG logic.
               4) Alter expansion to be expand TOPIC to CHAPTER and
                  CHAPTER to WISH. Expanding a WISH is invalid.
----- -------- --------------------------------------------------------
02.17 20170821 KTOMIAK "Kenneth Tomiak"<CBT_Ken@KTomiak.BIZ>
               1) Moved driver code above comment block.
----- -------- --------------------------------------------------------
02.16 20170715 KTOMIAK "Kenneth Tomiak"<CBT_Ken@KTomiak.BIZ>
               1) Added a sample Select-When block for using more
                  than one WISHLIST data set.
----- -------- --------------------------------------------------------
02.15 20170713 KTOMIAK "Kenneth Tomiak"<CBT_Ken@KTomiak.BIZ>
               1) Modified panel labels TOPIC, CHAPTER, and WISH.
               2) Using VENDOR.GENIE.WISHLIST.
----- -------- --------------------------------------------------------
02.14 20170613 KTOMIAK "Kenneth Tomiak"<CBT_Ken@KTomiak.BIZ>
               1) Support "X" currdsn = curdsname.
               2) Exhaustive search to free //BULK####.
                  I had an EXIT IF a member was asked for and granted.
               3) Corrected initialization of debuglvl to "".
               4) Corrected comparison of debuglvl to sort_rc.
               5) Moved init of sort variables sooner.
----- -------- --------------------------------------------------------
02.13 20170523 KTOMIAK "Kenneth Tomiak"<CBT_Ken@KTomiak.BIZ>
               1) Replaced Plib_Allocated.
                  a) Add PANELEND statements.
                  b) Renamed panel_def to Get_Sigl_Near_Plib.
----- -------- --------------------------------------------------------
02.12 20170522 KTOMIAK "Kenneth Tomiak"<CBT_Ken@KTomiak.BIZ>
               1) Adjusted the saf programmer name length.
----- -------- --------------------------------------------------------
02.11 20170518 KTOMIAK "Kenneth Tomiak"<CBT_Ken@KTomiak.BIZ>
               1) To_Uppercase(geniembr) before validation.
               2) Chased down the RACF name field in the ACEE.
----- -------- --------------------------------------------------------
02.10 20170517 KTOMIAK "Kenneth Tomiak"<CBT_Ken@KTomiak.BIZ>
               1) Blank out GENIEANS before prompting.
               2) Text-Flow the global disclaimer.
               3) Added sample text to PARSE VERSION.
               4) PUTKTM-REXX the comment blocks.
               5) Added ACF list to get programmer-name.
               6) Validate member name before attempting to fetch it.
               7) %genie * will show all of the WISH members.
----- -------- --------------------------------------------------------
02.09 20170516 KTOMIAK "Kenneth Tomiak"<CBT_Ken@KTomiak.BIZ>
               1) Added MSG(ISPZZ102) to TBDISPL: Row x to y of z.
----- -------- --------------------------------------------------------
02.08 20170515 KTOMIAK "Kenneth Tomiak"<CBT_Ken@KTomiak.BIZ>
               1) Process_Routine: allow tbdispl rc(4) on multiple
                  row selects.
                  a) IF keep_going > 0 changed to 4.
                  b) After THEN DO.. END set keep_going = 0.
               2) Added positioning to the first selected member.
                  Position_To_Key:
                    using frowsarg (first row search argument)
               3) Added primary command Locate to position to an member.
                  Also uses frowsarg and Position_To_Key.
----- -------- --------------------------------------------------------
02.07 20170510 KTOMIAK "Kenneth Tomiak"<CBT_Ken@KTomiak.BIZ>
               1) Handling the Parse of LISTUSER results differently.
----- -------- --------------------------------------------------------
02.06 20170508 KTOMIAK "Kenneth Tomiak"<CBT_Ken@KTomiak.BIZ>
               1) Added Top-Secret support to get username and dfltgrp.
               2) Rename RACF_ variables to SAF_.
               3) Added support for sysvaruid.
               4) IF RCVT or RTSS has trouble, or SECNAM not matched by
                  ACF2, RCVT, or RTSS then use GENIEASK to get name
                  and group.
----- -------- --------------------------------------------------------
02.05 20170507 KTOMIAK "Kenneth Tomiak"<CBT_Ken@KTomiak.BIZ>
               1) Added GENIEASK to get username and dfltgrp WHEN
                  not using RACF (CA-ACF2 and CA-Top Secret).
               2) Added id@type = "R" to process a routine.
               3) Renamed ins@line to id@type as in %GENIEBLD.
               4) Support exchange mvs_smfid.
               4) Support exchange zdate.
----- -------- --------------------------------------------------------
02.04 20170505 KTOMIAK "Kenneth Tomiak"<CBT_Ken@KTomiak.BIZ>
               1) Added code to get RCVTID and parse LISTUSER WHEN
                  the security product is RACF.
               2) Added id@type = "?" to pop-up intake panel GENIEASK.
               3) Fixed translating id@type to uppercase.
                  That means the WISH files are now mixed case
                  tolerant. For example, you can now use "X" or "x".
----- -------- --------------------------------------------------------
02.03 20170504 KTOMIAK "Kenneth Tomiak"<CBT_Ken@KTomiak.BIZ>
               1) Modified the 'History of Modifications" to
                  conform with PUTKTM-REXX.
               2) Support case-insensitive id@type values.
               3) Added support for user specified string substitution
                  based on id@type = X using GENIE reserved values.
----- -------- --------------------------------------------------------
02.02 20170417 KTOMIAK "Kenneth Tomiak"<CBT_Ken@KTomiak.BIZ>
               1) Handling a new $$$$$$$$ format of 1 to many
                  "T", "C", or "W" records per member.
               2) Add support for Keywords (1 to many).
----- -------- --------------------------------------------------------
02.01 20170411 KTOMIAK "Kenneth Tomiak"<CBT_Ken@KTomiak.BIZ>
               1) Rebranded INSERTS to GENIE using WISHLIST.
               2) No real zvm support as it lacks the concept
                  to support a PC folder or MVS DSORG=PO DSN.
               3) No real PC support now that SPF/PC will not
                  work on Windodws 8.0 and above.
               4) Using one generic TBDISPL panel.
----- -------- --------------------------------------------------------
02.00 20170330 KTOMIAK "Kenneth Tomiak"<CBT_Ken@KTomiak.BIZ>
               1) Changed zos_dsn to be userid centric.
----- -------- --------------------------------------------------------
01.08 20080119 KTOMIAK "Kenneth Tomiak"<CBT_Ken@KTomiak.BIZ>
               1) Added pop-up IF '?' used to see the complete
                  list of category/class names and description.
----- -------- --------------------------------------------------------
01.07 20080117 KTOMIAK "Kenneth Tomiak"<CBT_Ken@KTomiak.BIZ>
               1) IF PC, assume SPF/PC 4.0, get ZPANPATH and
                  check the panels for a line with &INSLVL = '##' and
                  check IF the level matches what this version of the
                  code requires. IF not found or not matched, check IF
                  we should update them or else exit.
----- -------- --------------------------------------------------------
01.06 20080112 KTOMIAK "Kenneth Tomiak"<CBT_Ken@KTomiak.BIZ>
               1) Renamed to INSERTS, adding PC and VM support.
----- -------- --------------------------------------------------------
01.05 20080103 KTOMIAK "Kenneth Tomiak"<CBT_Ken@KTomiak.BIZ>
               1) Corrected choosing a construct from the panel.
----- -------- --------------------------------------------------------
01.04 20080101 KTOMIAK "Kenneth Tomiak"<CBT_Ken@KTomiak.BIZ>
               1) Reworked how sub-levels are used.
----- -------- --------------------------------------------------------
01.03 20071230 KTOMIAK "Kenneth Tomiak"<CBT_Ken@KTomiak.BIZ>
               1) Force wish@dsn to be coded.
               2) Force wish@cfg to contain names and descriptions.
               3) Only allocate tables and panels IF needed.
----- -------- --------------------------------------------------------
01.02 20071225 KTOMIAK "Kenneth Tomiak"<CBT_Ken@KTomiak.BIZ>
      Merry Christmas.
               1) Expanded to read the class member in order to
                  map statements to files.
----- -------- --------------------------------------------------------
01.01 20071223 KTOMIAK "Kenneth Tomiak"<CBT_Ken@KTomiak.BIZ>
               1) Original base code.
----- -------- --------------------------------------------------------

-----------------------------------------------------------------------

STANDARD GLOBAL DISCLAIMER
The author explicitly disavows any claim whatsoever about the
correctness or functionality of this program, and disclaims liability
for anything and everything bad that might happen in connection with,
before, during, or after using it. I have tried to make it work right,
and I am personally pretty confident that it does, but everybody makes
mistakes, so IF you use it, you do so at your own risk.

=======================================================================
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> */

/* <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
=======================================================================
 This routine initializes constants and variables.
=======================================================================
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> */
Initialization_Routine:
  return_code = 0

  UPPER GENIEMBR

  ADDRESS ISREDIT "(editdsn,origdsn,origlibn) = DATASET"
  ADDRESS ISREDIT "(editmbr) = MEMBER"
  ADDRESS ISREDIT "(bndsl,bndsr) = BOUNDS"
  bnds_left = bndsl + 0
  bnds_right = bndsr + 0

  ZERRALRM = "NO"
  ZERRHM = "*"
  ZERRSM = ""
  ZERRLM = ""

  starter_line = COPIES("1a2b3c4d",9)
  plib_rc = -1

  sysvar_SYSLTERM = SYSVAR("SYSLTERM")
  sysvar_SYSWTERM = SYSVAR("SYSWTERM")

  ADDRESS ISREDIT "(toprow, botrow) = DISPLAY_LINES"
  isredit_rc = RC
  IF (isredit_rc <> 0) THEN DO
    toprow = 0
  END
  resume_row = toprow + 0

  ADDRESS ISREDIT "(zlstline) = LINENUM .ZLast"

  ADDRESS ISREDIT "PROCESS DEST"            /* ISPF adjusts for a/b */
  isredit_rc = RC
  SELECT
    WHEN (isredit_rc = 20) THEN DO /* empty file  */
      destline = 0
    END
    WHEN (isredit_rc = 16) THEN DO
      ZERRALRM = "YES"
      ZERRSM = "PRESS HELP"
      ZERRLM = "Incomplete or conflicting line commands."
      ADDRESS ISPEXEC "SETMSG MSG(ISRZ002)"
      return_code = 16
      RETURN return_code
    END
    WHEN (isredit_rc = 8) THEN DO
      destline = zlstline
      ZERRALRM = "YES"
      ZERRSM = "Insert command pending"
      ZERRLM = ,
        "An A/B line command has not been specified." ,
        ".ZLAST ("destline") will be used instead."
      ADDRESS ISPEXEC "SETMSG MSG(ISRZ002)"
    END
    WHEN (isredit_rc = 0) THEN DO
      line_pos = "AFTER"
      ADDRESS ISREDIT "(destline) = LINENUM .ZDEST"
      isredit_rc = RC
      IF (isredit_rc > 0) THEN DO
        destline = 0
      END
    END
    OTHERWISE DO
      destline = 0
    END
  END
  aftrline = destline + 0

  show_help = 0

  listuser_done = "No"
  valid_names = "@#$ABCDEFGHIJKLMNOPQRSTUVWXYZ0123456789"
  sortfld = "GENIEITM"
  dsplfld = "WISH ID"
  GENIESRT = "A"
  genielvl = "T"

  prior_string = ""
  exp_len = 0
  genieexp = ""
  geniekey = ""
  buildkey = "T"
  key_pos = POS("=",geniembr)
  IF (key_pos > 0) THEN DO
    geniekey = SUBSTR(geniembr,(key_pos + 1))
    GENIEFND = geniekey
    buildkey = ""
    geniembr = ""
  END
  key_len = LENGTH(geniekey)
  SELECT
    WHEN (geniembr = "?") THEN DO
      show_help = 1
      geniembr = ""
    END
    WHEN (geniembr = "*") THEN DO
      geniekey = "*"
    END
    WHEN (geniembr <> "") THEN DO
      mbr@len = LENGTH(geniembr)
      good_name = 0
      IF (mbr@len < 9) THEN DO
        geniember = To_Uppercase(geniembr)
        IF (POS(LEFT(geniembr,1),LEFT(valid_names,29)) = 0 ) THEN DO
          good_name = 1
          SAY "1st character bad:" LEFT(geniembr,1)
        END
        DO ltr = 2 TO mbr@len
          IF (POS(SUBSTR(geniembr,ltr,1),valid_names) = 0 ) THEN DO
            good_name = 1
          SAY ltr "character bad:" SUBSTR(geniembr,ltr,1)
          END
        END
        IF (good_name = 0) THEN DO
          fetch_item = geniembr
          fetchsel = "F"
          return_code = Fetch_The_Member()
          return_code = 2
          RETURN return_code
        END
        ELSE DO
          SAY geniembr "contains invalid characters for a WISH."
        END
      END
    END
    OTHERWISE DO
      NOP
    END
  END

  strglen = 80
  purgekey = ""

  return_code = Tbcreate_Genie()
  IF (return_code = 0) THEN DO
    return_code = Read_Zos_Index()
  END
  buildkey = ""
  plib_rc = Plib_Allocated()
  IF (plib_rc <> 0) THEN DO
    SAY "Problem building a PLIB:" plib_rc
    return_code = plib_rc
    RETURN return_code
  END

  RETURN return_code

/* <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
=======================================================================
 This routine does the processing.
=======================================================================
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> */
Process_Routine:
  ZSCR = "PAGE"
  ADDRESS ISPEXEC "VGET (ZSCREENW)"
  hw = (ZSCREENW - 17) % 2
  ZTDMARK = COPIES("-",hw)"> End of WISHes <"COPIES("-",hw)
  GENIEHD1 = '$$$$'
  GENIEHD2 = 'Genuine Every Noteworthy Itemized Examplizer'
  prevfld = sortfld
  prevsrt = GENIESRT
  frowsarg = ""
  keep_going = 0
  DO WHILE (keep_going = 0)
    ZTDSELS = 0
    ZCMD = ""
    initzcmd = ""
    IF (show_help = 1) THEN DO
      show_help = 0
      initzcmd = "?"
      ADDRESS ISPEXEC "CONTROL NONDISPL ENTER"
    END
    IF (frowsarg <> "") THEN DO
      function_rc = Position_To_Key()
    END
    ADDRESS ISPEXEC ,
      "TBDISPL GENIETBL PANEL(GENIEPRI)" ,
      "MSG(ISPZZ102)"
    keep_going = RC
    frowsarg = ""
    /* RETURN codes                      */
    /* 4  - The enter key or scroll      */
    /*      command used; additional data*/
    /*      remains to be stored         */
    /* 8  - The END or RETURN command was*/
    /*      entered.  IF crp at top, no  */
    /*      lines modified               */
    /*    - Panel was generated from tags*/
    /*      and the EXIT command was     */
    /*      entered                      */
    IF (keep_going = 8) THEN DO
      ZERRALRM = "NO"
      ZERRSM = "OK"
      ZERRLM = "You asked to end this dialog."
      ADDRESS ISPEXEC "SETMSG MSG(ISRZ002)"
      LEAVE
    END
    IF (keep_going > 4) THEN DO
      ZERRALRM = "YES"
      ZERRSM = "NO"
      ZERRLM = "Uh Oh, panel(GENIEPRI)",
        "failed to tbdispl, rc("keep_going")."
      ADDRESS ISPEXEC "SETMSG MSG(ISRZ002)"
      RETURN keep_going
    END
    keep_going = 0
    ZERRALRM = "NO"
    ZERRSM = ""
    ZERRLM = ""
    now_what = Zcmd_Main_Loop()
    IF (now_what <> 0) THEN DO
      keep_going = 1
    END
    IF (keep_going = 0) THEN DO
      now_what = Process_Zdtsel()
    END
  END
  RETURN 0

/* <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
=======================================================================
 This routine does any pre-termination cleanup.
=======================================================================
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> */
Termination_Routine:
  IF (table_create = 0) THEN DO
    ADDRESS ISPEXEC "TBEND SDUMPTBL"
  END
  IF (plib_rc = 0) THEN DO
    ADDRESS ISPEXEC ,
      "LMFREE DATAID(&TMPPNL)"
    ADDRESS ISPEXEC ,
      "LIBDEF ISPPLIB"
    ADDRESS TSO ,
      "FREE FILE("panel_dd")"
  END
  resume_row = resume_row + 1
  ADDRESS ISREDIT "CURSOR =" resume_row "1"
  isredit_rc = RC
  RETURN 0

/* <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
 Remove selected entries from the displayed table.
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> */
Purge_Genietbl:
  IF (purgekey = "") THEN DO
    RETURN 0
  END
  ADDRESS ISPEXEC "TBTOP GENIETBL"
  ADDRESS ISPEXEC "TBSKIP GENIETBL"
  purge_rc = RC
  DO WHILE (purge_rc = 0)
    SELECT
      WHEN (purgekey = "*") THEN DO
        ADDRESS ISPEXEC "TBDELETE GENIETBL"
        purge_rc = RC
      END
      WHEN (purgekey = "C") & ,
           (LENGTH(genieitm) > 4) THEN DO
        ADDRESS ISPEXEC "TBDELETE GENIETBL"
        purge_rc = RC
      END
      OTHERWISE DO
        NOP
      END
    END
    ADDRESS ISPEXEC "TBSKIP GENIETBL"
    purge_rc = RC
  END
  purgekey = ""
  RETURN 0

/* <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
 Create a table to hold the members.
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> */
Tbcreate_Genie:
  ADDRESS ISPEXEC ,
    "TBEND GENIETBL"
  ADDRESS ISPEXEC ,
    "TBCREATE GENIETBL",
    "KEYS(GENIEITM)" ,
    "NAMES(GENIESEL, GENIETPC, GENIECHA,",
      "GENIESEQ, GENIEDES, GENIEAKA)",
    "REPLACE",
    "NOWRITE"
  tbcreate_rc = RC
  /* RETURN codes                      */
  /*  4 - Duplicate table exists but   */
  /*      "replace" specified          */
  /*  8 - Table already exists;        */
  /*      "replace" not specified      */
  /* 12 - Table in use; enq failed     */
  /* 16 - Table input library not      */
  /*      allocated with "write"       */
  /* 20 - Severe error                 */
  IF (tbcreate_rc <> 0) THEN DO
    SAY "Uh Oh, table(GENIETBL) failed to create, rc("tbcreate_rc")."
    RETURN tbl_rc
  END
  RETURN 0

/* <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
 Read configuration member to store members, keywords, and titles.
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> */
Read_Zos_Index:
  "ALLOC FILE(GENIECFG) DATASET('"wish@dsn"("wish@cfg")') SHR REUSE"
  alloc_rc = RC
  IF (alloc_rc <> 0) THEN DO
    SAY "Allocation of the configuration file failed, RC="alloc_rc
    return_code = alloc_rc
    RETURN return_code
  END
  "EXECIO * DISKR GENIECFG (STEM cfg_record. FINIS)"
  diskr_rc = RC
  "FREE FILE(GENIECFG)"
  IF (diskr_rc <> 0) THEN DO
    SAY "Read of the configuration file failed, RC="diskr_rc
    ZERRSM = "DISKR!"
    ZERRLM = "GENIECFG DISKR error:" diskr_rc
    ADDRESS ISPEXEC "SETMSG MSG(ISRZ002)"
    return_code = 16
    RETURN return_code
  END
  return_code = Build_My_Table()
  key_len = 0
  exp_len = 0
  ADDRESS ISPEXEC "TBTOP GENIETBL"
  tbtop_rc = RC
  RETURN 0

/* <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
 Position to a key.
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> */
Position_To_Key:
  ADDRESS ISPEXEC "TBTOP GENIETBL"
  ADDRESS ISPEXEC "TBVCLEAR GENIETBL"
  genieitm = frowsarg
  ADDRESS ISPEXEC "TBSARG GENIETBL" ,
    "NEXT NAMECOND(GENIEITM,GE)"
  tbsarg_rc = RC
  ADDRESS ISPEXEC "TBScan GENIETBL"
  tbscan_rc = RC
  IF (tbscan_rc <> 0) THEN DO
    RETURN 0
  END
  RETURN 0

/* <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
 Sort the table.
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> */
Sort_The_Table:
  ADDRESS ISPEXEC "TBSORT GENIETBL FIELDS("sortfld",C,"GENIESRT")"
  sort_rc = RC
      /* RETURN codes                 */
      /* 12 - Table is not OPEN       */
      /* 16 - Numeric convert error   */
      /* 20 - Severe error            */
  IF (sort_rc <> 0) THEN DO
    IF (sort_rc > 1) THEN DO
      SAY "Sort_The_Table sort RC:" sort_rc
      SAY "  Field:" sortfld "Sequence:" geniesrt"."
    END
  END
  prevfld = sortfld
  prevsrt = GENIESRT
  RETURN 0

/* <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
 Compare the WISH to see if we should show it (add to table).
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> */
Would_It_Match:
  matching = ""
  SELECT
    WHEN (geniekey = "*") * (exp_len > 0) THEN DO
      IF ((To_Lowercase(LEFT(prior_item,LENGTH(genieexp))) = ,
          To_Lowercase(genieexp))) ,
    THEN DO
      matching = matching"*(itm) "
    END
    END
    WHEN (geniekey = "*") THEN DO
      matching = matching"key(*) "
    END
    WHEN (genieexp = "*") THEN DO
      matching = matching"exp(*) "
    END
    WHEN (key_len > 0) THEN DO
      IF (POS(To_Lowercase(geniekey),To_Lowercase(genieaka)) > 0) ,
        THEN DO
        matching = matching"aka "
        find_status = 1
      END
      IF (POS(To_Lowercase(geniekey),To_Lowercase(GENIEDES)) > 0) ,
        THEN DO
        matching = matching"des "
        find_status = 1
      END
      IF (To_Lowercase(geniekey) = ,
          To_Lowercase(LEFT(prior_item,key_len))) ,
        THEN DO
        matching = matching"key(itm) "
        matching = matching"des "
        find_status = 1
      END
    END
    WHEN (exp_len > 0) THEN DO
      IF ((To_Lowercase(LEFT(prior_item,LENGTH(genieexp))) = ,
          To_Lowercase(genieexp)) & ,
          (LENGTH(STRIP(prior_item,"T"," ")) = exp_len)) ,
        THEN DO
        matching = matching"exp(itm) "
        matching = matching"key(itm) "
        matching = matching"des "
        find_status = 1
      END
    END
    WHEN (buildkey = "T") & ,
         (prior_type = "T") THEN DO
      matching = matching"T "
    END
    OTHERWISE DO
      NOP
    END
  END
  RETURN LENGTH(matching)

/* <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
 Loop thru the configuration WISHLIST data and add to the table.
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> */
Build_My_Table:
  ADDRESS ISPEXEC "TBVCLEAR GENIETBL"
  first_sw = 0
  DO cfgx = 1 TO cfg_record.0
    PARSE VAR cfg_record.cfgx,
      =1  file_gen +1,
      =2  file_item +8,
      =10 blank_one +1,
      =11 file_typ +1,
      =12 blank_two +1,
      =13 file_txt
    IF (file_gen <> "G") THEN DO
      ITERATE
    END
    IF (first_sw = 0) THEN DO
      first_sw = 1
      prior_item = file_item
      prior_type = file_typ
      first_wish = ""
    END
    IF (file_item <> prior_item) THEN DO
      function_rc = Would_It_Match()
      IF (matching <> "") THEN DO
        IF (first_wish = "") THEN DO
          first_wish = prior_item
        END
        GENIEDES = STRIP(GENIEDES,"T"," ")
        ADDRESS ISPEXEC ,
          "TBADD GENIETBL ORDER"
        tbadd_rc = RC
        IF (tbadd_rc > 8) THEN DO
          SAY "Uh Oh," genieitm "add failed, rc("tbadd_rc")."
        END
      END
      /* RETURN codes                    */
      /*  4 - Number of rows parameter   */
      /*      was specified but storage  */
      /*      was only obtained for a    */
      /*      single row                 */
      /*  8 - Key tables: a row with the */
      /*      same key already exists;   */
      /*      crp set to top             */
      /* 12 - Table is not OPEN          */
      /* 20 - Severe error               */
      ADDRESS ISPEXEC "TBVCLEAR GENIETBL"
      prior_item = file_item
      prior_type = file_typ
    END
    genieitm = STRIP(file_item,"T"," ")
    GENIETPC = LEFT(file_item,4)
    IF (LENGTH(genieitm) > 5) THEN DO
      GENIECHA = SUBSTR(file_item,5,2)
    END
    IF (LENGTH(genieitm) = 8) THEN DO
      GENIESEQ = SUBSTR(file_item,7,2)
    END
    genietyp = file_typ
    genietxt  = STRIP(file_txt ,"T"," ")
    SELECT
      WHEN (file_typ = "T") THEN DO
        IF (GENIEDES = "") THEN DO
          GENIEDES = genietxt
        END
        ELSE DO
          GENIEDES = GENIEDES" "genietxt
        END
        GENIECHA = "=="
        GENIESEQ = "--"
        genieaka = ""
      END
      WHEN (file_typ = "C") THEN DO
        IF (GENIEDES = "") THEN DO
          GENIEDES = genietxt
        END
        ELSE DO
          GENIEDES = GENIEDES" "genietxt
        END
        GENIESEQ = "--"
        genieaka = ""
      END
      WHEN (file_typ = "W") THEN DO
        IF (GENIEDES = "") THEN DO
          GENIEDES = genietxt
        END
        ELSE DO
          GENIEDES = GENIEDES" "genietxt
        END
        genieaka = ""
      END
      WHEN (file_typ = "K") THEN DO
        IF (genieaka = "") THEN DO
          genieaka = genietxt
        END
        ELSE DO
          genieaka = genieaka" "genietxt
        END
      END
      OTHERWISE DO
        SAY "Huh?" ,
          LEFT(cfg_record.cfgx,70)
      END
    END
  END
  function_rc = Would_It_Match()
  IF (matching <> "") THEN DO
    GENIEDES = STRIP(GENIEDES,"T"," ")
    ADDRESS ISPEXEC ,
      "TBADD GENIETBL ORDER"
    tbadd_rc = RC
    IF (tbadd_rc > 8) THEN DO
      SAY "Uh Oh," genieitm "add failed, rc("tbadd_rc")."
    END
  END
  maxrows = 0
  ADDRESS ISPEXEC ,
    "TBSTATS GENIETBL ROWCURR(MAXROWS)"
  tbstats_rc = RC
    /*  RETURN codes                     */
    /*   0 - Normal RETURN even IF table */
    /*       does not exist              */
    /*  16 - Variable value truncated    */
    /*  20 - Severe error                */
  IF (maxrows > 0) THEN DO
    function_rc = Sort_The_Table()
  END
  ELSE DO
    ZERRALRM = "YES"
    ZERRSM = "PRESS HELP"
    ZERRLM = "What you chose led to an empty table." ,
      "Use the REFRESH primary command to view all TOPICs or",
      "use the FIND option to search for a different string."
    ADDRESS ISPEXEC "SETMSG MSG(ISRZ002)"
  END
  RETURN 0

/* <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
 Remove TOPIC or CHAPTER members.
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> */
Unexpand_Table:
  origfld = sortfld
  origsrt = GENIESRT
  sortfld = "GENIEITM"
  GENIESRT = "A"
  function_rc = Sort_The_Table()
  ADDRESS ISPEXEC "TBTOP GENIETBL"
  ADDRESS ISPEXEC "TBVCLEAR GENIETBL"
  genieitm = genieexp
  ADDRESS ISPEXEC "TBSARG GENIETBL" ,
    "Next NameCond(GENIEITM,GT)"
  tbsarg_rc = RC
  IF (tbsarg_rc <> 0) THEN DO
    RETURN 0
  END
  tbscan_rc = 0
  DO WHILE (tbscan_rc = 0)
    ADDRESS ISPEXEC "TBSCAN GENIETBL"
    tbscan_rc = RC
    IF (LEFT(genieitm,exp_len) <> genieexp) THEN DO
      LEAVE
    END
  ADDRESS ISPEXEC "TBDELETE GENIETBL"
  END
  RETURN 0

/* <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
 Process the FIXED section of the panel - COMMAND field.
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> */
Zcmd_Main_Loop:
  uppercase_zcmd = TRANSLATE(zcmd)
  ZCMD = ""
  SELECT
    WHEN (uppercase_zcmd = "?") THEN DO
      zcmdverb = uppercase_zcmd
    END
    WHEN (ABBREV("SORT",uppercase_zcmd,2) = 1) THEN DO
      zcmdverb = uppercase_zcmd
    END
    WHEN (ABBREV("ORDER",uppercase_zcmd,1) = 1) THEN DO
      zcmdverb = uppercase_zcmd
    END
    WHEN (ABBREV("FINDKYWD",uppercase_zcmd,1) = 1) THEN DO
      zcmdverb = uppercase_zcmd
      zcmditm = GENIEFND
    END
    WHEN (ABBREV("SEARCH",uppercase_zcmd,2) = 1) THEN DO
      zcmdverb = uppercase_zcmd
      zcmditm = GENIEFND
    END
    WHEN (ABBREV("SRCHTEXT",uppercase_zcmd,2) = 1) THEN DO
      zcmdverb = uppercase_zcmd
      zcmditm = GENIEFND
    END
    WHEN (uppercase_zcmd = "") & ,
      (prior_string <> GENIEFND) THEN DO
      IF (GENIEFND = "") THEN DO
        prior_string = ""
        RETURN 0
      END
      zcmdverb = "FINDKYWD"
      zcmditm = GENIEFND
    END
    WHEN (POS(" ",uppercase_zcmd) > 0) THEN DO
      PARSE VAR uppercase_zcmd zcmdverb zcmditm
    END
    WHEN (uppercase_zcmd = "") THEN DO
      RETURN 0
    END
    OTHERWISE DO
      PARSE VAR uppercase_zcmd zcmdverb +1 zcmditm
    END
  END
  function_rc = Zcmd_Selection()
  RETURN 0

/* <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
 Process the COMMAND.
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> */
Zcmd_Selection:
  SELECT
    WHEN (LEFT(zcmdverb,1) = "+") | ,
         (ABBREV("EXPAND",zcmdverb,1) = 1) THEN DO
      IF (LENGTH(zcmditm) = 8) THEN DO
        ZERRALRM = "YES"
        ZERRSM = "PRESS HELP"
        ZERRLM = "You cannot expand a WISH. Request denied!"
        ADDRESS ISPEXEC "SETMSG MSG(ISRZ002)"
      END
      ELSE DO
        genieexp = zcmditm
        exp_len = LENGTH(genieexp) + 2
        call_rc = Build_My_Table()
        exp_len = 0
      END
    END
    WHEN (LEFT(zcmdverb,1) = "*") THEN DO
      IF (LENGTH(zcmditm) = 8) THEN DO
        ZERRALRM = "YES"
        ZERRSM = "PRESS HELP"
        ZERRLM = "You cannot expand a WISH. Request denied!"
        ADDRESS ISPEXEC "SETMSG MSG(ISRZ002)"
      END
      ELSE DO
        geniekey = "*"
        genieexp = zcmditm
        exp_len = LENGTH(genieexp) + 4
        call_rc = Build_My_Table()
        exp_len = 0
      END
    END
    WHEN (LEFT(zcmdverb,1) = "-") | ,
         (ABBREV("UNEXPAND",zcmdverb,1) = 1) THEN DO
      IF (LENGTH(zcmditm) = 8) THEN DO
        ZERRALRM = "YES"
        ZERRSM = "PRESS HELP"
        ZERRLM = "You cannot unexpand (shrink) a WISH. Request denied!"
        ADDRESS ISPEXEC "SETMSG MSG(ISRZ002)"
      END
      ELSE DO
        genieexp = zcmditm
        exp_len = LENGTH(genieexp)
        call_rc = Unexpand_Table()
      END
    END
    WHEN (LEFT(zcmdverb,1) = "?") THEN DO
      GENIEZSL = ""
      go_again = 0
      DO WHILE (go_again = 0)
        ADDRESS ISPEXEC "ADDPOP"
        ADDRESS ISPEXEC "DISPLAY PANEL(GENIEZSL)"
        display_rc = RC
        ADDRESS ISPEXEC "REMPOP"
        IF (display_rc > 0) THEN DO
          go_again = 1
          LEAVE
        END
        SELECT
          WHEN (GENIEZSL = "?") THEN DO
            ZERRALRM = "YES"
            ZERRSM = "No way!"
            ZERRLM = "Asking for recursive help is inexscuable."
            ADDRESS ISPEXEC "SETMSG MSG(ISRZ002)"
          END
          WHEN (GENIEZSL = "") THEN DO
          END
          OTHERWISE DO
            zcmd = GENIEZSL
            function_rc = Zcmd_Main_Loop()
            go_again = 1
          END
        END
      END
    END
    WHEN (ABBREV("FIND" ,zcmdverb,1) = 1) | ,
         (ABBREV("FINDKYWD" ,zcmdverb,1) = 1) | ,
         (ABBREV("KEYWORD" ,zcmdverb,1) = 1) THEN DO
      IF (zcmditm = "") THEN DO
        ZERRALRM = "YES"
        ZERRSM = "Problem!"
        ZERRLM = "You asked to find nothing."
        ADDRESS ISPEXEC "SETMSG MSG(ISRZ002)"
        keep_going = 2
        RETURN 4
      END
      purgekey = "C"
      call_rc = Purge_Genietbl()
      GENIEFND = zcmditm
      geniekey = zcmditm
      key_len = LENGTH(geniekey)
      find_status = 0
      call_rc = Build_My_Table()
      IF (find_status = 0) THEN DO
        ZERRALRM = "YES"
        ZERRSM = "Not found"
        ZERRLM = GENIEFND "does not appear in titles nor keywords."
        ADDRESS ISPEXEC "SETMSG MSG(ISRZ002)"
      END
      ELSE DO
        frowsarg = first_wish
        function_rc = Position_To_Key()
      END
      prior_string = GENIEFND
      key_len = 0
    END
    WHEN (ABBREV("SEARCH",zcmdverb,2) = 1) | ,
         (ABBREV("SRCHTEXT",zcmdverb,2) = 1) THEN DO
      IF (zcmditm <> "") THEN DO
        GENIEFND = zcmditm
        zcmditm = ""
      END
      prior_string = GENIEFND
      genieask = "Search can take a long time, are you sure?" ,
        "Y for YES otherwise No."
      GENIELEN = 3
      GENIELIM = "            " COPIES("+",GENIELEN)
      GENIEANS = ""
      ADDRESS ISPEXEC "DISPLAY PANEL(GENIEASK)"
      IF (ABBREV("YES",To_Uppercase(GENIEANS),1) <> 1) THEN DO
        ZERRALRM = "NO"
        ZERRSM = "Search cancelled"
        ZERRLM = "You asked to cancel the long running search."
        ADDRESS ISPEXEC "SETMSG MSG(ISRZ002)"
        keep_going = 2
        RETURN 4
      END
      IF (GENIEFND = "") THEN DO
        ZERRALRM = "YES"
        ZERRSM = "Problem!"
        ZERRLM = "You asked to search for nothing."
        ADDRESS ISPEXEC "SETMSG MSG(ISRZ002)"
        keep_going = 2
        RETURN 4
      END
      purgekey = "C"
      call_rc = Purge_Genietbl()
      first_sw = 0
      blobfound = 0
      DO cfgx = 1 TO cfg_record.0
        PARSE VAR cfg_record.cfgx,
          =1  file_gen +1,
          =2  file_item +8,
          =10 blank_one +1,
          =11 file_typ +1,
          =12 blank_two +1,
          =13 file_txt
        IF (file_gen <> "G") THEN DO
          ITERATE
        END
        IF (first_sw = 0) THEN DO
          first_sw = 1
          prior_item = file_item
          prior_type = file_typ
          ADDRESS ISPEXEC "TBVCLEAR GENIETBL"
          first_wish = ""
        END
        IF (file_item <> prior_item) THEN DO
          function_rc = Search_Pds_Member()
          prior_item = file_item
          prior_type = file_typ
          ADDRESS ISPEXEC "TBVCLEAR GENIETBL"
        END
        genieitm = STRIP(file_item,"T"," ")
        GENIETPC = LEFT(file_item,4)
        IF (LENGTH(genieitm) > 5) THEN DO
          GENIECHA = SUBSTR(file_item,5,2)
        END
        IF (LENGTH(genieitm) = 8) THEN DO
          GENIESEQ = SUBSTR(file_item,7,2)
        END
        genietyp = file_typ
        genietxt  = STRIP(file_txt ,"T"," ")
        SELECT
          WHEN (file_typ = "T") THEN DO
            IF (GENIEDES = "") THEN DO
              GENIEDES = genietxt
            END
            ELSE DO
              GENIEDES = GENIEDES" "genietxt
            END
            GENIECHA = "=="
            GENIESEQ = "--"
          END
          WHEN (file_typ = "C") THEN DO
            IF (GENIEDES = "") THEN DO
              GENIEDES = genietxt
            END
            ELSE DO
              GENIEDES = GENIEDES" "genietxt
            END
            GENIESEQ = "--"
          END
          WHEN (file_typ = "W") THEN DO
            IF (GENIEDES = "") THEN DO
              GENIEDES = genietxt
            END
            ELSE DO
              GENIEDES = GENIEDES" "genietxt
            END
          END
          WHEN (file_typ = "K") THEN DO
            IF (genieaka = "") THEN DO
              genieaka = genietxt
            END
            ELSE DO
              genieaka = genieaka" "genietxt
            END
          END
          OTHERWISE DO
            SAY "Huh?" ,
              LEFT(cfg_record.cfgx,70)
          END
        END
        GENIEDES = STRIP(GENIEDES,"T"," ")
      END
      function_rc = Search_Pds_Member()
      IF (blobfound = 0) THEN DO
        ZERRALRM = "YES"
        ZERRSM = "Not found"
        ZERRLM = GENIEFND "does not appear in the WISHLIST members."
        ADDRESS ISPEXEC "SETMSG MSG(ISRZ002)"
      END
      ELSE DO
        frowsarg = first_wish
        function_rc = Position_To_Key()
      END
      prior_item = ""
      exp_len = 0
      genieexp = ""
      geniekey = ""
    END
    WHEN (ABBREV("INFO",zcmdverb,1) = 1) THEN DO
      fetch_item = zcmditm
      fetchsel = "I"
      call_rc = Itemize_Detail()
    END
    WHEN (ABBREV("LOCATE",zcmdverb,1) = 1) THEN DO
      frowsarg = zcmditm
      function_rc = Position_To_Key()
    END
    WHEN (ABBREV("ORDER",zcmdverb,1) = 1) THEN DO
      SELECT
        WHEN (GENIESRT = "A") THEN DO
          GENIESRT = "D"
        END
        OTHERWISE DO
          GENIESRT = "A"
        END
      END
      function_rc = Sort_The_Table()
    END
    WHEN (ABBREV("SORT",zcmdverb,4) = 1) THEN DO
      SELECT
        WHEN (prevfld = "GENIEDES") THEN DO
          sortfld = "GENIEITM"
          dsplfld = "WISH ID"
          GENIESRT = "A"
        END
        OTHERWISE DO
          sortfld = "GENIEDES"
          dsplfld = "Title"
          GENIESRT = "A"
        END
      END
      function_rc = Sort_The_Table()
    END
    WHEN (ABBREV("SELECT",zcmdverb,1) = 1) THEN DO
      fetch_item = zcmditm
      fetchsel = "S"
      call_rc = Fetch_The_Member()
      keep_going = 1
    END
    WHEN (ABBREV("WISH",zcmdverb,1) = 1) THEN DO
      purgekey = "C"
      call_rc = Purge_Genietbl()
      IF (GENIEFND = "") THEN DO
        ZERRALRM = "YES"
        ZERRSM = "Problem!"
        ZERRLM = "You asked to find nothing."
        ADDRESS ISPEXEC "SETMSG MSG(ISRZ002)"
        keep_going = 2
        RETURN 4
      END
      geniekey = GENIEFND
      key_len = LENGTH(geniekey)
      call_rc = Build_My_Table()
      key_len = 0
    END
    WHEN (ABBREV("REFRESH",zcmdverb,1) = 1) THEN DO
      genielvl = "T"
      purgekey = "*"
      call_rc = Purge_Genietbl()
      GENIEFND = ""
      key_len = 0
      geniekey = ""
      genieexp = ""
      exp_len = 0
      buildkey = "T"
      call_rc = Read_Zos_Index()
      IF (call_rc <> 0) THEN DO
        RETURN call_rc
      END
    END
    WHEN (ABBREV("CANCEL",zcmdverb,1) = 1) THEN DO
      ZERRALRM = "No"
      ZERRSM = "Cancel accepted."
      ZERRLM = "You asked to cancel the request."
      ADDRESS ISPEXEC "SETMSG MSG(ISRZ002)"
      keep_going = 2
      RETURN 4
    END
    OTHERWISE DO
      ZERRALRM = "YES"
      ZERRSM = "Press help"
      ZERRLM = "You asked GENIE to do something. However," ,
        zcmdverb "makes absolutely no sense."
      ADDRESS ISPEXEC "SETMSG MSG(ISRZ002)"
    END
  END
  zcmdverb = ""
  RETURN 0

/* <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
 Process the )MODEL section of the panel - ROW selection.
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> */
Process_Zdtsel:
  IF (ZTDSELS > 0) THEN DO
    frowsarg = genieitm
  END
  DO WHILE (ZTDSELS > 0)
    GENIESEL = To_Uppercase(GENIESEL)
    function_rc = Line_Selection()
    GENIESEL = ""
    ADDRESS ISPEXEC "TBPUT GENIETBL"
    IF (ZTDSELS > 1) THEN DO
      ADDRESS ISPEXEC "TBDISPL GENIETBL"
    END
    ELSE DO
      ZTDSELS = 0
    END
  END
  RETURN 0

/* <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
 Process the ROW selection.
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> */
Line_Selection:
  SELECT
    WHEN (GENIESEL = "S") THEN DO
      fetch_item = genieitm
      fetchsel = "S"
      call_rc = Fetch_The_Member()
      keep_going = 1
    END
    WHEN ((GENIESEL = "/") | (GENIESEL = "?")) THEN DO
      GENIELSL = ""
      ADDRESS ISPEXEC "ADDPOP"
      ADDRESS ISPEXEC "DISPLAY PANEL(GENIELSL)"
      ADDRESS ISPEXEC "REMPOP"
      SELECT
        WHEN (GENIELSL = "/") THEN DO
          ZERRALRM = "YES"
          ZERRSM = "No way!"
          ZERRLM = "Asking for recursive help is inexscuable."
          ADDRESS ISPEXEC "SETMSG MSG(ISRZ002)"
        END
        OTHERWISE DO
          GENIESEL = To_Uppercase(GENIELSL)
          function_rc = Line_Selection()
        END
      END
    END
    WHEN (GENIESEL = "I") THEN DO
      fetch_item = genieitm
      fetchsel = "I"
      call_rc = Itemize_Detail()
    END
    WHEN (GENIESEL = "P") THEN DO
      fetch_item = genieitm
      call_rc = Preview_Detail()
    END
    WHEN (GENIESEL = "+") | (GENIESEL = "E") THEN DO
      IF (LENGTH(genieitm) = 8) THEN DO
        ZERRALRM = "YES"
        ZERRSM = "PRESS HELP"
        ZERRLM = "You cannot expand a WISH. Request denied!"
        ADDRESS ISPEXEC "SETMSG MSG(ISRZ002)"
      END
      ELSE DO
        priorsel = GENIESEL
        genieexp = STRIP(genieitm,"B"," ")
        exp_len = LENGTH(genieexp) + 2
        call_rc = Build_My_Table()
        exp_len = 0
      END
    END
    WHEN (GENIESEL = "*") THEN DO
      IF (LENGTH(genieitm) = 8) THEN DO
        ZERRALRM = "YES"
        ZERRSM = "PRESS HELP"
        ZERRLM = "You cannot expand a WISH. Request denied!"
        ADDRESS ISPEXEC "SETMSG MSG(ISRZ002)"
      END
      ELSE DO
        geniekey = "*"
        genieexp = STRIP(genieitm,"B"," ")
        exp_len = LENGTH(genieexp)
        call_rc = Build_My_Table()
        exp_len = 0
      END
    END
    WHEN (GENIESEL = "-") | (GENIESEL = "U") THEN DO
      IF (LENGTH(genieitm) = 8) THEN DO
        ZERRALRM = "YES"
        ZERRSM = "PRESS HELP"
        ZERRLM = "You cannot unexpand (shrink) a WISH." ,
          "Request denied!"
        ADDRESS ISPEXEC "SETMSG MSG(ISRZ002)"
      END
      ELSE DO
        genieexp = genieitm
        exp_len = LENGTH(genieexp)
        call_rc = Unexpand_Table()
      END
    END
    OTHERWISE DO
      NOP
    END
  END
  RETURN 0

/* <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
 Process the full member list.
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> */
Search_Pds_Member:
  wish_mbr = STRIP(genieitm,"T"," ")
  reason1 = "Searching" wish_mbr
  ADDRESS ISPEXEC "CONTROL DISPLAY LOCK"
  ADDRESS ISPEXEC "DISPLAY PANEL(GENIEBAR)"
  msg_rc = MSG("OFF")
  "ALLOC FILE(GENIEMBR) DATASET('"wish@dsn"("wish_mbr")') SHR REUSE"
  alloc_rc = RC
  msg_rc = MSG("ON")
  IF (alloc_rc <> 0) THEN DO
    RETURN 0
  END
  "EXECIO * DISKR GENIEMBR (STEM mbr_record. FINIS)"
  diskr_rc = RC
  "FREE FILE(GENIEMBR)"
  IF (diskr_rc <> 0) THEN DO
    RETURN 0
  END
  wishblob = ""
  DO mbrx = 1 TO mbr_record.0
    wishtext = STRIP(SUBSTR(mbr_record.mbrx,2),"B"," ")
    wishblob = wishblob wishtext
  END
  IF (POS(To_Uppercase(GENIEFND),To_Uppercase(wishblob)) > 0) THEN DO
    ADDRESS ISPEXEC ,
      "TBADD GENIETBL ORDER"
    blobfound = 1
    IF (first_wish = "") THEN DO
      first_wish = wish_mbr
    END
  END
  RETURN 0

/* <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
 Break up a string into a smaller length.
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> */
Chop_String_To_Length:
  ts_len = LENGTH(this_string)
  IF (ts_len <= strglen) THEN DO
    return_string = this_string
    this_string = ""
    RETURN return_string
  END
  too_long = ""
  good_long = ""
  num_words = WORDS(this_string)
  a_space = ""
  DO word_ctr = 1 TO num_words
    next_word = WORD(this_string,1)
    IF (LENGTH(next_word) > strglen) THEN DO
      IF (good_long <> "") THEN DO
        RETURN good_long
      END
      ELSE DO
        return_string = LEFT(this_string,strglen)
        this_string = SUBSTR(this_string,(strglen + 1))
        RETURN return_string
      END
    END
    too_long = too_long""a_space""next_word
    line_len = LENGTH(too_long)
    IF (line_len > strglen) THEN DO
      RETURN good_long
    END
    good_long = good_long""a_space""next_word
    a_space = " "
    this_string = STRIP(DELSTR(this_string,1,LENGTH(next_word)),"L"," ")
  END
  RETURN good_long

/* <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
 Pad a string to a desired length.
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> */
Fill_String:
  PARSE ARG padlen , padstring
  IF (LENGTH(padstring) > padlen) THEN DO
    SAY "ABORT"
    RETURN LEFT(padstring,padlen)
  END
  IF (LENGTH(padstring) = padlen) THEN DO
    RETURN padstring
  END
  filllen = padlen - LENGTH(padstring)
  RETURN LEFT(padstring COPIES(" ",filllen),padlen)

/* <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
 Add a line to the current member.
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> */
Add_Line:
  IF ((type_line = "MSGLINE") | (type_line = "NOTELINE")) THEN DO
    IF (LENGTH(newtext) > 72) THEN DO
      oldtext = newtext
      newleft = LASTPOS(LEFT(oldtext,72)," ")
      IF (newleft = 0) THEN DO
        newleft = 72
      end
      newtext = LEFT(oldtext,newleft)
      call_rc = Add_Line()
      newtext = SUBSTR(oldtext,(newleft + 1))
    END
  END
  IF (newtext = "") THEN DO
    newtext = " "
  END
  oldtext = newtext
  IF (newtext = "INFOLINE") THEN DO
    newtext = "INFOLINE "
  END
  IF (type_line = "ERROR") THEN DO
    SAY "Add_Line: ERROR" newtext
    RETURN 0
  END
  ADDRESS ISREDIT ,
    "LINE_BEFORE" aftrline "=" type_line "(newtext)"
  insert_rc = RC
  IF (insert_rc <> 0) THEN DO
    ZERRSM = "Insert error, RC("insert_rc")"
    ZERRLM = LEFT("Really, insert has a problem fetchsel="fetchsel,76)
    ZERRLM = ZERRLM || ,
      LEFT(type_line "after" aftrline "record" cfgx "sigl" sigl,76)
    ZERRLM = ZERRLM || ,
      LEFT(">"newtext"<",76)
    ZERRLM = ZERRLM || ,
      LEFT(">"oldtext"<",76)
    ADDRESS ISPEXEC "SETMSG MSG(ISRZ002)"
    say "ZERR:" zerrsm
    say zerrlm
    return_code = 1
  END
  IF (type_line = "DATALINE") THEN DO
    aftrline = aftrline + 1
  END
  RETURN 0

/* <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
 Process the selected member.
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> */
Fetch_The_Member:
  filename_in = "'"STRIP(wish@dsn,"B","'")"("fetch_item")'"
  insmbr_rc = SYSDSN(filename_in)
  IF (POS("I",debuglvl) > 0) THEN DO
    SAY "Fetch_The_Member:    SysDsn:" insmbr_rc
  END
  IF (insmbr_rc <> "OK") THEN DO
    ZERRSM = "Not found"
    ZERRLM = "The" fetch_item "wish member was not found."
    ADDRESS ISPEXEC "SETMSG MSG(ISRZ002)"
    return_code = 4
    RETURN return_code
  END
  "ALLOC FILE(GENIEMBR) SHR REUSE",
    "DATASET('"wish@dsn"("fetch_item")')"
  alloc_rc = RC
  IF (alloc_rc <> 0) THEN DO
    SAY "Allocation of the wish file failed, RC="alloc_rc
    RETURN 16
  END
  "EXECIO * DISKR GENIEMBR (STEM mbr_record. FINIS)"
  diskr_rc = RC
  "FREE FILE(GENIEMBR)"
  IF (diskr_rc <> 0) THEN DO
    ZERRSM = "DISKR!"
    ZERRLM = "WISH DISKR error:" diskr_rc
    ADDRESS ISPEXEC "SETMSG MSG(ISRZ002)"
    return_code = 16
    RETURN return_code
  END
  IF (fetchsel <> "I") THEN DO
    newtext = starter_line
    ADDRESS ISREDIT ,
      "LINE_AFTER" aftrline "= DATALINE (newtext)"
    starter_rc = RC
    aftrline = aftrline + 1
    ADDRESS ISREDIT ,
      "LABEL" aftrline "= .DELME 0"
    newtext = "Your" fetch_item "wish begins below:"
    type_line = "INFOLINE"
    cfgx = "wish-begins"
    call_rc = Add_Line()
  END
  genieboo = ""
  geniecpy = ""
  geniedte = ""
  geniekwd = ""
  genientr = ""
  geniesrc = ""
  genietle = ""
  genieurl = ""
  genieweb = ""
  DO mbrx = 1 TO mbr_record.0
    mbr_record.mbrx = STRIP(mbr_record.mbrx,"T"," ")
    PARSE VAR mbr_record.mbrx id@type +1 file_txt
    id@type = To_Uppercase(id@type)
    newtext = STRIP(file_txt,"T"," ")
    SELECT
      WHEN (id@type = "@") THEN DO
        IF (geniecpy = "") THEN DO
          geniecpy = newtext
        END
        ELSE DO
          geniecpy = geniecpy newtext
        END
        ITERATE
      END
      WHEN (id@type = "*") THEN DO
        ITERATE
      END
      WHEN (id@type = "B") THEN DO
        IF (geniecpy = "") THEN DO
          genieboo = newtext
        END
        ELSE DO
          genieboo = genieboo newtext
        END
        ITERATE
      END
      WHEN (id@type = "D") THEN DO
        type_line = "DATALINE"
      END
      WHEN (id@type = "E") THEN DO
        IF (genientr = "") THEN DO
          genientr = newtext
        END
        ELSE DO
          genientr = genientr" "newtext
        END
        ITERATE
      END
      WHEN (id@type = "G") THEN DO
        ITERATE
      END
      WHEN (id@type = "I") THEN DO
        type_line = "INFOLINE"
      END
      WHEN (id@type = "K") THEN DO
        IF (geniekwd = "") THEN DO
          geniekwd = newtext
        END
        ELSE DO
          geniekwd = geniekwd" "newtext
        END
        ITERATE
      END
      WHEN (id@type = "M") THEN DO
        type_line = "MSGLINE"
      END
      WHEN (id@type = "N") THEN DO
        type_line = "NOTELINE"
      END
      WHEN (id@type = "R") THEN DO
        SELECT
          WHEN (To_Lowercase(newtext) = "system_symbols") THEN DO
            routine_rc = Ecvtsymt_Symbols()
          END
          OTHERWISE DO
            newtext = STRIP(newtext,"T"," ")
            ADDRESS ISPEXEC "VPUT (aftrline) SHARED"
            ADDRESS ISPEXEC "SELECT cmd("newtext")"
            macro_rc = RC
            ADDRESS ISPEXEC "VGET (aftrline) SHARED"
          END
        END
        ITERATE
      END
      WHEN (id@type = "S") THEN DO
        IF (geniesrc = "") THEN DO
          geniesrc = newtext
        END
        ELSE DO
          geniesrc = geniesrc" "newtext
        END
        ITERATE
      END
      WHEN (id@type = "T") THEN DO
        IF (genietle = "") THEN DO
          genietle = newtext
        END
        ELSE DO
          genietle = genietle" "newtext
        END
        ITERATE
      END
      WHEN (id@type = "U") THEN DO
        IF (genieurl = "") THEN DO
          genieurl = newtext
        END
        ELSE DO
          genieurl = genieurl""newtext
        END
        ITERATE
      END
      WHEN (id@type = "W") THEN DO
        IF (genieweb = "") THEN DO
          genieweb = newtext
        END
        ELSE DO
          genieweb = genieweb" "newtext
        END
        ITERATE
      END
      WHEN (id@type = "X") THEN DO
        PARSE VAR newtext cvarfrom " = " cvarto
        SELECT
          WHEN (To_Lowercase(cvarto) = "editdsn") THEN DO
            ADDRESS ISREDIT "(cvarto) = DATASET"
          END
          WHEN (To_Lowercase(cvarto) = "editmbr") THEN DO
            ADDRESS ISREDIT "(cvarto) = MEMBER"
          END
          WHEN (To_Lowercase(cvarto) = "logon_acct") THEN DO
            ADDRESS ISPEXEC "VGET (ZACCTNUM)"
            cvarto = ZACCTNUM
          END
          WHEN (To_Lowercase(cvarto) = "mvsvar_sysname") THEN DO
            cvarto = MVSVAR("SYSNAME")
          END
          WHEN (To_Lowercase(cvarto) = "saf_dfltgrp") THEN DO
            IF (listuser_done = "No") THEN DO
              listuser_done = Rcvt_Listuser()
            END
            cvarto = SPACE(saf_dfltgrp)
          END
          WHEN (To_Lowercase(cvarto) = "saf_name") THEN DO
            IF (listuser_done = "No") THEN DO
              listuser_done = Rcvt_Listuser()
            END
            cvarto = saf_username
          END
          WHEN (To_Lowercase(cvarto) = "sysvar_sysuid") THEN DO
            cvarto = sys_sysuid
          END
          WHEN (To_Lowercase(cvarto) = "zdate") THEN DO
            cvarto = DATE("S")
          END
          WHEN (To_Lowercase(cvarto) = "ztime") THEN DO
            cvarto = TIME()
          END
          OTHERWISE DO
            newtext = "You must change:" newtext
            type_line = "MSGLINE"
            IF (fetchsel <> "I") THEN DO
              cfgx = "id=r" mbrx
              call_rc = Add_Line()
            END
            ITERATE
          END
        END
        ADDRESS ISREDIT "C '&cvarfrom' '&cvarto' ALL"
        change_rc = RC
        IF (change_rc > 0) THEN DO
          ZERRLM = ZERRLM ,
            LEFT("var:" cvarfrom "is defined, but not used or" ,
              "errored out.",76)
        END
        ITERATE
      END
      WHEN (id@type = "Z") THEN DO
        IF (geniedte = "") THEN DO
          geniedte = newtext
        END
        ELSE DO
          geniedte = geniedte newtext
        END
        ITERATE
      END
      WHEN (id@type = "?") THEN DO
        PARSE VAR newtext cvarfrom " = " GENIELEN " = " genieask
        IF (plib_rc <> 0) THEN DO
          plib_rc = Plib_Allocated()
        END
        GENIELIM = LEFT("Char("GENIELEN")",11)": "COPIES("+",GENIELEN)
        GENIEANS = ""
        ADDRESS ISPEXEC "DISPLAY PANEL(GENIEASK)"
        IF (GENIEANS <> "") THEN DO
          cvarto = LEFT(GENIEANS,GENIELEN)
          ADDRESS ISREDIT "C '&cvarfrom' '&cvarto' ALL"
        END
        ITERATE
      END
      OTHERWISE DO
        SAY "Line id?" id@type"!"
        ITERATE
      END
    END
    IF (fetchsel <> "I") THEN DO
      cfgx = "member" mbrx
      call_rc = Add_Line()
    END
  END
  newtext = "Your" fetch_item "wish was granted above."
  type_line = "INFOLINE"
  IF (fetchsel <> "I") THEN DO
    cfgx = "wish-granted"
    call_rc = Add_Line()
  END
  ADDRESS ISREDIT "DELETE .DELME"
  cfgx = ".delme is gone"
  fetchsel = ""
  RETURN 0

/* <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
 Prompt for the user name WHEN unable to get it from the SAF product.
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> */
Ask_For_Username:
  genieask = "What is your name?"
  GENIELIM = LEFT("Char("GENIELEN")",20)": "COPIES("+",GENIELEN)
  ADDRESS ISPEXEC "DISPLAY PANEL(GENIEASK)"
  IF (GENIEANS <> "") THEN DO
    saf_username = LEFT(GENIEANS,GENIELEN)
  END
  genieask = "What is your group?"
  GENIELIM = LEFT("Char("GENIELEN")",8)": "COPIES("+",GENIELEN)
  ADDRESS ISPEXEC "DISPLAY PANEL(GENIEASK)"
  IF (GENIEANS <> "") THEN DO
    saf_dfltgrp = LEFT(GENIEANS,GENIELEN)
  END
  RETURN 0

/* <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
 Follow CVT to get SAF Product and then list user to get name.
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> */
Rcvt_Listuser:
  good_or_bad = "Bad"
  PTR2PSA = 0                                      /* Pointer to PSA */
  CVTPTR = STORAGE(D2X(PTR2PSA + 16),4)            /* Pointer to CVT */
  CVT = C2D(BITAND(CVTPTR,"7FFFFFFF"X))       /* Flip high order bit */
  JESCT = C2D(STORAGE(D2X(CVT + 296),4))          /* point to JESCT  */
  CVTRAC = C2D(STORAGE(D2X(CVT + 992),4))       /* point to RACF CVT */
  RCVTID = STORAGE(D2X(CVTRAC),4)                 /* point to RCVTID */
                                              /* ACF2, RCVT, or RTSS */
  saf_userid = "Unknown"
  saf_dfltgrp = "Unknown"
  saf_username = "Unknown"
  SELECT
    WHEN (RCVTID = "ACF2") THEN DO
      SECNAM = "CA-ACF2"  /* ACF2 is CA-ACF2     */
      saf_accessor_id = "Unknown"
      saf_accessor_name = "Uknown"
      saf_dfltgrp = "Unknown"
/* untested */
      MAKEBUF
      QUEUE "LIST" sys_sysuid "SECTION(ALL) PROFILE(ALL)"
      QUEUE "End"
      xx = OUTTRAP("acf2_back.")
      ADDRESS TSO "ACF"
      xx = OUTTRAP("OFF")
      DROPBUF
      IF (acf2_back.0 < 2) THEN DO
        ask_rc = Ask_For_Username()
      END
      ELSE DO
        DO saf_ix = 2 TO acf2_back.0
          SELECT
            WHEN (POS(sys_sysuid,acf2_back.saf_ix) = 3) THEN DO
              PARSE VAR acf2_back.saf_ix ,
              saf_id saf_logon_uid saf_accessor_name
              saf_accessor_name = STRIP(saf_accessor_name,"T"," ")
            END
            WHEN (POS("TSOACCT(",acf2_back.saf_ix) > 0) THEN DO
              saf_start = POS("TSOACCT(",acf2_back.saf_ix) + 8
              saf_end = ,
                INDEX(acf2_back.saf_ix,")",saf_start)
              saf_lenx = saf_end - saf_start
              saf_dfltgrp = ,
                SUBSTR(acf2_back.saf_ix,saf_start,saf_lenx)
            END
            OTHERWISE DO
              NOP
            END
          END
        END
      END
/* untested */
      good_or_bad = "Good"
    END
    WHEN (RCVTID = "RCVT") THEN DO
      SECNAM = "RACF"     /* RCVT is RACF        */
      psatold = STORAGE(D2X(548),4)
      ascbasxb = STORAGE(D2X(C2D(psatold)+108),4)
      asxbsenv = STORAGE(D2X(C2D(ascbasxb)+200),4)
      aceeptr = asxbsenv
      saf_dfltgrp = STORAGE(D2X(C2D(aceeptr)+30),8)
      aceeunaa = STORAGE(D2X(C2D(aceeptr)+100),4)
      IF (C2D(aceeunaa) > 0) THEN DO
        aceeunal = STORAGE(D2X(C2D(aceeunaa)),1)
        aceeunam = STORAGE(D2X(C2D(aceeunaa) + 1),C2D(aceeunal) - 1)
        saf_username = STRIP(aceeunam,"B"," ")
      END
      IF (saf_username = "Unknown") THEN DO
        xx = OUTTRAP("racf_back.")
        ADDRESS TSO "LISTUSER" sys_sysuid
        xx = OUTTRAP("Off")
        IF (racf_back.0 < 2) THEN DO
          ask_rc = Ask_For_Username()
        END
        ELSE DO
          dix = 1
          bpos = POS("USER=",racf_back.dix) + 5
          IF (bpos > 5) THEN DO
            saf_userid = SUBSTR(racf_back.dix,bpos,8)
          END
          ELSE DO
            saf_userid = "NotFound"
          END
          bpos = POS("NAME=",racf_back.dix) + 5
          IF (bpos > 5) THEN DO
            saf_username = SUBSTR(racf_back.dix,bpos,20)
          END
          ELSE DO
            ask_rc = Ask_For_Username()
          END
          bpos = POS("OWNER=",racf_back.dix) + 6
          IF (bpos > 6) THEN DO
            saf_owner = SUBSTR(racf_back.dix,bpos,8)
          END
          ELSE DO
            saf_owner = "NotFound"
          END
          bpos = POS("CREATED=",racf_back.dix) + 8
          IF (bpos > 8) THEN DO
            saf_created = SUBSTR(racf_back.dix,bpos,6)
          END
          ELSE DO
            saf_created = "NotFound"
          END

          dix = 2
          bpos = POS("DEFAULT-GROUP=",racf_back.dix) + 14
          IF (bpos > 14) THEN DO
            saf_dfltgrp = SUBSTR(racf_back.dix,bpos,8)
          END
          ELSE DO
            saf_dfltgrp = "NotFound"
          END
          good_or_bad = "Good"
        END
      END
    END
    WHEN (RCVTID = "RTSS") THEN DO
      SECNAM = "TopSecret" /* RTSS is Top Secret */
/* untested */
      xx = OUTTRAP("tss_back.")
      ADDRESS TSO "TSS LIST("sys_sysuid")"
      xx = OUTTRAP("Off")
      IF (tss_back.0 < 4) THEN DO
        ask_rc = Ask_For_Username()
      END
      ELSE DO
        PARSE VAR tss_back.2 . " = " saf_userid " " ,
          . " = " saf_username
        saf_username = STRIP(saf_username,"T"," ")
        DO tssx = 2 TO tss_back.0
          SELECT
            WHEN (POS("DFLTGRP    =",tss_back.tssx) > 0) THEN DO
              PARSE VAR tss_back.tssx . " = " saf_dfltgrp " " .
              tssx = tss_back.0
            END
            OTHERWISE DO
              NOP
            END
          END
        END
      END
/* untested */
      good_or_bad = "Good"
    END
    OTHERWISE DO
      SECNAM = RCVTID
      ask_rc = Ask_For_Username()
    END
  END
  RETURN good_or_bad

/* <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
 System Symbols information sub-routine.
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> */
/**********************************************************************
 Find System Symbols  - ASASYMBP MACRO
   ECVT+X'128' = ECVTSYMT
   2nd half word = # of symbols , after that each entry is 4 words
   1st word = offset to symbol name
   2nd word = length of symbol name
   3rd word = offset to symbol value
   4th word = length of symbol value
**********************************************************************/
Ecvtsymt_Symbols:
  PTR2PSA = 0                                      /* Pointer to PSA */
  CVTPTR = STORAGE(D2X(PTR2PSA + 16),4)            /* Pointer to CVT */
  CVT = C2D(BITAND(CVTPTR,"7FFFFFFF"X) )      /* Flip high order bit */
  ECVT = C2D(STORAGE(D2X(CVT + 140),4))          /* point to CVTECVT */
  ECVTSYMT = C2D(STORAGE(D2X(ECVT + 296),4))    /* point to ECVTSYMT */
  NUMSYMBS = C2D(STORAGE(D2X(ECVTSYMT + 2),2))  /* number of symbols */
  type_line = "DATALINE"
  DO numx = 1 TO NUMSYMBS
    SOFF = (numx * 16) - 16
    NAMOFF = C2D(STORAGE(D2X(ECVTSYMT+4+SOFF),4))     /* name offset */
    NAMLEN = C2D(STORAGE(D2X(ECVTSYMT+8+SOFF),4))     /* name length */
    VALOFF = C2D(STORAGE(D2X(ECVTSYMT+12+SOFF),4))   /* value offset */
    VALLEN = C2D(STORAGE(D2X(ECVTSYMT+16+SOFF),4))   /* value length */
    SYMNAME = STORAGE(D2X(ECVTSYMT+4+NAMOFF),NAMLEN)  /* symbol name */
    IF (VALLEN > 0) THEN DO
      symval = STORAGE(D2X(ECVTSYMT+4+VALOFF),VALLEN)       /* value */
      symname = STRIP(symname,"B"," ")
      symname = STRIP(symname,"B",".")
      symname = STRIP(symname,"B","&")
      newtext = "//"LEFT(SYMNAME,8," ") "SET" symname"='"symval"'"
      cfgx = "ECVTSYMT" numx
      call_rc = Add_Line()
    END /* IF VALLEN > 0 */
  END  /* do NUMSYMBS */
  RETURN s_symbol.0

/* <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
 Read a member to get information details.
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> */
Itemize_Detail:
  call_rc = Fetch_The_Member()
  IF (call_rc > 0) THEN DO
    RETURN call_rc
  END
  strglen = 80
  GENIESTF = ""
  this_string = "Source member selected is:" fetch_item
    partial_string = Chop_String_To_LENGTH()
    partial_string = Fill_String(strglen, partial_string)
    GENIESTF = GENIESTF""partial_string
  IF (geniesrc <> "") THEN DO
    this_string = "Source ID:" geniesrc
    DO WHILE (this_string <> "")
      partial_string = Chop_String_To_LENGTH()
      partial_string = Fill_String(strglen, partial_string)
      GENIESTF = GENIESTF""partial_string
    END
  END
  IF (geniekwd <> "") THEN DO
    this_string = "Also Known As (AKA) keywords:" geniekwd
    DO WHILE (this_string <> "")
      partial_string = Chop_String_To_LENGTH()
      partial_string = Fill_String(strglen, partial_string)
      GENIESTF = GENIESTF""partial_string
    END
  END
  IF (genientr <> "") THEN DO
    this_string = "Entered by:" genientr
    DO WHILE (this_string <> "")
      partial_string = Chop_String_To_LENGTH()
      partial_string = Fill_String(strglen, partial_string)
      GENIESTF = GENIESTF""partial_string
    END
  END
  IF (geniedte <> "") THEN DO
    this_string = "Dated:" geniedte
    DO WHILE (this_string <> "")
      partial_string = Chop_String_To_LENGTH()
      partial_string = Fill_String(strglen, partial_string)
      GENIESTF = GENIESTF""partial_string
    END
  END
  IF (genieboo <> "") THEN DO
    this_string = "Book/Manual:" genieboo
    DO WHILE (this_string <> "")
      partial_string = Chop_String_To_LENGTH()
      partial_string = Fill_String(strglen, partial_string)
      GENIESTF = GENIESTF""partial_string
    END
  END
  IF (genieweb <> "") THEN DO
    this_string = genieweb
    DO WHILE (this_string <> "")
      partial_string = Chop_String_To_LENGTH()
      partial_string = Fill_String(strglen, partial_string)
      GENIESTF = GENIESTF""partial_string
    END
  END
  IF (genieurl <> "") THEN DO
    this_string = genieurl
    DO WHILE (this_string <> "")
      partial_string = Chop_String_To_LENGTH()
      partial_string = Fill_String(strglen, partial_string)
      GENIESTF = GENIESTF""partial_string
    END
  END
  IF (geniecpy <> "") THEN DO
    this_string = "Copyright @" geniecpy
    DO WHILE (this_string <> "")
      partial_string = Chop_String_To_LENGTH()
      partial_string = Fill_String(strglen, partial_string)
      GENIESTF = GENIESTF""partial_string
    END
  END
  partial_string = COPIES(" ",strglen)
  GENIESTF = GENIESTF""partial_string
  partial_string = Fill_String(strglen, "Description")
  GENIESTF = GENIESTF""partial_string
  IF (genietle <> "") THEN DO
    this_string = genietle
    DO WHILE (this_string <> "")
      partial_string = Chop_String_To_LENGTH()
      partial_string = Fill_String(strglen, partial_string)
      GENIESTF = GENIESTF""partial_string
    END
  END
  GENIESTF = GENIESTF""COPIES(" ",strglen)
  center_text = "End of member's information."
  halves = (strglen - LENGTH(center_text)) / 2
  PARSE VAR halves half"." . halfknots
  partial_string = ,
    LEFT(COPIES("-",half)""center_text""COPIES("-",half),strglen)
  GENIESTF = GENIESTF""partial_string

  GENIESTF = GENIESTF""COPIES(" ",strglen)

  geniepfk = Pfkey_Retrieve("End")
  center_text = ,
    "Press the END PFKey <"geniepfk"> to exit."
  halves = (strglen - LENGTH(center_text)) / 2
  PARSE VAR halves half"." . halfknots
  partial_string = ,
    LEFT(COPIES("-",half)""center_text""COPIES("-",half),strglen)
  GENIESTF = GENIESTF""partial_string
  info_rc = 0
  DO WHILE (info_rc = 0)
    ADDRESS ISPEXEC "DISPLAY PANEL(GENIEINF)"
    info_rc = RC
  END
  DROP mbr_record.
  RETURN 0

/* <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
 Read a member to get information details.
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> */
Preview_Detail:
  filename_in = "'"STRIP(wish@dsn,"B","'")"("fetch_item")'"
  ADDRESS ISPEXEC "View" ,
    "DATASET("filename_in")"
  RETURN 0

/* <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
 Retrieve the 24 PFKeys and see which might match the argument.
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> */
Pfkey_Retrieve:
  PARSE UPPER ARG pfk
  ADDRESS ISPEXEC "VGET (ZPF01, ZPF02, ZPF03, ZPF04, ZPF05, ZPF06)"
  ADDRESS ISPEXEC "VGET (ZPF07, ZPF08, ZPF09, ZPF10, ZPF11, ZPF12)"
  ADDRESS ISPEXEC "VGET (ZPF13, ZPF14, ZPF15, ZPF16, ZPF17, ZPF18)"
  ADDRESS ISPEXEC "VGET (ZPF19, ZPF20, ZPF21, ZPF22, ZPF23, ZPF24)"
  pf_key = WORDPOS(pfk,  To_Uppercase( ,
                         ZPF01 ZPF02 ZPF03 ZPF04 ZPF05 ZPF06 ,
                         ZPF07 ZPF08 ZPF09 ZPF10 ZPF11 ZPF12 ,
                         ZPF13 ZPF14 ZPF15 ZPF16 ZPF17 ZPF18 ,
                         ZPF19 ZPF20 ZPF21 ZPF22 ZPF23 ZPF24) )
  pf_key = "PF"RIGHT("00"pf_key,2)
  IF (pf_key = "00") THEN DO
    pf_key = "N/A"
  END
  RETURN pf_key

/* <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
 Convert string to lowercase.
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> */
To_Lowercase: PARSE ARG myarg
  IF (lowercase_letters = "LOWERCASE_LETTERS") THEN DO
    lowercase_letters = "abcdefghijklmnopqrstuvwxyz"
  END
  IF (uppercase_letters = "UPPERCASE_LETTERS") THEN DO
    uppercase_letters = "ABCDEFGHIJKLMNOPQRSTUVWXYZ"
  END
  RETURN TRANSLATE(myarg,lowercase_letters,uppercase_letters)

/* <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
 Convert string to uppercase.
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> */
To_Uppercase: PARSE ARG myarg
  IF (lowercase_letters = "LOWERCASE_LETTERS") THEN DO
    lowercase_letters = "abcdefghijklmnopqrstuvwxyz"
  END
  IF (uppercase_letters = "UPPERCASE_LETTERS") THEN DO
    uppercase_letters = "ABCDEFGHIJKLMNOPQRSTUVWXYZ"
  END
  RETURN TRANSLATE(myarg,uppercase_letters,lowercase_letters)

/* <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
 FUNCTION TO TURN A CHARACTER STRING INTO BINARY BITS.
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> */
C2b: PROCEDURE EXPOSE BITS
  DATA = ARG(1)
  IF (BITS.F <> "1111") THEN DO
    BITS.0="0000"; BITS.1="0001"; BITS.2="0010"; BITS.3="0011"
    BITS.4="0100"; BITS.5="0101"; BITS.6="0110"; BITS.7="0111"
    BITS.8="1000"; BITS.9="1001"; BITS.A="1010"; BITS.B="1011"
    BITS.C="1100"; BITS.D="1101"; BITS.E="1110"; BITS.F="1111"
  END
  dx = C2X(DATA)
  BS=""
  DO I=1 TO LENGTH(dx)
    Q=SUBSTR(dx,I,1)
    BS=BS""BITS.Q
  END
  BS = STRIP(BS,"B"," ")
  RETURN BS

/* <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
 Access the ACEE and get user and group connections.
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> */
Rcvt_Acee:
  psatold = STORAGE(D2X(548),4)              /* Pointer to curr ASCB */
  ascbasxb = STORAGE(D2X(C2D(psatold)+108),4)    /* Get ASXB ADDRESS */
  asxbsenv = STORAGE(D2X(C2D(ascbasxb)+200),4)   /* Get ACEE ADDRESS */
  aceeptr = asxbsenv
  aceeacee = STORAGE(D2X(C2D(aceeptr)+0),4)        /* get eyecatcher */
  user_special = "?"
  saf_connect = ""
  IF (aceeacee <> "ACEE") THEN DO
    SAY "No ACEE"
    RETURN 0
  END

  aceeflg1 = STORAGE(D2X(C2D(aceeptr)+38),1)                 /* flag */
  IF (BITAND(aceeflg1,"80"X) = "80"x) THEN DO
    user_special = "YES"
  END
  ELSE DO
    user_special = "No"
  END

  aceeflg3 = STORAGE(D2X(C2D(aceeptr)+40),1)                 /* flag */
  aceeuser_flag3 = C2b(aceeflg3)
  IF (SUBSTR(aceeuser_flag3,1,1) = 1) THEN DO
    aceecgrp = STORAGE(D2X(C2D(aceeptr)+108),4)   /* addr group list */
    aceeeyec = STORAGE(D2X(C2D(aceecgrp)+00),4)   /* addr group list */
    IF (aceeeyec <> "CGRP") THEN DO
      SAY "Major error on CGRP"
      RETURN 0
    END
    aceecqty = STORAGE(D2X(C2D(aceecgrp)+8),2)            /* quantity */
    cgrp_pos = 32
    cgrp_eyecatcher = STORAGE(D2X(C2D(aceecgrp)+cgrp_pos),4)
    DO gix = 1 TO C2D(aceecqty)
      cgrp_item = STORAGE(D2X(C2D(aceecgrp)+cgrp_pos),8)
      saf_connect = saf_connect""cgrp_item" "
      cgrp_pos = cgrp_pos + 24
    END
  END
  RETURN 0

/* <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
 Allocate a temporary PDS and load dynamically generated panels.
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> */
Plib_Allocated:
  IF (plib_rc = 0) THEN DO
    RETURN 0
  END

  panel_dd = "BULK"RIGHT("0000"RANDOM(99999),4)
  "ALLOC NEW DEL F("panel_dd") DSO(PO) DIR(1) SP(3,3) TRACK",
         "REUSE RECFM(F B) BLKSIZE(0) LRECL(80) UNIT(SYSALLDA)"
  alloc_rc = RC
  IF (alloc_rc <> 0) THEN DO
    SAY "Allocation of BULK dd failed, rc("alloc_rc")."
    RETURN alloc_rc
  END
  ADDRESS ISPEXEC ,
    "LMINIT DATAID(TMPPNL) ENQ(EXCLU) DDNAME("panel_dd")"
  lminit_rc = RC
  IF (lminit_rc <> 0) THEN DO
    SAY "Trouble with LMINIT on TMPPNL, RC("lminit_rc")."
    return_code = lminit_rc
    RETURN return_code
  END
  SIGNAL ON SYNTAX NAME End_Of_Code
  @search = "/*PLIB"
  a_ix = Get_Sigl_Near_Plib()            /* Get near PLIB using SIGL */
  DO UNTIL (srcline = "/*PLIB")/* Find PLIB */
    srcline = SOURCELINE(a_ix)
    a_ix = a_ix + 1
  END
  srcline = SOURCELINE(a_ix)
  SIGNAL ON SYNTAX NAME End_Of_Code
  @search = "PLIB*/"
  DO UNTIL (srcline = "PLIB*/")
    SELECT
      WHEN (LEFT(srcline,6) = "PANEL ") THEN DO
        PARSE VAR srcline . panelname .
        ADDRESS ISPEXEC ,
          "LMOPEN DATAID(&tmppnl) OPTION(OUTPUT)"
      END
      WHEN (srcline = "PANELEND") THEN DO
        ADDRESS ISPEXEC ,
          "LMMADD DATAID(&TMPPNL) MEMBER("panelname")"
        ADDRESS ISPEXEC ,
          "LMClose DATAID(&tmppnl)"
        lmclose_rc = RC
      END
      OTHERWISE DO
        ADDRESS ISPEXEC ,
          "LMPUT DATAID(&TMPPNL) MODE(INVAR)" ,
          "DATALOC(SRCLINE) DATALEN(80)"
        lmput_rc = RC
      END
    END
    a_ix = a_ix + 1
    srcline = SOURCELINE(a_ix)
  END
  SIGNAL OFF SYNTAX
  ADDRESS ISPEXEC ,
    "LIBDEF ISPPLIB LIBRARY ID("panel_dd") STACK"
  RETURN 0

/* <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
Panel_def thru /*PLIB absolutley MUST remain in the same sequence. */
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> */
Get_Sigl_Near_Plib:
  SIGNAL ON SYNTAX
  RETURN aftrline()
aftrline:
  RETURN sigl+4

/*PLIB
PANEL GENIEASK
)ATTR DEFAULT(%+_)
 %   TYPE(TEXT) INTENS(HIGH) Caps(Off)
 +   TYPE(TEXT) INTENS(LOW) Caps(Off)
 _   TYPE(INPUT) INTENS(Low) CAPS(Off) JUST(LEFT) Hilite(UScore)
)BODY Expand(||)
%|-| GENIE has a question for you.|-|
%Command ===>_ZCMD                                                             #
+
%&GENIEASK
+Your answer:_GENIEANS
%&GENIELIM
+
+Search string: &GENIEFND
+
)INIT
)REINIT
)PROC
VER(&GENIEANS,LEN,LE,&GENIELEN)
)END
Contact:  "Kenneth Tomiak"<CBT_Ken@KTomiak.BIZ>
PANELEND
PANEL GENIEBAR
)ATTR DEFAULT(%+_)
  % TYPE(TEXT) INTENS(HIGH)
  + TYPE(TEXT) INTENS(LOW)
  _ TYPE(INPUT) INTENS(HIGH) CAPS(ON) JUST(LEFT)
)BODY Expand(||) WINDOW(80,12)
%| | GENIE Search in progress | |
+
+&Reason1
+
+Search string: &GENIEFND
+
)INIT
)PROC
)END
Contact:  "Kenneth Tomiak"<CBT_Ken@KTomiak.BIZ>
PANELEND
PANEL GENIEINF
)ATTR DEFAULT(%+_)
 %   TYPE(TEXT) INTENS(HIGH) Caps(Off)
 +   TYPE(TEXT) INTENS(LOW) Caps(Off)
 _   TYPE(INPUT) INTENS(HIGH) CAPS(ON) JUST(LEFT) Hilite(UScore)
 $ Type(Output) Caps(Off) Intens(Low) Color(Green)
 ? TYPE(PIN)                         /* panel instruction line       */
 # TYPE(NT)                          /* normal text attribute        */
 ! TYPE(DT)                          /* description text             */
 @ AREA(Dynamic) Scroll(On) Extend(On) /* extendable scrollable area */
)BODY Expand(||)
%|-| GENIE WISHLIST Member Description |-|
#
%Information for &fetch_item follows:
#
|-|
@GENIESTF                                                                      @
)INIT
.HELP = GENIEHLP
)REINIT
)PROC
)HELP
)END
Contact:  "Kenneth Tomiak"<CBT_Ken@KTomiak.BIZ>
PANELEND
PANEL GENIELSL
)ATTR DEFAULT(%~_)
 %   TYPE(TEXT) INTENS(HIGH) Caps(Off)
 ~   TYPE(TEXT) INTENS(LOW) Caps(Off)
 _   TYPE(INPUT) INTENS(HIGH) CAPS(ON) JUST(LEFT) Hilite(UScore)
)BODY EXPAND(||)
%|-| GENIE Line Commands |-|
~
%Line Command ==>_Z~
%Line Function
~---- |-|
%  S ~Selects a member.
%  P ~Preview a member.
%/   ~Brings up this pop-up panel.
%+ E ~Expands any TOPIC to CHAPTERs or CHAPTER to WISHes.
%- U ~Unexpands the TOPIC or CHAPTER.
%  I ~Shows additional information about the member.
~---- |-|
~
)INIT
.ZVARS = 'GENIELSL'
)REINIT
)PROC
)HELP
)END
Contact:  "Kenneth Tomiak"<CBT_Ken@KTomiak.BIZ>
PANELEND
PANEL GENIEPRI
)ATTR DEFAULT(%~_) FORMAT(MIX)
 /* % TYPE(TEXT) INTENS(HIGH)                                         */
 ~ TYPE(TEXT) INTENS(LOW)
 /* _ TYPE(INPUT) INTENS(HIGH) CAPS(ON) JUST(LEFT)                    */
 _ TYPE(Input)  Intens(High) Caps(Off) Just(LEFT) Hilite(uscore)
 # Type(Output) Intens(High) Caps(Off) Just(Left) Pas(On) Hilite(UScore)
 @ Type(Output) Intens(High) Caps(Off) Just(Left)
 $ Type(Output) Intens(Low)  Caps(Off) Just(Left) PAS(On) Hilite(Uscore)
)BODY EXPAND(||) WIDTH(&ZSCREENW)
%|-| GENIE Member Selector |-|
%COMMAND ==>_Z | |%SCROLL ==>_ZSCR~
%
#sort~:&dsplfld #Order~:_z~ String:_GENIEFND | |#Keyword~#Search~
@Z   %:~&GENIEHD2
%S~=Select %+~=Expand %-~=Unexpand %I~=Information %*~=Full expand %P~=Preview
~Sel  TOPIC CH  SQ  TITLE
~---  ----  --  --  |-|
)Model
~_Z~ @Z   ~@Z ~@Z ~$GENIEDES
)INIT
.ZVARS = 'ZCMD, GENIESRT, GENIEHD1, +
   GENIESEL, GENIETPC, GENIECHA, GENIESEQ'
.CURSOR = 'ZCMD'
&Sort = 'Sort'
&Order = 'Order'
&Keyword = 'Keyword'
&Search = 'Search'
IF (&initzcmd = '?')
  &ZCMD = &initzcmd
)REINIT
)PROC
)FIELD
Field(GENIEDES) Len(255) SCROLL(On)
)PNTS
FIELD(Sort)     VAR(ZCMD) VAL('SORT')
FIELD(Order)    VAR(ZCMD) VAL('ORDER')
FIELD(Keyword)  VAR(ZCMD) VAL('FINDKYWD')
FIELD(Search)   VAR(ZCMD) VAL('SRCHTEXT')
)END
Contact:  "Kenneth Tomiak"<CBT_Ken@KTomiak.BIZ>
PANELEND
PANEL GENIEZSL
)ATTR DEFAULT(%~_)
 %   TYPE(TEXT) INTENS(HIGH) Caps(Off)
 ~   TYPE(TEXT) INTENS(LOW) Caps(Off)
 _   TYPE(INPUT) INTENS(HIGH) CAPS(ON) JUST(LEFT) Hilite(UScore)
)BODY Expand(||)
%|-| GENIE Primary Commands |-|
~
%Primary Command ==>_GENIEZSL
%Primary            Function
~------------------ |-|
%Select wish       ~Selects a wish.
%?                 ~Brings up this pop-up panel.
%+ Expand level    ~Expands any TOPIC to CHAPTERs or CHAPTER to WISHes.
%*                 ~Expands all TOPICs and CHAPTERs.
%- Unexpand level  ~Unexpands the TOPIC or CHAPTER.
%Info member       ~Shows additional information about the member.
%Cancel            ~Just another way to end without selecting a wish.
%Find string       ~Finds your string in Descriptions and As Known
%                  ~As (AKA) keyword fields.
%Keyword string    ~Finds your string in Descriptions and As Known
%                  ~As (AKA) keyword fields.
%Search string     ~Slower search of all text.
%Locate            ~Scroll to the location of a member.
%Sort              ~Toggles between item and description fields.
%Order             ~Toggles between Ascending and Descending sequence.
%Refresh           ~Reread the configuration file and rebuild table.
~---------- |-|
~
)INIT
)REINIT
)PROC
)HELP
)END
Contact:  "Kenneth Tomiak"<CBT_Ken@KTomiak.BIZ>
PANELEND
PLIB*/

/* <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
 Should never get here.
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> */
End_Of_Code :
  SIGNAL OFF SYNTAX
  SAY "End of source found before" @search "was found."
Syntax:
  SIGNAL OFF SYNTAX
  RETURN 4

/* <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
=======================================================================
End of code.
=======================================================================
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> */
