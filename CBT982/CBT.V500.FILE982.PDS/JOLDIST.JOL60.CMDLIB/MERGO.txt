 listcat: macro(
    %3330,%3340,%2314,%3350,            /* UNITS WHICH MAY BE CODED */
    %L=,%NODE=,%LEVEL=,           /* NODE POINTS WHICH MAY BE CODED */
    %ALL,                  /* IF ALL IS NOT CODED,LISTCAT WILL ONLY
                              LIST THE CATALOG ON SYSRES.
                              HOWEVER,IF NODE OR L IS SPECIFIED.
                              ALL IS ASSUMMED */
    VOL,VOLS,CVOL,CVOLS    /* OPTIONALLY CODED VOLUMES FOR CATALOG
                              LISTING */
    );

/* Modified to use IDCAMS, May 2001 */
/* Check if no parameters, and if so, display a screen for the User */

  if %list(1) = ''   & %panels ¬= 'OFF'
  then do;

     %level='%sysuid';

     panel       (hi ctr 'ListCat Command')
                 (hi ctr '---------------')
           ///////
           ('      Enter Level of Catalog to List   ===> ',level,
                       30,'%level')
   /*      ('      And Control Volume Name(s) or ALL ==> ',cvol,
                       44,'%cvol')  */
         helpline('Press F3 to Leave without Listing the Catalog(ue)');
 chkpan:

     if %syspfk = 3 then exit;

     if %level = ''
     then do;
          panel reread from level 'Please Enter Catlog Level to List';
          redo chkpan;
     end;
 /*  if %cvol = ''
     then do;
          panel reread from cvol  'Please the Control Volume or ALL';
          redo chkpan;
     end;  */

  /* %level ='(%level)';                 /* Add Brackets */
     %cvol  ='(%cvol)';

  end;

  /* For VSAM Catlog(ue)s - May 2001, C. V. Clarke    */

     if %l¬=''|%level¬=''|%node¬=''
     then do;
          %ALL='ALL';
          %L=' LEVEL(%L%LEVEL%NODE)';
     end;
     %CONT1=' LISTCAT  %L';
     DCL PRINTER PRINTER NODCB;
     DCL CONTROLX * REPLACE;
%CONT1
EOF;
     DCL IDCAMS PROG
          SYSPRINT WRITES PRINTER
          SYSIN READS CONTROLX;

     RUN IDCAMS;

      exit;    /* Leave IEHLIST code temporarily - MVT ?? */
     /*------------------------------------------------------*/
    /* THESE NEXT LINES MUST BE ALTERED FOR EACH INSTALLATION */
 dcl %defunit init '3330';  /* Change if 2314 etc */
 dcl %defvol1 init 'DA04';  /* Change for 1st cvol,if you have one */
 dcl %defvol2 init 'D107';  /* Change for 2nd cvol,if you have one */
 dcl %defvol3 init '';      /* Change for 3rd cvol,if you have one */
 dcl %defvol4 init '';      /* Change for 4th cvol,if you have one */
 dcl %defvol5 init '';      /* Change for 5th cvol,if you have one */

    /* Now let's fix up the unit which we are going to generate */

   IF %3330¬='' | %3340¬=''
   |  %2314¬='' | %3350¬='' /* IS 3330,2314 ETC
                                         coded on instruction? */
     then %defunit='%3330%3340%2314%3350';
/*  'LISTCAT VOLS    SYSRES1,SYSRES2;' WE CAN
       SAFELY ASSUMME 'ALL',SO LET'S FIX THAT UP */
     if %vol¬=0 | %vols¬=0 | %cvol¬=0 | %cvols¬=0
     then do;
        %ALL='ALL';   /* SET ALL ON */
                 /* Now we want to pick up the volumes the user
                    specified,but we may as well reset %defvol1 etc
                    defined earlier, and initialise them to the vols
                    the user wanted, then we can use the same bit of
                    code for the 'all' processing later */

        %DEFVOL1='';
        %DEFVOL2='';
        %DEFVOL3='';
        %DEFVOL4='';
        %DEFVOL5='';
        %DEFVOL3='';

          /* The line of code merely sets %vol=the concatenation
             of cvol,cvols,vol and vols.

             Therefore, if the user codes:

                   'LISTCAT CVOL 11111 VOLS 222222';

             He will be in error,and some curious error messages
             will result, but you can't check for everything.
             can you ? */

        %vol=%vol+%vols;
        %vol=%vol+%cvol;
        %vol=%vol+%cvols;
        %vol=%vol+1;             /* Point to first vol */
        %defvol1= '%list(%vol)'; /* Set up first vol */

        %vol=%vol+1;
        if %list(%vol)¬='' then %defvol2='%list(%vol)';

        %vol=%vol+1;
        if %list(%vol)¬='' then %defvol3='%list(%vol)';
        %vol=%vol+1;
        if %list(%vol)¬='' then %defvol4='%list(%vol)';
        %vol=%vol+1;
        if %list(%vol)¬='' then %defvol5='%list(%vol)';
     end  /* Of vol,vols,cvol,cvols processing */   ;

     /* Now,if node | level set up,turn on 'all' again so we
        try all the volumes */

     if %l¬=''|%level¬=''|%node¬=''
     then do;
          %all='all';
          %l=',node=%l%level%node';
     end;
          /* Now we can set up the control cards using the
             'replace' facility.
             Note that if a card(after replacement) is all blank,
             Jol won't put it in the card stream,so we must use null
             symbolics to get this effect if we only want to generate
             one control card */

 DCL %CONT1 INIT '';
 DCL %CONT2 INIT '';
 DCL %CONT3 INIT '';
 DCL %CONT4 INIT '';
 DCL %CONT5 INIT '';
 DCL %CONT6 INIT '';
            %DDS='';
            IF %ALL='' /* ONLY WANT ONE CONTROL CARD TO
                       GENERATE A LISTCATLG FOR THE SYSRES VOL */
            THEN DO;
                 DCL SYSRES DS SYS1.LINKLIB;
                 DCL SYSRES2 DS NODSN VOLREF SYSRES;
                 %DDS=' DD1 READS SYSRES2 ';
                 %CONT1=' LISTCTLG ';
            END;
            IF %ALL¬='' /* ELSE,WE HAVE TO GENERATE A FEW CARDS */
            THEN DO;
                IF %DEFVOL1¬=''
                THEN DO;
                   DCL DD1 DS NODSN VOL %DEFVOL1 UNIT %DEFUNIT;
                   %DDS='%DDS DD1 READS DD1 ';
                   %CONT2=' LISTCTLG VOL=%DEFUNIT=%DEFVOL1%L';
                END;
                IF %DEFVOL2¬=''
                THEN DO;
                   DCL DD2 DS NODSN VOL %DEFVOL2 UNIT %DEFUNIT;
                   %DDS='%DDS DD2 READS DD2 ';
                   %CONT3=' LISTCTLG VOL=%DEFUNIT=%DEFVOL2%L';
                END;
                IF %DEFVOL3¬=''
                THEN DO;
                   DCL DD3 DS NODSN VOL %DEFVOL3 UNIT %DEFUNIT;
                   %DDS='%DDS DD3 READS DD3 ';
                   %CONT4=' LISTCTLG VOL=%DEFUNIT=%DEFVOL3%L';
                END;
                IF %DEFVOL4¬=''
                THEN DO;
                   DCL DD4 DS NODSN VOL %DEFVOL4 UNIT %DEFUNIT;
                   %DDS='%DDS DD4 READS DD4 ';
                   %CONT5=' LISTCTLG VOL=%DEFUNIT=%DEFVOL4%L';
                END;
                IF %DEFVOL5¬=''
                THEN DO;
                   DCL DD5 DS NODSN VOL %DEFVOL5 UNIT %DEFUNIT;
                   %DDS='%DDS DD5 READS DD5 ';
                   %CONT6=' LISTCTLG VOL=%DEFUNIT=%DEFVOL5%L';
                END;
            END;
            /* NOW GENERATE IEHLIST,AND EXECUTE IT */
     DCL PRINTER PRINTER NODCB;
     DCL CONTROLX * REPLACE;
%CONT1
%CONT2
%CONT3
%CONT4
%CONT5
%CONT6
EOF;
     DCL IEHLIST PROG
          SYSPRINT WRITES PRINTER
          SYSIN READS CONTROLX
          %DDS;
     RUN IEHLIST;
     END;
