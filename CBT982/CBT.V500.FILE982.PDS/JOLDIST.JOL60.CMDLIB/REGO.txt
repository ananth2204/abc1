MSSLIST:MACRO
         /************************************************************/
         /*                                                          */
         /*      M S S L I S T   M A C R O                           */
         /*                                                          */
         /* A MACRO TO ALLOW REPORTS ON MASS STORAGE VOLUME GROUPS   */
         /* TO BE PRODUCED.                                          */
         /*                                                          */
         /************************************************************/
(
 %GROUPS=,    /* LIST OF MASS STORAGE VOLUME GROUPS                  */
 %REPORT=USAGE, /* REPORT TYPE (USAGE OR STATUS)                     */
 %LEGEND,     /* IF SPECIFIED, PRODUCE A LEGEND                      */
 %NOLEGEND,   /* DUMMY                                               */
 %SYSOUT=,    /* SYSOUT                                              */
 %DEST=,      /* SYSOUT DESTINATION                                  */
 %NOPANEL,    /* IF SPECIFIED, DONT DISPLAY JOL PANEL                */
 %PANEL       /* DUMMY                                               */
);
DCL %LGNDTYPE INIT 'NOLEGEND';
IF %LEGEND ¬= '' THEN
    %LGNDTYPE = %LEGEND;
IF %NOPANEL = '' THEN
    PANEL
    ('                           M S S L I S T ')
    ('                           ============= ')/
    ('MASS STORAGE VOLUME GROUPS (MANDATORY)            ==>',
           GROUPS, 80, '%GROUPS')
    ('REPORT ("USAGE" OR "STATUS")                      ==>',
           REPORT, 6, '%REPORT')/
    ('LEGEND/NOLEGEND                                   ==>',
           LGNDTYPE, 8, '%LGNDTYPE')/
    ('SYSOUT CLASS (IF OMMITTED, SAME AS JOB MSGCLASS)  ==>',
           SYSOUT, 1, '%SYSOUT');
IF %LGNDTYPE ¬= 'LEGEND' & %LGNDTYPE ¬= 'NOLEGEND' THEN
    %LGNDTYPE = 'NOLEGEND';
IF %REPORT ¬= 'USAGE' & %REPORT ¬= 'STATUS' THEN
    %REPORT = 'USAGE';
IF %GROUPS = '' THEN
    STOP 'GROUPS PARAMETER NOT SUPPLIED';
ELSE
    DO;
DCL INPUT * REPLACE PRINTALL;
 LISTMSVI GROUP(-
 %GROUPS)-
          VOLUMELIST(ALL)-
          REPORT(%REPORT)-
          %LGNDTYPE
EOF;
DCL PRINT        SYSOUT %SYSOUT %DEST;
DCL KQCAMS       PROG  REGION = 256 K
        SYSPRINT WRITES PRINT
        SYSIN    READS  INPUT;
RUN KQCAMS;
END;
END;
