COMPRESS:MACRO(%SHR);
 /* THIS JOL MACRO WILL COMPRESS UP TO 5 DATA SETS IN 1 GO
       NOTE, UNLESS 'SHR' IS SPECIFIED,DATA SETS WILL GET AN 'OLD'
 DISPOSITION   */
     /* DEFINE PRINTER AND WORK FILES FOR 'IEBCOPY' */
DCL PRINTER PRINTER NODCB;
DCL WORK1 DS 5 CYLS NODCB SYSDA;
DCL WORK2 DS 5 CYLS NODCB SYSDA;
  /* NOW AT LEAST ONE DATA SET MUST BE COMPRESSED, SO LET'S DEFINE
 A SYMBOLIC FOR THAT VARIABLE (LATER, IF MORE THAN 1, WE SHALL
 CONCATENATE OTHER ITEMS TO IT  */
         IF %SHR='' THEN %SHR='UPDATES ';/* 'OLD' IF ¬'SHR' */
         IF %SHR='SHR' THEN %SHR=' READS ';
DCL %COMPDS INIT ' DD1 %SHR %LIST(1)'; /* 1ST DATA SET FIXED */
DCL %COMP2,%COMP3,%COMP4,%COMP5 INIT '';
DCL  %WK1,%WK2,%WK3,%WK4,%WK5 INIT '';
         /* NOW FIND OUT HOW MANY DSIDS ETC THERE ARE, AND FIX 'EM UP*/
         IF %LIST(2) THEN DO;      /* IF SECOND ITEM,DO FOLLOWING */
            %COMP2=' DD2 %SHR %LIST(2) ';
            %WK2=' COPY INDD=DD2,OUTDD=DD2';   /* 2ND CONTROL */
         END;  /* OF DSID NUMBER 2 */
         IF %LIST(3) THEN DO;      /* IF 3RD ITEM,DO FOLLOWING */
            %COMP3=' DD3 %SHR %LIST(3) ';
            %WK3=' COPY INDD=DD3,OUTDD=DD3';   /* 3RD CONTROL */
         END;  /* OF DSID NUMBER 3 */
         IF %LIST(4) THEN DO;      /* IF 4TH ITEM,DO FOLLOWING */
            %COMP4=' DD4 %SHR %LIST(4) ';
            %WK4=' COPY INDD=DD4,OUTDD=DD4';   /* 4TH CONTROL */
         END;  /* OF DSID NUMBER 4 */
         IF %LIST(5) THEN DO;      /* IF 5TH ITEM,DO FOLLOWING */
            %COMP5=' DD5 %SHR %LIST(5) ';
            %WK5=' COPY INDD=DD5,OUTDD=DD5';   /* 5TH CONTROL */
         END;  /* OF DSID NUMBER 5 */
 /* NOW LETS DEFINE IEBCOPY AND ITS WORK DATA SETS */
         DCL IEBCOPY PROG
               SYSPRINT WRITES PRINTER, SYSIN READS COPYCNTR,
               SYSUT3 WRITES WORK1,SYSUT4 WRITES WORK2,
               %COMPDS %COMP2 %COMP3 %COMP4 %COMP5;
 /* NOW DEFINE COPYCNTR WITH THE CONTROL STMTS IN IT */
           DCL COPYCNTR * REPLACE;
 COPY INDD=DD1,OUTDD=DD1
 %WK2
 %WK3
 %WK4
 %WK5
EOF;
%SYSLABEL:
         RUN IEBCOPY;   /*DO COMPRESS OR DEGAUSS */
     /* NOW CHECK IF WE HAVE > 6 DSIDS, AND IF SO, CALL OURSELVES
        'RE-ENTRANTLY'*/
DCL %COPY2 INIT '';
               IF %LIST(6)¬='' THEN DO;
               %COPY2=' %LIST(6) ';
               IF %LIST(7) THEN DO; %COPY2=' %COPY2 %LIST(7) ';
               IF %LIST(8) THEN DO; %COPY2=' %COPY2 %LIST(8) ';
               IF %LIST(9) THEN DO; %COPY2=' %COPY2 %LIST(9) ';
               IF %LIST(10) THEN DO; %COPY2=' %COPY2 %LIST(10) ';
               IF %LIST(11) THEN STOP 'TOO MANY DATA SETS TO COMPRESS'
               ;
             END;END;END;END;
             COMPRESS %COPY2  ;
             END;
         END  /* OF JOL MACRO COMPRESS */  ;
