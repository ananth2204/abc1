 /*****************************************************************/
 /**          Test if a Holiday            Created May, 1987     **/
 /**          -----------------            BY C. CLARKE          **/
 /**                                                             **/
 /** This macro tests if today is a Holiday.  It reads the       **/
 /**    member of the Schedule Data Set call HOLS1987, HOLS19nn  **/
 /**    and checks the lists of datas provided against today's   **/
 /**    date, and sets %HOLIDAY=YES or NO.                       **/
 /**                                                             **/
 /** It also %HOLIDAYA (a pseudo array) with two digit numbers   **/
 /**    indicating the holidays.                                 **/
 /** Then %LIST items are set to 'Y' based on the day of the     **/
 /**    month.  Then Users can quickly see if the next day, or   **/
 /**    the day after that is a holiday.                         **/
 /**                                                             **/
 /** It also sets %LASTDAY to the number of days in the month.   **/
 /**    Then Users can quickly see if the next day, or the day   **/
 /**    after that is the last day of the month.                 **/
 /**                                                             **/
 /*****************************************************************/
holidayt:MACRO ;
 /*****************************************************************/
 /******************SYMBOLIC DECLARES *****************************/
 /*****************************************************************/

   /* VARIABLES BELOW USED IN LOOPS FOR SUBSCRIPTS ETC */

 DCL %INDEX                        INIT('1');
 DCL %HOLIDAY                      ext init('NO');
 DCL %HOLIDAYA                     ext init(''); /* Return Array
                                                    of Holidays */
 DCL %LASTDAY                      ext init(''); /* # days in month */
 DCL %holiday                      ext init('NO');
 DCL %work,%work2,%number          INIT('');
 DCL %hmonth                       INIT('');
 DCL %hdayno                       INIT('');
 DCL %F                            INIT(' FOREVER');
 DCL %MONTHDAY                     INIT(
                                   '312831303130313130313031');
 /*****************************************************************/
 /*****CHECK THAT THERE IS VALID INPUT TO THIS MACRO***************/
 /*****************************************************************/

    IF %LIST(1)¬= ''
    THEN STOP 'Invalid Parameter on HOLIDAY Testing Macro';

 /* Set up %LASTDAY to contain the number of days in the month */

    %work=%monthno*2;              /* Get correct start point  */
    %work=%work-1;                 /* Get correct start point  */
    %lastday=substr(monthday,%work,2); /* Get number of Days */
    %lastday=%lastday+0;           /* Make sure it's numeric   */

 /* See if a Leap Year, and Feb                                */

    if %monthno=2                  /* Feb?                     */
    then do;
       %work=%year/4;
       %work=%work*4;
       if %work=%year
          then %lastday=%lastday+1;  /* Fixed Leap Year */
    end;

 /* Set up %HOLIDAYA to contain Saturday and Sunday day numbers */

    dcl %daywrkno      init('');

    %daywrkno=%dayno;               /* Copy */
 /* Reduce it to a number from 1 to 7  */
label loop1;
    if %daywrkno>7
    then do;
        %daywrkno=%daywrkno-7;
        redo loop1;
    end;
    %work=substr(day,1,3);         /* Get only MON if Monday     */
    if %work='SAT' then %i=0;
    if %work='SUN' then %i=1;
    if %work='MON' then %i=2;
    if %work='TUE' then %i=3;
    if %work='WED' then %i=4;
    if %work='THU' then %i=5;
    if %work='FRI' then %i=6;

    %work=%daywrkno-%i;            /* Got SAT offset             */

 /* Set %HOLIDAYA to contain the day-numbers of SAT and SUN      */
label loop2;
    %i=%work;
    if %i>0
    then do;
        if %i<10 then %i='0%i';
        %holidaya='%holidaya%i';   /* Set SAT  */
    end;
    %i=%work+1;
    if %i>0
    then do;
        if %i<10 then %i='0%i';
        %holidaya='%holidaya%i';   /* Set SUN  */
    end;
    %work=%work+7;                 /* For the following week */
    if %work<50 then redo loop2;   /* Allow a few extra days */

    alloc f(holidays) da(%$jolpref.jol.schedule(hols%year)) shr;
    open  f(holidays) input;
    IF %lastcc¬=0
    then do;
         STOP 'Holiday Member HOLS%year not found: Schedule Exiting';
         exit 16;
    end;
    %hmonth=substr(month,1,3);     /* Get the first 3 characters */
    %hdayno=%dayno+0;              /* Drop Leading Digit         */

 /*****************************************************************/
 /*****READ THE MEMBER LOOKING FOR A HOLIDAY **********************/
 /*****************************************************************/
 label readlup;
    if eof(holidays)
    then do;
       close file(holidays);
       free file(holidays);
       %index=index(holidaya,'%DAYNO');
       if %index¬=0 then %holiday='YES';
       exit;
    end;
    read file(holidays) into(work);

    %work=substr(work,1,71);       /* Drop Lines Numbers            */

 /* Drop off any data after a /* so that Users can code comments    */

    %index=index(work,'/*');
    if %index¬=0
    then %work=substr(work,1,%index); /* Drop Comments */

    %index=index(work,'%hmonth');  /* Does record have this month ? */
    if %index=0 then redo readlup%f; /* No, so get the next         */

 /* Here we have the right month.                                   */
 /* Now we must check if THIS DAY is mentioned in the record.
    and also set this day up in %HOLIDAYA "Array".                  */

    %work2=substr(work,%index);    /* Drop Month, copy days only    */
label sethola;
        if %work2=''                 /* No More left ?             */
           then redo readlup %f;     /* All blank no, read next rec*/
        %work3=substr(work2,1,1);    /* Get 1st character          */
        %work2=substr(work2,2);      /* Drop 1st character in work2*/

        if %work2='' & %work3=''     /* No More left ?             */
           then redo readlup %f;     /* All blank no, read next rec*/
        if %work3=''
           then redo sethola %f;     /* Blank, so skip it       */
  /* Non Blank character here */
        %type=type(work3);
        if %type¬='NUM'
           then redo sethola %f;     /* Alpha, so skip it    */
        %number='%work3';            /* Save the number            */
  /* Get next character, see if numeric */
        %work3=substr(work2,1,1);    /* Get 1st character          */
        %work2=substr(work2,2);      /* Drop 1st character in work2*/
        %type=type(work3);
        if %type¬='NUM'              /* Alpha, set up 1 number     */
        then do;
            %holidaya='%holidaya'||'0%number';/*add dates to 'array'*/
            redo sethola %f;         /* See if more on this line.  */
        end;
  /* Here, the DAYNO is greater than 10;                           */
        %holidaya='%holidaya%number%work3'; /* Add to 'array'  */
        redo sethola %f;             /* See if more on this line.  */
    end;
 END ; /* END OF MACRO */
