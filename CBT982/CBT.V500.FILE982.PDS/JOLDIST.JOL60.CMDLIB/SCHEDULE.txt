 print: macro (%form=,%class=,%free,%at,%end,%close,%hold,
            %recfm=,%lrecl=,%blksize=,
            %dest=,%route=,%copies=,
            on,part,%paper,%stationery,%outlim=);


dcl %f1                    init('');

/* If User codes a parameter, then execute the comand immediately,
   otherwise we will show the user a menu */

        if %list(1) ¬= '' then %f1='%list(1)';

        if %f1 ¬= '' & %buildjob='YES'
        then do;
            BLD04P                  '   Print  %commtail;';
            exit;
        end;

loop:


/* Check if no parameters, and if so, display a screen for the User */

        if %list(1) = ''   & %panels ¬= 'OFF'
        then do;
            list0001='';
            form    ='';
            class   ='';
            free    ='Y';
            dest    ='';
            route   ='';
            copies  ='';
            outlim  ='';

     panel box   (hi ctr 'Print Command')
                 (hi ctr '-------------')
           ////
           ('      File to Print ===> ',f1,
                       44,'%f1')
         / ('      Free File after Printing  ==> ',free,
                       1,'%free')
           ('      Number of Copies          ==> ',copies,
                       3,'%copies')
           ('      Sysout Class              ==> ',class,
                       1,'%class')
           ('      Form Class                ==> ',form,
                       8,'%form')
           ('      Destination               ==> ',dest,
                       8,'%dest')
           ('      Route                     ==> ',route,
                       8,'%route')
           ('      Outlim                    ==> ',outlim,
                       6,'%outlim')
   /*    / ('      Format Listing (Y or N)   ==> ',format,
                       1,'%format')
   */
helpline
('F3 Exits, F4 Select from Jol Data Base, F5 Select from Catalog.');

checkf1:
        if Syspfk=3 then exit;

        if %syspfk=4
        then do;
            dbfshow  all 'File to Print';
            f1='%showsel';
        end;

        if %syspfk=5
        then do;
            showdsn  all 'File to Print';
            f1='%showsel';
        end;
        if f1=''
        then do;
            panel reread from f1 'You must supply a file name';
            goto checkf1;
        end;

        if syspfk=3 then exit;   /* Back to Caller */

gotfile:
        commtail=f1;

        if %free   = 'Y'
        then %free='FREE AT END';

        commtail=commtail || ' %free';
        IF %CLASS ¬='' THEN %commtail='%commtail Class = %class';
        IF %COPIES¬='' THEN %commtail='%commtail Copies = %copies';
        IF %FORM¬=''   THEN %commtail='%commtail FORM %FORM ';
        IF %DEST¬=''   THEN %commtail='%commtail DEST %DEST ';
        IF %ROUTE¬='' THEN %commtail='%commtail ROUTE %ROUTE ';
        IF %HOLD¬=''   THEN %commtail='%commtail %HOLD ';
        IF %OUTLIM¬='' THEN %commtail='%commtail OUTLIM %OUTLIM ';
/*      IF %FORMAT¬='' THEN %commtail='%commtail FORMAT ';*/

        if %BUILDJOB='YES'
        then do;
            BLD04P                  '   Print  %commtail;';
            exit;
        end;
        Print %commtail;           /* Recurs to ourselves */
        exit;

   end;

   DCL %P INIT 'DCL PRINTER PRINTER %CLASS NODEF ';
   IF %FORM¬=''              THEN %P='%P FORM %FORM ';
   IF %DEST¬='' | %ROUTE¬='' THEN %P='%P DEST %DEST %ROUTE ';
   IF %FREE¬=''              THEN %P='%P %FREE %AT %END %CLOSE      ';
   IF %HOLD¬=''              THEN %P='%P %HOLD  ';
   IF %OUTLIM¬=''            THEN %P='%P OUTLIM %OUTLIM ';
   IF %RECFM   ¬=''          THEN %P='%P RECFM %RECFM   ';
   IF %LRECL   ¬=''          THEN %P='%P LRECL %LRECL   ';
   IF %BLKSIZE ¬=''          THEN %P='%P BLKSIZE %BLKSIZE ';
   IF %ON¬=0                 THEN %P='%P  %LIST(%ON+1) PART PAPER ';
   ELSE IF %PART¬=0          THEN %P='%P  %LIST(%PART-1) PART PAPER ';

   DCL PRINTER2 PRINTER %CLASS NODEF;
   DCL NULLFILE DS DUMMY;
   DCL IEBGENER PROG
       SYSPRINT WRITES PRINTER2
       SYSUT1 READS %LIST(1)
       SYSUT2 WRITES PRINTER
       SYSIN  READS NULLFILE;
   IF %COPIES¬=''
   THEN DO;
        %P= '%P COPIES=%COPIES';
   END;
   %P;
   RUN IEBGENER;
END  /* MACRO */  ;
