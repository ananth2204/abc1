ENDNET:MACRO;
 DCL %SYSNETID INIT '' EXT;
 DCL %SYSJOBS1 INIT '' EXT;  /* KEEPS LIST OF NETWORK JOBS SUBMITTED */
 DCL %SYSJOBS2 INIT '' EXT;  /* KEEPS LIST OF NETWORK JOBS SUBMITTED */
 DCL %SYSNTINC INIT('') EXT; /* KEEPS INCLUDE DATA SET FOR JOBS      */
 DCL %SYSNTMAC INIT('') EXT; /* KEEPS JOL MACRO DATA SETS FOR JOBS   */
 DCL %NAMES,%OPS INIT ''; /* LOCAL VARIABLE */
 DCL %L,%K     INIT '';   /* LOCAL VARIABLE */
 IF %SYSNETID=''
 THEN DO;
    STOP 'ENDNET COMMAND EXECUTED WITHOUT PRECEEDING NETWORK COMMAND';
    EXIT;
 END;
 WRITE FILE($$JCODE) FROM('    SAVESYMS');
 %NAMES=SUBSTR(SYSJOBS1,2);     /* DROP FIRST COMMA */
 LABEL OUTPUT;
 %OPS='';
 %L=LENGTH(NAMES);
 IF %L>50
 THEN DO;
      %OPS=SUBSTR(NAMES,1,50); /* TAKE FIRST 50 CHARACTERS */
      %NAMES=SUBSTR(NAMES,51); /* REDUCE INPUT STRING BY 50 CHARS */
      %K=INDEX(NAMES,',');     /* FIND A COMMA IN STRING THAT'S LEFT */
      IF %K¬=0                 /* THERE IS A COMMA */
      THEN DO;
          %Z=SUBSTR(NAMES,K);  /* SET Z TO STRING INCLUDING COMMA */
          %OPS='%OPS%Z';       /* ADD TO OPS (OUTPUT STRING */
          %K=%K+1;
          %NAMES=SUBSTR(NAME,K); /* REMOVE THAT BIT NOW */
      END;
      ELSE DO;
          %NAMES='%NAMES%OPS';
          %OPS='';
      END;
      WRITE FILE($$JCODE) FROM('        %OPS');
      IF %NAMES¬='' THEN REDO OUTPUT;
 END;
 WRITE FILE($$JCODE) FROM('        %NAMES');
 IF %SYSJOBS2¬=''
 THEN DO;
     %NAMES='%SYSJOBS2';
     REDO OUTPUT;
 END;

 WRITE FILE($$JCODE)
 FROM('        IN %$JOLPREF.JOLNET.STATUS(%SYSNETID);');
 WRITE FILE($$JCODE) FROM('    CLOSE FILE(TEMPNET);');
 WRITE FILE($$JCODE)
 FROM('    DEQ SPFDSN/''%$JOLPREF.JOLNET.STATUS'';');
 WRITE FILE($$JCODE) FROM('    IF %%JOBSUBD=''YES''');
 WRITE FILE($$JCODE) FROM('    THEN');
 WRITE FILE($$JCODE) FROM('    EXIT JOB; /* END THIS JOB COMPILE */');
 WRITE FILE($$JCODE) FROM('    ELSE EXIT QUIT; /* QUIT, FINISHED */');
 CLOSE F($$JSTAT);           /* CLOSE SYMBOLIC FILE */
 CLOSE F($$JCODE);           /* CLOSE CODE FILE */
 FREEFILE F($$JSTAT);        /* FREE THE FILES NOW */
 FREEFILE F($$JCODE);
 CHECKNET %SYSNETID;
 %SYSNETID='';
 END ;
