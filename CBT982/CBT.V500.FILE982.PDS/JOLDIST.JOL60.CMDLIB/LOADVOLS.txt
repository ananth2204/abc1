JOBCARD:MACRO;
      %JOBNAME = %SYSUID;
      IF %LIST(1) ¬= '' THEN %JOBNAME = '%JOBNAME%LIST(1)';
      ELSE %JOBNAME = '%JOBNAME'||'X';
 /* YOU MAY ALTER THE FOLLOWING CODE TO INCLUDE ACCOUNT NUMBERS
     FOR SPECIFIED USER-IDS.
 */
    %ACCT='NOACCOUNT';
    IF %SYSUID='OPSG10' THEN %ACCT='T94991I0133,0G10';
    IF %SYSUID='OPSG13' THEN %ACCT='T94991I0133,0G13';
 PANEL  ('        C.C.S. - J.O.L.       %DAY %DAYNO %MONTH %YEAR')
      /('                      STANDARD JOB CARD     ')
       ('          ENTER JOBNAME.       ===>',JOLNAME,8,'%JOBNAME')
       ('          ENTER ACCOUNT        ===>',ACCNT,30,'%ACCT')
       ('          ENTER PROGR NAME     ===>',PGNAME,10,'%SYSUID')
       ('          ENTER JOB CLASS      ===>',CLASS,1,'A')
       ('          ENTER MSGCLASS       ===>',MSGCLASS,1,'X')
       ('          ENTER USER           ===>',USER,8,'%SYSUID')
       ('          ENTER PASSWORD       ===>',PASS,8,'')
       ('          ENTER MSGLEVEL       ===>',MSGLV,3,'1,1')
       ('          ENTER PRIORITY       ===>',PRTY,2,'03')
       ('          ENTER NOTIFY ID.     ===>',NOTY,8,'%SYSUID');
      DCL %JOBC INIT '';
      DCL %JOBEXTRA INIT '';
      IF  %PGNAME ¬= '' THEN %JOBC = 'USER ''%PGNAME''';
      IF  %ACCNT ¬= '' THEN %JOBC = '%JOBC ACCT %ACCNT';
      IF  %CLASS  ¬= '' THEN %JOBC = '%JOBC CLASS %CLASS';
      IF  %MSGCLASS  ¬= '' THEN %JOBC = '%JOBC MSGCLASS %MSGCLASS';
      IF  %PASS  ¬= '' THEN %JOBEXTRA = 'PASSWORD=%PASS';
      IF  %USER  ¬= '' THEN %JOBEXTRA = '%JOBEXTRA,USER=%USER';
      IF  %PRTY  ¬= '' THEN %JOBC = '%JOBC PRTY %PRTY';
      IF  %NOTY  ¬= '' THEN %JOBC = '%JOBC NOTIFY %NOTY';
      EDIT MSGLV A,B A(1),X(1),A(1);
      IF  %A = '1' THEN %JOBC = '%JOBC PJCL';
      ELSE %JOBC = '%JOBC NPJCL';
      IF  %B = '1' THEN %JOBC = '%JOBC PALLOC';
      ELSE %JOBC = '%JOBC NPALLOC';
      %JOLNAME:JOB %JOBC JOBEXTRA='%JOBEXTRA';
      END;
