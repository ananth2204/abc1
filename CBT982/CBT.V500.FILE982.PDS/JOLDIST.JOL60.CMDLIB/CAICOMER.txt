 CAICOMER: MACRO;
   PANEL blue box
 //////////
(ctr hi 'Would you like to take a revision test')
(ctr hi 'of this chapter ?',#4,3,'NO');
if #4 = 'YES' | #4 = 'Y' then DO; CAACOMER; syspfk = 0; END;
if syspfk=3 | syspfk=15 then exit;
PAN005:
PANEL blue box/ (ctr hi 'COMMUNICATIONS AND ERROR HANDLING')
(ctr hi '(including READ/WRITE)')
 /('        There are a number of commands in JOL for operator')
  ('        communications, exceptions and error handling. They are:-')
//(hi '            DISPLAY, RETURN, TYPE, STOP,')
  (hi '            STOP WHEN, ON ERROR, SIGNAL ERROR.')
 /('        We shall discuss them here in some detail.')
        %helpline;
if syspfk=7 | syspfk=19 then goto pan005;
if syspfk=3 | syspfk=15 then exit;
PAN010:
PANEL blue box
 /('     The DISPLAY instruction prints a message on the Systems')
  ('     Log. The message is NOT typed on the operators console.')
  ('     The TYPE, SIGNAL ERROR, or STOP instruction should be used if')
  ('     the message is required to be typed on the operators console.')
 /('     Format:')
 /(hi '           DISPLAY  { number | ''message'' } ;')
 /('     The DISPLAY instruction is most often used to inform those')
  ('     examining the output from the job about certain situations')
  ('     or conditions. It can, for example, be used to indicate when')
  ('     a restart point has been passed, and if a restart is required')
  ('     for any reason - how it can be done.')
 /('     NOTE: ''message'' must be less than 100 characters and')
  ('     enclosed in single quotes.')
        %helpline;
if syspfk=7 | syspfk=19 then goto pan005;
if syspfk=3 | syspfk=15 then exit;
PAN015:
PANEL blue box
/(hi ctr 'Examples of the DISPLAY instruction:-')
 /('     1.   ' hi 'DISPLAY 10;')
 /('          The message JOLE11-01 ''10'' will appear'
' on the System Log.')
 /('     2.   ' hi '%%START=''SORT10'';')
  ('          ' hi 'DISPLAY ''RESTARTING AT %%START'';')
 /('          The following message will appear on the Systems Log:')
 /('           ' hi 'UJC10-01 ''RESTARTING AT SORT10''')
        %helpline;
if syspfk=7 | syspfk=19 then goto pan010;
if syspfk=3 | syspfk=15 then exit;
PAN020:
PANEL blue box
/(hi ctr 'The RETURN instruction.')
 /('        The RETURN instruction writes the message to the Systems')
  ('        Log terminates the job immediately.')
 /('        The message is not displayed on the operators console.')
  ('        After execution of the RETURN, the remainder of the job is')
  ('        flushed and no further processing is possible.')
 /('        Example:')
 /(hi '              RETURN ''SIMPLE EXAMPLE OF RETURN'' ;')
 /('        The message ''SIMPLE EXAMPLE OF RETURN'' will be displayed')
  ('        on the Systems Log and processing will be terminated')
  ('        immediately.')
        %helpline;
if syspfk=7 | syspfk=19 then goto pan015;
if syspfk=3 | syspfk=15 then exit;
PAN025:
PANEL blue box
/(hi ctr 'The TYPE instruction.')
 /('    The TYPE instruction types a message on the operators')
  ('    console and the Systems log.')
 /('    It is most often used to inform the operators about some')
  ('    situations or conditions. It can be used, for example, to')
  ('    inform the operator that a particular job will not execute for')
  ('    some reason or that a critical error occured in the job and it')
  ('    will be terminating before reaching the normal end of the job.')
 /('    Examples of the TYPE instruction:-')
 /('    1.   ' hi 'TYPE 10;')
 /('         The message JOLE10-01 ''10'' appears on the System Log')
  ('         and the Operators console.')
 /('    2.   ' hi 'TYPE ''THIS MESSAGE GOES TO THE OPERATOR'';')
  ('         ' hi 'DISPLAY ''RESTARTING AT %%START'';')
        %helpline;
if syspfk=7 | syspfk=19 then goto pan020;
if syspfk=3 | syspfk=15 then exit;
PAN030:
PANEL blue box
/(hi ctr 'The STOP instruction.')
 /('    The STOP instruction writes the message to the Systems Log and')
  ('    operators console. The job is terminated immediately.')
 /('    Format:  'hi'   STOP { ''message'' | number } ; ')
 /('    Note: If the STOP is executed after a SYMBOLIC IF statement')
  ('          or it is executed unconditionally, the message')
  ('          will be output as a Jol compiler message.')
 /('    Examples  1.' hi ' STOP ''SIMPLE EXAMPLE OF STOP'';')
  ('                 This message will be displayed with JOL compiler')
  ('                 error messages (if any); the job will not be')
  ('                 allowed to start to execute.')
 /('              2.' hi ' IF SORT -= 0 THEN')
  ('                ' hi ' STOP ''SORT FAILED'';')
  ('                 The message ''SORT FAILED'' will be displayed')
  ('                 on the operators console and the Systems Log and')
  ('                 the job will be terminated.')
        %helpline;
if syspfk=7 | syspfk=19 then goto pan025;
if syspfk=3 | syspfk=15 then exit;
PAN035:
PANEL blue box
 / (hi ctr 'The STOP WHEN instruction.')
 /('      The STOP WHEN instruction is used to terminate the job when')
  ('      certain return codes or ranges of return codes are issued')
 /('      Format:')
  (hi '              STOP WHEN condition |  | condition ....  ? ;')
 /('      where condition is:')
 /(hi '              ANY  'normal'comparision-operator   number')
  (hi '      Notes:')
  ('      1. The Jol default is STOP WHEN ANY=16 | ANY > 2000 ; thus')
  ('         whenever any program that executed returns a code of 16')
  ('         or greater than 2000, the job is terminated.')
  ('      2. You may not execute STOP WHEN instruction after an IF')
  ('         test; use the STOP instruction.')
  ('      3. You may code one STOP WHEN instruction per JOL job.')
 /('      Example      ' hi 'STOP WHEN ANY = 10;')
        %helpline;
if syspfk=7 | syspfk=19 then goto pan030;
if syspfk=3 | syspfk=15 then exit;
PAN040:
  PANEL blue box/ (hi ctr 'The ON ERROR command')
 /('     The ON ERROR command will cause a copy of the processing')
  ('     programs storage to be taken if an abend occurs.')
 /('     Format:')
 /('         'hi'ON ERROR  { SNAP | DUMP }  |SYSTEM | options? ;')
 /('     where the options are:-')
 /('         'hi'TO PRINTER classname     eg. TO PRINTER A ; ')
  ('         'hi'|HOLD | NOHOLD?')
 /('     'hi'Note:  Only one ON ERROR command should be coded, and')
  ('     this should preferably be done before any RUN commands.')
  ('     Example:'hi' ON ERROR SNAP TO PRINTER B HOLD;')
  ('             If an abend occurs, the dump will be written to')
  ('             a printer with class B, the dump will not be printed')
  ('             until the operator specifies it to be printed because')
  ('             HOLD was specified.')
        %helpline;
if syspfk=7 | syspfk=19 then goto pan035;
if syspfk=3 | syspfk=15 then exit;
PAN045:
  PANEL blue box/ (hi ctr 'The SIGNAL ERROR instruction')
 /('    The SIGNAL ERROR instruction is used to indicate that an error')
  ('    has occured.')
 /('    Format 'hi'SIGNAL ERROR severity, ''message'' ;')
 /('    where the severity is 0, 1, 2, 3 or 4 and indicates the')
  ('    severity of the error.')
 /('    Generally severity is'hi' 0 - information only')
  ('                         'hi' 1 - warning')
  ('                         'hi' 2 - minor error')
  ('                         'hi' 3 - severe error')
  ('                         'hi' 4 - terminal error.')
  ('    The job is allowed to run depending on error severity eg.')
 /('    if SIGNAL ERROR 4 then the job is not allowed to start,')
  ('    if SIGNAL ERROR 3 then the job is allowed to start only')
  ('    when the Operator releases it.')
        %helpline;
if syspfk=7 | syspfk=19 then goto pan040;
if syspfk=3 | syspfk=15 then exit;
PAN050:
  PANEL blue box/(ctr 'Examples of the 'hi'SIGNAL ERROR instruction.')
  //('      1.'hi' SIGNAL ERROR 1, ''SIMPLE EXAMPLE OR ERROR'' ;')
   /('      After executing the SIGNAL ERROR, the message will appear')
    ('      with any other JOL compiler error messages. Because')
    ('      severity was one (1) the job will be allowed to start.')
  //('      2.'hi' IF %%%%LIB = ''SYS2.SVCLIB'' THEN')
    ('        'hi' SIGNAL ERROR 3,''SYS2.SVCLIB IS TO BE REORGANISED'';'
    )
   /('      After SIGNAL ERROR is executed, the message will be placed')
    ('      on the error log, because the severity was 3 the operator')
    ('      will have to release the job manually.')
        %helpline;
if syspfk=7 | syspfk=19 then goto pan045;
if syspfk=3 | syspfk=15 then exit;
PAN055:
  PANEL box (hi ctr 'READ / WRITE instructions.')
 /('       READ  - used to place input from the terminal into a')
  ('               symbolic variable.')
 /('       WRITE - used to write a message to a terminal, when')
  ('               running JOL interactively.')
//('       For example:')
 /('          'hi'IF %%VOL = '' '' THEN  /* NULL VOLUME  */')
  ('          'hi'DO;')
  ('          'hi'   WRITE ''PLEASE SPECIFY VOLUME'' ;')
  ('          'hi'   READ VOL; ')
  ('          'hi'END;')
  ('          'hi'DUMPVOL  %%VOL; ')
        %helpline;
if syspfk=7 | syspfk=19 then goto pan045;
if syspfk=3 | syspfk=15 then exit;
PAN060:
syspfk = 0;
PANEL box //////////
(ctr hi 'Would you like to take a revision test')
(ctr hi 'of this chapter ?',#4,3,'YES');
    if #4 = 'YES' | #4 = 'Y' then CAACOMER;
CHNUM = CHNUM + 1;
if CHNUM > MAXCHAP then DO;
    PANEL box  ///////(ctr ' This is the end of this section.')
           /(ctr 'Press ENTER to get back to main index/menu.',X,1);
    CHNUM = 1;
    syspfk = 3;
  END;
END;  /* OF CAICOMER MACRO */
