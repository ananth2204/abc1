 /*****************************************************************/
 /**               SUBMIT MACRO            LAST MODIFIED MAY 1987**/
 /**               ------------            BY C. CLARKE          **/
 /**                                                             **/
 /** MODIFIED D. DOUBTFIRE 18.01.83 FOR JCL SUBMITS              **/
 /** MODIFIED C. CLARKE    26.03.87 FOR NETWORKING               **/
 /**                                                             **/
 /** THIS MACRO WILL SUBMIT BOTH JOL JOBS AND JCL JOBS           **/
 /**                                                             **/
 /** TO SUBMIT A JOL JOB SIMPLY PUT -SUBMIT JB1;                 **/
 /** WHERE JB1 IS A MEMBER OF YOUR INCLUDE LIBRARY CONTAINING    **/
 /** JOL CODE.                                                   **/
 /**                                                             **/
 /** TO SUBMIT JCL SIMPLY PUT -SUBMIT '//JOBNAME JOB MSBCLASS=A' **/
 /**                                  '//     EXEC PGM=MYPROG  ' **/
 /** ANY VALID JCL STATEMENTS MAY BE ENCLOSED ON QUOTES .        **/
 /** JOL AND JCL JOBS MAY BE SUBMITTED TOGETHER                  **/
 /**       EG.  -SUBMIT JB1 '//MYJOB1 JOB MSGCLASS=A ETC'        **/
 /**            WILL SUBMIT A JOL MEMBER CALLED JB1 AND A JCL    **/
 /**            JOB CALLED MYJOB1.                               **/
 /*****************************************************************/
SUBMIT:MACRO NOJOIN  %PI,%PE,%PM,%PJ,%SYMS=,%PROG=,%USING=XX,
  %NOCAT,%NOCATGDG,%ULIB=,%UMAC=,
  AFTER,%LET,%PO,
  %NPI=NPI,%NP=,
  %PROCLIB='%$JOLINCL',
  %MACROLIB='%$JOLPREF.JOL%$JOLVERS.CMDLIB');
 /*****************************************************************/
 /******************SYMBOLIC DECLARES *****************************/
 /*****************************************************************/
 DCL DUMMY DS DUMMY NODCB;
 DCL %TYPE INIT '';
 DCL %JCL1,%JCL2,%JCL3,%JCL4,%JCL5,%JCL6,%JCL7,%JCL8,%JCL9,%JCL10,
     %JCL11,%JCL12,%JCL13,%JCL14,%JCL15,%JCL16,%JCL17,%JCL18,
     %JCL19,%JCL20,%JCL21,%JCL22,%JCL23,%JCL24,%JCL25,%JCL26,
     %JCL27,%JCL28,%JCL29,%JCL30    INIT  '';
 DCL %JOL1,%JOL2,%JOL3,%JOL4,%JOL5,%JOL6,
     %JOL7,%JOL8,%JOL9,%JOL10 INIT '' ;
 DCL %SYM1,%SYM2,%SYM3,%SYM4,%SYM5,%SYM6,
     %SYM7,%SYM8,%SYM9,%SYM10 INIT '' ;

 /* ADDED FOR NETWORKING AND SCHEDULING C.CLARKE MARCH, 1987 */
 DCL %SYSNETID INIT('') EXT;  /* SET IF NETWORK EXECUTING */
 DCL %SYSJOBS1 INIT('') EXT;  /* KEEPS LIST OF NETWORK JOBS SUBMITTED */
 DCL %SYSJOBS2 INIT('') EXT;  /* KEEPS LIST OF NETWORK JOBS SUBMITTED */
 DCL %SYSNTINC INIT('') EXT;  /* KEEPS INCLUDE DATA SET FOR JOBS      */
 DCL %SYSNTMAC INIT('') EXT;  /* KEEPS JOL MACRO DATA SETS FOR JOBS   */
 DCL %CHK1,%CHK2,%CHK3,%CHK4,%CHK5,%CHK6,
     %CHK7,%CHK8,%CHK9,%CHK10 INIT '' ;

 DCL %CHECK,%CHKLEN,%CHLES1        INIT ''      ;
 DCL %JJOB INIT 'NO';
 DCL %JCJOB INIT 'NO';

   /* VARIABLES BELOW USED IN LOOPS FOR SUBSCRIPTS ETC */
 DCL %INDEX                        INIT('1');
 DCL %IND2                         INIT('1');
 DCL %WORK                         INIT('');
 DCL %L,%K                         INIT('');
 /*****************************************************************/
 /*****CHECK THAT THERE IS VALID INPUT TO THIS MACRO***************/
 /*****************************************************************/
    IF %LIST(1) = '' & %USING = 'XX' THEN STOP 'NO COMPILE INPUT' ;

    IF %PROG='' THEN %PROG='$JOLCP%$JOLVERS';

 /*****************************************************************/
 /*****CHECK IF WE ARE NETWORKING, AND AFTER SPECIFIED ************/
 /*****************************************************************/
    IF %AFTER¬=0 & %SYSNETID=''
    THEN DO;
         STOP '''AFTER'' CANCELLED: USE THE NETWORK COMMAND FIRST';
         EXIT 'SUBMIT ABANDONED';
    END;

 /*****************************************************************/
 /*****CHECK FOR SYMBOLICS TO BE PASSED TO JOL JOBS****************/
 /*****************************************************************/
    IF %SYMS ¬= ''
    THEN DO;
          %CHECK = SUBSTR(SYMS,1,1)         ; /* CHECK FOR    */
          IF %CHECK = '(' THEN                /* BRACKETS     */
          %SYMS = SUBSTR(SYMS,2)            ; /* AND REMOVE   */
          %CHKLEN = LENGTH (SYMS);            /* SAME         */
          %CHECK = SUBSTR(SYMS,%CHKLEN,1)   ;
          IF %CHECK = ')'
          THEN  DO                          ;
              %CHLES1 = %CHKLEN-1           ;
              %SYMS = SUBSTR(SYMS,1,%CHLES1);
          END;
    END;
 /*****************************************************************/
 /*****HANDLE AFTER CLAUSE, IF ANY ********************************/
 /*****************************************************************/

   DCL %SUB     INIT '0';
   DCL %X       INIT '    IF ';
   DCL %BRACKET INIT '0';
/* NOTE THAT JOL VERY NICELY JOINS UP ANY PARMS IN () FOR US, WHICH
   IS EXACTLY WHAT WE DON'T WANT HERE.

   THEREFORE , WE MUST EXPAND THEM IF THEY ARE FOUND.
*/
   IF %AFTER ¬=0 THEN DO;
   %SUB=%AFTER;
 LABEL TAFT;
       %SUB=%SUB+1;
 /* TEST IF JOL COMBINED PARAMETERS FOR US. */
       %Y='%LIST(%SUB)';
       %Z=TYPE(Y);
       IF %Y='(' THEN %BRACKET=%BRACKET+1;
       IF %Y=')' THEN %BRACKET=%BRACKET-1;
       IF %Y = '|' | %Y = '&'
       |  %Y = '('
       THEN DO;
           IF %BRACKET=0            /* A PARENTHESIZED EXPR ? */
           THEN DO;                 /* NO ... */
               WRITE FILE($$JCODE) FROM('%X');
               %X='    %Y  ';
           END;
           ELSE %X='%X %Y ';
       END;
       ELSE DO;
           IF %Y='ENDED' THEN %X='%X ''ENDED''';
           IF %Y=')' THEN %X='%X )';
           %WORK='%Y';                 /* SAVE NAME FOR CHECK LATER */
/*     %Y='%Y        '; %Y=SUBSTR(Y,1,8);   /* MAKE 8 CHARS LONG */
            IF %Y¬='ENDED' & %Y¬=')'   /* IS IT A JOB NAME ? */
            THEN DO;          ;        /* YES */
                %L=LENGTH(X);
                IF %L>60 THEN DO;
                    WRITE FILE($$JCODE) FROM('%X ');
                    %X='        ';
                END;
                %X='%X %%%Y=';        /* YES */
                %L=INDEX(SYSJOBS1,'%WORK');  /* SEE IF JOB SUBMITTED */
                %K=INDEX(SYSJOBS2,'%WORK');  /* SEE IF JOB SUBMITTED */
                IF %L=0 & %K=0
                THEN DO;
                STOP 'JOB %WORK NOT YET SUBMITTED:- NETWORK WILL FAIL';
                END;
            END;
       END;
       IF %SUB<%SYSNLIST THEN REDO TAFT;
    IF %X¬='' THEN  WRITE FILE($$JCODE) FROM('%X ');
    WRITE FILE($$JCODE) FROM('    THEN DO;');
    IF %BRACKET¬=0 THEN
    WRITE 'UNMATCHED PARENTHESES IF AFTER CLAUSE OF SUBMIT';
 END;   /* OF AFTER PREPROCESSING */

 /*****************************************************************/
 /**************TEST FOR TYPE OF SUBMISSION  JOL OR JCL ***********/
 /*****************************************************************/
 LABEL TTYPE;
       %IND2=%INDEX;
       %WORK=LENGTH(INDEX);
       IF %WORK=1 THEN %INDEX='000%INDEX';
       IF %WORK=2 THEN %INDEX='00%INDEX';
       IF %WORK=3 THEN %INDEX='0%INDEX';
       IF %LIST(%INDEX)
       THEN DO;
            %TYPE=TYPE(LIST%INDEX);
            %JCL$='JCL%IND2';
            %JOL$='JOL%IND2';
            %SYM$='SYM%IND2';
            %CHK$='CHK%IND2';
 /*****************************************************************/
 /*****IF JCL IS REQUIRED SET JCL SYMBOLICS AND JOB TEST **********/
 /*****************************************************************/
           IF %TYPE='LIT'
           THEN DO;
                IF %SYSNETID¬=''       /* ARE WE NETWORKING ? */
                THEN DO;
                     IF %JCJOB='NO'    /* ANY JCL SUBMITTED YET ? */
                     THEN DO;
                          IF %SYSLABEL='' THEN
                          DO;
                              STOP 'FOR JCL NETWORKING, YOU MUST CODE A
 LABEL ON THE SUBMIT STATEMENT';
                              STOP 'SUBMIT FAILED';
                              EXIT;
                          END;
                         %WORK='%SYSLABEL:         ';
                         %WORK=SUBSTR(WORK,1,9); /* MAKE 9 CHARS LONG */
                         WRITE FILE($$JCODE) FROM('%WORK NETSUB');
                         %WORK='%SYSLABEL        ';
                         %WORK=SUBSTR(WORK,1,8); /* MAKE 8 CHARS LONG */
                         WRITE FILE($$JSTAT) FROM('%%%WORK='''';');
                         %WORK='%SYSLABEL';
                     END;
                     ELSE %WORK='';
                     WRITE FILE($$JCODE) FROM('''%LIST(%IND2)''');
                END;
                %JCJOB='YES';
                SET %JCL$='%LIST(%IND2)';
           END;
 /*****************************************************************/
 /*****IF JOL SUBMISSION SUBSTITUTE SYMBOLICS FOR CARD FILE********/
 /*****************************************************************/
           IF %TYPE='CHAR'         /* JOL JOB HERE */
           THEN DO;
                %USING=' JOLIN ';
                %JJOB='YES';
                IF %SYSNETID=''    /* ORDINARY SUBMIT HERE */
                THEN DO;
                     SET %SYM$='*  JOL/%SYMS;';
                     SET %JOL$='   INCLUDE %LIST(%IND2);';
                END;
                IF %SYSNETID¬=''   /* WE ARE IN A NETWORK HERE */
                THEN DO;
                     IF %JCJOB='YES'
                     THEN DO;
                         STOP 'WHEN NETWORKING, YOU CANNOT SUBMIT A JCL
JOB AND A JOL JOB IN THE SAME SUBMIT COMMAND';
                         STOP 'USE SEPERATE SUBMIT.  SUBMIT ENDED';
                         EXIT;
                     END;
                     %WORK='         NETSUB %LIST(%IND2)';
                     WRITE FILE($$JCODE) FROM(WORK);
                     IF %SYMS¬='' THEN
                     WRITE FILE($$JCODE) FROM(
          '           SYMS(''%SYMS'')');
                     WRITE FILE($$JCODE) FROM(
          '           %NPI %PO %LET %PE %PM %PJ %NP %NOCAT %NOCATGDG;');
                     %WORK='%LIST(%IND2)        ';
                     %WORK=SUBSTR(WORK,1,8);   /* MAKE 8 CHARS LONG */
                     WRITE FILE($$JSTAT) FROM('%%%WORK='''';');
                     %WORK='%LIST(%IND2)';
                END;
           END;
       END;
       %L=LENGTH(SYSJOBS1);  /* SAVE SUBMITTED JOBS */
       IF %WORK¬=''          /* 2ND LINE OF JCL JOB ? */
       THEN DO;              /* NO */
           IF %L<240 THEN %SYSJOBS1='%SYSJOBS1,%WORK';
           ELSE %SYSJOBS2='%SYSJOBS2,%WORK';
       END;
       %INDEX=%INDEX+1;
       IF %AFTER=0 THEN IF %LIST(%INDEX) THEN REDO TTYPE;
       IF %AFTER¬=0 THEN IF %INDEX<%AFTER THEN REDO TTYPE;
       IF %AFTER¬=0
       THEN DO;
           IF %JCJOB='YES'
           THEN WRITE FILE($$JCODE) FROM('    ;');
           WRITE FILE($$JCODE) FROM('    END;');
       END;
 /*****************************************************************/
 /*****IF NETWORKING, THEN WRITE OUT OPTIONS, AND END *************/
 /*****************************************************************/
       IF %SYSNETID¬=''
       THEN DO;
           IF %JCJOB='YES'
           THEN WRITE FILE($$JCODE) FROM('    ;');
           IF %UMAC¬=' ' THEN %SYSNTMAC='%UMAC';  /* SAVE FOR ENDNET */
           IF %ULIB¬=' ' THEN %SYSNTINC='%ULIB';  /* SAVE FOR ENDNET */
           EXIT;
       END;
 /*****************************************************************/
 /*****SET UP CARD FILE FOR JOL BATCH PROGRAM       ***************/
 /*****************************************************************/
    DCL JOLIN * REPLACE JOL;
%SYM1
%JOL1
%CHK1
%SYM2
%JOL2
%CHK2
%SYM3
%JOL3
%CHK3
%SYM4
%JOL4
%CHK4
%SYM5
%JOL5
%CHK5
%SYM6
%JOL6
%CHK6
%SYM7
%JOL7
%CHK7
%SYM8
%JOL8
%CHK8
%SYM9
%JOL9
%CHK9
%SYM10
%JOL10
%CHK10
EOF;
 /*****************************************************************/
 /*****SET UP CARD FILE FOR JCL BATCH PROGRAM       ***************/
 /*****************************************************************/
  DCL CARDS * REPLACE;
%JCL1
%JCL2
%JCL3
%JCL4
%JCL5
%JCL6
%JCL7
%JCL8
%JCL9
%JCL10
%JCL11
%JCL12
%JCL13
%JCL14
%JCL15
%JCL16
%JCL17
%JCL18
%JCL19
%JCL20
%JCL21
%JCL22
%JCL23
%JCL24
%JCL25
%JCL26
%JCL27
%JCL28
%JCL29
%JCL30
EOF;
 /*****************************************************************/
 /*****SET UP USERS JOL LIBRARIES *********************************/
 /*****************************************************************/
         IF %PI ¬= '' THEN %NPI = '' ;
         IF %UMAC¬=' ' THEN %UMAC='%UMAC||';
         ELSE IF %SYSUID ¬= ' ' THEN %UMAC = '%SYSUID.JOL ||';
         IF %ULIB¬=' ' THEN %ULIB='%ULIB||';
         ELSE IF %SYSUID ¬= ' ' THEN %ULIB = '%SYSUID.JOL ||';
         IF %NP='' THEN DCL $$JPRINT PRINTER VBA 150,160;
                   ELSE DCL $$JPRINT DS 'NULLFILE';

 /*****************************************************************/
 /*****SET UP JOL BATCH COMPILE DECLARATIONS AND RUN STATEMENT*****/
 /*****************************************************************/
 IF %JJOB='YES' | %USING¬='XX' THEN DO;
 DCL $$JCL       SYSOUT=A PGM=INTRDR FB 80,80 BUFNO=1;
 DCL $$JLOAD     DS %$JOLPREF.JOL%$JOLVERS.LOAD ;
 DCL $$JINCL     DS %$JOLINCL;
 DCL $$JMACRO    DS %$JOLPREF.JOL%$JOLVERS.CMDLIB ;
 DCL $$JWORK1    DS SYSDA FB 80,800 600 BLKS NORLSE ;
 DCL $$JWORK2    DS SYSDA VB 200,800 600 BLKS NORLSE ;
 DCL %PROG       PROG
      $$JPRINT   WRITES $$JPRINT
      $$JCL      WRITES $$JCL
      $$JLOAD    READS  $$JLOAD
      $$JINCL    READS  %ULIB  $$JINCL
      $$JMACRO   READS  %UMAC  $$JMACRO
      $$JWORK1   WRITES $$JWORK1
      $$JWORK2   WRITES $$JWORK2
      $$JIN      READS  %USING ;
%SYSLABEL:       RUN %PROG PARM
      '%NPI,%PO,%LET,%PE,%PM,%PJ,%NP,%NOCAT,%NOCATGDG/ ';
 END;
 /*****************************************************************/
 /*****SET UP JCL BATCH STATEMENTS TO SUBMIT JCL JOB **************/
 /*****************************************************************/
 IF %JCJOB='YES'
  THEN DO;
  DCL DUMMYIN DS DUMMY;
  DCL PRINTFIL  PRINTER NODCB;
  DCL OUTJCL SYSOUT A PGM=INTRDR;
   IEBGENER: DCL IEBGENER PROG
             SYSUT1 READS CARDS
             SYSUT2 WRITES OUTJCL
             SYSIN  READS DUMMYIN
             SYSPRINT WRITES PRINTFIL;
     RUN IEBGENER ;
 END ;
 END ; /* END OF MACRO */
