getfield:macro;

dcl %change init('');
dcl %fldind init '';         /* Need to decalre for MVS, VSE etc */

panel option caps;
panel option showfield;

Input:

%fldnum=1;                   /* Start at Field 1 */

if %system = 'JOLSHELL'
then  /* */
Panel  box
/( ctr hi 'Jol SORT/MERGE Fields Input Panel')
/('        This PANEL allows you to enter SORT or MERGE Parameters,')
 ('        one Parameter at a time.  Up to 16 Fields may be entered')
 ('        on separate screens.')
/('        Press F3 after you have entered all the required parameters.')
/('Note:   In the FIELD Indicator position, you can enter an')
 ('        ''F'', which means a Field instead of a starting column.')
 ('                    Field      Field     Field    Type     Sort')
 ('                   Indicator   Position  Length           Direction')
 //
 ('Enter Details  ===>   ',fldind,1,'')
 (' ',fldstart,4,'',16,32,16,33)
 (' ',fldlen,4,'',16,43,16,44)
 (' ',fldtyp,4,'',16,50,16,52)
 (' ',flddir,1,'',16,60,16,62)

/
 ('Field Parameters Built Now Are:-')
 ('  ',retfield,65,'%retfield')
/
helpline ('Press F3 To Return To Previous Menu');

else           /* OS (MVS, FSP etc */
Panel  box
/( ctr hi 'Jol SORT/MERGE Fields Input Panel')
/('        This PANEL allows you to enter SORT or MERGE Parameters,')
 ('        one Parameter at a time.  Up to 16 Fields may be entered')
 ('        on separate screens.')
/('        Press F3 after you have entered all the required parameters.')
///
 ('                               Field     Field    Type     Sort')
 ('                               Position  Length           Direction')
 //
 ('Enter Details  ===>    ')
 (' ',fldstart,4,'',16,32,16,33)
 (' ',fldlen,4,'',16,43,16,44)
 (' ',fldtyp,4,'',16,50,16,52)
 (' ',flddir,1,'',16,60,16,62)

/
 ('Field Parameters Built Now Are:-')
 ('  ',retfield,65,'%retfield')
/
helpline ('Press F3 To Return To Previous Menu');

valid1:

    if %fldind='' & %fldstart='' & %fldlen='' & %fldtyp=''
       & %flddir=''
    then do;
        if %syspfk=3 then goto return;
        panel reread from fldind
                  'Press F3 to Exit, or Enter Data');
        goto valid1;
    end;

    if %fldind¬=''      /* Check if Field Indicator valid        */
    &  %fldind¬='F'
    then do;
        panel reread from fldind
                  'Filetype must be F or Blank');
        goto valid1;
    end;

    %work=type(fldstart); /* Check if Field Start is Valid      */
    if %work¬='NUM'
    |  %fldstart=''
    then do;
          panel reread from fldstart 'Field Starting Posn Not Numeric';
          goto valid1;
    end;
    if %work='NUM'  & %fldstart>10000
    then do;
          panel reread from fldstart 'Field Size is > 10000';
          goto valid1;
    end;

    %work=type(fldlen); /* Check if Field Length Valid      */
    if %work¬='NUM'
    &  %fldlen¬=''
    then do;
          panel reread from fldlen 'Field Length Not Numeric';
          goto valid1;
    end;
    if %work='NUM'  & %fldlen>255
    then do;
          panel reread from fldlen 'Field Length is > 255';
          goto valid1;
    end;

    if %fldtyp=''      /* Check if Field Type valid        */
    then do;
         %fldtyp='CH';  /* Set Default                     */
         panel reread from fldtyp
                'Default Field Type CH Assigned');
         goto valid1;
    end;

    if %fldtyp¬='CH '   /* Check if Field Type valid        */
    &  %fldtyp¬='ZD '
    &  %fldtyp¬='NU M'
    &  %fldtyp¬='FL '
    &  %fldtyp¬='CA PS'
    then do;
         panel reread from fldtyp
                'Type Must be CH, ZD, NUM, FL or CAPS';
         goto valid1;
    end;

    if %flddir=''      /* Check if Field Direction valid   */
    then do;
         %flddir='A';  /* Set Default                     */
         panel reread from flddir
                'Default Direction Ascending Assumed');
         goto valid1;
    end;

    if %flddir¬='A'    /* Check if Field Type valid        */
    &  %flddir¬='D'
    then do;
         panel reread from flddir
                'Type Must be A or D';
         goto valid1;
    end;

/* Build Return String */

    if %retfield¬=' '       /* Have we already started building ? */
    then %retfield='%retfield,';    /* Put in a comma */

    %retfield
       ='%retfield%fldind%fldstart,%fldlen,%fldtyp,%flddir';

    %fldind='';
    %fldstart='';
    %fldlen='';
    %fldtyp='';
    %flddir='';

    if %Syspfk¬=3
    then do;
         %fldnum=%fldnum+1;
         panel reread;
         goto valid1;
    end;

return:

    panel option noshowfield;
end;
