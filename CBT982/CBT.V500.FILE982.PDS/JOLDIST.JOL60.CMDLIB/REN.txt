 /*****************************************************************/
 /**               NETSUB MACRO                                  **/
 /**               ------------                                  **/
 /**                                                             **/
 /** WRITTEN: C. CLARKE    26.03.86 FOR NETWORKING               **/
 /**                                                             **/
 /** THIS MACRO CREATES JOL CODE THAT IS THEN COMPILED           **/
 /**      AND SUBMITTED FOR EXECUTION.                           **/
 /**                                                             **/
NETSUB:MACRO(%PI,%PE,%PM,%PJ,%SYMS=,%PROG=$JOLCP40,%USING=XX,
  %NOCAT,%NOCATGDG,%ULIB=,%UMAC=,%PROCLIB='CLARKE.JOL',
  AFTER,
  %LET,%PO,%MACROLIB='%$JOLPREF.JOL%$JOLVERS.CMDLIB',%NPI=NPI,%NP=) ;
 /*****************************************************************/
 /******************SYMBOLIC DECLARES *****************************/
 /*****************************************************************/
 DCL DUMMY DS DUMMY NODCB;

 /* ADDED FOR NETWORKING AND SCHEDULING C.CLARKE MARCH, 1987 */
 DCL %SYSNETID INIT('') EXT;  /* SET IF NETWORK EXECUTING */
 DCL %SYSJOBS1 INIT('') EXT;  /* KEEPS LIST OF NETWORK JOBS SUBMITTED */
 DCL %SYSJOBS2 INIT('') EXT;  /* KEEPS LIST OF NETWORK JOBS SUBMITTED */
 DCL %SYSNTINC INIT('') EXT;  /* KEEPS INCLUDE DATA SET FOR JOBS      */
 DCL %SYSNTMAC INIT('') EXT;  /* KEEPS JOL MACRO DATA SETS FOR JOBS   */
 DCL %JOBSUBD  INIT('') EXT;  /* TELLS US IF A JOB WAS SUBMITTED      */
 DCL %CHK1,%CHK2,%CHK3,%CHK4,%CHK5,%CHK6,
     %CHK7,%CHK8,%CHK9,%CHK10 INIT '' ;

 DCL %CHECK,%CHKLEN,%CHLES1        INIT ''      ;
 DCL %JJOB INIT 'NO';
 DCL %JCJOB INIT 'NO';

   /* VARIABLES BELOW USED IN LOOPS FOR SUBSCRIPTS ETC */
 DCL %INDEX                        INIT('1');
 DCL %IND2                         INIT('1');
 DCL %WORK                         INIT('');
 DCL %TYPE                         INIT('');
 DCL %L,%K                         INIT('');
 /*****************************************************************/
 /*****CHECK THAT THERE IS VALID INPUT TO THIS MACRO***************/
 /*****************************************************************/
    IF %LIST(1) = '' THEN STOP 'NO INPUT FOR NETSUB';

 /*****************************************************************/
 /*****CHECK IF WE ARE NETWORKING, AND AFTER SPECIFIED ************/
 /*****************************************************************/
    IF %AFTER¬=0 & %SYSNETID=''
    THEN DO;
         STOP '''AFTER'' CANCELLED: USE THE NETWORK COMMAND FIRST';
    END;

 /*****************************************************************/
 /*****CHECK FOR SYMBOLICS TO BE PASSED TO JOL JOBS****************/
 /*****************************************************************/
    %CHKLEN = LENGTH (SYMS);                  /* GET LENGTH   */
    IF %CHKLEN ¬= 0
    THEN DO;
          %CHECK = SUBSTR(SYMS,1,1)         ; /* CHECK FOR    */
          IF %CHECK = '(' THEN                /* BRACKETS     */
          DO;
             %CHKLEN = LENGTH (SYMS);            /* AND REMOVE   */
             %CHLES1 = %CHKLEN-2           ;
             %SYMS = SUBSTR(SYMS,2,%CHLES1)   ;
          END;
    END;
 /*****************************************************************/
 /*****CHECK IF THE JOB HAS ALREADY BEEN SUBMITTED ****************/
 /*****************************************************************/

   %TYPE=TYPE(LIST0001);       /* CHECK IF JOL OR JCL JOB */
   IF %TYPE='CHAR' THEN        /* THIS IS A JOL JOB */
   %WORK='SET WORK=%%%LIST(1)'; /* GET NAME OF JOB */
   ELSE %WORK='SET WORK=%%%SYSLABEL'; /* GET NAME OF JCL JOB */
   %WORK;                      /* SET WORK = VALUE OF JOBNAME */

   IF %WORK=''                 /* HAVE WE ALREADY SUBMITTED THIS JOB */
   &  %TYPE='CHAR'             /* HERE IS A JOL JOB */
   THEN DO;                    /* NO, SO WE SHALL COMPILE AND SUBMIT */
        WRITE FILE(TEMPNET) FROM('*JOL/%SYMS;');
        WRITE FILE(TEMPNET) FROM('   INCLUDE %LIST(1);');
        WRITE FILE(TEMPNET) FROM('   CHECKNET %SYSNETID %LIST(1);');
        %WORK='%LIST(1)';      /* GET NAME OF JOB */
        SET %WORK='SUBMITTED';
        %JOBSUBD='YES';          /* TELLS US A JOB WAS SUBMITTED */
    /*  EXIT;     /* DONE ALL WE NEED FOR A JOL JOB */
   END; /* OF JOL JOB */

 /*****************************************************************/
 /************* HERE WE HAVE A JCL JOB  ***************************/
 /*****************************************************************/

   IF %WORK=''                 /* HAVE WE ALREADY SUBMITTED THIS JOB */
   THEN DO;
       %INDEX=1;
       IF %SYSLABEL=''
       THEN DO;
           STOP 'NO LABEL ON NETSUB COMMAND ... NETWORK HALTED';
           EXIT;
       END;
 LABEL OPLOOP;
       %IND2=%INDEX;
       %WORK=LENGTH(INDEX);
       IF %WORK=1 THEN %INDEX='000%INDEX';
       IF %WORK=2 THEN %INDEX='00%INDEX';
       IF %WORK=3 THEN %INDEX='0%INDEX';
       IF %LIST(%INDEX)
       THEN DO;
            WRITE FILE($$JCL) FROM('%LIST(%IND2)');
            %INDEX=%INDEX+1;
            REDO OPLOOP;
       END;
       SET %SYSLABEL='SUBMITTED';
   /*  PRINTSYM; */
       WRITE FILE($$JCL) FROM('//CHECKNET EXEC PGM=$JOLCP%$JOLVERS,');
       WRITE FILE($$JCL) FROM('//  PARM=''/NETJOB=%SYSLABEL''');
       WRITE FILE($$JCL)
         FROM('//$$JPRINT DD  SYSOUT=*,DCB=BLKSIZE=1330');
       WRITE FILE($$JCL) FROM('//$$JCL    DD  SYSOUT=(A,INTRDR)');
       WRITE FILE($$JCL) FROM('//SYSUDUMP DD  SYSOUT=*');
       WRITE FILE($$JCL) FROM (
         '//$$JLOAD  DD  DSN=%$JOLPREF.JOL%$JOLVERS.LOAD,DISP=SHR');
       WRITE FILE($$JCL) FROM (
         '//$$JINCL  DD  DSN=%$JOLPREF.JOLNET.STATUS,DISP=SHR');
       WRITE FILE($$JCL) FROM (
         '//$$JMACRO DD  DSN=%$JOLPREF.JOL%$JOLVERS.CMDLIB,DISP=SHR');
       WRITE FILE($$JCL) FROM (
         '//$$JWORK1 DD  UNIT=SYSDA,SPACE=(TRK,10),');
       WRITE FILE($$JCL) FROM (
         '//         DCB=(RECFM=FB,LRECL=80,BLKSIZE=800)');
       WRITE FILE($$JCL) FROM (
         '//$$JWORK2 DD  UNIT=SYSDA,SPACE=(TRK,10),');
       WRITE FILE($$JCL) FROM (
         '//         DCB=(RECFM=VB,LRECL=200,BLKSIZE=800)');
       WRITE FILE($$JCL) FROM (
         '//TEMPNET  DD  UNIT=SYSDA,SPACE=(TRK,10),');
       WRITE FILE($$JCL) FROM (
         '//         DCB=(RECFM=FB,LRECL=80,BLKSIZE=800)');
       WRITE FILE($$JCL) FROM (
         '//$$JIN    DD  DSN=%$JOLPREF.JOLNET.CODE(%SYSNETID),');
       WRITE FILE($$JCL) FROM (
         '//         DISP=SHR');
       WRITE FILE($$JCL) FROM (
         '//         DD  DSN=*.TEMPNET,VOL=REF=*.TEMPNET,DISP=SHR');
   END; /* OF JCL JOB */
 END ; /* END OF MACRO */
