ASM:MACRO (
/*********************************************************************/
/*                JOL ASM COMMAND                                    */
/*          AUSTRALIAN BUREAU OF STATISTICS                          */
/*                   16/04/81                                        */
/*********************************************************************/
  %NOPANEL                   /* DEFAULT FOR BACKGROUND COMPILE       */
  %PANEL                     /* DEFAULT FOR TSO MODE COMPILE         */
  %LINK                      /* FOR CHAINING TO JOL LINK COMMAND     */
/*********************************************************************/
  %PRINT=,%SYSOUT=,          /* DEFAULT SYSOUT TO MSGCLASS           */
  %OBJ='&&OBJ',%NOOBJ        /* DEFINE AN OUTPUT OBJECT DATA SET     */
  %MACLIB %NOMACLIB          /* MACLIB IS THE DEFAULT                */
  %LIB=,                     /* DEFINE AN INPUT LIBRARY IF REQ'D     */
  %ALIGN %NOALIGN            /* ALIGN IS THE DEFAULT                 */
  %ALOGIC %NOALOGIC          /* NOALOGIC IS THE DEFAULT              */
  %BATCH %NOBATCH            /* NOBATCH IS THE DEFAULT               */
  %BUFSIZE=,                 /* STD IS THE DEFAULT                   */
  %DECK %NODECK              /* NODECK IS THE DEFAULT                */
  %ESD %NOESD                /* ESD IS THE DEFAULT                   */
  %FLAG=,                    /* ALL ERRORS IS THE DEFAULT            */
  %LIBMAC %NOLIBMAC          /* NOLIBMAC IS THE DEFAULT              */
  %LINECNT=,                 /* 55 IS THE DEFAULT                    */
  %LIST %NOLIST              /* LIST  IS THE DEFAULT                 */
  %MCALL %NOMCALL            /* NOMCALL IS THE DEFAULT               */
  %MLOGIC %NOMLOGIC          /* NOMLOGIC IS THE DEFAULT              */
  %NAME %NONAME              /* NONAME IS THE DEFAULT                */
  %NUM %NONUM                /* NUM IS THE DEFAULT                   */
  %RENT %NORENT              /* NORENT IS THE DEFAULT                */
  %RLD %NORLD                /* RLD    IS THE DEFAULT                */
  %STMT %NOSTMT              /* STMT   IS THE DEFAULT                */
  %SYSPARM=,                 /* NOSYSPARM IS THE DEFAULT             */
  %TERM %NOTERM              /* NOTERM IS THE DEFAULT                */
  %TEST %NOTEST              /* NOTEST IS THE DEAFULT                */
  %XREF %SHORT %FULL         /* SHORT IS THE DEFAULT FOR XREF        */
  %NOXREF                    /* NOXREF IS DEFAULT IF UNSPECIFIED     */
  );
/*********************************************************************/
/*                  SET UP ASSEMBLER OPTIONS                         */
/*********************************************************************/
   DCL %PARM INIT '';
   IF %NOOBJ ¬= ''    THEN %PARM = 'NOOBJ' ;
                      ELSE %PARM = 'OBJ' ;
   IF %XREF      = '' &
      %SHORT     = '' &
      %FULL      = '' THEN %PARM = '%PARM,NOXREF' ;
   ELSE
   DO;
      IF %FULL   = '' THEN %PARM = '%PARM,XREF(SHORT)' ;
                      ELSE %PARM = '%PARM,XREF(FULL)' ;
   END;
   IF %ALIGN    ¬= '' THEN %PARM = '%PARM,ALIGN';
   IF %NOALIGN  ¬= '' THEN %PARM = '%PARM,NOALIGN';
   IF %BATCH    ¬= '' THEN %PARM = '%PARM,BATCH';
   IF %NOBATCH  ¬= '' THEN %PARM = '%PARM,NOBATCH';
   IF %BUFSIZE  ¬= '' THEN %PARM = '%PARM,BUFSIZE(%BUF)';
   IF %ALOGIC   ¬= '' THEN %PARM = '%PARM,ALOGIC';
   IF %NOALOGIC ¬= '' THEN %PARM = '%PARM,NOALOGIC';
   IF %ESD      ¬= '' THEN %PARM = '%PARM,ESD';
   IF %NOESD    ¬= '' THEN %PARM = '%PARM,NOESD';
   IF %DECK     ¬= '' THEN %PARM = '%PARM,DECK';
   IF %NODECK   ¬= '' THEN %PARM = '%PARM,NODECK';
   IF %FLAG     ¬= '' THEN %PARM = '%PARM,FLAG(%FLAG)';
   IF %LIBMAC   ¬= '' THEN %PARM = '%PARM,LIBMAC';
   IF %NOLIBMAC ¬= '' THEN %PARM = '%PARM,NOLIBMAC';
   IF %LINECNT  ¬= '' THEN %PARM = '%PARM,LINECNT(%LINECNT)';
   IF %LIST     ¬= '' THEN %PARM = '%PARM,LIST';
   IF %NOLIST   ¬= '' THEN %PARM = '%PARM,NOLIST';
   IF %MCALL    ¬= '' THEN %PARM = '%PARM,MCALL';
   IF %NOMCALL  ¬= '' THEN %PARM = '%PARM,NOMCALL';
   IF %MLOGIC   ¬= '' THEN %PARM = '%PARM,MLOGIC';
   IF %NOMLOGIC ¬= '' THEN %PARM = '%PARM,NOMLOGIC';
   IF %NAME     ¬= '' THEN %PARM = '%PARM,NAME';
   IF %NONAME   ¬= '' THEN %PARM = '%PARM,NANAME';
   IF %NUM      ¬= '' THEN %PARM = '%PARM,NUM';
   IF %NONUM    ¬= '' THEN %PARM = '%PARM,NONUM';
   IF %RENT     ¬= '' THEN %PARM = '%PARM,RENT';
   IF %NORENT   ¬= '' THEN %PARM = '%PARM,NORENT';
   IF %RLD      ¬= '' THEN %PARM = '%PARM,RLD';
   IF %NORLD    ¬= '' THEN %PARM = '%PARM,NORLD';
   IF %STMT     ¬= '' THEN %PARM = '%PARM,STMT';
   IF %NOSTMT   ¬= '' THEN %PARM = '%PARM,NOSTMT';
   IF %SYSPARM  ¬= '' THEN %PARM = '%PARM,SYSPARM(%SYSPARM)';
   IF %TERM     ¬= '' THEN %PARM = '%PARM,TERM';
   IF %NOTERM   ¬= '' THEN %PARM = '%PARM,NOTERM';
   IF %TEST     ¬= '' THEN %PARM = '%PARM,TEST';
   IF %NOTEST   ¬= '' THEN %PARM = '%PARM,NOTEST';
/*                                                                   */
/*********************************************************************/
/*          INITIALISE SOME INTERNAL SYMBOLS FOR THIS MACRO          */
/*          SET NOPANEL AS DEFAULT WHEN NOT IN TSO MODE              */
/*          SET OPTION TO CHAIN TO LINK COMMAND IF SPECIFIED         */
/*********************************************************************/
DCL %ANS     INIT '' ;
DCL %$TYPE   INIT '' ;
DCL %PRTCAT  INIT '' ;
DCL %OBJCAT  INIT '' ;
IF %TSOCLASS = '' THEN %NOPANEL = 'NOPANEL' ;
IF %LINK    ¬= '' THEN %LINK    = 'Y' ;
/*                                                                   */
/*********************************************************************/
/*          SET OPTION FOR PRINT SYSOUT OR NOPRINT                   */
/*          DEFAULT IS TO MSGCLASS                                   */
/*********************************************************************/
IF %PRINT    ¬= '' & %SYSOUT¬='' THEN %PRINT = '' ;
IF %TSOCLASS ¬= '' THEN DCL %PFILE INIT 'SYSOUT %TSOCLASS';
                   ELSE DCL %PFILE INIT 'SYSOUT A';
IF %PRINT    ¬= '' THEN %PFILE = '%PRINT';
IF %SYSOUT   ¬= '' THEN %PFILE = 'SYSOUT %SYSOUT';
IF %NOPRINT  ¬= '' THEN %PFILE = 'NO PRINT REQUIRED';
/*          SET DSN OR DSID FOR COMPILER INPUT                      */
/*                DEFAULT IS SYSPREF.ASM                            */
/********************************************************************/
IF %LIST(1)='' THEN DO;
DCL %DSIN INIT '%SYSPREF.ASM';
END;
DCL %MEM INIT  '';
IF %LIST(1) ¬= '' THEN
   DO;
      %DSIN = '%LIST(1)';
      DCL %###X INIT '';
      DCL %###L INIT '';
      %###X = INDEX(DSIN,'(');
      IF %###X ¬= 0 THEN
         DO;
            %MEM  = SUBSTR(DSIN,%###X);
            %MEM  = SUBSTR(MEM,2);
            %###L = LENGTH(MEM);
            %###L = %###L - 1;
            %MEM  = SUBSTR(MEM,1,%###L);
            %###X = %###X - 1;
            %DSIN = SUBSTR(DSIN,1,%###X);
            IF %DSIN = '' THEN %DSIN = '%SYSPREF.ASM';
            %###X = '';
         END;
   END;
/********************************************************************/
/*              SELECT SYSTEM MACRO LIBRARY IF REQUIRED             */
/********************************************************************/
IF %NOMACLIB ¬= '' THEN %MACLIB = '' ;
ELSE %MACLIB = 'SYS1.MACLIB' ;
/**********************************************************************/
/*              JOL ASSEMBLER COMMAND PANEL                         */
/********************************************************************/
IF %NOPANEL = '' THEN
   PANEL
   ('             *****  JOL ASM COMMAND  *****               ')
   ('          ANY HIGHLIGHTED FIELD CAN BE CHANGED           ')
   ('COMPILER OPTIONS CAN BE CHANGED OR ADDED TO - COMMAS REQD')
   ('        ALL DATA SET NAMES MUST BE FULLY QUALIFIED       ') //
   ('        LINK ***** Y/N ****',LINK,1,'%LINK')                //
   ('SOURCE DATA SET         =====>',DSIN,    44, '%DSIN'  )
   ('SOURCE DATA SET MEMBER  =====>',MEM,     44, '%MEM'   )
   ('OBJECT DATA SET         =====>',OBJ,     44, '%OBJ'   )
   ('MACRO LIBRARY           =====>',MACLIB,  44, '%MACLIB')
   ('USER MACRO LIBRARY      =====>',LIB,    100, '%LIB'   )
   ('PRINT OPTION            =====>',PFILE,   44, '%PFILE' )
   ('COMPILER OPTIONS        =====>',PARM,   100, '%PARM'  )
   ('*************************************************************');
/*                                                                  */
/********************************************************************/
/*            SET INPUT TO THE COMPILER                             */
/********************************************************************/
DCL %LOADMEM INIT '' ;
IF %MEM ¬= '' THEN
   DO;
      %DSIN = '%DSIN(%MEM)';
      %LOADMEM = 'NAME %MEM' ;
   END;
DCL INP DS DSN %DSIN NODEF;
/********************************************************************/
/*            SET UP OBJECT CODE DATASET                             */
/*            DEFAULT IS AN EXISTING DATASET                         */
/*********************************************************************/
DCL %OBJFILE INIT '' ;
DCL %OBJDISP INIT '' ;
IF %OBJ = '&&OBJ' THEN                 /* DEFAULT TO TEMP OBJECT     */
DO;                                    /* WITH DSID OF OBJ           */
   %OBJ     = 'OBJ' ;
   %OBJFILE = 'OBJ' ;
   DCL OBJ DS DSN &&OBJ BLKSIZE 800 5 CYLS NORLSE SYSDA EXT;
   %OBJDISP = 'MODS';
END;
ELSE                                   /* OBJECT DATASET DSID OR DSN */
DO;
   %$TYPE = INDEX(OBJ,'.');
   IF %$TYPE = 0 THEN                  /* DSID FOR OBJECT DATASET    */
   DO;                                 /* ASSUME NEW AND CATALOG IT  */
      %OBJFILE = 'OBJ2' ;
      DCL OBJ2 DS LIKE %OBJ ;
      %OBJDISP = 'WRITES' ;
      %OBJCAT  = 'CATALOGUE OBJ2' ;
   END;
   IF %$TYPE ¬= 0 & %NOPANEL ¬= '' THEN /* DSN FOUND AND PANEL OFF   */
   DO;                                  /* SO ASSUME OLD DSN USED    */
      %OBJFILE = 'OBJ2' ;
      DCL OBJ2 DS DSN %OBJ NODEF ;
      %OBJDISP = 'UPDATES' ;
   END;
   IF %$TYPE ¬= 0 & %NOPANEL = '' THEN /* DSN FOUND AND PANEL ON     */
   DO;                                 /* SO WE CAN GET MORE INFO    */
      PANEL
      ('         ***** JOL  ASM  COMMAND *****                ') ///
      ('YOU HAVE SPECIFIED AN OBJECT DATASET FOR THIS ASSEMBLY') ///
      ('IS THIS DATA SET CATALOGUED ********* Y/N',ANS,1)        ///
      ('**********************************************************');
      %OBJFILE = 'OBJ2';
      IF %ANS = 'Y' THEN               /* OBJECT DSN IS CATALOGUED   */
      DO;                              /* SO DECLARE IT AS OLD       */
         DCL OBJ2 DS DSN %OBJ NODEF;
         %OBJDISP = 'UPDATES';
      END;
      ELSE                             /* NEW DSN FOUND SO TELL      */
      DO;                              /* USER AND CATALOGUE IT      */
         PANEL
         ('      *****  JOL  ASM  COMMAND *****             ') ///
         ('   ANY HIGHLIGHTED FIELD CAN BE CHANGED          ') ///
         ('YOUR OBJECT DATA SET DEFINITION IS AS FOLLOWS    ') ///
         ('',DCLDEF,99,
          'DCL OBJ2 DS DSN %OBJ SYSDA 5,3 TRKS FB 80,3040 EXT') ///
         ('********************************************************');
         %OBJDISP = 'WRITES' ;
         %OBJCAT  = 'CATALOGUE OBJ2' ;
         %DCLDEF;
      END;
      %ANS = '';
   END;
   %$TYPE = '' ;
END;
/********************************************************************/
/*               SET UP PRINTED OUTPUT OR PRINT DATASET             */
/********************************************************************/
DCL %OUTDISP INIT '' ;
%$TYPE = SUBSTR(PFILE,1,6);
IF %$TYPE = 'SYSOUT' THEN              /* SYSOUT CLASS DEFINED      */
   DO;
      DCL PRT %PFILE;
      %SYSOUT = '%PFILE';
      %$TYPE = 1;
      %OUTDISP = 'WRITES';
   END;
IF %$TYPE = 'NO PRI' THEN               /* NOPRINT OPTION CODED      */
   DO;
      DCL PRT DS DUMMY NODEF;
      %$TYPE = 1;
      %OUTDISP = 'WRITES';
   END;
IF %$TYPE ¬= 1 THEN                     /* PRINT DSN OR DSID CODED   */
DO;
   %$TYPE = INDEX(PFILE,'.');
   IF %$TYPE = 0 THEN                   /* DSID FOR PRINT DATASET    */
   DO;                                  /* ASSUME NEW AND CATALOG IT */
      DCL PRT DS LIKE %PFILE ;
      %OUTDISP = 'WRITES' ;
      %PRTCAT  = 'CATALOGUE PRT' ;
   END;
   IF %$TYPE ¬= 0 & %NOPANEL ¬= '' THEN /* DSN FOUND AND PANEL OFF   */
   DO;                                  /* SO ASSUME OLD DSN USED    */
      DCL PRT DS DSN %PFILE NODEF ;
      %OUTDISP = 'UPDATES';
   END;
   IF %$TYPE ¬= 0 & %NOPANEL = ''  THEN /* DSN FOUND AND PANEL ON    */
   DO;                                  /* SO WE CAN GET MORE INFO   */
      PANEL
      ('       ***** JOL  ASM  COMMAND  *****'               ) ///
      ('YOU HAVE SPECIFIED A PRINT DATASET FOR THIS ASSEMBLY') ///
      ('IS THIS DATASET ALREADY CATALOGUED *** Y/N',ANS,1    ) ///
      ('*********************************************************');
      IF %ANS = 'Y' THEN               /* PRINT DSN IS CATALOGUED    */
      DO;                              /* SO DECLARE IT AS OLD       */
         DCL PRT DS DSN %PFILE NODEF;
         %OUTDISP = 'UPDATES';
      END;
      ELSE                              /* NEW DSN FOUND SO TELL     */
      DO;                               /* USER AND CATALOGUE IT     */
         PANEL
         ('        ***** JOL  ASM  COMMAND  *****           ')
         ('      ANY HIGHLIGHTED FIELD CAN BE CHANGED       ') ///
         ('YOUR OUTLIST DATA SET DEFINITION IS AS FOLLOWS   ')
         ('',DCLDEF,80,
          'DCL PRT DS DSN %PFILE SYSDA 5,3 TRKS FBA 121,6050 EXT') ///
         ('********************************************************');
         %OUTDISP = 'WRITES';
         %DCLDEF;
         %PRTCAT = 'CATALOGUE PRT';
      END;
      %ANS = '';
   END;
   %$TYPE = '' ;
END;
/*********************************************************************/
/*          SET UP SYSTEM MACLIB AND USER MACRO LIBRARY              */
/*********************************************************************/
IF %LIB ¬= '' & %MACLIB ¬= '' THEN
     %LIB = '%LIB || %MACLIB' ;
ELSE %LIB = '%LIB %MACLIB' ;
IF %LIB ¬= '' THEN %LIB = 'SYSLIB READS %LIB' ;
/*********************************************************************/
/*               WORK DATA SET DECLARATIONS                          */
/*********************************************************************/
     DCL DS001  DS  5 CYLS  SYSDA NORLSE NODEF ;
     DCL DS002  DS  5 CYLS  SYSDA NORLSE NODEF ;
     DCL DS003  DS  5 CYLS  SYSDA NORLSE NODEF ;;
     DCL DS004  DS  DUMMY NODEF ;
/*********************************************************************/
/*               ASSEMBLER PROGRAM DECLARATION                       */
/*********************************************************************/
DCL      ASMBLR   PROG
         SYSPRINT %OUTDISP PRT
         %LIB
         SYSIN    READS    %DSIN
         SYSUT1   WRITES   DS001
         SYSUT2   WRITES   DS002
         SYSUT3   WRITES   DS003
         SYSPUNCH WRITES   DS004
         SYSGO    %OBJDISP %OBJFILE ;
/*********************************************************************/
/*               RUN ASSEMBLER PROGRAM                               */
/*********************************************************************/
ASM:
RUN      ASMBLR    '%PARM';
/*********************************************************************/
/*          CATALOGUE PRINT AND OBJECT DATASETS IF NECESSARY         */
/*********************************************************************/
%PRTCAT ;
IF ASM < 8 THEN
DO;
   %OBJCAT ;
   DISPLAY 'ASM RETURNED LESS THAN 8' ;
END;
/*********************************************************************/
/*               CHAIN TO LINK MACRO IF REQUIRED                     */
/*********************************************************************/
IF %LINK = 'Y' THEN
IF ASM < 12  THEN
   LINK '%OBJ' %LOADMEM %SYSOUT ASM %NOPANEL ;
/********************************************************************/
END;     /* END OF JOL ASM MACRO                                    */
