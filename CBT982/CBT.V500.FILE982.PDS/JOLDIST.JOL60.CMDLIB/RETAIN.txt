  ON: MACRO (%SNAP,%DUMP,     /* SET IF SNAP OR DUMP CODED */
             %SYSTEM,         /* SET IF %SYSTEM SET        */
             TO,              /* LOCATES 'TO' IN LIST      */
             %HOLD,%NOHOLD ); /* SET IF 'HOLD' | 'NOHOLD CODED */
         DCL %DDNAME          INIT 'SYSUDUMP';
         DCL %HOLDX           INIT ',HOLD=YES';
         DCL %SPACE           INIT ',SPACE=(TRK,(0,100))';
         DCL %OUT             INIT 'SYSOUT=A';
         DCL %DSN,%UNIT,%STEPNO INIT '';
         IF %LIST(1)='ERROR'       /* WAS ON ERROR CODED ?  */
         THEN DO;
             IF %SNAP='' & %DUMP='' /* ON ERROR ; ?  */
             THEN JOLOPT DUMPCARD='';   /* CANCEL ON ERROR */
             ELSE DO;
                 IF %SYSTEM¬=''
                 THEN %DDNAME='SYSABEND';    /* CHANGE DEFAULT */
                 IF %NOHOLD¬='' THEN %HOLDX=''; /* ALTER HOLD IF NEC */
      /*         IF %SYSTEM¬='VS2' THEN %HOLDX=''; /* ONLY FOR MVS  */
                 IF %TO=0 | (%TO¬=0 & %LIST(%TO+1)='PRINTER')
                 THEN DO;
                    IF %TO¬=0 THEN %OUT='SYSOUT=%LIST(%TO+2)';
                    IF %SPOOL¬='' THEN %SPACE=',OUTLIM=999999';
                    JOLOPT DUMPCARD='//%DDNAME DD %OUT%SPACE%HOLDX';
                 END;
                 ELSE DO;
                     /* HERE %TO¬=0, OR  IT DOESN'T POINT TO A PRINTER
                      */
                     IF %LIST(%TO+1)¬='TAPE' & %LIST(%TO+1)¬='DISK'
                     THEN SIGNAL ERROR 3,'''TO'' DOES POINT TO INVALID
KEYWORK ''%LIST(%TO+1)'':- ON IGNORED';
                     ELSE DO;
                         %DSN='D%SYSDATE.T%HOURS%MINS.S%SECS';
                         DCL DUMP DS  DSN=%DSN UNIT=%LIST(%TO+1)
                             0,100 TRACKS;
                         DCL IEFBR14 PROG
                             DD1 MAY WRITE DUMP,DD2 MAY READ DUMP;
                         RUN IEFBR14;
                         JOLOPT DUMPCARD '//%DDNAME DD DSN=%DSN,DISP=(M
OD,PASS,CATLG),UNIT=(,,DEFER)';
                         DISPLAY 'DUMP WILL GO TO ''%DSN'' DATA SET';
                     END;
                 END;
             END;
         END;
         END   /* OF ON ERROR MACRO COMMAND   */   ;
