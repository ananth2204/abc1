PROTECT: MACRO(%VOL=,%UNIT=);
/*******************************************************************/
/*   NOTE'  THIS MACRO VARIES FROM THE MANUAL IN THAT IT REQUIRES **/
/*          EXTRA PARAMETERS.                                     **/
/*       IE. VOL=   AND  UNIT=                                    **/
/*   THIS IS BECAUSE AT PRESENT WE HAVE TO USE IEHPROGM TO        **/
/*   EXECUTE THE PROTECT COMMAND.    LOOK OUT FOR AN UPDATE       **/
/*                                   IN THE NEAR FUTURE           **/
/*   THE FORMAT IS                                                **/
/*    PROTECT DSN                                                 **/
/*            FUNCTION   (ADD/DELETE/REPLACE/LIST)                **/
/*            OLD-PASSWORD                                        **/
/*            NEWPASSWORD                                         **/
/*            TYPE       (PWREAD/PWRITE/NOPWREAD/NOWRITE)         **/
/*            VOL=XXXXXX (VOLUME DSN IS ON)                       **/
/*            INIT=XXXXX (UNIT TYPE )                             **/
/*            DATA 'DATA STRING' (TEXT DATA)                      **/
/*******************************************************************/
/**    TEST FOR DATA FOR PROTECT COMMAND      **********************/
/*******************************************************************/
DCL %PASS,%NEWPASS,%FUNC,%DSN1,%TYPE,%DTA,%UN1,%A INIT '';
IF %LIST(1) THEN %DSN1='%LIST(1)';
IF %LIST(2) THEN DO              ;
      %FUNC='%LIST(2)'           ;
END;
IF %LIST(2)='ADD' THEN %NEWPASS='%LIST(3)' ;
IF %LIST(2)='REPLACE' THEN DO              ;
   %PASS='%LIST(3)'; %NEWPASS='%LIST(4)'   ;
END;
IF %LIST(2)='DELETE' THEN %PASS='%LIST(3)';
IF %LIST(2)='LIST'   THEN %PASS='%LIST(3)';
IF %LIST(2)='REPLACE' & %LIST(5)='PWREAD'   THEN %TYPE='1';
IF %LIST(2)='REPLACE' & %LIST(5)='NOPWREAD' THEN %TYPE='3';
IF %LIST(2)='REPLACE' & %LIST(5)='PWRITE'   THEN %TYPE='3';
IF %LIST(2)='REPLACE' & %LIST(5)='NOWRITE'  THEN %TYPE='2';
IF %LIST(2)='ADD' & %LIST(4)='PWREAD'       THEN %TYPE='1';
IF %LIST(2)='ADD' & %LIST(4)='NOPWREAD'     THEN %TYPE='3';
IF %LIST(2)='ADD' & %LIST(4)='PWRITE'       THEN %TYPE='3';
IF %LIST(2)='ADD' & %LIST(4)='NOWRITE'      THEN %TYPE='2';
IF %LIST(5)='DATA' THEN %DTA='%LIST(6)';
IF %LIST(6)='DATA' THEN %DTA='%LIST(7)';
IF %LIST(1)='' THEN DO;
/*******************************************************************/
/**    NO DATA SO USE PANEL                   **********************/
/*******************************************************************/
PANEL ('   PASSWORD PROTECT MACRO   ')
      ('   ----------------------   ')
     /(' ENTER DSNAME                ==>',DSN1,44)
      (' ENTER VOL SER NUMBER        ==>',VOL,6)
      (' ENTER UNIT TYPE             ==>',UNIT,6)
      (' ENTER FUNCTION             ')
      (' EG. ADD/REPLACE/DELETE/LIST ==>',FUNC,44)
      (' ENTER PASSWORD              ==>',PASS,8)
      (' ENTER NEW PASSWORD          ==>',NEWPASS,8)
     /(' ENTER TYPE OF PROTECTION')
      (' EG. 1. PASSWORD READ  - NO PASSWORD WRITE ')
      ('     2. PASSWORD READ  - NO WRITE ')
      ('     3. PASSWORD WRITE - NO PASSWORD READ     ==>',TYPE,17,'3')
      (' DATA                                         ==>',DTA,77);
      END;
/*******************************************************************/
/**    SET UP SYMBOLICS FOR CONTROL CARD      **********************/
/*******************************************************************/
 IF %FUNC='ADD' THEN %FUNC='ADD DSNAME=%DSN1'        ;
 IF %FUNC='REPLACE' THEN %FUNC='REPLACE DSNAME=%DSN1';
 IF %FUNC='DELETE' THEN %FUNC='DELETEP DSNAME=%DSN1' ;
 IF %FUNC='LIST' THEN %FUNC='LIST DSN=%DSN1'         ;
 IF %PASS¬='' THEN %PASS=',PASWORD1=%PASS'           ;
 IF %NEWPASS¬='' THEN %NEWPASS=',PASWORD2=%NEWPASS'  ;
 IF %TYPE¬='' THEN %TYPE=',TYPE=%TYPE'               ;
 IF %DTA¬='' THEN %DTA=',DATA=''%DTA'''              ;
 IF %UNIT='' THEN STOP 'NO UNIT INFORMATION GIVEN'   ;
 IF %VOL='' THEN STOP 'NO VOLUME INFORMATION GIVEN'  ;
 %UN1=',VOL=%UNIT=%VOL'     ;
/*******************************************************************/
/**   DECLARE FILES  FOR IEHPROGM             **********************/
/*******************************************************************/
  DCL INPUT DS %DSN1        ;
  DCL PRINTFIL PRINTER NODCB;
DCL CARDSIN * REPLACE;
 %FUNC%PASS%NEWPASS%TYPE%UN1%DTA
EOF;
/*******************************************************************/
/**   DECLARE PROGRAM                         **********************/
/*******************************************************************/
        IEHPROGM: DCL IEHPROGM PROG
                  SYSUT1   UPDATES INPUT
                  SYSIN    READS  CARDSIN
                  SYSPRINT WRITES PRINTFIL ;
/*******************************************************************/
/**   RUN PROGRAM                             **********************/
/*******************************************************************/
        RUN IEHPROGM ;
END; /*  END OF MACRO */
