BLDJOBC:MACRO;
 /* THIS MACRO BUILDS A JOBCARD FOR THE USER. THE USER HAS A CHOICE
    OF CREATING A JOBCARD OR USING THE $JOB MACRO  */


   UJY02P '/*  This Job was Built Using Jol BUILDJOB Command on';
   UJY02P '    %DAY, %MONTH %DAYNO,%YEAR  */ ';


    %JOBNAME = %SYSUID;
    IF %LIST(1) ¬= '' THEN %JOBNAME = '%JOBNAME%LIST(1)';
    ELSE %JOBNAME = '%JOBNAME'||'X';
    %ACCT='NOACCOUNT';
    IF %SYSUID='OPSG10' THEN %ACCT='T94991I0133,0G10';
    IF %SYSUID='OPSG13' THEN %ACCT='T94991I0133,0G13';
    panel
  / ('              %title')
  / ('       This panel MAY produce a job card')

  /('       You can choose now to use these Job details each time')
   ('       you run your Jol program. You may fill in the details now')
   ('       so they will used each time you use your program, or you')
   ('       may choose to provide the details when you run the program')
   /*  'Select "Y" to use THIS Jobcard or "N" to use the values you')
       (ctr'fill in now.')                            */
     / ('  Do you want to use these details each time? ==>',ANS,1,'Y')
     / ('          Jobname        ===>',JOLNAME,8,'%JOBNAME')
       ('          Account Code   ===>',ACCNT,30,'%ACCT')
       ('          Programmer     ===>',PGNAME,10,'%SYSUID')
       ('          Job Class      ===>',CLASS,1,'A')
       ('          Msgclass       ===>',MSGCLASS,1,'X')
       ('          User           ===>',USER,8,''/*'%SYSUID'*/)
       ('          Password       ===>',PASS,8,'')
       ('          Msglevel       ===>',MSGLV,3,'1,1')
       ('          Priority       ===>',PRTY,2,'03')
       ('          Notify User    ===>',NOTY,8,'%SYSUID');
 IF %SYSPFK = 3 | %SYSPFK=15 THEN EXIT;
 if %ans = 'N' then /* THEY SELECTED TO USE THE $JOB MACRO */
   do;
     UJY02P ' $JOB ; ';
     exit;
   end;
      DCL %JOBC INIT '';
      DCL %JOBC2 INIT '';
      DCL %JOBEXTRA INIT '';
      IF  %PGNAME    ¬= '' THEN %JOBC = 'USER %PGNAME';
  /* FIX IF  %PGNAME     = '' THEN %JOBC = 'USER ''%PGNAME'''; */
      IF  %ACCNT     ¬= '' THEN %JOBC = '%JOBC ACCT %ACCNT';
      IF  %CLASS     ¬= '' THEN %JOBC2 = '%JOBC2 CLASS %CLASS';
      IF  %MSGCLASS  ¬= '' THEN %JOBC2 = '%JOBC2 MSGCLASS %MSGCLASS';
      IF  %PASS      ¬= '' THEN %JOBEXTRA = 'PASSWORD=%PASS';
      IF  %USER      ¬= '' THEN %JOBEXTRA = '%JOBEXTRA,USER=%USER';
      IF  %PRTY      ¬= '' THEN %JOBC2 = '%JOBC2 PRTY %PRTY';
      IF  %NOTY      ¬= '' THEN %JOBC2 = '%JOBC2 NOTIFY %NOTY';
      %a=substr(msglv,1,1);
      %b=substr(msglv,3,1);
/*    EDIT MSGLV A,B A(1),X(1),A(1); */
      IF  %A = '1' THEN %JOBC2 = '%JOBC2 PJCL';
      ELSE %JOBC2 = '%JOBC2 NPJCL';
      IF  %B = '1' THEN %JOBC2 = '%JOBC2 PALLOC';
      ELSE %JOBC2 = '%JOBC2 NPALLOC';
      UJY02P '%JOLNAME:JOB ';
   IF %JOBC     ¬= '' THEN UJY02P '        %JOBC';
   IF %JOBC2    ¬= '' THEN UJY02P '       %JOBC2';
   /* FIX DOUBLE QUOTES  */
   IF %JOBEXTRA ¬= '' THEN UJY02P ' JOBEXTRA=''%JOBEXTRA'';';
   /* */
   IF %JOBEXTRA ¬= '' THEN UJY02P ' JOBEXTRA=%JOBEXTRA ;';
   ELSE UJY02P '                  ; ';
 END; /* OF MACRO */
