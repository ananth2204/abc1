DELINDEX: MACRO (
/*********************************************************************/
/*                                                                   */
/*          COMMAND TO DELETE AN INDEX STRUCTURE                     */
/*          OR AN ALIAS FOR THE HIGHEST LEVEL INDEX                  */
/*          ABS SOFTWARE SERVICES SECTION - 20/06/81                 */
/*                                                                   */
/*********************************************************************/
          %INDEX,       /* OPTIONAL KEYWORD - A LIST OF UP 5 INDEXES */
                        /* MAY BE SPECIFIED AS POSITIONAL PARAMETERS */
          %ALIAS,       /* MUST BE SPECIFIED TO DELETE INDEX ALIAS   */
          %VOL=,        /* OPTIONAL - CVOL VOLUME NUMBER             */
          %UNIT=,       /* OPTIONAL - CVOL UNIT TYPE                 */
          %SYSOUT=      /* DEFAULT IS TO MSGCLASS                    */
          ) ;
/*********************************************************************/
DCL %CVOL     INIT '' ;
DCL %COMMAND  INIT '' ;
DCL %COMMAND1 INIT '' ;
DCL %COMMAND2 INIT '' ;
DCL %COMMAND3 INIT '' ;
DCL %COMMAND4 INIT '' ;
DCL %COMMAND5 INIT '' ;
IF %LIST(1) = '' THEN
                 STOP 'NO INDEX SPECIFIED FOR DELETE INDEX' ;
IF %VOL    ¬= '' & %UNIT = '' THEN
                 SIGNAL 2 'UNIT NOT SPECIFIED - VOL HAS BEEN IGNORED' ;
IF %UNIT   ¬= '' & %VOL = '' THEN
                 SIGNAL 2 'VOL NOT SPECIFIED - UNIT HAS BEEN IGNORED' ;
IF %VOL    ¬= '' & %UNIT ¬= '' THEN %CVOL = ',CVOL=%UNIT=%VOL' ;
IF %ALIAS  ¬= '' THEN
DO;
   %COMMAND  = ' DLTA' ;
   %COMMAND1 = '%COMMAND ALIAS=%LIST(1) %CVOL' ;
END;
ELSE
DO;
   %COMMAND  = ' DLTX' ;
   %COMMAND1 = '%COMMAND INDEX=%LIST(1) %CVOL' ;
   IF %LIST(2) ¬= '' THEN %COMMAND2 = '%COMMAND INDEX=%LIST(2) %CVOL';
   IF %LIST(3) ¬= '' THEN %COMMAND3 = '%COMMAND INDEX=%LIST(3) %CVOL';
   IF %LIST(4) ¬= '' THEN %COMMAND4 = '%COMMAND INDEX=%LIST(4) %CVOL';
   IF %LIST(5) ¬= '' THEN %COMMAND5 = '%COMMAND INDEX=%LIST(5) %CVOL';
END;
/*********************************************************************/
/*          DECLARE DATASETS FOR IEHPROGM                            */
/*********************************************************************/
DCL     PRINT     SYSOUT %SYSOUT ;
DCL     SYSRES    DS
                  UNIT SYSDA
                  VOL  SYSRES
                  NODCB ;
DCL     COMMANDS  *  REPLACE ;
 %COMMAND1
 %COMMAND2
 %COMMAND3
 %COMMAND4
 %COMMAND5
EOF;
/*********************************************************************/
/*          DECLARE  IEHPROGM  PROGRAM                               */
/*********************************************************************/
DELINDX:
DCL      IEHPROGM PROGRAM
                  SYSPRINT  WRITES PRINT
                  SYSRES    READS  SYSRES
                  SYSIN     READS  COMMANDS ;
/*********************************************************************/
/*          RUN  IEHPROGM  PROGRAM                                   */
/*********************************************************************/
RUN DELINDX ;
END;
