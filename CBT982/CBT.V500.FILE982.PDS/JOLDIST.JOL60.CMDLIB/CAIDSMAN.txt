 CAIDSMAN : MACRO;  /* what is the chapter number for this one */
      DECLARE %COM$NUM INIT 1 EXT;
      DECLARE %TYPE$COM INIT '';
      DECLARE %ERR1 INIT '';
      DECLARE %ERR2 INIT '';
      DECLARE %MAX$COM INIT 14 EXT;
      DECLARE %CALL$COM INIT '';
  LABEL DSMMENU;
   PANEL blue box/ (hi ctr 'DATA SET MANAGEMENT')
('      One of the most important tasks any job control language must')
('      be able to perform is DATA SET MANAGEMENT ie. SORTing files')
('      LISTing catalogs, PRINTing files, COMPARing then and so forth.')
('      In this section we will consider these facilities, provided by')
('      JOL.  The DATA SET MANAGEMENT commands are:')
/('          1. SORT       7.  MERGE       13. LOADVOL')
('          2. BUILDGDG   8.  PRINT       14. TEST ')
('          3. COMPRESS   9.  COMPARE')
('          4. COPY       10. DUMPVOL')
('          5. LISTCAT    11. EXTEND')
('          6. LIST       12. FREE')
/('      Please make a selection',COM$NUM,2,'%COM$NUM')
(hi'              %ERR1 %ERR2')
/('      Press ENTER to start from the beginning or select an')
('      instruction. NOTE : if you have not done this course')
('      before - start from the beginning.')
('      Press PFK 3 to return to MAIN menu/index of the course');
      %ERR1 = '';
      %ERR2 = '';
    IF %SYSPFK ¬= 3 & %SYSPFK¬=15 THEN DO;
       %FIRSTD = SUBSTR(COM$NUM,1,1);
       %SECONDD = SUBSTR(COM$NUM,2,1);
        IF %FIRSTD = ' ' | %FIRSTD = 0 | %FIRSTD = '0' THEN %FIRSTD='';
        IF %SECONDD = ' ' THEN %SECONDD = ''; /* SET TO NULL */
        %TYPEF = TYPE(FIRSTD); %TYPES = TYPE(SECONDD);
        IF (%TYPEF ¬= 'NUM' & %FIRSTD ¬= '') |
           (%TYPES ¬= 'NUM' & %SECONDD ¬= '') THEN
           %COM$NUM = '%FIRSTD%SECONDD';
        ELSE %COM$NUM = %FIRSTD%SECONDD;
       %TYPE$COM = TYPE(COM$NUM);
      IF %TYPE$COM = 'NUM' THEN DO;
         IF %COM$NUM = '0' | %COM$NUM = 0 | %COM$NUM > %MAX$COM THEN DO;
            %ERR1 = ' COMMAND NUMBER OUT OF RANGE ';
            %ERR2 = ' PLEASE RETYPE ';
             REDO DSMMENU;
         END;
         ELSE DO;
         LABEL NEXTCOM;
                  %CALL$COM = '';
                  IF %COM$NUM = 1 THEN %CALL$COM = 'CAISORT';
                  IF %COM$NUM = 2 THEN %CALL$COM = 'CAIBGDG';
                  IF %COM$NUM = 3 THEN %CALL$COM = 'CAICOMPR';
                  IF %COM$NUM = 4 THEN %CALL$COM = 'CAICOPY';
                  IF %COM$NUM = 5 THEN %CALL$COM = 'CAILCAT';
                  IF %COM$NUM = 6 THEN %CALL$COM = 'CAILIST';
                  IF %COM$NUM = 7 THEN %CALL$COM = 'CAIMERGE';
                  IF %COM$NUM = 8 THEN %CALL$COM = 'CAIPRINT';
                  IF %COM$NUM = 9 THEN %CALL$COM = 'CAICOMPA';
                  IF %COM$NUM = 10 THEN %CALL$COM = 'CAIDUMPV';
                  IF %COM$NUM = 11 THEN %CALL$COM = 'CAIEXT';
                  IF %COM$NUM = 12 THEN %CALL$COM = 'CAIFREE';
                  IF %COM$NUM = 13 THEN %CALL$COM = 'CAILOADV';
                  IF %COM$NUM = 14 THEN %CALL$COM = 'CAADSMAN';
                    %CALL$COM;
                  IF %SYSPFK = 3 | %SYSPFK=15 THEN REDO DSMMENU;
                  ELSE REDO NEXTCOM;
              END;
      END;
      ELSE DO; %ERR1 = ' COMMAND NUMBER IS NOT NUMERIC ';
               %ERR2 = ' PLEASE RETYPE ';
               REDO DSMMENU;
           END;
    END;  /* IF %SYSPFK ¬= 3 */
         %CHNUM = %CHNUM + 1;
         IF %CHNUM > %MAXCHAP THEN DO;
 PANEL blue box/ ///////(ctr ' This is the end of this section.')
       /(ctr 'Press ENTER to get back to main index/menu.',X,1);
            %CHNUM = 1;
            %SYSPFK = 3;
         END;
 END; /* OF MACRO CAIDSMAN */
