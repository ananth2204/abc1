LINK: MACRO ( /* JOL LINK EDIT MACRO  */
  %COMP
  %STEPLIB=,                   /* IF DIFFERENT LIB REQUIRED*/
  %NOPANEL,                   /* IF PANELS NOT REQUIRED              */
  %CONC=,
  %AUTOLIB=,
  /* DEFINE POSSIBLE STANDARD AUTO CALL LIBRARIES */
  %PLILIB,%PLICMIX,%PLIBASE,%FORTLIB,%COBLIB,
  %LIB=,                     /* DEFINE AN INPUT LIBRARY IF REQ'D     */
  %SYSOUT=,                  /* DEFAULT TO X FOR TSO OR A FOR BATCH  */
  %LOAD=,                    /* DEFINE AN OUTPUT LIBRARY IF REQ'D    */
  %SSI=,                     /* DEFAULT SSI=NONE, MAYBE DATE LATER   */
  %INCLUDE=,                 /* AN INCLUDE LIBRARY MAY BE SPECIFIED  */
  %NAME=,                    /* AND A NAME GENERATOR                 */
  %SIZE='(,)',               /* SIZE OPTION                          */
  %DCBS=,                    /* OPTIONAL BLOCKSIZE FOR OUTPUT        */
  %LIST, %NOLIST,            /* LIST OPTION                          */
  %MAP, %NOMAP,              /* MAP OPTION                           */
  %XREF, %NOXREF,            /* XREF OPTION                          */
  %CROSSREF,                 /* AS ABOVE                             */
  %TEST,%NOTEST,             /* TEST OPTION                          */
  %LET, %NOLET,              /* LET OPTION                           */
  %RENT,%NORENT,             /* REENTRANT OPTION                     */
  %REUS,%NOREUS,             /* REUSABLE OPTION                      */
  %REFR,%NOREFR,             /* REFRESHABLE OPTION                   */
  %NCAL,%NONCAL,             /* NO-CALL OPTION                       */
  %DYNAMIC
  %XCAL,%NOXCAL,             /* EXCLUSIVE CALL OPTION                */
  %ENTRY=,                   /* ENTRY POINT                          */
  %OL,%ONLYLOAD,%NOOL,       /* ONLY LOADABLE OPTION                 */
  %MEMBERS=,                 /* INCLUDE LIBRARY MEMBERS              */
  %NE,%NONE,                 /*                                      */
  %AC=,%PARM=) ;             /* AUTHORISATION REGUIRED               */
IF %SYSOUT = '' THEN
   DO;
      IF %TSOCLASS = '' THEN %SYSOUT = A;
      ELSE %SYSOUT = %TSOCLASS;
   END;
IF %LOAD = '' THEN %LOAD = '%SYSPREF.LOAD';
IF %ONLYLOAD ¬= '' THEN %OL = 'OL' ;
IF %CROSSREF ¬= '' THEN %XREF = 'XREF' ;
IF %PARM = '' THEN
DO ;
   IF %XREF    ¬= ''| %XREF = '' & %NOXREF = ''
    THEN %PARM = 'XREF' ;
   IF %SIZE    ¬= '' & %PARM = '' THEN %PARM = 'SIZE=%SIZE' ;
   IF %SIZE    ¬= '' THEN %PARM = '%PARM,SIZE=%SIZE' ;
   IF %DYNAMIC ¬= '' THEN %PARM = '%PARM,DYNAMIC';
   IF %DCBS    ¬= '' THEN %PARM = '%PARM,DCBS' ;
   IF %LIST    ¬= '' THEN %PARM = '%PARM,LIST' ;
   IF %MAP     ¬= '' THEN %PARM = '%PARM,MAP' ;
   IF %TERM    ¬= '' THEN %PARM = '%PARM,TERM' ;
   IF %TEST    ¬= '' THEN %PARM = '%PARM,TEST' ;
   IF %LET     ¬= '' | %LET = '' THEN %PARM = '%PARM,LET' ;
   IF %RENT    ¬= '' THEN %PARM = '%PARM,RENT' ;
   IF %REUS    ¬= '' THEN %PARM = '%PARM,REUS' ;
   IF %REFR    ¬= '' THEN %PARM = '%PARM,REFR' ;
   IF %NCAL    ¬= '' THEN %PARM = '%PARM,NCAL' ;
   IF %XCAL    ¬= '' THEN %PARM = '%PARM,XCAL' ;
   IF %OL      ¬= '' THEN %PARM = '%PARM,OL' ;
   IF %DC      ¬= '' THEN %PARM = '%PARM,DC' ;
   IF %ALIGN2  ¬= '' THEN %PARM = '%PARM,ALIGN2' ;
   IF %AC      ¬= '' THEN %PARM = '%PARM,AC=%AC' ;
   END;
  DCL %INP INIT '';
  IF %LIST(1) ¬= '' THEN %INP = '%LIST(1)';
  IF %INP = 'OBJ' THEN %INP = '&&OBJ';
DCL %@@@X INIT '';
DCL %@@@L INIT '';
IF %NAME = '' THEN;
   IF %INP ¬= '' THEN DO;
      %@@@X = INDEX(INP,'(');
      IF %@@@X ¬= 0 THEN DO;
         %NAME = SUBSTR(INP,%@@@X);
         %NAME = SUBSTR(NAME,2);
         %@@@L = LENGTH(NAME);
         %@@@L = %@@@L - 1;
         %NAME = SUBSTR(NAME,1,%@@@L);
         %@@@X = '';
      END;
   END;
   DCL %AUTO INIT 1;
   IF %COBLIB = '' & %PLIBASE = '' & %PLILIB = '' & %FORTLIB = ''
   & %PLIMIX = '' THEN %AUTO = '';
  IF %NOPANEL = ''& %AUTO = '' THEN
PANEL ('               JOL LINK COMMAND')
  ////('WHAT AUTOCALL LIBRARY(S) DO YOU WISH TO USE?')
//('PLIBASE *** Y/N ***',PLIBASE,1,'N')
('COBLIB  *** Y/N ***',COBLIB,1,'N')
('FORTLIB *** Y/N ***',FORTLIB,1,'N')
('PLILIB  *** Y/N ***',PLILIB,1,'N')
('PLIMIX  *** Y/N ***',PLIMIX,1,'N')
/////('*********************************************************');
IF %NOPANEL = '' THEN
PANEL ('               JOL LINK COMMAND')
      ('     ANY HIGHLIGHTED FIELD CAN BE CHANGED')
      ('     ALL DATA SETS MUST BE FULLY QUALIFIED')
      ('IF MULTIPLE DATA SETS, DSIDS OR A MIXTURE OF BOTH ARE USED')
      ('     ******* USE || BETWEEN THEM *******')
//('INPUT DATA SET ==========================>',INP,100,'%INP')
('LOAD LIBRARY ============================>',LOAD,44,'%LOAD')
('PROGRAM NAME ============================>',NAME,8,'%NAME')
('ENTRY POINT =============================>',ENTRY,8,'%ENTRY')
('OTHER AUTOCALL LIBRARY(S) ===============>',LIB,100,'%LIB')
('SYSOUT CLASS ============================>',SYSOUT,1,'%SYSOUT')
('INCLUDE LIBRARY(S) ======================>',INCLUDE,100,'%INCLUDE')
('INCLUDE MODULES '','' BETWEEN MULTIPLES =>',MEMBERS,99,'%MEMBERS')
('LINKAGE PARAMETERS FOR THIS RUN =========>',PARM,100,'%PARM');
/********************************************************************/
/* NOW FIX UP ANY AUTO CALL LIBRARIES. AS ONE CAN MIX ALL LANGUAGES */
/* WE WILL ALLOW ALL THE AUTOCALL LIBRARIES TO BE SPECIFIED. THEN   */
/* WE WILL CONCATENATE THEM IF REQUIRED.                            */
/********************************************************************/
IF %INP = '&&OBJ' THEN %INP = 'OBJ';
IF %PL1LIB ¬= '' & %PL1LIB ¬= 'N' THEN
DO ;
   %AUTOLIB = '%AUTOLIB %CONC SYS1.PL1LIB' ;
   %CONC = '||' ;
END ;
IF %PLILIB ¬= '' & %PLILIB ¬ = 'N' THEN
DO ;
   %AUTOLIB = '%AUTOLIB %CONC SYS1.PLILIB' ;
   %CONC = '||' ;
END ;
IF %PLIBASE ¬= '' & %PLIBASE ¬= 'N' THEN
DO ;
   %AUTOLIB = '%AUTOLIB %CONC SYS1.PLIBASE' ;
   %CONC = '||' ;
END ;
IF %PLICMIX ¬= '' & %PLIMIX ¬= 'N' THEN
DO ;
   %AUTOLIB = '%AUTOLIB %CONC SYS1.PLICMIX' ;
   %CONC = '||' ;
END ;
IF %FORTLIB ¬= '' & %FORTLIB ¬= 'N' THEN
DO ;
   %AUTOLIB = '%AUTOLIB %CONC SYS1.FORTLIB' ;
   %CONC = '||' ;
END ;
IF %COBLIB ¬= '' & %COBLIB ¬= 'N' THEN
DO ;
   %AUTOLIB = '%AUTOLIB %CONC SYS1.COBLIB' ;
   %CONC = '||' ;
END ;
IF %LIB ¬= '' THEN
   %AUTOLIB = '%AUTOLIB %CONC %LIB' ;
IF %AUTOLIB ¬= '' THEN %AUTOLIB = 'SYSLIB READS %AUTOLIB' ;
/********************************************************************/
/* NOW LETS SEE WHERE WE ARE TO OUTPUT THE LOAD MODULE TO           */
/********************************************************************/
DCL %LKEDOUT INIT '';
IF %LOAD ¬= '' THEN %LKEDOUT = 'SYSLMOD READS %LOAD' ;
IF %LOAD = '' THEN
DO ;
   DCL LOADLIB DS DSN '&LOAD' 5,1,30 CYLS SYSDA EXT;
   %LKEDOUT = 'SYSLMOD MODS LOADLIB' ;
END ;
/********************************************************************/
/* OK NOW FIX THE INPUTS TO THE LINK EDITOR, AND THERE COULD BE     */
/* QUITE A FEW OF THEM AND SO THIS WILL TAKE SOME ANALYSING         */
/* FIRST THE PRIMARY INPUTS.                                        */
/********************************************************************/
IF %INP = '' THEN /* ANY INPUT OR ARE WE MERELY RELINKING      */
DO ;
 IF %LOAD = '' & %INCLUDE = '' THEN
      STOP 'NO INPUTS OR OUTPUTS SPECIFIED FOR LINK' ;
   IF %INP =  '' & (%INCLUDE = '' | %MEMBERS = '') THEN
      STOP 'ATTEMPTING TO RELINK MODULE(S) BUT NO INCLUDE LIB AND/OR
           MODULE NAME SUPPLIED' ;
 END;
/********************************************************************/
/* WE MAY HAVE TO GENERATE SOME CONTROL CARD INPUT, SO DO IT NOW.   */
/********************************************************************/
DCL %LKEDENT INIT '';
DCL %LKEDINCL INIT '';
DCL %INCLCARD INIT '';
DCL %LKEDSSI INIT '';
DCL %LKEDNAME INIT '';
DCL %LKEDIN INIT '';
IF %INP ¬= '' THEN DO;
   %LKEDIN = '%INP';
   %CONC = '||';
   END;
ELSE %CONC = '';
IF %MEMBERS ¬= '' THEN
DO ;
   IF %INCLUDE ¬= '' THEN
   DO ;
      %LKEDINCL = ' INCLIB READS %INCLUDE' ;
      %INCLCARD = ' INCLUDE INCLIB(%MEMBERS)' ;
   END ;
   IF %INCLUDE = '' THEN          /* RELINK OLD MEMBER FROM LIBRARY */
   DO ;
      %LKEDINCL = 'INCLIB READS %LOAD' ;
      %INCLCARD = ' INCLUDE INCLIB(%MEMBERS)' ;
   END ;
END ;
/********************************************************************/
/* NOW WE MAY STILL HAVE SOME CONTROL CARDS TO GENERATE             */
/********************************************************************/
IF %SSI='' THEN DO;
           %SSI=SUBSTR(SYSDATE,2);
           %SSI='%SSI%HOURS%MINS';
END;
IF %SSI  ¬= '' THEN %LKEDSSI = ' SETSSI %SSI ' ;
WRITE 'LKEDSSI=%LKEDSSI';
IF %ENTRY ¬= '' THEN %LKEDENT = ' ENTRY %ENTRY';
IF %NAME ¬= '' THEN %LKEDNAME = ' NAME %NAME' ;
/*IF %NAME = '' THEN %LKEDNAME = ' NAME TEMPNAME';*/
IF %LKEDNAME ¬= '' THEN
   %LKEDNAME = ' %LKEDNAME(R)' ;
/********************************************************************/
/* NOW FINAL BIT OF LINK EDITOR PRIMARY INPUT PROCESSING            */
/********************************************************************/
IF %LKEDSSI ¬= '' THEN %NOPANEL = 1;
IF %LKEDNAME ¬= '' THEN %NOPANEL = 1;
IF %INCLCARD ¬= '' THEN %NOPANEL = 1;
IF %LKEDENT ¬= '' THEN %NOPANEL = 1;
IF %NOPANEL = 1 THEN
        %LKEDIN = '%LKEDIN %CONC LINKCNTL' ;
IF %NOPANEL = 1
  THEN DCL LINKCNTL * REPLACE ;
%INCLCARD
%LKEDSSI
%LKEDENT
%LKEDNAME
EOF;
IF %LKEDIN = '' THEN STOP 'NO LINKAGE EDITOR INPUT SPECIFIED' ;
IF %LKEDIN ¬=  '' THEN %LKEDIN = 'SYSLIN READS %LKEDIN' ;
/********************************************************************/
/* NOW FINALLY LETS SET UP THE PROGRAM DECLARE AND WORK FILES       */
/* AND RUN THE LINKAGE EDITOR PROGRAM.                              */
/********************************************************************/
DCL PRINTER SYSOUT %SYSOUT   FBA 121,1210 1,2 TRKS ;
IF %STEPLIB ¬= '' THEN %STEPLIB = 'STEPLIB READS %STEPLIB';
DCL WORKFILE DS 5 CYLS SYSDA NODCB;
DCL IEWL PROG SYSPRINT WRITES PRINTER
              SYSUT1 WRITES WORKFILE
              %LKEDOUT        /* OUTPUT */
              %STEPLIB       /* OPTIONAL STEPLIB */
              %AUTOLIB       /* OPTIONAL INPUTS */
              %LKEDINCL         /*  INCLUDE LIBRARY*/
              %LKEDIN ;        /* INPUTS  */
/********************************************************************/
RUN IEWL PARM '%PARM' ;
IF %COMP ¬= '' & %INP = 'OBJ' THEN DELETE OBJ;
/********************************************************************/
END ;
/********************************************************************/
