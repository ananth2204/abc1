 compress:macro(%shr);
  /* This jol macro will compress up to 5 data sets in 1 go
     note, unless 'shr' is specified,data sets will get an 'old'
           disposition   */

/* Check if no parameters, and if so, display a screen for the User */

  if %list(1) = ''   & %panels ¬= 'OFF'
  then do;
     list0001='';
     comptyp  ='N';

     panel       (ctr    'Compress Command')
                 (hi ctr '----------------')
           ///////
           ('      Enter Name of File to Compress  ===> ',list0001,
                       44,'%list0001')
           ('      Enter Y for Shared Operation    ===> ',comptyp,
                       1,'%comptyp')
           helpline('Press F3 to Leave without Compressing Files');
 chkpan:

     if %syspfk = 3 then exit;

     if %list(1) = ''
     then do;
          panel reread from list0001 'Please Enter File Name';
          redo chkpan;
     end;
     if %comptyp ¬= ' ' & %comptyp ¬='Y'
     then do;
          panel reread from comptyp  'Please '' '' or ''Y''';
          redo chkpan;
     end;

     if %comppyp = 'Y' then %shr='SHR';

  end;


  /* Define printer and work files for 'iebcopy' */

 dcl printer printer nodcb;
 dcl work1 ds 5 cyls nodcb sysda;
 dcl work2 ds 5 cyls nodcb sysda;

  /* Now at least one data set must be compressed, so let's define
     a symbolic for that variable (later, if more than 1, we shall
     concatenate other items to it  */

          if %shr='' then %shr='updates ';/* 'old' if ¬'shr' */
          if %shr='SHR' then %shr=' reads ';
 dcl %compds init ' dd1 %shr %list(1)'; /* 1st data set fixed */
 dcl %comp2,%comp3,%comp4,%comp5 init '';
 dcl  %wk1,%wk2,%wk3,%wk4,%wk5 init '';
          /* Now find out how many dsids etc there are, and fix 'em up*/
          if %list(2) then do;      /* if second item,do following */
             %comp2=' dd2 %shr %list(2) ';
             %WK2=' COPY INDD=DD2,OUTDD=DD2';   /* 2ND CONTROL */
          end;  /* of dsid number 2 */
          if %list(3) then do;      /* if 3rd item,do following */
             %comp3=' dd3 %shr %list(3) ';
             %WK3=' COPY INDD=DD3,OUTDD=DD3';   /* 3RD CONTROL */
          end;  /* of dsid number 3 */
          if %list(4) then do;      /* if 4th item,do following */
             %comp4=' dd4 %shr %list(4) ';
             %WK4=' COPY INDD=DD4,OUTDD=DD4';   /* 4TH CONTROL */
          end;  /* of dsid number 4 */
          if %list(5) then do;      /* if 5th item,do following */
             %comp5=' dd5 %shr %list(5) ';
             %WK5=' COPY INDD=DD5,OUTDD=DD5';   /* 5TH CONTROL */
          end;  /* of dsid number 5 */
  /* now lets define iebcopy and its work data sets */
          dcl iebcopy prog
                sysprint writes printer, sysin reads copycntr,
                sysut3 writes work1,sysut4 writes work2,
                %compds %comp2 %comp3 %comp4 %comp5;
  /* now define copycntr with the control stmts in it */
            dcl copycntr * replace;
  COPY INDD=DD1,OUTDD=DD1
  %WK2
  %WK3
  %WK4
  %WK5
EOF;
 %syslabel:
          run iebcopy;   /*do compress or degauss */
    /* Now check if we have > 6 dsids, and if so, call ourselves
         're-entrantly'*/
 dcl %copy2 init '';
                if %list(6)¬='' then do;
                %copy2=' %list(6) ';
                if %list(7) then do; %copy2=' %copy2 %list(7) ';
                if %list(8) then do; %copy2=' %copy2 %list(8) ';
                if %list(9) then do; %copy2=' %copy2 %list(9) ';
                if %list(10) then do; %copy2=' %copy2 %list(10) ';
                if %list(11) then stop 'too many data sets to compress'
                ;
              end;end;end;end;
              compress %copy2  ;
              end;
          end  /* of jol macro compress */  ;
