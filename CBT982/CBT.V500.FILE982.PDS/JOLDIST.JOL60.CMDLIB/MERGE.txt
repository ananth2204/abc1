 JOLUPD : MACRO;
 $JOB;                                      /* CREATE JOBCARD */
 /*************************************************************/
 /* THIS MACRO IS USED TO UPDATE THE JOL DATASETS REQUIRED   **/
 /* TO EXECUTE JOL.                                          **/
 /*************************************************************/
 /* A JOL TAPE CALLED 'JUPDXX' WILL BE SUPPLIED BY YOUR      **/
 /* LOCAL JOL SUPPORT GROUP, AND SHOULD BE INPUT INTO        **/
 /* THIS JOB;                                                **/
 /*************************************************************/
 /*  THE DEFAULT DSNAMES WILL HAVE BEEN SET UP AT            **/
 /*  INSTALLATION TIME BUT MAY BE CHANGED BEFORE EXECUTION.  **/
 /*************************************************************/
 /* SET UP SYMBOLICS FOR USER DATASET NAMES                  **/
 /*************************************************************/
 DCL %TAPENO INIT '';
 %INSTALL='SYS2.JOL40.INSTALL';
 %LOAD='SYS2.JOL40.LOAD'      ;
 %CMDLIB='SYS2.JOL40.CMDLIB'  ;
 %INCLUDE='SYS2.JOL40.INCLUDE';
 %SOURCE='SYS2.JOL40.SOURCE'  ;
 %MACLIB='SYS2.JOL40.MACLIB'  ;
 %PTFS='SYS2.JOL40.PTFS'      ;
 %UCMDLIB='SYS2.USER.CMDLIB'  ;
 /*************************************************************/
 /* SET UP PANEL FOR OPTION SELECTION                        **/
 /*************************************************************/
  PANEL ('  *******  JOL SYSTEM UPDATE   **********')
       /('  ENTER THE OPTION YOU REQUIRE   ')
        ('  ----------------------------    ')
       /(' 1. PRINT OUT THE CURRENT STATUS OF JOL ')
        (' 2. UPDATE JOL WITH THE SUPPLIED JOLUPD TAPE ')
        (' 3. EXIT                                     ')
       /(' ENTER SELECTION ==>',SEL,1,'1');
  VALIDATE SEL 1 3;                /* TEST FOR CORRECT REPLY  */
  IF %SEL='3' THEN EXIT;
 /*************************************************************/
 /* SET UP PANEL FOR OPTION 1   -- PRINT STATUS              **/
 /*************************************************************/
  IF %SEL='1' THEN DO;
  PANEL ('  ********  JOL SYSTEM STATUS *********')
       /('  THE CURRENT STATUS OF JOL WILL BE PRINTED  ')
        ('  FROM MEMBER ''STATUS'' IN DATASET',INSTALL,44,'%INSTALL');
  PRINT %INSTALL(STATUS) ;
  END;
 /*************************************************************/
 /* SET UP PANEL FOR OPTION 2   -- UPDATE JOL                **/
 /*************************************************************/
  IF %SEL='2' THEN DO;
  PANEL ('  ********  JOL UPDATE PROCEDURE *******')
        ('  THE FOLLOWING DATASETS WILL BE UPDATED ')
        ('  1. THEIR NAMES MAY BE OVERRIDDEN HERE.    ')
        ('  2. THE INSTALL DATASET ALWAYS CONTAINS A MEMBER CALLED ')
        ('     STATUS, WHICH CONTAINS DOCUMENTATION RELATING TO THIS')
        ('     RELEASE. IF YOU DO NOT HAVE THIS DATASET INSTALLED YOU')
        ('     CAN DIRECT IT TO ANY OTHER FB 80 DATASET')
       /('ENTER JOL UPD TAPE ==> JUPD',TAPENO,2)
       /('  INSTALL LIBRARY  ==>',INSTALL,44,'%INSTALL')
        ('  LOAD    LIBRARY  ==>',LOAD,44,'%LOAD')
        ('  COMMAND LIBRARY  ==>',CMDLIB,44,'%CMDLIB')
        ('  INCLUDE LIBRARY  ==>',INCLUDE,44,'%INCLUDE')
        ('  SOURCE  LIBRARY  ==>',SOURCE,44,'%SOURCE')
        ('  MACRO   LIBRARY  ==>',MACLIB,44,'%MACLIB')
        ('  PTFS    LIBRARY  ==>',PTFS,44,'%PTFS')
        ('  USER CMD LIBRARY ==>',UCMDLIB,44,'%UCMDLIB');
 /*************************************************************/
 /* OUTPUT FILE DECLARATIONS                                 **/
 /*************************************************************/
 DCL LOADOUT  DS %LOAD     ;
 DCL SORCOUT  DS %SOURCE   ;
 DCL CMDOUT   DS %CMDLIB   ;
 DCL UCMDOUT  DS %UCMDLIB  ;
 DCL INSTOUT  DS %INSTALL  ;
 DCL INCLOUT  DS %INCLUDE  ;
 DCL MACOUT   DS %MACLIB   ;
 DCL PTFSOUT  DS %PTFS     ;
 /*************************************************************/
 /*  INPUT FILE DECLARATIONS                                 **/
 /*************************************************************/
 %TAPENO='JUPD'||%TAPENO;
 DCL INSTIN   DS JOL.INSTALL TAPE VOL %TAPENO   LABEL 1      ;
 DCL LOADIN   DS JOL.LOAD TAPE    VOL %TAPENO   LABEL 2      ;
 DCL CMDIN    DS JOL.CMDLIB TAPE  VOL %TAPENO   LABEL 3      ;
 DCL INCLIN   DS JOL.INCLUDE TAPE VOL %TAPENO   LABEL 4      ;
 DCL SORCIN   DS JOL.SOURCE TAPE  VOL %TAPENO   LABEL 5      ;
 DCL MACIN    DS JOL.MACLIB TAPE  VOL %TAPENO   LABEL 6      ;
 DCL PTFSIN   DS JOL.PTFS TAPE    VOL %TAPENO   LABEL 7      ;
 DCL UCMDIN   DS JOL.UCMDLIB TAPE VOL %TAPENO   LABEL 8      ;
 /*************************************************************/
 /*  RUN COPY PROGRAMS AND PRINT STATUS                      **/
 /*************************************************************/
 IF %INSTALL¬='' THEN COPY INSTIN TO INSTOUT PDS REPLACE OLD ;
 IF %LOAD   ¬='' THEN COPY LOADIN TO LOADOUT PDS REPLACE OLD ;
 IF %CMDLIB ¬='' THEN COPY CMDIN  TO CMDOUT  PDS REPLACE OLD ;
 IF %INCLUDE¬='' THEN COPY INCLIN TO INCLOUT PDS REPLACE OLD ;
 IF %SOURCE ¬='' THEN COPY SORCIN TO SORCOUT PDS REPLACE OLD ;
 IF %MACLIB ¬='' THEN COPY MACIN  TO MACOUT  PDS REPLACE OLD ;
 IF %PTFS   ¬='' THEN COPY PTFSIN TO PTFSOUT PDS REPLACE OLD ;
 IF %UCMDLIB¬='' THEN COPY UCMDIN TO UCMDOUT PDS REPLACE OLD ;
 PRINT %INSTALL(STATUS);
 END;
 /*************************************************************/
 END; /* END OF MACRO  */
