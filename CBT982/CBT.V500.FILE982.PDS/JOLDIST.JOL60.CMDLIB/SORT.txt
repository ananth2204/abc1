   /* THIS IS THE JOL PROGRAM REGISTER COMMAND  */
   /* USING THE REGISTER COMMAND, ONE CAN DEFINE TO JOL PROGRAM
      CHARACTERISTICS AND OTHER DETAILS OF THESE PROGRAMS, SUCH AS
      THE LANGUAGE THEY ARE CODED IN, COMPILER OPTIONS NORMALLY USED,
      INPUTS AND OUTPUTS AND SO ON.

      THEN THE JOL COMPILE, AND EXEC COMMANDS CAN BE SIMPLY USED TO
      COMPILE, LINK AND RUN THESE PROGRAMS.

      FOR EXAMPLE, THE COMMAND

           REGISTER MASTUPDT  LANG PLI
                    FUNC 'THIS PROGRAM UPDATES PAYROLL'
                    COMPOPT 'NEST,GOSTMT'
                    LINKOPT 'LIST,MAP'
                    SOURCE 'APPL.SOURCE.LIB'
                    COPYLIB 'APPL.COPY.LIB'
                    LOAD 'APPL.LOADLIB'
                    FILES  'MASTIN READS,MASTOUT WRITES';

           WILL REGISTER THE PROGRAM TO JOL.

      WHENEVER THE PROGRAM IS TO BE COMPILED, IT IS SIMPLY A
      MATTER OF CODING:-

          COMPILE MASTUPDT;

      AND THE PROGRAM WILL BE COMPILED.

      TO EXECUTE THE PROGRAM, SIMPLY CODE THE FOLLOWING:-

          EXEC MASTUPDT 'INPUT.MASTER' 'TRANS.INPUT' 'MASTER.OUT'
               PRINTER;

       OR

         DCL INPUT   DS INPUT.MASTER;
         DCL OUTPUT  DS OUTPUT.MASTER VB 70,7000 SYSDA 10 CYLS;
         DCL TRANSIN DS TRANS.INPUT;
         DCL PRT     PRINTER B FORM 100 FBA 121,1210;
         EXEC MASTUPDT INPUT,TRANSIN,OUTPUT,PRT;

      IT WILL EVEN BE POSSIBLE TO HAVE STANDARD DEFAULT DATA SETS
      THAT THE PROGRAM WILL USE, UNLESS OVER-RIDDEN, THUS IT IS
      POSSIBLE TO SIMPLY CODE:-

          EXEC MASTUPDT;

     TO HAVE THE PROGRAM EXECUTED. (NOT THIS VERSION!!!!).

     SEE THE JOL DOCUMENTATION FOR FURTHER DETAILS.

  */
  REGISTER:MACRO(%LANG=,      /*DEFAULT LANGUAGE IS PL1             */
         %ASM,%FORT,%COB,%COBOL,%PLI,%PL1,
         %FUNC='',            /*FUNCTION MUST BE CODED BY USER      */
         %COMPOPT=,           /*DEFAULT COMPILER OPTIONS ARE NONE   */
         %LINKOPT=,           /*DEFAULT LINK EDITOR OPTIONS ARE NONE*/
         %COPYLIB=,           /*NO DEFAULT INCLUDE OR COPYLIB       */
         %SOURCE=,            /*NO DEFAULT SOURCE LIBRARY           */
         %LOAD=,              /*NO DEFAULT LOAD MODULE LIBRARY      */
         %AUTOCALL=,          /*NO DEFAULT LINK AUTO CALL LIB       */
         %FILES='',           /*NO DEFAULT INPUTS                   */
         %REPLACE,            /*REPLACE OPTION MAY BE CODED         */
         %NAME=,              /*NAME TO BE GIVEN TO LOAD MODULE (IF */
                              /*        DIFFERENT TO PROGRAM NAME). */
         %PARM=,              /*NO DEFAULT PROGRAM PARAMETER        */
         %SSI='');

   DCL %CNT    INIT 0;
   DCL %SUB    INIT 0;
   DCL %SUBMOD INIT '';
   DCL %JOLDS1 INIT '%SYSPREF.JOL';
   DCL %ERROR, %PGMNAME, %ERR01A                               INIT '';
   DCL %ERR01, %ERR02, %ERR03, %ERR04, %ERR05, %ERR06, %ERR07  INIT '';
   DCL %ERR08, %ERR09, %ERR10, %ERRSTAT                        INIT '';
   IF %LIST(1) THEN %PGMNAME=%LIST(1);
   IF %REPLACE='REPLACE' THEN %REPLACE='REPL';
   ELSE %REPLACE='ADD';
   IF %LIST(1) THEN;ELSE DCL %LIST0001 INIT '';
   IF %LIST(2) THEN STOP 'INVALID ITEM %LIST(2) CODED';
   DCL           %LIST0002,%LIST0003,%LIST0004,%LIST0005,
        %LIST0006,%LIST0007,%LIST0008,%LIST0009,%LIST0010,
        %LIST0011,%LIST0012,%LIST0013,%LIST0014,%LIST0015,
        %LIST0016,%LIST0017,%LIST0018,%LIST0019,%LIST0020,
        %LIST0021,%LIST0022,%LIST0023,%LIST0024,%LIST0025,
        %LIST0026,%LIST0027,%LIST0028,%LIST0029,%LIST0030,
        %LIST0031,%LIST0032,%LIST0033,%LIST0034,%LIST0035,
        %LIST0036,%LIST0037,%LIST0038,%LIST0039,%LIST0040,
        %LIST0041,%LIST0042,%LIST0043,%LIST0044,%LIST0045,
        %LIST0046,%LIST0047,%LIST0048,%LIST0049,%LIST0050,
                                 INIT '';
   DCL %O1,%O2,%O3,%O4,%O5,%O6,%O7,%O8,%O9,%O10,
       %O11,%O12,%O13,%O14,%O15,%O16,%O17,%O18,%O19,%O20,
       %O21,%O22,%O23,%O24,%O25,
       %TYP001,%TYP002,%TYP003,%TYP004,%TYP005,%TYP006,
       %TYP007,%TYP008,%TYP009,%TYP010,%TYP011,%TYP012,
       %TYP013,%TYP014,%TYP015,%TYP016,%TYP017,%TYP018,
       %TYP019,%TYP020,%TYP021,%TYP022,%TYP023,%TYP024,%TYP025,
                                 INIT '';
   DCL %I1,%I2,%I3,%I4,%I5,%I6,%I7,%I8,%I9,%I10,
       %I11,%I12,%I13,%I14,%I15,%I16,%I17,%I18,%I19,%I20,
       %I21,%I22,%I23,%I24,%I25
                                 INIT '';
   IF %LANG='' THEN DO;
         IF %COBOL¬='' THEN %COBOL='COB';
         %LANG='%ASM%COB%COBOL%PLI%PL1%FORT';
   END;
   IF %LANG='' THEN DO; WRITE 'ENTER TYPE OF PROGRAM';READ LANG;END;
   IF %LANG='PLI' | %LANG='PL1'
   | %LANG='COBOL' | %LANG='COB'
   | %LANG='ALGOL'
   | %LANG='FORTRAN' | %LANG='FORT'
   | %LANG='ASSEMBLER' | %LANG='ASM'
   THEN DO;
      SET LANG=SUBSTR(LANG,1,3);
      IF %LANG='ASS' THEN %LANG='ASM';
      IF %LANG='FOR' THEN %LANG='FORT'; /* 6TH. AUGUST 1982 */
      IF %LANG='ALG' THEN %LANG='ALGOL'; /* 6TH. AUGUST 1982 */
   END;
   ELSE DO;
      %ERROR=1;
      STOP 'LANGUAGE IS INCORRECTLY SET TO ''%LANG''';
   END;
   IF %SOURCE='' THEN %SOURCE='%SYSUID.%LANG';
   IF %COPYLIB='' THEN %COPYLIB='%SYSUID.%LANG';
   IF %LOAD='' THEN %LOAD='%SYSUID.LOAD';
   IF %AUTOCALL='' THEN DO;
       IF %LANG¬='ASM' THEN
       %AUTOCALL='SYS1.%LANG'||'LIB';
   END;
   IF %LINKOPT='' THEN %LINKOPT='LIST';
   IF %COMPOPT='' THEN %COMPOPT='LIST';
  LABEL RPANL;
  PANEL
  ('NAME OF PROGRAM TO BE REGISTERED ',PGMNAME,8,'%PGMNAME')
  ('NAME OF YOUR JOL DATA SET ',JOLDS1,36,'%JOLDS1')
  ('%ERR01 %ERR01A')
  ('LANGUAGE (ASM,COBOL,PLI,FORT)    ',LANG,8,'%LANG')
  ('                        %ERR02')
  ('WHAT DOES THE PROGRAM DO?',FUNC,80'%FUNC')
  ('                        %ERR03')
  ('WHAT IS THE SOURCE LIBRARY NAME? ',SOURCE,44,'%SOURCE')
  ('                        %ERR04')
  ('WHAT IS THE COPY LIBRARY NAME?   ',COPYLIB,44,'%COPYLIB')
  ('                        %ERR05')
  ('LOAD MODULE LIBRARY NAME         ',LOAD,44,'%LOAD')
  ('                        %ERR06')
  ('DEFAULT AUTO CALL LIBRARY        ',AUTOCALL,44,'%AUTOCALL'
  ('                        %ERR07')
  ('WHAT SUB MODULES ARE USED?',SUBMOD,44,'%SUBMOD')
  ('                        %ERR08')
  ('DEFAULT COMPILER OPTIONS         ',COMPOPT,80,'%COMPOPT')
  ('                        %ERR09')
  ('DEFAULT LINK EDIT OPTIONS'       ,LINKOPT,80,'%LINKOPT')
  ('                        %ERR10');
  IF %SYSPFK=3 | %SYSPFK=15 THEN EXIT;
  %CNT=%CNT+1; /* ONLY REPEAT SCREEN 2 TIMES UNLESS THE PGNAME IS
                  WRONG (IF IT IS THE SECOND TIME USE STOP COMMAND) */

 %ERR01A ='';
 %ERR01=''; %ERR02=''; %ERR03=''; %ERR04=''; %ERR05=''; %ERR06='';
 %ERR07=''; %ERR08=''; %ERR09=''; %ERR10=''; %ERRSTAT = '';
   IF %PGMNAME = '' THEN
                 %ERR01 = 'ERROR ENTER PROGRAM NAME TO BE REGISTERED ';
   IF %JOLDS1 = '' THEN
                %ERR01A = 'ERROR ENTER JOL DATA SET NAME';
   IF %LANG    = '' THEN %ERR02 = 'ERROR ENTER LANGUAGE ';
   IF %FUNC    = '' THEN %ERR03 = 'ERROR ENTER PROGRAM FUNCTION ';
   IF %SOURCE  = '' THEN %ERR04 = 'ERROR ENTER PROGRAM SOURCE LIBRARY';
   IF %COPYLIB = '' THEN %ERR05 = 'ENTER MACRO/COPY SOURCE LIBRARY';
   IF %LOAD    = '' THEN %ERR06 = 'ENTER PROGRAM LOAD LIBRARY';
   IF %AUTOCALL= '' THEN %ERR07 =
                                'ENTER AUTO-CALL LIBRARY * OPTIONAL *';
   IF %SUBMOD  = '' THEN %ERR08 = 'ERROR SUB-MODULE NAME * OPTIONAL *';
   IF %COMPOPT = '' THEN %ERR09 = 'ENTER DEFAULT COMPILER OPTIONS';
   IF %LINKOPT = '' THEN %ERR10 = 'ENTER DEFAULT LINK OPTIONS';
   IF %ERR01 ='' & %ERR02 = '' & %ERR03 = '' & %ERR04 = ''
   & %ERR01A ='' & %ERR05 ='' & %ERR06 = '' & %ERR07 = ''
   &  %ERR09 ='' & %ERR10 = '' THEN; ELSE IF %CNT < 2 THEN REDO RPANL;

%CNT=0; /* RESET COUNTER FOR LATER USE */

   IF %FUNC    = '' THEN SIGNAL 2,'NO FUNCTION SPECIFIED';
   %X=TYPE(PGMNAME);
   IF %X¬='CHAR' THEN DO;
       %ERROR=1;
       STOP 'NO PROGRAM SPECIFIED, OR INCORRECT' ;
   END;
LABEL REGPL2;
PANEL ('PLEASE ENTER PROGRAM FILES OR DDNAMES THAT THE PROGRAM USES.')
 / ('ENTER THEM LIKE THIS:-')
 /('SYSUT1 WRITES, SYSUT2 MAY WRITE, ')
 /('YOU MAY USE READS,WRITES,MODS OR UPDATES')
 /('',FILES,250,'%FILES')
 /('%ERR01');
IF %SYSPFK=3 | %SYSPFK=15 THEN EXIT;
%CNT=%CNT+1;
%ERR01 = '';
   IF %FILES  = '' THEN %ERR01 = 'ERROR ENTER PROGRAM FILES ';
   IF %ERR01 ¬= '' & %CNT < 2 THEN REDO REGPL2;
   IF %FILES  = '' THEN DO;
       %ERROR=1;
       SIGNAL ERROR 1,'NO INPUTS OR OUTPUTS SPECIFIED';
   END;
   IF %FILES¬=''
   THEN DO;
      SPLIT INPUTS,8 %FILES;
      EDIT  INPUTS LIST0001,LIST0002,LIST0003,LIST0004,LIST0005,
         LIST0006,LIST0007,LIST0008,LIST0009,
         LIST0010,LIST0011,LIST0012,LIST0013,LIST0014,LIST0015,
         LIST0016,LIST0017,LIST0018,LIST0019,LIST0020,LIST0021,
         LIST0022,LIST0023,LIST0024,LIST0025,LIST0026,LIST0027,
         LIST0028,LIST0029,LIST0030,LIST0031,LIST0032,LIST0033,
         LIST0034,LIST0035,LIST0036,LIST0037,LIST0038,LIST0039,
         LIST0040,LIST0041,LIST0042,LIST0043,LIST0044,LIST0045,
         LIST0046,LIST0047,LIST0048,LIST0049,LIST0050
                   A(8);
 PANEL ('IF YOUR FILES REQUIRE SPECIFIC FILE TYPES ')
       (' OR LRECL PLS INSERT THEM HERE OR ENTER NULL')
       (' EG.   FB,200     OR   VB,6400        ')
       (' -----------------------------------------')
       (' %LIST0001            ',TYP001,10)
       (' %LIST0003            ',TYP002,10)
       (' %LIST0005            ',TYP003,10)
       (' %LIST0007            ',TYP004,10)
       (' %LIST0009            ',TYP005,10)
       (' %LIST0011            ',TYP006,10)
       (' %LIST0013            ',TYP007,10)
       (' %LIST0015            ',TYP008,10)
       (' %LIST0017            ',TYP009,10)
       (' %LIST0019            ',TYP010,10);
  IF %LIST0021 ¬='' THEN DO;
 PANEL ('IF YOUR FILES REQUIRE SPECIFIC FILE TYPES ')
       (' OR RECORD LENGTHS INSERT THEM HERE OR ENTER NULL ')
       (' EG   FB,200      OR    VB,6400                   ')
       (' -----------------------------------------')
       (' %LIST0021            ',TYP011,10)
       (' %LIST0023            ',TYP012,10)
       (' %LIST0025            ',TYP013,10)
       (' %LIST0027            ',TYP014,10)
       (' %LIST0029            ',TYP015,10)
       (' %LIST0031            ',TYP016,10)
       (' %LIST0033            ',TYP017,10)
       (' %LIST0035            ',TYP018,10)
       (' %LIST0037            ',TYP019,10)
       (' %LIST0039            ',TYP020,10);
 END;
  IF %LIST0041¬='' THEN DO;
 PANEL ('IF YOUR FILES REQUIRE SPECIFIC FILE TYPES ')
       (' OR RECORD LENGTHS INSERT THEM HERE OR ENTER NULL ')
       (' EG.   FB,200          VB,6400                    ')
       (' -----------------------------------------')
       (' %LIST0041            ',TYP021,10)
       (' %LIST0043            ',TYP022,10)
       (' %LIST0045            ',TYP023,10)
       (' %LIST0047            ',TYP024,10)
       (' %LIST0049            ',TYP025,10);
 END;
/* THIS CODE CHANGED 1/6/82 USING REDO  */

      IF %LIST(1)¬=''  THEN DO;
           IF %LIST(2)¬='MAY' THEN DO;
                %O1='%LIST(1)  %LIST(2)';
                %SUB=3;
           END;
           ELSE DO;
                %O1='%LIST(1) MAY %LIST(2)';
                %SUB=4;
           END;
      END;
%CNT=1;

LABEL RESOLVE;
      IF %LIST(%SUB)¬=''  THEN DO;
           %CNT=%CNT+1;
           IF %LIST(%SUB+1)¬='MAY' THEN DO;
           SET  O%CNT ='%LIST(%SUB)  %LIST(%SUB+1)';
                %SUB=%SUB+2; REDO RESOLVE;
           END;
           ELSE DO;
           SET  O%CNT ='%LIST(%SUB)  MAY %LIST(%SUB+2)';
                %SUB=%SUB+3; REDO RESOLVE;
           END;
      END;

   END/* FILES DECODE */  ;
   IF %SYSTEM='VS1' THEN DO;
   DCL CNTL * REPLACE ;
./ %REPLACE NAME=%PGMNAME,LIST=ALL
  /* THIS PROGRAM WAS REGISTERED ON %DAY %DAYNO %MONTH %YEAR */
  /* FUNCTION %FUNC  */
  %%PGMNAME=%PGMNAME;%%LANG='%LANG';
  IF %%COMPOPT='' THEN %%COMPOPT='%COMPOPT';
  IF %%LINKOPT='' THEN %%LINKOPT='%LINKOPT';
  IF %%SOURCE='' THEN %%SOURCE='%SOURCE';
  IF %%LOAD='' THEN %%LOAD='%LOAD';
  IF %%LIB='' THEN %%LIB='%COPYLIB';
  IF %%AUTOCALL='' THEN %%AUTOCALL='%AUTOCALL';
  %%O1='%O1';%%O2='%O2';
  %%O3='%O3';%%O4='%O4';
  %%O5='%O5';%%O6='%O6';
  %%O7='%O7';%%O8='%O8';
  %%O9='%O9';%%O10='%O10';
  %%O11='%O11';%%O12='%O12';
  %%O13='%O13';%%O14='%O14';
  %%O15='%O15';%%O16='%O16';
  %%O17='%O17';%%O18='%O18';
  %%O19='%O19';%%O20='%O20';
  %%O21='%O21';%%O22='%O22';
  %%O23='%O23';%%O24='%O24';
  %%O25='%O25';
  %%TYP001='%TYP001';%%TYP002='%TYP002';%%TYP003='%TYP003';
  %%TYP004='%TYP004';%%TYP005='%TYP005';%%TYP006='%TYP006';
  %%TYP007='%TYP007';%%TYP008='%TYP008';%%TYP009='%TYP009';
  %%TYP010='%TYP010';%%TYP011='%TYP011';%%TYP012='%TYP012';
  %%TYP013='%TYP013';%%TYP014='%TYP014';%%TYP015='%TYP015';
  %%TYP016='%TYP016';%%TYP017='%TYP017';%%TYP018='%TYP018';
  %%TYP019='%TYP019';%%TYP020='%TYP020';%%TYP021='%TYP021';
  %%TYP022='%TYP022';%%TYP023='%TYP023';%%TYP024='%TYP024';
  %%TYP025='%TYP025';
EOF;
     DCL IEBUPDTE PROG
         SYSPRINT WRITES P
         ENQ      WRITES ENQ,  /* SO TWO OF THESE JOBS CAN'T RUN */
         SYSIN    READS  CNTL
         SYSUT1   READS  %JOLDS1
         SYSUT2   READS  %JOLDS1;
     DCL P PRINTER NODCB;
     DCL ENQ DS %SYSPREF.JOL.HOLD.JOB 0 TRACKS SYSDA NODEF;
     RUN IEBUPDTE;
     END;
     ELSE DO;
     UJY09OP '%JOLDS1' %PGMNAME
' /* THIS PROGRAM WAS REGISTERED ON %DAY %DAYNO %MONTH %YEAR */'
' /* FUNCTION %FUNC  */ '
' %%PGMNAME=%PGMNAME;%%LANG=''%LANG'';'
' IF %%COMPOPT='''' THEN %%COMPOPT=''%COMPOPT''; '
' IF %%LINKOPT='''' THEN %%LINKOPT=''%LINKOPT''; '
' IF %%SOURCE='''' THEN %%SOURCE=''%SOURCE''; '
' IF %%LOAD='''' THEN %%LOAD=''%LOAD'';'
' IF %%LIB='''' THEN %%LIB=''%COPYLIB'';'
' IF %%AUTOCALL='''' THEN %%AUTOCALL=''%AUTOCALL'';'
' %%O1=''%O1'';%%O2=''%O2'';'
' %%O3=''%O3'';%%O4=''%O4'';'
' %%O5=''%O5'';%%O6=''%O6'';'
' %%O7=''%O7'';%%O8=''%O8'';'
' %%O9=''%O9'';%%O10=''%O10'';'
' %%O11=''%O11'';%%O12=''%O12'';'
' %%O13=''%O13'';%%O14=''%O14'';'
' %%O15=''%O15'';%%O16=''%O16'';'
' %%O17=''%O17'';%%O18=''%O18'';'
' %%O19=''%O19'';%%O20=''%O20'';'
' %%O21=''%O21'';%%O22=''%O22'';'
' %%O23=''%O23'';%%O24=''%O24'';'
' %%O25=''%O25'';'
' %%TYP001=''%TYP001'';%%TYP002=''%TYP002'';%%TYP003=''%TYP003'';'
' %%TYP004=''%TYP004'';%%TYP005=''%TYP005'';%%TYP006=''%TYP006'';'
' %%TYP007=''%TYP007'';%%TYP008=''%TYP008'';%%TYP009=''%TYP009'';'
' %%TYP010=''%TYP010'';%%TYP011=''%TYP011'';%%TYP012=''%TYP012'';'
' %%TYP013=''%TYP013'';%%TYP014=''%TYP014'';%%TYP015=''%TYP015'';'
' %%TYP016=''%TYP016'';%%TYP017=''%TYP017'';%%TYP018=''%TYP018'';'
' %%TYP019=''%TYP019'';%%TYP020=''%TYP020'';%%TYP021=''%TYP021'';'
' %%TYP022=''%TYP022'';%%TYP023=''%TYP023'';%%TYP024=''%TYP024'';'
' %%TYP025=''%TYP025'';';
 END;
 END;
