$$init: macro;
/* This macro is called ONCE by $jolprof at JOL startup time. */

    dcl init_dir              EXT init('');

    if %system='SHELL'
    then do;
      getdir;                   /* Get current Directory  */
      init_dir=sysdir;          /* and SAVE for later use */
      init_dir=%sysuid;         /* and SAVE for later use */
    end;

    if %system='MVS'
    then do;
      if %sysuid=''             /* Any User ID?           */
      then panel // (ctr 'Jol Start Up' )
                 // (ctr 'Information Needed')
                 // (' Userid or Sysuid ====> ',sysuid,8,'')
                 helpline ('Information for Jol Program');
    end;

    dcl color                 EXT      init('');

  /* The colors below are used in the Jol Menus.

     You may change them, and they will apply to most of the Jol menus,
     unless BW is specified (this turns off all color commands).

     You are strongly advised not to color the background if you
     call DOS commands, because they will show parts of the screen
     in black and white, overwriting Jol's background colors.
     HOWEVER, if you also tell ANSI.SYS to use the same background
     colors as Jol, you can safely color the background with an
     instruction like BLUE BACKGROUND.
  */

    dcl boxcolor              EXT init(' blue ');
    dcl hedcolor              EXT init(' bright green ');
    dcl bakcolor              EXT init(' black ');
    dcl txtcolor              EXT init(' cyan ');
    dcl hicolor               EXT init(' bright yellow ');

    if gui='NO'   /* Are we in Batch?  */
    then exit;


    if syscrt='BW' then color='NO';
    else color='YES';

    if color¬='' then goto setcolor; /* Color on through PARM option? */

    panel box ('Do You Have a Color Monitor ? (Y or N) ',
    color,1,'%color',
        10,10);
treply:
    color = caps(color);
    if substr(color,1,1)¬='Y' & substr(color,1,1)¬='N'
    then do;
        panel reread from color 'Please Enter Y or N';
        goto treply;
    end;
setcolor:

    if substr(color,1,1)='Y' | substr(color,1,1)='y'
    then do;
       if %system¬='MVS'
       then do;
           jolopt color;
            %txtcolor text;
            %bakcolor background;
            %hicolor highlights;
       end;
    end;
    else jolopt bw;                   /* Set colors off */

    if %system¬='MVS'
    then do;
        if substr(init_dir,1,1) = 'A'     /* Test if Floppy  */
        |  substr(init_dir,1,1) = 'B'     /* Test if Floppy  */
        then do;
          panel box
          /////
          (ctr flash 'Warning')
          //
          (ctr 'Running Jol from Drive A or B will be')
          (ctr 'very, very much slower than running from a ')
          (ctr 'Fixed Disk.')
          helpline ('Press Enter to Continue');
        end;
    end;

end /* Macro */;
