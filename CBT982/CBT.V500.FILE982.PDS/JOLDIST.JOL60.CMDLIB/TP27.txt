 SAVESYMS:MACRO; /* THIS MACRO SAVES ANY SYMBOLICS SENT TO IT */

    /* Modified for Personal Jol, March, 1988 */
    /* Note, MEMBER name not supported for PERSONAL Jol */

    /* THE FORMAT OF THE MACRO IS:-

       SAVESYMS YEAR DATE MONTH IN 'DATA.SET.NAME(MEMBER)';

       WHERE YEAR DATE AND MONTH ARE THE SYMBOLICS
       DATA.SET.NAME IS THE DATA SET NAME TO SAVE THE
       MEMBER.   */

 /*NOTE           IF A SYMBOLIC IS NULL IT IS SAVED
   ****           IN THIS VERSION. EG: IF %A IS NULL THEN SAVESYMS WILL
                  PRODUCE A LINE %A=''; */

      /* IF SAVESYMS IS CALLED WITHOUT THE DSN TO SAVE IT
         OR NO LIST ITEMS, NOTHING IS SAVED.
         IF IT IS CALLED WITH "SAVESYMS A IN;" THEN A STOP
         MESSAGE IS ISSUED.

      */

  DCL LISTIN, CNT, LTMB, RTMB         INIT 0 ; /* SYMBOLICS USED */
  DCL JOLDS, MEMBER, CHECK, CHECKVAL  INIT ''; /* IN THIS MACRO  */
  DCL QUOTE INIT '''';

 IF LIST(2) ¬= '' THEN DO;
    LISTIN = SYSNLIST;  /* NUMBER OF LIST ITEMS */

    LISTIN=LISTIN-1;
    IF LIST(LISTIN) = 'IN' THEN DO;
       LISTIN=LISTIN+1;
       JOLDS= '%LIST(%LISTIN)' ; /* DSN TO SAVE DATA */
       LTMB = INDEX(JOLDS,'('); /* POSITION OF (    */
       RTMB = INDEX(JOLDS,')'); /* POSITION OF )    */
       IF RTMB ¬= 0 THEN DO;
               RTMB = RTMB - 1;
               JOLDS=SUBSTR(JOLDS,1,RTMB); /* REMOVE RIGHT ) */
       END;     /* IF NO RIGHT BRACKET THEN OK */

       IF LTMB >= 1 THEN DO;
          LTMB = LTMB +1;
          MEMBER=SUBSTR(JOLDS,LTMB); /* NOW WE HAVE THE MEMBER NAME */
          LTMB=LTMB-2;
          JOLDS=SUBSTR(JOLDS,1,LTMB); /* NOW WE HAVE THE DSN */
       END;
/*     WRITE '%JOLDS   %MEMBER';
       ELSE DO;
          STOP 'CANNOT FIND MEMBER TO PLACE SYMBOLICS CORRECTLY';
          EXIT;
       END;     */
    END; /* IF IN */
 ELSE DO;
      WRITE 'No Data Set Specified for SAVESYMS:- Ignored';
      exit;
      END;

/* IF JOLDS ¬= '' & MEMBER ¬= '' THEN DO; */
   IF JOLDS ¬= '' THEN DO;
      LISTIN = LISTIN-2;
      alloc f($saves) da('%jolds') new;
      open f($saves) output;

      CNT = 0;                  /* Counter FOR REDOING LABELS */
RESOLVE:
          CNT=CNT+1;
          CHECK = '%LIST(%CNT)'; /* EG LIST1, LIST2 ETC */
          set SET = 'SET CHECKVAL = ''%%%CHECK''';
          if length(set)=16
          then do;
             write file($saves) from('%CHECK='' '';');
             IF CNT < LISTIN then goto RESOLVE;
          end;
          %SET;
          write file($saves) from(
               '%CHECK =''%CHECKVAL'';'); /* OUTPUT THE DATA */
       IF CNT < LISTIN then goto RESOLVE;
       close f($saves);          /*  CLOSE FILE AND FREE IT */
     END;
 END;
 ELSE DO;
      WRITE 'Not Enough Items Found in SAVESYMS, Nothing Saved';
      END;
 END; /* OF MACRO */

