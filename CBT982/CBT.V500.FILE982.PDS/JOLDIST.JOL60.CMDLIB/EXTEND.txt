EXTEND : MACRO (
/*********************************************************************/
/*                    EXTEND COMMAND                                 */
/*                                                                   */
/* TO COPY A NUMBER OF DATASETS TO THE END OF A SINGLE DATASET       */
/*                                                                   */
/*********************************************************************/
         WITH,               /* TO BE FOLLOWED BY DSN LIST           */
         %SYSOUT=*           /* FOR PRINT OUTPUT FROM UTILITY        */
         ) ;                 /*                                      */
/*********************************************************************/
DCL %MODLIST INIT '' ;
DCL %UTILITY INIT '' ;
DCL %$T      INIT '' ;
IF %LIST(1)  = '' THEN STOP 'NO DATASET SPECIFIED TO EXTEND' ;
IF %WITH     = 0  THEN STOP 'KEYWORD WITH NOT SPECIFIED' ;
IF %SYSNLIST < 3  THEN STOP 'NO EXTEND DATASETS SPECIFIED' ;
/*********************************************************************/
/*           CONCATENATE DATASETS TO BE ADDED - MAX 10               */
/*********************************************************************/
IF %SYSNLIST >=  3 THEN %MODLIST = '%LIST(3)' ;
IF %SYSNLIST >=  4 THEN %MODLIST = '%MODLIST||%LIST(4)' ;
IF %SYSNLIST >=  5 THEN %MODLIST = '%MODLIST||%LIST(5)' ;
IF %SYSNLIST >=  6 THEN %MODLIST = '%MODLIST||%LIST(6)' ;
IF %SYSNLIST >=  7 THEN %MODLIST = '%MODLIST||%LIST(7)' ;
IF %SYSNLIST >=  8 THEN %MODLIST = '%MODLIST||%LIST(8)' ;
IF %SYSNLIST >=  9 THEN %MODLIST = '%MODLIST||%LIST(9)' ;
IF %SYSNLIST >= 10 THEN %MODLIST = '%MODLIST||%LIST(10)' ;
IF %SYSNLIST >= 11 THEN %MODLIST = '%MODLIST||%LIST(11)' ;
IF %SYSNLIST >= 12 THEN %MODLIST = '%MODLIST||%LIST(12)' ;
IF %SYSNLIST >  12 THEN SIGNAL 2,
                'MORE THAN 10 EXTEND DATASETS SPECIFIED - '
                'REMAINDER IGNORED' ;
/*********************************************************************/
/*              SET UP DATASETS FOR GENER UTILITY                    */
/*********************************************************************/
%$T = INDEX(LIST0001,'(') ;
IF %$T ¬= 0 THEN STOP 'CANNOT EXTEND A PARTITIONED DATASET MEMBER' ;
%$T = INDEX(LIST0001,'.') ;
IF %$T ¬= 0 THEN
DO;
   LOCATEF '%LIST(1)' ;
   IF %SYSRC ¬= 0 THEN STOP
      'DSN SPECIFIED NOT CATALOGUED - DECLARE AND ALLOCATE SPACE' ;
END;
DCL PRINTER SYSOUT %SYSOUT OUTLIM 1000;
DCL DUMMY   DS DUMMY NODCB ;
/*********************************************************************/
/*              DECLARE THE UTILITY PROGRAM                          */
/*********************************************************************/
IF %SYSTEM = 'OS4' THEN %UTILITY = 'JSDGENER' ;
                   ELSE %UTILITY = 'IEBGENER' ;
EXTEND:
      DCL %UTILITY PROG
                   SYSUT1   READS  %MODLIST
                   SYSUT2   MODS   %LIST(1)
                   SYSIN    READS  DUMMY
                   SYSPRINT WRITES PRINTER ;
/*********************************************************************/
RUN EXTEND ;
/*********************************************************************/
END;
