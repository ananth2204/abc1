menu:macro;                               /* Jol Main Menu. */

dcl %command  init('');
dcl %commtail init('') EXT;               /* Command Tail put here */
dcl %commstrt init('') EXT;               /* Command Start here */
dcl %syspfk   init 0 ext;
dcl %empty    init 0 ext;                 /* J60                */
dcl %helpline init '';


dcl boxcolor  EXT init(' blue ');
dcl hedcolor  EXT init(' cyan ');
dcl bakcolor  EXT init(' black ');
dcl txtcolor  EXT init(' cyan ');
dcl hicolor   EXT init(' bright yellow ');

    if %Panels='NO'    /* Jol 6.0 Not running interactively - exit */
    then do;
         fs off;       /* Tell Jol and command that PANELs are off */
         exit;
    end;

loop:
    commtail='';
    panel option caps;

    if %system='SHELL' then
helpline='Press F1 for Help Screen; F3 to Return To Jol Command Mode';
    if %system¬='SHELL' then
    helpline='F1 Help, F3 Exits After Making JCL';
    if %buildjob = 'YES' then
    helpline='BUILDJOB is Active: F1 Help, F3 Exits After Making Job';

    panel /* %boxcolor box */
  menubar (
'Files Applications BuildJob Compile Run/Submit  Options eXit Help')

/ (ctr %hedcolor  'Jol Universal Command Language Primary Menu'
                  ' For %system')
/
(ctr'Place the enhanced character of the menu you wish to select in'
    ' the box')
(ctr'or code any Command (or part of) directly in the command line.')
(ctr 'Note: You can use <Alt F>, <Alt C> etc as HotKeys on PCs.')
/
(hi '  Command === > ',command,60,'%commstrt')/* Leave last command */
/
('  The following Jol menus are available:')
/(hi '       A  ' normal 'Run Other Programs, Spreadsheets,'
                         ' Wordprocessing etc.')
 (hi '       B  ' normal 'Builds a Job to Execute Later.')
 (hi '       C  ' normal 'Compiles Programs (Asm, C, Basic and PL/I).')
 (hi '       F  ' normal 'Browse, Copy, Rename or Delete Files; '
                         ' Drop to Unix/DOS/TSO.')
 (hi '       H  ' normal 'Help and Learn Facilities.')
 (hi '       S  ' normal 'Submit Jobs and Programs '
                         'to Background or Mainframes.')
 (hi '       O  ' normal 'Configure Jol or Set Dos Options'
                         ' (TIME, DATE etc).')
 (hi '       R  ' normal 'Runs Programs or Jobs.')
 (hi '       X  ' normal 'Leave Jol.')

  helpline('%helpline')
;

    commtail='';
    commstrt='';

waitin:

/*  if syspfk=3 then do; cls; exit; end; */
/*   printsyms;  */

    if syspfk=3 then goto endjob;

    if command='' & sysmenu='' & syspfk=0
    then do;
        empty=empty+1;        /* If four times here, assume BATCH */
        if empty>4 then exit; /* Clem Clerke, May 2001            */
        panel reread;
        goto waitin;
    end;
    empty=0;

    if syspfk¬=0 & syspfk¬=1 & sysmenu=''
    then do;
        panel reread;
        goto waitin;
    end;
    if syspfk=1 then do;
        $doshlp1;
        goto loop;
    end;

    sysmenu=caps(sysmenu);
/*  ? 'SYSMENU=%sysmenu'; */

    if command ¬='' & sysmenu = '' then goto docomm;

    If sysmenu='QUIT'              then exit quit;

    If sysmenu='EXIT'              then exit quit;

    If sysmenu='APPLICATIONS'      then $applic;

    If sysmenu='FILES'             then $files;

    If sysmenu='COMPILE'           then $compile;

    If sysmenu='SUBMIT'            then $submit;

    If sysmenu='OPTIONS'           then $persopt;

    if sysmenu='HELP'              then helppop;

    if sysmenu='BUILDJOB'          then buildjob;


    If substr(sysmenu,1,3)='RUN'
    then do;
        popup
           (     'Job            ')
           (     'Program        ',exec /*pgmlist*/ )
           ;
        if syspop='JOB'
        then do;
           runjob;
           goto endjob;
        end;
    end;

    %syspfk=0;
    %sysmenu='';
    goto loop;

docomm:

    commtail='';
    commstrt=command;

    if index(command,' ') ¬=0
    then do;
         commstrt = substr(command,1, index(command,' '));
         commtail = substr(command,index(command,' ') +1);
    end;

    if length(strip(command)) =2
    &  substr(command,2,1) = ':'
    then do;
        call command '/c %command';
        goto loop;
    end;

/* Set the menu names to their own names if they are non blank so that
   GETJMENU can load the correct menus for us */

    if %system ¬='SHELL'
    then do;
/* Some commands need altering if MVS, VSE etc */
        if %commstrt = 'CATALOG'
        |  %commstrt = 'CATLG'
           then %commstrt ='$CATLG';
        if %commstrt = 'DELETE'
           then %commstrt ='$DEL';
        if %commstrt = 'TYPE'
           then %commstrt ='PRINT';
        %commstrt %commtail;
        goto loop;
    end;

    %command;
    goto loop;

    if viewmenu¬='' then viewmenu='VIEWMENU';
    if wpmenu  ¬='' then wpmenu  ='EDITMENU';
    if compmenu¬='' then compmenu='COMPMENU';
    if dosmenu ¬='' then dosmenu ='DOSMENU';
    if execmenu¬='' then execmenu='EXECMENU';

/* Call GETJMENU to analyse User input, and call either program or
   Jol Menus as appropriate.                                       */

getjmenu command='%command'
    %dosmenu %execmenu %wpmenu %compmenu /* These are '' or DOSMENU */
    %viewmenu
    menus=
    'DELETE RENAME TYPE COMPARE COPY CHDIR CD DEL DIR REN SORT '
    ;
  goto loop;

endjob:
      ;
/* end;
cls; */
if %system¬='SHELL'
then do;
    if %buildjob='YES'
    then do;
       %syspfk=0;
       close f($code);
       %BUILDJOB = '';           /* Get EXEC etc to operate as usual */
       exit quit;
    end;
    if $job_done ¬='YES'
    then $job;
    exit;      /* Generate JCL */
end;
exit;
