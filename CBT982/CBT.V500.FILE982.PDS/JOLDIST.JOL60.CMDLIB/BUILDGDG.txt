 buildgdg:macro (%vsam=NO,               /* ***** build gdg's ***** */
  %entries=3,%cvol=,%unit=3330,%empty,%delete,   /*general keywords */
  %limit=,%scratch,%owner=,%to=,%for=,%catalog=);  /* vsam keywords */

  if %sysnlist > 5 then stop 'Too Many Entries to BuildGDG';
  dcl %e1,%e2,%e3,%e4,%e5,%option init '';
  dcl %ddcvol init '';
  dcl printer printer nodcb;

/* Check if no parameters, and if so, display a screen for the User */

  if %list(1) = ''   & %panels ¬= 'OFF'
  then do;
     list0001='';
     entries =3;
     fulltyp ='D';

     panel       (ctr    'BuildGDG Command')
                 (hi ctr '----------------')
           ///////
           ('        Enter Name of Index to Build   ===> ',list0001,
                       44,'%list0001')
           ('        Number of Entries              ===> ',entries,
                       3,'%entries')
           ('        Delete or Empty When Full      ===> ',fulltyp,
                       1,'%fulltyp')
           ('        Catalog Name (Optional)        ===> ',catalog,
                       44,'%catalog')
           helpline('Press F3 to Leave without Building the Index');
 chkpan:

     if %syspfk = 3 then exit;

     if %list(1) = ''
     then do;
          panel reread from list0001 'Please Enter an Index Name';
          redo chkpan;
     end;
     if %entries = ''
     then do;
          panel reread from entries  'Please Enter Number of Files';
          redo chkpan;
     end;
     if %fulltyp = '' | (%fulltyp ¬= 'D' & %fulltyp ¬='E')
     then do;
          panel reread from fulltyp  'Please ''D'' or ''E''';
          redo chkpan;
     end;

     if %fulltyp = 'D' then %nodelete='';
     if %fulltyp = 'E' then %empty 'EMPTY';


  end;

  if %entries > 255 then
  do ;
     signal error 3,'Entries=%entries:- set to 255' ;
     %entries=255;
  end;
  if %system = 'MVT' | %system = 'MFT' | %system = 'VS1'
   | %system = 'VS2'  then
  do;
     %option = ',ENTRIES=%entries';
     if %cvol ¬= '' then
     do;
        %ddcvol = 'ddcvol reads cvoldsid';
        dcl cvoldsid ds noname vol=%cvol unit=%unit;
        %option='%option,CVOL=%unit=%cvol';
     end;
     IF %NODELETE = '' THEN %OPTION = '%OPTION,DELETE';
     IF %EMPTY   ¬= '' THEN %OPTION = '%OPTION,EMPTY';
     IF %LIST(1) ¬= '' THEN %E1 = ' BLDG INDEX=%LIST(1)%OPTION';
     IF %LIST(2) ¬= '' THEN %E2 = ' BLDG INDEX=%LIST(2)%OPTION';
     IF %LIST(3) ¬= '' THEN %E3 = ' BLDG INDEX=%LIST(3)%OPTION';
     IF %LIST(4) ¬= '' THEN %E4 = ' BLDG INDEX=%LIST(4)%OPTION';
     IF %LIST(5) ¬= '' THEN %E5 = ' BLDG INDEX=%LIST(5)%OPTION';
     dcl iehprogm prog 30 k 15 secs
     sysprint writes printer
     sysin reads gdginput
     %ddcvol;
  end; /* of non vs2 | vm catalog build */
  /* here we have a vs2 | vm catalog to build, using idcams */
  else
 do ;
     %VSAM = 'YES' ;
     IF %LIMIT ¬= '' & %ENTRIES = '' THEN %OPTION = ' LIMIT(%LIMIT) '; 0 0870000
     IF %LIMIT = '' & %ENTRIES ¬= '' THEN %OPTION = ' LIMIT(%ENTRIES) ';
     IF %CVOL    ¬= '' THEN SIGNAL 1,'CVOL NOT SUPPORTED FOR VSAM' ;
     IF %EMPTY   ¬= '' THEN %OPTION = '%OPTION EMPTY ';
     IF %DELETE  ¬= '' THEN %OPTION = '%OPTION SCRATCH ';
     IF %SCRATCH ¬= '' THEN %OPTION = '%OPTION SCRATCH ';
     IF %OWNER   ¬= '' THEN %OPTION = '%OPTION OWNER(%OWNER) ' ;
     IF %FOR     ¬= '' THEN %OPTION = '%OPTION FOR(%FOR) ' ;
     IF %TO      ¬= '' THEN %OPTION = '%OPTION TO(%TO) ' ;
     IF %CATALOG ¬= '' THEN %CATALOG = 'CATALOG(%CATALOG)' ;
     IF %LIST(1) ¬= '' THEN
        %E1 = ' DEFINE GDG (NAME(%LIST(1)) %OPTION) %CATALOG';
     IF %LIST(2) ¬= '' THEN
        %E2 = ' DEFINE GDG (NAME(%LIST(2)) %OPTION) %CATALOG';
     IF %LIST(3) ¬= '' THEN
        %E3 = ' DEFINE GDG (NAME(%LIST(3)) %OPTION) %CATALOG';
     IF %LIST(4) ¬= '' THEN
        %E4 = ' DEFINE GDG (NAME(%LIST(4)) %OPTION) %CATALOG';
     IF %LIST(5) ¬= '' THEN
        %E5 = ' DEFINE GDG (NAME(%LIST(5)) %OPTION) %CATALOG';
     dcl idcams prog 60k 15 secs
     sysprint writes printer
     sysin reads gdginput;
     dcl sysprint printer nodcb;
  end;
  dcl gdginput * replace;
 %E1
 %E2
 %E3
 %E4
 %E5
EOF;
  IF %VSAM = 'YES' THEN RUN IDCAMS ;
  IF %VSAM = 'NO'  THEN RUN IEHPROGM ;
 END ;
