BUILDGDG:MACRO (
/*********************************************************************/
/*              COMMAND TO BUILD A GENERATION DATA GROUP             */
/*********************************************************************/
/*                                                                   */
         %VSAM=YES,               /* INDICATE TYPE OF SYSTEM CATALOG */
         %SYSOUT=,                /* SYSOUT CLASS                    */
                                  /***********************************/
                                  /* GENERAL KEYWORDS                */
                                  /***********************************/
         %ENTRIES=3,              /* DEFAULT IS 3                    */
         %CVOL=,                  /* NON VSAM CATALOGUE ONLY         */
         %UNIT=SYSDA,             /*                                 */
         %EMPTY,  %NOEMPTY,       /* DEFAULT IS NOEMPTY              */
         %DELETE, %NODELETE,      /* DEFAULT IS DELETE               */
                                  /***********************************/
                                  /* VSAM KEYWORDS                   */
                                  /***********************************/
         %LIMIT=,                 /* DEFAULT IS 3 ENTRIES            */
         %SCRATCH, %NOSCRAT,      /* DEFAULT IS SCRATCH              */
                   %NOSCRATC,     /* DEFAULT IS SCRATCH              */
         %OWNER=,                 /* OWNER-ID                        */
         %TO=,                    /* EXPIRATION DATE YYDDD           */
         %FOR=,                   /* RETENTION PERIOD 0 - 9999       */
         %CATALOG=                /* CATALOG NAME/PASSWORD           */
         ) ;
/*********************************************************************/
 IF %SYSNLIST > 5 THEN STOP 'TOO MANY INDEX ENTRIES - MAXIMUM IS 5';
/*********************************************************************/
 DCL %E1     INIT '';
 DCL %E2     INIT '';
 DCL %E3     INIT '';
 DCL %E4     INIT '';
 DCL %E5     INIT '';
 DCL %OPTION INIT '';
 DCL %DDCVOL INIT '';
/*********************************************************************/
 DCL PRINTER SYSOUT %SYSOUT NODCB ;
 IF %ENTRIES > 255 THEN
 DO ;
    SIGNAL ERROR 3,'ENTRIES=%ENTRIES:- SET TO 255' ;
    %ENTRIES=255;
 END;
 IF %SYSTEM = 'MVT' | %SYSTEM = 'MFT' | %SYSTEM = 'VS1' |
    %SYSTEM = 'VS2' | %VSAM   = 'NO'    THEN
/*********************************************************************/
/*                  NON VSAM SYSTEM CATALOGUE                        */
/*********************************************************************/
 DO;
    IF %ENTRIES ¬= '' THEN %OPTION = ',ENTRIES=%ENTRIES';
    IF %CVOL    ¬= '' THEN
    DO;
       %DDCVOL = 'DDCVOL READS CVOLDSID';
       DCL CVOLDSID DS NONAME VOL=%CVOL UNIT=%UNIT;
       %OPTION = '%OPTION,CVOL=%UNIT=%CVOL';
    END;
    IF %NODELETE = '' THEN %OPTION = '%OPTION,DELETE';
    IF %EMPTY   ¬= '' THEN %OPTION = '%OPTION,EMPTY';
    IF %LIST(1) ¬= '' THEN %E1 = ' BLDG INDEX=%LIST(1)%OPTION';
    IF %LIST(2) ¬= '' THEN %E2 = ' BLDG INDEX=%LIST(2)%OPTION';
    IF %LIST(3) ¬= '' THEN %E3 = ' BLDG INDEX=%LIST(3)%OPTION';
    IF %LIST(4) ¬= '' THEN %E4 = ' BLDG INDEX=%LIST(4)%OPTION';
    IF %LIST(5) ¬= '' THEN %E5 = ' BLDG INDEX=%LIST(5)%OPTION';
    /*****************************************************************/
    /*           DECLARE UTILITY PROGRAM                             */
    /*****************************************************************/
    IF %SYSTEM    = 'OS4' THEN %UTILITY = 'JSGPROGM' ;
                          ELSE %UTILITY = 'IEHPROGM' ;
    DCL %UTILITY PROG      15 SECS
                 SYSPRINT WRITES PRINTER
                 SYSIN    READS  GDGINPUT
                 %DDCVOL;
 END;
/*********************************************************************/
/*                      VSAM SYSTEM CATALOGUE                        */
/*********************************************************************/
 ELSE
 DO ;
    IF %LIMIT   ¬= '' THEN %OPTION = '%OPTION LIMIT(%LIMIT) ';
    IF %LIMIT    = '' & %ENTRIES ¬= ''
                      THEN %OPTION = '%OPTION LIMIT(%ENTRIES) ';
    IF %CVOL    ¬= '' THEN SIGNAL 1,'CVOL NOT SUPPORTED FOR VSAM' ;
    IF %EMPTY   ¬= '' THEN %OPTION = '%OPTION EMPTY ';
    IF %NODELETE = '' & %NOSCRAT = '' & %NOSCRATC = ''
                      THEN %OPTION = '%OPTION SCRATCH ';
    IF %OWNER   ¬= '' THEN %OPTION = '%OPTION OWNER(%OWNER) ' ;
    IF %FOR     ¬= '' THEN %OPTION = '%OPTION FOR(%FOR) ' ;
    IF %TO      ¬= '' THEN %OPTION = '%OPTION TO(%TO) ' ;
    IF %CATALOG ¬= '' THEN %CATALOG = 'CATALOG(%CATALOG)' ;
    IF %LIST(1) ¬= '' THEN
                %E1 = ' DEFINE GDG (NAME(%LIST(1)) %OPTION) %CATALOG';
    IF %LIST(2) ¬= '' THEN
                %E2 = ' DEFINE GDG (NAME(%LIST(2)) %OPTION) %CATALOG';
    IF %LIST(3) ¬= '' THEN
                %E3 = ' DEFINE GDG (NAME(%LIST(3)) %OPTION) %CATALOG';
    IF %LIST(4) ¬= '' THEN
                %E4 = ' DEFINE GDG (NAME(%LIST(4)) %OPTION) %CATALOG';
    IF %LIST(5) ¬= '' THEN
                %E5 = ' DEFINE GDG (NAME(%LIST(5)) %OPTION) %CATALOG';
    /*****************************************************************/
    /*           DECLARE UTILITY PROGRAM                             */
    /*****************************************************************/
    IF %SYSTEM   = 'OS4' THEN %UTILITY = 'KQCAMS' ;
                         ELSE %UTILITY = 'IDCAMS' ;
    DCL %UTILITY PROG      15 SECS
                 SYSPRINT WRITES PRINTER
                 SYSIN    READS  GDGINPUT;
 END;
/*********************************************************************/
 DCL GDGINPUT * REPLACE;
%E1
%E2
%E3
%E4
%E5
EOF;
/*********************************************************************/
 RUN %UTILITY ;
END ;
