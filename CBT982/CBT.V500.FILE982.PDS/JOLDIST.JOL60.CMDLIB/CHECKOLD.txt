CHECKNET:MACRO;
/* USAGE:   CHECKNET NETWORK JUST-FINISHED-JOBNAME;  */
 /* ADDED FOR NETWORKING AND SCHEDULING C.CLARKE MARCH, 1987 */
 DCL %X        INIT '';   /* LOCAL VARIABLE */
 DCL %JOBNAME  INIT '';   /* LOCAL VARIABLE */
 IF %LIST(1)=''
 THEN DO;
    STOP 'CHECKNET COMMAND EXECUTED WITHOUT NETWORK ID';
    EXIT;
 END;

 IF %LIST(2)¬=''          /* ASKED TO NOTIFY A JOB ENDED? */
 THEN DO;
      %JOBNAME='NETJOB=%LIST(2)';
 END;

 DCL %SYSNETID INIT('') EXT;  /* SET IF NETWORK EXECUTING */
 DCL %SYSJOBS1 INIT('') EXT;  /* KEEPS LIST OF NETWORK JOBS SUBMITTED */
 DCL %SYSJOBS2 INIT('') EXT;  /* KEEPS LIST OF NETWORK JOBS SUBMITTED */
 DCL %SYSNTINC INIT('') EXT;  /* KEEPS INCLUDE DATA SET FOR JOBS      */
 DCL %SYSNTMAC INIT('') EXT;  /* KEEPS JOL MACRO DATA SETS FOR JOBS   */

 /*****************************************************************/
 /*****SET UP USERS JOL LIBRARIES *********************************/
 /*****************************************************************/
         %UMAC='%SYSNTMAC';
         %ULIB='%SYSNTINC';
         IF %UMAC¬=' ' THEN %UMAC='%UMAC||';
         ELSE IF %SYSUID ¬= ' ' THEN %UMAC = '%SYSUID.JOL ||';
         IF %ULIB¬=' ' THEN %ULIB='%ULIB||';
         ELSE IF %SYSUID ¬= ' ' THEN %ULIB = '%SYSUID.JOL ||';
         IF %NP='' THEN DCL $$JPRINT PRINTER VBA 150,160;
                   ELSE DCL $$JPRINT DS 'NULLFILE';

 /*****************************************************************/
 /*****SET UP JOL BATCH COMPILE DECLARATIONS AND RUN STATEMENT*****/
 /*****************************************************************/
 DCL TEMPSUB     DS FB 80,800 SYSDA;
 DCL $$JCL       SYSOUT=A PGM=INTRDR FB 80,80 BUFNO=1;
 DCL $$JLOAD     DS %$JOLPREF.JOL%$JOLVERS.LOAD ;
 DCL $$JINCL     DS %$JOLINCL ;
 DCL $$JMACRO    DS %$JOLPREF.JOL%$JOLVERS.CMDLIB ;
 DCL $$JWORK1    DS SYSDA FB 80,800 600 BLKS NORLSE ;
 DCL $$JWORK2    DS SYSDA VB 200,800 600 BLKS NORLSE ;
 DCL $JOLCP%$JOLVERS   PROG
      $$JPRINT   WRITES $$JPRINT
      TEMPNET    WRITES TEMPSUB
      $$JCL      WRITES $$JCL
      $$JLOAD    READS  $$JLOAD
      $$JINCL    READS  %$JOLPREF.JOLNET.STATUS || %ULIB $$JINCL
      $$JMACRO   READS  %UMAC  $$JMACRO
      $$JWORK1   WRITES $$JWORK1
      $$JWORK2   WRITES $$JWORK2
      $$JIN      READS  %$JOLPREF.JOLNET.CODE(%LIST(1))||TEMPSUB;
%SYSLABEL:       RUN $JOLCP%$JOLVERS PARM
      '/%JOBNAME SYSUID=%SYSUID';
 /*   '%NPI,%PO,%LET,%PE,%PM,%PJ,%NP,%NOCAT,%NOCATGDG/%JOBNAME '; */
 /*****************************************************************/
 /*****SET UP JCL BATCH STATEMENTS TO SUBMIT JCL JOB **************/
 /*****************************************************************/
 IF %JCJOB='YES'
  THEN DO;
  DCL DUMMYIN DS DUMMY;
  DCL PRINTFIL  PRINTER NODCB;
  DCL OUTJCL SYSOUT=A PGM=INTRDR FB 80,80 BUFNO=1;
   IEBGENER: DCL IEBGENER PROG
             SYSUT1 READS CARDS
             SYSUT2 WRITES OUTJCL
             SYSIN  READS DUMMYIN
             SYSPRINT WRITES PRINTFIL;
     RUN IEBGENER ;
 END ;
 END ; /* END OF MACRO */
