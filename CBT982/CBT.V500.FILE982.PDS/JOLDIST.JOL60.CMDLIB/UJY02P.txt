 /*****************************************************************/
 /**          Copy Scheduled Jobs          Created MAY, 1987     **/
 /**          -------------------          BY C. CLARKE          **/
 /**                                                             **/
 /** This Macro copies scheduled jobs to a work data set for     **/
 /**    Jol to Submit.                                           **/
 /**                                                             **/
 /** The Jobs are held in members of the Schedule Dats Set in    **/
 /**    various members.                                         **/
 /**                                                             **/
 /*****************************************************************/
Schedule:MACRO ;
 /*****************************************************************/
 /******************SYMBOLIC DECLARES *****************************/
 /*****************************************************************/

   /* VARIABLES BELOW USED IN LOOPS FOR SUBSCRIPTS ETC */

 DCL %INDEX                        INIT('1');
 DCL %holiday                      ext init('NO');
 DCL %lastday                      ext init(''); /* # days in month */
 DCL %work,%work2                  INIT('');
 DCL %wkmonth                      INIT('');   /* JAN, FEB etc */
 DCL %wkday                        INIT('');   /* MON, TUE etc */

 DCL %SYSJOBS1 INIT('') ;     /* KEEPS LIST OF JOBS SUBMITTED */
 DCL %SYSJOBS2 INIT('') ;     /* KEEPS LIST OF JOBS SUBMITTED */
 DCL %SYSJOBS3 INIT('') ;     /* KEEPS LIST OF JOBS SUBMITTED */
 DCL %SYSJOBS4 INIT('') ;     /* KEEPS LIST OF JOBS SUBMITTED */
 DCL %SYSJOBS5 INIT('') ;     /* KEEPS LIST OF JOBS SUBMITTED */
 DCL %SYSNTINC INIT('') EXT;  /* KEEPS INCLUDE DATA SET FOR JOBS      */
 DCL %SYSNTMAC INIT('') EXT;  /* KEEPS JOL MACRO DATA SETS FOR JOBS   */
 /*****************************************************************/
 /*****CHECK THAT THERE IS VALID INPUT TO THIS MACRO***************/
 /*****************************************************************/
    IF %LIST(2)¬= ''
    THEN STOP 'Invalid Parameter on SCHEDULE Macro';

    alloc f(schedwrk) new catlg fb 80,800 10 tracks sysda
          da %sysuid.jol.tempschd;
    open  f(schedwrk) output;
    IF %lastcc¬=0
    then do;
         STOP 'SCHEDULE Allocate Work Data Set Failed - Exiting';
         exit 16;
    end;

/* Now call the SCHEDSUB macro to copy data for Jol to the
   work data set. We do this repeatedly until all members
   have been done.  SCHEDSUB only submits a job once, no
   matter how many times it is selected.

   NOTE WELL: If a member containing jobs is not present,
              processing continues.

   The logic for Selection is as follows:-

   1. If specific members such as JUN03 are present, they will
      be submitted, even on Holidays. Presumably, the Scheduler
      has set up appropriate jobs for that day.

   2. If it is a Weekend, then those jobs are submitted; it is
      already a holiday.

   3. Otherwise, a check is made to see if it is a Holiday.
      If it is a Holiday, then member HOLIDAY is used,
      otherwise WORKDAY plus any other daily members.
  */

    %wkmonth=substr(month,1,3);    /* Get the first 3 characters */

    %wkday  =substr(day,1,3);      /* Get the first 3 characters */

    schedsub %wkmonth%dayno;       /* Submit jobs for today      */

    if %day='SATURDAY' | %day='SUNDAY'
    then do;
         schedsub %day;            /* Submit jobs for WEEKEND    */
         EXIT 0;                   /* ================== <       */
    end;

 /* Now test if it is a HOLIDAY, and submit the other jobs       */

    holidayt;                      /* Set %HOLIDAY=YES or NO     */
    holidaya;                      /* Builds %LIST items for
                                      SPECIAL Member             */
    if %holiday='YES'
    then do;
         write 'Today is a HOLIDAY';
         schedsub HOLIDAY;         /* Submit jobs for HOLIDAY    */
         EXIT 0;                   /* ================== <       */
    end;

    schedsub WORKDAY;              /* Submit jobs for WORKDAY    */
    schedsub %day;                 /* Submit DAILY jobs          */

  /* Calculate the WEEK number                                   */
    dcl %weekno  init (0);
    %weekno=substr(sysdate,3);     /* JULIAN day                 */
    %weekno=%weekno/7;
    %weekno=%weekno+1;             /* Weeks start at 1, not zero */
    %weekno='WK'||%weekno;
    schedsub %wkday%weekno;        /* Submit MONWK01, MONWK02    */

    schedsub SPECIAL;              /* Submit jobs for SPECIAL    */

    EXIT QUIT;                     /* Stop Jol now, it has created
                                      the data set for the next
                                      step in the SCHEDULE CLIST */
 end ; /* END OF MACRO */
 exit quit;
