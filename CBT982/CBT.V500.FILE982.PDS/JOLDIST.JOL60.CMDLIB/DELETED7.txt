 VALIDATE: MACRO ;
 /*************************************************************/
 /* THIS MACRO IS USED TO VALIDATE INPUT FROM PANELS         **/
 /* OR ANY OTHER SOURCE.                                     **/
 /*************************************************************/
 /* CHECK FOR INPUT TO MACRO .                               **/
 /* MUST BE IN THE FORM OF. VALIDATE INPUT TEST1 TEST2       **/
 /* INPUT=SYMBOLIC BEING VALIDATED.                          **/
 /* TEST1=START OF TEST RANGE .APPEND A / FOR SUBSTRING.     **/
 /* TEST2=END OF TEST RANGE   .(OPTIONAL)                    **/
 /*************************************************************/
 /*  AN INCORRECT INPUT DISPLAYS MESSAGE PANEL AND ASKS      **/
 /*  FOR CORRECT INPUT.                                      **/
 /*************************************************************/
 /* SET UP SYMBOLICS FOR LENGTH AND TYPE OF INPUT            **/
 /*************************************************************/
 %IN2=''; %IN3=''; %IN4=''; %IN='';
  %LST1='SET LST1=%%%LIST(1)';
  %LST1;
  %A=''; %B=''; %C=''; %D='';
  %LST2='%LIST(2)';
  %LST3='';
  IF %LIST(3) THEN %LST3='%LIST(3)';
  %L2=LENGTH(LST2);
  %L3=LENGTH(LST3);
  /*****************************/
  %E=INDEX(LST2,'/');
  IF %E¬=0 & %L2>%E THEN DO;
  %E=%E+1;
  %LST3=SUBSTR(LST2,%E);
  %L3=LENGTH(LST3);
  %L3=%L3-1;
  %E=%E-1;
  %LST2=SUBSTR(LST2,1,%E);
  %L2=LENGTH(LST2);
  %L2=%L2-1;
  END;
  %A=LENGTH(LST2);
  %B=SUBSTR(LST2,%A,1);
  IF %B='/' THEN DO;
  %LST2A='/';
  %A=%A-1;
  %LST2=SUBSTR(LST2,1,%A);
  END;
  %C=LENGTH(LST3);
  %D=SUBSTR(LST3,%C,1);
  IF %D='/' THEN DO;
  %LST3A='/';
  %C=%C-1;
  %LST3=SUBSTR(LST3,1,%C);
  END;
  IF %D¬='/' THEN DO;
  %L2=%L2-1;
  END;
 /*************************************************************/
 /**      CHECK FOR INPUT TO MACRO                *************/
 /*************************************************************/
 IF %LST1 ='' THEN DO;
  PANEL('**** NO INPUT TO VALIDATE MACRO ***')
       ('*** SB. VALIDATE SYMBOLIC TEST1 TEST2(OPT)***');
  END;
 /*************************************************************/
 /**        CHECK FOR MACRO TEST         **********************/
 /*************************************************************/
 IF %LST2 ='' THEN DO;
  PANEL ('****NO TEST FOR VALIDATE****')
        ('****SB VALIDATE SYMBOLIC TEST1 TEST2(OPT)***');
  END;
 /*************************************************************/
  %L1=LENGTH(LST1);
 /*************************************************************/
 /*************************************************************/
 /* TEST FOR SIMILAR INPUT TO TEST1 ONLY                     **/
 /*************************************************************/
   IF %LST2A='' & %LST3='' THEN DO;
 /*************************************************************/
 IF %LST1¬=%LST2 THEN DO;
  LABEL FIRST;
  PANEL //('*************************************************')
         /('****INPUT HAS FAILED VALIDATION TEST FOR %LST1   ')
         /('****INPUT MUST BE           ==  %LST2 ==         ')
         /('****ENTER NEW INPUT         ==>',IN,%L1,'%LST2');
  %LST1=%IN;
         IF %IN ¬= %LST2 THEN REDO FIRST;
  END;
  END;
 /*************************************************************/
  IF %LST2A='/' & %LST3A='' THEN DO;
  %LST1=SUBSTR(LST1,1,%L2);
  IF %LST1¬=%LST2 THEN DO;
  LABEL SECOND;
  PANEL //('*************************************************')
         /('****INPUT HAS FAILED VALIDATION TEST FOR %LST1   ')
         /('****INPUT MUST START WITH             == %LST2== ')
         /('****ENTER NEW INPUT                  ==>',IN2,%L1'%LST2');
         %TST1=SUBSTR(IN2,1,%L2);
         %LST1=%IN2;
         IF %TST1¬='%LST2' THEN REDO SECOND;
  END;
  END;
 /*************************************************************/
 /*************************************************************/
 /* TEST FOR RANGE IN FIRST FEW NUMBERS                      **/
 /*************************************************************/
  IF %LST2A='/' & %LST3A='/' THEN DO;
  %S=SUBSTR(LST1,1,%L2);
  %R=SUBSTR(LST1,1,%L3);
  IF %R > '%LST3' | %S < '%LST2'  THEN DO;
  LABEL THIRD;
  PANEL //('*************************************************')
         /('****  INPUT %LST1 IS OUT OF RANGE            ')
         /('****FIRST CHARACTERS MUST BE IN RANGE        ')
         /('****       %LST2 TO %LST3                    ')
         /('**** ENTER NEW INPUT      ==>',IN3,%L1);
         %TST2=SUBSTR(IN3,1,%L2);
         %TST3=SUBSTR(IN3,1,%L3);
         %LST1=%IN3;
  IF %TST3 > %LST3 | %TST2 < %LST2  THEN REDO THIRD;
         SET %LIST(1)=%IN3;
  END;
  END;
 /*************************************************************/
 /* TEST FOR INPUT WITHIN RANGE OF TEST1 AND TEST2           **/
 /*************************************************************/
  IF %LST2¬='' & %LST2A='' & %LST3¬='' & %LST3A='' THEN DO;
  IF %LST1 < %LST2 | %LST1 > %LST3 THEN DO;
  LABEL FOURTH;
  PANEL //('*************************************************')
         /('****  INPUT  %LST1 IS OUT OF RANGE           ')
         /('****  INPUT MUST BE IN THE RANGE             ')
         /('****    %LST2 TO %LST3                       ')
         /('****ENTER NEW INPUT      ==>',IN4,%L1);
         %LST1=%IN4;
  IF %IN4  < %LST2 | %IN4  > %LST3 THEN REDO FOURTH;
        SET %LIST(1) = %IN4;
  END;
  END;
  END;
