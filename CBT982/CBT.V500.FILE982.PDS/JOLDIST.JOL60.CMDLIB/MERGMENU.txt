LIST: MACRO (
/*********************************************************************/
/*                 JOL  LIST  COMMAND                                */
/*                                                                   */
/*        USES IDCAMS OR IEBPTPCH TO LIST OR DUMP DATASETS           */
/*        DEVELOPED BY ABS SOFTWARE SERVICES SECTION - 25/06/81      */
/*********************************************************************/
      %SYSOUT=X,             /* CLASS FOR LIST OUTPUT                */
      %SYSPRINT=X,           /* CLASS FOR UTILITY MESSAGES           */
      %OUTLIM=,              /* FOR LIST OUTPUT > DEFAULT LIMIT      */
      RECS, RECORDS,         /* NUMBER OF RECORDS TO BE PRINTED      */
      %DIR,%DIRECTOR,        /* USED IF PDS DIRECTORY TO BE DUMPED   */
      %PDS,                  /* DEFAULT IS SEQUENTIAL DATA SET       */
      %CHAR, %HEX, %DUMP,    /* DEFAULT IS IDCAMS DUMP IN HEX & CHAR */
      %STARTAT=,             /* FIRST RECORD TO BE PROCESSED         */
      %EVERY=,               /* NO OF RECORDS TO SKIP BETWEEN PRINTS */
      %RECSIZE=, %LRECL=,    /* RECORD SIZE FOR LISTING CHAR RECORDS */
      %PRCNTL=,              /* TYPE OF PRINTER CONTROL CHARACTERS   */
      ) ;
/*********************************************************************/
/*                 DECLARE SYMBOLICS FOR THIS MACRO                  */
/*********************************************************************/
DCL %INPFILE INIT '' ;
DCL %STOPAFT INIT '' ;
DCL %PROGM   INIT '' ;
DCL %INRECFM INIT '' ;
DCL %OPTION1 INIT '' ;
DCL %OPTION2 INIT '' ;
DCL %OPTION3 INIT '' ;
DCL %F1      INIT '' ;
/*********************************************************************/
/*                 CHECK FOR LOGICAL PARAMETERS                      */
/*********************************************************************/

/* If User codes a parameter, then execute the comand immediately,
   otherwise we will show the user a menu */

        if %list(1) ¬= '' then %f1='%list(1)';

        if %f1 ¬= '' & %buildjob='YES'
        then do;
            BLD04P                  '   LIST   %COMMTAIL;';
            exit;
        end;

loop:


/* Check if no parameters, and if so, display a screen for the User */

        if %list(1) = ''   & %panels ¬= 'OFF'
        then do;
            list0001='';
            form    ='';
            class   ='';
            free    ='Y';
            dest    ='';
            route   ='';
            copies  ='';
            outlim  ='';

     panel box   (hi ctr 'Print Command')
                 (hi ctr '-------------')
           ////
           ('      File to Print ===> ',f1,
                       44,'%f1')
         / ('      Free File after Printing  ==> ',free,
                       1,'%free')
           ('      Number of Copies          ==> ',copies,
                       3,'%copies')
           ('      Sysout Class              ==> ',class,
                       1,'%class')
           ('      Form Class                ==> ',form,
                       8,'%form')
           ('      Destination               ==> ',dest,
                       8,'%dest')
           ('      Route                     ==> ',route,
                       8,'%route')
           ('      Char Format (Y ? N)       ==> ',char,
                       1,'%char')
           ('      Hex Format (Y ? N)        ==> ',hex,
                       1,'%hex')
           ('      Number of Records         ==> ',recs,
                       5,'%recs')
           ('      Outlim                    ==> ',outlim,
                       6,'%outlim')
   /*    / ('      Format Listing (Y or N)   ==> ',format,
                       1,'%format')
   */
helpline
('F3 Exits, F4 Select from Jol Data Base, F5 Select from Catalog.');

checkf1:
        if Syspfk=3 then exit;

        if %syspfk=4
        then do;
            dbfshow  all 'File to Print';
            f1='%showsel';
        end;

        if %syspfk=5
        then do;
            showdsn  all 'File to Print';
            f1='%showsel';
        end;
        if f1=''
        then do;
            panel reread from f1 'You must supply a file name';
            goto checkf1;
        end;

        if syspfk=3 then exit;   /* Back to Caller */

gotfile:

       list0001='%f1';
   end;

IF %OUTLIM ¬= '' THEN %OUTLIM = 'OUTLIM %OUTLIM' ;
IF %CHAR   ¬= '' & %HEX ¬= '' THEN
                 DO;
                 SIGNAL 1, 'CHAR & HEX SPECIFIED - DUMP USED';
                 %DUMP = 'DUMP' ;
                 END;
IF %HEX   = '' & %CHAR = '' & %DUMP = '' THEN %DUMP = 'DUMP' ;
IF %DUMP ¬= ''   THEN
                 DO;
                 %HEX  = '' ;
                 %CHAR = '' ;
                 END;
IF %RECS ¬= 0 & %RECORDS ¬= 0 THEN
                 DO;
                 SIGNAL 2,
                 'RECORDS AND RECS BOTH SPECIFIED - RECS IGNORED' ;
                 %RECS = '' ;
                 END;
ELSE
                 DO;
                 IF %RECS ¬= 0 | %RECORDS ¬= 0 THEN
                    %STOPAFT = '%LIST(%RECS+%RECORDS-1)' ;
                 END;
IF %RECSIZE ¬= '' & %LRECL ¬= '' THEN
                 DO;
                 SIGNAL 2,
                 'RECSIZE AND LRECL BOTH SPECIFIED - LRECL IGNORED' ;
                 %LRECL = '' ;
                 END;
IF %LRECL    ¬= '' THEN %RECSIZE = %LRECL ;
IF %DIRECTOR ¬= '' THEN %DIR = 'DIR' ;
/*********************************************************************/
/*              MAKE DECISION REGARDING THE UTILITY TO BE USED       */
/*********************************************************************/
IF %EVERY   = '' &                     /*                            */
   %PDS     = '' &                     /*  THESE OPTIONS ARE NOT     */
   %DIR     = '' &                     /*  CATERED FOR IN IDCAMS     */
   %PRCNTL  = '' &                     /*                            */
   %RECSIZE = '' THEN                  /*                            */
     %PROGM = 'IDCAMS' ;
ELSE %PROGM = 'IEBPTPCH' ;
/*********************************************************************/
/*                   SET UP LIST OPTIONS FOR IEBPTPCH                */
/*********************************************************************/
IF %PROGM = 'IEBPTPCH' THEN
DO;
   %OPTION1  = ' PRINT' ;
   IF %DIR ¬= '' THEN %INRECFM = 'RECFM U 256,256' ;
   ELSE %INRECFM = 'NODEF' ;
   IF %PDS  ¬= '' & %DIR = ''
                     THEN %OPTION1  = '%OPTION1 TYPORG=PO' ;
                     ELSE %OPTION1  = '%OPTION1 TYPORG=PS' ;
   IF %PRCNTL  ¬= '' THEN %OPTION1  = '%OPTION1,PREFORM=%PRCNTL' ;
   IF %STOPAFT ¬= '' THEN %OPTION1  = '%OPTION1,STOPAFT=%STOPAFT';
   IF %EVERY   ¬= '' THEN %OPTION1  = '%OPTION1,SKIP=%EVERY';
   IF %STARTAT ¬= '' THEN
   DO;
      %STARTAT = %STARTAT - 1 ;
      %OPTION1 = '%OPTION1,STRTAFT=%STARTAT' ;
   END;
   IF %DUMP    ¬= '' THEN
   DO;
      SIGNAL 2,
      'HEX AND CHAR NOT POSSIBLE FOR OPTIONS SPECIFIED - HEX USED' ;
      %HEX = 'HEX' ;
   END;
   IF %HEX     ¬= '' THEN %OPTION1 = '%OPTION1,TOTCONV=XE' ;
   IF %HEX ¬= '' & %RECSIZE ¬= '' THEN
   DO;
      IF %RECSIZE > 66 THEN
      DO;
         SIGNAL 2, 'RECSIZE TOO BIG FOR HEX PRINT - RECSIZE IGNORED' ;
         %RECSIZE = '' ;
      END;
   END;
   IF %RECSIZE ¬= ''  THEN
   DO;
      %OPTION1 = '%OPTION1,MAXFLDS=1' ;
      IF %HEX ¬= '' THEN %OPTION2 = ' RECORD FIELD=(%RECSIZE,,XE,)' ;
      ELSE %OPTION2 = ' RECORD FIELD=(%RECSIZE,,,)' ;
   END;
END;
/*********************************************************************/
/*                   SET UP LIST OPTIONS FOR IDCAMS                  */
/*********************************************************************/
IF %PROGM = 'IDCAMS' THEN
DO;
   %OPTION1 = ' PRINT INFILE(SYSUT1) -' ;
   %OPTION2 = ' OUTFILE(SYSUT2)      -' ;
   %OPTION3 = ' %CHAR %HEX %DUMP'       ;
   IF %STARTAT ¬= '' THEN
   DO;
      %STARTAT = %STARTAT - 1;
      %OPTION3 = '%OPTION3 SKIP(%STARTAT)' ;
   END;
   IF %STOPAFT ¬= '' THEN %OPTION3 = '%OPTION3 COUNT(%STOPAFT)' ;
END;
/*********************************************************************/
/*                 DECLARE  LIST  DATA SETS                          */
/*********************************************************************/
DCL     LISTCNTL   *  REPLACE ;
 %OPTION1
 %OPTION2
 %OPTION3
EOF;
IF %DIR = '' THEN %INPFILE = '%LIST(1)' ;
ELSE
DO;
   %$I = INDEX(LIST0001,'.') ;
   IF %$I ¬= 0 THEN                            /* DSN FOUND          */
      DCL  INPUT      DS
                      DSN      %LIST(1)
                      %INRECFM ;
   ELSE                                        /* DSID FOUND - MUST  */
      DCL  INPUT      DS                       /* HAVE A DSN OR      */
                      LIKE %LIST(1)            /* THIS WILL NOT WORK */
                      %INRECFM ;
   %INPFILE = 'INPUT' ;
END;
DCL     OUTPUT     PRINTER  %SYSOUT
                   %OUTLIM
                   RECFM    FBA 133,1330 ;
DCL     PRINTER    PRINTER  %SYSPRINT
                   NODCB ;
/*********************************************************************/
/*                   DECLARE IEBPTPCH PROGRAM                        */
/*********************************************************************/
LISTSTEP:  DCL     %PROGM   PROG
                   SYSIN    READS   LISTCNTL
                   SYSUT1   READS   %INPFILE
                   SYSUT2   WRITES  OUTPUT
                   SYSPRINT WRITES  PRINTER ;
/*********************************************************************/
/*                 RUN   LIST   PROGRAM                              */
/*********************************************************************/
RUN     LISTSTEP ;
END;
