DELGDG : MACRO (
/*********************************************************************/
/*                                                                   */
/*          COMMAND TO DELETE A GENERATION DATASET BASE              */
/*          AND TO OPTIONALLY DELETE ALL GDG GENERATIONS.            */
/*          ABS SOFTWARE SERVICES SECTION - 15/07/82                 */
/*                                                                   */
/*********************************************************************/
    %ENTRY,             /* ENTRYNAME IS POSITIONAL                   */
    %PASSWORD=,         /* ENTRY PASSWORD                            */
    %GDGBASE,           /* DEFAULT DELETION IS OF BASE ENTRY ONLY    */
                        /*         AFTER ALL GDG MEMBERS ARE REMOVED */
    %ALL, %GENERATI,    /* ALL GENERATIONS MUST BE SPECIFIED TO      */
                        /*      DELETE ALL GENERATIONS AND BASE GDG  */
    %PURGE, %NOPURGE,   /* NOPURGE IS THE DEFAULT. PURGE ALLOWS AN   */
                        /*      UNEXPIRED GDG TO BE DELETED          */
    %SYSOUT=            /* OUTPUT FROM AMS COMMAND EXECUTED          */
          ) ;
/*********************************************************************/
DCL %COMMAND1 INIT '' ;
DCL %COMMAND2 INIT '' ;
DCL %FORCE    INIT '' ;
IF %LIST(1) = '' THEN
   STOP 'NO GDG ENTRY NAME SPECIFIED FOR DELETE GDG' ;
IF %ALL ¬= '' & %GENERATI ¬= '' THEN
   DO;
   SIGNAL 2
   'WARNING - ALL GENERATIONS WILL BE UNCATALOGUED BUT NOT SCRATCHED';
   %FORCE = 'FORCE' ;
   END;
IF %PURGE ¬= '' THEN
   DO;
   SIGNAL 2 'WARNING - UNEXPIRED GDG DATASETS WILL BE DELETED' ;
   %PURGE = 'PURGE' ;
   END;
IF %PASSWORD ¬= '' THEN %PASSWORD = '/%PASSWORD' ;
%COMMAND1 = ' DELETE (%LIST(1)%PASSWORD) -' ;
%COMMAND2 = '         GDG %PURGE %FORCE' ;
/*********************************************************************/
/*          DECLARE DATASETS FOR IDCAMS / KQCAMS                     */
/*********************************************************************/
DCL     PRINT     SYSOUT %SYSOUT ;
DCL     COMMANDS  *  REPLACE ;
 %COMMAND1
 %COMMAND2
EOF;
/*********************************************************************/
/*          DECLARE  VSAM AMS  PROGRAM                               */
/*********************************************************************/
DCL %PROGRAM INIT 'IDCAMS' ;
IF %SYSTEM = 'OS4' THEN %PROGRAM = 'KQCAMS' ;
DELGDG :
DCL   %PROGRAM    PROGRAM
                  SYSPRINT  WRITES PRINT
                  SYSIN     READS  COMMANDS ;
/*********************************************************************/
/*          RUN  VSAM AMS  PROGRAM                                   */
/*********************************************************************/
RUN DELGDG  ;
END;
