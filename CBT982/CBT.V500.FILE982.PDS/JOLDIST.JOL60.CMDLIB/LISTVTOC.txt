 /*****************************************************************/
 /**       Build %LIST items for Holidays   Created May, 1987    **/
 /**       ------------------------------   BY C. CLARKE         **/
 /**                                                             **/
 /** This macro builds an list of %LIST items that can be        **/
 /**    used if simple IF statements to determine if today,      **/
 /**    or the day+1, +2 or -1 is (or was) a Holiday.            **/
 /**                                                             **/
 /** This information is used in member SPECIAL of the sample    **/
 /**    provided with Jol.                                       **/
 /**                                                             **/
 /*****************************************************************/
holidaya:MACRO ;
end;                 /* NOTE ========== <                         */

 /*****************************************************************/
 /******************SYMBOLIC DECLARES *****************************/
 /*****************************************************************/

   /* VARIABLES BELOW USED IN LOOPS FOR SUBSCRIPTS ETC */

 DCL %hind,%hlen                   INIT('1');
 DCL %holidaya                     ext init('');
 DCL %hwork,%hwork2                INIT('');
 DCL %hmonth                       INIT('');
 /*****************************************************************/
 /*****CHECK THAT THERE IS VALID INPUT TO THIS MACRO***************/
 /*****************************************************************/
    IF %LIST(1)¬= ''
    THEN STOP 'Invalid Parameter on HOLIDAY Build Array Macro';
    %hind=1;                     /* Incremented, used in SUBSTR   */
    %hlen=length(holidaya);      /* Stop Length                   */
label looph;
    if %hind<%hlen
    then do;
        %hmonth=substr(holidaya,%hind,2);  /* Get two digits     */
        %hwork='SET LIST00%hmonth=HOL';
        %hwork;
        %hind=%hind+2;
        redo looph;
    end;
/* Now set other %LISTs to 'WORK' meaning WORKDAYS */
    %hind=1;                     /* Incremented, used in SUBSTR   */
label loopw;
    if %hind<50                  /* Extra to make testing easier  */
    then do;
        if %list(%hind)¬='HOL'   /* Already set to a Holiday ?    */
        then do; /* No                            */
            if %hind<10 then %hind='0%hind';
            %hwork='SET LIST00%hind=WORK';
            %hwork;
        end;
        %hind=%hind+1;
        redo loopw;
    end;
