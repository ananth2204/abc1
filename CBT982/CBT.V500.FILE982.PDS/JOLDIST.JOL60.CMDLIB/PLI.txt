LOADGO: MACRO ( /* JOL LOADGO MACRO  */
  %AUTOLIB=,
  %NOPANEL,                   /* IF PANELS NOT REQUIRED              */
  %COMP,
  %CONC=,
  %DYNAMIC,%DYNAM,%NODYNAM,
  %ENTRY=,                   /* ENTRY POINT                          */
  %INCLUDE=,                 /* AN INCLUDE LIBRARY MAY BE SPECIFIED  */
  %LET, %NOLET,              /* LET OPTION                           */
  %LIKE=,                    /* TO DEFINE A DCL PROG 'LIKE' DECLARE  */
  %LIST, %NOLIST,            /* LIST OPTION                          */
  %MAP, %NOMAP,              /* MAP OPTION                           */
  %MEMBERS=,                 /* INCLUDE LIBRARY MEMBERS              */
  %NAME=,                    /* AND A NAME GENERATOR                 */
  %NCAL,%NONCAL,%CALL,       /* NO-CALL OPTION                       */
  /* DEFINE POSSIBLE STANDARD AUTO CALL LIBRARIES */
  %PLILIB,%PLICMIX,%PLIBASE,%FORTLIB,%COBLIB,
  %PRINT,%NOPRINT,           /* LIST OPTION                          */
  %RES,%NORES,               /*                                      */
  %SYSOUT=,                  /* DEFAULT TO X FOR TSO OR A FOR BATCH  */
  %PARM=) ;
IF %SYSOUT = '' THEN DO;
      IF %TSOCLASS = '' THEN %SYSOUT = A;
      ELSE %SYSOUT = %TSOCLASS;
END;

IF %PARM = '' THEN DO ;
      IF %DYNAMIC ¬= '' THEN %PARM = '%PARM,DYNAMIC';
      IF %LIST    ¬= '' THEN %PARM = '%PARM,LIST' ;
      IF %MAP     ¬= '' THEN %PARM = '%PARM,MAP' ;
      IF %LET     ¬= '' THEN %PARM = '%PARM,LET' ;
      IF %NCAL    ¬= '' THEN %PARM = '%PARM,NCAL' ;
END;
  DCL %INP INIT '';
  IF %LIST(1) ¬= '' THEN %INP = '%LIST(1)';
  IF %INP = 'OBJ' THEN %INP = '&&OBJ';
DCL %@@@X INIT '';
DCL %@@@L INIT '';
IF %INP ¬= '' THEN DO;
   %@@@X = INDEX(INP,'(');
   IF %@@@X ¬= 0 THEN DO;
      %NAME = SUBSTR(INP,%@@@X);
      %NAME = SUBSTR(NAME,2);
      %@@@L = LENGTH(NAME);
      %@@@L = %@@@L - 1;
      %NAME = SUBSTR(NAME,1,%@@@L);
      %@@@X = '';
   END;
END;

DCL %AUTO INIT 1;
IF %COBLIB='' & %PLIBASE='' & %PLILIB='' & %FORTLIB='' & %PLIMIX=''
    THEN %AUTO = '';
IF %NOPANEL = ''& %AUTO = '' THEN
PANEL     ('               JOL LOADER COMMAND')
  ////('WHAT AUTOCALL LIBRARY(S) DO YOU WISH TO USE?')
    //('PLIBASE *** Y/N ***',PLIBASE,1,'N')
      ('COBLIB  *** Y/N ***',COBLIB,1,'N')
      ('FORTLIB *** Y/N ***',FORTLIB,1,'N')
      ('PLILIB  *** Y/N ***',PLILIB,1,'N')
      ('PLIMIX  *** Y/N ***',PLIMIX,1,'N')
/////('*********************************************************');
IF %NOPANEL = '' THEN
PANEL ('                    JOL LOADER COMMAND')
      ('         ANY HIGHLIGHTED FIELD CAN BE CHANGED')
      ('        ALL DATA SETS MUST BE FULLY QUALIFIED')
      ('IF MULTIPLE DATA SETS, DSIDS OR A MIXTURE OF BOTH ARE USED')
      ('     ******* USE || BETWEEN THEM *******')
//('INPUT DATA SET ==========================>',INP,100,'%INP')
('PROGRAM NAME ============================>',NAME,8,'%NAME')
('ENTRY POINT =============================>',ENTRY,8,'%ENTRY')
('OTHER AUTOCALL LIBRARY(S) ===============>',LIB,100,'%LIB')
('SYSOUT CLASS ============================>',SYSOUT,1,'%SYSOUT')
('INCLUDE LIBRARY(S) ======================>',INCLUDE,100,'%INCLUDE')
('INCLUDE MODULES '','' BETWEEN MULTIPLES =>',MEMBERS,99,'%MEMBERS')
('LINKAGE PARAMETERS FOR THIS RUN =========>',PARM,100,'%PARM');
/********************************************************************/
/* NOW FIX UP ANY AUTO CALL LIBRARIES. AS ONE CAN MIX ALL LANGUAGES */
/* WE WILL ALLOW ALL THE AUTOCALL LIBRARIES TO BE SPECIFIED. THEN   */
/* WE WILL CONCATENATE THEM IF REQUIRED.                            */
/********************************************************************/
IF %INP = '&&OBJ' THEN %INP = 'OBJ';
IF %PL1LIB¬='' & %PL1LIB¬='N' THEN %AUTOLIB='%AUTOLIB SYS1.PL1LIB||';
IF %PLILIB¬='' & %PLILIB¬='N' THEN %AUTOLIB='%AUTOLIB SYS1.PLILIB||' ;
IF %PLIBASE¬='' & %PLIBASE¬='N' THEN %AUTOLIB='%AUTOLIB SYS1.PLIBASE||';
IF %PLICMIX¬='' & %PLIMIX¬='N' THEN %AUTOLIB='%AUTOLIB SYS1.PLICMIX||';
IF %FORTLIB¬='' & %FORTLIB¬='N' THEN %AUTOLIB='%AUTOLIB SYS1.FORTLIB||';
IF %COBLIB¬='' & %COBLIB¬='N' THEN %AUTOLIB='%AUTOLIB SYS1.COBLIB||' ;
IF %LIB¬='' THEN %AUTOLIB='%AUTOLIB %LIB||' ;
IF %AUTOLIB ¬= ''
THEN DO;
       %AUTOLIB = 'SYSLIB READS %AUTOLIB' ;
       %ZZ=LEN(AUTOLIB); %ZY=%ZY-2; %ZX=SUBSTR(AUTOLIB,%ZY);
       IF %ZX='||' THEN %AUTOLIB=SUBSTR(AUTOLIB,1,%ZY);
       WRITE 'AUTOLIB=%AUTOLIB';
END;
/********************************************************************/
/* OK NOW FIX THE INPUTS TO THE LOADER, AND THERE COULD BE          */
/* QUITE A FEW OF THEM AND SO THIS WILL TAKE SOME ANALYSING         */
/* FIRST THE PRIMARY INPUTS.                                        */
/********************************************************************/
IF %INP = '' THEN /* ANY INPUT */
DO ;
      STOP 'NO INPUTS SPECIFIED FOR LOADGO' ;
END;
/********************************************************************/
/* WE MAY HAVE TO GENERATE SOME CONTROL PARAMETERS FOR INPUT.       */
/********************************************************************/
DCL %LKEDENT INIT '';
DCL %LKEDINCL INIT '';
DCL %INCLCARD INIT '';
DCL %LKEDNAME INIT '';
DCL %LKEDIN INIT '';
IF %INP ¬= '' THEN DO;
   %LKEDIN = '%INP';
   %CONC = '||';
   END;
ELSE %CONC = '';
IF %MEMBERS ¬= '' THEN
DO ;
   IF %INCLUDE ¬= '' THEN %LKEDINCL = ' INCLIB READS %INCLUDE' ;
   IF %INCLUDE = '' THEN          /* RELINK OLD MEMBER FROM LIBRARY */
      %LKEDINCL = 'INCLIB READS %LOAD' ;

   %INCLCARD = ' INCLUDE INCLIB(%MEMBERS)' ;
END ;
/********************************************************************/
/* NOW WE MAY STILL HAVE SOME CONTROL PARMS TO GENERATE             */
/********************************************************************/
IF %ENTRY ¬= '' THEN %LKEDENT = ',ENTRY=%ENTRY';
IF %NAME ¬= '' THEN %LKEDNAME = ',NAME=%NAME' ;
IF %NAME = '' THEN %LKEDNAME = ',NAME=TEMPNAME';
/********************************************************************/
/* NOW FINAL BIT OF LINK EDITOR PRIMARY INPUT PROCESSING            */
/********************************************************************/
IF %LKEDNAME ¬= '' THEN %NOPANEL = 1;
IF %INCLCARD ¬= '' THEN %NOPANEL = 1;
IF %LKEDENT ¬= '' THEN %NOPANEL = 1;
IF %NOPANEL = 1 THEN
        %LKEDIN = '%LKEDIN %CONC LINKCNTL' ;
IF %NOPANEL = 1
  THEN DCL LINKCNTL * REPLACE ;
%INCLCARD
%LKEDENT
%LKEDNAME
EOF;
IF %LKEDIN = '' THEN STOP 'NO LINKAGE EDITOR INPUT SPECIFIED' ;
ELSE %LKEDIN = 'SYSLIN READS %LKEDIN' ;
/********************************************************************/
/* NOW FINALLY LETS SET UP THE PROGRAM DECLARE AND WORK FILES       */
/* AND RUN THE LINKAGE EDITOR PROGRAM.                              */
/********************************************************************/
DCL PRINTER SYSOUT %SYSOUT   FBA 121,1210 1,2 TRKS ;
DCL LOADER PROG SYSPRINT WRITES PRINTER
              %AUTOLIB       /* OPTIONAL INPUTS */
              %LKEDINCL         /*  INCLUDE LIBRARY*/
              %LKEDIN ;        /* INPUTS  */
/********************************************************************/
RUN LOADER PARM '%PARM' ;
IF %COMP ¬= '' & %INP = 'OBJ' THEN DELETE OBJ;
END/* OF LOADGO */  ;
