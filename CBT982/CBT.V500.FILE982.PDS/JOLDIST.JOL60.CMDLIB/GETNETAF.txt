GETNETAF: MACRO NOJOIN;    /* UNDOCUMENTED NOJOIN */
 /*****************************************************************/
 /*****HANDLE AFTER CLAUSE, IF ANY ********************************/
 /*****************************************************************/

/* NOTE THAT JOL VERY NICELY JOINS UP ANY PARMS IN () FOR US, WHICH
   IS EXACTLY WHAT WE DON'T WANT FOR THE AFTER CLAUSE IN SUBMIT.

   THEREFORE , WE MUST EXPAND THEM IF THEY ARE FOUND.
*/

 DCL %SYSNETID INIT('') EXT;  /* SET IF NETWORK EXECUTING */
 DCL %SYSJOBS1 INIT('') EXT;  /* KEEPS LIST OF NETWORK JOBS SUBMITTED */
 DCL %SYSJOBS2 INIT('') EXT;  /* KEEPS LIST OF NETWORK JOBS SUBMITTED */
 DCL %SYSNTINC INIT('') EXT;  /* KEEPS INCLUDE DATA SET FOR JOBS      */
 DCL %SYSNTMAC INIT('') EXT;  /* KEEPS JOL MACRO DATA SETS FOR JOBS   */
 DCL %CHK1,%CHK2,%CHK3,%CHK4,%CHK5,%CHK6,
     %CHK7,%CHK8,%CHK9,%CHK10 INIT '' ;

   DCL %SUB     INIT '0';
   DCL %X       INIT '    IF ';
   DCL %BRACKET INIT '0';
   IF %list(1)¬='AFTER' then exit;   /* no work to do */
   %SUB=1;                           /* Set 'array' inedx to 1 */
 LABEL TAFT;
       %SUB=%SUB+1;
       %Y='%LIST(%SUB)';
       %Z=TYPE(Y);
       IF %Y='(' THEN %BRACKET=%BRACKET+1;
       IF %Y=')' THEN %BRACKET=%BRACKET-1;
       IF %Y = '|' | %Y='OR' | %Y = '&' | %Y='AND'
       |  %Y = '('
       THEN DO;
           IF %BRACKET=0            /* A PARENTHESIZED EXPR ? */
           THEN DO;                 /* NO ... */
               WRITE FILE($$JCODE) FROM('%X');
               %X='    %Y  ';
           END;
           ELSE %X='%X %Y ';
       END;
       ELSE DO;
           IF %Y='ENDED' THEN %X='%X ''ENDED''';
           IF %Y=')' THEN %X='%X )';
           %WORK='%Y';                 /* SAVE NAME FOR CHECK LATER */
/*     %Y='%Y        '; %Y=SUBSTR(Y,1,8);   /* MAKE 8 CHARS LONG */
            IF %Y¬='ENDED' & %Y¬=')'   /* IS IT A JOB NAME ? */
            THEN DO;          ;        /* YES */
                %L=LENGTH(X);
                IF %L>60 THEN DO;
                    WRITE FILE($$JCODE) FROM('%X ');
                    %X='        ';
                END;
                if '%LIST(%SUB+1)'¬='ENDED'
                then  %X='%x %%%y=''ENDED''';
                else %X='%X %%%Y=';        /* YES */
                %L=INDEX(SYSJOBS1,'%WORK');  /* SEE IF JOB SUBMITTED */
                %K=INDEX(SYSJOBS2,'%WORK');  /* SEE IF JOB SUBMITTED */
                IF %L=0 & %K=0
                THEN DO;
                STOP 'JOB %WORK NOT YET SUBMITTED:- NETWORK WILL FAIL';
                END;
            END;
       END;
       IF %SUB<%SYSNLIST THEN REDO TAFT;
    IF %X¬='' THEN  WRITE FILE($$JCODE) FROM('%X ');
    WRITE FILE($$JCODE) FROM('    THEN DO;');
    IF %BRACKET¬=0 THEN
    WRITE 'UNMATCHED PARENTHESES IN AFTER CLAUSE OF SUBMIT';
 END;   /* OF AFTER PREPROCESSING */
