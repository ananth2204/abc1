000100 Identification Division.
000200 Program-Id. Showmem.
000300 Author. Jim Moore - Concentrated Logic Incorporated.
000400*--Remarks.------------------------------------------------------*
000500* Compile Options To Use:                                        *
000600*                                                                *
000700* Parm=(Rent,Dyn,Offset,Noseq,Quote,Noadv,Source,Map,Xref)       *
000800*                                                                *
000900* Link-Edit Options To Use:                                      *
001000*                                                                *
001100* Parm=(Let,List,Map,Xref,Rent)                                  *
001200*                                                                *
001300* -------------------------------------------------------------- *
001400* This program is a COBOL ISPF dialog function. It calls the     *
001500* program named CLCCSTOR to retrieve storage limits and in-use   *
001600* amounts from the Local Data Area (IHALDA). It then calculates  *
001700* the amount of "Available 24 and 31" storage and displays a     *
001800* pop-up with all values.                                        *
001900*                                                                *
002000* It uses the &DAS ("Display As") variable to adjust the way     *
002100* the values appear in the pop-up: As "K", "M" or "B" - kilo,    *
002200* meg or edited bytes.                                           *
002300*                                                                *
002400* Bytes is the default and is the most accurate representation.  *
002500* "K" is a bit less accurate but easier to read. "M" is the      *
002600* "granular" but is probably easiest of all to read.             *
002700*                                                                *
002800* The &DAS variable is stored in whatever NEWAPPL profile pool   *
002900* the dialog is executed in.                                     *
003000*                                                                *
003100*                                                                *
003200*----------------------------------------------------------------*
003300 Environment Division.
003400 Data Division.
003500 Working-Storage Section.
003600 01  Misc-Fields.
003700     05  Ispf-Talking-To-Me  Pic S9(08) Binary.
003800         88  Pf3-Pressed     Value 8.
003900 01  Ispf-Api-Services.
004000     05  Vdefine             Pic  X(08) Value "VDEFINE".
004100     05  Vput                Pic  X(08) Value "VPUT".
004200     05  Setmsg              Pic  X(08) Value "SETMSG".
004300     05  Addpop              Pic  X(08) Value "ADDPOP".
004400     05  Rempop              Pic  X(08) Value "REMPOP".
004500     05  Display-It          Pic  X(08) Value "DISPLAY".
004600 01  Ispf-Api-Parameters.
004700     05  Fixd                Pic  X(08) Value "FIXED".
004800     05  Char                Pic  X(08) Value "CHAR".
004900     05  Copy-It             Pic  X(08) Value "(COPY)".
005000     05  Ispz000             Pic  X(08) Value "ISPZ000".
005100     05  Isplink             Pic  X(08) Value "ISPLINK".
005200     05  Main-Panel          Pic  X(08) Value "SHOWMEMP".
005300     05  Profpool            Pic  X(08) Value "PROFILE".
005400     05  Buf-Len             Pic S9(08) Binary.
005500 01  Other-Parameters.
005600     05  Clccstor            Pic  X(08) Value "CLCCSTOR".
005700 01  Ispf-Literals.
005800     05  Storage-Char-24     Pic  X(19)
005900         Value "(MAX24,USD24,AVL24)".
006000     05  Storage-Char-31     Pic  X(24)
006100         Value "(MAX31,USD31,REGN,AVL31)".
006200     05  Shortmsg            Pic  X(10)
006300         Value "(ZMSG000S)".
006400     05  Longmsg             Pic  X(10)
006500         Value "(ZMSG000L)".
006600     05  Display-Flag        Pic  X(05)
006700         Value "(DAS)".
006800 01  Function-Pool.
006900     05  Edit-Storage-Values-24.
007000         10  Max-24          Pic Zz,Zzz,Zzz.
007100         10  Max-24-R        Redefines
007200             Max-24          Pic  X(10).
007300         10  Used-24         Pic Zz,Zzz,Zzz.
007400         10  Used-24-R       Redefines
007500             Used-24         Pic  X(10).
007600         10  Avail-24        Pic Zz,Zzz,Zzz.
007700         10  Avail-24-R      Redefines
007800             Avail-24        Pic  X(10).
007900     05  Edit-Storage-Values-31.
008000         10  Max-31          Pic Z,Zzz,Zzz,Zzz.
008100         10  Max-31-R        Redefines
008200             Max-31          Pic  X(13).
008300         10  Used-31         Pic Z,Zzz,Zzz,Zzz.
008400         10  Used-31-R       Redefines
008500             Used-31         Pic  X(13).
008600         10  Region-Size     Pic Z,Zzz,Zzz,Zzz.
008700         10  Region-Size-R   Redefines
008800             Region-Size     Pic  X(13).
008900         10  Avail-31        Pic Z,Zzz,Zzz,Zzz.
009000         10  Avail-31-R      Redefines
009100             Avail-31        Pic  X(13).
009200     05  Zmsg000s            Pic  X(24) Value Spaces.
009300     05  Zmsg000l            Pic  X(76) Value Spaces.
009400     05  Display-As          Pic  X(01) Value Spaces.
009500         88  Display-As-K    Value "K".
009600         88  Display-As-M    Value "M".
009700         88  Display-As-B    Value "B".
009800 01  Return-Storage-Values   Binary.
009900     05  Max-24-Bit-Value    Pic S9(08).
010000     05  Used-24-Bit-Value   Pic S9(08).
010100     05  Max-31-Bit-Value    Pic S9(08).
010200     05  Used-31-Bit-Value   Pic S9(08).
010300     05  Region-Value        Pic S9(08).
010400 01  Available-24            Pic S9(08) Binary.
010500 01  Available-31            Pic S9(08) Binary.
010600 01  Divisor                 Pic S9(08) Binary.
010700 01  Generic-K-Number        Pic S9(06)V9(02).
010800 01                          Redefines Generic-K-Number.
010900     05  Gn-K-Integer        Pic  9(06).
011000     05  Gn-K-Decimal        Pic Sv9(02).
011100 01  Generic-M-Number        Pic S9(04)V9(01).
011200 01  Gn-K-Edit-Area.
011300     05  Gn-K-Edit           Pic  Zzz,Zz9.
011400     05                      Pic  X(01) Value "K".
011500 01  Gn-M-Edit-Area.
011600     05  Gn-M-Edit           Pic  Z,Zz9.9.
011700     05                      Pic  X(01) Value "M".
011800 Linkage Section.
011900 01  Passed-Linkage.
012000     05  Parm-Debug          Pic  X(100).
012100/
012200 Procedure Division Using Passed-Linkage.
012300     Move Length   Of Max-24     To Buf-Len.
012400     Call Isplink  Using Vdefine Storage-Char-24
012500                         Edit-Storage-Values-24 Char Buf-Len.
012600     Move Length   Of Max-31     To Buf-Len.
012700     Call Isplink  Using Vdefine Storage-Char-31
012800                         Edit-Storage-Values-31 Char Buf-Len.
012900     Move Length   Of Zmsg000s   To Buf-Len.
013000     Call Isplink  Using Vdefine Shortmsg Zmsg000s Char Buf-Len.
013100     Move Length   Of Zmsg000l   To Buf-Len.
013200     Call Isplink  Using Vdefine Longmsg Zmsg000l Char Buf-Len.
013300     Move Length   Of Display-As To Buf-Len.
013400     Call Isplink  Using Vdefine Display-Flag
013500                         Display-As Char Buf-Len Copy-It.
013600
013700     Perform Until Pf3-Pressed
013800       Call Clccstor Using Return-Storage-Values
013900       Move Max-24-Bit-Value   To Max-24
014000       Move Used-24-Bit-Value  To Used-24
014100       Move Max-31-Bit-Value   To Max-31
014200       Move Used-31-Bit-Value  To Used-31
014300       Move Region-Value       To Region-Size
014400       Compute Available-24 =
014500         (Max-24-Bit-Value - Used-24-Bit-Value)
014600       Move Available-24       To Avail-24
014700       Compute Available-31 =
014800         (Max-31-Bit-Value - Used-31-Bit-Value)
014900       Move Available-31       To Avail-31
015000       Perform 1000-Check-Display-Format Thru 1000-Exit
015100       Call Isplink            Using Addpop
015200       Call Isplink            Using Display-It Main-Panel
015300       Move Return-Code        To Ispf-Talking-To-Me
015400       Call Isplink            Using Rempop
015500       Call Isplink            Using Vput Display-Flag Profpool
015600     End-Perform.
015700
015800     Goback.
015900/
016000 1000-Check-Display-Format.
016100     Move 1                  To Divisor.
016200     Evaluate True
016300        When Display-As-K
016400           Move 1024         To Divisor
016500        When Display-As-M
016600           Move 1024000      To Divisor
016700     End-Evaluate.
016800
016900     If Display-As-K
017000*       24-Bit Max
017100        Compute Generic-K-Number Rounded =
017200        (Max-24-Bit-Value / Divisor)
017300        If Gn-K-Decimal Not = Zeros
017400          Add 1 To Gn-K-Integer
017500        End-If
017600        Move Gn-K-Integer To Gn-K-Edit
017700        Move Gn-K-Edit-Area To Max-24-R
017800*       24-Bit Used
017900        Compute Generic-K-Number Rounded =
018000         (Used-24-Bit-Value / Divisor)
018100        If Gn-K-Decimal Not = Zeros
018200          Add 1 To Gn-K-Integer
018300        End-If
018400        Move Gn-K-Integer To Gn-K-Edit
018500        Move Gn-K-Edit-Area To Used-24-R
018600*       24-Bit Available
018700        Compute Generic-K-Number Rounded =
018800         (Available-24 / Divisor)
018900        If Gn-K-Decimal Not = Zeros
019000          Add 1 To Gn-K-Integer
019100        End-If
019200        Move Gn-K-Integer To Gn-K-Edit
019300        Move Gn-K-Edit-Area To Avail-24-R
019400*       31-Bit Max
019500        Compute Generic-K-Number Rounded =
019600         (Max-31-Bit-Value / Divisor)
019700        If Gn-K-Decimal Not = Zeros
019800          Add 1 To Gn-K-Integer
019900        End-If
020000        Move Gn-K-Integer To Gn-K-Edit
020100        Move Gn-K-Edit-Area To Max-31-R
020200*       31-Bit Used
020300        Compute Generic-K-Number Rounded =
020400         (Used-31-Bit-Value / Divisor)
020500        If Gn-K-Decimal Not = Zeros
020600          Add 1 To Gn-K-Integer
020700        End-If
020800        Move Gn-K-Integer To Gn-K-Edit
020900        Move Gn-K-Edit-Area To Used-31-R
021000*       31-Bit Available
021100        Compute Generic-K-Number Rounded =
021200         (Available-31 / Divisor)
021300        If Gn-K-Decimal Not = Zeros
021400          Add 1 To Gn-K-Integer
021500        End-If
021600        Move Gn-K-Integer To Gn-K-Edit
021700        Move Gn-K-Edit-Area To Avail-31-R
021800*       Region Amount
021900        Compute Generic-K-Number Rounded =
022000         (Region-Value / Divisor)
022100        If Gn-K-Decimal Not = Zeros
022200          Add 1 To Gn-K-Integer
022300        End-If
022400        Move Gn-K-Integer To Gn-K-Edit
022500        Move Gn-K-Edit-Area To Region-Size-R
022600     Else
022700     If Display-As-M
022800*       24-Bit Max
022900        Compute Generic-M-Number Rounded =
023000        (Max-24-Bit-Value / Divisor)
023100        Move Generic-M-Number To Gn-M-Edit
023200        Move Gn-M-Edit-Area   To Max-24-R
023300*       24-Bit Used
023400        Compute Generic-M-Number Rounded =
023500        (Used-24-Bit-Value / Divisor)
023600        Move Generic-M-Number To Gn-M-Edit
023700        Move Gn-M-Edit-Area   To Used-24-R
023800*       24-Bit Available
023900        Compute Generic-M-Number Rounded =
024000        (Available-24      / Divisor)
024100        Move Generic-M-Number To Gn-M-Edit
024200        Move Gn-M-Edit-Area   To Avail-24-R
024300*       31-Bit Max
024400        Compute Generic-M-Number Rounded =
024500        (Max-31-Bit-Value / Divisor)
024600        Move Generic-M-Number To Gn-M-Edit
024700        Move Gn-M-Edit-Area   To Max-31-R
024800*       31-Bit Used
024900        Compute Generic-M-Number Rounded =
025000        (Used-31-Bit-Value / Divisor)
025100        Move Generic-M-Number To Gn-M-Edit
025200        Move Gn-M-Edit-Area   To Used-31-R
025300*       31-Bit Available
025400        Compute Generic-M-Number Rounded =
025500        (Available-31      / Divisor)
025600        Move Generic-M-Number To Gn-M-Edit
025700        Move Gn-M-Edit-Area   To Avail-31-R
025800*       Region Amount
025900        Compute Generic-M-Number Rounded =
026000        (Region-Value      / Divisor)
026100        Move Generic-M-Number To Gn-M-Edit
026200        Move Gn-M-Edit-Area   To Region-Size-R.
026300 1000-Exit. Exit.
