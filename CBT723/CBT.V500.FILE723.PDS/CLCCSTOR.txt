000100 Identification Division.
000200 Program-ID. CLCCSTOR Is Initial.
000300 Author. JB Moore - Concentrated Logic Incorporated.
000400 Environment Division.
000500*
000600*REMARKS: -------------------------------------------------------*
000700* Cobol Compile-Time Parms:-->>> TRUNC(BIN) MANDATORY!!!!!       *
000800* Or, declare all Binary fields COMP-5 if compiler supports it.  *
000900*                                                                *
001000* PARM=(RENT,RMODE(ANY),SSRANGE,APOST,DYN,OFFSET,LIB,            *
001100* MAP,XREF,TRUNC(BIN),NUMPROC(PFD),DATA(31))                     *
001200*                                                                *
001300* Binder (Link-Edit) Parms:                                      *
001400*                                                                *
001500* PARM='LET,LIST,MAP,XREF,RENT'                                  *
001600* ============================================================== *
001700* This program is meant to be called, never a "PGM=". It will    *
001800* return the following five fields to the caller:                *
001900*                                                                *
002000* 1) 24-bit max amount of storage                                *
002100* 2) 24-bit used amount of storage                               *
002200* 3) 31-bit max amount of storage                                *
002300* 4) 31-bit used amount of storage                               *
002400* 5) REGION amount for address space                             *
002500*                                                                *
002600* Nothing is passed on input. No Working-Storage needed.         *
002700* MUST have "AMODE(31)" for Local Data Area ("LDA-Block").       *
002800*END-REMARKS.----------------------------------------------------*
002900*
003000 Data Division.
003100 Linkage Section.
003200 01  Return-Storage-Values            Binary.
003300     05  LS-Max-24-Bit-Value          Pic S9(08).
003400     05  LS-Used-24-Bit-Value         Pic S9(08).
003500     05  LS-Max-31-Bit-Value          Pic S9(08).
003600     05  LS-Used-31-Bit-Value         Pic S9(08).
003700     05  LS-Region                    Pic S9(08).
003800
003900 01  PSA-Block.
004000     05                               Pic  X(548).
004100     05  PSAAOLD                      Pointer.
004200
004300 01  ASCB-Block.
004400     05                               Pic  X(04).
004500         88  ASCB-Literal             Value "ASCB".
004600     05                               Pic  X(44).
004700     05  ASCBLDA                      Pointer.
004800
004900 01  LDA-Block.
005000     05                               Pic  X(04).
005100         88  LDA-Literal              Value "LDA ".
005200     05                               Pic  X(200).
005300     05  REGION-Value                 Pic S9(08) Binary.
005400     05  Max-24-Bit-Value             Pic S9(08) Binary.
005500     05                               Pic  X(04).
005600     05  Max-31-Bit-Value             Pic S9(08) Binary.
005700     05                               Pic  X(12).
005800     05  Used-24-Bit-Value            Pic S9(08) Binary.
005900     05                               Pic  X(04).
006000     05  Used-31-Bit-Value            Pic S9(08) Binary.
006100/
006200 Procedure Division Using Return-Storage-Values.
006300     Initialize Return-Storage-Values.
006400     Set Address of PSA-Block     To Null.
006500     Set Address of ASCB-Block    To PSAAOLD.
006600     If ASCB-Literal
006700        Set Address of LDA-Block  To ASCBLDA
006800        If LDA-Literal
006900           Move REGION-Value      To LS-Region
007000           Move Max-24-Bit-Value  To LS-Max-24-Bit-Value
007100           Move Max-31-Bit-Value  To LS-Max-31-Bit-Value
007200           Move Used-24-Bit-Value To LS-Used-24-Bit-Value
007300           Move Used-31-Bit-Value To LS-Used-31-Bit-Value.
007400     Goback.
