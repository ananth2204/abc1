* ---------------------------------------------------------------- *
*  ALTERNATE VERSION OF THE DSE PROGRAM IF OPEN WITH EXTEND DOES   *
*  NOT WORK ON YOUR VERSION OF MVS.                                *
* ---------------------------------------------------------------- *
         MACRO
&NAME    BEGIN &REG1=3,&REG2=4,&PGM=XXXXXXXX,&DATE=00000000
&NAME    START
         STM   14,12,12(13)            SAVE REGISTERS
         BALR  &REG1,0       LOAD 1ST BASE REGISTER
         USING *,&REG1,&REG1+1,&REG1+2,&REG1+3
A0ZE     ST    13,SAVE+4
         LR    12,13
         LA    13,SAVE
         ST    13,8(12)
         LM    &REG1,&REG2,BASEADDR
         B     SAVE+72             BRANCH AROUND CONSTANTS
         DC    CL8'&PGM'           PROGRAM NAME
         DC    CL8'&DATE'          CREATION DATE
BASEADDR DC    A(A0ZE,A0ZE+4096,A0ZE+8192,A0ZE+12288)
SAVE     DS    9D                      SAVEAREA
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         EJECT
         MEND
DSEPROG  BEGIN PGM=DSEPROG
         L     R1,0(R1)
         LH    R2,0(R1)
         LTR   R2,R2
         BZ    NOPARM
         MVC   PARM,2(R1)
NOPARM   EQU   *
         USING INFMJFCB,R7
         LA    R7,JFCB
         RDJFCB DSETAPE
         SR    R5,R5
         IC    R5,JFCBNVOL
         LA    R6,1
         CLI   PARM,C'H'
         BE    WTOR
         LR    R6,R5
         LA    R5,1
         B     WTOR
LOOP     EQU   *
         STH   R6,JFCBVLSQ
         OPEN  (DSETAPE,(INPUT)),TYPE=J
         EXCP  IOBSNS
         WAIT  ECB=ECB
         TM    INPUT+1,X'02'
         BNZ   REMOUNT
PROCEED  L     R4,=F'2'
         TM    JFCBLTYP,X'01'
         BZ    LOOP1
         BCTR  R4,0
LOOP1    EQU   *
         EXCP  IOBFSF
         WAIT  ECB=ECB
         BCT   R4,LOOP1
         EXCP  IOBWTM
         WAIT  ECB=ECB
         EXCP  IOBOUT
         WAIT  ECB=ECB
         CLI   ECB,X'7F'
         BE    ERGAP
         EXCP  IOBSNS
         WAIT  ECB=ECB
         TM    INPUT+4,X'20'
         BZ    FEHLER
ERGAP    MVI   ERGCCW+4,X'20'
         L     R4,=F'8'
EXCP     EXCP  IOBOUT
         WAIT  ECB=ECB
         BCT   R4,EXCP
         EXCP  IOBWTM
         WAIT  ECB=ECB
CLOSE    CLOSE DSETAPE
         LA    R6,1(R6)
         MVI   ERGCCW+4,X'60'
         BCT   R5,LOOP
         L     13,SAVE+4
        RETURN (14,12),RC=0
WTOR     WTO   '*** MOUNT TAPES WITH FILE-PROTECT RING AND       ',    *
               ROUTCDE=(2,7),DESC=(2)
         WTO   '* REPLY U TO OVERWRITE FILES                     ',    *
               ROUTCDE=(2,7),DESC=(2)
         B     LOOP
REMOUNT  EQU   *
         CLOSE DSETAPE
         WTO   '*** THE TAPE HAS NO FILE-PROTECT RING ',ROUTCDE=(7,11)
         WTO   '* UNLOAD THE TAPE, PUT A PROTECT RING',ROUTCDE=(7,11)
         WTO   '* ON IT, MOUNT THE TAPE ON THE SAME  ',ROUTCDE=(7,11)
         MVC   ECB,=F'0'
         WTOR  '* UNIT AND REPLY   Y    ',REPLY,1,ECB,ROUTCDE=(7,11)
WAIT     WAIT  ECB=ECB
         CLI   REPLY,C'Y'
         BE    LOOP
         MVC   ECB,=F'0'
         WTOR  '***  REPLY: Y  ***',REPLY,1,ECB,ROUTCDE=(7,11)
         B     WAIT
FEHLER   WTO   '*** ERASE PROGRAMM           ****',ROUTCDE=(2,7,11)
         WTO   '*** ERROR IN CHANNEL PROGRAMM **',ROUTCDE=(2,7,11)
         WTO   '** TAPE IS NOT COMPLETELY ERASED    ',ROUTCDE=(2,7,11)
         CLOSE DSETAPE
         L     13,SAVE+4
         ABEND 4090,DUMP
         DS    0D
SNSCCW   CCW   X'04',INPUT,X'20',10
FSFCCW   CCW   X'3F',INPUT,X'20',10
ERGCCW   CCW   X'17',INPUT,X'60',10
DSECCW   CCW   X'97',INPUT,X'20',10
WTMCCW   CCW   X'1F',INPUT,X'20',10
IOBOUT   DC    X'02',4X'00'
         DC    AL3(ECB)
         DC    6X'00'
         DS    CL2
         DC    X'00'
         DC    AL3(ERGCCW)
         DC    X'00'
         DC    AL3(DSETAPE)
         DC    4X'00',H'1',10X'00'
IOBWTM   DC    X'02',4X'00'
         DC    AL3(ECB)
         DC    6X'00'
         DS    CL2
         DC    X'00'
         DC    AL3(WTMCCW)
         DC    X'00'
         DC    AL3(DSETAPE)
         DC    4X'00',H'1',10X'00'
IOBFSF   DC    X'02',4X'00'
         DC    AL3(ECB)
         DC    6X'00'
         DS    CL2
         DC    X'00'
         DC    AL3(FSFCCW)
         DC    X'00'
         DC    AL3(DSETAPE)
         DC    4X'00',H'1',10X'00'
IOBSNS   DC    X'02',4X'00'
         DC    AL3(ECB)
         DC    6X'00'
         DS    CL2
         DC    X'00'
         DC    AL3(SNSCCW)
         DC    X'00'
         DC    AL3(DSETAPE)
         DC    4X'00',H'1',10X'00'
ECB      DS    F
DSETAPE  DCB   DEVD=TA,DSORG=PS,DDNAME=TAPE,MACRF=E,EXLST=EXITJFC
REPLY    DC    C' '
PARM     DC    C'F'
         LTORG
EXITJFC  DS    0F
         DC    X'87',AL3(JFCB)
         DS    0F
JFCB     DS    CL176
INPUT    DS    CL10
         DSECT
         IEFJFCBN
         END
