  OPTIONS LS=076 PS=60 NOCENTER ;
  DATA CPUUTIL ;
INFILE CPUUTIL ;
  INPUT @1     RECORD     $CHAR150.
        @1     DATE       $CHAR8.
        @1     MONTH      $CHAR2.
        @4     DAY        $CHAR2.
        @7     YEAR       $CHAR2.
        @10    TIME       $CHAR8.
        @10    TIME5      $CHAR5.
        @10    HOURS      $CHAR2.
        @13    MINS       $CHAR2.
        @16    SECS       $CHAR2.
        @19    SYSID      $CHAR3.
        @23    MSGID      $CHAR8.
        @32    LIT1       $CHAR20.
    ;

*********************************************************************;
* CHANGE THE SIZE LIMITS FOR YOUR SHOP FOR THE LENGTH STATMENTS     *;
* BELOW FOR THE FOLLOWING FIELDS (MAX SIZE):                        *;
*   LPARNAME HARDWNME ROLLAVG CECCAPAC LPRCAPAC                     *;
*********************************************************************;

  LENGTH SORTKEY $40 DATE10 $10 ;
  DATE10  = "20"||YEAR||"/"||MONTH||"/"||DAY ;
  SORTKEY = SYSID||YEAR||MONTH||DAY||TIME5||REPEAT(" ",26) ;

  PROC SORT DATA=CPUUTIL  ;
       BY SORTKEY ;

  DATA UTILIZ ;
    SET CPUUTIL ;

  LENGTH LPARNAME $7 HARDWNME $3 ;
  LENGTH ROLLAVG  $2 CECCAPAC $2 LPRCAPAC $2 ;

  RETAIN LPARNAME HARDWNME ROLLAVG CECCAPAC LPRCAPAC " " ;

  LENGTH PREVKEY $40 SOFTCAP LIMITED $1 STRING $15 ;
  RETAIN PREVKEY SOFTCAP LIMITED " " ;

  IF LIT1 =  :"LPAR " THEN DO ;    * HAVE UTILIZATION MESSAGE * ;
     LPARNAME = SCAN(LIT1,2,", ") ;
     U1  = INDEX(RECORD,"UTILIZATION IS") ;
     IF U1 > 0 THEN DO ;
        STRING  = SUBSTR(RECORD,U1+15,15) ;
        ROLLAVG = SCAN(STRING,1,", ") ;
        IF LENGTH(ROLLAVG) = 1      THEN
           ROLLAVG = "0"||ROLLAVG ;
     END ;
     U2  = INDEX(RECORD,"SOFTCAPPING IS CURRENTLY IMPOSED") ;
     SOFTCAP = "N"     ;
     LIMITED = "N"     ;
     IF U2 > 0 THEN DO ;
        SOFTCAP = "Y"  ;
        LIMITED = "Y"  ;
     END ;
     U3  = INDEX(RECORD,"SOFTCAPPING IS ENABLED") ;
     IF U3 > 0 THEN
        LIMITED = "Y"  ;
  END ;

  IF LIT1 =  :"SY" THEN DO ;    * HAVE CAPACITY MESSAGE * ;
     HARDWNME = SCAN(LIT1,1,", ") ;
     C1  = INDEX(RECORD,"CAPACITY IS") ;
     IF C1 > 0 THEN
        STRING   = TRIM(SUBSTR(RECORD,C1+12,15)) ;
        CECCAPAC = SCAN(STRING,1,", ") ;
        IF LENGTH(CECCAPAC) = 1       THEN
           CECCAPAC = "0"||CECCAPAC ;
     C2  = INDEX(RECORD,"LPAR CAPACITY IS") ;
     IF C2 > 0 THEN
        STRING   = TRIM(SUBSTR(RECORD,C2+17,15)) ;
        LPRCAPAC = SCAN(STRING,1,", ") ;
        IF LENGTH(LPRCAPAC) = 1       THEN
           LPRCAPAC = "0"||LPRCAPAC ;
  END ;

  IF SORTKEY = PREVKEY THEN DO ;
     OUTPUT ;
     LPARNAME = " " ;
     ROLLAVG  = " " ;
     SOFTCAP  = " " ;
     LIMITED  = " " ;
     HARDWNME = " " ;
     CECCAPAC = " " ;
     LPRCAPAC = " " ;
     PREVKEY  = " " ;
  END ;

  PREVKEY = SORTKEY ;
  DELETE ;

  DATA ;
   SET UTILIZ ;
   FILE OUTPUT  NOPRINT ;

   RETAIN TAB "," ;
   LENGTH HEAD1 HEAD2 $80 ;

   HEAD1 = "    WLM     "||TAB||" WLM "||TAB||"SYS"||TAB||
           " LPAR  "||TAB||"LPAR "||TAB||"ROLLING"||TAB||"CEC "||
           TAB||" CEC "||TAB||"SOFTCAP "||TAB||"SOFTCAP " ;

   HEAD2 = "    DATE    "||TAB||"TIME "||TAB||"ID "||TAB||
           " NAME  "||TAB||"CAPAC"||TAB||" AVG.  "||TAB||"NAME"||
           TAB||"CAPAC"||TAB||"ENABLED?"||TAB||"IMPOSED?" ;

   IF _N_ = 1 THEN DO ;
      PUT @1   HEAD1        $80. ;
      PUT @1   HEAD2        $80. ;
   END ;

    PUT @1     DATE10     $10.
        @11    TAB        $1.
        @12    TIME5      $5.
        @17    TAB        $1.
        @18    SYSID      $3.
        @21    TAB        $1.
        @22    LPARNAME   $7.
        @29    TAB        $1.
        @30    LPRCAPAC   $2.
        @32    TAB        $1.
        @33    ROLLAVG    $2.
        @35    TAB        $1.
        @36    HARDWNME   $3.
        @39    TAB        $1.
        @40    CECCAPAC   $2.
        @42    TAB        $1.
        @43    LIMITED    $1.
        @44    TAB        $1.
        @45    SOFTCAP    $1.
     ;

  PROC PRINT DATA=UTILIZ  U SPLIT="*" ;
    VAR DATE10 TIME5 SYSID LPARNAME LPRCAPAC ROLLAVG HARDWNME
        CECCAPAC LIMITED SOFTCAP
     ;
  TITLE1 " LISTING OF WLM CPU UTILIZATION BY TIME-OF-DAY INTERVALS  " ;
  TITLE2 " SORTED BY DATE AND TIME                                  " ;
  LABEL DATE10     = "WLM*DATE"
        TIME5      = "WLM*TIME"
        SYSID      = "SYS*ID"
        LPARNAME   = "LPAR*NAME"
        LPRCAPAC   = "LPAR*CAPAC"
        ROLLAVG    = "ROLLING*AVG"
        HARDWNME   = "CEC*NAME"
        CECCAPAC   = "CEC*CAPAC"
        LIMITED    = "SOFTCAP*ENABLED?"
        SOFTCAP    = "SOFTCAP*IMPOSED?"
   ;
