/**** REXX subc *******************************************************/
/*                                                                    */
/*  REXX-Programmm                      : subc                        */
/*   |                                                                */
/*   '-> Unterprogramme (intern)        : -----                       */
/*   |   Unterprogramme (extern)        : subcmac                     */
/*   |                                                                */
/*   '-> Messages                       : -----                       */
/*   |                                                                */
/*   '-> Panels                         : -----                       */
/*   |                                                                */
/*   '-> Tables                         : -----                       */
/*   |                                                                */
/*   '-> Skeletons                      : -----                       */
/*                                                                    */
/*  Edit macro to change system symbols in JCL stream. This macro     */
/*  only allocates a temporay dataset, copies the shown JCL, insert   */
/*  a '/*JOBPARM SYSAFF=....' statement (to secure that the job runs  */
/*  on the local system) and at least calls ISPEXEC EDIT with         */
/*  macro 'subcmac' to change system symbols.                         */
/*                                                                    */
/*  The 'subcmac' also allow the usage of the date variables          */
/*  &TODAY and &YESTERDAY.                                            */
/*                                                                    */
/*  I have also supplied a REXX called 'subcc' which does the same    */
/*  as described before but don't submit the changed JCL directly.    */
/*  The resulting JCL will be displayed for checking purposes and you */
/*  must submit the JCL manually.                                     */
/*                                                                    */
/*                                                                    */
/*  Author: Karl-Heinz Doppelfeld                                     */
/*          Karl-Heinz.DoppelfeldÂ§Sparkassen-Informatik.de            */
/*                                                                  */*/
/**** REXX ************************************************************/

SYSNAME  = MVSVAR('SYSNAME')    /* get system name      */

jobuser = USERID()              /* get uid              */

ADDRESS ISREDIT
"MACRO (PARM)"
"(status) = USER_STATE"         /* save current bounds  */
"(lastln) = LINENUM .ZLAST"     /* last data line       */

x = msg('off')
address tso "delstack"
address tso "newstack"
address tso "DROPBUF"
address tso "MAKEBUF"

ADDRESS TSO "FREE DD($tmpjob)"
ADDRESS TSO "ALLOC DD($tmpjob) UNIT(sysda)",
            "space(5 5) tracks lrecl(80) recfm(f b)"
if (rc > 0) then
  do
    zedsmsg = 'TempFile not allocated'
    zedlmsg = 'Temporary file could not allocated.',
              'DDname $tmpjob already allocated.'
    Address ISPEXEC "SETMSG MSG(ISRZ001)"  /* msg - with alarm */
    exit
  end
trace off

/*  Here you can insert a job card, otherwise the jobcard will
    be generated by submit and you must specify a job character

queue "//"jobuser"E JOB 0000,"
queue "//             "jobuser",  **JOB STMT generated by SUBC**"
queue "//             NOTIFY="jobuser",CLASS=A,             "
queue "//             MSGLEVEL=(1,1),USER="jobuser
*/

queue "//*"
queue "/*JOBPARM SYSAFF="sysname

currln = 1
Do while currln <= lastln
  "(data1) = LINE "currln     /* data1 = current line */
  queue data1
  currln = currln + 1
End   /* do while */
queue ''

address TSO "EXECIO * DISKW $tmpjob (FINIS"

ADDRESS ISPEXEC "LMINIT DATAID(DATAID) DDNAME($tmpjob)"

subcparm = 'YES'

address ispexec "EDIT DATAID("DATAID")",
        "CONFIRM(NO) PARM(subcparm) MACRO(#subcmac)"
ADDRESS ISPEXEC "LMFREE DATAID("DATAID")"
ADDRESS TSO     "FREE file($tmpjob)"

exit
