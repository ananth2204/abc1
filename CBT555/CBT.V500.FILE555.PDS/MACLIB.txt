./ ADD NAME=$$CHANGE 0101-06058-06058-0935-00002-00002-00000-SPGSRS
Changes:   2002/06/13
Change EPA macro in specifying AMODE and RMODE
./ ADD NAME=$HEAD    0101-01362-01363-1116-00638-00638-00002-SPGSRS
         MACRO
        $HEAD  &STRING,           STRING IS EXPANDED                   *
               &BLOCK=YES,        STRING IS TO BE BLOCKED              *
               &TITLE=            (A,'B') -GEN TITLE STATEMENT WITH    *
                                  NAME FIELD A, AND TITLE 'B'
.*
         GBLB  &HEADB
         GBLC  &HEADC(497)
         LCLA  &I
         LCLA  &J
         LCLA  &K
         LCLA  &L
         LCLA  &M
         LCLA  &N
         LCLC  &F(64)
         LCLC  &STR(64)
         LCLC  &B
         LCLC  &W
.*
.*
         AIF   (&HEADB).PROC
&HEADB   SETB  (1)
&HEADC(1)    SETC  ' '
&HEADC(2)    SETC  '        '
&HEADC(3)    SETC  '        '
&HEADC(4)    SETC  '        '
&HEADC(5)    SETC  '        '
&HEADC(6)    SETC  '        '
&HEADC(7)    SETC  '        '
&HEADC(8)    SETC  '        '
&HEADC(9)    SETC  '        '
&HEADC(10)   SETC  'A'
&HEADC(11)   SETC  '    A   '
&HEADC(12)   SETC  '   AAA  '
&HEADC(13)   SETC  '  AA AA '
&HEADC(14)   SETC  ' AA   AA'
&HEADC(15)   SETC  ' AA   AA'
&HEADC(16)   SETC  ' AAAAAAA'
&HEADC(17)   SETC  ' AA   AA'
&HEADC(18)   SETC  ' AA   AA'
&HEADC(19)   SETC  'B'
&HEADC(20)   SETC  ' BBBBBB '
&HEADC(21)   SETC  ' BB   BB'
&HEADC(22)   SETC  ' BB   BB'
&HEADC(23)   SETC  ' BBBBBB '
&HEADC(24)   SETC  ' BB   BB'
&HEADC(25)   SETC  ' BB   BB'
&HEADC(26)   SETC  ' BB   BB'
&HEADC(27)   SETC  ' BBBBBB '
&HEADC(28)   SETC  'C'
&HEADC(29)   SETC  '  CCCCC '
&HEADC(30)   SETC  ' CC   CC'
&HEADC(31)   SETC  ' CC   CC'
&HEADC(32)   SETC  ' CC     '
&HEADC(33)   SETC  ' CC     '
&HEADC(34)   SETC  ' CC   CC'
&HEADC(35)   SETC  ' CC   CC'
&HEADC(36)   SETC  '  CCCCC '
&HEADC(37)   SETC  'D'
&HEADC(38)   SETC  ' DDDDDD '
&HEADC(39)   SETC  ' DD   DD'
&HEADC(40)   SETC  ' DD   DD'
&HEADC(41)   SETC  ' DD   DD'
&HEADC(42)   SETC  ' DD   DD'
&HEADC(43)   SETC  ' DD   DD'
&HEADC(44)   SETC  ' DD   DD'
&HEADC(45)   SETC  ' DDDDDD '
&HEADC(46)   SETC  'E'
&HEADC(47)   SETC  ' EEEEEE '
&HEADC(48)   SETC  ' EE     '
&HEADC(49)   SETC  ' EE     '
&HEADC(50)   SETC  ' EEEE   '
&HEADC(51)   SETC  ' EE     '
&HEADC(52)   SETC  ' EE     '
&HEADC(53)   SETC  ' EE     '
&HEADC(54)   SETC  ' EEEEEEE'
&HEADC(55)   SETC  'F'
&HEADC(56)   SETC  ' FFFFFFF'
&HEADC(57)   SETC  ' FF     '
&HEADC(58)   SETC  ' FF     '
&HEADC(59)   SETC  ' FFFFF  '
&HEADC(60)   SETC  ' FF     '
&HEADC(61)   SETC  ' FF     '
&HEADC(62)   SETC  ' FF     '
&HEADC(63)   SETC  ' FF     '
&HEADC(64)   SETC  'G'
&HEADC(65)   SETC  '  GGGGG '
&HEADC(66)   SETC  ' GG   GG'
&HEADC(67)   SETC  ' GG   GG'
&HEADC(68)   SETC  ' GG     '
&HEADC(69)   SETC  ' GG  GGG'
&HEADC(70)   SETC  ' GG   GG'
&HEADC(71)   SETC  ' GG   GG'
&HEADC(72)   SETC  '  GGGGG '
&HEADC(73)   SETC  'H'
&HEADC(74)   SETC  ' HH   HH'
&HEADC(75)   SETC  ' HH   HH'
&HEADC(76)   SETC  ' HH   HH'
&HEADC(77)   SETC  ' HHHHHHH'
&HEADC(78)   SETC  ' HH   HH'
&HEADC(79)   SETC  ' HH   HH'
&HEADC(80)   SETC  ' HH   HH'
&HEADC(81)   SETC  ' HH   HH'
&HEADC(82)   SETC  'I'
&HEADC(83)   SETC  '  IIII  '
&HEADC(84)   SETC  '   II   '
&HEADC(85)   SETC  '   II   '
&HEADC(86)   SETC  '   II   '
&HEADC(87)   SETC  '   II   '
&HEADC(88)   SETC  '   II   '
&HEADC(89)   SETC  '   II   '
&HEADC(90)   SETC  '  IIII  '
&HEADC(91)   SETC  'J'
&HEADC(92)   SETC  '      JJ'
&HEADC(93)   SETC  '      JJ'
&HEADC(94)   SETC  '      JJ'
&HEADC(95)   SETC  '      JJ'
&HEADC(96)   SETC  '      JJ'
&HEADC(97)   SETC  ' JJ   JJ'
&HEADC(98)   SETC  ' JJ   JJ'
&HEADC(99)   SETC  '  JJJJJ '
&HEADC(100)  SETC  'K'
&HEADC(101)  SETC  ' KK   KK'
&HEADC(102)  SETC  ' KK  KK '
&HEADC(103)  SETC  ' KK KK  '
&HEADC(104)  SETC  ' KKKK   '
&HEADC(105)  SETC  ' KKKK   '
&HEADC(106)  SETC  ' KK KK  '
&HEADC(107)  SETC  ' KK  KK '
&HEADC(108)  SETC  ' KK   KK'
&HEADC(109)  SETC  'L'
&HEADC(110)  SETC  ' LL     '
&HEADC(111)  SETC  ' LL     '
&HEADC(112)  SETC  ' LL     '
&HEADC(113)  SETC  ' LL     '
&HEADC(114)  SETC  ' LL     '
&HEADC(115)  SETC  ' LL     '
&HEADC(116)  SETC  ' LL     '
&HEADC(117)  SETC  ' LLLLLLL'
&HEADC(118)  SETC  'M'
&HEADC(119)  SETC  ' M     M'
&HEADC(120)  SETC  ' MM   MM'
&HEADC(121)  SETC  ' MMM MMM'
&HEADC(122)  SETC  ' MMMMMMM'
&HEADC(123)  SETC  ' MM M MM'
&HEADC(124)  SETC  ' MM   MM'
&HEADC(125)  SETC  ' MM   MM'
&HEADC(126)  SETC  ' MM   MM'
&HEADC(127)  SETC  'N'
&HEADC(128)  SETC  ' NN   NN'
&HEADC(129)  SETC  ' NNN  NN'
&HEADC(130)  SETC  ' NNN  NN'
&HEADC(131)  SETC  ' NNNN NN'
&HEADC(132)  SETC  ' NN NNNN'
&HEADC(133)  SETC  ' NN  NNN'
&HEADC(134)  SETC  ' NN  NNN'
&HEADC(135)  SETC  ' NN   NN'
&HEADC(136)  SETC  'O'
&HEADC(137)  SETC  '  OOOOO '
&HEADC(138)  SETC  ' OO   OO'
&HEADC(139)  SETC  ' OO   OO'
&HEADC(140)  SETC  ' OO   OO'
&HEADC(141)  SETC  ' OO   OO'
&HEADC(142)  SETC  ' OO   OO'
&HEADC(143)  SETC  ' OO   OO'
&HEADC(144)  SETC  '  OOOOO '
&HEADC(145)  SETC  'P'
&HEADC(146)  SETC  ' PPPPPP '
&HEADC(147)  SETC  ' PP   PP'
&HEADC(148)  SETC  ' PP   PP'
&HEADC(149)  SETC  ' PP   PP'
&HEADC(150)  SETC  ' PPPPPP '
&HEADC(151)  SETC  ' PP     '
&HEADC(152)  SETC  ' PP     '
&HEADC(153)  SETC  ' PP     '
&HEADC(154)  SETC  'Q'
&HEADC(155)  SETC  '  QQQQQ '
&HEADC(156)  SETC  ' QQ   QQ'
&HEADC(157)  SETC  ' QQ   QQ'
&HEADC(158)  SETC  ' QQ   QQ'
&HEADC(159)  SETC  ' QQ Q QQ'
&HEADC(160)  SETC  ' QQ  QQQ'
&HEADC(161)  SETC  ' QQ   QQ'
&HEADC(162)  SETC  '  QQQQQQ'
&HEADC(163)  SETC  'R'
&HEADC(164)  SETC  ' RRRRRR '
&HEADC(165)  SETC  ' RR   RR'
&HEADC(166)  SETC  ' RR   RR'
&HEADC(167)  SETC  ' RR   RR'
&HEADC(168)  SETC  ' RRRRRR '
&HEADC(169)  SETC  ' RR RR  '
&HEADC(170)  SETC  ' RR  RR '
&HEADC(171)  SETC  ' RR   RR'
&HEADC(172)  SETC  'S'
&HEADC(173)  SETC  '  SSSSS '
&HEADC(174)  SETC  ' SS   SS'
&HEADC(175)  SETC  ' SS     '
&HEADC(176)  SETC  '  SSSSS '
&HEADC(177)  SETC  '      SS'
&HEADC(178)  SETC  ' SS   SS'
&HEADC(179)  SETC  ' SS   SS'
&HEADC(180)  SETC  '  SSSSS '
&HEADC(181)  SETC  'T'
&HEADC(182)  SETC  'TTTTTTTT'
&HEADC(183)  SETC  '   TT   '
&HEADC(184)  SETC  '   TT   '
&HEADC(185)  SETC  '   TT   '
&HEADC(186)  SETC  '   TT   '
&HEADC(187)  SETC  '   TT   '
&HEADC(188)  SETC  '   TT   '
&HEADC(189)  SETC  '   TT   '
&HEADC(190)  SETC  'U'
&HEADC(191)  SETC  ' UU   UU'
&HEADC(192)  SETC  ' UU   UU'
&HEADC(193)  SETC  ' UU   UU'
&HEADC(194)  SETC  ' UU   UU'
&HEADC(195)  SETC  ' UU   UU'
&HEADC(196)  SETC  ' UU   UU'
&HEADC(197)  SETC  ' UU   UU'
&HEADC(198)  SETC  '  UUUUU '
&HEADC(199)  SETC  'V'
&HEADC(200)  SETC  ' VV   VV'
&HEADC(201)  SETC  ' VV   VV'
&HEADC(202)  SETC  ' VV   VV'
&HEADC(203)  SETC  ' VV   VV'
&HEADC(204)  SETC  ' VV   VV'
&HEADC(205)  SETC  '  VV VV '
&HEADC(206)  SETC  '   VVV  '
&HEADC(207)  SETC  '    V   '
&HEADC(208)  SETC  'W'
&HEADC(209)  SETC  ' WW   WW'
&HEADC(210)  SETC  ' WW   WW'
&HEADC(211)  SETC  ' WW   WW'
&HEADC(212)  SETC  ' WW   WW'
&HEADC(213)  SETC  ' WW W WW'
&HEADC(214)  SETC  ' WWWWWWW'
&HEADC(215)  SETC  ' WWW WWW'
&HEADC(216)  SETC  '  W   W '
&HEADC(217)  SETC  'X'
&HEADC(218)  SETC  ' XX   XX'
&HEADC(219)  SETC  ' XX   XX'
&HEADC(220)  SETC  '  XX XX '
&HEADC(221)  SETC  '   XXX  '
&HEADC(222)  SETC  '   XXX  '
&HEADC(223)  SETC  '  XX XX '
&HEADC(224)  SETC  ' XX   XX'
&HEADC(225)  SETC  ' XX   XX'
&HEADC(226)  SETC  'Y'
&HEADC(227)  SETC  ' YY   YY'
&HEADC(228)  SETC  ' YY   YY'
&HEADC(229)  SETC  ' YY   YY'
&HEADC(230)  SETC  '  YY YY '
&HEADC(231)  SETC  '   YYY  '
&HEADC(232)  SETC  '   YY   '
&HEADC(233)  SETC  '   YY   '
&HEADC(234)  SETC  '   YY   '
&HEADC(235)  SETC  'Z'
&HEADC(236)  SETC  ' ZZZZZZZ'
&HEADC(237)  SETC  '      ZZ'
&HEADC(238)  SETC  '     ZZ '
&HEADC(239)  SETC  '    ZZ  '
&HEADC(240)  SETC  '   ZZ   '
&HEADC(241)  SETC  '  ZZ    '
&HEADC(242)  SETC  ' ZZ     '
&HEADC(243)  SETC  ' ZZZZZZZ'
&HEADC(244)  SETC  '1'
&HEADC(245)  SETC  '    11  '
&HEADC(246)  SETC  '   111  '
&HEADC(247)  SETC  '    11  '
&HEADC(248)  SETC  '    11  '
&HEADC(249)  SETC  '    11  '
&HEADC(250)  SETC  '    11  '
&HEADC(251)  SETC  '    11  '
&HEADC(252)  SETC  '   1111 '
&HEADC(253)  SETC  '2'
&HEADC(254)  SETC  '   2222 '
&HEADC(255)  SETC  '  22  22'
&HEADC(256)  SETC  '      22'
&HEADC(257)  SETC  '   2222 '
&HEADC(258)  SETC  '  22    '
&HEADC(259)  SETC  '  22    '
&HEADC(260)  SETC  '  22    '
&HEADC(261)  SETC  '  222222'
&HEADC(262)  SETC  '3'
&HEADC(263)  SETC  '   3333 '
&HEADC(264)  SETC  '  33  33'
&HEADC(265)  SETC  '      33'
&HEADC(266)  SETC  '    33  '
&HEADC(267)  SETC  '      33'
&HEADC(268)  SETC  '  33  33'
&HEADC(269)  SETC  '  33  33'
&HEADC(270)  SETC  '   3333 '
&HEADC(271)  SETC  '4'
&HEADC(272)  SETC  '     44 '
&HEADC(273)  SETC  '    444 '
&HEADC(274)  SETC  '   4444 '
&HEADC(275)  SETC  '  44 44 '
&HEADC(276)  SETC  ' 4444444'
&HEADC(277)  SETC  '     44 '
&HEADC(278)  SETC  '     44 '
&HEADC(279)  SETC  '     44 '
&HEADC(280)  SETC  '5'
&HEADC(281)  SETC  '  555555'
&HEADC(282)  SETC  '  55    '
&HEADC(283)  SETC  '  55    '
&HEADC(284)  SETC  '  55555 '
&HEADC(285)  SETC  '      55'
&HEADC(286)  SETC  '  55  55'
&HEADC(287)  SETC  '  55  55'
&HEADC(288)  SETC  '   5555 '
&HEADC(289)  SETC  '6'
&HEADC(290)  SETC  '   6666 '
&HEADC(291)  SETC  '  66  66'
&HEADC(292)  SETC  '  66    '
&HEADC(293)  SETC  '  66666 '
&HEADC(294)  SETC  '  66  66'
&HEADC(295)  SETC  '  66  66'
&HEADC(296)  SETC  '  66  66'
&HEADC(297)  SETC  '   6666 '
&HEADC(298)  SETC  '7'
&HEADC(299)  SETC  '  777777'
&HEADC(300)  SETC  '      77'
&HEADC(301)  SETC  '      77'
&HEADC(302)  SETC  '     77 '
&HEADC(303)  SETC  '    77  '
&HEADC(304)  SETC  '   77   '
&HEADC(305)  SETC  '   77   '
&HEADC(306)  SETC  '   77   '
&HEADC(307)  SETC  '8'
&HEADC(308)  SETC  '   8888 '
&HEADC(309)  SETC  '  88  88'
&HEADC(310)  SETC  '  88  88'
&HEADC(311)  SETC  '   8888 '
&HEADC(312)  SETC  '  88  88'
&HEADC(313)  SETC  '  88  88'
&HEADC(314)  SETC  '  88  88'
&HEADC(315)  SETC  '   8888 '
&HEADC(316)  SETC  '9'
&HEADC(317)  SETC  '   9999 '
&HEADC(318)  SETC  '  99  99'
&HEADC(319)  SETC  '  99  99'
&HEADC(320)  SETC  '  99  99'
&HEADC(321)  SETC  '   99999'
&HEADC(322)  SETC  '      99'
&HEADC(323)  SETC  '  99  99'
&HEADC(324)  SETC  '   9999 '
&HEADC(325)  SETC  '0'
&HEADC(326)  SETC  '   0000 '
&HEADC(327)  SETC  '  00  00'
&HEADC(328)  SETC  '  00  00'
&HEADC(329)  SETC  '  00 000'
&HEADC(330)  SETC  '  000 00'
&HEADC(331)  SETC  '  00  00'
&HEADC(332)  SETC  '  00  00'
&HEADC(333)  SETC  '   0000 '
&HEADC(334)  SETC  '$'
&HEADC(335)  SETC  '    $   '
&HEADC(336)  SETC  '  $$$$$ '
&HEADC(337)  SETC  ' $$ $ $$'
&HEADC(338)  SETC  ' $$ $   '
&HEADC(339)  SETC  '  $$$$$ '
&HEADC(340)  SETC  '    $ $$'
&HEADC(341)  SETC  ' $$ $ $$'
&HEADC(342)  SETC  '  $$$$$ '
&HEADC(343)  SETC  '#'
&HEADC(344)  SETC  '  ## ## '
&HEADC(345)  SETC  '  ## ## '
&HEADC(346)  SETC  ' #######'
&HEADC(347)  SETC  '  ## ## '
&HEADC(348)  SETC  '  ## ## '
&HEADC(349)  SETC  ' #######'
&HEADC(350)  SETC  '  ## ## '
&HEADC(351)  SETC  '  ## ## '
&HEADC(352)  SETC  '@'
&HEADC(353)  SETC  '  @@@@@ '
&HEADC(354)  SETC  ' @     @'
&HEADC(355)  SETC  ' @  @@ @'
&HEADC(356)  SETC  ' @ @ @ @'
&HEADC(357)  SETC  '  @  @ @'
&HEADC(358)  SETC  ' @   @ @'
&HEADC(359)  SETC  ' @   @ @'
&HEADC(360)  SETC  '  @@@ @ '
&HEADC(361)  SETC  ','
&HEADC(362)  SETC  '        '
&HEADC(363)  SETC  '        '
&HEADC(364)  SETC  '        '
&HEADC(365)  SETC  '        '
&HEADC(366)  SETC  '   ,,   '
&HEADC(367)  SETC  '   ,,   '
&HEADC(368)  SETC  '    ,   '
&HEADC(369)  SETC  '   ,    '
&HEADC(370)  SETC  '.'
&HEADC(371)  SETC  '        '
&HEADC(372)  SETC  '        '
&HEADC(373)  SETC  '        '
&HEADC(374)  SETC  '        '
&HEADC(375)  SETC  '        '
&HEADC(376)  SETC  '   ..   '
&HEADC(377)  SETC  '   ..   '
&HEADC(378)  SETC  '        '
&HEADC(379)  SETC  '('
&HEADC(380)  SETC  '    ((  '
&HEADC(381)  SETC  '   ((   '
&HEADC(382)  SETC  '  ((    '
&HEADC(383)  SETC  '  ((    '
&HEADC(384)  SETC  '  ((    '
&HEADC(385)  SETC  '  ((    '
&HEADC(386)  SETC  '   ((   '
&HEADC(387)  SETC  '    ((  '
&HEADC(388)  SETC  ')'
&HEADC(389)  SETC  '   ))   '
&HEADC(390)  SETC  '    ))  '
&HEADC(391)  SETC  '     )) '
&HEADC(392)  SETC  '     )) '
&HEADC(393)  SETC  '     )) '
&HEADC(394)  SETC  '     )) '
&HEADC(395)  SETC  '    ))  '
&HEADC(396)  SETC  '   ))   '
&HEADC(397)  SETC  '+'
&HEADC(398)  SETC  '        '
&HEADC(399)  SETC  '    +   '
&HEADC(400)  SETC  '    +   '
&HEADC(401)  SETC  '    +   '
&HEADC(402)  SETC  ' +++++++'
&HEADC(403)  SETC  '    +   '
&HEADC(404)  SETC  '    +   '
&HEADC(405)  SETC  '    +   '
&HEADC(406)  SETC  '/'
&HEADC(407)  SETC  '        '
&HEADC(408)  SETC  '      //'
&HEADC(409)  SETC  '     // '
&HEADC(410)  SETC  '    //  '
&HEADC(411)  SETC  '   //   '
&HEADC(412)  SETC  '  //    '
&HEADC(413)  SETC  ' //     '
&HEADC(414)  SETC  '//      '
&HEADC(415)  SETC  '"'
&HEADC(416)  SETC  '   ""   '
&HEADC(417)  SETC  '   ""   '
&HEADC(418)  SETC  '        '
&HEADC(419)  SETC  '        '
&HEADC(420)  SETC  '        '
&HEADC(421)  SETC  '        '
&HEADC(422)  SETC  '        '
&HEADC(423)  SETC  '        '
&HEADC(424)  SETC  '-'
&HEADC(425)  SETC  '        '
&HEADC(426)  SETC  '        '
&HEADC(427)  SETC  '        '
&HEADC(428)  SETC  '        '
&HEADC(429)  SETC  ' -------'
&HEADC(430)  SETC  ' -------'
&HEADC(431)  SETC  '        '
&HEADC(432)  SETC  '        '
&HEADC(433)  SETC  '*'
&HEADC(434)  SETC  '        '
&HEADC(435)  SETC  '  *   * '
&HEADC(436)  SETC  '   * *  '
&HEADC(437)  SETC  ' *******'
&HEADC(438)  SETC  '   * *  '
&HEADC(439)  SETC  '  *   * '
&HEADC(440)  SETC  '        '
&HEADC(441)  SETC  '        '
&HEADC(442)  SETC  'END'           MARKS END OF THE CHARACTER LIST
.PROC    ANOP  ,
.*
.*
         AIF   (K'&TITLE LE 0).NTITL
.*
         AIF   ('&TITLE' NE 'EJECT').NEJEC
         EJECT
         AGO   .NTITL
.*
.NEJEC   ANOP
&TITLE(1) TITLE &TITLE(2)
.*
.*
.NTITL   ANOP
.*
.*
&K       SETA  K'&STRING
         AIF   ((&K EQ 0) OR (&K GT 64)).DONE
.*
.*
         AIF   ('&BLOCK' EQ 'NO').NBLK1
 MNOTE   '**************************************************************
               **********'
&B       SETC  '*'
.NBLK1   ANOP  ,
.*
.*
.*
&M       SETA  1
.COPY2   ANOP  ,
&STR(&M) SETC  '&STRING'(&M,1)
&M       SETA  &M+1
         AIF   (&M LE &K).COPY2
.*
&J       SETA  0
&M       SETA  0
.*
.*
.EDIT    AIF   (&M EQ &K).AGIN
&M       SETA  &M+1
&I       SETA  &M
         AIF   ('&STR(&M)' NE '''').EDIT
.*
         AIF   (&J GT 0).QTCK
.*
&J       SETA  1
         AGO   .COMP
.*
.*
.QTCK    AIF   ('&STR(&M+1)' NE '''').COMP
.*
&STR(&M) SETC  '"'
&J       SETA  0
&M       SETA  &M+1
&I       SETA  &M
.*
.*
.COMP    ANOP
&STR(&I) SETC  '&STR(&I+1)'
&I       SETA  &I+1
         AIF   (&I LT &K).COMP
.*
&STR(&K) SETC  ' '
&K       SETA  &K-1
&M       SETA  &M-1
         AGO   .EDIT
.*
.*
.AGIN    ANOP
&I       SETA  1
&L       SETA  &K-&N
         AIF   (&L LE 0).ENDOFF
.*
         AIF   (&L LE 8).CK4
.*
&L       SETA  8
.*
.*
.CK4     ANOP
&J       SETA  (8-&L)/2*8
.*
.*
.FILT    AIF   (&I GT &J).SETL
.*
&F(&I)   SETC  '        '
&F(&I+1) SETC  '        '
&F(&I+2) SETC  '        '
&F(&I+3) SETC  '        '
&F(&I+4) SETC  '        '
&F(&I+5) SETC  '        '
&F(&I+6) SETC  '        '
&F(&I+7) SETC  '        '
&I       SETA  &I+8
         AGO   .FILT
.*
.*
.SETL    ANOP
&L       SETA  &L+&N
.*
.*
.LOOP    AIF   (&N EQ &L).FINI
.*
&N       SETA  &N+1
&J       SETA  1
&W       SETC  '&STR(&N)'
.*
.*
.MTCH    AIF   ('&W' EQ '&HEADC(&J)').GO
.*
&J       SETA  &J+9
         AIF   ('&HEADC(&J)' NE 'END').MTCH
&J       SETA  &J-9
.*
.*
.GO      ANOP
.*
&F(&I)   SETC  '&HEADC(&J+1)'
&F(&I+1) SETC  '&HEADC(&J+2)'
&F(&I+2) SETC  '&HEADC(&J+3)'
&F(&I+3) SETC  '&HEADC(&J+4)'
&F(&I+4) SETC  '&HEADC(&J+5)'
&F(&I+5) SETC  '&HEADC(&J+6)'
&F(&I+6) SETC  '&HEADC(&J+7)'
&F(&I+7) SETC  '&HEADC(&J+8)'
&I       SETA  &I+8
         AGO   .LOOP
.*
.*
.FINI    AIF   (&I GT 64).DOIT
.*
&F(&I)   SETC  '        '
&F(&I+1) SETC  '        '
&F(&I+2) SETC  '        '
&F(&I+3) SETC  '        '
&F(&I+4) SETC  '        '
&F(&I+5) SETC  '        '
&F(&I+6) SETC  '        '
&F(&I+7) SETC  '        '
&I       SETA  &I+8
         AGO   .FINI
.*
.*
.DOIT    AIF   ('&BLOCK' EQ 'NO').SPACE
 MNOTE   '*                                                            *
                        *'
 MNOTE   '*                                                            *
                        *'
         AGO   .DOIT2
.SPACE   ANOP   ,
         SPACE 2
.DOIT2   ANOP  ,
 MNOTE   '&B  &F(1)&F(9)&F(17)&F(25)&F(33)&F(41)&F(49)&F(57)   &B'
 MNOTE   '&B  &F(2)&F(10)&F(18)&F(26)&F(34)&F(42)&F(50)&F(58)   &B'
 MNOTE   '&B  &F(3)&F(11)&F(19)&F(27)&F(35)&F(43)&F(51)&F(59)   &B'
 MNOTE   '&B  &F(4)&F(12)&F(20)&F(28)&F(36)&F(44)&F(52)&F(60)   &B'
 MNOTE   '&B  &F(5)&F(13)&F(21)&F(29)&F(37)&F(45)&F(53)&F(61)   &B'
 MNOTE   '&B  &F(6)&F(14)&F(22)&F(30)&F(38)&F(46)&F(54)&F(62)   &B'
 MNOTE   '&B  &F(7)&F(15)&F(23)&F(31)&F(39)&F(47)&F(55)&F(63)   &B'
 MNOTE   '&B  &F(8)&F(16)&F(24)&F(32)&F(40)&F(48)&F(56)&F(64)   &B'
         AGO   .AGIN
.*
.*
.ENDOFF  AIF   ('&BLOCK' EQ 'NO').DONE
 MNOTE   '*                                                            *
                        *'
 MNOTE   '*                                                            *
                        *'
 MNOTE   '**************************************************************
               **********'
.*
.*
.DONE    ANOP
         SPACE 3
         MEND
./ ADD NAME=CLEAR    0101-00271-02078-2358-00044-00044-00001-SPGSRS
         MACRO
&LABEL   CLEAR &FIELD,&PAD=,&LEN=
.*
.*  CLEAR FIELD MACRO
.*   NOTE : THE ASSEMBLER WILL VALIDATE IF THE SUPPLIED
.*          PADDING CHARACTER IS VALID
.*          DEFAULT PADDING CHARACTER IS SPACES
.*
         LCLA  &W,&F
         LCLC  &L,&C,&R
&L       SETC  'L'''
&C       SETC  'C'' '''
         AIF   (T'&PAD EQ 'O').GO
&C       SETC  'C''&PAD'''
         AIF   (K'&PAD EQ 1).GO
&C       SETC  '&PAD'
.GO      ANOP
         AIF   ('&FIELD'(1,1) EQ '(').REGFORM
&LABEL   MVI   &FIELD,&C      PUT PADDING CHARACTER
         AIF   (T'&LEN NE 'O').WLEN
&F       SETA  0              F = FIELD POSITION
&W       SETA  K'&FIELD       W = LOOP COUNTER
.LOOP1   ANOP                 FIND OUT IF FIELD HAS DISPLACEMENT
&F       SETA  &F+1           NEXT POSITION
         AIF   ('&FIELD'(&F,1) EQ '+').WDISP
         AIF   ('&FIELD'(&F,1) EQ '-').WDISP
&W       SETA  &W-1           REDUCE LOOP COUNTER
         AIF   (&W GT 0).LOOP1
         MVC   &FIELD+1(&L&FIELD-1),&FIELD      CLEAR REST OF FIELD
         MEXIT
.WDISP   AIF   (T'&LEN EQ 'O').ERROR
.WLEN    ANOP
&W       SETA  &LEN-1
         MVC   &FIELD+1(&W),&FIELD              CLEAR REST OF FIELD
         MEXIT
.REGFORM AIF   (T'&LEN EQ 'O').ERROR
&W       SETA  &LEN-1
&LABEL   MVI   0&FIELD,&C     PUT PADDING CHARACTER
&F       SETA  K'&FIELD-2     GET REGISTER LENGTH
&R       SETC  '&FIELD'(2,&F) R = REGISTER NO.
         MVC   1(&W,&R),0&FIELD                 CLEAR REST OF FIELD
         MEXIT
.ERROR   MNOTE 8,'**** LENGTH NOT SPECIFIED ****'
         MEND
./ ADD NAME=EPA      0100-02164-02164-1458-00031-00031-00000-SPGSRS
         MACRO
&NAME    EPA   &TEXT,&AMODE=,&RMODE=
.*
.*  ENTRY POINT ADDRESS (EPA) MACRO
.*
.*  THIS MACRO CREATES AN ENTRY POINT CSECT AND AN ENTRY POINT
.*  IDENTIFIER. IF NO IDENTIFIER TEXT IS SUPPLIED, THE CSECT
.*  NAME IS THE DEFAULT. THIS MACRO ALSO SAVES THE REGISTERS
.*  USING THE SAVE MACRO INSTRUCTION.
.*
         LCLA  &L1
         LCLC  &T1,&T2
         AIF   (T'&NAME  EQ 'O').NOCSECT
&T1      SETC  '&NAME'             SET DEFAULT VALUE
&T2      SETC  '&NAME'
         AIF   (T'&TEXT  EQ 'O').HEAD
&L1      SETA  K'&TEXT-2           GET LENGTH OF TEXT W/O APOSTROPHE'S
&T1      SETC  '&TEXT'(2,&L1)      EXTRACT TEXT INSIDE APOSTROPHE'S
&T2      SETC  '&NAME - &T1'       ASSEMBLE TEXT
.HEAD    ANOP
         $HEAD &NAME,TITLE=(&NAME,' - &T1')
&NAME    CSECT
         AIF   (T'&AMODE EQ 'O').RMODE
&NAME    AMODE &AMODE
.RMODE   AIF   (T'&RMODE EQ 'O').SAVE
&NAME    RMODE &RMODE
.SAVE    ANOP
         SAVE  (14,12),,&T2
         MEXIT
.NOCSECT MNOTE 12,'ENTRY ADDRESS OR CSECT NOT SPECIFIED'
         MEND
./ ADD NAME=MSG      0101-00271-02078-2358-00012-00012-00000-SPGSRS
         MACRO
&LABEL   MSG   &TEXT
.*       GENERATES A MESSAGE TEXT
.*  FORMAT:
.*       BYTE 0 - AL2 - LENGTH OF TEXT + 2 (INCLUDES THESE BYTES)
.*       BYTE 2 - C   - TEXT
.*
         LCLA  &A
&A       SETA  K'&TEXT
&LABEL   DC    AL2(&A)
         DC    C&TEXT
         MEND
./ ADD NAME=MSGCP    0101-00271-02078-2359-00035-00035-00000-SPGSRS
         MACRO
&LABEL   MSGCP &TEXT,&OFFSET,&LAST
.*       GENERATES A MESSAGE TEXT FOR COMMAND PROCESSORS
.*  FORMAT:
.*       WORD 0 - A   - ADDRESS OF NEXT SEGMENT (FOR MULTI LINE MSGS)
.*       BYTE 0 - AL2 - LENGTH OF TEXT + 4 (INCLUDES THESE BYTES)
.*       BYTE 2 - AL2 - OFFSET TO TEXT
.*       BYTE 4 - C   - TEXT
.*  -- ANY VALUE PLACED IN THE LAST OPERAND WILL MAKE THIS THE
.*     LAST LINE IN A MULTI LINE MESSAGE
.*
         GBLC  &MSGCPL
         LCLA  &A,&B
&A       SETA  K'&TEXT+2
&B       SETA  0
         AIF   (T'&OFFSET EQ 'O').OK
&B       SETA  &OFFSET
.OK      ANOP
         AIF   ('&MSGCPL' EQ '').SINGLE
&MSGCPL  DS    0A
         AIF   (T'&LAST EQ 'O').NOTLAST
&MSGCPL  SETC  ''
         DC    A(0)
         AGO   .COMMON
.NOTLAST ANOP
&MSGCPL  SETC  '$MSG&SYSNDX'
         DC    A(&MSGCPL)
         AGO   .COMMON
.SINGLE  ANOP
&LABEL   DS    0A
.COMMON  ANOP
         DC    AL2(&A)
         DC    AL2(&B)
         DC    C&TEXT
         MEND
./ ADD NAME=MSGLIST  0101-00271-02078-2359-00007-00007-00000-SPGSRS
         MACRO
&LABEL   MSGLIST
.*       GENERATES A MESSAGE LIST FOR COMMAND PROCESSORS
         GBLC  &MSGCPL
&LABEL   DS    0A
&MSGCPL  SETC  '$MSG&SYSNDX'
         MEND
./ ADD NAME=MVTXT    0101-00271-02078-2359-00015-00015-00001-SPGSRS
         MACRO
&LABEL   MVTXT &FIELD,&TEXT
.*
.*  MOVES A CONSTANT TO A FIELD
.*
         LCLA  &TL
         LCLC  &C
&TL      SETA  K'&TEXT-2      REMOVE QUOTES
&C       SETC  '&FIELD(1)'
         AIF   ('&FIELD'(1,1) EQ '(').REGFORM
&LABEL   MVC   &C.(&TL),=C&TEXT    MOVE CONSTANT
         MEXIT
.REGFORM ANOP
&LABEL   MVC   0(&TL,&C),=C&TEXT   MOVE CONSTANT
         MEND
./ ADD NAME=PARSEMSG 0101-00271-02079-0000-00026-00026-00001-SPGSRS
         MACRO
         PARSEMSG
         LCLC  &L1,&L2,&L3,&L4,&L5,&L6,&L7
&L1      SETC  'PC04&SYSNDX'
&L2      SETC  'PC08&SYSNDX'
&L3      SETC  'PC0C&SYSNDX'
&L4      SETC  'PC10&SYSNDX'
&L5      SETC  'PC14&SYSNDX'
&L6      SETC  'PC18&SYSNDX'
&L7      SETC  'PC1C&SYSNDX'
&L1      MSGCP 'SYNTAX ERROR - COMMAND CANNOT PROMPT'
&L2      MSGCP 'ATTENTION INTERRUPT OCCURRED'
&L3      MSGCP 'PCL ERROR'
&L4      MSGCP 'NECESSARY AREA COULD NOT BE OBTAINED'
&L5      MSGCP 'VALIDITY CHECK ROUTINE TERMINATED PROCESSING'
&L6      MSGCP 'PCE ERROR'
&L7      MSGCP 'TERMINAL WAS DISCONNECTED'
PARSEMSG DC    A(0)
         DC    A(&L1)
         DC    A(&L2)
         DC    A(&L3)
         DC    A(&L4)
         DC    A(&L5)
         DC    A(&L6)
         DC    A(&L7)
         MEND
./ ADD NAME=SBA      0107-01148-02079-0000-00035-00025-00035-SPGSRS
.*       SET BUFFER ADDRESS MACRO FOR 3270 TERMINALS
.*
.*       USAGE:     SBA ROW,COL,LEN
.*       WHERE LEN IS AN OPTIONAL VALUE (DEFAULT IS 80) OF THE
.*             COLUMN WIDTH OF THE TERMINAL
.*
         MACRO
&LABEL   SBA   &LN,&CL,&LL
         LCLA  &VAL,&IX,&LINELEN
         LCLC  &R,&C         ROW,COL
         LCLC  &H,&L         HIGH & LOW NIBBLE
.*             ----------------------
.*             INITIALIZE BIT PATTERN
.*             TABLE IS SET AS HIGH AND LOW NIBBLES TO AVOID
.*             CODING PROBLEMS W/ THE '&' OR X'50' CHARACTER
.*             ----------------------
&H       SETC  '4CCCCCCCCC4444445DDDDDDDDD55555566EEEEEEEE666666'
&L       SETC  '0123456789ABCDEF0123456789ABCDEF0123456789ABCDEF'
&H       SETC  '&H'.'FFFFFFFFFF777777'
&L       SETC  '&L'.'0123456789ABCDEF'
.*             ----------------------
&LINELEN SETA  80
         AIF   (T'&LL EQ 'O').VAL
&LINELEN SETA  &LL
.VAL     ANOP
&VAL     SETA  &LINELEN*(&LN-1)+(&CL-1)
.*                           CALCULATE ROW VALUE
&IX      SETA  &VAL/64+1     SRL RX,6 -> 11XXXXXX -> 00000011 DROP X
&R       SETC  '&H'(&IX,1).'&L'(&IX,1)  SET BYTE 1 HIGH & LOW NIBBLE
.*                           CALCULATE COL VALUE
&IX      SETA  &VAL-((&VAL/64)*64)+1
&C       SETC  '&H'(&IX,1).'&L'(&IX,1)  SET BYTE 2 HIGH & LOW NIBBLE
.SBAEND  ANOP
&LABEL   DC    X'11&R&C'     SBA ORDER+BYTE1+BYTE2
         MEND
./ ADD NAME=SF       0107-00271-02079-0001-00080-00085-00000-SPGSRS
         MACRO
&LABEL   SF    &ATTR
         LCLA  &IX,&VAL
         LCLC  &B1,&B2,&BIT2
         LCLB  &PROT,&NUM,&LU,&LD,&LDH,&NON,&MDT,&CUR
         LCLB  &BLK,&RVS,&OVL,&UDL,&VTL
         LCLC  &H,&L         HIGH & LOW NIBBLE BIT PATTERNS
.*             ----------------------
.*             INITIALIZE BIT PATTERN
.*             TABLE IS SET AS HIGH AND LOW NIBBLES TO AVOID
.*             CODING PROBLEMS W/ THE '&' OR X'50' CHARACTER
.*             ----------------------
&H       SETC  '4CCCCCCCCC4444445DDDDDDDDD55555566EEEEEEEE666666'
&L       SETC  '0123456789ABCDEF0123456789ABCDEF0123456789ABCDEF'
&H       SETC  '&H'.'FFFFFFFFFF777777'
&L       SETC  '&L'.'0123456789ABCDEF'
.*             ----------------------
&BIT2    SETC  '00'
&PROT    SETB  0
&NUM     SETB  0
&LU      SETB  0
&LD      SETB  0
&LDH     SETB  0
&NON     SETB  0
&MDT     SETB  0
&BLK     SETB  0
&RVS     SETB  0
&OVL     SETB  0
&UDL     SETB  0
&VTL     SETB  0
&CUR     SETB  0
&IX      SETA  N'&ATTR+1
.LOOP    AIF   (&IX EQ 1).SETBIT
&IX      SETA  &IX-1
         AIF   ('&ATTR(&IX)' NE 'CUR').SKIP
&CUR     SETB  1
.SKIP    AIF   ('&ATTR(&IX)' NE 'SKIP').PROT
&PROT    SETB  1
&NUM     SETB  1
.PROT    AIF   ('&ATTR(&IX)' NE 'PROT').NUM
&PROT    SETB  1
.NUM     AIF   ('&ATTR(&IX)' NE 'NUM').NON
&NUM     SETB  1
.NON     AIF   ('&ATTR(&IX)' NE 'NON').LD
&BIT2    SETC  '11'
         AGO   .MDT
.LD      AIF   ('&ATTR(&IX)' NE 'LD').LDH
&BIT2    SETC  '01'
         AGO   .MDT
.LDH     AIF   ('&ATTR(&IX)' NE 'LDH').MDT
&BIT2    SETC  '10'
.MDT     AIF   ('&ATTR(&IX)' NE 'MDT').BLK
&MDT     SETB  1
.BLK     AIF   ('&ATTR(&IX)' NE 'BLK').RVS
&BLK     SETB  1
.RVS     AIF   ('&ATTR(&IX)' NE 'RVS').OVL
&RVS     SETB  1
.OVL     AIF   ('&ATTR(&IX)' NE 'OVL').UDL
&OVL     SETB  1
.UDL     AIF   ('&ATTR(&IX)' NE 'UDL').VTL
&UDL     SETB  1
.VTL     AIF   ('&ATTR(&IX)' NE 'VTL').NEXT
&VTL     SETB  1
.NEXT    AGO   .LOOP
.SETBIT  ANOP
&B1      SETC  'B''&PROT&NUM&BIT2'.'0'.'&MDT'''
&VAL     SETA  &B1+1         BINARY TO DECIMAL
&B1      SETC  '&H'(&VAL,1).'&L'(&VAL,1)
.SFTEST  AIF   (NOT(&BLK OR &RVS OR &OVL OR &UDL OR &VTL)).SF
&B2      SETC  'B''&BLK&RVS&OVL&UDL&VTL'.'0'''
&VAL     SETA  &B2+1         BINARY TO DECIMAL
&B2      SETC  '&H'(&VAL,1).'&L'(&VAL,1)
&LABEL   DC    X'1B&B1&B2'   SFE ORDER+BYTE1+BYTE2
         AGO   .CURCHK
.SF      ANOP
&LABEL   DC    X'1D&B1'      SF ORDER+BYTE1
.CURCHK  AIF   (NOT(&CUR)).MXIT
         DC    X'13'         SET CURSOR TO THIS FIELD
.MXIT    ANOP
         MEND
