PAUSE    EPA   'SENDS A MESSAGE TO THE OPERATOR AND WAITS'
         BALR  12,0                12 IS BASE REGISTER
         USING *,12                TELL ASSEMBLER
         USING CMDLINE,11
         USING WORKSTG,13
         L     11,0(,1)            GET ADDRESS OF PARM LINE
         LA    0,WKLEN
         GETMAIN R,LV=(0)
         ST    13,4(,1)            SAVE CALLER'S SVAREA ADDRESS
         ST    1,8(,13)            SET BACKWARD CHAIN
         LR    13,1                SET FORWARD CHAIN
         LH    2,CMDLEN
         LA    8,DEFLTMSG          USE DEFAULT MESSAGE IF NONE GIVEN
         CHI   2,1                 ANY MESSAGE PASSED?
NOPARMS  BL    SENDMSG             NO -- OTHERWISE
         LR    8,11                USE MESSAGE FROM PARM LINE
SENDMSG  LA    0,WTOLEN            INPUT --
         GETMAIN R,LV=(0)             R8 -- ADDRESS OF MESSAGE
         LR    3,1                    R3 -- WTO CONTROL BLOCK
         MVC   0(WTOLEN,3),WTOPUT
         WTO   TEXT=(8),MF=(E,(3)) SEND MESSAGE TO CONSOLE
WAIT     LA    4,ECB               INPUT --
         LA    2,RPLY                 R2 -- ADDRESS OF REPLY AREA
         LA    8,WTOGET               R8 -- TEXT ADDRESS
         OI    12(3),X'08'            SET REPLY
         WTOR  TEXT=((8),(2),1,(4)),MF=(E,(3),EXTENDED)
         WAIT  ECB=(4)                R4 -- ECB ADDRESS
         LA    0,WTOLEN
         FREEMAIN R,LV=(0),A=(3)
         CLI   RPLY,C'X'
         BNE   CODE0
         ABEND 222                 ABEND THE JOB CODE=222
CODE0    LR    1,13
         L     13,4(,1)
         LA    0,WKLEN
         FREEMAIN R,LV=(0),A=(1)
         XR    15,15               SET RC=0
         RETURN (14,12),,RC=(15)   RESTORE REGS AND QUIT
WTOPUT   WTO   TEXT=,MF=L
WTOLEN   EQU   *-WTOPUT
DEFLTMSG MSG   'JOB NOW IN PAUSE STATE, ENTER REPLY'
WTOGET   MSG   'REPLY ANY KEY TO PROCEED, -X- TO END'
WORKSTG  DSECT
         DS    18F
RPLY     DS    1F
ECB      DS    1F
WKLEN    EQU   *-WORKSTG
CMDLINE  DSECT
CMDLEN   DS    1H
MSG      DS    C                   MESSAGE TO BE SENT
         END
