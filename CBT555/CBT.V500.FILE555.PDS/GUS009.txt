***********************************************
*                                             *
*  PARMS:                                     *
*     YYYYMMDD = PIC 9(8)  INPUT              *
*       RC     = 1F BINARY RETURNED OPTIONAL  *
*  REGISTERS ON RETURN:                       *
*     1 - 14   AS IS                          *
*     15       RETURN CODE                    *
*              0 NORMAL                       *
*              8 INPUT DATE IS INVALID        *
*                                             *
***********************************************
GUS009   EPA   'DATE VALIDATION ROUTINE'
         BALR  12,0                12 IS BASE REGISTER
         USING *,12                TELL ASSEMBLER
         USING WORKSTG,11
         USING LINKAGE,10
         LR    10,1
         LA    0,WKLEN
         GETMAIN R,LV=(0)          ALLOC VS FOR WS STORAGE
         LR    11,1
         ST    13,STACK+4          SAVE CALLER'S SVAREA ADDRESS
         LA    9,STACK             LOAD SAVE AREA JUST IN CASE
         ST    9,8(,13)            SET BACKWARD CHAIN
         LR    13,9                SET FORWARD CHAIN
         L     2,IEFIN
         XR    15,15
         PACK  WORK(8),0(8,2)      GET YEAR FIELD
         DP    WORK(8),=P'4'       CHECK IF DIVISIBLE BY 4
         MVI   LYI,C'0'            DEFAULT = NOT LEAP YEAR
         CP    WORK+7(1),=P'0'     LEAP YEAR ?
         BNE   GETMM               NO
         MVI   LYI,C'1'
         PACK  WORK(8),0(8,2)      CHECK FOR CENTURY YEAR
         DP    WORK(8),=P'100'     CHECK IF DIVISIBLE BY 100
         CP    WORK+6(2),=P'0'     CENTURY YEAR ?
         BNE   GETMM               NO
         MVI   LYI,C'0'            DEFAULT = NOT LEAP YEAR
         PACK  WORK(8),0(8,2)      CHECK FOR CENTURY YEAR
         DP    WORK(8),=P'400'     CHECK IF DIVISIBLE BY 400
         CP    WORK+6(2),=P'0'     LEAP YEAR ?
         BNE   GETMM               NO
         MVI   LYI,C'1'            SET TO LEAP YEAR
GETMM    PACK  WORK(8),4(2,2)      GET MONTH FIELD
         CP    WORK+4(4),=P'1'     MM < 1
         BL    INVALID
         CP    WORK+4(4),=P'12'    MM > 12
         BH    INVALID
         CVB   4,WORK              USE MM AS INDEX
         XR    5,5
         IC    5,TBL-1(4)          GET TBL1(MM) VALUE
         CP    WORK+4(4),=P'2'     FEB ?
         BNE   GETDD
         CLI   LYI,C'1'            LEAP YEAR
         BNE   GETDD
         LA    5,1(,5)             ADD 1 DAY TO DAYS LIMIT
GETDD    PACK  WORK(8),6(2,2)      GET DAY FIELD
         CVB   6,WORK              CONVERT TO BINARY
         LTR   6,6                 LESS THAN 1 DAY ?
         BZ    INVALID
         CR    5,6
         BL    INVALID             UPPER LIMIT < GIVEN DAY
EXIT     LR    9,15                SAVE RETURN CODE
         TM    IEFIN,X'80'         LAST PARM ?
         BO    *+12                YEAH
         L     8,IEFRC             GET RC ADDRESS
         ST    9,0(,8)             PASS RC IF REQUESTED
         L     13,STACK+4
         LA    0,WKLEN
         FREEMAIN R,LV=(0),A=(11)
         LR    15,9                SET RC
         RETURN (14,12),,RC=(15)   RESTORE AND QUIT
INVALID  LA    15,8
         B     EXIT
TBL      DC    AL1(31)
         DC    AL1(28)
         DC    AL1(31)
         DC    AL1(30)
         DC    AL1(31)
         DC    AL1(30)
         DC    AL1(31)
         DC    AL1(31)
         DC    AL1(30)
         DC    AL1(31)
         DC    AL1(30)
         DC    AL1(31)
         LTORG
WORKSTG  DSECT
STACK    DS    18F
         DS    0D                  DOUBLE WORD ALIGN
WORK     DS    1F
         DS    1F
LYI      DS    CL1                 LEAP YEAR INDICATOR
WKLEN    EQU   *-WORKSTG
LINKAGE  DSECT
IEFIN    DS    A
IEFRC    DS    A
         END
