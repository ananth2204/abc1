LIST     EPA   'DISPLAYS CONTENTS OF DATASET ENTERED FROM COMMAND LINE'
         BALR  12,0
         USING *,12
         LR    6,1                      SAVE ADDR OF CPPL
*
         L     0,LVLEN
         GETMAIN R,LV=(0)
         ST    13,4(,1)
         ST    1,8(,13)
         LR    13,1
         USING WORKAREA,13
         LOAD  EPLOC=$$DAIR
         ST    0,GUS012
         LOAD  EPLOC=$$PUTL
         ST    0,GUS016
         LOAD  EPLOC=$$PARS
         ST    0,GUS014
         XR    15,15                    SET UP PUTLINE PARMLIST
         ST    6,$GUS016                CPPL
         ST    15,$GUS016+4             GETMAIN AREA
*
         L     0,=V(MAINPCL)
         L     15,GUS014
         LA    2,ANSAREA
         CALL  (15),((6),(0),(2)),VL,   PARSE COMMAND LINE FOR DSNAME  X
               MF=(E,$GUS014)
         LTR   9,15
         BNZ   PARSERR
*
         L     6,ANSAREA
         USING MAINPDL,6
*
         TM    DSNAME+6,X'80'
         BNO   ERRNAM
*
GETDSN   L     10,DSNAME                DSNAME FROM COMMAND LINE
         LH    11,DSNAME+4              LENGTH OF DSNAME TEXT
         BCTR  11,0                     1 LESS FOR EX INSTRUCTION
*                                       DSNAME OBTAINED IS QUALIFIED
         CLEAR IODDNM,LEN=60            BASED ON TSO RULES
         EX    11,MVDSN                 MOVE TO PARAMETER AREA
         L     10,DSNAME+8              GET MEMBER NAME IF SPECIFIED
         LH    11,DSNAME+8+4            LENGTH OF MEMBER NAME TEXT
         LTR   11,11                    WAS MEMBER NAME SPECIFIED?
         BZ    ALLOC
         BCTR  11,0                     1 LESS FOR EX INSTRUCTION
         EX    11,MVMEM                 MOVE TO PARAMETER AREA
*
ALLOC    MVI   IOFCODE,X'01'
         L     15,GUS012                ALLOCATE IT!
         CALL  (15),(IOERRCD,IOFCODE,IODDNM,IODSNM,IOMEMN),            +
               VL,MF=(E,$GUS012)
         LTR   9,15
         BZ    OPENFILE
         LA    11,ERROPEN               ALLOC FAILED
         BAL   14,$PUTLINE              TELL USER
         B     END                      R9 HAS RETCODE
*                                       SET UP I-O PARAMETERS
OPENFILE MVC   SYSDCB(DCBLEN),DCB1
         MVI   $$OPEN,X'80'             SET UP OPENLIST
         MVC   DCBDDNAM(8),IODDNM       DDNAME FROM DYNALLOC
         OPEN  SYSDCB,MF=(E,$$OPEN)
         LTR   9,15                     CHECK OPEN RETURN CODE
         BNZ   ERROR1
         LH    2,DCBLRECL
         LA    2,4(,2)                  ADD HEADERS
         STH   2,$LRECL                 SAVE IT
         GETMAIN RC,LV=(2)              STORAGE FOR RECORD AREA
         LR    10,1
         USING RECAREA,10
         XR    15,15
         STH   15,DATAOFF               LINE OFFSET IS 0
         LR    11,10                    PUTLINE IS R11
READIN   GET   SYSDCB,DATALINE
         LH    2,DCBLRECL               GET LENGHT OF THIS LINE
         LA    2,4(,2)                  ADD HEADERS
         STH   2,DATALEN                TELL PUTLINE
         BAL   14,$PUTLINE              PRINT THIS LINE
         B     READIN                   NEXT RECORD
*
EOD      EQU   *                        EOF OCCURRED
         CLOSE SYSDCB,MF=(E,$$OPEN)
FREEDS   MVI   IOFCODE,X'02'
         L     15,GUS012                FREE IT!
         CALL  (15),MF=(E,$GUS012)
         LH    0,$LRECL                 SAVE IT
         FREEMAIN RC,LV=(0),A=(10)      FREE STORAGE FOR RECORD AREA
         B     EXIT0
*
PARSERR  L     11,PARSEMSG(9)
         BAL   14,$PUTLINE
         B     END
*
ERRNAM   LA    11,ERRNAMM
         BAL   14,$PUTLINE
         B     END
*
EXIT0    EQU   *                        LET'S FINISH OFF
         XR    9,9                      SET RETURN CODE
*
END      EQU   *                        LET'S FINISH OFF
         L     6,ANSAREA
         IKJRLSA (6)                    RELEASE PARSE DATAAREA
         XR    14,14
         L     15,GUS016
         CALL  (15),(,,(14)),MF=(E,$GUS016) FREEMAIN PUTLINE
         DELETE EPLOC=$$DAIR
         DELETE EPLOC=$$PARS
         DELETE EPLOC=$$PUTL
         LR    1,13
         L     13,4(,13)
         LA    0,WORKLEN
         FREEMAIN R,LV=(0),A=(1)
*
         LR    15,9
         RETURN (14,12),RC=(15)
*---------------------------------------------------------------
$PUTLINE ST    14,R14SAVE
         L     15,GUS016
         CALL  (15),(,,(11)),VL,MF=(E,$GUS016)
         L     14,R14SAVE
         BR    14
*---------------------------------------------------------------
MVDSN    MVC   IODSNM(1),0(10)
MVMEM    MVC   IOMEMN(1),0(10)
*
ERROR1   LA    11,ERRINP
         BAL   14,$PUTLINE
         B     END
ERROR2   LH    2,0(1)
         CH    2,=X'0130'
         BNER  14
         LA    11,ERRMEM
         BAL   14,$PUTLINE
         B     END
         B     FREEDS                   FREE THE ALLOCATED DATASET
*
         TITLE 'D A T A   A R E A'
*
*        CONSTANTS
*
ERRNAMM  MSGCP 'DATASET NAME NOT FOUND IN PARSE*******'
ERROPEN  MSGCP 'ERROR IN ALLOCATING DATASET ******'
ERRMEM   MSGCP 'MEMBER NOT FOUND'
ERRINP   MSGCP 'DATASET NOT FOUND'
         PARSEMSG ,                     STANDARD PARSE ERROR MESSAGES
*
MAINPCL  IKJPARM DSECT=MAINPDL
DSNAME   IKJPOSIT DSTHING,USID,LIST,PROMPT='DATASET NAME',             X
               HELP='DSNAME WHOSE CONTENTS YOU WANT TO LIST'
         IKJENDP
LVLEN    DC    A(WORKLEN)
$$DAIR   DC    CL8'GUS012'
$$PARS   DC    CL8'GUS014'
$$PUTL   DC    CL8'GUS016'
DCB1     DCB   DDNAME=SYS00000,EODAD=EOD,MACRF=GM,DSORG=PS,EXLST=EXLST
DCBLEN   EQU   *-DCB1
EXLST    DC    XL4'09'
         DC    X'11',AL3(ERROR2)
         DC    X'80'
*
*        DATA AREA
*
WORKAREA DSECT
SAVEAREA DS    18F
GUS012   DS    V                 DYNALLOC
GUS014   DS    V                 PARSE
GUS016   DS    V                 PUTLINE
$GUS012  DS    4A                DAIR PARMLIST
$GUS014  DS    3A                PARSE
$GUS016  DS    3A                PUTLINE PARMLIST
IOERRCD  DS    1F
IOFCODE  DS    XL1
IODDNM   DS    CL8
IODSNM   DS    CL44
IOMEMN   DS    CL8
ANSAREA  DS    A
R14SAVE  DS    A
$$OPEN   OPEN  DCB1,MF=L
SYSDCB   DS    CL(DCBLEN)
DCBDDNAM EQU   SYSDCB+40         DDNAME
DCBBLKSI EQU   SYSDCB+62         BLKSIZE
DCBLRECL EQU   SYSDCB+82         RECLEN
DCBRECFM EQU   SYSDCB+36         RECFM
$LRECL   DS    H
WORKLEN  EQU   *-WORKAREA
*
RECAREA  DSECT
DATALEN  DS    H
DATAOFF  DS    H
DATALINE DS    0H
         PRINT NOGEN
         CVT   DSECT=YES
         END   LIST
