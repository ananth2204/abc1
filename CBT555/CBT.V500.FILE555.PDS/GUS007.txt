***********************************************
*                                             *
*  PARMS:                                     *
*       YYYYMMDD = PIC 9(8)                   *
*       DAY1     = PIC X(9)                   *
*                  DAY OF THE WEEK IN WORDS   *
*       DAY2     = PIC 9(1)                   *
*                  DAY OF THE WEEK IN CODE    *
*                  0-SAT 1-SUN 2-MON ETC.     *
*       RC     = 1F BINARY RETURNED OPTIONAL  *
*  REGISTERS ON RETURN:                       *
*     1 - 14   AS IS                          *
*     15       RETURN CODE                    *
*              0 NORMAL                       *
*              8 INPUT DATE IS INVALID        *
*                                             *
***********************************************
GUS007   EPA   'RETURNS THE DAY OF THE WEEK '
         LR    12,15               12 IS BASE REGISTER
         USING GUS007,12           TELL ASSEMBLER
         USING WORKSTG,13
         USING LINKAGE,11
         LR    11,1
         LA    0,WKLEN
         GETMAIN R,LV=(0)          ALLOC VS FOR WS STORAGE
         ST    13,4(,1)            SAVE CALLER'S SVAREA ADDRESS
         ST    1,8(,13)            SET BACKWARD CHAIN
         LR    13,1                SET FORWARD CHAIN
*-
*- USING ZELLER'S CONGRUENCE
*-
*- R7 = DD + TRUNC(((M + 1) * 26) / 10) + Y + (Y % 4)
*- R7 = R7 + TRUNC(C / 4) + 5 * C
*- DAYNR = R7 // 7
*-
         L     2,IEFIN
         PACK  WORK(8),0(4,2)      SETUP R5->YYYY
         CVB   5,WORK
         PACK  WORK(8),4(2,2)      SETUP R7->MM
         CVB   7,WORK
         PACK  WORK(8),6(2,2)      SETUP R8->DD
         CVB   8,WORK
         CHI   7,2                 IF MM < 3 THEN DO
         BH    F001                   MM = MM + 12
         LA    7,12(,7)               YY = YY-1
         BCTR  5,0                 END
F001     LA    7,1(,7)             R7=MM+1
         M     6,=F'26'            R7=(R7 * 26)
         D     6,=F'10'            R7=R7 / 10
         AR    7,8                 R7=R7 + DD
         XR    4,4
         D     4,=F'100'           R4=YY,R5=CC
         AR    7,4                 R7=R7 + YY
         LR    3,4
         XR    2,2
         D     2,=F'4'             R3=YY % 4
         AR    7,3                 R7=R7 + (Y % 4)
         LR    3,5
         XR    2,2                 DISCARD REMAINDER
         D     2,=F'4'             R3=CC % 4
         AR    7,3                 R7=R7 + C/4
         MHI   5,5                 5 * C
         AR    7,5                 ADD EM ALL UP!
         XR    6,6
         D     6,=F'7'             R6=R7 // 7
         LR    3,6                 R6=DAYNR
         L     8,IEFOUT1           GET OUTPUT ADDRESS
         MHI   3,9                 FIX LENGTH
         LA    4,DAYTAB(3)         GET ADDRESS OF DAY TEXT
         MVC   0(9,8),0(4)         PUT IT IN RESULT FIELD
         TM    IEFOUT1,X'80'       WAS DAY CODE REQUESTED ?
         BO    EXIT                NO, GO TO EXIT
         L     8,IEFOUT2           GET OUTPUT ADDRESS
         STC   6,0(,8)             STORE IT IN RESULT FIELD
         OI    0(8),X'F0'          AND UNPACK IT
EXIT     LR    9,15                SAVE RETURN CODE
         TM    IEFOUT1,X'80'       LAST PARM ?
         BO    *+20                YEAH
         TM    IEFOUT2,X'80'       LAST PARM ?
         BO    *+12                YEAH
         L     8,IEFRC             GET RC ADDRESS
         ST    9,0(8)              PASS RC IF REQUESTED
         LR    1,13
         L     13,4(,1)
         LA    0,WKLEN
         FREEMAIN R,LV=(0),A=(1)
         LR    15,9                SET RC
         RETURN (14,12),,RC=(15)   RESTORE AND QUIT
DAYTAB   DC    CL9'SATURDAY'
         DC    CL9'SUNDAY'
         DC    CL9'MONDAY'
         DC    CL9'TUESDAY'
         DC    CL9'WEDNESDAY'
         DC    CL9'THURSDAY'
         DC    CL9'FRIDAY'
         LTORG
WORKSTG  DSECT
         DS    18F
WORK     DS    D
WKLEN    EQU   *-WORKSTG
*
LINKAGE  DSECT
IEFIN    DS    A
IEFOUT1  DS    A
IEFOUT2  DS    A
IEFRC    DS    A
         END
