***********************************************
*                                             *
*  PARMS:                                     *
*       YYMMDD = PL4 PD    INPUT              *
*       YYDDD  = PL4 PD    RETURNED           *
*       RC     = 1F BINARY RETURNED OPTIONAL  *
*  REGISTERS ON RETURN:                       *
*     1 - 14   AS IS                          *
*     15       RETURN CODE                    *
*              0 NORMAL                       *
*              8 INPUT DATE IS INVALID        *
*                                             *
***********************************************
GUS002   EPA   'CONVERTS YYMMDD TO YYDDD FORMAT'
         BALR  12,0                12 IS BASE REGISTER
         USING *,12                TELL ASSEMBLER
         USING WORKSTG,11
         USING LINKAGE,10
         LR    10,1
         LA    0,WKLEN
         GETMAIN R,LV=(0)          ALLOC VS FOR WS STORAGE
         LR    11,1
         ST    13,STACK+4          SAVE CALLER'S SVAREA ADDRESS
         LA    9,STACK             LOAD SAVE AREA JUST IN CASE
         ST    9,8(,13)            SET BACKWARD CHAIN
         LR    13,9                SET FORWARD CHAIN
         L     2,IEFIN
         L     8,IEFOUT
         MVC   F2(2),0(2)          GET YEAR FIELD
         OI    F2+1,X'0F'          PACK IT
         ZAP   WORK(8),F2(2)       CLEAR WORK FIELD
         MP    WORK(8),=P'1000'    SET UP YY PART
         MVC   0(4,8),WORK+4       FILL RESULT FIELD W/ YY PART
         ZAP   WORK(4),F2(2)
         DP    WORK(4),=P'4'       CHECK IF DIVISIBLE BY 4
         MVI   LYI,C'0'            DEFAULT = NOT LEAP YEAR
         CP    WORK+3(1),=P'0'     LEAP YEAR ?
         BNE   *+8                 NO
         MVI   LYI,C'1'
         MVC   F2(2),1(2)          GET MONTH FIELD
         NI    F2,X'0F'            EDIT IT
         OI    F2+1,X'0F'          PACK IT
         CP    F2(2),=P'1'         MM < 1
         BL    INVALID
         CP    F2(2),=P'12'        MM > 12
         BH    INVALID
         ZAP   SVMM(2),F2(2)       SAVE MM, PACKED UNSIGNED
         ZAP   WORK(8),F2(2)
         CVB   4,WORK              USE MM AS INDEX
         AR    4,4                 FIX OFFSET
         LA    3,TBL1-2(4)         LOCATE CELL
         XR    5,5
         ICM   5,3,0(3)            GET TBL1(MM) VALUE
         CVD   5,WORK              & PACK IT
         AP    0(4,8),WORK+4(4)    ADD MONTH OFFSET
         MVC   F2(2),2(2)          GET DAY FIELD
         NI    F2,X'0F'            EDIT IT
         AP    0(4,8),F2(2)        ADD WHAT REMAINS
         CLI   LYI,C'0'            WAS THAT A LEAP YEAR ?
         BE    EXIT                NO, EXIT
         CP    SVMM(2),=P'2'
         BNH   EXIT
         AP    0(4,8),=P'1'
EXIT     OI    3(8),X'0F'          PACK RESULT UNSIGNED
         LR    9,15                SAVE RETURN CODE
         TM    IEFOUT,X'80'        LAST PARM ?
         BO    *+12                YEAH
         L     8,IEFRC             GET RC ADDRESS
         ST    9,0(,8)             PASS RC IF REQUESTED
         L     13,STACK+4
         LA    0,WKLEN
         FREEMAIN R,LV=(0),A=(11)
         LR    15,9                SET RC
         RETURN (14,12),,RC=(15)   RESTORE AND QUIT
INVALID  LA    15,8
         B     EXIT
TBL1     DC    AL2(0)
         DC    AL2(31)
         DC    AL2(59)
         DC    AL2(90)
         DC    AL2(120)
         DC    AL2(151)
         DC    AL2(181)
         DC    AL2(212)
         DC    AL2(243)
         DC    AL2(273)
         DC    AL2(304)
         DC    AL2(334)
         LTORG
WORKSTG  DSECT
STACK    DS    18F
         DS    0D                  DOUBLE WORD ALIGN
WORK     DS    1F
         DS    1F
F2       DS    CL2
SVMM     DS    CL2
LYI      DS    CL1                 LEAP YEAR INDICATOR
WKLEN    EQU   *-WORKSTG
LINKAGE  DSECT
IEFIN    DS    A
IEFOUT   DS    A
IEFRC    DS    A
         END
