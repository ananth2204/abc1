**********************************************************************
*                                                                    *
*  PARMS:                                                            *
*       CCPLADDR = CPPL ADDRESS                                      *
*       WORKADDR = ADDRESS SET BY THIS ROUTINE AS WORKAREA OBTAINED  *
*                  BY GETMAIN. IF ZERO IS SET THEN THIS CALL IS      *
*                  FIRST PASS. THIS IS DONE TO INITIALIZE THE        *
*                  IOPL ONLY ONCE AND NOT DURING EVERY CALL.         *
*                  IF SET AS LAST PARM, THEN THE REQUEST IS TO FREE  *
*                  THE AREA POINTED TO BY THIS ADDRESS.              *
*       PARMADDR = A NON ZERO VALUE WILL CAUSE A FREEMAIN TO THE     *
*                  ADDRESS CONTAINED BEFORE A NEW VALUE IS POSTED    *
*                  TO THIS FIELD. ON RETURN, THIS FIELD POINTS TO    *
*                  THE INPUT LINE BUFFER.                            *
*                                                                    *
*  REGISTERS ON RETURN:                                              *
*     1 - 14   AS IS                                                 *
*     15       RETURN CODE                                           *
*                                                                    *
**********************************************************************
GUS017   EPA   'GETLINE SERVICE ROUTINE'
         BALR  12,0                12 IS BASE REGISTER
         USING *,12                TELL ASSEMBLER
         USING LINKAGE,11
         USING WORKSTG,13
         USING GTPB,9
         USING CPPL,10
         USING IOPL,1
         LR    11,1
         L     10,WORKADDR
         LTR   10,10               FIRST PASS ?
         BZ    GETMAIN
         ST    13,4(,10)           SAVE CALLER'S SVAREA ADDRESS
         LR    13,10               ESTABLISH WORK AREAS
         TM    WORKADDR,X'80'      FREEMAIN REQUEST ?
         BO    EXITF
         LA    1,GETLST            ESTABLISH IOPL ADDRESS
         LA    9,WGTPB             ESTABLISH GTPB ADDRESS
         B     MAIN
GETMAIN  LA    0,WKLEN
         GETMAIN R,LV=(0)
         ST    13,4(,1)            SAVE CALLER'S SVAREA ADDRESS
         ST    1,8(,13)            SET BACKWARD CHAIN
         LR    13,1                SET FORWARD CHAIN
         ST    1,WORKADDR          SET FOR NEXT CALL
*
         XC    ECB,ECB             CLEAR ECB
         LA    1,GETLST            ESTABLISH IOPL ADDRESS
         LA    9,WGTPB             ESTABLISH GTPB ADDRESS
         L     10,CPPLADDR         GET CPPLADDR
         L     2,CPPLUPT
         L     3,CPPLECT
         LA    4,ECB
         LR    5,9                 GETLINE PARAMETER BLOCK
         STM   2,5,IOPL            SET UP IOPL CONTROL BLOCK
MAIN     MVC   WGTPB(8),TEMPLATE   SET UP GTPB
         L     2,PARMADDR
         LTR   2,2                 ANY ADDRESS TO FREE ?
         BZ    CALLTSSR            IF NONE GETLINE
         LH    0,0(,2)             GET LVLEN
         ICM   0,8,=X'01'          SPECIFY SUBPOOL 1
         FREEMAIN R,LV=(0),A=(2)
         LA    1,GETLST            RE-ESTABLISH IOPL ADDRESS
CALLTSSR CALLTSSR EP=IKJGETL,MF=(E,(1)) CALL PUTLINE ROUTINE
         LTR   8,15                SAVE PUTLINE RET CODE
         BNZ   EXIT                NORMAL END ?
         L     2,GTPBIBUF          GET FORMATTED LINE ADDRESS
         ST    2,PARMADDR          PASS IT
*
EXIT     L     13,STACK+4
         LR    15,8                SET RETURN CODE
         RETURN (14,12),,RC=(15)   RESTORE AND QUIT
EXITF    LR    1,13
         L     13,STACK+4
         LA    0,WKLEN
         FREEMAIN R,LV=(0),A=(1)
         ST    15,WORKADDR         ZERO OUT GETMAIN ADDRESS
         B     EXIT+4
TEMPLATE GETLINE MF=L
WORKSTG  DSECT
STACK    DS    18F
GETLST   DS    4A                  GETLINE PARAMETER LIST
ECB      DS    F
WGTPB    DS    2F
WKLEN    EQU   *-WORKSTG
LINKAGE  DSECT
CPPLADDR DS    A
WORKADDR DS    A
PARMADDR DS    A
         IKJCPPL
         IKJIOPL
         IKJGTPB
         CVT   DSECT=YES
         END
