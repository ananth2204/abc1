*
* THIS ROUTINE GETS THE CPUID OF A MACHINE THAT'S RUNNING MVS.
*
* CONTROL BLOCK CHAIN:                       MAP
* CVTPTR -> CVT                             (CVT)
*           CVTPCCAT-> PCCAVT               (IHAPCCAT)
*                      PCCAT00P-> PCCA
*                       ...        ...
*                      PCCAT15P-> PCCA      (IHAPCCA)
*                                 PCCACPID = CPU SERIAL NUMBER
*
*
* USAGE:  CALL 'GETCPUID' USING CPU-SERIALS.
*
* WHERE:  01  CPU-SERIALS.
*             02 CPU-ID    PIC X(12) OCCURS 16.
*
* FOR A POSSIBLE 16 CPU PER MACHINE
*
* CPU-ID (IX+1) WILL CONTAIN HIGH-VALUES IF CPU-ID (IX) IS THE
* LAST AVAILABLE CPU FOR THIS MACHINE.
*
* AUTHOR: ST. MON.
*
GETCPUID EPA   'GET THE CPU ID FROM MVS',AMODE=ANY,RMODE=ANY
         BALR  12,0
         USING *,12
*
* WE'RE NOT CALLING ANYTHING SO NO NEED TO SET UP A REGISTER SAVEAREA
*
         L     2,0(,1)            GET ADDRESS OF PARM AREA
         MVI   0(2),X'FF'
         MVC   1(191,2),0(2)      INITIALIZE TO HIGH-VALUES
         L     3,CVTPTR
         L     3,CVTPCCAT-CVT(,3) GET ADDRESS OF CCA VECTOR TABLE
         LA    5,16               POSSIBLE 16 CPUS FOR THIS MACHINE
LOOP     EQU   *
         ICM   4,B'1111',0(3)     ANY MORE CPUS?
         BZ    RETURN             NO MORE CPUS AVAILABLE
         MVC   0(12,2),PCCACPID-PCCA(4) MOVE CPUID TO PARAMETER AREA
         LA    3,4(,3)            NEXT CCA
         LA    2,12(,2)           NEXT CPUID SLOT
         BCT   5,LOOP
*
RETURN   XR    15,15
         RETURN (14,12),RC=(15)
*
         CVT   DSECT=YES
         IHAPCCAT
         IHAPCCA
         END
