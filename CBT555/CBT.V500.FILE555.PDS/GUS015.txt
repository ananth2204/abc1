GUS015   EPA   'SCAN SERVICE ROUTINE'
         BALR  12,0                12 IS BASE REGISTER
         USING *,12                TELL ASSEMBLER
         USING LINKAGE,11
         USING WORKSTG,13
         USING CPPL,10
         USING CSPL,1
         LR    11,1
         LA    0,WKLEN
GETMAIN  GETMAIN R,LV=(0)
         ST    13,4(,1)            SAVE CALLER'S SVAREA ADDRESS
         ST    1,8(,13)            SET BACKWARD CHAIN
         LR    13,1                SET FORWARD CHAIN
*
         LA    1,SCANLST           GET SCANLIST ADDRESS
         L     10,CPPLADDR
         L     3,CPPLUPT
         L     4,CPPLECT
         XC    ECB(8),ECB          CLEAR ECB AND CSPLFLAG
         LA    5,ECB
         LA    6,FLG
         L     7,CSOAADDR          CSPL0A
         L     8,BUFFADDR
         STM   3,8,CSPL            ASSEMBLE SCANLIST
         TM    BUFFADDR,X'80'      IF NOT SET AS LAST PARM THEN
         BNO   GOSCAN              STANDARD SYNTAX CHECK IS DONE
         MVI   FLG,X'80'           SET TO NONSTANDARD SYNTAX CHECK
GOSCAN   CALLTSSR EP=IKJSCAN,MF=(E,(1)) CALL SCAN ROUTINE
         LR    9,15                SAVE SCAN RET CODE
*
EXIT     LR    1,13
         L     13,STACK+4
         LA    0,WKLEN
         FREEMAIN R,LV=(0),A=(1)
         LR    15,9                SET RETURN CODE
         RETURN (14,12),,RC=(15)   RESTORE AND QUIT
WORKSTG  DSECT
STACK    DS    18F
SCANLST  DS    6A                  SCAN PARAMETER LIST
ECB      DS    F
FLG      DS    F                   FLAG
WKLEN    EQU   *-WORKSTG
LINKAGE  DSECT
CPPLADDR DS    A
CSOAADDR DS    A
BUFFADDR DS    A
         IKJCPPL
         IKJCSPL
         CVT   DSECT=YES
         END
