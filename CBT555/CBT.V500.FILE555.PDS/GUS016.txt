**********************************************************************
*                                                                    *
*  PARMS:                                                            *
*       CCPLADDR = CPPL ADDRESS                                      *
*       WORKADDR = ADDRESS SET BY THIS ROUTINE AS WORKAREA OBTAINED  *
*                  BY GETMAIN. IF ZERO IS SET THEN THIS CALL IS      *
*                  FIRST PASS. THIS IS DONE TO INITIALIZE THE        *
*                  IOPL ONLY ONCE AND NOT DURING EVERY CALL.         *
*                  IF SET AS LAST PARM, THEN THIS REQUEST IS TO FREE *
*                  THE ADDRESS CONTAINED IN THIS FIELD.              *
*       PARMADDR = IF EDITTING IS OR MULTI LINE DISPLAY IS REQ'RED   *
*                  THIS FIELD CONTAINS A PTPB (PUTLINE PARM BLOCK)   *
*                  ADDRESS. OTHERWISE THIS WOULD CONTAIN A DATA      *
*                  LINE ADDRESS. IF BIT 0 IS TURNED ON; PARMADDR     *
*                  CONTAINS A LINE ADDRESS. OTHERWISE A PTPB IS      *
*                  CONTAINED IN PARMADDR. IF ZERO IS CONTAINED; THEN *
*                  THIS REQUEST IS TO FREE THE ADDRESS CONTAINED IN  *
*                  WORKADDR                                          *
*                                                                    *
*  REGISTERS ON RETURN:                                              *
*     1 - 14   AS IS                                                 *
*     15       RETURN CODE                                           *
*                                                                    *
**********************************************************************
GUS016   EPA   'PUTLINE SERVICE ROUTINE'
         BALR  12,0                12 IS BASE REGISTER
         USING *,12                TELL ASSEMBLER
         USING LINKAGE,11
         USING WORKSTG,13
         USING PTPB,9
         USING CPPL,10
         USING IOPL,1
         LR    11,1
         L     10,WORKADDR
         LTR   10,10               FIRST PASS ?
         BZ    GETMAIN
         ST    13,4(,10)           SAVE CALLER'S SVAREA ADDRESS
         LR    13,10               ESTABLISH WORK AREAS
         TM    WORKADDR,X'80'      FREEMAIN REQUEST ?
         BO    EXITF
         LA    1,PUTLST            ESTABLISH IOPL ADDRESS
         LA    9,WPTPB             ESTABLISH PTPB ADDRESS
         B     MAIN
GETMAIN  LA    0,WKLEN
         GETMAIN R,LV=(0)
         ST    13,4(,1)            SAVE CALLER'S SVAREA ADDRESS
         ST    1,8(,13)            SET BACKWARD CHAIN
         LR    13,1                SET FORWARD CHAIN
         ST    1,WORKADDR          SET FOR NEXT CALL
*
         XC    ECB,ECB             CLEAR ECB
         LA    1,PUTLST            ESTABLISH IOPL ADDRESS
         LA    9,WPTPB             ESTABLISH PTPB ADDRESS
         L     10,CPPLADDR         GET CPPLADDR
         L     2,CPPLUPT
         L     3,CPPLECT
         LA    4,ECB
         LR    5,9                 PUTLINE PARAMETER BLOCK
         STM   2,5,IOPL            SET UP IOPL CONTROL BLOCK
MAIN     L     2,PARMADDR
         LTR   2,2                 FREEMAIN REQUEST ?
         BZ    EXITF               EXIT W/ FREEMAIN IF YES
         XC    FREEADDR,FREEADDR   CLEAR SAVED LINE ADDRESS
         TM    PARMADDR,X'80'      DATA LINE WAS PASSED ?
         BO    DATALINE
         MVC   WPTPB(12),0(2)      MOVE PTPB TO PARM BLOCK
         B     CALLTSSR            PUTLINE
DATALINE MVC   WPTPB(12),TEMPLATE  SET UP DATALINE PTPB
         ST    2,PTPBOPUT
CALLTSSR CALLTSSR EP=IKJPUTL,MF=(E,(1)) CALL PUTLINE ROUTINE
         LTR   8,15                SAVE PUTLINE RET CODE
         BNZ   EXIT                NORMAL END ?
         TM    PTPB+1,X'20'        EDIT REQUEST ?
         BNO   CLEANUP             NO
         L     2,PTPBFLN           GET FORMATTED LINE ADDRESS
         ST    2,FREEADDR
         B     DATALINE
*
CLEANUP  L     1,FREEADDR          ANY ADDRESS TO FREE ?
         LTR   1,1
         BZ    EXIT                NONE SO GET OUT
         LH    0,0(,1)             GET LVLEN
         ICM   0,8,=X'01'          SPECIFY SUBPOOL 1
         FREEMAIN R,LV=(0),A=(1)
EXIT     L     13,STACK+4
         LR    15,8                SET RETURN CODE
@EXIT    RETURN (14,12),,RC=(15)   RESTORE AND QUIT
EXITF    LR    1,13
         L     13,STACK+4
         LA    0,WKLEN
         FREEMAIN R,LV=(0),A=(1)
         ST    15,WORKADDR         ZERO OUT GETMAIN ADDRESS
         B     @EXIT
TEMPLATE PUTLINE OUTPUT=(TEMPLATE,DATA),MF=L
WORKSTG  DSECT
STACK    DS    18F
PUTLST   DS    4A                  PUTLINE PARAMETER LIST
ECB      DS    F
FREEADDR DS    F
WPTPB    DS    3F
WKLEN    EQU   *-WORKSTG
LINKAGE  DSECT
CPPLADDR DS    A
WORKADDR DS    A
PARMADDR DS    A
         IKJCPPL
         IKJIOPL
         IKJPTPB
         CVT   DSECT=YES
         END
