GUS014   EPA   'PARSE SERVICE ROUTINE'
         BALR  12,0                12 IS BASE REGISTER
         USING *,12                TELL ASSEMBLER
         USING LINKAGE,11
         USING WORKSTG,13
         USING CPPL,10
         USING PPL,1
         LR    11,1
         LA    0,WKLEN
         GETMAIN R,LV=(0)
         ST    13,4(,1)            SAVE CALLER'S SVAREA ADDRESS
         ST    1,8(,13)            SET BACKWARD CHAIN
         LR    13,1                SET FORWARD CHAIN
*
         LA    1,PARSELST          GET PARSELIST ADDRESS
         L     10,CPPLADDR
         L     3,CPPLUPT
         L     4,CPPLECT
         XC    ECB,ECB
         LA    5,ECB
         L     6,PCLADDR
         L     7,PDLADDR
         STM   3,7,PPL             ASSEMBLE PARSELIST
         TM    PDLADDR,X'80'       IF COMMAND ADDR NOT SPEC, THEN
         BO    PARSCPPL            COMMAND LINE IS TO BE PARSED
         MVC   PPLCBUF,CMDADDR
         B     GOPARSE
PARSCPPL MVC   PPLCBUF,CPPLCBUF
GOPARSE  ST    13,PPLUWA
         CALLTSSR EP=IKJPARS,MF=(E,(1)) CALL PARSE ROUTINE
         LR    9,15                SAVE PARSE RET CODE
*
         LR    1,13
         L     13,STACK+4
         LA    0,WKLEN
         FREEMAIN R,LV=(0),A=(1)
         LR    15,9                SET RETURN CODE
         RETURN (14,12),,RC=(15)   RESTORE AND QUIT
WORKSTG  DSECT
STACK    DS    18F
PARSELST DS    7A                  PARSE PARAMETER LIST
ECB      DS    F
WKLEN    EQU   *-WORKSTG
LINKAGE  DSECT
CPPLADDR DS    A
PCLADDR  DS    A
PDLADDR  DS    A
CMDADDR  DS    A
         IKJCPPL
         IKJPPL
         CVT   DSECT=YES,LIST=YES
         END
