FLEPRT   EPA   'PRINTS A SEQUENTIAL DATASET'
         BALR  12,0                12 IS BASE REGISTER
         USING *,12                TELL ASSEMBLER
         ST    13,STACK+4          SAVE CALLER'S SVAREA ADDRESS
         LA    10,STACK            LOAD SAVE AREA JUST IN CASE
         ST    10,8(,13)           SET BACKWARD CHAIN
         LR    13,10               SET FORWARD CHAIN
         RDJFCB SYSUT1             SET UP JFCB TO OBTAIN DSNAME
         OPEN  (SYSUT1,,LPPRINT,OUTPUT)
         MVC   HDSNAME(44),JFCBDSNM
         CLI   JFCBELNM,C' '       ANY MEMBER NAME??
         BE    GETRECFM            NONE SPECIFIED
         LA    11,HDSNAME+43       LAST CHARACTER OF DSNAME
NONSPC   CLI   0(11),C' '
         BNE   MVMEM               FOUND ADDDRESS OF LAST CHAR
         BCT   11,NONSPC           CHECK BACKWARDS FROM HERE
MVMEM    MVC   1(10,11),=C'  MEMBER ='
         MVC   12(8,11),JFCBELNM
GETRECFM CLI   DCBRECFM,X'C0'      THIS ROUTINE
         BNE   FMTRTN              WILL GET THE
         MVI   PRECFM,C'U'         RECORD FORMAT
         B     GETDCB
FMTRTN   CLI   DCBRECFM,X'00'
         BNE   *+14
         MVC   PRECFM(2),=C'**'
         B     GETDCB
         LA    2,PRECFM
         LA    3,TBL1              VALUES
         XR    4,4
         IC    4,=X'80'            BIT SWITCH
         LA    5,7                 LOOP COUNTER
LOOP     EX    4,TEST              TEST IF SWITCH IS ON
         BNO   ITER                GET NEXT FORM
         MVC   0(1,2),0(3)         SET RECFM DESC
         LA    2,1(,2)             NEXT COL
ITER     LA    3,1(,3)             NEXT VALUE
         SRL   4,1                 NEXT SWITCH
         BCT   5,LOOP
GETDCB   LH    2,DCBBLKSI
         CVD   2,DWAL
         MVC   EDTAREA(8),EDTCDE
         ED    EDTAREA(8),DWAL+4
         MVC   PBLKSI(6),EDTAREA+2
         LH    2,DCBLRECL          HOW LONG ?
         STH   2,RECLEN
         CVD   2,DWAL
         MVC   EDTAREA(8),EDTCDE
         ED    EDTAREA(8),DWAL+4
         MVC   PRECL(6),EDTAREA+2
         LH    0,DCBLRECL
         GETMAIN R,LV=(0)
         USING RECAREA,10          TELL ASSEMBLER WHERE WORK AREA IS
         LR    10,1                INITIALIZE BASE REGISTER
         CH    2,=H'120'
         BH    F1
         STH   2,LINLEN
F1       ZAP   LCTR(2),=P'66'
NXTREC   GET   SYSUT1,RECAREA
         AP    RCTR(4),=P'1'       COUNT THIS RECORD
         MVC   PAREA+1(8),EDTCDE
         ED    PAREA+1(8),RCTR
         LR    8,10                GET BUFFER ADDRESS
         LH    9,DCBLRECL          LOAD RECLEN
         CH    9,LINLEN            RECORDS WITH NO CONTINUATIONS
         BNH   PAD                 SHOULD BE SINGLE SPACED
         MVI   PAREA,C'0'          SET CONTROL CHARACTER
PAD      O     9,=X'40000000'      PUT PADDING CHARACTER
NXTLIN   AP    LCTR(2),=P'1'       ALLOC FOR NEW LINE
         CLI   PAREA,C'0'          DOUBLE SPACED ?
         BNE   CONT                NO IF ITS A CONTINUATION LINE
         AP    LCTR(2),=P'1'       TELL LINE COUNTER IT'S DOUBLE SPACED
CONT     CP    LCTR(2),=P'60'      BE SURE IT FITS THE PAGE
         BL    NOHEAD
         BAL   14,$$HDG
         MVC   PAREA+1(8),EDTCDE   RE-EDIT BECAUSE REC. NUM WAS
         ED    PAREA+1(8),RCTR     WIPED OUT
NOHEAD   LA    6,PAREA+10
         LH    7,LINLEN
         MVCL  6,8                 MOVE THE RECORD
         BAL   14,$$PLN            & PRINT IT
         MVI   PAREA,C' '          SINGLE SPACE CONTINUATION LINES
         MVC   PAREA+1(9),PAREA    & CLEAR LINE NO. FIELD
         C     9,=X'40000000'      MORE CHARACTERS ?
         BNE   NXTLIN
         B     NXTREC
EOD      CLOSE (SYSUT1,,LPPRINT)
         LH    0,RECLEN
         FREEMAIN R,LV=(0),A=(10) FREE VS USED
         L     13,STACK+4
         LM    14,12,12(13)        RESTORE AND
         XR    15,15               SET RC = 0
         BR    14                  QUIT
* SUBROUTINES
$$HDG    EQU   *                   HEADING ROUTINE
         ST    14,SVAREA
         BAL   14,$$SPC
         MVC   PAREA+10(H1LEN),HDG
         MVI   PAREA,C'1'          SET PAGE EJECT
         BAL   14,$$PLN
         MVC   PAREA+10(LN2LEN),LIN2
         MVI   PAREA,C'0'          SET CONTROL CHARACTER
         BAL   14,$$PLN
         BAL   14,$$SPC
         STM   4,7,SVAREA2
         LA    4,PAREA+1
         LH    5,LINLEN
         A     5,=F'9'             ADD OFFSET OF MARGIN
         LA    6,RULER
         LR    7,5
         MVCL  4,6
         LM    4,7,SVAREA2
         BAL   14,$$PLN
         ZAP   LCTR(2),=P'6'
         MVI   PAREA,C' '
         L     14,SVAREA
         BR    14
$$SPC    MVI   PAREA+1,X'40'
         MVC   PAREA+2(131),PAREA+1
         BR    14
$$PLN    ST    14,SVAREA+4
         PUT   LPPRINT,PAREA
         L     14,SVAREA+4
         BR    14
STACK    DS    18F
SVAREA   DS    2F
SVAREA2  DS    4F
UT1EXIT  DC    X'87'
         DC    AL3(INFMJFCB)
         IEFJFCBN LIST=YES
PAREA    DC    133C' '
RECLEN   DS    1H
LINLEN   DC    1H'100'
RCTR     DC    PL4'0'
LCTR     DS    PL2
DWAL     DS    1D
EDTAREA  DS    CL8
HDG      DC    C'CARD IMAGE OF '
HDSNAME  DC    44C' '
         DS    C'  MEMBER = XXXXXXXX'
H1LEN    EQU   *-HDG
LIN2     DC    C'RECORD LENGTH ='
PRECL    DS    CL6
         DC    C'     BLOCK LENGTH ='
PBLKSI   DS    CL6
         DC    C'     RECORD FORMAT = '
PRECFM   DC    CL5' '
LN2LEN   EQU   *-LIN2
EDTCDE   DC    X'4020202020202020'
RULER    DC    C'  REC #  '
         DC    C'....+....1....+....2....+....3'
         DC    C'....+....4....+....5....+....6'
         DC    C'....+....7....+....8....+....9'
         DC    C'....+....0....+....1....+....2'
TBL1     DC    C'FVTBSAM'
         PRINT NOGEN
TEST     TM    DCBRECFM,X'00'
SYSUT1   DCB   DDNAME=SYSUT1,DSORG=PS,MACRF=GM,EXLST=UT1EXIT,          X
               EODAD=EOD
DCBBLKSI EQU   SYSUT1+62
DCBLRECL EQU   SYSUT1+82
DCBRECFM EQU   SYSUT1+X'24'
LPPRINT  DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=PM,                      X
               BLKSIZE=133,LRECL=133,                                  X
               RECFM=FA
RECAREA  DSECT
         END
