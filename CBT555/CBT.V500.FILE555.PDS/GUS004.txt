***********************************************
*                                             *
*  PARMS:                                     *
*       YYDDD  = PL4 PD    INPUT              *
*       YYMMDD = PL4 PD    RETURNED           *
*       RC     = 1F BINARY RETURNED OPTIONAL  *
*  REGISTERS ON RETURN:                       *
*     1 - 14   AS IS                          *
*     15       RETURN CODE                    *
*              0 NORMAL                       *
*              8 INPUT DATE IS INVALID        *
*                                             *
***********************************************
GUS004   EPA   'CONVERTS YYDDD TO YYMMDD FORMAT '
         LR    12,15               12 IS BASE REGISTER
         USING GUS004,12           TELL ASSEMBLER
         USING WORKSTG,11
         USING LINKAGE,10
         LR    10,1
         LA    0,WKLEN
         GETMAIN R,LV=(0)          ALLOC VS FOR WS STORAGE
         LR    11,1
         ST    13,STACK+4          SAVE CALLER'S SVAREA ADDRESS
         LA    9,STACK             LOAD SAVE AREA JUST IN CASE
         ST    9,8(,13)            SET BACKWARD CHAIN
         LR    13,9                SET FORWARD CHAIN
         L     2,IEFIN
         L     8,IEFOUT
         XR    3,3
         ICM   3,3,1(2)            GET YY FIELD
         SRL   3,4                 SET TO PACK
         STCM  3,3,F2              SAVE YEAR FIELD (YY0)
         OI    F2+1,X'0F'          PACK IT         (YYF)
         ZAP   WORK(8),F2(2)       CLEAR WORK FIELD
         MP    WORK(8),=P'10000'   SET UP YY PART
         MVC   0(4,8),WORK+4       FILL RESULT FIELD W/ YY PART
         ZAP   WORK(4),F2(2)
         DP    WORK(4),=P'4'       CHECK IF DIVISIBLE BY 4
         MVI   LYI,C'0'            DEFAULT = NOT LEAP YEAR
         CP    WORK+3(1),=P'0'     LEAP YEAR ?
         BNE   GETDDD              NO
         MVI   LYI,C'1'
GETDDD   MVC   F2(2),2(2)          GET MONTH FIELD
         CP    F2(2),=P'366'       INVALID DATE ?
         BH    INVALID             REJECT
         ZAP   WORK(8),F2(2)       GET MONTH & DAY
         CVB   3,WORK
         LA    2,12                LOOP COUNTER; 12 CHARS
         LA    6,TBL1-2            DATE ARRAY
         CLI   LYI,C'0'
         BE    LOOP1
         LA    6,TBL2-2            DATE ARRAY
LOOP1    LR    4,2                 LOAD INDEX REGISTER
         AR    4,4                 FIX OFFSET
         LH    7,0(4,6)            GET MM(IX) WHERE IX=R4
         CR    3,7
         BH    SAVMM
         BCT   2,LOOP1
         B     INVALID
SAVMM    CVD   2,WORK              CONVERT MONTH BINARY TO PACKED
         MP    WORK(8),=P'100'     SET UP MM FIELD
         AP    0(4,8),WORK+4(4)    SAVE IT IN RESULT FIELD
         SR    3,7                 GET DAY
         CVD   3,WORK              & PACK IT
         AP    0(4,8),WORK+4(4)    ADD DAY
         OI    3(8),X'0F'          PACK RESULT UNSIGNED
EXIT     LR    9,15                SAVE RETURN CODE
         TM    IEFOUT,X'80'        LAST PARM ?
         BO    *+12                YEAH
         L     8,IEFRC             GET RC ADDRESS
         ST    9,0(8)              PASS RC IF REQUESTED
         L     13,STACK+4
         LA    0,WKLEN
         FREEMAIN R,LV=(0),A=(11)
         LR    15,9                SET RC
         RETURN (14,12),,RC=(15)   RESTORE AND QUIT
INVALID  LA    15,8
         B     EXIT
TBL1     DC    AL2(0)              DAYS TABLE FOR NON LEAP YEARS
         DC    AL2(31)
         DC    AL2(59)
         DC    AL2(90)
         DC    AL2(120)
         DC    AL2(151)
         DC    AL2(181)
         DC    AL2(212)
         DC    AL2(243)
         DC    AL2(273)
         DC    AL2(304)
         DC    AL2(334)
TBL2     DC    AL2(0)              DAYS TABLE FOR LEAP YEARS
         DC    AL2(31)
         DC    AL2(60)
         DC    AL2(91)
         DC    AL2(121)
         DC    AL2(152)
         DC    AL2(182)
         DC    AL2(213)
         DC    AL2(244)
         DC    AL2(274)
         DC    AL2(305)
         DC    AL2(335)
         LTORG
WORKSTG  DSECT
STACK    DS    18F
         DS    0D                  DOUBLE WORD ALIGN
WORK     DS    1F
         DS    1F
F2       DS    CL2
LYI      DS    CL1                 LEAP YEAR INDICATOR
WKLEN    EQU   *-WORKSTG
LINKAGE  DSECT
IEFIN    DS    A
IEFOUT   DS    A
IEFRC    DS    A
         END
