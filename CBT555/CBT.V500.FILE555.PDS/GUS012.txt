*****************************************************************
* GUS012 SUBROUTINE                                             *
* PARAMETERS PASSED:                                            *
*      FIELD      LENGTH         DESC                           *
*     IOERRCD      1F           ERROR CODE; RETURNED            *
*                                 CONSIST OF 2 HALF WORDS       *
*                                 ERRCD AND INFO CODE           *
*     IOFCODE      XL1          FUNCTION CODE;                  *
*                                 X'01' - ALLOCATE              *
*                                 X'02' - FREE                  *
*     IODDNM       CL8          DDNAME                          *
*     IODSNM       CL44         DSNAME                          *
*     IOMEM        CL8          MEMBER NAME IF PO               *
*     IOPASSW      CL8          PASSWORD IF PASSWORD PROTECTED  *
*     IOVOLSER     CL6          VOLUME SERIAL IF NOT IN CATALOG *
*                                                               *
*****************************************************************
GUS012   EPA   'ALLOCATES A PS DATASET'
         BALR  12,0                12 IS BASE REGISTER
         USING *,12                TELL ASSEMBLER
         LR    9,1                 R9 WILL NOW HOLD PARMLIST ADDRS
         USING PLIST,9             TELL ASSEMBLER
         LA    0,WSLEN
         GETMAIN R,LV=(0)          OBTAIN WORKING STORAGE AREA
         ST    13,4(,1)            SAVE CALLER'S SVAREA ADDRESS
         ST    1,8(,13)            LOAD SAVE AREA JUST IN CASE
         LR    13,1
         USING WKSTG,13
GETVS    L     0,LVDATA
         GETMAIN R,LV=(0)
         LR    11,1                SET PARMAREA BASE ADDRESS
         USING PARMAREA,11         TELL ASSEMBLER WHERE IT IS
         MVI   RBLEN,X'14'
         MVI   FCODE,X'01'         ALLOC
         XR    0,0
         STH   0,FLAG1
         STH   0,ERROR
         STH   0,INFO
         ST    0,RSV01
         ST    0,FLAG2
* INITIALIZE PARAMETER LIST
         MVC   DDNAMEP(6),=X'0001,0001,0008'
         MVC   DSNAMEP(6),=X'0002,0001,002C'
         MVC   MEMNAMEP(6),=X'0003,0001,0008'
         MVC   PASSWRDP(6),=X'0050,0001,0008'
         MVC   DISP(7),=X'0004,0001,0001,08'   DISP=SHR
         MVC   FREE(4),=X'001C,0000'           FREE AFTER CLOSE
         MVC   VOLSERP(6),=X'0010,0001,0006'
         MVI   MEMNAME,C' '
         MVI   PASSWORD,C' '
         MVI   VOLSER,C' '
         L     4,IOFCODE           GET FUNCTION CODE
         CLI   0(4),X'02'          OPTION "2" ?
         BNE   *+8
         MVI   FCODE,X'02'         SET FUNCTION CODE
         TM    IOFCODE,X'80'       LAST PARM ?
         BO    ASM
         L     4,IODDNM            GET INPUT DSNAME ADDRESS
         CLC   0(8,4),=CL8' '      IF BLANK, DDNAME IS REQUESTED
         BNE   SETDDNME
         MVC   DDNAMEP(2),=X'0055' SET REQUEST DDNAME KEY
SETDDNME MVC   DDNAME(8),0(4)      MOVE DDNAME TO PARM AREA
         L     4,IODSNM            GET INPUT DSNAME ADDRESS
         MVC   DSNAME(44),0(4)     MOVE DSNAME TO PARM AREA
         TM    IODSNM,X'80'        LAST PARM ?
         BO    ASM
         L     4,IOMEM
         CLI   0(4),C' '
         BE    *+10                NO MEMBER NAME PASSED
         MVC   MEMNAME(8),0(4)
         TM    IOMEM,X'80'
         BO    ASM
         L     4,IOPASSW
         CLI   0(4),C' '
         BE    *+10                NO PASSWORD PASSED
         MVC   PASSWORD(8),0(4)
         TM    IOPASSW,X'80'
         BO    ASM                 NO VOLSER PASSED
         L     4,IOVOLSER
         MVC   VOLSER(6),0(4)
ASM      LA    1,RB
         ST    1,RBPTR
         OI    RBPTR,X'80'         SET VL MODE
         LA    1,TUPL
         ST    1,TXTPP
         LA    1,DDNAMEP
         LA    2,DSNAMEP
         LA    3,DISP
         LA    4,FREE
         STM   1,4,TUPL
         CLI   FCODE,X'02'         FUNCTION 2 SET ?
         BNE   *+12
         OI    TUPL,X'80'          SET DDNAME AS LAST PARM FOR FREE
         B     GO
         LA    3,TUPL+12           FREE=CLOSE AS LAST PARM
         CLI   VOLSER,C' '
         BE    *+16
         LA    2,VOLSERP
         ST    2,4(,3)
         LA    3,4(,3)
         CLI   MEMNAME,C' '
         BE    *+16
         LA    2,MEMNAMEP
         ST    2,4(,3)
         LA    3,4(,3)
         CLI   PASSWORD,C' '
         BE    *+16
         LA    2,PASSWRDP
         ST    2,4(,3)
         LA    3,4(,3)
         OI    0(3),X'80'
GO       LA    1,RBPTR
         DYNALLOC
         ST    15,RETCODE
         L     3,IOERRCD
         L     2,ERROR
         ST    2,0(,3)
         L     4,IODDNM            RETURN DDNAME
         MVC   0(8,4),DDNAME
FREE1    LR    1,11
         L     0,LVDATA
         FREEMAIN R,LV=(0),A=(1)
         L     8,STACK+4           SAVE THESE AREAS, WE'LL LOOSE
         L     9,RETCODE           THEM IN FREEMAIN
         LR    1,13                PASS WORK AREA BASE REG
         LA    0,WSLEN             LEN TO FREE
         FREEMAIN R,LV=(0),A=(1)   FREE WORK AREA USED
         LR    13,8                RESTORE &
         LR    15,9                SET RETURN CODE THEN
         RETURN (14,12),,RC=(15)   QUIT
         DS    0F                  FULL WORD ALIGN
LVDATA   DC    AL1(0),AL3(PARMEND-PARMAREA)
*
WKSTG    DSECT
STACK    DS    18F
RETCODE  DS    1F
RBPTR    DS    F
WSLEN    EQU   *-WKSTG
*
PLIST    DSECT
IOLIST   DS    0F
IOERRCD  DS    A
IOFCODE  DS    A
IODDNM   DS    A
IODSNM   DS    A
IOMEM    DS    A
IOPASSW  DS    A
IOVOLSER DS    A
*
PARMAREA DSECT                     PARAMETER LIST FOR ALLOC
RB       DS    0F
RBLEN    DS    X'14'
FCODE    DS    X'01'               ALLOC
FLAG1    DS    X'0000'
ERROR    DS    X'0000'
INFO     DS    X'0000'
TXTPP    DS    AL4
RSV01    DS    X'00000000'
FLAG2    DS    X'00000000'
DDNAMEP  DS    X'000100010008'
DDNAME   DS    C'SYSUT1  '
DSNAMEP  DS    X'00020001002C'
DSNAME   DS    CL44' '
MEMNAMEP DS    X'000300010008'
MEMNAME  DS    CL8' '
PASSWRDP DS    X'005000010008'
PASSWORD DS    CL8' '
DISP     DS    X'000400010001'
         DS    X'08'               DISP=SHR
FREE     DS    X'001C00000000'     FREE AFTER CLOSE
VOLSERP  DS    X'001000010006'
VOLSER   DS    CL6' '              VOL=SER=??????
TUPL     DS    7F
PARMEND  EQU   *
*        IEFZB4D0
*        IEFZB4D2
         END
