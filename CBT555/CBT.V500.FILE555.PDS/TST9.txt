FMUX3    EPA   'PUTLINE GETLINE TEST PROGRAM'
         BALR  12,0                12 IS BASE REGISTER
         USING *,12                TELL ASSEMBLER
         USING CPPL,11
         USING WKSTG,10
         USING CSOA,2
         LR    11,1
         LA    0,WKLEN
         GETMAIN R,LV=(0)
         LR    10,1
         ST    13,STACK+4          SAVE CALLER'S SVAREA ADDRESS
         LA    9,STACK             LOAD SAVE AREA JUST IN CASE
         ST    9,8(,13)            SET BACKWARD CHAIN
         LR    13,9                SET FORWARD CHAIN
         MVC   WCPPL(16),0(11)     COPY CPPL
         LOAD  EPLOC=GUS015E
         ST    0,GUS015
         LOAD  EPLOC=GUS016E
         ST    0,GUS016
         LOAD  EPLOC=GUS017E
         ST    0,GUS017
         XC    GUS015W,GUS015W
         ST    11,GUS015W
         MVC   GUS016W,GUS015W
         MVC   GUS017W(12),GUS016W
         LA    8,WCSOA
         ST    8,GUS015W+4
*
         L     15,GUS016
         CALL  (15),(,,MSG1),VL,MF=(E,GUS016W)
         LA    5,10                DO 10 TIMES
LOOP     L     15,GUS017
         CALL  (15),MF=(E,GUS017W)
         LTR   8,15                GET GETLINE RETCODE
         BNZ   ENDER
         L     7,GUS017BF          GET FORMATTED LINE ADDRESS
         L     15,GUS015
         CALL  (15),(,,(7)),MF=(E,GUS015W) SCAN GETLINE
         LA    2,WCSOA
         L     15,CSOACNM          GET EPLOC NAME ADDRESS
         LTR   15,15
         BZ    NEXTCMD             IGNORE IF NO COMMAND ENTERED
         LH    14,CSOALNM
         BCTR  14,0
         CLEAR EPLOC
$$$MV    MVC   EPLOC(1),0(15)      MOVE EPLOC NAME
         EX    14,$$$MV
         ST    7,WCPPL             STORE COMMAND LINE
         LA    1,WCPPL
         LA    14,EPLOC
         LINK  EPLOC=(14),MF=(E,(1))
         STFSMODE OFF
         L     15,GUS016
         CALL  (15),(,,MSG2),VL,MF=(E,GUS016W)
NEXTCMD  BCT   5,LOOP
*
         ANOP  0,4
ENDER    L     15,GUS016
         CALL  (15),(,,MSG3),VL,MF=(E,GUS016W)
         L     15,GUS016
         CALL  (15),(,),VL,MF=(E,GUS016W)
         L     15,GUS017
         CALL  (15),(,),VL,MF=(E,GUS017W)
         L     1,GUS017W+8         GET FREEMAIN ADDRESS
         LH    0,0(,1)
         ICM   0,8,=X'01'          SUBPOOL 1
         FREEMAIN R,LV=(0),A=(1)
         DELETE EPLOC=GUS015E
         DELETE EPLOC=GUS016E
         DELETE EPLOC=GUS017E
         L     13,STACK+4
         LA    0,WKLEN
         FREEMAIN R,LV=(0),A=(10)
         LR    15,8                SET RETURN CODE
         RETURN (14,12),,RC=(15)   RESTORE AND QUIT
GUS015E  DC    CL8'GUS015'
GUS016E  DC    CL8'GUS016'
GUS017E  DC    CL8'GUS017'
MSG1     MSGCP 'ENTER A COMMAND'
MSG2     MSGCP 'ENTER NEXT COMMAND'
MSG3     MSGCP 'ABOUT TO FREEMAIN'
WKSTG    DSECT
ECB      DS    F
STACK    DS    18F
GUS015W  DS    3A
GUS016W  DS    3A
GUS017W  DS    2A
GUS017BF DS    A
GUS015   DS    A
GUS016   DS    A
GUS017   DS    A
WCSOA    DS    A
         DS    A
WCPPL    DS    4A
EPLOC    DS    CL8
RC       DS    F
WKLEN    EQU   *-WKSTG
         IKJCPPL
         IKJCSOA
         END
