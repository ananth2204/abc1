CALENDAR EPA   'DISPLAYS A CALENDAR OF THE CURRENT MONTH'
         BALR  12,0                12 IS BASE REGISTER
         USING *,12                TELL ASSEMBLER
         USING CMDLINE,11
         USING PARMS,10
         USING CPPL,1
         L     11,CPPLCBUF         GET ADDRESS OF COMMAND LINE
         ST    13,STACK+4          SAVE CALLER'S SVAREA ADDRESS
         LA    10,STACK            LOAD SAVE AREA JUST IN CASE
         ST    10,8(,13)           SET BACKWARD CHAIN
         LR    13,10               SET FORWARD CHAIN
*
**** PROCESS PARAMETER ON COMMAND LINE
*
         LH    2,LINELEN
         SH    2,CMDLEN
         CH    2,=H'4'
         BNH   NOPARMS             NO PARMS ON COMMAND LINE
         LA    10,COMMAND
         AH    10,CMDLEN           GO PAST COMMAND
         PACK  DATAREA2(8),0(8,10) PACK THE DATE IN PARM LINE
         MVC   DATAREA(4),DATAREA2+4
         NI    DATAREA,X'0F'       CLEAR HIGH NIBBLE
         B     GO                  PROCESS THE CALENDAR
*
NOPARMS  TIME  DEC,DATAREA0,DATETYPE=YYYYMMDD,LINKAGE=SYSTEM
         L     5,DATAREA0+8        YYYYMMDD
         XR    4,4                 00000000
         SLDL  4,4                 0000000Y YYYMMDD0
         STM   4,5,DATAREA2
         MVC   DATAREA(4),DATAREA2+4
         OI    DATAREA+3,X'0F'     PACK IT         CYYMMDDF
GO       NC    DATAREA2+6,=X'F00F' SET DD TO 0
         MVI   DATAREA2+7,X'1F'    SET DD TO 1
         XR    4,4
         ICM   4,7,DATAREA2+3      GET YY PART     000CCYYM
         ST    4,WORK              PREPARE TO PACK
         OI    WORK+3,X'0F'        PACK IT         000CCYYF'
         UNPK  YEAR1(4),WORK(4)    PUT HEADING 1
         UNPK  YEAR2(4),WORK(4)    PUT HEADING 2
         XR    4,4                 00000000
         ICM   4,3,DATAREA+1       GET MM PART     0000YMMD
         SRDL  4,12                CLEAR BYTE 2    0000000Y MMD00000
         XR    4,4                 CLEAR Y         00000000 MMD00000
         SLDL  4,12                SHIFT MM PART   00000MMD 00000000
         ZAP   WORK(8),=P'0'
         ST    4,WORK+4            PREPARE TO PACK 00000000 00000MMD
         OI    WORK+7,X'0F'        PACK IT         00000000 00000MMF
         CVB   4,WORK
         LR    8,4                 SAVE MM
         MH    4,=H'9'             FIX OFFSET
         LA    5,MONTHTAB-9(4)     GET ADDRESS OF CURRENT MONTH
         MVC   MONTH(9),0(5)       PUT IN HEADING
         ICM   4,3,DATAREA+2       GET DD PART     0000MDDF
         STCM  4,3,CDAY            SAVE                MDDF
         NI    CDAY,X'0F'          CLEAR HIGH NIBBLE   0DDF
         UNPK  CYMDDATE(8),DATAREA2(8)
         LA    1,GUS007            LOAD PARAMETER LIST ADDRESS
         LINK  EP=GUS007,MF=(E,(1))
         PACK  WORK(8),DAYNUM(1)
         CP    WORK(8),=P'0'       0 IS USED AS A SATURDAY
         BH    *+10                NOT SATURDAY
         ZAP   WORK(8),=P'7'       SET TO SATURDAY
         CVB   4,WORK
         AR    4,4                 FIX OFFSET
         XR    2,2
         IC    2,LOOPCTR-1(8)      USE MM IN R8 AS POINTER
         LA    3,NUMAREA-2(4)      GET ADDRESS OF LINE
         LA    5,NUMAREA-4(4)      GET ADDRESS OF PREVIOUS CELL
LOOP     AP    0(2,3),0(2,5)
         AP    0(2,3),=P'1'        ADD 1 MORE
         LR    5,3                 SAVE PREVIOUS CELL
         LA    3,2(,3)             NEXT CELL
         BCT   2,LOOP
         LA    2,LINEADDR
         LA    6,NUMAREA
         LA    3,6                 6 LINES TO TRANSLATE
OUTER    L     5,0(,2)             GET ADDRESS OF CURRENT LINE
         LA    4,7                 7 CHARS/LINE
INNER    MVC   EDTAREA(4),EDTCDE
         ED    EDTAREA(4),0(6)
         MVC   0(4,5),EDTAREA
         CP    0(2,6),CDAY(2)
         BNE   *+10
         MVC   36(4,5),=C' ***'
         LA    5,4(,5)
         LA    6,2(,6)
         BCT   4,INNER
         LA    2,4(,2)
         BCT   3,OUTER
         TPUT  SCREEN,SCRLEN,FULLSCR
         TGET  AREA,4
         L     13,STACK+4
         LM    14,12,12(13)        RESTORE AND
         XR    15,15               SET RETURN CODE THEN
         BR    14                  QUIT
STACK    DS    18F
SCREEN   EQU   *
         DC    C'     '
R05C09   SBA   5,9
         SF    (PROT,NUM)
         DC    C'+'
         DC    29C'-'
         DC    C'+'
R06C09   SBA   6,9
         SF    (PROT,NUM)          YEAR1 = R06C09+7
         DC    C'|'
         DC    10C' '
MONTH    DC    CL9' '
         DC    10C' '              YEAR2 = MONTH+14
         DC    C'|'
R07C09   SBA   7,9
         SF    (PROT,NUM)
         DC    C'|'
         DC    29C'-'
         DC    C'|'
R08C09   SBA   8,9
         SF    (PROT,NUM)
         DC    C'|'
         DC    CL29' SUN MON TUE WED THU FRI SAT'
         DC    C'|'
R09C09   SBA   9,9
         SF    (PROT,NUM)
         DC    C'|'
         DC    29C'-'
         DC    C'|'
R10C09   SBA   10,9
         SF    (PROT,NUM)
         DC    C'|'
LINE1    DC    29C' '
         DC    C'|'
R11C09   SBA   11,9
         SF    (PROT,NUM)
         DC    C'|'
         DC    29C' '
         DC    C'|'
R12C09   SBA   12,9
         SF    (PROT,NUM)
         DC    C'|'
LINE2    DC    29C' '
         DC    C'|'
R13C09   SBA   13,9
         SF    (PROT,NUM)
         DC    C'|'
         DC    29C' '
         DC    C'|'
R14C09   SBA   14,9
         SF    (PROT,NUM)
         DC    C'|'
LINE3    DC    29C' '
         DC    C'|'
R15C09   SBA   15,9
         SF    (PROT,NUM)
         DC    C'|'
         DC    29C' '
         DC    C'|'
R16C09   SBA   16,9
         SF    (PROT,NUM)
         DC    C'|'
LINE4    DC    29C' '
         DC    C'|'
R17C09   SBA   17,9
         SF    (PROT,NUM)
         DC    C'|'
         DC    29C' '
         DC    C'|'
R18C09   SBA   18,9
         SF    (PROT,NUM)
         DC    C'|'
LINE5    DC    29C' '
         DC    C'|'
R19C09   SBA   19,9
         SF    (PROT,NUM)
         DC    C'|'
         DC    29C' '
         DC    C'|'
R20C09   SBA   20,9
         SF    (PROT,NUM)
         DC    C'|'
LINE6    DC    29C' '
         DC    C'|'
R21C09   SBA   21,9
         SF    (PROT,NUM)
         DC    C'+'
         DC    29C'-'
         DC    C'+'
SCRLEN   EQU   *-SCREEN
YEAR1    EQU   R06C09+7
YEAR2    EQU   MONTH+14
AREA     DC    CL4' '
EDTAREA  DS    CL4
EDTCDE   DC    X'40202020'
CDAY     DS    PL2
         DC    PL2'0'              DUMMY AREA FOR COUNTER
NUMAREA  DC    42PL2'0'
CYMDDATE DS    CL8
DAY      DS    CL9
DAYNUM   DS    CL1
MONTHTAB DC    CL9' JANUARY'
         DC    CL9'FEBRUARY'
         DC    CL9'  MARCH'
         DC    CL9'  APRIL'
         DC    CL9'   MAY'
         DC    CL9'  JUNE'
         DC    CL9'  JULY'
         DC    CL9' AUGUST'
         DC    CL9'SEPTEMBER'
         DC    CL9' OCTOBER'
         DC    CL9'NOVEMBER'
         DC    CL9'DECEMBER'
LOOPCTR  DC    AL1(31)
         DC    AL1(28)
         DC    AL1(31)
         DC    AL1(30)
         DC    AL1(31)
         DC    AL1(30)
         DC    AL1(31)
         DC    AL1(31)
         DC    AL1(30)
         DC    AL1(31)
         DC    AL1(30)
         DC    AL1(31)
LINEADDR DC    A(LINE1)
         DC    A(LINE2)
         DC    A(LINE3)
         DC    A(LINE4)
         DC    A(LINE5)
         DC    A(LINE6)
GUS007   CALL  ,(CYMDDATE,DAY,DAYNUM),VL,MF=L
         DS    0D
WORK     DS    1F
         DS    1F
DATAREA0 DS    CL12
DATAREA  DS    PL4                 0Y YM MD DF
DATAREA2 DS    PL8                 00 00 00 0C CY YM M0 1F
         DS    1D
CMDLINE  DSECT
LINELEN  DS    1H
CMDLEN   DS    1H
COMMAND  DS    C
PARMS    DSECT
         DS    C
         IKJCPPL
         END
