***********************************************
*                                             *
*  PARMS:                                     *
*       AMOUNT = PIC 9(8)V99 INPUT            *
*       TEXT   = PIC X(100)  RETURNED         *
*       RC     = 1F BINARY RETURNED OPTIONAL  *
*  REGISTERS ON RETURN:                       *
*     1 - 14   AS IS                          *
*     15       RETURN CODE                    *
*                                             *
***********************************************
         MACRO
&LABEL   TEENS &WORD
         LCLA  &LEN
&LEN     SETA  K'&WORD-1
         DC    AL1(&LEN)           LENGTH LESS 1 FOR MVC INSTR
         DC    CL9'&WORD'          AMOUNT IN WORDS
         MEND
         MACRO
&LABEL   TENS  &WORD
         LCLA  &LEN
&LEN     SETA  K'&WORD-1
         DC    AL1(&LEN)           LENGTH LESS 1 FOR MVC INSTR
         DC    CL7'&WORD'          AMOUNT IN WORDS
         MEND
GUS005   EPA   'CONVERTS AMOUNTS TO WORDS '
         BALR  12,0                12 IS BASE REGISTER
         USING *,12                TELL ASSEMBLER
         USING WORKSTG,11
         USING LINKAGE,10
         LR    10,1
         LA    0,WKLEN
         GETMAIN R,LV=(0)          ALLOC VS FOR WS STORAGE
         LR    11,1
         ST    13,STACK+4          SAVE CALLER'S SVAREA ADDRESS
         LA    9,STACK             LOAD SAVE AREA JUST IN CASE
         ST    9,8(,13)            SET BACKWARD CHAIN
         LR    13,9                SET FORWARD CHAIN
         L     2,IEFIN
         L     8,IEFOUT
         MVI   FLAG,C'0'
         CLEAR (8),LEN=100         CLEAR OUTPUT FIELD
         PACK  WORK(8),0(10,2)     PACK HIS NUMBER SO WE CAN WORK
* MILLIONS ROUTINE
M1       XR    7,7                 BIT BUCKET
         XR    6,6                 WORK REGISTER
         L     6,WORK
         SRDL  6,4                 LEAVE TEN MILLIONS PART IN R6
         LTR   6,6
         BZ    HT1                 GO TO HUNDRED THOUSAND ROUTINE
         CH    6,=H'32'
         BL    M2
         SRDL  6,4                 LEAVE TENS UNITS IN R6
         BAL   14,TENSRTN
         SLDL  6,4                 GET ONES UNITS IN R6
         LTR   6,6
         BZ    MILLEND
M2       CH    6,=H'16'            10 BINARY = 16
         BL    *+8
         SH    6,=H'6'             CHANGE DECIMAL TO BINARY
         BAL   14,TEENSRTN
MILLEND  MVTXT (8),'MILLION '
         LA    8,8(,8)
HT1      SLDL  6,4                 GET ONES UNITS IN R6
         LTR   6,6
         BZ    HT2
         MVI   FLAG,C'1'
         BAL   14,TEENSRTN
         MVTXT (8),'HUNDRED '
         LA    8,8(,8)
HT2      IC    6,WORK+4
         LTR   6,6
         BZ    HTEND1              GO TO ONE HUNDRED ROUTINE
         CH    6,=H'32'
         BL    HT3
         SRDL  6,4                 LEAVE TENS UNITS IN R6
         BAL   14,TENSRTN
         SLDL  6,4                 GET ONES UNITS IN R6
         LTR   6,6
         BZ    HTEND
HT3      CH    6,=H'16'            10 BINARY = 16
         BL    *+8
         SH    6,=H'6'             CHANGE DECIMAL TO BINARY
         BAL   14,TEENSRTN
         B     HTEND
HTEND1   CLI   FLAG,C'0'
         BE    H1
HTEND    MVTXT (8),'THOUSAND '
         LA    8,9(,8)
H1       ICM   7,12,WORK+5
         SLDL  6,4                 LEAVE ONE HUNDRED PART IN R6
         LTR   6,6
         BZ    T1                  GO TO TENS ROUTINE
         BAL   14,TEENSRTN
         MVTXT (8),'HUNDRED '
         LA    8,8(,8)
T1       SLDL  6,8                 GET TENS UNITS IN R6
         LTR   6,6
         BZ    TEND                GO TO END OF NUMERIC ROUTINE
         CH    6,=H'32'
         BL    T2
         SRDL  6,4                 LEAVE TENS UNITS IN R6
         BAL   14,TENSRTN
         SLDL  6,4                 GET ONES UNITS IN R6
         LTR   6,6
         BZ    TEND
T2       CH    6,=H'16'            10 BINARY = 16
         BL    *+8
         SH    6,=H'6'             CHANGE DECIMAL TO BINARY
         BAL   14,TEENSRTN
TEND     L     2,IEFIN
         CLC   8(2,2),=C'00'       NO DECIMAL PART?
         BE    EXIT
         MVTXT (8),'AND '
         MVC   4(2,8),8(2)
         MVC   6(4,8),=C'/100'
EXIT     LR    9,15                SAVE RETURN CODE
         TM    IEFOUT,X'80'        LAST PARM ?
         BO    *+12                YEAH
         L     8,IEFRC             GET RC ADDRESS
         ST    9,0(,8)             PASS RC IF REQUESTED
         L     13,STACK+4
         LA    0,WKLEN
         FREEMAIN R,LV=(0),A=(11)
         LR    15,9                SET RC
         RETURN (14,12),,RC=(15)   RESTORE AND QUIT
INVALID  LA    15,8
         B     EXIT
TEENSRTN EQU   *
         LR    2,6
         MH    2,=H'10'            OFFSET OF 10 BYTE WORD
         LA    5,TEENS(2)
         B     F001                BRANCH TO COMMON ROUTINE
TENSRTN  EQU   *
         LR    2,6
         MH    2,=H'8'             OFFSET OF 8 BYTE WORD
         LA    5,TENS(2)           GET CELL
F001     XR    3,3
         IC    3,0(5)              GET LENGTH OF THIS TEXT
         EX    3,MOVETXT
         AR    8,3
         LA    8,2(,8)
         XR    6,6
         BR    14
MOVETXT  MVC   0(1,8),1(5)
TEENS    EQU   *
         TEENS ZERO
         TEENS ONE
         TEENS TWO
         TEENS THREE
         TEENS FOUR
         TEENS FIVE
         TEENS SIX
         TEENS SEVEN
         TEENS EIGHT
         TEENS NINE
         TEENS TEN
         TEENS ELEVEN
         TEENS TWELVE
         TEENS THIRTEEN
         TEENS FOURTEEN
         TEENS FIFTEEN
         TEENS SIXTEEN
         TEENS SEVENTEEN
         TEENS EIGHTEEN
         TEENS NINETEEN
TENS     EQU   *
         TENS  ZERO
         TENS  TEN
         TENS  TWENTY
         TENS  THIRTY
         TENS  FORTY
         TENS  FIFTY
         TENS  SIXTY
         TENS  SEVENTY
         TENS  EIGHTY
         TENS  NINETY
         LTORG
WORKSTG  DSECT
STACK    DS    18F
         DS    0D                  DOUBLE WORD ALIGN
WORK     DS    1F
         DS    1F
FLAG     DS    CL1                 FLAG FOR HUNDRED THOUSAND
WKLEN    EQU   *-WORKSTG
LINKAGE  DSECT
IEFIN    DS    A
IEFOUT   DS    A
IEFRC    DS    A
         END
