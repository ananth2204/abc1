* LBC - This module is used to read and delete another TSO user's
* broadcast messages. This is especially useful when a user has been
* receiving numerous message, but is away from the office. This
* allows an authorized user to keep the broadcast dataset from filling
* up.
*
* Written by Martin Kline
*
* INSTALLATION
*
* Add the LBC program to the authorized command table in IKJTSOxx.
* Assemble the source and link it to produce an authorized module.
* Put the module into an authorized linklist library.
*
* USAGE
*
* It is probably best to use this program in conjuncion with the
* READBC program that reads the entire SYS1.BRODCAST dataset for all
* users. Run READBC first to determine who has lots of messages. Then
* run this program to delete messages for given userids.
*
* Enter the LBC command from anywhere in TSO
*
* The command responds with 'ENTER TSO USERID FOR LISTBC COMMAND'.
*
* Reply with a valid userid.
*
* The user's messages are displayed and deleted from SYS1.BRODCAST.
*
* The userid prompt is displayed again, and the process repeats
* until the user enters 'END'.
*
LBC      STM   14,12,12(13)            Save registers
         LR    12,15                   Set up base reg
         USING LBC,12                  Establish addressability
         LA    2,SAVEAREA              Point to save area
         ST    2,8(,13)                Forward chain save areas
         ST    13,4(,2)                Backward chain save areas
         LR    13,2                    Set up save area pointer
         ST    1,SAVER1                Save register 1
*
LOOP1    MVC   USER,BLANKS             Initialize field
         TPUT  MSG1,L'MSG1,,NOWAIT     Prompt for userid
*
         TGET  USER,L'USER,,WAIT       Get userid
*
         STC   1,USERLEN               Save length
         OC    USER,BLANKS             Make sure it is upper case
         CLC   USER,BLANKS             Anything entered ?
         BE    LOOP1                   No - try again
*
         CLC   USER(4),=C'END '        END command ?
         BE    EXIT                    Yes - exit
*
         L     6,540                   Point to my TCB
         L     6,180(,6)               Point to my JSCB
         L     6,348(,6)               Point to active JSCB
         L     6,264(,6)               Point to PSCB
*
         MODESET KEY=ZERO              Gotta be in key zero
         MVC   SAVEUSER,0(6)           Save the old userid
         MVC   0(8,6),USER             Set the new userid
         MODESET KEY=NZERO             No longer key zero
*
         L     1,SAVER1                Get original parm register
         LA    2,COMMAND               Point to pseudo command
         LM    3,5,4(1)                Call the real LISTBC program
         LINK  EP=LISTBC,PARAM=((2),(3),(4),(5))
*
         MODESET KEY=ZERO              Gotta be key zero
         MVC   0(8,6),SAVEUSER         Restore userid field
         MODESET KEY=NZERO             No longer key zero
*
         B     LOOP1                   Then do it all again
*
EXIT     L     13,4(,13)               Point to caller's save area
         LM    14,12,12(13)            Restore caller's regs
         XR    15,15                   Set zero return code
         BR    14                      Return
*
SAVEAREA DC    18F'0'
SAVER1   DC    F'0'
SAVEUSER DC    CL8' '
COMMAND  DC    AL2(10,6),CL8'LISTBC'
MSG1     DC    C'ENTER TSO USERID FOR LISTBC COMMAND'
USER     DC    CL7' '
USERLEN  DC    X'00'
BLANKS   DC    CL7' '
         END
