* READBC - This program reads all messages in the SYS1.BRODCAST
* dataset, and displays them. Especially useful to identify users who
* are wasting space in the broadcast dataset.
*
* Note: This program only works if you are still using SYS1.BRODCAST
*       for all TSO SEND messages. If each user has their own
*       dataset, then this will not work.
*
* Written by Martin Kline
*
* INSTALLATION
*
* Assemble the source and link it. Module does not require
* authorization.
*
* USAGE
*
* Best if run in batch, so you can scroll through the messages,
*
* //READBC   EXEC PGM=READBC
* //STEPLIB   DD  DISP=SSHR,DSN=your.load.library
* //BRODCAST  DD  DSN=SYS1.BRODCAST,DISP=SHR
* //SYSOUT    DD  SYSOUT=*
*
READBC   CSECT
         STM   14,12,12(13)            Save caller's regs
         LR    12,15                   Set up base reg
         USING READBC,12               Establish addressability
         LA    2,SAVEAREA              Point to my save area
         ST    2,8(,13)                Forward chain save areas
         ST    13,4(,2)                Backward chain save areas
         LR    13,2                    Set save area pointer
*
         L     2,0(,1)                 load parm address
         SR    15,15                   clear register 15
         ICM   15,3,0(2)               load parm length
         BZ    START                   if zero, branch
         CLC   2(5,2),=C'NOMSG'        no message text?
         BNE   START                   no, branch
         MVI   SWITCH,X'00'            set switch
*
START    OPEN  MF=(E,OPEN)             open files
*
         LA    1,0                     rba = 0
         BAL   14,READ1                read 1st record of sys1.brodcast
         ICM   1,15,R1BCPTRP           rba of 1st notices directory
*
LOOP0    BAL   14,READ2                read notices directory
         LA    1,B2ENT                 point to 1st entry
         LA    15,25                   set loop limit
A        TM    0(1),X'80'              unused entry?
         BO    C                       yes, branch
B        AP    COUNT2U,=P'1'           increment counter
         B     D                       branch
C        AP    COUNT2AV,=P'1'          increment counter
         B     D                       branch
D        LA    1,5(,1)                 next entry
         BCT   15,A                    loop
         SR    1,1                     clear r1
         ICM   1,7,B2NEXT              rba of next notice directory
         BNZ   LOOP0                   read next one
*
         SR    1,1                     clear r1
         ICM   1,7,BUFFER1+5           rba of 1st user mail directory
LOOP1    BAL   14,READ3                read user mail directory record
         LA    6,BUFFER3               point to 1st entry
         LA    7,9                     max. entries
*
LOOP1A   DS    0H
         USING USDENTRY,6              provide addressability
         CLC   USERID,ZEROS            empty slot?
         BE    NEXT                    yes, skip it
         ZAP   COUNT,=P'0'             clear message count
         SR    1,1                     clear register 1
         ICM   1,7,RBA1ST              load rba of 1st message
         BZ    ENDLOOP2                branch if zero
*
LOOP2    BAL   14,READ4                read user mail record
         AP    COUNT,=P'1'             bump counter
         TM    SWITCH,X'80'            print message text?
         BZ    SKIP                    no, branch
         MVC   OUTPUT,BLANKS           clear output line
         MVC   OUTPUT(7),USERID        copy userid
         SR    15,15                   clear register 15
         IC    15,USMLNG               load message length
         BCTR  15,0                    subtract 1
         EX    15,MOVEMSG              move message to output
         PUT   SYSPRINT,OUTPUT         print message text
*
SKIP     SR    1,1                     clear register 1
         ICM   1,7,USMNEXT             load rba of next record
         BNZ   LOOP2                   process next record
*
ENDLOOP2 CP    COUNT,=P'0'             any messages?
         BE    NEXT                    no, branch
         MVC   TRAILER(7),USERID       copy userid
         OI    COUNT+2,X'0F'           insure numeric sign
         UNPK  TRAILER+8(4),COUNT      unpack message count
         PUT   SYSPRINT,TRAILER        write total line
*
NEXT     LA    6,13(,6)                point to next entry
         BCT   7,LOOP1A                loop
         SR    1,1                     clear register 1
         ICM   1,7,USDNEXT             rba of next directory record
         BNZ   LOOP1                   zero, no more
*
         MVC   TRAILER1(10),=X'40206B2020206B202120'
         ED    TRAILER1(10),COUNT1
         PUT   SYSPRINT,TRAILER1
         MVC   TRAILER2(10),=X'40206B2020206B202120'
         ED    TRAILER2(10),COUNT2
         PUT   SYSPRINT,TRAILER2
         MVC   TRAILER3(10),=X'40206B2020206B202120'
         ED    TRAILER3(10),COUNT2U
         PUT   SYSPRINT,TRAILER3
         MVC   TRAILER4(10),=X'40206B2020206B202120'
         ED    TRAILER4(10),COUNT2AV
         PUT   SYSPRINT,TRAILER4
         MVC   TRAILER5(10),=X'40206B2020206B202120'
         ED    TRAILER5(10),COUNT3
         PUT   SYSPRINT,TRAILER5
         MVC   TRAILER6(10),=X'40206B2020206B202120'
         ED    TRAILER6(10),COUNT4
         PUT   SYSPRINT,TRAILER6
         ICM   1,15,R1RECNUM
         CVD   1,DWORD
         SP    DWORD,COUNT1
         SP    DWORD,COUNT2
         SP    DWORD,COUNT2U
         SP    DWORD,COUNT2AV
         SP    DWORD,COUNT3
         SP    DWORD,COUNT4
         MVC   TRAILER7(10),=X'40206B2020206B202120'
         ED    TRAILER7(10),DWORD+4
         PUT   SYSPRINT,TRAILER7
EXIT     CLOSE MF=(E,CLOSE)            CLOSE FILES
         L     13,4(,13)
         LM    14,12,12(13)
         XR    15,15
         BR    14
*
READ1    ST    14,SAVER14              SAVE RETURN ADDRESS
         STCM  1,7,RBA                 SAVE RBA
         READ  DECB1,DI,MF=E           READ CONTROL RECORD
         CHECK DECB1                   WAIT FOR READ TO COMPLETE
         AP    COUNT1,=P'1'            INCREMENT COUNTER
         L     14,SAVER14              LOAD RETURN ADDRESS
         BR    14                      RETURN
*
READ2    ST    14,SAVER14              SAVE RETURN ADDRESS
         STCM  1,7,RBA                 SAVE RBA
         READ  DECB2,DI,MF=E           READ NOTICE DIRECTORY
         CHECK DECB2                   WAIT FOR READ TO COMPLETE
         AP    COUNT2,=P'1'            INCREMENT COUNTER
         L     14,SAVER14              LOAD RETURN ADDRESS
         BR    14                      RETURN
*
READ3    ST    14,SAVER14              SAVE RETURN ADDRESS
         STCM  1,7,RBA                 SAVE RBA
         READ  DECB3,DI,MF=E           READ DIRECTORY RECORD
         CHECK DECB3                   WAIT FOR READ TO COMPLETE
         AP    COUNT3,=P'1'            INCREMENT COUNTER
         L     14,SAVER14              LOAD RETURN ADDRESS
         BR    14                      RETURN
*
READ4    ST    14,SAVER14              SAVE RETURN ADDRESS
         STCM  1,7,RBA                 SAVE RBA
         READ  DECB4,DI,MF=E           READ MESSAGE RECORD
         CHECK DECB4                   WAIT FOR READ TO COMPLETE
         AP    COUNT4,=P'1'            INCREMENT COUNTER
         L     14,SAVER14              LOAD RETURN ADDRESS
         BR    14                      RETURN
*
MOVEMSG  MVC   OUTPUT+8(0),USMTEXT     DUMMY INSTRUCTION
         EJECT
BRODCAST DCB   DDNAME=BRODCAST,DSORG=DA,OPTCD=R,RECFM=F,MACRF=(RIC)
         EJECT
SYSPRINT DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=PM,RECFM=FB,LRECL=133
         EJECT
OPEN     OPEN  (BRODCAST,INPUT,SYSPRINT,OUTPUT),MF=L
CLOSE    EQU   OPEN
*
         READ  DECB1,DI,BRODCAST,BUFFER1,'S',0,RBA,MF=L
*
         READ  DECB2,DI,BRODCAST,BUFFER2,'S',0,RBA,MF=L
*
         READ  DECB3,DI,BRODCAST,BUFFER3,'S',0,RBA,MF=L
*
         READ  DECB4,DI,BRODCAST,BUFFER4,'S',0,RBA,MF=L
*
SAVEAREA DC    18F'0'
DWORD    DC    D'0'
SAVER14  DC    F'0'
RBA      DC    AL3(0),AL1(0)
SWITCH   DC    XL1'80'
COUNT    DC    PL3'0'
COUNT1   DC    PL4'0'                  INDEX RECORDS READ
COUNT2   DC    PL4'0'                  TOTAL NOTICE DIRECTORY RECS.
COUNT2U  DC    PL4'0'                  NOTICE RECORDS USED
COUNT2AV DC    PL4'0'                  NOTICE RECORDS AVAIL.
COUNT3   DC    PL4'0'                  TOTAL MAIL DIRECTORY RECS.
COUNT4   DC    PL4'0'                  USED MAIL RECORDS
COUNT5   DC    PL4'0'                  UNUSED MAIL RECORDS
ZEROS    DC    XL7'00'
BLANKS   DC    CL133' '
OUTPUT   DC    CL133' '
TRAILER  DC    CL133'XXXXXXX 0000 MESSAGES'
TRAILER1 DC    CL133' 0,000,000 INDEX RECORD'
TRAILER2 DC    CL133' 0,000,000 NOTICE DIRECTORY RECORDS'
TRAILER3 DC    CL133' 0,000,000 USED NOTICE RECORDS'
TRAILER4 DC    CL133' 0,000,000 UNUSED NOTICE RECORDS'
TRAILER5 DC    CL133' 0,000,000 MAIL DIRECTORY RECORDS'
TRAILER6 DC    CL133' 0,000,000 USED MAIL RECORDS'
TRAILER7 DC    CL133' 0,000,000 UNUSED MAIL RECORDS'
         EJECT
BUFFER1  DS    0CL129
R1BCPTRP DC    XL4'00'           RBA OF 1ST NOTICES DIRECTORY
R1USPTRP DC    XL4'00'           RBA OF 1ST USER MAIL DIRECTORY
R1RECNUM DC    XL4'00'           TOTAL RECORDS IN SYS1.BRODCAST
R1BCMAX  DC    XL2'00'           MAX. BRODCAST MESSAGE NUMBER
R1DSN    DC    CL24' SYS1.BRODCAST DATA SET '
R1LEVEL  DC    CL7'LEVEL 2'
         DC    XL1'00'
R1FRESRH DC    XL3'00'           RBA TO FREE RECORD ???
R1GENNUM DC    XL4'00'           GENERATION NUMBER FOR NOTICE TABLE
         DC    XL76'00'
*
BUFFER2  DS    0CL129
B2ENT    DS    25XL5
B2END    DS    XL1
B2NEXT   DS    XL3
*
BUFFER3  DS    0CL129
ENTRY    DS    9CL13
         DS    XL8
USDREND  DS    XL1
USDNEXT  DS    XL3
*
BUFFER4  DS    0CL129
USMLNG   DS    XL1
USMTEXT  DS    CL125
USMNEXT  DS    XL3
*
         LTORG
*
USDENTRY DSECT
USERID   DS    CL7
RBA1ST   DS    XL3
RBALAST  DS    XL3
*
         END
