 TITLE 'LISTAPF - List all APF datasets'
 PRINT GEN
*______________________________________________________________________
*
*  Written by John Gateley in February 2017.
*  March 2017 John Gateley output volume information as well.
*  May   2018 John Gateley use CSVAPF to access list of APF libraries
*                          instead of undocumented.
*
*    D I S C L A I M E R
*    -------------------
*
*  This program is FREEWARE. Use at your own risk.  Neither John
*  Gateley, nor other contributing organizations or individuals
*  accept any liability of any kind howsoever arising out of the use
*  of this program. You are free to use and modify this program as
*  you desire, however, the author does ask that you leave his name
*  in the source and give credit to him as the original programmer.
*  You can also just grab the bit of code you want if you do not like
*  all the structured programming macros.
*______________________________________________________________________
*
*  This program gets a list of dynamic APF authorised libraries and
*  outputs them to SYSOUT.
*
*  It then gets all the dataset names concatenated to STEPLIB and
*  checks if they are in the APF authorised list, if not it lists them.
*
*  Finally it does the same for JOBLIB.
*
LISTAPF PRGDEF FSEG=MAIN_BIT,RMODE=24,RENT=Y
*
* RENT=Y means the storage up to PRGEDEF is dynamically allocated at
* run time but program is not re-entrant due to SYSOUT DCB.
*
               DS    0D
DSN_COUNT      DS    PL8
USE_LIB        DS    CL8
APF_TOTL       DS    F
RETCODE        DS    F
RSNCODE        DS    F
*
KEEP_ARLAREA   DC    F'0'
KEEP_ARLRLEN   DC    F'0'
KEEP_ARLPOOL   DC    H'0'
KEEP_ARLRTRVD  DC    H'0'
THIS_ARL_REQ   DC    H'0'
*
TEST_IT        DS    CL50
*
&MAX_APF       SETA  2000              should be enough slots
*&MAX_APF       SETA  100               should NOT be enough slots
*
HEAD_APF       DS    (APFHDR_LEN)CL1
DETAIL_APF     DS    (&MAX_APF*APFE_LEN)CL1
TOTAL_APF_LEN  EQU   HEAD_APF
*
         CSVAPFAA
*
         PRGEDEF
*______________________________________________________________________
*
 SEGS MAIN_BIT
*
   USING APFHDR,HEAD_APF
*
   SEGDO OPEN_SYSOUT                   open output file
*
   SEGDO BUILD_APF_LIST
*
*  L     R1,APFHNUMREM                 number not returned
   L     R1,APFH#REM                   number not returned
   IF (LTR,R1,R1,NZ)
     MVC   MESS_TXT(L'ERR_MESS_1),ERR_MESS_1
     SEGDO PUT_SYSOUT
     SETRC RC=4                        set return code to 4
   ELSE
     MVC   USE_LIB,=CL8'STEPLIB'       first try STEPLIB
     SEGDO CHECK_CONCAT
     MVC   USE_LIB,=CL8'JOBLIB'        then try JOBLIB
     SEGDO CHECK_CONCAT
   ENDIF
*
   SEGDO CLOSE_SYSOUT
*
 SEGE MAIN_BIT
*______________________________________________________________________
*
 SEGS BUILD_APF_LIST
*
   LHI   R1,&MAX_APF                   number of entries available
   MHI   R1,APFE_LEN                   multiply by the entry length
   AHI   R1,APFHDR_LEN                 add length of header
   ST    R1,APF_TOTL
   CSVAPF REQUEST=LIST,ANSAREA=HEAD_APF,ANSLEN=APF_TOTL,               /
               RETCODE=RETCODE,RSNCODE=RSNCODE
   IF (LTR,R15,R15,NZ)
     XUNPK RETCODE,4,ERR_MESS_2_RC
     XUNPK RSNCODE,4,ERR_MESS_2_RS
*    L     R1,APFHNUMREC               number received
     L     R1,APFH#REC                 number received
     CVD   R1,DOUB_WORD
     OI    DOUB_WORD+7,X'0F'
     UNPK  ERR_MESS_2_AV,DOUB_WORD
*    A     R1,APFHNUMREM               number remaining
     A     R1,APFH#REM                 number remaining
     CVD   R1,DOUB_WORD
     OI    DOUB_WORD+7,X'0F'
     UNPK  ERR_MESS_2_TL,DOUB_WORD
     MVC   MESS_TXT(ERR_MESS_2_L),ERR_MESS_2
     SEGDO PUT_SYSOUT                  output error message
     PRGQUIT RC=16
   ENDIF
*
   MVC   MESS_TXT+17(L'APF_HEADING),APF_HEADING
   SEGDO PUT_SYSOUT                    output concatenation name
   MVI   MESS_TXT+17,C'-'
   MVC   MESS_TXT+18(L'APF_HEADING-1),MESS_TXT+17
   SEGDO PUT_SYSOUT                    underline it
*
   ZAP   DSN_COUNT,=P'0'               initialise
*  L     R5,APFHNUMREC                 number of libraries
   L     R5,APFH#REC                   number of libraries
   LA    R6,APFHDR
   A     R6,APFHOFF
   USING APFE,R6
   DO FROM=(R5)
     AP    DSN_COUNT,=P'1'             increment count
     MVC   MESS_TXT(L'ED_PAT),ED_PAT         set edit mask
     ED    MESS_TXT(L'ED_PAT),DSN_COUNT      edit name count
     MVC   MESS_TXT+L'ED_PAT+1(6),APFEVOLUME      volume
     MVC   MESS_TXT+L'ED_PAT+8(44),APFEDSNAME     dataset name
     SEGDO PUT_SYSOUT                  output details
     AH    R6,APFELEN
   ENDDO
*
 SEGE BUILD_APF_LIST
*______________________________________________________________________
*
 SEGS CHECK_CONCAT
*
   SEGDO PUT_SYSOUT                    output blank line
   MVC   MESS_TXT+8(8),USE_LIB
   MVC   MESS_TXT+16(L'CON_CAT),CON_CAT
   SEGDO PUT_SYSOUT                    output concatenation name
   MVI   MESS_TXT+8,C'-'
   MVC   MESS_TXT+9(L'CON_CAT+7),MESS_TXT+8
   SEGDO PUT_SYSOUT                    underline it
*
   LA    R8,TESTDCB                    point to TESTDCB
   USING IHADCB,R8                     map DCBD
   MVC   DCBDDNAM,USE_LIB              copy required DDNAME
   RDJFCB  ((R8))                      read details of DDNAME
   IF (LTR,R15,R15,NZ)                 not available
     MVC   MESS_TXT+8(L'MISS_MESS),MISS_MESS
     SEGDO PUT_SYSOUT                  output missing message
     SEGQUIT                           get out of segment
   ENDIF
*
   MVC   KEEP_ARLAREA,ARLAREA          save ARLA details
   MVC   KEEP_ARLRTRVD,ARLRTRVD
   XC    KEEP_ARLRLEN,KEEP_ARLRLEN
   MVC   KEEP_ARLRLEN+1(3),ARLRLEN
   XC    KEEP_ARLPOOL,KEEP_ARLPOOL
   MVC   KEEP_ARLPOOL+1(1),ARLPOOL
   MVC   THIS_ARL_REQ,=H'1'
*
   DO INF
     MVC   TEST_IT(6),JFCBVOLS         copy first volume of DSN
     MVC   TEST_IT+6(L'JFCBDSNM),JFCBDSNM  and dataset name
     MVC   MESS_TXT(6),JFCBVOLS        output first volume of DSN
     MVC   MESS_TXT+8(L'JFCBDSNM),JFCBDSNM
     LA    R6,APFHDR
     A     R6,APFHOFF
*    L     R15,APFHNUMREC              get count
     L     R15,APFH#REC                get count
     DO FROM=(R15)
     DOEXIT (CLC,TEST_IT,EQ,APFEVOLUME)  matching volume+dataset name
       AH    R6,APFELEN
     ENDDO
     IF (LTR,R15,R15,Z)                not found in list
       MVC   MESS_TXT+10+L'JFCBDSNM(L'MISS_ING),MISS_ING
     ENDIF
     SEGDO PUT_SYSOUT                  output dataset name
   DOEXIT (CLC,KEEP_ARLRTRVD,LE,THIS_ARL_REQ)  exit at end of list
     SEGDO GET_NEXT_CONCATENATED_DETAILS
   ENDDO
*
   L     R2,KEEP_ARLAREA               release RDJFCB storage
   L     R3,KEEP_ARLRLEN
   LH    R15,KEEP_ARLPOOL
   FREEMAIN RU,LV=(R3),A=(R2),SP=(R15)
*
   DROP  R8
*
 SEGE CHECK_CONCAT
*______________________________________________________________________
*
 SEGS GET_NEXT_CONCATENATED_DETAILS
*
   L     R1,KEEP_ARLAREA               point to start
   IF (LTR,R1,R1,NZ)                   something there
     PUSH  USING
     USING ARA,R1
     IF (CLC,THIS_ARL_REQ,LT,KEEP_ARLRTRVD)  not at last dataset
       LH    R15,THIS_ARL_REQ                get dataset number
       MH    R15,ARALEN                      multiply by length
       LA    R1,0(R15,R1)                    point at details
       MVC   INFMJFCB(JFCBLGTH),ARAJFCB      and copy
       LH    R15,THIS_ARL_REQ                increment count
       LA    R15,1(,R15)
       STH   R15,THIS_ARL_REQ
     ENDIF
     POP   USING
   ENDIF
*
 SEGE GET_NEXT_CONCATENATED_DETAILS
*______________________________________________________________________
*
 SEGS WRITE_LOG
*
   WTOX
*
 SEGE WRITE_LOG
*______________________________________________________________________
*
 PRGDCBO DCB=SYSOUT,REC=MESS_TXT,CLEAR=Y
*______________________________________________________________________
*
               PRGSTAT
*
ED_PAT         DC    X'40202020202020202020202020202020'
APF_HEADING    DC    C'APF libraries'
CON_CAT        DC    C' concatenation'
MISS_ING       DC    C' not APF authorised'
MISS_MESS      DC    C'DDNAME not available or RDJFCB error'
ERR_MESS_0     DC    C'//SYSOUT DD SYSOUT=* is required in the JCL'
ERR_MESS_1     DC    C'More than &MAX_APF APF authorised libraries'
ERR_MESS_2     DC    C'Error in CSVAPF macro RC='
ERR_MESS_2_RC  DC    CL8' '
               DC    C'x RS='
ERR_MESS_2_RS  DC    CL8' '
               DC    C'x Avail='
ERR_MESS_2_AV  DC    CL7' '
               DC    C' Required='
ERR_MESS_2_TL  DC    CL7' '
ERR_MESS_2_L   EQU   *-ERR_MESS_2
*
SYSOUT   DCB   DSORG=PS,DDNAME=SYSOUT,LRECL=80,MACRF=(PM),RECFM=FB
*
*        A DCB used to get list of concatanated datasets
TESTDCB  DCB   DDNAME=XXXXXXXX,MACRF=(GL),DSORG=PS,EXLST=LIST1
LIST1    DS    0F
         DC    X'13',AL3(MY_JFCB_13)
         DC    X'87',AL3(MY_JFCB_07)
*
               LTORG
*
               PUSH  PRINT,NOPRINT
               PRINT NOGEN
MY_JFCB_07     DS    0F
               IEFJFCBN
LEN_MY_JFCB_07 EQU   *-MY_JFCB_07
MY_JFCB_13     IHAARL DSECT=NO
               IHAARA
               DCBD  DSORG=QS,DEVD=DA
               POP   PRINT,NOPRINT
*
               PRGESTAT
               PRGEND
               END
