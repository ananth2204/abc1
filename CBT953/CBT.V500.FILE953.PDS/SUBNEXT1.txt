/*REXX*/
/*                                                                 */
/*  This is the second part of the VERY basic schedule system.     */
/*  It is run by the step that is added to the end of the job      */
/*  to submit the next job.                                        */
/*                                                                 */
/*  This reads the list of remaining jobs and submits the first    */
/*  and includes the rest of the job list.                         */
/*                                                                 */
/*  If there are no more jobs in the list then the extra step      */
/*  is not added.                                                  */
/*                                                                 */
/*  Do not run this except from the step added by SUBNEXT          */
/*                                                                 */
/*      TSO EXEC 'MY.EXEC(SUBNEXT)' 'TEST.JCL(#SCHD01)'            */
/*                                                                 */

  ni = 0
  ab = 0

  /* read in the list of all the other jobs in the schedule        */
  do while ab = 0
     pull sched_name
     say  sched_name
     if sched_name = '' then ab = 1
     else do
        ni = ni + 1
        SCHED.ni = strip(sched_name)
     end
  end
  SCHED.0 = ni     /* put the total into the stem header           */

  first_job = strip(SCHED.1)           /* copy first jobname    */
  first_job = word(first_job,1)        /* trim any extra text   */
  say 'first job is ' first_job

  /* read the first job into a stem variable                       */
  "ALLOC FILE(INFILE) DSN("first_job") SHR"
  al_o = rc
  if al_o /= 0 then do
     say 'allocation INFILE to' first_job
     say 'alloc rc = ' al_o
     exit 12
  end
  "EXECIO * DISKR INFILE (STEM THEJOB. FINIS"
  "FREE  FILE(INFILE)"

  /* QUEUE each line of JCL until no more in stem                  */
  /* quit if a line contains just '//' in columns 1 and 2 as this  */
  /* is treated as end of file by SUBMIT.                          */
  DO I = 1 TO THEJOB.0        /* read all the lines of the member */
     test_end = strip(thejob.i)
     if length(test_end) = 2 then
        if test_end = "//" then leave               /* end of job */
     QUEUE thejob.i           /* and queue them                   */
  END

  /* if there are more jobs in the schedule then include an        */
  /* additional step which will submit the subsequent jobs         */
  if sched.0 > 1 then do
     Queue "//****************************                   "
     Queue "//CHECKRC EXEC PGM=IKJEFT01,DYNAMNBR=20,COND=(0,NE)   "
     Queue "//SYSTSIN  DD  *                                 "
     Queue "  PROFILE NOPREFIX                               "
     Queue "  EXEC 'MY.EXEC(SUBNEXT1)'                       "
     DO I = 2 TO SCHED.0         /* output rest of schedule     */
        th_is = strip(sched.i)
        Queue "   " th_is   "                                "
     END
     Queue "//SYSTSPRT DD  SYSOUT=*                          "
  end

  /* finally add a step to output job statistics                   */
  Queue "//****************************                      "
  Queue "//STATS   EXEC FLOWER                               "
  Queue "ZZ"
  "SUBMIT * END(ZZ)"
  EXIT
