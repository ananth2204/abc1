 TITLE 'PIPECNT - PIPE SYMBOL COUNTER'
*______________________________________________________________________
*
*    READ A PIPE ¦ DELIMITED FILE IN SYSIN COUNTING THE NUMBER OF
*    PIPES IN EACH RECORD AND THE MAXIMUM LENGTH OF EACH FIELD.
*    OUTPUT FILE SUMMARY.
*
*    CAN SPECIFY DELIMETER USING PARM='|' IN JCL
*______________________________________________________________________
*
PIPECNT  PRGDEF  FSEG=MAIN_PART,RMODE=24
*
ADDR_OUTREC            DC    F'0'
ADDR_OUTREC_POS        DC    F'0'
ADDR_OUTREC_CNT        DC    F'0'
*
WORK8                  DS    CL8
THIS_COUNT             DS    PL4
*
WAS_DELI_METER         DS    CL1
EOF_SYSIN              DS    CL1
VARIABLE_RECORD        DS    CL1
SYSOUT_REC             DS    CL80
*
OUTREC_DSECT           DSECT
MAX_COUNT              DC    PL4'0'
NEXT_OUTREC            DC    F'0'
LEN_OUTREC             EQU   *-OUTREC_DSECT
*
         PRGEDEF
*
    USING OUTREC_DSECT,R9
    USING IHADCB,SYSIN
*______________________________________________________________________
*
 SEGS MAIN_PART
*
   L     R1,0(,R1)               GET PARAMETER ADDRESS
   IF (CLC,0(2,R1),EQ,=H'1')
     MVC   DELI_METER,2(R1)
   ENDIF
*
   MVI   SYSOUT_REC,C' '
   MVC   SYSOUT_REC+1(L'SYSOUT_REC-1),SYSOUT_REC
*
   SEGDO OPEN_SYSIN
   SEGDO OPEN_SYSOUT
*
   MVC   SYSOUT_REC(30),=CL30'PROGRAM STARTING'
   SEGDO WRITE_SYSOUT
   SEGDO WRITE_SYSOUT
   MVC   SYSOUT_REC(30),=CL30'  DELIMITER USED WAS '
   MVC   SYSOUT_REC+30(1),DELI_METER
   SEGDO WRITE_SYSOUT
   SEGDO WRITE_SYSOUT
*
   DO INF
     SEGDO GET_SYSIN
   DOEXIT (CLI,EOF_SYSIN,EQ,C'Y')
     SEGDO MAIN_PROCESS
   ENDDO
*
   L     R9,ADDR_OUTREC
   DO INF
   DOEXIT (LTR,R9,R9,Z)
     MVC   SYSOUT_REC+1(L'ED_PL4),ED_PL4
     ED    SYSOUT_REC+1(L'ED_PL4),MAX_COUNT
     SEGDO WRITE_SYSOUT
     L     R9,NEXT_OUTREC
   ENDDO
*
*  use LNKDEL to free the linked list built by LNKBLD
*
   LNKDEL  START=ADDR_OUTREC,NEXT=NEXT_OUTREC,                         /
               REG=R9,LENGTH=LEN_OUTREC
*
   SEGDO WRITE_SYSOUT
   MVC   SYSOUT_REC(30),=CL30'PROGRAM TERMINATING'
   SEGDO WRITE_SYSOUT
*
   SEGDO CLOSE_SYSIN
   SEGDO CLOSE_SYSOUT
*
 SEGE MAIN_PART
*______________________________________________________________________
*
 SEGS MAIN_PROCESS
*
   IF (CLI,VARIABLE_RECORD,EQ,C'Y')
     LH    R5,0(,R8)                   get actual record length
     SH    R5,=H'4'                    subtract RDW length
     LA    R8,4(,R8)                   point to data start
   ELSE
     LH    R5,DCBLRECL                 get record length
   ENDIF
*
   ZAP   THIS_COUNT,=P'0'
   L     R9,ADDR_OUTREC                point to first entry in list
   MVI   WAS_DELI_METER,C'N'
   DO FROM=(R5)
     IF (CLC,0(1,R8),EQ,DELI_METER)    found delimeter
       MVI   WAS_DELI_METER,C'Y'       flag it
       IF (LTR,R9,R9,Z)                no current list item
         SEGDO ADD_TO_OUTREC_TABLE
       ENDIF
       IF (CP,THIS_COUNT,GT,MAX_COUNT)
         ZAP   MAX_COUNT,THIS_COUNT    set new maximum field length
       ENDIF
       L     R9,NEXT_OUTREC            point to next item
       ZAP   THIS_COUNT,=P'0'          reset field length
     ELSE
       MVI   WAS_DELI_METER,C'N'
       AP    THIS_COUNT,=P'1'          increment field length
     ENDIF
     LA    R8,1(,R8)
   ENDDO
   IF (CLI,WAS_DELI_METER,NE,C'Y')     did not end on a delimeter
     IF (LTR,R9,R9,Z)                  no current list item
       SEGDO ADD_TO_OUTREC_TABLE
     ENDIF
     IF (CP,THIS_COUNT,GT,MAX_COUNT)
       ZAP   MAX_COUNT,THIS_COUNT      set new maximum field length
     ENDIF
     L     R9,NEXT_OUTREC              point to next item
     ZAP   THIS_COUNT,=P'0'
   ENDIF
*
 SEGE MAIN_PROCESS
*______________________________________________________________________
*
 SEGS ADD_TO_OUTREC_TABLE
*
   L     R9,ADDR_OUTREC_POS
*
*  use LNKBLD to build a linked table of getmained storage containing
*  the count of characters between each delimiter.
*
   LNKBLD  START=ADDR_OUTREC,NEXT=NEXT_OUTREC,LOC=31,                  /
               REG=R9,LENGTH=LEN_OUTREC,                               /
               END=ADDR_OUTREC_POS,COUNT=ADDR_OUTREC_CNT
*
   XC    NEXT_OUTREC,NEXT_OUTREC       initialise next pointer
   ZAP   MAX_COUNT,=P'0'               initialise count
*
 SEGE ADD_TO_OUTREC_TABLE
*______________________________________________________________________
*
 SEGS WRITE_SYSOUT
*
   PUT   SYSOUT,SYSOUT_REC
*
   MVI   SYSOUT_REC,C' '
   MVC   SYSOUT_REC+1(L'SYSOUT_REC-1),SYSOUT_REC
*
 SEGE WRITE_SYSOUT
*______________________________________________________________________
*
 SEGS OPEN_SYSOUT
*
   OPEN  (SYSOUT,OUTPUT),MODE=31
   IF (LTR,R15,R15,NZ)
     PRGQUIT RC=32
   ENDIF
*
 SEGE OPEN_SYSOUT
*______________________________________________________________________
*
 SEGS CLOSE_SYSOUT
*
   CLOSE (SYSOUT),MODE=31
   IF (LTR,R15,R15,NZ)
     PRGQUIT RC=34
   ENDIF
*
 SEGE CLOSE_SYSOUT
*______________________________________________________________________
*
 SEGS OPEN_SYSIN
*
   OPEN  (SYSIN,INPUT),MODE=31
   IF (LTR,R15,R15,NZ)
     PRGQUIT RC=36
   ENDIF
   IF (TM,DCBRECFM,DCBRECV,NZ)
     MVI   VARIABLE_RECORD,C'Y'
   ENDIF
*
 SEGE OPEN_SYSIN
*______________________________________________________________________
*
 SEGS CLOSE_SYSIN
*
   CLOSE (SYSIN),MODE=31
   IF (LTR,R15,R15,NZ)
     PRGQUIT RC=38
   ENDIF
*
 SEGE CLOSE_SYSIN
*______________________________________________________________________
*
 SEGS GET_SYSIN
*
   MVI   EOF_SYSIN,C'N'
   GET   SYSIN
   LR    R8,R1
   SEGQUIT                             return to calling segment
*
ENDOFEM    DS    0H                    it is the end of SYSIN
   MVI   EOF_SYSIN,C'Y'                so flag it
*
 SEGE GET_SYSIN
*______________________________________________________________________
*
         PRGSTAT
*
SYSIN    DCB   DDNAME=SYSIN,MACRF=(GL),DSORG=PS,EODAD=ENDOFEM
SYSOUT   DCB   DDNAME=SYSOUT,MACRF=(PM),DSORG=PS,RECFM=FB,LRECL=80
*
DELI_METER     DC    C'¦'
ED_PL4         DC    X'4020202020202120'
*______________________________________________________________________
*
         LTORG
*______________________________________________________________________
*
         DCBD  DSORG=QS,DEVD=DA
*______________________________________________________________________
*
         PRGESTAT
         PRGEND
         END
