 TITLE 'STEMSR35 - E35 exit for the SORT, part of STEMSORT'
*______________________________________________________________________
*
* Written by John Gateley - use at your own risk
*
* This is called by the SORT to output the sorted record.
*______________________________________________________________________
*
STEMSR35 PRGDEF FSEG=MAIN_PART,INREX=Y
*
         USING STMSRTLN,R3
         USING STEM_AREA,R6
*
         PRGEDEF
*______________________________________________________________________
*
 SEGS MAIN_PART
*
*  R3 points to    RX_REC_LEN     DS    F
*                  RX_REC_ADR     DS    F
*                  RX_THE_STEM    DS    F
*                  RX_VAR_LEN_ADR DS    F
*                  RX_#SAV_REX    DS    4F
*                  RX_TOT_RECS    DS    PL4
*
   L     R7,0(,R1)                     address of the output record
   L     R3,=V(STMSRTLN)               point to stuff in STEMSORT
   L     R4,RX_REC_LEN                 get sort length
   L     R6,RX_THE_STEM                get output stem area
   ST    R6,MY_P_1                     save as first parm
   MVC   #SAV_REX(4*4),RX_#SAV_REX     copy IRX routine addresses
   IF (LTR,R7,R7,NZ)                   is there a record to output
     ST    R7,STEM_TEXT_ADR            save output record address
     LA    R7,0(R4,R7)                 point to end of record
     MVC   STEM_TEXT_LEN,0(R7)         copy original length
     MVHHI STEM_FUNCTION,2             set STEM value
     LA    R1,MY_PARM                  point to parameter list
     L     R15,=V(PROCSTEM)            get program address
     BASR  R14,R15                     go and set stem value
     IF (LTR,R15,R15,NZ)               error ?
       ST    R15,R_C                   set return code
       MVC   MESS_TXT(20),=CL20'PROCSTEM failed  RC='
       XUNPK R_C,4,MESS_TXT+20
       SEGDO WRITE_LOG
       PRGQUIT                         QUIT
     ENDIF
     SETRC RC=4                        next record wanted
   ELSE
     MVHHI STEM_FUNCTION,3             set stem.0 to STEM_ZERO_COUNT
     LA    R1,MY_PARM                  point to parameter list
     L     R15,=V(PROCSTEM)            get program address
     BASR  R14,R15                     go and get stem value
     IF (LTR,R15,R15,NZ)               error ?
       ST    R15,R_C                   set return code
       PRGQUIT                         QUIT
     ENDIF
     SETRC RC=8                        no more output records
   ENDIF
*
 SEGE MAIN_PART
*______________________________________________________________________
*
 SEGS WRITE_LOG
*
   WTOX
*
 SEGE WRITE_LOG
*______________________________________________________________________
*
         PRGSTAT
*
         LTORG
*
         PRGSTEM DSECT=Y
*
STMSRTLN       DSECT
RX_REC_LEN     DS    F
RX_REC_ADR     DS    F
RX_THE_STEM    DS    F
RX_VAR_LEN_ADR DS    F
RX_#SAV_REX    DS    4F                rexx environment addresses
RX_TOT_RECS    DS    PL4
LEN_STMSRTLN   EQU   *-STMSRTLN
*
         PRGESTAT
         PRGEND
         END
