       Identification Division.
       Program-ID.    J7200507.
       Author.        Craig Schneiderwent.
      *
      *
      * Sample Use:
      *
      * Working-Storage Section.
      * ...
      * COPY J7200507 REPLACING ==:PRFX:== BY ==J7200507-==.
      * ...
      * Procedure Division.
      * CALL MQ-INQ-PGM USING
      *     HCONN
      *     J7200507-SELECTORCOUNT
      *     J7200507-SELECTORS-TABLE
      *     J7200507-INTATTRCOUNT
      *     J7200507-INTATTRS-TABLE
      *     J7200507-CHARATTRLENGTH
      *     J7200507-CHARATTRS-TABLE
      *     WS-CMQODV-INQ
      *     WS-CMPN-CD
      *     WS-RSN-CD
      * END-CALL
      *
      * IF WS-CMPN-CD = MQCC-OK
      *     inquiry was successful
      * ELSE
      *     error processing
      * END-IF
      *
      * Return Codes:
      *
      * INFO-RETRIEVED-OK           VALUE +0.
      * MQ-ERROR                    VALUES +10 THRU +19.
      * OPEN-FAILED                 VALUE +11.
      * INQUIRE-FAILED              VALUE +12.
      * CLOSE-FAILED                VALUE +13.
      *
      *
      *
       Environment Division.
       Data Division.
       Working-Storage Section.
       01  CONSTANTS.
           05  MYNAME                  PIC X(008) VALUE 'J7200507'.
           05  MQ-API-PGMS.
               COPY CMQAPIDC.
           05  WS-CMQV.
               COPY CMQV.

       Local-Storage Section.
       01  WORK-AREAS.
           05  OPEN-OPTIONS            PIC S9(008) COMP-5 VALUE +0.
           05  CLOSE-OPTIONS           PIC S9(008) COMP-5 VALUE +0.
           05  HOBJ                    PIC S9(008) COMP-5.
           05  SAVE-RC                 PIC S9(008) COMP-5 VALUE +0.
               88  INFO-RETRIEVED-OK           VALUE +0.
               88  MQ-ERROR                    VALUES +10 THRU +19.
               88  OPEN-FAILED                 VALUE +11.
               88  INQUIRE-FAILED              VALUE +12.
               88  CLOSE-FAILED                VALUE +13.

       Linkage Section.
       01  DFHCOMMAREA.
           05  LS-HCONN    PIC S9(008) COMP-5.
       COPY J7200507 REPLACING ==:PRFX:== BY ==LS-==.
       01  LS-MQOD.
       COPY CMQODL.
       01  LS-CMPN-CD  PIC S9(008) COMP-5.
       01  LS-RSN-CD   PIC S9(008) COMP-5.

       Procedure Division Using
           DFHCOMMAREA
           LS-SELECTORCOUNT
           LS-SELECTORS-TABLE
           LS-INTATTRCOUNT
           LS-INTATTRS-TABLE
           LS-CHARATTRLENGTH
           LS-CHARATTRS-TABLE
           LS-MQOD
           LS-CMPN-CD
           LS-RSN-CD
           .

Debug *    DISPLAY
Debug *        MYNAME
Debug *        ' Begin'

           COMPUTE OPEN-OPTIONS =
               MQOO-INQUIRE +
               MQOO-FAIL-IF-QUIESCING
           MOVE MQCO-NONE      TO CLOSE-OPTIONS

           PERFORM 8010-OPEN

           IF LS-CMPN-CD = MQCC-OK
               PERFORM 8020-INQUIRE
               IF LS-CMPN-CD = MQCC-OK
                   PERFORM 8030-CLOSE
               END-IF
           END-IF

Debug *    DISPLAY
Debug *        MYNAME
Debug *        ' End'

           MOVE SAVE-RC TO RETURN-CODE
           GOBACK
           .

       8010-OPEN.
Debug *    DISPLAY
Debug *        MYNAME
Debug *        ' 8010-OPEN'

           CALL MQOPEN USING
               LS-HCONN
               LS-MQOD
               OPEN-OPTIONS
               HOBJ
               LS-CMPN-CD
               LS-RSN-CD
           END-CALL

           IF LS-CMPN-CD = MQCC-OK
               CONTINUE
           ELSE
               SET OPEN-FAILED TO TRUE
           END-IF
           .

       8020-INQUIRE.
Debug *    DISPLAY
Debug *        MYNAME
Debug *        ' 8020-INQUIRE'

           CALL MQINQ USING
               LS-HCONN
               HOBJ
               LS-SELECTORCOUNT
               LS-SELECTORS-TABLE
               LS-INTATTRCOUNT
               LS-INTATTRS-TABLE
               LS-CHARATTRLENGTH
               LS-CHARATTRS-TABLE
               LS-CMPN-CD
               LS-RSN-CD
           END-CALL

           IF LS-CMPN-CD = MQCC-OK
               CONTINUE
           ELSE
               SET INQUIRE-FAILED TO TRUE
           END-IF
           .

       8030-CLOSE.
Debug *    DISPLAY
Debug *        MYNAME
Debug *        ' 8030-CLOSE'

           CALL MQCLOSE USING
               LS-HCONN
               HOBJ
               CLOSE-OPTIONS
               LS-CMPN-CD
               LS-RSN-CD
           END-CALL

           IF LS-CMPN-CD = MQCC-OK
               CONTINUE
           ELSE
               SET CLOSE-FAILED TO TRUE
           END-IF
           .

