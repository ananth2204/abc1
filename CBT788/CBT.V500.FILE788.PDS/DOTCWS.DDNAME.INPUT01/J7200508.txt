       Identification Division.
       Program-ID.    J7200508.
       Author.        Craig Schneiderwent.
      *
      * The purpose of this program is to examine a string of bytes and
      * output their hex codes in readable format.
      *
      * Input:  x'01234567'
      * Output: x'F0F1F2F3F4F5F6F7'
      *
      * The output string must be twice the length of the input string.
      *
      * The length of the input string must not exceed 16K.
      *
      * This module is dynamically callable in CICS or batch.
      *
      * Sample Usage:
      *
      * Working-Storage Section.
      * 01  CONSTANTS.
      *     05  HEX-CONV-PGM PIC X(008) VALUE 'J7200508'.
      *
      * 01  WORK-AREAS.
      *     05  STRING-LN PIC S9(008) COMP-5.
      *     05  A-STRING  PIC X(008).
      *     05  X-STRING  PIC X(016).
      *
      * Procedure Division.
      * MOVE LENGTH OF A-STRING TO STRING-LN
      * MOVE X'0123456789ABCDEF' TO A-STRING
      * CALL HEX-CONV-PGM USING
      *     STRING-LN
      *     A-STRING
      *     X-STRING
      * END-CALL
      *
      *
       Environment Division.
       Data Division.
       Working-Storage Section.
       01  CONSTANTS.
           05  MYNAME                  PIC X(008) VALUE 'J7200508'.
           05  HEX-CODE-VAL.
               10                      PIC X(160)
                                 VALUE '000102030405060708090A0B0C0D0E0F
      -                                '101112131415161718191A1B1C1D1E1F
      -                                '202122232425262728292A2B2C2D2E2F
      -                                '303132333435363738393A3B3C3D3E3F
      -                             '404142434445464748494A4B4C4D4E4F'.
               10                      PIC X(160)
                                 VALUE '505152535455565758595A5B5C5D5E5F
      -                                '606162636465666768696A6B6C6D6E6F
      -                                '707172737475767778797A7B7C7D7E7F
      -                                '808182838485868788898A8B8C8D8E8F
      -                             '909192939495969798999A9B9C9D9E9F'.
               10                      PIC X(160)
                                 VALUE 'A0A1A2A3A4A5A6A7A8A9AAABACADAEAF
      -                                'B0B1B2B3B4B5B6B7B8B9BABBBCBDBEBF
      -                                'C0C1C2C3C4C5C6C7C8C9CACBCCCDCECF
      -                                'D0D1D2D3D4D5D6D7D8D9DADBDCDDDEDF
      -                             'E0E1E2E3E4E5E6E7E8E9EAEBECEDEEEF'.
               10                      PIC X(032)
                              VALUE 'F0F1F2F3F4F5F6F7F8F9FAFBFCFDFEFF'.
           05  HEX-CODE-TBL
               REDEFINES
               HEX-CODE-VAL
               OCCURS 256              PIC X(002).

       Local-Storage Section.
       01  WORK-AREAS.
           05  WS-HEX-TBL-SUB          PIC S9(008) COMP-5 VALUE +0.
           05  WS-HEX-TBL-SUB-X
               REDEFINES
               WS-HEX-TBL-SUB.
               10                      PIC X(003).
               10  WS-HEX-TBL-SUB-LOW-BYTE
                                       PIC X(001).
           05  WS-HEX-STRN-SUB         PIC S9(008) COMP-5 VALUE +0.
           05  WS-HEX-STRN-OUT-LN      PIC S9(008) COMP-5.

       Linkage Section.
       01  LS-HEX-STRN-IN-LN PIC S9(008) COMP-5.
       01  LS-HEX-STRN-IN.
           05  LS-HEX-STRN-IN-TBL
               OCCURS 1 TO 16384 DEPENDING ON LS-HEX-STRN-IN-LN
               PIC X(001).
       01  LS-HEX-STRN-OUT.
           05  LS-HEX-STRN-OUT-TBL
               OCCURS 1 TO 32768 DEPENDING ON WS-HEX-STRN-OUT-LN
               PIC X(002).

       Procedure Division Using
           LS-HEX-STRN-IN-LN
           LS-HEX-STRN-IN
           LS-HEX-STRN-OUT
           .

           COMPUTE WS-HEX-STRN-OUT-LN = LS-HEX-STRN-IN-LN * 2
           PERFORM VARYING WS-HEX-STRN-SUB FROM 1 BY 1
           UNTIL WS-HEX-STRN-SUB > LS-HEX-STRN-IN-LN
               MOVE +0 TO WS-HEX-TBL-SUB
               MOVE LS-HEX-STRN-IN-TBL(WS-HEX-STRN-SUB)
                 TO WS-HEX-TBL-SUB-LOW-BYTE
               ADD  1 TO WS-HEX-TBL-SUB
               MOVE HEX-CODE-TBL(WS-HEX-TBL-SUB)
                 TO LS-HEX-STRN-OUT-TBL(WS-HEX-STRN-SUB)
           END-PERFORM
           GOBACK
           .

