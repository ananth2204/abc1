       Identification Division.
       Program-ID. J7200505.
       Author. Craig Schneiderwent.
      *
      * Return name of queue manager this process is currently
      * connected to (CICS) or should connect to (Batch).
      *
      * Syntax:
      *
      * CALL J7200505-PGM USING
      *     ENVR-LTR
      *     QMGR-NM
      *     CMPN-CD
      *     RSN-CD
      * END-CALL
      *
      * Return codes:
      * 0 - success
      * 1 - MQOPEN failed
      * 2 - MQINQ failed
      * 3 - MQCLOSE failed
      * 8 - LPAR/Environment not found in internal table
      *
       Environment Division.
       Data Division.
       Working-Storage Section.
       01  CONSTANTS.
           05  MYNAME               PIC X(008) VALUE 'J7200505'.
           05  SYSINFO-PGM          PIC X(008) VALUE 'J7200504'.
           05  QMGR-PGM             PIC X(008) VALUE 'J7200503'.
           05  LPAR-QMGR-XREF-X     PIC X(080)
                                                 VALUE 'PRD1M01PPRD2M02P
      -                                                'DEV1M01DDEV2M02D
      -                                                'DEV1M01SDEV2M02S
      -                                                'DEV1M01ADEV2M02A
      -                                             'PRD1M01EPRD2M02E'.
           05  LPAR-QMGR-XREF
               REDEFINES
               LPAR-QMGR-XREF-X
               OCCURS 10 TIMES
               INDEXED LPAR-QMGR-XREF-INDX.
               10  LPAR-NM          PIC X(004).
               10  QMGR-NM.
                   15               PIC X(003).
                   15  QMGR-ENVR    PIC X(001).


       COPY SYSINFO2.

       Linkage Section.
      *    Input field  - environment letter
       01  IN-ENVR              PIC X(001).
      *    Output field - set to queue manager name
       01  OUT-QMGR-NM          PIC X(048).
      *    Output field - set to MQ completion code of last call
       01  OUT-CMPN-CD          PIC S9(008) COMP-5.
      *    Output field - set to MQ reason code of last call
       01  OUT-RSN-CD           PIC S9(008) COMP-5.

       Procedure Division Using
           IN-ENVR
           OUT-QMGR-NM
           OUT-CMPN-CD
           OUT-RSN-CD
           .

           CALL SYSINFO-PGM USING
               SYSINFO2-PARMS
           END-CALL

           IF SYSINFO2-PGM-NM(1:3) = 'DFH'
               PERFORM 8010-THIS-IS-CICS
           ELSE
               PERFORM 8020-SEARCH-FOR-QMGR-NM
           END-IF

           GOBACK
           .

       8010-THIS-IS-CICS.
           CALL QMGR-PGM USING
               OUT-QMGR-NM
               OUT-CMPN-CD
               OUT-RSN-CD
           END-CALL
           .

       8020-SEARCH-FOR-QMGR-NM.
           SET LPAR-QMGR-XREF-INDX TO 1
           SEARCH LPAR-QMGR-XREF VARYING LPAR-QMGR-XREF-INDX
               AT END MOVE 8 TO RETURN-CODE
               WHEN LPAR-NM( LPAR-QMGR-XREF-INDX )
                  = SYSINFO2-SMF-SYSID
                AND QMGR-ENVR( LPAR-QMGR-XREF-INDX ) = IN-ENVR
                    MOVE QMGR-NM( LPAR-QMGR-XREF-INDX )
                      TO OUT-QMGR-NM
           END-SEARCH
           .

