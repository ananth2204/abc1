       Identification Division.
       Program-ID. J7200503.
       Author. Craig Schneiderwent.
      *
      * Return name of queue manager this process is currently
      * connected to.
      *
      * Syntax:
      *
      * CALL J7200503-PGM USING
      *     QMGR-NM
      *     CMPN-CD
      *     RSN-CD
      * END-CALL
      *
      * Return codes:
      * 0 - success
      * 1 - MQOPEN failed
      * 2 - MQINQ failed
      * 3 - MQCLOSE failed
      *
       Environment Division.
       Data Division.
       Working-Storage Section.
       01  CONSTANTS.
           05  MYNAME               PIC X(008) VALUE 'J7200503'.
           05  MQ-INQ-PGM           PIC X(008) VALUE 'J7200507'.
           05  SELECTED-CMQV-FIELDS.
               10  MQCC-OK                PIC S9(8) COMP-5 VALUE 0.
               10  MQCA-Q-MGR-NAME        PIC S9(8) COMP-5 VALUE 2015.
               10  MQ-Q-MGR-NAME-LENGTH   PIC S9(8) COMP-5 VALUE 48.
               10  MQOT-Q-MGR             PIC S9(8) COMP-5 VALUE 5.
               10  MQOO-INQUIRE           PIC S9(8) COMP-5 VALUE 32.
               10  MQOO-FAIL-IF-QUIESCING PIC S9(8) COMP-5 VALUE 8192.
               10  MQCO-NONE              PIC S9(8) COMP-5 VALUE 0.
               10  MQHC-DEF-HCONN         PIC S9(8) COMP-5 VALUE 0.
           05  MQ-API-PGMS.
               COPY CMQAPIDC.
           05  SUCCESSFUL-CALL            PIC S9(008) COMP-5 VALUE +0.
           05  OPEN-FAILED                PIC S9(008) COMP-5 VALUE +1.
           05  INQUIRE-FAILED             PIC S9(008) COMP-5 VALUE +2.
           05  CLOSE-FAILED               PIC S9(008) COMP-5 VALUE +3.

       01  WORK-AREAS.
           05  HCONN          PIC S9(008) COMP-5.
           05  OBJECT-DESCRIPTOR.
               COPY CMQODV.
           05  J7200507-RC    PIC S9(008) COMP-5 VALUE +0.
               88  INFO-RETURNED-OK                      VALUE +0.
               88  MQ-ERROR                    VALUES +10 THRU +19.
               88  OPEN-FAILED                           VALUE +11.
               88  INQUIRE-FAILED                        VALUE +12.
               88  CLOSE-FAILED                          VALUE +13.

       COPY J7200507 REPLACING ==:PRFX:== BY ==J7200507-==.

       Linkage Section.
      *    Output field - set to queue manager name
       01  QMGR-NM          PIC X(048).
      *    Output field - set to MQ completion code of last call
       01  CMPN-CD          PIC S9(009) BINARY.
      *    Output field - set to MQ reason code of last call
       01  RSN-CD           PIC S9(009) BINARY.

       Procedure Division Using
           QMGR-NM
           CMPN-CD
           RSN-CD
           .

           MOVE SUCCESSFUL-CALL TO RETURN-CODE

           MOVE +0                   TO J7200507-INTATTRCOUNT
           MOVE +1                   TO J7200507-SELECTORCOUNT
           MOVE MQ-Q-MGR-NAME-LENGTH TO J7200507-CHARATTRLENGTH
           MOVE MQCA-Q-MGR-NAME      TO J7200507-SELECTORS( 1 )
           MOVE MQOT-Q-MGR           TO MQOD-OBJECTTYPE
           MOVE MQHC-DEF-HCONN       TO HCONN

           CALL MQ-INQ-PGM USING
               HCONN
               J7200507-SELECTORCOUNT
               J7200507-SELECTORS-TABLE
               J7200507-INTATTRCOUNT
               J7200507-INTATTRS-TABLE
               J7200507-CHARATTRLENGTH
               J7200507-CHARATTRS-TABLE
               OBJECT-DESCRIPTOR
               CMPN-CD
               RSN-CD
           END-CALL

           IF CMPN-CD = MQCC-OK
               MOVE J7200507-CHARATTRS-TABLE TO QMGR-NM
           ELSE
               MOVE RETURN-CODE TO J7200507-RC
               EVALUATE TRUE
                   WHEN OPEN-FAILED     OF J7200507-RC
                        MOVE OPEN-FAILED OF CONSTANTS TO RETURN-CODE
                   WHEN INQUIRE-FAILED  OF J7200507-RC
                        MOVE INQUIRE-FAILED OF CONSTANTS TO RETURN-CODE
                   WHEN CLOSE-FAILED    OF J7200507-RC
                        MOVE CLOSE-FAILED OF CONSTANTS TO RETURN-CODE
               END-EVALUATE
           END-IF

           GOBACK
           .

