/*REXX****************************************************************/
/* EXEC NAME := JSPCCONF (based on IBM exec: ISPCCONF from ISPF 5.7) */
/*                                                                   */
/* DESCRIPTIVE_NAME := Configuration table main driver               */
/*                                                                   */
/* FUNCTION  = Main driver exec for the configuration table dialog   */
/*                                                                   */
/* $M1= CONLEY  z/OSR5  06/19/2001 020136:  Force ISPCCONF to run    */
/*  under application id ISR                                         */
/* $01=OA09639 2004/11/04 HO: Debug not passed on recursion          */
/*-------------------------------------------------------------------*/
/* MODIFICATIONS (for "Extended ISPF Configuration Utility"):        */
/*    1) use panel JSPPCONF  (instead of IBM panel: ISPPCONF)        */
/*    2) add extra option 0, to invoke %JSPCATAB                     */
/*    3) change option 5, to invoke %JSPCCONV                        */
/*                                                                   */
/* Last Updated: 10 Jan 2007      by: Ron Brown        Version: 2.0  */
/*********************************************************************/
Trace o
Parse Upper Arg debug
Address ispexec
'VGET (ZAPPLID) SHARED'                                       /* @M1A*/
If zapplid ¬= 'ISR' Then                                      /* @M1A*/
  Do                                                          /* @M1A*/
    'SELECT CMD(ISPCCONF 'debug') NEWAPPL(ISR)'               /* @01C*/
    Exit                                                      /* @M1A*/
  End                                                         /* @M1A*/
'CONTROL ERRORS RETURN'
'VGET (ZCNVSRCE ZCNVSRCM ZCNVKWD ZCNVKWD2 ZCNVOPT) PROFILE'
'VGET (ZCNVLOAD ZCNVOBJ) PROFILE'
m = ''
c = 'ZCMD'
local_zcmd = ''
dsrc = 0
display_rc = 0
display_rc2 = 0
Do While display_rc = 0
  If m ¬= ' ' Then
    msg = 'MSG('m')'
  Else
    msg = ''
  If dsrc > 0 Then
    zcmd = local_zcmd
  'DISPLAY PANEL(JSPPCONF)' msg 'CURSOR('c')'        /** Ron **/
  display_rc = rc
  m = ''
  c = 'ZCMD'
  local_zcmd = zcmd
  zcmd = ''
  dsrc = 0
  If display_rc = 0 Then
    Do
      'VPUT (ZCNVSRCE ZCNVSRCM ZCNVKWD ZCNVKWDM ZCNVOPT) PROFILE'
      Select
        When local_zcmd = ' ' Then
          m = 'ISRU292 '
        /* ---- start of lines added by Ron ----------------------- */
        When local_zcmd = 0 Then
          Do
            m = 'ISPC255'
            If zcnvsrce = '' Then Do    /* no file name specified */
               zuser = Userid()
               If Sysvar(SYSPREF) <> zuser & Sysvar(SYSPREF) <> '' Then
                  zuser = Sysvar(SYSPREF)||'.'||zuser
               zcnvsrce = "'"zuser".JSPCCONF.ASM'"
               'VPUT (ZCNVSRCE) PROFILE'
            End
            Address TSO '%JSPCATAB' debug
            If rc > 0 Then
              m = 'ISPC256'
            'VGET (ZCNVCSR) PROFILE'
            c = zcnvcsr
          End
        /* ---- end of lines added by Ron ------------------------- */
        When local_zcmd = 1 Then
          Do
            m = 'ISPC250'
            Call verify_keyword_file
            If dsrc = 0 Then
              Do
                c = 'ZCMD'
                Address TSO ispcmod
                dsrc = rc
                'VGET (ZCNVOUTN) SHARED'
                If dsrc > 0 Then
                  m = ''
                If dsrc = 4 Then
                  dsrc = 0
              End
          End
        When local_zcmd = 2 Then
          Do
            m = 'ISPC271'
            Call verify_keyword_file
            If dsrc = 0 Then
              Do
                c = 'ZCMD'
                'CONTROL REFLIST NOUPDATE'
                "LMINIT DATASET("zcnvkwd") DATAID(DATA1)"
                'CONTROL REFLIST UPDATE'
                "EDIT DATAID("data1") MEMBER("zcnvkwdm")",
                      "PANEL(ISPCEDIT)"
                e_rc = rc
                "LMFREE DATAID("data1")"
                If e_rc = 0 Then
                  Do
                    'ADDPOP'
                    'DISPLAY PANEL(ISPPVERQ)'
                    ver_rc = rc
                    'REMPOP'
                    If ver_rc = 0 Then
                      Do
                        Address TSO ispcverf
                        dsrc = rc
                        Address ISPEXEC 'VGET (ZKWD) SHARED'
                        If dsrc > 0 Then
                          m = 'ISPC272'
                      End
                    Else
                      m = 'ISPC273'
                  End
                Else
                  Do
                    m = ''
                    If e_rc > 4 Then
                      'SETMSG MSG(ISRZ002)'
                  End
              End
          End
        When local_zcmd = 3 Then
          Do
            m = 'ISPC251'
            Call verify_keyword_file
            If dsrc = 0 Then
              Do
                c = 'ZCMD'
                Address TSO ispcverf
                dsrc = rc
                If dsrc > 0 Then
                  m = 'ISPC252'
              End
          End
        When (local_zcmd = 4 | local_zcmd = 6) Then
          Do
            If local_zcmd = 4 Then
              Do
                zcnvtype = 'LOAD'
                build_panel = 'ISPPBLD'
                'VGET (ZCNVLOAD ZCNVOBJ ZCNVKWD) PROFILE'
                'VGET (ZCNVMEM ZCNVMEM2 ZCNVKWDM) PROFILE'
              End
            Else
              Do
                zcnvtype = 'SMPE'
                build_panel = 'ISPPSMP'
                'VGET (ZCNVKWD ZCNVKWDM ZCNVSMPE ZCNVFMID) PROFILE'
                'VGET (ZCNVSMID ZCNVSSRC ZCNVSLOD) PROFILE'
                'VGET (ZCNVSUP1 ZCNVDSRC ZCNVDLOD) PROFILE'
                'VGET (ZCNVSUP2 ZCNVSUP3 ZCNVSUP4) PROFILE'
              End
            dsrc = 0
            Do Until dsrc = 0 | display_rc2 > 0
              dsrc = 0
              If m ¬= ' ' Then
                msg = 'MSG('m')'
              Else
                msg = ''
              'ADDPOP'
              'DISPLAY PANEL('build_panel')' msg 'CURSOR('c')'
              display_rc2 = rc
              'REMPOP'
              m = ''
              c = 'ZCMD'
              If display_rc2 = 0 Then
                Do
                  If zcnvtype = 'LOAD' Then
                    Do
                    'VPUT (ZCNVLOAD ZCNVOBJ ZCNVKWD) PROFILE'
                    'VPUT (ZCNVMEM ZCNVMEM2 ZCNVKWDM) PROFILE'
                    End
                  Else
                    Do
                    'VPUT (ZCNVKWD ZCNVKWDM ZCNVSMPE ZCNVFMID) PROFILE'
                    'VPUT (ZCNVSMID ZCNVSSRC ZCNVSLOD) PROFILE'
                    'VPUT (ZCNVSUP1 ZCNVDSRC ZCNVDLOD) PROFILE'
                    'VPUT (ZCNVSUP2 ZCNVSUP3 ZCNVSUP4) PROFILE'
                    End
                  Call verify_keyword_file
                  If dsrc = 0 & zcnvtype = 'LOAD' Then
                    Call verify_load_datasets
                  If dsrc = 0 & zcnvtype = 'SMPE' Then
                    Call verify_smp_dataset
                  If dsrc = 0 Then
                    Do
                      c = 'ZCMD'
                      Address TSO ispcverf
                      dsrc = rc
                      If dsrc = 0 Then
                        Do
                          Address TSO ispcbld zcnvtype debug
                          dsrc = rc
                          Select
                            When dsrc = 0 Then
                              m = 'ISPC265'
                            When dsrc = 1 Then
                              Do
                                m = 'ISPC253'
                                dsrc = 0
                              End
                            When dsrc = 2 Then
                              Do
                                m = 'ISPC294'
                                dsrc = 0
                              End
                            When dsrc < 8 Then  /* catches 4 & 5 */
                              Do
                                If zcnvmem = '' Then
                                  zcnvmod1 = 'ISPCFIGU'
                                Else
                                  zcnvmod1 = zcnvmem
                                If zcnvmem2 = '' Then
                                  zcnvmod2 = 'ISPCFIGV'
                                Else
                                  zcnvmod2 = zcnvmem2
                                If dsrc = 4 Then
                                  m = 'ISPC264'
                                Else
                                  m = 'ISPC260'
                                dsrc = 0
                              End
                            When dsrc = 8 Then
                              Do
                                If zcnvmem = '' Then
                                  zcnvmod = 'ISPCFIGU'
                                Else
                                  zcnvmod = zcnvmem
                                m = 'ISPC254'
                              End
                            When dsrc = 9 Then
                              Do
                                If zcnvmem2 = '' Then
                                  zcnvmod = 'ISPCFIGV'
                                Else
                                  zcnvmod = zcnvmem2
                                m = 'ISPC254'
                              End
                            When dsrc = 10 Then
                              Do
                                m = 'ISPC234'
                              End
                            Otherwise
                              Nop
                          End   /* end select ISPCBLD msg */
                        End  /* end call ISPCBLD */
                      Else
                        m = 'ISPC252'
                    End
                End
            End
          End
        When local_zcmd = 5 Then
          Do
            m = 'ISPC255'
            Call verify_old_config
            If dsrc = 0 Then
              Call verify_keyword_file
            If dsrc = 0 Then
              Do
                If notfound = 0 Then
                  Do
                    'ADDPOP'
                    'DISPLAY PANEL(ISPPCONQ)'
                    qrc = rc
                    'REMPOP'
                  End
                Else
                  qrc = 0
                If qrc = 0 Then
                  Do
                    Address TSO JSPCCONV /* ispcconv -> jspcconv, Ron */
                    If rc > 0 Then
                      m = 'ISPC256'
                    'VGET (ZCNVCSR) PROFILE'
                    c = zcnvcsr
                  End
                Else
                  m = ''
              End
          End
        Otherwise
          m = 'ISPD241'
      End
    End
End
Exit
/* */
verify_old_config:
/* Check source data set */
c = 'ZCNVSRCE'
If pos('(',zcnvsrce) > 0 Then
  Do
    'SETMSG MSG(ISPC282)'
    dsrc=4
  End
If dsrc = 0 Then
  Do
    'CONTROL REFLIST NOUPDATE'
    'LMINIT DATAID(AA) ENQ(SHR) DATASET('zcnvsrce')'
    dsrc=rc
    'CONTROL REFLIST UPDATE'
    If dsrc=0 Then
      Do
        'LMOPEN DATAID('aa') OPTION(INPUT) ORG(ORG)'
        dsrc=rc
        If dsrc = 0 Then
          If org = 'PO' Then
            If zcnvsrcm \= '' Then
              Do
                If dsrc=0 Then
                  'LMMFIND DATAID('aa') MEMBER('zcnvsrcm') STATS(NO)'
                dsrc=rc
                If dsrc = 8 Then
                  c = 'ZCNVSRCM'
              End
            Else
              Do
                c = 'ZCNVSRCM'
                'SETMSG MSG(ISRU007)'
                dsrc=4
              End
          Else
            If zcnvsrcm \= '' Then
              Do
                c = 'ZCNVSRCM'
                'SETMSG MSG(ISRG081)'
                dsrc=4
              End
        Else
          Nop
      End
  End
If dsrc>7 Then
  'SETMSG MSG(ISRZ002)'
'LMFREE DATAID('aa')'
Return
/* */
verify_keyword_file:
/* Check target data set */
notfound = 0
c = 'ZCNVKWD'
If pos('(',zcnvkwd) > 0 Then
  Do
    'SETMSG MSG(ISPC282)'
    dsrc=4
  End
If dsrc = 0 Then
  Do
    'CONTROL REFLIST NOUPDATE'
    'LMINIT DATAID(BB) ENQ(SHR) DATASET('zcnvkwd')'
    dsrc=rc
    'CONTROL REFLIST UPDATE'
    If dsrc=0 Then
      Do
        'LMOPEN DATAID('bb') OPTION(INPUT) ORG(ORG) LRECL(LRECL)'
        dsrc = rc
        If dsrc = 0 Then
          If org ¬= 'PO' | lrecl < 251 Then
            Do
              'SETMSG MSG(ISPC268)'
              dsrc=4
            End
          Else
            If zcnvkwdm = '' Then
              Do
                c = 'ZCNVKWDM'
                'SETMSG MSG(ISRU007)'
                dsrc=4
              End
            Else
              Do
                'LMMFIND DATAID('bb') MEMBER('zcnvkwdm') STATS(NO)'
                dsrc = rc
                If dsrc = 8 Then
                  If (local_zcmd = 1 | local_zcmd = 5) Then
                    Do
                      notfound = 1
                      dsrc = 0
                    End
                  Else
                    c = 'ZCNVKWDM'
              End
      End
  End
If dsrc>7 Then
  'SETMSG MSG(ISRZ002)'
'LMFREE DATAID('bb')'
'VPUT (NOTFOUND) SHARED'
Return
/* */
verify_smp_dataset:
/* Check target data set for USERMOD */
c = 'ZCNVSMPE'
If pos('(',zcnvsmpe) > 0 Then     /* included member name */
  Do
    'SETMSG MSG(ISPC297)'
    dsrc=4
  End
If dsrc = 0 Then
  Do
    'CONTROL REFLIST NOUPDATE'
    'LMINIT DATAID(DD) ENQ(SHR) DATASET('zcnvsmpe')'
    dsrc=rc
    'CONTROL REFLIST UPDATE'
    If dsrc=0 Then
      Do
        'LMOPEN DATAID('dd') OPTION(INPUT) ORG(ORG) LRECL(LRECL)',
        'RECFM(smpfm)'
        dsrc = rc
        If dsrc = 0 Then
          If org ¬= 'PO' | lrecl ¬= 80 | smpfm ¬= 'FB' Then
            Do
              'SETMSG MSG(ISPC298)'
              dsrc=4
            End
      End
  End
If dsrc > 7 Then
  'SETMSG MSG(ISRZ002)'
'LMFREE DATAID('dd')'
'VPUT (NOTFOUND) SHARED'
Return
/* */
verify_load_datasets:
/* Check load data set */
c = 'ZCNVLOAD'
ret=listdsi(zcnvload) /* Check for existance of data set*/
Parse Var zcnvload before'('member')'after
'CONTROL REFLIST NOUPDATE'
'LMINIT DATAID(CC) ENQ(SHR) DATASET('before||after')'
dsrc=rc
'CONTROL REFLIST UPDATE'
If dsrc=0 Then
  Do
    'LMOPEN DATAID('cc') OPTION(INPUT) ORG(ORG)'
    dsrc = rc
    If dsrc = 0 & member ¬= '' Then
      Do
         'SETMSG MSG(ISPC257)'
         dsrc=4
      End
  End
If dsrc>7 Then
  'SETMSG MSG(ISRZ002)'
'LMFREE DATAID('cc')'
If dsrc = 0 & zcnvobj ¬= '' Then
  Do
    /* Check object data set */
    c = 'ZCNVOBJ'
    ret=listdsi(zcnvobj) /* Check for existance of data set*/
    Parse Var zcnvobj before'('member')'after
    'CONTROL REFLIST NOUPDATE'
    'LMINIT DATAID(CC) ENQ(SHR) DATASET('before||after')'
    dsrc=rc
    'CONTROL REFLIST UPDATE'
    If dsrc=0 Then
      Do
        'LMOPEN DATAID('cc') OPTION(INPUT) ORG(ORG)'
        dsrc = rc
        If dsrc = 0 & member ¬= '' Then
          Do
             'SETMSG MSG(ISPC257)'
             dsrc=4
          End
      End
    If dsrc>7 Then
      'SETMSG MSG(ISRZ002)'
    'LMFREE DATAID('cc')'
  End
Return
        /*  5694-A01 (C) COPYRIGHT IBM CORP. 1999, 2004 */
