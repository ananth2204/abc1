//jobname  JOB 'ISPFCMDL',MSGCLASS=?,CLASS=?,NOTIFY=&SYSUID
//*
//ASM      EXEC PGM=ASMA90,
//         PARM='NODECK,LIST,OBJECT'
//SYSIN    DD  *
         TITLE 'GET ADDRESS OF PROGRAM LOAD POINT'
***********************************************************************
* ISPFCMDL                                                            *
* --------                                                            *
* External REXX function to return the address of a loaded module.    *
*                                                                     *
*   Written by : Ron Brown - January 2000                             *
***********************************************************************
LOADPNT  RMODE ANY
LOADPNT  AMODE 31
LOADPNT  CSECT
         USING LOADPNT,R15               SET UP ADDRESSABILITY
         SAVE  (14,12)
         LR    R12,R15
         DROP  R15
         USING LOADPNT,R12
         B     START                     GO AROUND EYECATCHER
*
PROGNAME DC    CL8'ISPFCMDL'             EYECATCHER
         DC    CL8' VERSION'
VERSION  DC    CL6' 01.00'
DATE_ASM DC    CL11' &SYSDATE'           DATE ASSEMBLED
TIME_ASM DC    CL8' &SYSTIME'            TIME ASSEMBLED
*
START    DS    0H                        REXX STUFF:
         USING EFPL,R1                        MAP EVAL PARM LIST
         L     R2,EFPLEVAL                    LOAD EVAL POINTER
         L     R11,0(R2)                      ADRESS OF EVAL BLOCK
         USING EVALBLOCK,R11                  MAP EVAL BLOCK
         L     R5,EFPLARG                     LOAD ARG LST PNTR
         USING ARGTABLE_ENTRY,R5              MAP THE ENTRY
*
*----------------------------------------------------------------------
*        GET PROGRAM NAME FROM PARM
*----------------------------------------------------------------------
         LM    R2,R3,ARGTABLE_ARGSTRING_PTR   LOAD ARG ADDR/LNGTH
         C     R3,=F'8'                       > 8?
         BH    BADPARM                        YES, GOTO BADPARM
*        MVC   PGMNAME,0(R2)                  (only valid if length=8)
         LA    R8,PGMNAME
         LR    R9,R3
         MVCL  R8,R2                          SET PGMNAME
*
         LA    R5,ARGTABLE_NEXT-ARGTABLE_ENTRY(R5) GET NEXT PARM
*                                                ADDR/LENGTH
         CLC   ARGTABLE_ARGSTRING_LENGTH,=8X'FF'   MORE PARMS?
         BE    CSVQUERY                        NO, INVOKE QUERY
*                                                ADDR/LENGTH
BADPARM  LA    R15,8                           RC=8
         B     EXIT
*
*----------------------------------------------------------------------
*        INVOKE CSVQUERY TO GET THE ADDRESS
*----------------------------------------------------------------------
CSVQUERY DS    0H
         CSVQUERY INEPNAME=PGMNAME,                                    X
               OUTEPA=PGMADDR,                                         X
               MF=S
*
*----------------------------------------------------------------------
*        RETURN THE PROGRAM ADDRESS TO REXX EXEC
*----------------------------------------------------------------------
         NI    PGMADDR,X'7F'                  FIRST BIT SHOULD BE ZERO
         MVC   EVALBLOCK_EVLEN,=F'4'          VALUE LENGTH
         MVC   EVALBLOCK_EVDATA(4),PGMADDR    VALUE DATA
*
         LA    R15,0                     RC=0
EXIT     DS    0H
         RETURN (14,12),RC=(15)          RETURN TO CALLER
*
***********************************************************************
CONSTNTS DS    0F
*
PGMNAME  DC CL8'        '                PROGRAM NAME
PGMADDR  DS 1F
*
         YREGS
         IRXEFPL
         IRXEVALB
         IRXARGTB
         END   LOADPNT
/*
//****************************************************************
//SYSLIN   DD  DSN=&&TEMPOBJ,DISP=(NEW,PASS),UNIT=VIO,
//             SPACE=(TRK,(2,2),RLSE),DCB=(RECFM=FB,LRECL=80)
//SYSUT1   DD  UNIT=SYSDA,SPACE=(CYL,(3,5))
//SYSLIB   DD  DSN=SYS1.MACLIB,DISP=SHR
//         DD  DSN=SYS1.MODGEN,DISP=SHR
//SYSPRINT DD  SYSOUT=*
//****************************************************************
//*   LINK EDIT
//****************************************************************
//LINK    EXEC PGM=IEWL,COND=(4,LT,ASM),REGION=6M,
// PARM='XREF,LIST,LET'
//SYSLMOD  DD  DSN=your.ISPF.Load.library,DISP=SHR
//SYSUT1   DD  UNIT=SYSDA,SPACE=(1024,(50,20))
//SYSUT2   DD  UNIT=SYSDA,SPACE=(1024,(50,20))
//SYSPRINT DD  SYSOUT=*
//SYSLIB   DD  DSN=SYS1.LINKLIB,DISP=SHR
//SYSLIN   DD  DSN=*.ASM.SYSLIN,DISP=(SHR,DELETE)
//         DD  *
 NAME ISPFCMDL(R)
/*
//
