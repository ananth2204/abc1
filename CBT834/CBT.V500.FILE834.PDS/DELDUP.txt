/* REXX                                                              */
/*
   This macro deletes equal strings.
   File must be sorted before this command execution.
   It is possible to enter first and last positions for compare.

   Author: Alexander I. Vasilenko,
           LUXOFT, Moscow.
           e-mail: ali_vas@mail.ru
                   Alexander.I.Vasilenko@boeing.com
*/

Address ISREDIT
'MACRO (PARMS) NOPROCESS'
if RC > 0 then do
  say ' It is EDIT macro. Do not use TSO prefix.'
  exit 0
end
upper parms
if word(parms,1) = '?' |,
   word(parms,1) = 'HELP'
  then do
  Show_help:
  eoh = 0
  do line = 3 until eoh
    if substr(sourceline(line + 1),1,2) = '*/' then
      eoh = 1
    say sourceline(line)
  end
end
/*                                */
Address ISPEXEC CONTROL ERRORS RETURN
CALL ON ERROR
trace o
/*                 */
Address ISREDIT
Fend    = 0
l       = 1
c       = 0
substring = 0
if parms Â¬= '' then do
  parse var parms fpos lpos .
  substring = 1
  lpos = word(parms,2)
  if verify(fpos,'1234567890') > 0 |,
     verify(lpos,'1234567890') > 0 then do
    say '  Positions must be numerical.'
    exit 4
  end
end
'(LNUM)    = LINENUM .ZLAST'
'(TEXTO) = LINE ' l
l = l + 1

do until Fend
  '(TEXT) = LINE ' l
  if l >= Lnum then
    Fend = 1
  if substring then do
    T1 = substr(Text,fpos,lpos-fpos+1)
    T2 = substr(TextO,fpos,lpos-fpos+1)
  end
  else do
    T1 = Text
    T2 = TextO
  end
  if T1 = T2 then do
    delete l
    c = c + 1
    Lnum = Lnum - 1
  end
  else do
    TextO = Text
    l = l + 1
  end
end
if c > 0 then do
  ZEDSMSG = "Deleted "c" lines."
  ZEDLMSG = "Some lines have been deleted from edited file."
  Address ISPEXEC "SETMSG MSG(ISRZ001)"
end
exit
/*                    */
ERROR:
if RC = 4 then do
  Range_RC = RC
  firstln  = 1
  '(LNUM)    = LINENUM .ZLAST'
  return
end
trace o
  say 'Error in string - 'SIGL', RC - 'RC
  say sourceline(SIGL)
  say 'Error text:'
  say ERRORTEXT(RC)
  say 'Curr line = ' l 'Total lines = ' Lnum
exit 4
