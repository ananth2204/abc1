/* REXX                                                              */
/*
       This macro was designed for facilitate new data sets allocation.
   You must have PS or PDS/PDSE data set at least 133 LRECL for data
   set attributes list. Perform 'NUM OFF' edit command!!!
   Insert DSNs in columns 12-55. After macro is run, allocation
   attributes appear in the right columns. This information will be
   used as input for new DS allocation.

          Call format:

          MIGRATE (REQ|ALLOC (DEL)|?) {FOR Region}

          REQ   - for request allocation attributes.
          ALLOC - for new DS allocation using retrieved attributes.
          DEL   - use this option to delete existing data sets. This
                  option has effect only with ALLOC option.
                  Macro will check if attributes of old DS are the same
                  as in data set list. If no one of attributes differ
                  from existing DS, then deletion will not be performed.
                  Otherwise existing Ds will be deleted and a new one
                  will be allocated.
          FOR   - it is reserved word. If you use it, you must issue
                  Region too.
          Region- ENDEVOR region. In common case it is member name
                  with variavles for substitution into DSN.
                  You will issue this name without '#' but real
                  member name is started with '#'.
                  See Dict parameter of interface to define library
                  where this member resides.
          ?     - this help text.

   First position of every string is used for status.
   * - means comment or DS incompatible.
   A - DS was allocated.
   D - DS was allocated after deletion.
   X - DS already exist.
   E - error during allocation.
   I - one or more attributes missing.
   V - variable in DSN does not exist in dictionary.
   N - DS does not exist or error in parameters.

   You also may use range commands according common ISPF rules:
     Z - for single string;
     ZZ or Zn - for range of strings.

   Author: Alexander I. Vasilenko,
           LUXOFT, Moscow.
           e-mail: ali_vas@mail.ru
                   Alexander.I.Vasilenko@boeing.com
*/
/* Change log.
   12/24 New functionality. Added 'FOR Region' capability.
   12/28 New format of DS list.
               - DS name
               - DISP from JOB/PROC
               - DS type from IDCAMS
               - allocation parameters list in the same order.
   03/03 Macro is learned to recall migrated DS.
   03/18/2011  - New status is added. N - DS does not exist.
   03/21/2011  - history capability is activated.
   03/15/2011  - print errors in log line.
   04/20/2011  - blank out param columns before fill attributes.
   08.09.2012  - LARGE and EXTENDED types are recognized
*/
Address ISREDIT
'MACRO (PARMS) NOPROCESS'
if RC > 0 then do
  say ' It is EDIT macro. Do not use TSO prefix.'
  exit 0
end
Address ISPEXEC CONTROL ERRORS RETURN
S = outtrap(tso_mes.,,noconcat)
upper parms
/*                                                                   */
if substr(parms,1,1) = '?' |,
   substr(parms,1,1) = ' ' then
  call Show_help

/*        Interface  and variavles */    /* 12/24 */
Dict = 'DATALIB.WIRS.SERVBLKP.DICT' /* dictionary */
/* Dict = 'DATALIB.WIRS.PRDP.SERVPROD.DICT' */
/*Dict = 'IS0485.TEST.REXX'           test only */
NewValue. = ''
Region  = ' '            /* 03/21/2011 */
Error.  = 0              /* 04/15/2011 */
OK      = 0              /* 04/20/2011 for OK process */
Ex      = 0              /* 04/20/2011 for Already exist */
ForMode = 0
StPos   = 3 /* 12  data set name start position */
WrongDSN = "It is not a real Data Set name. Use macro with option",
   "'FOR Region'."
WrongVar = "Variabel was not found in configuration file. Check your",
           "configuration."
/*      End of Interface  */

/*       Main line                                                   */
if pos(' FOR ',parms) > 0 then do                          /* 12/24 */
  parse var parms . 'FOR' Region .                       /* 12/24 */
  if Region = '' then do                                 /* 12/24 */
    say '  Empty Region.'                                /* 12/24 */
    call Show_Help /* ... and leave macro */             /* 12/24 */
  end                                                    /* 12/24 */
  Region = strip(Region)                                 /* 12/24 */
  ForMode = 1                                            /* 12/24 */
  DS = "'"Dict||"(#"||Region||")'"                       /* 12/24 */
  Address TSO "ALLOC FI(INPUT) DS("DS")"                 /* 12/24 */
  if RC > 0 then do                                      /* 12/24 */
    say "Data set can't be allocated:"                   /* 12/24 */
    say DS ', RC = ' rc                                  /* 12/24 */
    exit 12                                              /* 12/24 */
  end                                                    /* 12/24 */
  Address TSO "execio * diskr INPUT (stem Input. finis)" /* 12/24 */
  Address TSO "FREE  FI(INPUT)"                          /* 12/24 */
  do i = 1 to Input.0                                    /* 12/24 */
    if pos('SET',Input.i) > 0 then do                    /* 12/24 */
      parse var Input.i OldValue '=' NewValue            /* 12/24 */
      OldValue = word(OldValue,words(OldValue))          /* 12/24 */
      NewValue = word(NewValue,1)                        /* 12/24 */
      NewValue.OldValue = OldValue NewValue              /* 12/24 */
    end                                                  /* 12/24 */
  end                                                    /* 12/24 */
end                                                      /* 12/24 */
Address ISREDIT

'process range Z'
RC_Range = RC
'(LNUM)    = LINENUM .ZLRANGE'
'(FIRSTLN) = LINENUM .ZFRANGE'
/*'RES' */
/* Next part determines if logging is required and writes log */
'(LogM) = LINE ' 1   /* read first line */
History  = 0    /* do not write history */
LogAFound = 0   /* active log found */
EndOfLog = 0
do i = 1 by 1 until EndOfLog
  '(LogM) = LINE ' i
  if substr(LogM,2,4) = ' LOG' then do
    History  = 1    /* write history */
    if substr(LogM,6,1) = 'A' then do /* current active log */
      LogAFound = 1
      LogPos = i       /* position of active log */
      LogM = overlay(' LOG ',LogM,2,5)
      'LINE ' i ' = (LogM)' /* replace line */
    end
    else do
    end
    iterate
  end
  else do
    if (i = 1) then
      leave i /* Log not found in first line - no logging */
    EndOfLog = 1
    LastPos = i - 1 /* last log position */
  end
end

if History then do    /* write log */
  if LogAFound then do
    if LastPos = LogPos then do /* active log in last pos */
      LogPos = 1    /* move it in first line */
    end
    else
      LogPos = LogPos + 1 /* choose nex line for acive log */
  end
  else        /* active log not found */
    LogPos = 1
  FirstLn = FirstLn + 0 /* to remove 0 from num */
  Lnum    = Lnum + 0    /* to remove 0 from num */
  LogM = overlay('*',LogM,1,133) /* comment line and blank out */
  LogM = overlay('LOGA',LogM,3,4) /* LOG prefix */
  parse value DATE('U') with cDay '/' cMonth '/' .
  parse value DATE() with . . cYear
  cDate = cDay || '/' || cMonth || '/' || cYear
  cTime = substr(TIME(),1,5)
  LogM = overlay(cDate,LogM,8,10)
  LogM = overlay(cTime,LogM,19,8)
  UserID = UserID()
  LogM = overlay(UserID,LogM,28,8)
  LogM = overlay(FirstLn,LogM,37,8)
  LogM = overlay(Lnum,LogM,46,8)
  'LINE ' LogPos ' = (LogM)' /* replace line */
end
/*   END of logging     03/21/2011 */

select
  when substr(word(parms,1),1,3) = 'REQ' then do
    LogM = overlay('REQ  ',LogM,55,5)   /* 03/21/2011 */
    LogM = overlay(Region,LogM,65,4)    /* 03/21/2011 */
    call Fill_attr
    LogM = strip(LogM,'T')                          /* 04/15/2011 */
    LogM = LogM 'V='Error.v 'N='Error.N 'E='Error.e 'OK='OK 'EX='Ex
    'LINE ' LogPos ' = (LogM)'          /* 03/21/2011 */
  end
  when substr(word(parms,1),1,5) = 'ALLOC' then do
    LogM = overlay('ALLOC',LogM,55,5)   /* 03/21/2011 */
    LogM = overlay(Region,LogM,65,4)    /* 03/21/2011 */
    if substr(word(parms,2),1,3) = 'DEL' then do
      LogM = overlay('DEL',LogM,61,3)   /* 03/21/2011 */
      delete = 1
    end
    else
      delete = 0
    call Do_Alloc
    LogM = strip(LogM,'T')                          /* 04/15/2011 */
    LogM = LogM 'V='Error.v 'N='Error.N 'E='Error.e 'OK='OK 'EX='Ex
    'LINE ' LogPos ' = (LogM)'          /* 03/21/2011 */
  end
  otherwise
    call No_parm
end
/*                 */
exit 0
/*                    */
Fill_attr:
do l = Firstln to lnum
  '(TEXT) = LINE ' l
/*Texts = substr(Text,3) */
  if substr(Text,1,1) = '*' then iterate
  else do
    DSN = word((substr(text,StPos,49)),1)
    parse var DSN DSN '(' . /* isolate DSN if member typed */
    UpdDSN = 0 /* suppose DSN has not been changed */ /* 12/24 */
    VarFound = 0 /* if '&' was found */                     /* 12/24 */
    VarAlert = 0                                            /* 12/24 */
    Call CheckVariable                                      /* 12/24 */
    if UpdDSN then do /* substitution has benn performed */ /* 12/24 */
      'LINE_AFTER 'l' = INFOLINE (DSN)'                     /* 12/24 */
    end                                                     /* 12/24 */
    else do                                                 /* 12/24 */
      if VarAlert then                                      /* 12/24 */
        iterate                                             /* 12/24 */
      if VarFound & \ForMode then                           /* 12/24 */
        iterate                                             /* 12/24 */
    end                                                     /* 12/24 */

    x = LISTDSI("'"DSN"'" directory recall) /* 03.03.2011 */
    if SYSREASON = 0 then do
      if SYSDSORG = 'PS' |,
         SYSDSORG = 'PO' then do
        text = overlay(' ',text,StPos + 58,100)   /* 04.20.2011 */
        text = overlay(SYSDSORG,text,StPos + 58,5)   /* 12/28 */
        if SYSSEQDSNTYPE = 'LARGE' then do           /* 08.09.2012 */
          text = overlay('-L',text,StPos + 60,2)     /* 08.09.2012 */
        end                                          /* 08.09.2012 */
        if SYSSEQDSNTYPE = 'EXTENDED' then do        /* 08.09.2012 */
          text = overlay('-E',text,StPos + 60,2)     /* 08.09.2012 */
        end                                          /* 08.09.2012 */
        text = overlay(SYSRECFM,text,StPos + 63,8)   /* 12/28 */
        text = overlay(SYSLRECL,text,StPos + 71,7)   /* 12/28 */
        text = overlay(SYSBLKSIZE,text,StPos + 78,9) /* 12/28 */
        text = overlay(SYSUNITS,text,StPos + 87,10)  /* 12/28 */
        text = overlay(SYSPRIMARY,text,StPos + 97,7) /* 12/28 */
        text = overlay(SYSSECONDS,text,StPos + 104,7) /* 12/28 */
        if SYSADIRBLK = '' then
           SYSADIRBLK = 0
        text = overlay(SYSADIRBLK,text,StPos + 111,8) /* 12/28 */
        text = overlay(' ',text,1,1,' ')
        OK = OK + 1                   /* 04.20.2011 */
        'LINE' l ' = (TEXT)'
      end
      else do
        temp = 'Data set is not PS or PO organization.'
        text = overlay(' ',text,StPos + 58,85)
        text = overlay('N',text,1)         /* 03/18/2011 */
        Error.N = Error.N + 1          /* 04/15/2011 */
        text = overlay(temp,text,StPos + 58)
        'LINE' l ' = (TEXT)'
      end
    end
    else do
/*    temp = 'RC = ' SYSREASON*/
      temp = SYSMSGLVL2
      text = overlay(' ',text,StPos + 58,85)
      text = overlay('N',text,1)           /* 03/18/2011 */
      Error.N = Error.N + 1          /* 04/15/2011 */
      text = overlay(temp,text,StPos + 58)
      'LINE' l ' = (TEXT)'
    end
  end
end
return

Do_Alloc:
Type  =  ''                                        /* 08.09.2012 */
do l = Firstln to lnum
  '(TEXT) = LINE ' l
  Texts = substr(Text,StPos + 58) /* check number of parameters 12/28 */
  if substr(Text,1,1) = '*' |,              /* 03/18/2011 */
     substr(Text,1,1) = 'N' then iterate    /* 03/18/2011 */
  else do
    if words(Texts) < 8 then do /* list incomplete 12/28 */
      call List_incomplete
      iterate
    end
    DSN    = word((substr(text,StPos,49)),1)    /* 12/28 */
    parse var DSN DSN '(' . /* isolate DSN if member typed */
    UpdDSN = 0                                        /* 12/24 */
    VarFound = 0 /* if '&' was found */               /* 12/24 */
    VarAlert = 0                                      /* 12/24 */
    Call CheckVariable                                /* 12/24 */
    if UpdDSN then do                                 /* 12/24 */
      'LINE_AFTER 'l' = INFOLINE (DSN)'               /* 12/24 */
    end                                               /* 12/24 */
    else do                                           /* 12/24 */
      if VarAlert then                                /* 12/24 */
        iterate                                       /* 12/24 */
      if VarFound & \ForMode then                     /* 12/24 */
        iterate                                       /* 12/24 */
    end                                               /* 12/24 */
    SA = 'A'   /* suppose normal allocation */
    x = LISTDSI("'"DSN"'" directory recall)   /* 03.03.2011 */
    if SYSREASON = 0 then do
      if delete then do
        call CheckAttrs
        if Changed then do
          Address TSO "DELETE ('"DSN"')"
          SA = 'D' /* allocation after deletion */
        end
        else do
          Ex = Ex + 1
          text = overlay('X',text,1)
          'LINE' l ' = (TEXT)'
          iterate
        end
      end
      else do
        Ex = Ex + 1
        text = overlay('X',text,1)
        'LINE' l ' = (TEXT)'
        iterate
      end
    end
    Prim      = word(texts,6)   /* 12/28 */
    select
      when word(texts,5) = 'TRACK' then        /* 12/28 */
        AllocUnit = 'TRACKS'
      when word(texts,5) = 'CYLINDER' then     /* 12/28 */
        AllocUnit = 'CYLINDERS'
      when word(texts,5) = 'BLOCK' then        /* 12/28 */
        AllocUnit = 'BLOCK('prim')'
      otherwise
        say 'AllocUnit = ' word(texts,5) /*this is an error*//* 12/28 */
        exit 8
    end
    Sec       = word(texts,7)   /* 12/28 */
    Blksz     = word(texts,4)   /* 12/28 */
    Dsorg     = word(texts,1)   /* 12/28 */
    if substr(DSOrg,1,2) = 'PS' then do               /* 08.09.2012 */
      select                                          /* 08.09.2012 */
        when substr(DSOrg,3,2) = '-L' then            /* 08.09.2012 */
          Type = 'DSNTYPE(LARGE)'                     /* 08.09.2012 */
        when substr(DSOrg,3,2) = '-E' then            /* 08.09.2012 */
          Type = 'DSNTYPE(EXTREQ)'                    /* 08.09.2012 */
        otherwise                                     /* 08.09.2012 */
      end                                             /* 08.09.2012 */
      DSOrg = 'PS'                                    /* 08.09.2012 */
    end
    if word(texts,8) = 'NO_LIM' then /* 12/28 */
      Dir    = 0
    else
      Dir    = word(texts,8)    /* 12/28 */
    Lrecl     = word(texts,3)   /* 12/28 */
    Rf = word(texts,2)          /* 12/28 */
    Recfmt    = ''
    do i = 1 to LENGTH(Rf)
      if i > 1 then
        Recfmt  = Recfmt||' '
      Recfmt  = Recfmt||substr(Rf,i,1)
    end
    select
      when (datatype(word(texts,8)) = 'NUM') &,       /* 12/28 */
           (word(texts,8)) > 0            then        /* 12/28 */
        Type  = 'DSNTYPE(PDS)'
      when word(texts,8) = 'NO_LIM' then
        Type  = 'DSNTYPE(LIBRARY)'
      otherwise
    end

    Address TSO
    "Allocate FILE (MALLOC) DSName('"DSN"') NEW "AllocUnit,
    "SPACE("Prim Sec") BLKSIZE("Blksz") DIR("Dir") LRECL("Lrecl")",
    "RECFM("Recfmt") DSORG("Dsorg") "Type
    Address ISREDIT
    if RC = 0 then do
      OK = OK + 1
      text = overlay(SA,text,1)
      'LINE' l ' = (TEXT)'
    end
    else do
      text = overlay('E',text,1)
      Error.E = Error.E + 1       /* 04/15/2011 */
      'LINE' l ' = (TEXT)'
      do si = tso_mes.0 to 1 by -1
         Temp = tso_mes.si
        'LINE_AFTER 'l' = INFOLINE (Temp)'
      end
    end
    Address TSO
    "FREE FILE (MALLOC)"
    Address ISREDIT
  end
end
return

CheckAttrs:
if word(Texts,1) \= SYSDSORG then do        /* 12/28 */
  Changed = 1
  return
end
if word(Texts,2) \= SYSRECFM then do        /* 12/28 */
  Changed = 1
  return
end
if word(Texts,3) \= SYSLRECL then do        /* 12/28 */
  Changed = 1
  return
end
if word(Texts,4) \= SYSBLKSIZE then do      /* 12/28 */
  Changed = 1
  return
end
if word(Texts,5) \= SYSUNITS then do        /* 12/28 */
  Changed = 1
  return
end
if word(Texts,6) \= SYSPRIMARY then do      /* 12/28 */
  Changed = 1
  return
end
if word(Texts,7) \= SYSSECONDS then do      /* 12/28 */
  Changed = 1
  return
end
if word(Texts,8) \= SYSADIRBLK then do      /* 12/28 */
  Changed = 1
  return
end
Changed = 0
return

List_incomplete:
text = overlay('I',text,1)
'LINE' l ' = (TEXT)'
return

CheckVariable:                                        /* 12/24 */
if pos('&',DSN) > 0 then do
  VarFound = 1
  if ForMode then do
    parse var DSN FirstPart '&' OldValue '.' LastPart
    if word(NewValue.OldValue,1) \= '' then do
      DSN = FirstPart||word(NewValue.OldValue,2)||LastPart
      UpdDSN = 1
      Call CheckVariable
    end
    else do /* variable has not been found in config file */
      UpdDSN = 0
      VarAlert = 1
      text = overlay('V',text,1,2)
      Error.V = Error.V + 1      /* 04/15/2011 */
      'LINE 'l' = (TEXT)'
      'LINE_AFTER 'l' = INFOLINE (WrongVar)'
      'LINE_AFTER 'l' = INFOLINE (DSN)'
    end
  end
  else do
    text = overlay('N',text,1,2)          /* 03/18/2011 */
    Error.N = Error.N + 1       /* 04/15/2011 */
    'LINE 'l' = (TEXT)'
    'LINE_AFTER 'l' = INFOLINE (WrongDSN)'
  end
end
return

No_parm:
say '  Incorrect parameter was specified - 'word(parms,1)'.'
say '  Enter REQ for request of attributes.'
say '  Enter ALLOC for allocation process.'
return

Show_help:
eoh = 0
do line = 3 until eoh
  if substr(sourceline(line + 1),1,2) = '*/' then
    eoh = 1
  say sourceline(line)
end
/*'RES' */
exit 0
