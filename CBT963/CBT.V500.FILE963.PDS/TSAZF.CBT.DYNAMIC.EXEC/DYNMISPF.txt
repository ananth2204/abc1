/*                                   REXX
  This logon REXX is Executed by a TSO LOGON PROC win NO Dataset
       Allocations, so if there are any allocation issues will
       it will leave you at the TSO READY PROMPT.

       1. Verify USER's ISPF.PROFILE & Z.CLIST DSNs exist. If not,
          clean up catalog (possible DR issue), then allocate them.

       2. Set Shop Standard ISPF/TSO Datasets for later Allocation
       3. Add Image Specific ISPF/TSO Datasets for later Allocation
       4. Add USERID Based ISPF/TSO Datasets for later Allocation

       5. Do actual allocations based on DD Values built above

       6. Move TSO Variable Storage Above the Line.
       7. IF a personal ISPF.PROFILE(STARTUP) REXX, QUEUE for Execution,
          ELSE determine how to start ISPF and QUEUE it.
       8. Exit and allow QUEUED Commands to execute in Order
  ------------------------ Change History ------------------------------
  2017-07-10 - TSAZFA - v2.01 - Add Personel USER DSNs to @STARTUP
  2017-01-11 - TSAZFA - v1.60 - Minor refactoring
  2016-12-20 - TSAZFA - v1.57 - Update ID Prefix Logic for CO & DC
  2016-12-20 - TSAZFA - v1.54 - Add SYS1.HELPENP to SYSHELP DD.
  2016-02-11 - TSAZFA - v1.51 - Remove OPSHLQ, no longer needed
  2016-01-21 - TSAZFA - v1.47 - Tweaks for INS1 & JOB Scan upgrade
  2015-12-15 - TSAZFA - v1.44 - Move all allocations into REXX and out
                        of TSO LOGON PROC. Handle different allocation
                        based on 1st 2 charactors of USERID.
  2015-10-30 - TSAZFA - v1.30 - Minor Re-Factoring
  2015-10-30 - TSAZFA - v1.28 - Add IDIOFF as DUMMY
  2015-10-30 - TSAZFA - v1.25 - Add ISPLOG SYSOUT(0), trash after LOGOFF
  2015-10-13 - TSAZFA - v1.22 - Minor Cleanup and comments
  2015-10-12 - TSAZFA - v1.17 - Tweaks to make more generic
  2015-10-12 - TSAZFA - v1.15 - Fix issues remove dead code.
  2015-10-12 - TSAZFA - v1.00 - First SCDC Version
*/
ARG args
CALL ADD_SHOP_ALLOCATIONS
CALL ADD_IMAGE_ALLOCATIONS
CALL ADD_USER_GROUP_ALLOCATIONS

CALL ALLOC_PERSONAL_DSNS
CALL ADD_USER_ALLOCATIONS

CALL DO_STANDARD_ALLOCATIONS
              /* Move TSO Variable Storage Above the Line            */
ADDRESS "TSO" "PROFILE VARSTORAGE(HIGH)"
SELECT
  WHEN SYSDSN("ISPF.PROFILE(STARTUP)") = "OK" THEN /* Custom Startup */
       QUEUE "EXEC '"SYSVAR("SYSUID")".ISPF.PROFILE(STARTUP)'"
  WHEN args = ""                              THEN /* Basic Startup  */
       QUEUE "ISPSTART PANEL(ISR@PRIM) NEWAPPL(ISR) SHRPROF"
OTHERWISE; QUEUE args                        ;END  /* Passed Startup */

EXIT 0
/*--------------------------------------------------------------------
     Add all standard Shop Datasets to initial concatinations
  --------------------------------------------------------------------*/
ADD_SHOP_ALLOCATIONS:
  ISPLLIB = "'SYS3.ISPF.ISPLLIB' 'SSWCSYS.ISPF.STEPLIB'"
  ISPMLIB = "'SYS3.ISPF.ISPMLIB' 'ISP.SISPMENU' 'SSWCSYS.ISPF.ISPMLIB'"
  ISPPLIB = "'SYS3.ISPF.ISPPLIB' 'ISP.SISPPENU' 'SSWCSYS.ISPF.ISPPLIB'"
  ISPSLIB = "'SYS3.ISPF.ISPSLIB' 'ISP.SISPSENU' 'ISP.SISPSLIB'"
  ISPTLIB = "'SYS3.ISPF.ISPTLIB' 'ISP.SISPTENU' 'SSWCSYS.ISPF.ISPTLIB'"

  SYSEXEC = "'SYS3.ISPF.EXEC' 'ISP.SISPEXEC'"
  SYSHELP = "'SYS1.HELP' 'SYS1.HELPENP' 'ISP.SISPHELP' 'SYS3.ISPF.HELP'"
  SYSPROC = "'SYS3.ISPF.CLIST' 'ISP.SISPCLIB' 'SSWCSYS.ISPF.SYSPROC' Z.CLIST"
RETURN
/*--------------------------------------------------------------------
     Add all Image Specific Datasets to concatinations
  --------------------------------------------------------------------*/
ADD_IMAGE_ALLOCATIONS:
  EnvClone = MVSVAR("SYM","ENVCLONE")
  SYSNAME  = MVSVAR("SYSNAME")
  SELECT
    WHEN ABBREV(SYSNAME,"DEV")  THEN DO /* Development Images ...     */
      SYSEXEC = SYSEXEC "'SSWCSYS.ISPF.SYSEXEC'"
      SYSPROC = "'"EnvClone"SYS.JOBSCAN.CLIST'" SYSPROC
     END
    WHEN ABBREV(SYSNAME,"PRDA") THEN DO /* AWS Prod Test Image ...    */
      SYSEXEC = SYSEXEC "'SSWCSYS.ISPF.SYSEXEC'"
      SYSPROC = "'"EnvClone"SYS.JOBSCAN.CLIST'" SYSPROC
     END
    WHEN ABBREV(SYSNAME,"PRD")  THEN DO /* Production Images ...      */
      SYSEXEC = SYSEXEC "'SSWCSYS.ISPF.SYSEXEC'"
      SYSPROC = "'"EnvClone"SYS.JOBSCAN.CLIST'" SYSPROC
     END
    WHEN ABBREV(SYSNAME,"INS")  THEN DO /* Installation Images ...    */
      SYSEXEC = SYSEXEC "'"EnvClone"SWCSYS.ISPF.SYSEXEC'"
      SYSPROC = "'SSWCSYS.JS781.CLIST'" SYSPROC
     END
  OTHERWISE; NOP; END
RETURN
/*--------------------------------------------------------------------
     Add all USER GROUP Based Datasets to concatinations
  --------------------------------------------------------------------*/
ADD_USER_GROUP_ALLOCATIONS:
  fst2 = LEFT(SYSVAR("SYSUID"),2)
  SELECT
    WHEN WORDPOS(fst2,"AR")       > 0 THEN DO
     /* ISPPLIB = "'SSWCSYS.ISPF.ISPPLIB.AR'" ISPPLIB   */
      END
    WHEN WORDPOS(fst2,"CO DC TS") > 0 THEN DO
        ISPLLIB = "'SYS3.TECH.ISPLLIB'" ISPLLIB
        ISPMLIB = "'SYS3.TECH.ISPMLIB'" ISPMLIB
        ISPPLIB = "'SYS3.TECH.ISPPLIB'" ISPPLIB
        ISPSLIB = "'SYS3.TECH.ISPSLIB'" ISPSLIB
        ISPTLIB = "'SYS3.TECH.ISPTLIB'" ISPTLIB
        SYSEXEC = "'SYS3.TECH.EXEC'"    SYSEXEC "'SYS2.OPSMVS.CCLXEXEC'"
        SYSHELP = "'SYS3.TECH.HELP'"    SYSHELP "'SYS2.OPSMVS.CCLXHENU'"
        SYSPROC = "'SYS3.TECH.CLIST'"   SYSPROC
      END
    WHEN WORDPOS(fst2,"CT")       > 0 THEN DO
     /* ISPPLIB = "'SSWCSYS.ISPF.ISPPLIB.CT'" ISPPLIB   */
      END
    WHEN WORDPOS(fst2,"LS")       > 0 THEN DO
        ISPPLIB = "'SSWCSYS.ISPF.ISPPLIB.LS'" ISPPLIB   /* Cleanup ISR@PRIM */
      END
    WHEN WORDPOS(fst2,"MS")       > 0 THEN DO
     /* ISPPLIB = "'SSWCSYS.ISPF.ISPPLIB.MS'" ISPPLIB   */
      END
    WHEN WORDPOS(fst2,"OM")       > 0 THEN DO
     /* ISPPLIB = "'SSWCSYS.ISPF.ISPPLIB.OM'" ISPPLIB   */
      END
    WHEN WORDPOS(fst2,"SP")       > 0 THEN DO
     /* ISPPLIB = "'SSWCSYS.ISPF.ISPPLIB.SP'" ISPPLIB   */
      END
    WHEN WORDPOS(fst2,"VP")       > 0 THEN DO
     /* ISPPLIB = "'SSWCSYS.ISPF.ISPPLIB.VP'" ISPPLIB   */
      END
  OTHERWISE; NOP; END
RETURN
/*--------------------------------------------------------------------
     Ensure Personal ISPF.PROFILE and Z.CNTL Datasets Exist
  --------------------------------------------------------------------*/
ALLOC_PERSONAL_DSNS:
  ISPPROF = "ISPF.PROFILE"
  ALLOCOP = "CATALOG TRACKS SPACE(15,30) DSNTYPE(LIBRARY,2)" ,
            "DSORG(PO) RECFM(F,B) LRECL(80) BLKSIZE(0)"
  IF SYSDSN(ISPPROF) ¬= "OK" THEN DO
     SAY "=*=*=> Allocating your personal ISPF.PROFILE dataset!"
     ADDRESS "TSO" "DELETE" ISPPROF "NVSAM NSCR"  /* If bad CAT entry */
     ADDRESS "TSO" "ALLOC DD(ISPPROF) DSN("ISPPROF") NEW REUSE" ALLOCOP
     ADDRESS "TSO" "FREE DD(ISPPROF)"
  END
  MYCLIST = "Z.CLIST"
  IF SYSDSN(MYCLIST) ¬= "OK" THEN DO
     SAY "=*=*=> Allocating your personal Z.CLIST dataset!"
     ADDRESS "TSO" "DELETE" MYCLIST "NVSAM NOSCR" /* If bad CAT entry */
     ADDRESS "TSO" "ALLOC DD(MYCLIST) DSN("MYCLIST") NEW REUSE" ALLOCOP
     ADDRESS "TSO" "FREE DD(MYCLIST)"
  END
RETURN
/*---------------------------------------------------------------------*
          Update concatinations for User's Personal ISPF DSNs
 *---------------------------------------------------------------------*/
ADD_USER_ALLOCATIONS:         /* Setup existing personal ISPF DSNs     */
  IF SYSDSN("ISPF.ISPLLIB") = "OK" THEN IF,
     ISPLLIB = "" THEN ISPLLIB = "ISPF.ISPLLIB"
                  ELSE ISPLLIB = "ISPF.ISPLLIB" ISPLLIB
  IF SYSDSN("ISPF.ISPMLIB") = "OK" THEN IF,
     ISPMLIB = "" THEN ISPMLIB = "ISPF.ISPMLIB"
                  ELSE ISPMLIB = "ISPF.ISPMLIB" ISPMLIB
  IF SYSDSN("ISPF.ISPPLIB") = "OK" THEN IF,
     ISPPLIB = "" THEN ISPPLIB = "ISPF.ISPPLIB"
                  ELSE ISPPLIB = "ISPF.ISPPLIB" ISPPLIB
  IF SYSDSN("ISPF.ISPSLIB") = "OK" THEN IF,
     ISPSLIB = "" THEN ISPSLIB = "ISPF.ISPSLIB"
                  ELSE ISPSLIB = "ISPF.ISPSLIB" ISPSLIB
  IF SYSDSN("ISPF.ISPTLIB") = "OK" THEN IF,
     ISPTLIB = "" THEN ISPTLIB = "ISPF.ISPTLIB"
                  ELSE ISPTLIB = "ISPF.ISPTLIB" ISPTLIB
                              /* Setup existing personal TSO DSNs      */
  IF SYSDSN("Z.EXEC") = "OK"       THEN IF,
     SYSEXEC = "" THEN SYSEXEC = "Z.EXEC"
                  ELSE SYSEXEC = "Z.EXEC" SYSEXEC
  IF SYSDSN("Z.HELP") = "OK"       THEN IF,
     SYSHELP = "" THEN SYSHELP = "Z.HELP"
                  ELSE SYSHELP = "Z.HELP" SYSHELP
  IF SYSDSN("Z.CLIST") = "OK"      THEN IF, /* Move personal CList 1st */
     SYSPROC = "" THEN SYSPROC = "Z.CLIST"
                  ELSE SYSPROC = "Z.CLIST" SYSPROC
  IF SYSDSN("Z.LOADLIB") = "OK"    THEN IF,
     TSOLIB = ""  THEN TSOLIB = "Z.LOADLIB"
                  ELSE TSOLIB = "Z.LOADLIB" TSOLIB
  IF SYSDSN("Z.TSOLIB") = "OK"     THEN IF,
     TSOLIB = ""  THEN TSOLIB = "Z.TSOLIB"
                  ELSE TSOLIB = "Z.TSOLIB" TSOLIB
RETURN
/*--------------------------------------------------------------------
     Allocate the necessary ISPF PROFILE and TABLE DDs
  --------------------------------------------------------------------*/
DO_STANDARD_ALLOCATIONS:
                /* Allocate Standard Shared ISPF Datasets             */
  ADDRESS "TSO" "ALLOC DD(ISPLLIB) SHR REUSE DSN("ISPLLIB")"
  ADDRESS "TSO" "ALLOC DD(ISPMLIB) SHR REUSE DSN("ISPMLIB")"
  ADDRESS "TSO" "ALLOC DD(ISPPLIB) SHR REUSE DSN("ISPPLIB")"
  ADDRESS "TSO" "ALLOC DD(ISPSLIB) SHR REUSE DSN("ISPSLIB")"
  ADDRESS "TSO" "ALLOC DD(ISPTLIB) SHR REUSE DSN("ISPPROF ISPTLIB")"
                /* Allocate Standard Personal ISPF Datasets           */
  ADDRESS "TSO" "ALLOC DD(ISPPROF) SHR REUSE DSN("ISPPROF")"
  ADDRESS "TSO" "ALLOC DD(ISPTABL) SHR REUSE DSN("ISPPROF")"
                /* Allocate Standard TSO Datasets                     */
  ADDRESS "TSO" "ALLOC DD(SYSEXEC) SHR REUSE DSN("SYSEXEC")"
  ADDRESS "TSO" "ALLOC DD(SYSHELP) SHR REUSE DSN("SYSHELP")"
  ADDRESS "TSO" "ALLOC DD(SYSPROC) SHR REUSE DSN("SYSPROC")"
                /* Allocate any additional Datasets or DDs            */
  ADDRESS "TSO" "ALLOC DD(IDIOFF) DUMMY"       /* No Fault Analalyzer */
  ADDRESS "TSO" "ALLOC DD(ISPLOG) SYSOUT(0) RECFM(V,B,A) LRECL(125) REUSE"
RETURN
