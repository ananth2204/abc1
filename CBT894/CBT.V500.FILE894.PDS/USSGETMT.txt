/****************************** REXX *********************************/
/* Purpose: Print the Mount Table with selected details              */
/*-------------------------------------------------------------------*/
/* Syntax:  USSGETMT                                                 */
/*-------------------------------------------------------------------*/
/* Parms: N/A        - N/A                                           */
/*                                                                   */
/*********************************************************************/
/*                      Change Log                                   */
/*                                                                   */
/*  Author       Date      Reason                                    */
/*  --------    ---------  ----------------------------------------- */
/*  R. Zenuk    Dec 2001   Initial Creation                          */
/*                                                                   */
/*********************************************************************/
/* Dub the address space                                             */
/*********************************************************************/
 EXITRC = 0
 if syscalls('ON') <> 0 then
    do
     EXITRC = RC
     msg = 'Error dubbing RC='EXITRC
     say msg
    end
/*********************************************************************/
/* GETMNTENT                                                         */
/*********************************************************************/
 EXITRC = ussapi('getmntent' 'mnt.')
/*********************************************************************/
/* Loop through all mounts                                           */
/*********************************************************************/
 do i=1 to mnt.0
/*********************************************************************/
/* Make MODE readable                                                */
/*********************************************************************/
    mntmode = mnt.MNTE_MODE.i
    select
      when mntmode = MNT_MODE_RDWR       then mode = 'RDWR'
      when mntmode = MNT_MODE_RDONLY     then mode = 'RDONLY'
      when mntmode = MNT_MODE_AUNMOUNT   then mode = 'AUNMOUNT'
      when mntmode = MNT_MODE_CLIENT     then mode = 'CLIENT'
      when mntmode = MNT_MODE_EXPORT     then mode = 'EXPORT'
      when mntmode = MNT_MODE_NOAUTOMOVE then mode = 'NOAUTOMOVE'
      when mntmode = MNT_MODE_NOSEC      then mode = 'NOSEC'
      when mntmode = MNT_MODE_NOSETID    then mode = 'NOSETID'
      otherwise mode = 'Unknown='mntmode
    end
/*********************************************************************/
/* Print                                                             */
/*********************************************************************/
    say strip(mnt.MNTE_SYSNAME.i),
        strip(mnt.MNTE_FSTYPE.i),
        left(mode,10),
        strip(mnt.MNTE_FSNAME.i),
        strip(mnt.MNTE_PATH.i),
        strip(mnt.MNTE_PARM.i)
 end
 exit(EXITRC)
/*********************************************************************/
/* USS API subroutine                                                */
/*********************************************************************/
 ussapi: parse arg usscmd
         say center(' Before' usscmd' ',76,'-')
         address SYSCALL usscmd
         say 'RETVAL='RETVAL 'ERRNO='ERRNO 'ERRNOJR='ERRNOJR
         if ERRNO <> 0 | ERRNOJR <> 0 then
            do
             address SYSCALL 'strerror' ERRNO ERRNOJR 'err.'
             say
             say err.SE_ERRNO
             say
             parse var err.SE_REASON . '15'x errmsg
             say errmsg
             say
             say err.SE_ACTION
            end
         say center(' After' usscmd' ',76,'-')
         return RETVAL
