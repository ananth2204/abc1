Subject:  Re: IDCAMS
From:     Robert Zenuk <Robzenuk@aol.com>
Date:     3/30/2006 1:42 PM

Yup.  The JCL PATH statement is the "magic".  You could do this
with any program like IEFBR14, IEBGENER, IKJEFT01, IDCAMS,
BPXBATCH, etc.  Any program that uses QSAM to write to a file can
use a DD with a PATH statement.  Just incorporate your PATH DD
into whatever program will be used to
load/read/update/delete/whatever the file.

We use a lot of NFS and wanted to know in the JCL if the file was
successfully created before being written to.  This was a technique
incorporated to minimize the impact and complexity of job restarts
if a permissions problem (or other unexpected problem) was
encountered.  To accomplish this we used a small REXX utility that
simply checked to see if the file exists after PATH DD allocation
and returns a non-zero return code (RC=1) since IEFBR14 does not do
this.  This step is usually strategically placed at the beginning
of the jobstream.

Here is the REXX EXEC:

/****************************** REXX *********************************/
/*  Purpose: Use BPXWUNIX to check if a file exists                  */
/*-------------------------------------------------------------------*/
/*  Syntax:  BPXEXIST  file                                          */
/*-------------------------------------------------------------------*/
/*  Parms: file       - Full path to the file to look for            */
/*                                                                   */
/*  Notes: RC=0         File  exists                                 */
/*         RC=1         File does not  exist                         */
/*                                                                   */
/*********************************************************************/
/*                       Change  Log                                 */
/*                                                                   */
/*  Author        Date       Reason                                  */
/*  --------    ---------   ---------------------------------------- */
/*  R.  Zenuk    Mar 2004   Initial  Creation                        */
/*                                                                   */
/*********************************************************************/
parse  arg  file
command = 'ls'  file
say mvsvar('SYSNAME') mvsvar('SYMDEF','JOBNAME') date()  time()
say
say 'Executing command:'  command
say
exit  BPXWUNIX(command,'DD:stdin','DD:stdout','DD:stderr','0')

Here is some sample JCL to run it:

//jobcard
//*******************************************************************
//*  EXECUTE BPXEXIST TO SEE IF A FILE EXISTS                       *
//* RC=0, FILE  EXISTS                                              *
//* RC=1, FILE DOES NOT  EXIST                                      *
//*******************************************************************
//EXISTS   PROC  FILE=
//BPXEXIST EXEC PGM=IKJEFT01,PARM='BPXEXIST  &FILE'
//SYSEXEC  DD    DSN=your.exec.pds,DISP=SHR
//SYSTSPRT DD    SYSOUT=*
//SYSTSIN  DD    DUMMY
//STDOUT   DD    SYSOUT=*
//STDERR   DD    SYSOUT=*
//STDIN    DD    DUMMY
//FILE     DD    PATH='&FILE',
//          PATHOPTS=(OWRONLY,OCREAT),PATHDISP=(KEEP,DELETE),
//          PATHMODE=(SIRWXU,SIRWXG,SIRWXO),FILEDATA=TEXT
//         PEND
//CHECK    EXEC  EXISTS,FILE='/u/your/yournewfile.txt'


Hope This Helps,
Rob

