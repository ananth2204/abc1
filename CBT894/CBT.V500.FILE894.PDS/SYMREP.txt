Subject:  Re: Using system symbol to form a filename to be FTP-ed
From:     Aitor San Juan <asanjuan@bolsabilbao.es>
Date:     6/22/2007 3:05 AM

Oh, thanks a lot, Robert. That's been very kind of you.
Thanks also to Roland.

I think my colleagues will use my JCL inside another bigger JCL
skeleton which will be passed the necessary parameters through a
CList, as they don't use REXX in this installation.  However,
I'll bear in mind Robert's approach using REXX to simulate the
system's symbolic substitution.

I thought that getting today's date in a simple JCL was more
straightforward.  Anyways, I'm very thankful to both of you.

Regards,
Aitor.

-----Original message-----
De: MVS OpenEdition [mailto:MVS-OE@VM.MARIST.EDU]En nombre de Robert
Zenuk
Enviado el: jueves, 21 de junio de 2007 18:00
Para: MVS-OE@VM.MARIST.EDU
Asunto: Re: Using system symbol to form a filename to be FTP-ed

There are a couple issues here.  First, you can not use symbolics
in any "DD *" input.  This is not valid JCL substitution syntax
but the JES  Converter/Interpreter does not produce a JCL error.
The "DD *" input is just passed "as is" to the program (by
design).  Symbolics can only be used on lines (after the
jbocard) that begin with "//".  Second, normal batch jobs can not
use the  system symbols (except &SYSUID), only Started Tasks can
use them.

It was mentioned to use REXX.  Here is a quick and dirty way to
accomplish  what you want, by using a SYSIN massager and passing
the results as a temporary dataset.  The SYMREP REXX EXEC will
look for all MVSVAR's in an input file  and replace them with
their actual values.  The symbol syntax must use the trailing
"." to work.

Sample JCL:

//***************************************************************
//*  SYMREP                                                     *
//***************************************************************
//SYMREP    EXEC  PGM=IKJEFT01,PARM=SYMREP
//SYSEXEC  DD    DSN=OPSROZ.EXEC,DISP=SHR
//SYSTSPRT DD    SYSOUT=*
//SYSTSIN  DD    DUMMY
//INPUT    DD    *
10.1.2.3
USERID  PASSWD
CD  /USERID/LOG
PUT '&SYSNAME..S0001.RECORD.CSV'  &LYYMMDD._SESSION.CSV
QUIT
//OUTPUT   DD    DSN=&INPUT,DISP=(,PASS),UNIT=VIO,SPACE=(TRK,1)
//***************************************************************
//*  PRINT                                                      *
//***************************************************************
//PRINT     EXEC  PGM=IEBGENER
//SYSPRINT DD    SYSOUT=Z
//SYSUT1   DD    DSN=&INPUT,DISP=(OLD,DELETE)
//SYSUT2   DD    SYSOUT=*
//SYSIN    DD    DUMMY

SYMREP REXX EXEC (if it loses the indentation and you want good
copy, let me  know OFFLIST):

/****************************** REXX *********************************/
/*  Purpose: Read input and replace all the  MVSVAR's                */
/*-------------------------------------------------------------------*/
/*  Syntax:   SYMREP                                                 */
/*-------------------------------------------------------------------*/
/*  Parms: N/A        -  N/A                                         */
/*                                                                   */
/* Notes: All System Symbols MUST use strict syntax  (&xxx.          */
/*        JCL MUST include INPUT and  OUTPUT  DD's                   */
/*                                                                   */
/* Sample  JCL:                                                      */
/*                                                                   */
/* //SYMREP   EXEC  PGM=IKJEFT01,PARM=SYMREP                         */
/* //SYSEXEC  DD    DSN=OPSROZ.EXEC,DISP=SHR                         */
/* //SYSTSPRT DD    SYSOUT=*                                         */
/* //SYSTSIN  DD    DUMMY                                            */
/* //INPUT    DD    *                                                */
/*  10.1.2.3                                                         */
/* USERID  PASSWD                                                    */
/* CD  /USERID/LOG                                                   */
/* PUT '&SYSNAME..S0001.RECORD.CSV'  &LYYMMDD._SESSION.CSV           */
/*  QUIT                                                             */
/* //OUTPUT   DD    DSN=&INPUT,DISP=(,PASS),UNIT=VIO,SPACE=(TRK,1)   */
/*                                                                   */
/*********************************************************************/
/*                       Change  Log                                 */
/*                                                                   */
/*  Author        Date       Reason                                  */
/*  --------    ---------   ---------------------------------------- */
/*  R.  Zenuk    Jun 2007   Initial  Creation                        */
/*                                                                   */
/*********************************************************************/
/*  Read  input                                                      */
/*********************************************************************/
"EXECIO  * DISKR INPUT (STEM INPUT.  FINIS"
/*********************************************************************/
/*  Pass each line through the  substitution                         */
/*********************************************************************/
do  i=1 to input.0
output.i =  symsub(input.i)
end
/*********************************************************************/
/*  Write the new  output                                            */
/*********************************************************************/
"EXECIO  * DISKW OUTPUT (STEM OUTPUT. FINIS"
exit  0
/*********************************************************************/
/*  Replace a system symbol in a string using  MVSVAR                */
/*********************************************************************/
symsub:  procedure
parse arg  string
start =  1
do  forever
amper =  pos('&',string,start)
if amper <> 0  then
do
period =  pos('.',string,amper)
if period <> 0  then
do
sym =  substr(string,amper,period-amper+1)
sb  =  amper+1
se  =  period-amper-1
val =  mvsvar('SYMDEF',substr(string,sb,se))
string =  chgstr(string,val,sym)
end
else
return  string
end
else
return string
end
/*********************************************************************/
/*  Replace a string with another  string                            */
/*********************************************************************/
chgstr:  procedure
if arg() < 3  then
return 'chgstr: missing args, must have string, new and  old'
parse arg string, new,  old, style
lnew =  length(new)
lold =  length(old)
x =  1
do  forever
if  pos(old,string,x) = 0 then return  string
x =  pos(old,string,x)
string =  insert(new,delstr(string,x,lold),x-1,lnew)
x = x + length(new)
end


Hope This Helps,

Rob
