Subject:  Re: Search a dataset for a string from JCL using *grep*
From:     Robert Zenuk <Robzenuk@aol.com>
Date:     12/11/2006 11:08 AM

How large is the input dataset?  A quick and dirty REXX EXEC could
accomplish this easily.

/****************************** REXX *********************************/
/*  Purpose: Find a string and put in the OUTPUT dataset             */
/*-------------------------------------------------------------------*/
/*  Syntax:  FINDSTR string                                          */
/*-------------------------------------------------------------------*/
/*  Parms: STRING     - Any string                                   */
/*                                                                   */
/*  Notes: RC      00 - Everything worked                            */
/*                 12 - TSO command error                            */
/*                 13 - INPUT read  error                            */
/*                 14 - OUTPUT write error                           */
/*                                                                   */
/*  Sample JCL:                                                      */
/*                                                                   */
/*   //FINDSTR  EXEC PGM=IKJEFT01,PARM='FINDSTR  ALLOC'              */
/*   //SYSEXEC   DD   DSN=your.rexx.pds,DISP=SHR                     */
/*   //INPUT     DD   DSN=your.input,DISP=SHR                        */
/*   //OUTPUT    DD   DSN=your.output,DISP=(,CATLG),REFDD=*.INPUT,   */
/*   //          DSORG=PS,SPACE=(TRK,(1,20))                         */
/*   //SYSTSPRT  DD   SYSOUT=*                                       */
/*   //SYSTSIN   DD   DUMMY                                          */
/*                                                                   */
/*********************************************************************/
/*   Change  Log                                                     */
/*                                                                   */
/*   Author       Date      Reason                                   */
/*  --------     ---------  ---------------------------------------- */
/*  R. Zenuk     Dec 2006   Initial Creation                         */
/*                                                                   */
/*********************************************************************/
/*  Accept input string                                              */
/*********************************************************************/
parse  arg string
say 'Search string:'  string
/*********************************************************************/
/*  Read input file into the input stem                              */
/*********************************************************************/
"EXECIO  * DISKR INPUT (STEM INPUT. FINIS"
if RC <> 0 then exit 13
ilrc =  listdsi('INPUT' 'FILE')
say input.0 'records found in the INPUT dataset'  sysdsname
/*********************************************************************/
/*  Loop through all the input records                               */
/*********************************************************************/
o  = 0
do i=1 to  input.0
/*********************************************************************/
/*  Exclude all comments (REXX and *)                                */
/*********************************************************************/
if left(strip(input.i),2) <> '/*' &,
left(strip(input.i),1) <> '*'  then
do
/*********************************************************************/
/*  Match all records using the pos built-in and copy to output stem */
/*********************************************************************/
if pos(string,input.i) <> 0 then
do
o = o  + 1
output.o = input.i
end
end
end
/*********************************************************************/
/*  Write all the matching records from the output stem              */
/*********************************************************************/
"EXECIO  * DISKW OUTPUT (STEM OUTPUT. FINIS"
if RC <> 0 then exit 14
olrc =  listdsi('OUTPUT' 'FILE')
say o 'matching records written to the OUTPUT  dataset' sysdsname
exit 0


Hope This Helps,
Rob

