//SCOTTC  JOB 'CLEARSCR',CLASS=A,MSGCLASS=X,NOTIFY=SCOTT,
//         MSGLEVEL=(1,1),REGION=2048K
//STEP1    EXEC  ASMFCL
//ASM.SYSIN  DD  *
CLS      START 0
         TITLE 'CLS      - DOCUMENTATION                              '
* *********************************************************************
*
*     NAME:     CLS
*
*     PURPOSE:  TO CLEAR THE USER'S TSO SCREEN
*
*     WRITTEN:  SCOTT VETTER
*
*     DATE:     2008
*
*     ASSEMBLY: STARDARD
*
*     LINKAGE:  STANDARD
*
*     RUN-TIME: MUST BE RUN ON A 3270-2 TERMINAL
*
*     SECURITY CONSIDERATIONS: NONE
*
*     NOTES:    NOT TESTED FOR SCREENS LARGER THAN 24X80 - MODEL 2
*
*     INPUT:    NONE
*
*     OUTPUT:   THE TSO USER'S SCREEN IS CLEARED
*
*     MESSAGES: NONE
*
*     ABENDS:   NONE
*
*     R/C'S:    ONLY ZERO (0) IS RETURNED
*
*     MAPPING MACROS:    NONE
*
*     EXECUTION MACROS:  TPUT
*
*
* --------------------------------------------------------------------
*     CHANGE LOG:
*
*
*
*
* *********************************************************************
         TITLE 'CLS      - REGISTER DEFINITION / CONSTANTS            '
         SPACE 3
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         TITLE 'CLS      - CODE: INITIALIZATION, PURPOSE, TERMINATION '
         SPACE 3
         USING *,R15
         B     AROUNDID
         DC    CL8'CLS     '
         DC    CL1'-'
         DC    CL8'&SYSDATE'
         DC    CL1'-'
         DC    CL8'&SYSTIME'
         DC    CL1'-'
         DC    CL8'V1.R1.M0'
         DC    CL1'-'
         SPACE 3
AROUNDID DS    0H
         DROP  R15
         STM   R14,R12,12(R13)      SAVE CALLERS REGISTERS
         BALR  R12,0                COPY BASE REGISTER
         USING *,R12
         ST    R13,SAVEAREA+4       SAVE CALLERS SAP
         LR    R2,R13               COPY CALLERS SAP
         LA    R13,SAVEAREA         POINT TO OUR SAVE AREA
         ST    R13,8(R2)            SET OUR SAP IN CALLERS SA
         LR    R4,R1                SAVE PARM POINTER
         SPACE 3
         TPUT  CLEAR,CLEARX,FULLSCR    CLEAR THE SCREEN
         SPACE 3
         L     R13,SAVEAREA+4       RELOAD CALLERS SAP
         LM    R14,R12,12(R13)      RELOAD CALLERS REGISTERS
         SR    R15,R15              CLEAR RETURN CODE
         BR    R14                  RETURN TO CALLER
         TITLE 'CLS      - STORAGE AREAS                              '
         SPACE 3
SAVEAREA DS    18F
         SPACE 3
CLEAR    DC    X'27'          ECC
         DC    X'F5'          ERASE/WRITE
         DC    X'C1'          WCC
         DC    X'115D7F'      SET BUFFER ADDRESS L24C80
         DC    X'3C5D7F00'    RA L24C80,X'00'
CLEARX   EQU   *-CLEAR
         SPACE 3
         LTORG
         END
/*
//LKED.SYSLMOD DD  DSN=SYS2.LINKLIB(CLS),DISP=SHR
/*
