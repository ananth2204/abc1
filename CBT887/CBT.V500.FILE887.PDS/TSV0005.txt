//SCOTTZ  JOB 'IKJEFF10/TSV0005',CLASS=S,MSGCLASS=X,MSGLEVEL=(1,1),
//            NOTIFY=SCOTT,REGION=4096K
//*           USER=SCOTT,PASSWORD=SCOTTY
//*
//*   NAME:    SYS2.USERMODS(TSV0005 )
//*   PURPOSE: USING SMP, INSTALL THE USERMOD TSV0005 WHICH UPDATES THE
//*            TSO SUBMIT EXIT IKJEFF10.
//*            ONLY PERFORMS &SYSUID REPLACEMENT
//*
//REJRES  EXEC SMP,RVOL=MVSRES
//SMPCNTL  DD  *
  REJECT  S(TSV0005) .
 RESETRC .
  RESTORE S(TSV0005) .
/*
//RECEIVE EXEC SMP,RVOL=MVSRES
//SMPPTFIN DD *
++ USERMOD(TSV0005) /* TSO SUBMIT EXIT */ .
++ VER(Z038)
   FMID(EBB1102) .
++ SRC(IKJEFF10) DISTLIB(ACMDLIB) /* SYSLIB(SAMPLIB) */ .
IKJEFF10 CSECT
*
*  THIS EXIT INSERTS A CONTINUATION OF EACH JOB CARD SUBMITTED BY
*  A RACF DEFINED USER. THE CONTINUATION CARD CONTAINS THE USER ID
*  AND LOGON PASSWORD OF THE PERSION SUBMITTING THE JOB. IF THERE
*  IS NO ROOM TO INSERT A COMMA AND A BLANK THE JOB IS SUBMITTED
*  WITHOUT ADDING A CONTINNUATION CARD AND A MESSAGE IS SENT TO THE
*  USER INFORMING THEM OF THIS. IF THE USER IS NOT RACF DEFINED OR
*  EITHER 'USER' OR 'PASSWORD' KEY WORDS ARE FOUND THEN THE JOB IS
*  PASSED ON ASIS AND NO MESSAGE IS SENT. YOU CANNOT GET SOMEONE
*  ELSES PASSWORD BY USING THIS EXIT AS WRITTEN.
*
*  THIS EXIT IKJEFF10 REPLACES THE IBM VERSION OF IKJEFF10 WHICH IS
*  EFECTIVELY A BR14.  THIS EXIT WORKS WITH OR WITHOUT THE TSO/E OR
*  THE EARLYER TSO COMMAND PACKAGE AS THE DUMMY EXIT IS IN THE BASE
*  TSO CODE.  THIS EXIT WAS DEVELOPED AT THE GEORGIA DEPARTMENT OF
*  LABOR AND HAS BEEN IN USE FOR OVER ONE YEAR WITH NO KNOWN PROBLEMS.
*  WE WILL ATTEMPT TO FIX ERRORS AS LONG AS WE CONTINUE TO USE THIS
*  EXIT, BUT DO NOT PROMISE THAT WE WILL FIX BUGS OR PROVIDE ANY
*  SUPPORT IN THE FUTURE.
*
*  SEND COMMENTS AND ERROR REPORTS TO:
*        SYSTEMS SUPPORT UNIT
*        GEORGIA DEPARTMENT OF LABOR
*        ROOM 370 STATE LABOR BUILDING
*        ATLANTA, GA  30334
*
*   CHANGE LOG:
*       SKV - 02/25/09 - CHANGED TO REPLACE THE &SYSUID. VARIABLE WITH
*                        THE USERID THAT SUBMITTED THE JOB.
*
         EJECT
R0       EQU 0  OS LINKAGE
R1       EQU 1  OS LINKAGE - POINTER TO POINTER TO PARM LIST (IEEXITL)
R2       EQU 2  WORK REGISTER FOR GETMAIN, CVT, USERJCL, AND GENCD
R3       EQU 3  BASE REGISTER FOR ASCB
R4       EQU 4  BASE REGISTER FOR ASXB
R5       EQU 5  BASE REGISTER FOR ACEE
R6       EQU 6  BASE REGISTER FOR TSB
R7       EQU 7  BASE REGISTER FOR IEEXITL
R8       EQU 8  BASE REGISTER FOR IESUBCTD
R9       EQU 9  WORK REGISTER FOR CHANGING USERJCL (BASE FROM IECARDP)
R10      EQU 10 WORK REGISTER FOR USERJCL, MSGTEXT1, AND GENCD
R11      EQU 11 BASE REGISTER FOR GETMAIN AREA (STORED IN IEEXITWD)
R12      EQU 12 BASE REGISTER
R13      EQU 13 SAVE AREA
R14      EQU 14 OS LINKAGE
R15      EQU 15 OS LINKAGE - RETURN CODE FOR CALLING PROGRAM IKJEFF09
*
         USING *,R15
*        SAVE  (14,12),,IKJEFF10-SUBMIT-USER-EXIT-&SYSDATE-&SYSTIME
         STM   R14,R12,12(R13)
         B     AROUND
         DC    CL8'IKJEFF10'
         DC    CL1'-'
         DC    CL8'&SYSDATE'
         DC    CL1'-'
         DC    CL8'&SYSTIME'
         DC    CL1'-'
         DC    CL8'TSV0005 '
         DC    CL1'-'
AROUND   DS    0H
         BALR  R12,R0
         USING *,R12
*
PBASE    EQU   *
         DROP  R15
*
*                                    REGISTERS NOT CHAINED UNTIL AFTER
*                                      GETMAIN
*
         L     R7,0(0,R1)            GET ADDRESS OF PARM LIST (IEEXITL)
         USING IEEXITL,R7
         L     R8,IESUBCTP           GET ADDRESS OF SUBMIT JCL INFO
         USING IESUBCTD,R8
*
         CLC   IEEXITWD,ZEROS        FIRST INVOCATION OF THIS EXIT?
         BE    GETMAIN               YES, INITIALIZE
*
         L     R11,IEEXITWD          NO, POINT TO SAME GETMAIN AREA
         BAL   R14,READLOOP          GO DO SUBSTITUTION             SKV
         B     SETRC0                                               SKV
         B     CONTINUA              GO AROUND GETMAIN
*
GETMAIN  EQU   *
         L     R2,SIZDATD
         GETMAIN  R,LV=(2),SP=230
         LR    R11,R1                GET ADDRESS OF GETMAIN DSECT
         USING DATD,R11
         ST    R11,IEEXITWD          POINT TO GETMAIN AREA
         MVC   DATD(1),BLANKS
         MVC   DATD+1(254),DATD         CLEAR FIRST HALF GETMAIN AREA
*KV1     LOAD  EP=TCS
*        LR    R2,R0
         LA    R11,0
         ESTAE (R2),MF=(E,(R11))
*ENDDATDX EQU   ENDDATD-DATD
*
         L     R2,CVTPTR             GET ADDRESS OF CVT (X'10')
         L     R2,0(R2)              GET ADDRESS OF LIST AF ADDRESSES
         L     R3,12(R2)             GET ADDRESS OF CURRENT ASCB
         USING ASCB,R3
         L     R4,ASCBASXB           GET ADDRESS OF CURRENT ASXB
         USING ASXB,R4
         L     R5,ASXBSENV           GET ADDRESS OF CURRENT ACEE
         USING ACEE,R5
         TM    ACEEFLG1,ACEERACF     IS USER RACF DEFINED?
***SKV   BZ    TAKEEXOF              NO, NO MORE JOB CARDS TO COME
*                                      TO THIS EXIT FOR THIS SUBMIT
         L     R6,ASCBTSB            GET ADDRESS OF TSB
         LTR   R6,R6                 IS THERE A TSB ADDRESS?
*        BZ    TAKEEXOF              NO, CAN'T INSERT CARD
*
*        ABEND 22,DUMP
*
         USING TSB,R6
         MODESET KEY=ZERO
         MVC   SAVEPSWD,TSBPSWD      SAVE PASSWORD FROM TSB
         MODESET KEY=NZERO
         CLC   SAVEPSWD,ZEROS        IS THERE A LOGON PASSWORD?
         BE    TAKEEXOF              NO, CAN'T INSERT CARD
*
         MVC   SAVEUSER,ACEEUSER     SAVE PASSWORD FROM TSB
         B     CONTINUA
*
TAKEEXOF EQU   *
         MVI   IETAKEEX,ZEROHEX      TURN OFF TAKE EXIT SWITCH
         B     SETRC0
*
CONTINUA EQU   *
         LA    R1,SAVE
         ST    R13,4(,R1)             BACK CHAIN SAVE AREAS
         ST    R1,8(,R13)             FORWARD CHAIN SAVE AREAS
         LR    R13,R1                 SET R13 TO NEW SAVE AREA
         L     R1,4(R13)              SET R1 TO SAVE AREA AT ENTRY
         L     R1,24(,R1)             RESTORE R1 FROM SAVE AREA
*
         TM    IETAKEEX,IETJOB       TAKE EXIT FOR JOB CARD?
         BZ    SETRC0                SHOULD NOT BE IN THIS EXIT
*
         TM    IESTMTYP,IESJOB       IS STATEMENT JOB CARD?
         BZ    SETRC0                NO, DONT INSERT
         BAL   R14,READLOOP          GO DO SUBSTITUTION             SKV
         B     SETRC0                                               SKV
*
         CLC   IEMSGP,ZEROS          RETURN FROM SENDING MESSAGE?
         BE    REENT                 NO,
*
         MVC   IEMSGP,ZEROS          YES, CLEAR POINTER TO MESSAGE
         B     SETRC0                THIS CARD ALREADY PROCESSED
*
REENT    EQU   *
         BAL   R14,READLOOP          GO DO SUBSTITUTION             SKV
         L     R9,IECARDP            GET ADDRESS OF CURRENT
         LTR   R9,R9                 IS EXIT BEING RE-ENTERED?
         BZ    GENCD                 YES, INSERT USER AND PASSWORD CARD
*
OPRAND   EQU   *
         CLI   IEOPRAND,ZEROHEX      OPERAND COLUMN?
         BZ    NOOPRAND              NO OPERANDS ON THIS CARD
*
         SR    R2,R2
         IC    R2,IEOPRAND
         AR    R9,R2                 REGISTER TO POINT TO OPERAND
         BCTR  R9,R0
*
         L     R10,COMMALMT          IN LOOP, LOOK AT 71 COLUMNS
         SR    R10,R2                LESS FIRST PART OF CARD
*
         SR    R2,R2                 CLEAR FOR QUOTE SEARCH
*
COMPQUOT EQU   *
         CLC   0(1,R9),QUOTE         IS IT A QUOTE MARK?
         BNE   CKQUOT                NO,
*
         LTR   R2,R2                 IS IT THE BEGINNING QUOTE?
         BZ    BEGQUOT               YES
*
         SR    R2,R2                 NO, END OF QUOTE, RESET SWITCH
         B     NEXTCOL
*
BEGQUOT  EQU   *
         LA    R2,1                  SET SWITCH ON FOR QUOTE
         B     NEXTCOL               AND GO TO NEXT COLUMN
*
CKQUOT   EQU   *
         LTR   R2,R2                 ARE WE IN A QUOTATION?
         BP    NEXTCOL               YES, DONT LOOK FOR PASSWORD=
*
COMPPSWD EQU   *
         CLC   0(9,R9),PSWDCON       IS IT PASSWORD=?
         BE    SETSW                 YES, DONT INSERT CARD
*
COMPUSER EQU   *
         CLC   0(5,R9),USRCON        IS IT USER=?
         BE    SETSW                 YES, DONT INSERT CARD
*
COMPBLK  EQU   *
         CLC   0(1,R9),BLANKS        END OF OPERANDS?
         BNE   NEXTCOL               NO,
*
         B     CONTOPER              YES
*
NEXTCOL  EQU   *
         LA    R9,1(R9)              TRY NEXT COLUMN
         BCT   R10,COMPQUOT          IF NOT AT END OF CARD, LOOP
*
         B     CONTOPER              YES, IS OPERAND CONTINUED?
*
SETSW    EQU   *
         MVI   SW,ONE                TURN SWITCH ON, DONT NEED CARD
*
CONTOPER EQU   *
         TM    IESTMTYP,IESOPCON     IS OPERAND TO BE CONTINUED?
         BO    SETRC0                YES
*
         CLI   SW,ONE                NEED TO INSERT CARD?
         BE    NOINSERT              NO, GO GET NEXT STATEMENT, IF ANY
*
         CLC   SAVEUSER,BLANKS       HAVE A GOOD USER VALUE?
         BE    SETRC0                NO,
*
         CLC   SAVEPSWD,BLANKS       HAVE A GOOD PSWD VALUE?
         BE    SETRC0                NO,
*
         CLC   0(2,R9),BLANKS        ROOM FOR COMMA AND BLANK?
         BE    MVCOMMA               YES, CAN INSERT CARD
*
         CLC   0(1,R9),BLANKS        ROOM FOR JUST COMMA?
         BE    LASTCOL               MAYBE OK
*
         B     WARNING               CANT INSERT CARD
*
LASTCOL  EQU   *
         C     R10,ZEROS             AT COLUMN 71?
         BE    MVCOMMA               YES, CAN PUT COMMA WITHOUT BLANK
*                                       FOLLOWING
WARNING  EQU   *
         MVC   MSG,MSG1              MOVE MSG TO MSG AREA
         LA    R10,MSG               POINT REGISTER TO MESSAGE AREA
         ST    R10,IEMSGP            GIVE ADDRESS TO CALLER
         LA    R15,IEMSG             RC=8, TELL CALLER TO SEND MESSAGE
         B     RETURN
*
MVCOMMA  EQU   *
         MVI   0(R9),COMMA
*
         MVC   SAVETYP,IESTMTYP      SAVE SWITCHES
         OI    IESTMTYP,IESOPCON     NOW THERE IS A COMMA,
         OI    IESTMTYP,IESSCON        SO SET THE FLAGS
         LA    R15,IERETURN          RC=4, TELL CALLER TO RETURN FOR
         B     RETURN                    INSERTED CARDS
*
NOOPRAND EQU   *
         CLI   SW,ONE                USER OR PASSWORD ALREADY FOUND
         BNE   SETRC0                OR NOT FOUND AND GENERATED
*
NOINSERT EQU   *
         MVC   SW,BLANKS             TURN SWITCH OFF FOR NEXT JOB CARD
*
SETRC0   EQU   *
         LA    R15,IECONTIN          RC=0, TELL CALLER TO COMPLETE
*
RETURN   EQU   *
         L     R13,4(R13)            RESTORE SAVE AREA
         RETURN (14,12),RC=(15)
*
GENCD    EQU   *
         LA    R10,CD               POINT WORK REGISTER TO GETMAIN AREA
         ST    R10,IECARDP          POINT TO INSERTED CARD FOR CALLER
         MVC   CD(1),BLANKS
         MVC   CD+1(ENDCD-CD-1),CD  CLEAR CARD AREA
         MVC   0(19,R10),CDUSER     MOVE USER CONSTANT
         LA    R10,19(R10)
         MVC   0(8,R10),SAVEUSRI    MOVE USER TO CARD
         SR    R2,R2                CLEAR REG FOR LENGTH
         IC    R2,SAVEUSRL
         AR    R10,R2               BUMP PAST USER
         MVC   0(10,R10),CDPSWD     MOVE PASSWORD CONSTANT TO CARD
         LA    R10,10(R10)
         MVC   0(8,R10),SAVEPSWD    MOVE PASSWORD TO CARD
         LA    R10,8(R10)
         MVC   0(30,R10),CDMSG      MOVE COMMENT CONSTANT
*
         MVI   IEOPRAND,FIFTEEN     MOVE OPERAND COLUMN NUMBER
*
         MVC   IESTMTYP,SAVETYP     RESTORE SWITCH
         TM    IESTMTYP,IESSCON     IS THIS LAST CARD?
         BZ    SETCONTN             YES
*
         LR    R10,R11              SET UP WORK REGISTER
         LA    R10,71(R10)          TO COLUMN 72
         MVI   0(R10),NONBLANK      AND MOVE X TO COLUMN 72
*
SETCONTN EQU   *
         OI    IESTMTYP,IESCONTN    SET CONTINUATION FLAG
         B     SETRC0               AND LEAVE
         EJECT
*                                                                   SKV
*  PURPOSE:
*     GET INPUT, ESTABLISH POINTERS, AND PASS CONTROL TO PROCESS UID.
*
*  REQUIRES:
*     R1 POINTS TO INPUT LOCATION (WILL BE EST. BELOW)
*
READLOOP DS    0H
         STM   R0,R15,SA            SAVE REGISTERS
         L     R1,IECARDP           GET ADDRESS OF CURRENT
         LR    R3,R1                COPY INPUT LOCATION
         LTR   R3,R3                INPUT LOCATION EMPTY?
         BZ    READLOOX              YES - DO NOTHING THEN
         LA    R4,71(R3)            POINT TO THE END
         BAL   R14,GETUIDL          GO GET LENGTH OF UID
*        BAL   R14,SCANANS          SO SCAN & SUBSTITUTE SYSUID IN JCL
READLOOX DS    0H
         LM    R0,R15,SA            RESTORE REGISTERS
         BR    R14                  RETURN TO CALLER
         EJECT
*
*  PURPOSE:
*     TO COMPUTE LENGTH OF THE GIVEN USERID.
*
*  REQUIRES:
*     R5  - POINTS TO THE START OF THE USER ID (WILL BE EST. BELOW)
*     R14 - AN ADDRESS TO RETURN TO CALLER
*
GETUIDL  DS    0H
         STM   R0,R15,SA1      SAVE REGISTERS
         LA    R5,SAVEUSRI     POINT TO USER NAME
         LA    R4,8(R5)        SET MAX UID LENGTH (8)
         LR    R6,R5           SAVE UID POINTER
         LR    R7,R5           SAVE UID POINTER
GETUIDLL DS    0H
         CR    R6,R4           AT END?
         BE    GETUIDLE         YES - GO END
         CLI   0(R6),C' '      GOT A SPACE?
         BE    GETUIDLE         YES - ALL DONE
         LA    R6,1(R6)        POINT TO NEXT POSIT
         B     GETUIDLL        GO CHECK NEXT POSIT
GETUIDLE DS    0H
         SR    R6,R7           COMPUTE LENGTH - R6 HAS LENGTH
         ST    R6,UIDLEN       SAVE UID LENGTH
         LM    R0,R15,SA1      RESTORE REGISTERS
         BR    R14             RETURN TO CALLER
         EJECT
*
*  PURPOSE:
*     TO SCAN THE INPUT JCL FOR '&SYSUID.' OR '&SYSUID'
*        IF NOTHING FOUND, NOTHING IS DONE TO IT.
*        IF FOUND, THEN REPLACE IT WITH THE GIVEN TSO USERID.
*
*  REQUIRES:
*     R3 - POINTS TO THE START OF THE JCL IMAGE  (COL. 1)
*     R4 - POINTS TO THE END OF USABLE JCL IMAGE (COL. 71)
*
SCANANS  DS    0H
         CR    R3,R4                AT END?
         BE    LOOPX                 YES - GO END
         CLC   0(08,R3),LSYSUIDP    FOUND THING TO SUBSTITUTE?
         BE    LOOPF                 YES - GO DO SUBSTITUTION
         CLC   0(07,R3),LSYSUID     FOUND THING TO SUBSTITUTE?
         BE    LOOPF                 YES - GO DO SUBSTITUTION
LOOPL    DS    0H
         LA    R3,1(R3)             POINT TO NEXT POSITION
         B     SCANANS              GO BACK AND PROCESS NEXT POSITION
LOOPX    DS    0H
         B     SCANANS              AT END, SO GO BACK AND GET MORE JCL
*
*  PURPOSE:
*     WE GOT '&SYSUID.' SO NOW
*        REPLACE '&SYSUID.' WITH THE TSO USER ID.
*        SHIFT THE FOLLOWING INFORMATION
*
*  REQUIRES:
*     UIDLEN - HAS LENGTH OF TSO USER ID.
*     R3 - POINTS THE START OF WHERE '&SYSUID.' WAS FOUND.
*     R4 - POINTS THE END OF THE USABLE JCL IMAGE (COL. 71)
*
LOOPF    DS    0H
         STM   R0,R15,SA1      SAVE REGISTERS
*  REPLACE &SYSUID. WITH TSO USERID
         L     R11,UIDLEN           GET UID LENGTH
         LA    R10,9(R3)            POINT PAST &SYSUID. (8+1)
         LR    R9,R3                COPY CURRENT POSITION
         AR    R9,R11               POINT TO POSITION OF DATAMOVE START
         BCTR  R10,0                REDUCE BY 1
         EX    R11,XUIDMOV          MOVE IN UID
*  SHIFT DATA FOLLOWING .
         LR    R11,R10              COPY START OF WHERE TO MOVE TO
         LR    R7,R4                COPY END POINT
         SR    R7,R11               SUBTRACT START FROM END GIVING LEN
         EX    R7,XSFTMOV           SHIFT REMAINDER TO NEW POSIT
         LM    R0,R15,SA1           RESTORE REGISTERS
         B     LOOPL                RETURN TO CALLER
XUIDMOV  MVC   0(0,R3),SAVEUSRI     MOVE UID
XSFTMOV  MVC   0(0,R9),0(R10)       SHIFT DATA ON CARD
         EJECT
*                        CONSTANTS IN PROGRAM
ZEROS    DC    D'0'
*
COMMALMT DC    F'71'
*
SIZDATD  DC    AL1(0)
         DC    AL3(ENDDATD-DATD)
*
USRCON   DC    CL5'USER='
PSWDCON  DC    CL9'PASSWORD='
BLANKS   DC    CL9'         '
QUOTE    DC    XL1'7D'
*
CDUSER   DC    CL19'//            USER='
CDPSWD   DC    CL10',PASSWORD='
CDMSG    DC    CL30'          GENERATED BY TSV0005'
*
MSG1     EQU   *
MSGL1    DC    AL2(L'MSGTEXT1+2)
MSGTEXT1 DC    C'NO SPACE FOR COMMA - CAN NOT INSERT USER/PASSWORD'
*
MSGAREAL EQU   L'MSGTEXT1+2          LENGTH OF AREA IN GETMAIN AREA
*
COMMA    EQU   C','
ONE      EQU   C'1'
NONBLANK EQU   C'X'
ZEROHEX  EQU   X'00'
FIFTEEN  EQU   X'0F'
*
LSYSUIDP DC    X'50',C'SYSUID.'
LSYSUID  DC    X'50',C'SYSUID'
         LTORG
*                        CONSTANTS IN GETMAIN AREA
DATD     DSECT
*                        CONSTANTS IN GETMAIN AREA
CD       DS    CL80                       CARD TO BE INSERTED IN JCL
ENDCD    EQU   *
*
SAVE     DS    CL72                       REGISTER SAVE AREA
*
MSG      DS    CL(MSGAREAL)               WARNING MESSAGE TO TSO USER
*
SW       DS    CL1                 SWITCH ON(=1) IF USER OR PASSWORD
*                                     ALREADY ON THE JOB STATEMENT
SAVEUSER DS    0CL9
SAVEUSRL DS    AL1
SAVEUSRI DS    CL8
*
SAVEPSWD DS    CL8
*
SAVETYP  DS    CL1
*
SAVER14B DS    F
SAVER14A DS    F
SAVER14  DS    F
SA       DS    18F
SA1      DS    18F
UIDLEN   DS    F
ENDDATD  EQU   *
ENDDATDX EQU   ENDDATD-DATD
         EJECT
         CVT      DSECT=YES
         EJECT
         IHAACEE
         EJECT
         IHAASCB
         EJECT
         IHAASXB
         EJECT
         IKJEFFIE IETYPE=SUBMIT
         EJECT
         IKJTSB   LIST=YES
         END   IKJEFF10
/*
//SMPCNTL  DD  *
  RECEIVE S(TSV0005) .
/*
//APPL    EXEC SMP,RVOL=MVSRES
//* LINKLIB  DD DSN=SYS2.LINKLIB,DISP=SHR
//SMPCNTL  DD  *
  APPLY S(TSV0005)
        CHECK
        ASSEM
        BYPASS(ID) .
  APPLY S(TSV0005)
        ASSEM .
/*
