//SCOTTZ   JOB 'TSV0026 UMOD',NOTIFY=SCOTT,
//             CLASS=S,MSGCLASS=X,MSGLEVEL=(1,1)
//*
//*   NAME:    SYS2.USERMODS(TSV0026 )
//*   PURPOSE: TO ADD A TSO COMMAND TO DISPLAY THE APF LIST ON THE
//*            TSO USER'S TERMINAL.
//*
//*
//*
//RR     EXEC SMP,
//           RVOL=MVSRES
//SMPCNTL  DD *
  REJECT  S(TSV0026) .
   RESETRC .
  RESTORE S(TSV0026) .
   RESETRC .
/*
//RECV   EXEC SMP,
//           RVOL=MVSRES
//SMPPTFIN DD  DATA,DLM='$$'
++USERMOD(TSV0026) .
++VER(Z038) FMID(EBB1102) .
++SRC(DAPF) DISTLIB(AOSC5) SYSLIB(SAMPLIB) .
DAPF     START 0
         TITLE 'DAPF    -  DOCUMENTATION                              '
         SPACE 3
* *********************************************************************
*
*     NAME:       DAPF
*     PURPOSE:    TO DISPLAY THE APF TABLE ENTRIES
*     WRITTEN BY: SCOTT VETTER
*     DATE:       01 JANUARY 2011
*     WHY:        I LIKE MUCKING AROUND IN MVS!
*     INPUT:      NONE
*     PARM INPUT: NONE
*     OUTPUT:     THOSE DATASETS, WITH THE VOLSER, THAT ARE IN THE
*                 APF LIST.
*     RETURN CODE(S):         ALWAYS ZERO.
*     ABEND CODE(S):          NONE.
*     ASSEMBLY REQUIREMENTS:  REQUIRES ACCESS TO THE MACRO LIBRARY
*                             FOR THE CVT MAPPING.
*     LINKAGE REQUIREMENTS:   NOTHING SPECIAL.
*     NON-EXECUTABLE MACROS:  CVT
*     EXECUTABLE MACROS:      TPUT
*
*     CVT (CVTAUTHL) -> APF D.S. TABLE
*        AL(2) NUMBER OF ENTRIES
*        ENTRIES:  XL(1)  LENGTH OF ENTRY
*                  CL(6)  VOL/SER
*                  XL(1)  LENGTH OF DSNAME
*                  XL(??) THE DSNAME
*
* ---------------------------------------------------------------------
*
*     CHANGE LOG:
*
*
* *********************************************************************
         TITLE 'DAPF    -  REGISTER USAGE                             '
         SPACE 3
R0       EQU   0       WORK REGISTER
R1       EQU   1       WORK REGISTER
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12      BASE REGISTER
R13      EQU   13      SAVE AREA POINTER
R14      EQU   14      WORK REGISTER
R15      EQU   15      WORK REGISTER
         TITLE 'DAPF    -  ENTRY / EXIT CODE                          '
         SPACE 3
         USING *,R15
         B     AROUNDID
         DC    CL8'DAPF    '
         DC    CL1'-'
         DC    CL8'&SYSDATE'
         DC    CL1'-'
         DC    CL8'&SYSTIME'
         DC    CL1'-'
         DC    CL8'V1.R1.M0'
         DC    CL1'-'
         SPACE 2
AROUNDID DS    0H
         DROP  R15
         STM   R14,R12,12(R13)      SAVE CALLERS REGISTERS
         BALR  R12,0                COPY BASE REGISTER
         USING *,R12
         ST    R13,SAVEAREA+4       SAVE CALLERS SAP
         LA    R13,SAVEAREA         POINT TO OUR SAVE AREA
         LR    R4,R1                SAVE PARM POINTER
         B     DOMAIN               GO DO THE PURPOSE OF THHIS PGM
         SPACE 3
EXITPGM  DS    0H
         L     R13,SAVEAREA+4       RELOAD CALLERS SAP
         LM    R14,R12,12(R13)      RELOAD CALLERS REGISTERS
         SR    R15,R15              ZERO RETURN CODE
         BR    R14                  RETURN TO CALLER
         TITLE 'DAPF    -  MAIN CODE                                  '
         SPACE 3
* *********************************************************************
*
*    DO INITILIZATION
*       GET THE POINTER TO THE TABLE FROM THE CVT,
*       GET THE NUMBER OF ENTRIES,
*       AND WRITE THE HEADING.
*
* *********************************************************************
         SPACE 3
DOMAIN   DS    0H
         SPACE 3
         L     R2,CVTPTR            POINT TO THE CVT
         USING CVT,R2                AND MAP IT
         L     R3,CVTAUTHL          POINT TO THE START OF THE APF TABLE
         SR    R5,R5
         LH    R5,0(R3)             GET COUNT OF NUMBER OF ENTRIES
         LA    R3,2(R3)             POINT PAST NMBR OF ENTRIES FEILD
         TPUT  MSGLNEH,MSGLNEHL     PRINT THE RESULTS HEADING
         SPACE 3
* *********************************************************************
*
*    PROCESS EACH ENTRY
*       CLEAR THE OUTPUT LINE,
*       GET ENTRY LENGTH
*       WRITE OUT THE VOLSER
*       WRITE OUT THE VARIABLE LENGTH DSNAME,
*       ADJUST TO GET THE NEXT ENTRY,
*       WRITE THE RESULTS,
*       AND DO OVER AGIN IF NEED BE.
*
* *********************************************************************
         SPACE 3
LISTNTRY DS    0H
         MVI   MSGLNE1,X'40'
         MVC   MSGLNE1+1(51),MSGLNE1
         SR    R6,R6                CLEAR ENTRY LENGTH REG
         ICM   R6,X'0001',0(R3)     ST ENTRY LENGTH IN REG
         LR    R7,R6                SAVE LENGTH
         MVC   MSGLNE1V(6),1(R3)    MOVE VOLSER TO BUFFER
         S     R6,=F'6'             SUBTRACT VOLSER LENGTH
         S     R6,=F'1'             SUBTRACT 1 FOR EX INSTRUCTION
         EX    R6,MOVEDSN           MOVE DATASET NAME IN BUFFER
         SPACE 3
         TPUT  MSGLNE1,MSGLNE1L     PRINT THE RESULTS HEADING
         SPACE 3
         AR    R3,R7                POINT PAST DSNAME
         LA    R3,1(R3)             ADD 1 TO ACCOUNT FOR LENGTH FIELD
*        LA    R4,20(R3)            POINT TO A POSSIBLE END
         SPACE 3
         BCT   R5,LISTNTRY          GO PROCESS THE ENTRY
         SPACE 3
         B     EXITPGM              GO AROUND THE MOVE
         SPACE 3
MOVEDSN  MVC   MSGLNE1D(0),7(R3)    MOVE DSNAME TO BUFFER
         TITLE 'DAPF    -  DATA AREAS                                 '
         SPACE 3
SAVEAREA DS    18F
         SPACE 3
MSGLNEH  DS    0C
         DC    C'SVAP001 DATASETS IN THE APFLIST'
MSGLNEHL EQU   *-MSGLNEH
         SPACE 3
MSGLNE1  DS    0C
         DS    CL1
MSGLNE1V DS    CL6
         DS    CL1
MSGLNE1D DS    CL44
MSGLNE1L EQU   *-MSGLNE1
         SPACE 3
         LTORG
         TITLE 'DAPF    -  DSECTS                                     '
         CVT   LIST=NO,DSECT=YES
         EJECT
* *********************************************************************
*
*    THE END...
*
* *********************************************************************
         END
++JCLIN .
//TSV0026 JOB 'DAPF TSO COMMAND',CLASS=A
//ASMSTEP   EXEC  ASMS,OBJ=AOSC5,MOD=DAPF,CLASS='*'
//*YSLIB    DD  DSN=SYS1.MACLIB,DISP=SHR
//*         DD  DSN=SYS1.AMODGEN,DISP=SHR
//SYSIN    DD  DSN=SYS1.SAMPLIB(DAPF),DISP=SHR
//LNKSTEP  EXEC  LINKS,Â³ARM='LIST,LET,NCAL,XREF',
//        UNIT=3350,SER=MVSRES,N=SYS1,
//        NAME=LINKLIB,P1=' ',MOD=,P2=' ',
//        OBJ=AOSC5,CLASS='*'
//SYSLIN    DD  *
   INCLUDE SYSPUNCH(DAPF)
   NAME DAPF(R)
$$
/*
//SMPCNTL  DD *
  RECEIVE S(TSV0026) .
/*
//APP    EXEC SMP,
//           RVOL=MVSRES
//*SYSLIB   DD  DSN=DSTONE.SYM101.F01,DISP=SHR
//*         DD  DSN=SYS1.SMPMTS,DISP=SHR
//*         DD  DSN=SYS1.MODGEN,DISP=SHR
//*         DD  DSN=SYS1.AMACLIB,DISP=SHR
//*         DD  DSN=SYS1.AMODGEN,DISP=SHR
//SMPCNTL  DD *
  APPLY S(TSV0026)
       ASSEM
       CHECK .
  APPLY S(TSV0026)
       ASSEM .
/*
