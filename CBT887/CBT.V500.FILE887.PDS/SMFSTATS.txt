//SCOTTC  JOB 'SMFSTATS',CLASS=A,MSGCLASS=X,NOTIFY=SCOTT,
//         MSGLEVEL=(1,1),REGION=2048K
//STEP1    EXEC  ASMFCLG
//ASM.SYSLIB DD  DSN=SYS1.MACLIB,DISP=SHR
//        DD  DSN=SYS1.AMODGEN,DISP=SHR
//ASM.SYSIN  DD  *
SMFSTATS START 0
         SPACE 3
*********************************************************************
*
*   NAME:     SMFSTATS
*
*   PURPOSE:  TO DISPLAY THE SOME STATISTICS OF THE SMF RECORDS IN
*           SMF DATA INPUT.
*
*   AUTHOR:   SCOTT VETTER
*
*   DATE WRITTEN:  22 AUGUST 2003
*
*   COPYRIGHT:
*      ALL RIGHTS RESIDE WITH THE AUTHOR SCOTT VETTER.
*      THIS PROGRAM MAY BE FREELY USED, DISTRIBUTED, OR MODIFIED.
*      THE COPYRIGHT NOTE MUST NOT BE REMOVED.
*
*   NOTICE:
*      YOU USE THIS PROGRAM AT YOUR OWN RISK.
*      NO WARRANTY IS GIVEN EITHER EXPRESSED OR IMPLIED.
*
*   OPERATING ENVIRONMENT:
*      MVS 3.8
*
*   MODIFICATIONS:
*      NONE
*
*   REQUIREMENTS:
*      NONE
*
*   REGISTER CONVENTIONS
*      0  =  WORK REGISTER
*      1  =  WORK REGISTER / PARM POINTER
*      2  =
*      3  =  BASE REGISTER
*      4  =  BASE REGISTER
*      5  =  BASE REGISTER
*      6  =
*      7  =
*      8  =
*      9  =  SUBROUTINE RETURN REGISTER
*      10 =  SUBROUTINE RETURN REGISTER
*      11 =MAPPING REGISTER TO BASIC SMF RECORD
*      12 =
*      13 =  POINTER TO OUR SAVE AREA
*      14 =  RETURN REGISTER
*      15 =  CALL REGISTER / RETURN CODE
*
*   RETURN CODES:
*      0 = NORMAL - NO PROBLEMS
*
*   ABEND CODES:
*      U001 - MORE THAN FOUR SMFIDS HAVE BEEN FOUND AND WOULD EXCEED
*             THE TABLE HOLDING HOLDING THIS INFORMATION.
*
*   EXECUTION JCL:
*      //SCOTTS  JOB  'SMFSTATS',CLASS=A,MSGCLASS=X,REGION=1024K,
*      //            MSGLEVEL=(1,1),NOTIFY=SCOTT
*      //GO      EXEC PGM=SMFSTATS,REGION=1024K
*      //STEPLIB  DD  DSN=SYS2.LINKLIB,DISP=SHR
*      //SMFRPT   DD  SYSOUT=*
*      //SYSOUT   DD  SYSOUT=*
*      //SYSPRINT DD  SYSOUT=*
*      //SYSUDUMP DD  SYSOUT=*
*      //SMFDATA  DD  DSN=SMFDATA,DISP=SHR
*      /*
*
*   INPUT:
*      CONTROL CARDS - NONE
*      SMFRPT - CONTINS THE SMF DATA TO BE PROCESSED.
*
*   PARMS:
*      NONE
*
*   OUTPUT:
*      WTOS   - NONE
*      SMFRPT - A SET OF STATISTICS OF THE DATA FOUND IN THE SMFDATASET
*
*   EXTERNAL MODULES:
*      NONE
*
*   CONTROL BLOCKS:
*      NONE
*
*   MACROS:
*     ABEND, OPEN, GET, PUT, CLOSE, DCB, IFASMFR
*
*   SPECIAL NOTE(S):
*     ON ASSEMBLY, THE SYS1.AMODGEN DATASET MUST BE SPECIFIED IN
*     THE SYSLIB CONCATINATION LIST.
*
*
*********************************************************************
         SPACE 3
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE 3
         USING CSMFR,R11            EASTABLISH BASE ADDR FOR COMMON SMF
         SPACE 3
         DS    0H
         STM   R14,R12,12(R13)      SAVE CALLERS REGISTERS
         BALR  R3,0                 COPY BASE REGISTER
         USING *,R3,R4,R5
START    LM    R4,R5,BASEADDR
         ST    R13,SAVEAREA+4       SAVE CALLERS SAP
         LA    R13,SAVEAREA         POINT TO OUR SAVE AREA
*        LR    R4,R1                SAVE PARM POINTER
         SPACE 3
         OPEN  SMFDATA              OPEN SMF DATA FILE
         OPEN  (SMFRPT,OUTPUT)      OPEN SMF REPORT FILE
         B     PROCESS              GO PROCESS THE DATA
         SPACE 3
END      DS    0H
         MVC   REPORTL+10(29),=C'NUMBER OF RECORDS PROCESSED: '
         UNPK  REPORTL+39(8),RECCTR(8)
         OI    REPORTL+46,X'F0'
         BAL   R9,PRT               PRINT THE LINE
         MVC   REPORTL+10(33),=C'SMF ID(S) FOUND AND RECORD COUNT:'
         BAL   R9,PRT               PRINT THE LINE
         MVC   REPORTL+12(4),RSMFIDS          PRINT FIRST SMFID
         MVC   REPORTL+18(8),=C'--------'     PRINT FIRST SMFID
         UNPK  REPORTL+28(8),RSMFIDS+4(8)     UNPACK 1ST COUNTER
         OI    REPORTL+35,X'F0'
         BAL   R9,PRT               PRINT THE LINE
         MVI   REPORTL,C' '         PREP TO CLEAR THE REPORT LINE
         MVC   REPORTL+1(132),REPORTL  AND CLEAR THE REMAINDER
         MVC   REPORTL+12(4),RSMFIDS+12       PRINT FIRST SMFID
         MVC   REPORTL+18(8),=C'--------'     PRINT FIRST SMFID
         UNPK  REPORTL+28(8),RSMFIDS+16(8)    UNPACK 2ND COUNTER
         OI    REPORTL+35,X'F0'
         BAL   R9,PRT               PRINT THE LINE
         MVI   REPORTL,C' '         PREP TO CLEAR THE REPORT LINE
         MVC   REPORTL+1(132),REPORTL  AND CLEAR THE REMAINDER
         MVC   REPORTL+12(4),RSMFIDS+24       PRINT FIRST SMFID
         MVC   REPORTL+18(8),=C'--------'     PRINT FIRST SMFID
         UNPK  REPORTL+28(8),RSMFIDS+28(8)    UNPACK 2ND COUNTER
         OI    REPORTL+35,X'F0'
         BAL   R9,PRT               PRINT THE LINE
         MVI   REPORTL,C' '         PREP TO CLEAR THE REPORT LINE
         MVC   REPORTL+1(132),REPORTL  AND CLEAR THE REMAINDER
         MVC   REPORTL+12(4),RSMFIDS+36       PRINT FIRST SMFID
         MVC   REPORTL+18(8),=C'--------'     PRINT FIRST SMFID
         UNPK  REPORTL+28(8),RSMFIDS+40(8)    UNPACK 2ND COUNTER
         OI    REPORTL+35,X'F0'
         BAL   R9,PRT               PRINT THE LINE
         BAL   R9,PRT               PRINT THE LINE
         MVC   REPORTL+10(27),=C'EARLIEST DATE FOUND: YY.DDD'
         UNPK  REPORTL+31(6),LOWDATE(4)     MOVE LOWDATE
         OI    REPORTL+34,X'F0'
         MVC   REPORTL+40(25),=C'LATEST DATE FOUND: YY.DDD'
         UNPK  REPORTL+59(6),HIGHDATE(4)    MOVE HIGHEST DATE
         OI    REPORTL+62,X'F0'
         BAL   R9,PRT               PRINT THE LINE
         BAL   R9,PRT               PRINT THE LINE
         MVC   REPORTL+10(23),=C'SUMMARY OF RECORD TYPES'
         BAL   R9,PRT               PRINT THE LINE
         BAL   R10,PRTSMFNU         PRINT THE IDS & COUNTS
         CLOSE SMFDATA              CLOSE SMFDATA FILE
         CLOSE SMFRPT               CLOSE SMF REPORT FILE
         L     R13,SAVEAREA+4       RELOAD CALLERS SAP
         LM    R14,R12,12(R13)      RELOAD CALLERS REGISTERS
         SR    R15,R15              CLEAR RETURN CODE
         BR    R14                  RETURN TO CALLER
         SPACE 3
PROCESS  DS    0H
         GET   SMFDATA              GET MORE ANOTHER RECORD
         LR    R11,R1               GET MORE ANOTHER RECORD
         AP    RECCTR(8),=PL8'1'    ADD ONE TO THE RECORD COUNTER
         BAL   R10,ADDSMFID         ADD SMF IDS TO TABLE
         BAL   R10,ADDSMFNU         ADD SMF RECORD TYPE TO TABLE
         BAL   R10,PROCDATE         GO RECORD DATES
*        ABEND 001,DUMP
         B     PROCESS              GO PROCESS MORE DATA
         SPACE 3
*
*    ADD SYSTEM ID'S TO TABLE AND ADD RECORD COUNTS FOR EACH
*
         SPACE 1
ADDSMFID DS    0H                   ADD SMF IDS TO TABLE
         LA    R2,RSMFIDS           POINT TO TABLE START
ADDSMFL  DS    0H                   ADD SMF IDS TO TABLE
         CLC   0(4,R2),RSMFIDX      ARE WE AT THE END?
         BE    ADDSMF1               YES - EXIT
         CLC   0(4,R2),CSMFRSID     DID WE FIND A MATCH?
         BNE   ADDSMF2               NO - CONTINUE
         AP    4(8,R2),=PL8'1'      ADD ONE TO COUNTER TO MATCHING ID
         B     ADDSMFX              GO EXIT
ADDSMF2  EQU   *
         CLC   0(4,R2),=C'----'     IS THE ENTRY EMPTY?
         BNE   ADDSMF3               NO -
         MVC   0(4,R2),CSMFRSID     ADD SID TO TABLE
         AP    4(8,R2),=PL8'1'      ADD ONE TO COUNTER TO MATCHING ID
         B     ADDSMFX              GO EXIT
ADDSMF3  EQU   *
         LA    R2,12(R2)            POINT TO NEXT ENTRY IN TABLE
         B     ADDSMFL              GO CHECK NEXT ENTRY
ADDSMF1  DS    0H
         ABEND 002                  AT END OF TABLE
ADDSMFX  DS    0H                   ADD SMF IDS TO TABLE
         BR    R10                  RETURN TO CALLER
         SPACE 3
*
*    ADD THE ONE TO THE RECORD COUNT FOR EACH RECORD TYPE FOUND INTO
*    THE TABLE.
*
         SPACE 1
ADDSMFNU DS    0H
         LA    R2,RSMFNUM           POINT TO TABLE START
         MVC   ID+3(1),CSMFRID      MOVE RECORD TYPE
         L     R7,ID                LOAD COUNTER INTO REG5
         L     R6,=F'0'             MOVE RECORD TYPE
ADDSMFNL DS    0H                   ADD SMF IDS TO TABLE
         CR    R6,R7                ARE WE AT THE ENTRY
         BE    ADDSMFNX              YES - WE ARE THERE
         LA    R2,8(R2)             POINT TO THE NEXT ENTRY
         LA    R6,1(R6)             POINT TO THE NEXT ENTRY
         B     ADDSMFNL             GO DO NEXT ENTRY
ADDSMFNX DS    0H                   ADD SMF IDS TO TABLE
         AP    0(8,R2),=PL8'1'      ADD ONE TO THE ID ENTRY
         BR    R10                  RETURN TO CALLER
         SPACE 3
*
*    PRINT THE RECORD ID AND THE COUNT FOUND IF THE COUNT IS GREATER
*    THAN ZERO.
*
         SPACE 1
PRTSMFNU DS    0H                   ADD SMF IDS TO TABLE
         LA    R2,RSMFNUM           POINT TO TABLE START
         L     R7,=F'255'           SET ENDING POINT
         L     R6,=F'0'             MOVE RECORD TYPE
         MVC   COUNTER1(4),=PL4'0'  SET COUNTER TO ZERO
PRTSMFNL DS    0H                   ADD SMF IDS TO TABLE
         CR    R6,R7                ARE WE AT THE ENTRY
         BH    PRTSMFNX              YES - WE ARE THERE
         CP    0(8,R2),=PL8'0'      IS THE COUNT ZERO?
         BE    PRTSMFNC              YES - SKIP PRINTING
         UNPK  REPORTL+13(4),COUNTER1(4) UNPACK COUNT
         OI    REPORTL+16,X'F0'     MAKE IT READABLE
         MVC   REPORTL+18(5),=C'-----'
         UNPK  REPORTL+25(8),0(8,R2) UNPACK COUNT TO REPORT LINE
         OI    REPORTL+32,X'F0'     MAKE IT READABLE
         BAL   R9,PRT               PRINT THE LINE
PRTSMFNC DS    0H                   ADD SMF IDS TO TABLE
         AP    COUNTER1(4),=PL4'1'  ADD ONE TO COUNTER
         LA    R6,1(R6)             ADD ONE TO COUNTER
         LA    R2,8(R2)             POINT TO THE NEXT ENTRY
         B     PRTSMFNL             GO DO NEXT ENTRY
PRTSMFNX DS    0H                   ADD SMF IDS TO TABLE
         BR    R10                  RETURN TO CALLER
         SPACE 3
*   RECORD THE EARLIST AND LATEST DATES FOUND
         SPACE 1
PROCDATE DS    0H
*     DATE FORMAT IS: '0075233F'
         LA    R9,CSMFRDTE           POINT TO THE RECORD'S DATE
         CP    LOWDATE(4),=PL4'0'    FIRST RUN?
         BNE   PROCDAT1              YES - SKIP INITIAL SET
         MVC   LOWDATE(4),CSMFRDTE   SAVE DATE
         MVC   HIGHDATE(4),CSMFRDTE  SAVE DATE
PROCDAT1 DS    0H
         CP    CSMFRDTE(4),LOWDATE   RECORD DATE EARLIER THAN LOWEST?
         BH    PROCDAT2               NO - SKIP SET
         MVC   LOWDATE(4),CSMFRDTE   SET THE LOWER DATE
PROCDAT2 DS    0H
         CP    CSMFRDTE(4),HIGHDATE  RECORD DATE EARLIER THAN HIGHER?
         BL    PROCDAT3              NO - SKIP SET
         MVC   HIGHDATE(4),CSMFRDTE  SET THE LOWER DATE
PROCDAT3 DS    0H
         BR    R10                  RETURN TO CALLER
         SPACE 3
*
*   PRINT THE REPORT LINE BUT IF NEEDED, PRINT THE PAGE HEADINGS
*
         SPACE 1
PRT      DS    0H
         CP    LINECT(4),=PL4'56'   ARE HEADINGS NEEDED?
         BL    PRTLINE               NO - PRINT JUST THE LINE
         AP    PAGECT(4),=PL4'1'    ADD ONE TO THE PAGE COUNT
         MVC   LINECT(4),=PL4'4'    SET LINE COUNT TO 4
         MVC   HEADINGL+1(132),HEADINGL AND CLEAR THE REMAINDER
         MVI   HEADINGL,C'1'         SKIP TO TOP OF FORM
         MVC   HEADINGL+55(21),=C'SMF RECORD STATISTICS'
         MVC   HEADINGL+121(6),=C'PAGE: '
         UNPK  HEADINGL+127(4),PAGECT(4)    MAKE PAGE NUMBER PRINTABLE
         OI    HEADINGL+130,X'F0'
         PUT   SMFRPT,HEADINGL      PRINT THE RECORD COUNTER
         MVI   HEADINGL,C' '        PREP TO CLEAR THE REPORT LINE
         MVC   HEADINGL+1(132),HEADINGL   AND CLEAR THE REMAINDER
         PUT   SMFRPT,HEADINGL      PRINT A BLANK LINE
         MVI   HEADINGL+1,C'-'      PREP TO CLEAR THE REPORT LINE
         MVC   HEADINGL+2(130),HEADINGL+1 AND CLEAR THE REMAINDER
         PUT   SMFRPT,HEADINGL      PRINT A DASHED LINE
         MVI   HEADINGL,C' '        PREP TO CLEAR THE REPORT LINE
         MVC   HEADINGL+1(132),HEADINGL   AND CLEAR THE REMAINDER
         PUT   SMFRPT,HEADINGL      PRINT A BLANK LINE
PRTLINE  DS    0H                   ADD SMF IDS TO TABLE
         MVI   REPORTL,C' '         SET LINE SPACEING TO ONE
         AP    LINECT(4),=PL4'1'    ADD ONE TO THE PAGE COUNT
         PUT   SMFRPT,REPORTL       PRINT THE RECORD COUNTER
         MVI   REPORTL,C' '         PREP TO CLEAR THE REPORT LINE
         MVC   REPORTL+1(132),REPORTL  AND CLEAR THE REMAINDER
         BR    R9                   RETURN TO CALLER
         EJECT
*
*   LOCAL VARABLES
*
         SPACE 1
BASEADDR DC    A(START+4096,START+8192)
SAVEAREA DS    18F
LINECT   DC    PL4'99'
PAGECT   DC    PL4'00'
LOWDATE  DC    PL4'0'
HIGHDATE DC    PL4'0'
RECTYPEC DC    255F'0'
COUNTER  DC    F'0'
COUNTER1 DC    PL4'0'
ID       DC    F'0'
RECCTR   DC    PL8'0'
REPORTL  DC    133CL1' '
HEADINGL DC    133CL1' '
         LTORG
         SPACE 3
*
*   LOCAL TABLES
*
         SPACE 1
RSMFIDS  DS    0H                   SMF ID RECORDING TABLE
         DC    CL4'----',PL8'0'
         DC    CL4'----',PL8'0'
         DC    CL4'----',PL8'0'
         DC    CL4'----',PL8'0'
RSMFIDX  DC    CL4'****',PL8'0'
RSMFNUM  DC    255PL8'0',PL8'0'     SMF RECORD NUMBER COUNTER TABLE
         PRINT NOGEN
         SPACE 3
*
*   DATASET DEFINITIONS
*
         SPACE 1
SMFDATA  DCB   DDNAME=SMFDATA,DSORG=PS,MACRF=GL,EODAD=END
SMFRPT   DCB   DDNAME=SMFRPT,DSORG=PS,MACRF=PM,RECFM=FBA,LRECL=133,    X
               BLKSIZE=133
         SPACE 3
*
*   BASIC SMF RECORD DESCRIPTION
*
         SPACE 1
CSMFR    DSECT
CSMFRLEN DS    BL2
CSMFRSEG DS    BL2
CSMFRHDR DS    BL1
CSMFRID  DS    BL1
CSMFRTOD DS    BL4
CSMFRDTE DS    BL4
CSMFRSID DS    CL4
         SPACE 3
*
*   DETAILED SMF RECORD DESCRIPTION
*
         SPACE 1
         IFASMFR (0,4,5,6,7,9,10,11,14,17,18,20,25,26,31,34,35)
         IFASMFR (43,45,47,48,62,63,64,67,68,69)
         END
//LKED.SYSLMOD DD  DSN=SYS2.LINKLIB(SMFSTATS),DISP=SHR
//GO.SMFRPT   DD  SYSOUT=*
//GO.SYSOUT   DD  SYSOUT=*
//GO.SYSUDUMP DD  SYSOUT=*
//GO.SMFDATA  DD  DSN=SYS2.SMFDATA,DISP=SHR
/*
