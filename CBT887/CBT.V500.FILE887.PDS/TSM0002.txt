//SCOTTZ   JOB 'TSM0002 UMOD',NOTIFY=SCOTT,
//   CLASS=S,MSGCLASS=X,MSGLEVEL=(1,1),
//   USER=SCOTT,PASSWORD=SCOTTY
//*
//*   NAME:    SYS2.USERMODS(TSM0002 )
//*   PURPOSE: TO INSTALL USERMOD TSM0002 WHICH INSTALLS IEFUTL
//*            (SMF TIME LIMIT EXIT).
//*
//*   NOTE(S): A IPL WITH A CLPA IS REQUIRED.
//*
//*
//*
//*
//RR     EXEC SMP,RVOL=MVSRES
//SMPJCLIN DD DATA,DLM='$$'
$$
//SMPCNTL  DD *
  RESTORE G(TSM0002) .
   RESETRC .
  REJECT  S(TSM0002) .
/*
//RECV   EXEC SMP,RVOL=MVSRES
//SMPPTFIN DD *
++USERMOD(TSM0002) .
++VER(Z038) FMID(EBB1102) .
++SRC(IEFUTL) DISTLIB(AOSB3) SYSLIB(SAMPLIB) .
         TITLE 'IEFUTL   - SMF TIME LIMIT EXIT - MACRO DEFINITION(S) '
         MACRO                GENERATES CALLING SEQUENCE FOR TSMFWTM
&NAME    SMFWTM &MSGAD
         AIF   ('&MSGAD' EQ '').E1                NO OPERAND
         AIF   ('&MSGAD' EQ '(1)').BAL            REG 1 ALREADY LOADED
         AIF   ('&MSGAD'(1,1) EQ '(').R2        REG(NOT 1) SPECIFIED
         AGO   .LODIT                             ADDRESS SPECIFIED
.E1      MNOTE '* * * NO OPERAND SPECIFIED * * *'
         MEXIT
.BAL     ANOP
         CNOP  0,4
&NAME    BAL   15,*+8
.LIST    DC    V(TSMFWTM)                         ADDRESS OF TSMFWTM
         L     15,0(15)
         BALR  14,15
         MEXIT
.R2    ANOP
&NAME    LR    1,&MSGAD(1)
         CNOP  0,4
         BAL   15,*+8
         AGO   .LIST
.LODIT   ANOP
&NAME    LA    1,&MSGAD
         CNOP  0,4
         BAL   15,*+8
         AGO   .LIST
         MEND
         TITLE 'IEFUTL   - SMF TIME LIMIT EXIT - DOCUMENTATION       '
IEFUTL   CSECT
*
*
*
*C 450000                                                      @YM04803
*C TO GIVE RC8 AND TIME EXTENSION IN SECONDS TO TESTEXIT       @ZA17320
*
* STATUS - CHANGE LEVEL 1
*
* FUNCTION
*    THIS MODULE IS ENVOKED WHEN THE JOB TIME, STEP TIME OR
*    JOB WAIT TIME LIMIT IS EXCEEDED. THE JOB IS CANCELED IF THE
*    JOB CPU TIME LIMIT EXPIRES. IF THE STEP CPU TIME LIMIT    @ZA17320
*    EXPIRES, THE TIME WILL BE EXTENDED BY A FIXED NUMBER OF   @ZA17320
*    TIMER UNITS. IF THE CONTINUOUS WAIT TIME LIMIT EXPIRES,   @ZA17320
*    THE TIME WILL BE EXTENDED BY A FIXED NUMBER OF SECONDS.   @ZA17320
*    IF MORE THAN THREE EXTENSIONS ARE REQUIRED, THE JOB WILL  @ZA17320
*    BE CANCELLED. EACH TIME A JOB WAIT TIME IS EXCEEDED, A    @ZA17320
*    RECORD IS WRITTEN TO THE SMF DATA SET.                    @ZA17320
*
* ENTRY POINTS -
*         IEFUTL
*
* INPUT
*    REGISTER 1 POINTS TO A 4 BYTE ADDRESS FOR THE FOLLOWING
*    PARAMETER:
*       36 BYTES OF THE FOLLOWING JOB INFORMATION:
*       JOBNAME (8 BYTES)
*       TIME STAMP (8 BYTES)
*       SYSTEM ID (4 BYTES)
*       USER ID (8 BYTES)
*       USER COMMUNICATIONS AREA (4 BYTES) - THE FIRST BYTE
*       IS USED AS AN EXTENSION INDICATOR. IT IS ZERO FOR
*       THE INITIAL ENTRY AND CONTAINS AN INCREMENTED
*       VALUE FOR SUBSEQUENT ENTRIES.
*    REGISTER 0 WILL CONTAIN ONE OF THE FOLLOWING ENTRY CODES
*    INDICATING WHICH TIME LIMIT WAS EXCEEDED:
*             0 - JOB CPU TIME
*             4 - STEP CPU TIME
*             8 - JOB WAIT TIME
*                                                              @ZA17320
* OUTPUT
*    EACH TIME THE JOB WAIT TIME LIMIT IS EXCEEDED, A 43 BYTE
*    RECORD IS WRITTEN TO THE SMF DATA SET USING THE SMFWTM MACRO.
*    THE FORMAT OF THIS RECORD IS DESCRIBED BY THE DSECT FOR THE
*    SMF RECORD.
*    IF THE TIME LIMIT IS EXTENDED, THE AMOUNT OF THE EXTENSION
*    IN TIMER UNITS MUST BE RETURNED IN REGISTER 1.
*    REGISTER 15 MUST CONTAIN ONE OF THE FOLLOWING RETURN CODES:
*             0 - CONTINUE PROCESSING (CANCEL THE JOB)
*             4 - CONTINUE THE JOB WITH THE TIME EXTENSION IN REG 1
*                 IN TIMER UNITS.                              @ZA17320
*             8 - CONTINUE THE JOB WITH THE TIME EXTENSION IN  @ZA17320
*                 REG1 IN SECONDS.                             @ZA17320
*                                                              @ZA17320
* EXTERNAL REFERENCES - NONE
*                                                              @ZA17320
* EXITS,NORMAL - RETURN TO CALLER
*                                                              @ZA17320
* EXITS,ERROR - NONE
*                                                              @ZA17320
* TABLES/WORK AREAS - DSECT FOR SMF RECORD
*                   - JOBNAMES TO ALLOW CONTINUATION FOR 10 MIN
*                     IRREGARDLESS OF WHAT TIME EXPIRATION OCCURS.
*
* ATTRIBUTES - STANDARD
*                                                              @ZA17320
* CHARACTER CODE DEPENDENCY - NONE
*                                                              @ZA17320
* MACROS USED - GETMAIN, FREEMAIN, TIME, SMFWTM AND WTO
*
* COMPILING - ADD "RENT" OPTION TO THE ASSEMBLER PARM.
*
* LINKING   - SEE THE STAGE 2 FOR LINKAGE EDITER INFORMATION.
*             THIS EXIT IS LINKED WITH OTHER MODULES.
*
* DESTINATION - SYS1.LPALIB
*
* NOTES -  SOME MESSAGES COMMENTED OUT AS THEY ARE USED FOR DEBUGGING
*
* CHANGE LOG:
*       SKV - 06/05/05 - MODIFIED TO FIT ENVIRONMENT
*
*
*
*
         TITLE 'IEFUTL   - SMF TIME LIMIT EXIT - REGISTER / EQUATES  '
R0       EQU   0                       REG 0
R1       EQU   1                       REG 1
R2       EQU   2                       @ OF 36 BYTES OF JOB INFO
R3       EQU   3                       TIME EXTENSION
R4       EQU   4                       DSECT BASE
R5       EQU   5                       WORK REG (CONTAINS 4)
R6       EQU   6                       REG 6
R7       EQU   7                       REG 7
R8       EQU   8                       REG 8
R9       EQU   9                       REG 9
R10      EQU   10                      REG 10
R11      EQU   11                      REG 11
R12      EQU   12                      REG 12
R13      EQU   13                      REG 13
R14      EQU   14                      REG 14
R15      EQU   15                      REG 15
D0       EQU   0                       DISP OF 0
D1       EQU   1                       DISP OF 1
D4       EQU   4                       DISP OF 4
D8       EQU   8                       DISP OF 8
D12      EQU   12                      DISP OF 12
D24      EQU   24                      DISP OF 24
D32      EQU   32                      DISP OF 32
D16      EQU   16                      DISP OF 16
D20      EQU   20                      DISP OF 20
D33      EQU   33                      DISP OF 33
L4       EQU   4                       LENGTH OF 4
L6       EQU   6                       LENGTH OF 6
L8       EQU   8                       LENGTH OF 8
L16      EQU   16                      LENGTH OF 16
         TITLE 'IEFUTL   - SMF TIME LIMIT EXIT - INITIAL ENTRY       '
         USING *,R15
         B     HERE                   BRANCH AROUND MOD NAME
         DC    CL8'IEFUTL'            MODULE NAME
         DC    CL8'&SYSDATE'          DATE ASSEMBLED
         DC    CL8'&SYSTIME'          TIME ASSEMBLED
HERE     DS    0H
         STM   R14,R12,D12(R13)       STANDARD
         DROP  R15
         BALR  R11,0                  ENTRY LINKAGE
         USING *,R11
         LR    R3,R0                  SAVE ENTRY CODE
         L     R2,D0(R1)              GET PARAMETER ADDRESS
*
         GETMAIN   R,LV=2048          GET STORAGE FOR OUR WORK
         LR    R4,R1                  SET BASE REG FOR OUR WORK AREA
         USING SMFDSECT,R4            TELL ASSEMBLER TO MAP IT
*
*        LA    R1,MSG1             POINT TO THE MESSAGE TO DISPLAY
*        WTO   MF=(E,(R1))         DISPLAY THE MESSAGE
         B     MSG1BYP             BRANCH AROUND MESSAGE
*
MSG1     DS    0F
         DC    AL2(MSG1X-MSG1)
         DC    H'0'
         DC    C'IEFUTL: ENTERED'
MSG1X    EQU   *
MSG1BYP  DS    0H
         TITLE 'IEFUTL   - SMF TIME LIMIT EXIT - PRELIMINARY PROCESS '
*
*   LOOP THROUGH THE DON'T CARE TABLE FOR A MATCHINE JOBNAME.  WE DON'T
*  CARE WHAT KIND OF TIME TERMINATION OCCURED.  IF IT'S IN THE TABLE WE
*  CONTINUE FOR TEN MINUTES.  THAT'S ALL WE DO FOR RIGHT NOW.  ALL
*  'SAMPLE' CODE IS BYPASSED.  KEPT FOR FUTURE EXAMINATION.
*  IF WE GET HERE WITHOUT A MATCH IN THE TABLE, WE CANCEL TO JOB.
*
         SPACE 3
*   DISPLAY WHAT INFORMATION SMF PROVIDED US
         SPACE 1
*        MVC   WTOAREA(48),MSGB      MOVE MESSAGE TO MESSAGE AREA
*        MVC   WTOAREA+12(36),0(R2)  MOVE SMF DATA TO MESSAGE AREA
*        LA    R1,WTOAREA            POINT TO THE MESSAGE AREA
*        WTO   MF=(E,(R1))           DISPLAY THE MESSAGE
         SPACE 3
*   DISPLAY WHAT JOB THIS PROCESSING IF FOR
         SPACE 1
*        LA    R7,NCWRTBLS           POINT TO START OF TABLE
*        MVC   WTOAREA(37),MSGA      MOVE MESSAGE TO MESSAGE AREA
*        MVC   WTOAREA+12(8),0(R2)   MOVE JOBNAME TO MESSAGE AREA
*        LA    R1,WTOAREA            POINT TO THE MESSAGE AREA
*        WTO   MF=(E,(R1))           DISPLAY THE MESSAGE
         LA    R7,NCWRTBLS           POINT TO START OF TABLE
*   LOOP THROUGH THE TABLE TO SEE IF THE JOB NAME IS THERE
         SPACE 1
DCLOOP   DS    0H
         CLC   0(8,R7),NCWRTBLE      AT END OF TABLE?
         BE    CONT1                  YES - GO PERFORM OTHER PROCESSING
*        MVC   WTOAREA(37),MSGA      MOVE MESSAGE TO MESSAGE AREA
*        MVC   WTOAREA+12(8),0(R7)   MOVE JOBNAME TO MESSAGE AREA
*        LA    R1,WTOAREA            POINT TO THE MESSAGE AREA
*        WTO   MF=(E,(R1))           DISPLAY THE MESSAGE
         CLC   0(8,R7),0(R2)         DOES THE JOBANEM MATCH?
         BE    DCLOOPC                YES - EXIT SO JOB RUNS NON-STOP
         LA    R7,8(R7)              POINT TO NEXT TABLE ENTRY
         B     DCLOOP                GO CHECK THE NEXT ENTRY
         SPACE 3
*   THE JOB NAME WAS IN THE TABLE, SO WE CONTINUE FOR TEN MINUTES
         SPACE 1
DCLOOPC  DS    0H
**       MVC   WTOAREA(36),MSG2      MOVE MESSAGE TO MESSAGE AREA
**       MVC   WTOAREA+12(8),0(R7)   MOVE JOBNAME TO MESSAGE AREA
**       LA    R1,WTOAREA            POINT TO THE MESSAGE TO DISPLAY
**       WTO   MF=(E,(R1))           DISPLAY THE MESSAGE
         B     MSG2BYP               BRANCH AROUND MESSAGE
*
MSG2     DS    0F
         DC    AL2(MSG2X-MSG2)
         DC    H'0'
         DC    C'IEFUTL: ******** JOB CONTINUED  '
MSG2X    EQU   *
MSG2BYP  DS    0H
         MVC   D24(L4,R13),TENMIN    PUT 10 MINUTES IN R1
         B     EXIT8                 GO EXIT SO JOB CAN CONTINUE
*   JOB WAS NOT FOUND IN THE TABLE, SO WE EXIT STATING THAT THE JOB
*   MUST BE CANCELED.
CONT1    DS    0H
         MVC   WTOAREA(36),MSG3      MOVE MESSAGE TO MESSAGE AREA
         MVC   WTOAREA+12(8),0(R7)   MOVE JOBNAME TO MESSAGE AREA
         LA    R1,WTOAREA          POINT TO THE MESSAGE TO DISPLAY
         WTO   MF=(E,(R1))         DISPLAY THE MESSAGE
         B     MSG3BYP             BRANCH AROUND MESSAGE
*
MSG3     DS    0F
         DC    AL2(MSG3X-MSG3)
         DC    H'0'
         DC    C'IEFUTL: ******** JOB CANCELED   '
MSG3X    EQU   *
MSG3BYP  DS    0H
         TITLE 'IEFUTL   - SMF TIME LIMIT EXIT - OTHER PROCESS       '
*
*   THIS PROCESS FOR FUTURE REFERENCE.
*
*   CHECK WHAT KIND OF TIME EXPIRATION OCCURED.  BRANCH TO THE ROUTINE
*   THAT HANDELS THE PROCESS.
*
         SPACE 3
*   TEST THE TYPE OF ENTRY 0-JOB, 4-STEP, 8 - WAIT
         SPACE 1
CONT2    DS    0H
         C     R3,=F'0'              WAS ENTRY DUE TO JOB EXEC TIME?
         BE    JOBEXEC                YES - GO PROCESS
         C     R3,=F'4'              WAS ENTRY DUE TO STEP EXEC TIME?
         BE    STEPEXEC               YES - GO PROCESS
         C     R3,=F'8'              WAS ENTRY DUE TO WAIT TIME?
         BE    WAITTIME               YES - GO PROCESS
         LA    R1,MSG4             POINT TO THE MESSAGE TO DISPLAY
         WTO   MF=(E,(R1))         DISPLAY THE MESSAGE
         B     MSG4BYP             BRANCH AROUND MESSAGE
*
MSG4     DS    0F
         DC    AL2(MSG4X-MSG4)
         DC    H'0'
         DC    C'IEFUTL: SHOULD NOT GET HERE...'
MSG4X    EQU   *
MSG4BYP  DS    0H
         L     R15,0                 SET CANCEL (RC=0) IN REG 15
         B     EXIT                  GO EXIT
         TITLE 'IEFUTL   - SMF TIME LIMIT EXIT - JOB TIME EXPIRATION '
*
*   ENTRY HERE MEANS THAT THE JOB'S EXECUTION TIME HAS BEEN EXCEEDED
*   WHAT WE DO FOR NOW IS JUST TO CANCEL.
*
         SPACE 3
JOBEXEC  DS    0H
         LA    R1,MSG5             POINT TO THE MESSAGE TO DISPLAY
         WTO   MF=(E,(R1))         DISPLAY THE MESSAGE
         B     MSG5BYP             BRANCH AROUND MESSAGE
*
MSG5     DS    0F
         DC    AL2(MSG5X-MSG5)
         DC    H'0'
         DC    C'IEFUTL: JOB EXEC TIME EXCEEDED - CANCELING'
MSG5X    EQU   *
MSG5BYP  DS    0H
         L     R15,0                 SET CANCEL (RC=0) IN REG 15
         B     EXIT                  GO EXIT
         TITLE 'IEFUTL   - SMF TIME LIMIT EXIT - STEP TIME EXPIRATION'
*
*   ENTRY HERE MEANS THAT THE STEP'S EXECUTION TIME HAS BEEN EXCEEDED
*   WHAT WE DO FOR NOW IS JUST TO CANCEL.
*
         SPACE 3
STEPEXEC DS    0H
         LA    R1,MSG6             POINT TO THE MESSAGE TO DISPLAY
         WTO   MF=(E,(R1))         DISPLAY THE MESSAGE
         B     MSG6BYP             BRANCH AROUND MESSAGE
*
MSG6     DS    0F
         DC    AL2(MSG6X-MSG6)
         DC    H'0'
         DC    C'IEFUTL: STEP EXEC TIME EXCEEDED - CANCELING'
MSG6X    EQU   *
MSG6BYP  DS    0H
         L     R15,0                 SET CANCEL (RC=0) IN REG 15
         B     EXIT                  GO EXIT
         TITLE 'IEFUTL   - SMF TIME LIMIT EXIT - WAIT TIME EXPIRATION'
*
*   ENTRY HERE MEANS THAT THE WAIT TIME HAS BEEN EXCEEDED
*   WHAT WE DO FOR NOW IS JUST TO CANCEL.
*
         SPACE 3
WAITTIME DS    0H
         LA    R1,MSG7             POINT TO THE MESSAGE TO DISPLAY
         WTO   MF=(E,(R1))         DISPLAY THE MESSAGE
         B     MSG7BYP             BRANCH AROUND MESSAGE
*
MSG7     DS    0F
         DC    AL2(MSG7X-MSG7)
         DC    H'0'
         DC    C'IEFUTL: WAIT TIME EXCEEDED - CANCELING'
MSG7X    EQU   *
MSG7BYP  DS    0H
         L     R15,0                 SET CANCEL (RC=0) IN REG 15
         B     EXIT                  GO EXIT
         TITLE 'IEFUTL   - SMF TIME LIMIT EXIT - ORIGINAL CODE -UNUSED'
*
*  ORIGINAL CODE FOLLOWS - UNUSED BUT PROVIDED FOR FUTURE REFERENCE.
*
         SPACE 3
CONT3    DS    0H
         LA    R5,L4                  SET COMPARAND REGISTER TO 4
         CR    R0,R5                  TEST FOR JOB/STEP TIME EXCEEDED
         BL    CANCEL                   JOB CPU TIME EXCEEDED  @ZA17320
         GETMAIN   R,LV=56              GET CORE FOR SMF RECORD
         LR    R3,R0                 SET ENTRY CODE          @ZA17320
         LR    R4,R1
         USING SMFDSECT,R4
         TIME      BIN
         ST    R0,TIME                  TIME FOR SMF RECORD
         ST    R1,DATE                  DATE FOR SMF RECORD
         MVC   SYSID(L4),D16(R2)      BUILD SMF RECORD
         MVC   JOBLOG(D16),D0(R2)     CONTAINING JOB LOG NO.
         MVC   USERID(L8),D20(R2)     USER ID
         MVC   HEADER(L6),SMFRCD        AND HEADER
         SR    R3,R3
         IC    R3,D32(R2)           GET TIME EXTENSION INDICATOR
         STC   R3,EXTCODE             PUT EXTENSION CODE IN RECORD
         LA    R1,SMFOUT
*        SMFWTM    (1)                  WRITE SMF RECORD TYPE 132
         LR    R1,R4
         FREEMAIN  R,LV=56,A=(1)
         LA    R5,L4                  SET COMPERAND VALUE    @ZA17320
         CR    R3,R5                CHECK STEP CPU TIME    @ZA17320
         BH    PROC8                    NO, GO PROC WAIT TIME  @ZA17320
         TM    D33(R2),X'01'          1ST ENT FOR STEP TIME  @ZA17320
         BO    CANCEL                   NO, CANCEL STEP        @ZA17320
         OI    D33(R2),X'01'          SET 1ST TIME INDICATOR @ZA17320
         B     TIMUN                    PROCESS TIMER UNITS    @ZA17320
PROC8    SR    R6,R6                CLEAR REGISTER         @ZA17320
         IC    R6,D32(R2)           GET ENTRY INDICATOR    @ZA17320
         CH    R6,CONS1               UTL MORE THAN TWICE    @ZA17320
         BH    CANCEL                   YES, NOT ALLOWED       @ZA17320
         LA    R6,D1(R6)            INCREMENT ENTRY VALUE  @ZA17320
         STC   R6,D32(R2)           SAVE NEW VALUE         @ZA17320
         MVC   D24(L4,R13),SECEXT       PUT SECONDS IN R1      @ZA17320
         B     INCEXT                                          @ZA17320
TIMUN    MVC   D24(L4,R13),TIMEXT       PUT TIMER UNITS IN R1  @ZA17320
INCEXT   LR    R5,R3                SAVE CODE              @ZA17320
         B     EXIT                     RETURN
CANCEL   SR    R5,R5                SET RETURN CODE VALUE TO 0
         B     EXIT                     RETURN
         TITLE 'IEFUTL   - SMF TIME LIMIT EXIT - EXIT ROUTINES        '
         SPACE 3
*
*   EXIT  - CAUSES THE JOB TO BE CANCELED WITHOUT ANY EXTENSION.
*
         SPACE 1
EXIT     DS    0H
         LR    R1,R4                  POINT TO OUR STORAGE AREA
         FREEMAIN  R,LV=2048,A=(1)
         ST    R5,D16(R13)            PUT RETURN CODE IN REG 15 AREA
         LM    R14,R12,D12(R13)       RESTORE REGS
         LA    R15,0                  SET REG 15 TO RC=0
         BR    R14                    RETURN
         SPACE 3
*
*   EXIT4 - CAUSES THE JOB TO BE EXTENTED BY THE SPECIFIED AMOUNT OF
*           TIMER UNITS.  (CURRENTLY UNUSED.)
         SPACE 1
EXIT4    DS    0H
         LR    R1,R4                  POINT TO OUR STORAGE AREA
         FREEMAIN  R,LV=2048,A=(1)
*        ST    R5,D16(R13)            PUT RETURN CODE IN REG 15 AREA
         LM    R14,R12,D12(R13)       RESTORE REGS
         LA    R15,4                  SET REG 15 TO RC=4
         BR    R14                    RETURN
         SPACE 3
*
*   EXIT4 - CAUSES THE JOB TO BE EXTENTED BY THE SPECIFIED AMOUNT OF
*           SECONDS.
         SPACE 1
EXIT8    DS    0H
         LR    R1,R4                  POINT TO OUR STORAGE AREA
         FREEMAIN  R,LV=2048,A=(1)
*        ST    R5,D16(R13)            PUT RETURN CODE IN REG 15 AREA
         LM    R14,R12,D12(R13)       RESTORE REGS
         LA    R15,8                  SET REG 15 TO RC=8
         BR    R14                    RETURN
         TITLE 'IEFUTL   - SMF TIME LIMIT EXIT - CONSTANTS DEFINITIONS'
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
*   DEFINE DATA CONSTANTS                                             *
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
TIMEXT   DC    F'2307692'               ONE MINUTE TIME EXTENSION
SECEXT   DC    F'00000060'              SIXTY SEC TIME EXT     @ZA17320
TENMIN   DC    F'00001000'              TEN MINUTES TIME EXT
SMFRCD   DC    X'002B00000284'          SMF RECORD             @YM04803
CONS1    DC    H'0001'                                         @ZA17320
         TITLE 'IEFUTL   - SMF TIME LIMIT EXIT - TABLE DEFINITIONS    '
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
*   DEFINE TEBLES                                                     *
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
         SPACE 3
*   DON'T CARE TABLE - USED IRREGARDLESS WHAT TYPE OF TIME EXPIRATION
*   OCCURS.
         SPACE 3
NCWRTBLS DC    CL8'SCOTTS  '         START OF DON'T CANCEL JOBNAME TBL
         DC    CL8'SCOTT   '
         DC    CL8'SCOTT1  '
         DC    CL8'SCOTT2  '
         DC    CL8'********'      PATCH AREA
         DC    CL8'********'      PATCH AREA
         DC    CL8'********'      PATCH AREA
         DC    CL8'********'      PATCH AREA
         DC    CL8'********'      PATCH AREA
         DC    CL8'********'      PATCH AREA
NCWRTBLE DC    8C'*'              END OF TABLE - DO NOT CHANGE / REMOVE
         TITLE 'IEFUTL   - SMF TIME LIMIT EXIT - MESSAGE DEFINITIONS  '
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
*   DEFINE MESSAGES                                                   *
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
         SPACE 3
MSGA     DS    0F
         DC    AL2(MSGAX-MSGA)
         DC    H'0'
         DC    C'IEFUTL: ******** IS THE JOBNAME  '
MSGAX    EQU   *
         SPACE 3
MSGB     DS    0F
         DC    AL2(MSGBX-MSGB)
         DC    H'0'
         DC    C'IEFUTL: ********************************************'
MSGBX    EQU   *
         TITLE 'IEFUTL   - SMF TIME LIMIT EXIT - GOTTEN STRG. MAPPING '
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
*   DEFINE DSECT FOR JOB WAIT TIME LIMIT RECORD (TYPE X'84')          *
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
         SPACE 3
SMFDSECT DSECT
         DS    1H                       ALIGN TIME/DATE ON FWD
SMFOUT   EQU   *
HEADER   DS    6C                       HEADER
TIME     DS    1F                       TIME
DATE     DS    1F                       DATE
SYSID    DS    1F                       SYSTEM ID
JOBLOG   DS    16C                      JOB LOG
USERID   DS    8C                       USER ID
EXTCODE  DS    1C                       EXIT CODE
*  END OF SMF RECORD MAPPING
*
*  MAPPING FOR OTHER PROCESSES
*  SUCH AS A PLACE FOR MODIFING WTO MESSAGES
         DS    0F
WTOAREA  DS    200C                     EXIT CODE
         SPACE 3
*
*  THE END OF THE PROGRAM - AREN'T YOU GLAD!?!
         SPACE 1
         END
/*
//SMPCNTL  DD *
  RECEIVE S(TSM0002) .
/*
//APP    EXEC SMP,RVOL=MVSRES
//SMPCNTL  DD *
  APPLY S(TSM0002)
       ASSEM
       CHECK .
  APPLY S(TSM0002)
       ASSEM .
/*
