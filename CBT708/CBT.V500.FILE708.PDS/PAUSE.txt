//A EXEC ASMACL,PARM.C='OBJECT,NODECK,XREF',
//         PARM.L='XREF,LET,LIST'
//C.SYSIN DD *
**************************************************************
*    THIS ROUTINE READS IN THE PAUSE AMOUNT IN SECONDS AND   *
*    WAITS FOR THAT AMOUNT OF TIME                           *
*                -E.G.,-                                     *
*  //A EXEC PGM=PAUSE,PARM='0010'                            *
*                                                            *
*    WILL PAUSE FOR 10 SECONDS  (MAX=9999 SECONDS)           *
**************************************************************
PAUSE    CSECT
         LR    12,15
         USING PAUSE,12
         STM   R14,R12,12(R13)
         ST    R13,SAVEAREA+4             CHAIN SAVEAREAS
         LA    R2,SAVEAREA
         ST    R2,8(R13)
         LR    R13,R2
*
*  GET PAUSE TIME FROM PARM
*
         L     R2,0(R1)                  R2 = @PARM
         LH    R3,0(R2)                  R3 = LENGTH OF PARM
         BCTR  R3,0                            MINUS 1
         LA    R2,2(R2)                  LEAP OVER LENGTH HALFWORD
         EX    R3,MOVE                   MOVE PARM INTO HOLDING AREA
*
         PACK  PACKED,SECCHAR            TO DECIMAL
         CVB   R5,SECPACK                TO BINARY
         SR    R4,R4                     SET UP R4/R5 AS EVEN/ODD PAIR
         M     R4,=F'100'                TO TENTHS OF SECONDS
         ST    R5,SECONDS
*
*   NOW WAIT FOR THE SPECIFIED TIME TO EXPIRE
*
         STIMER WAIT,BINTVL=SECONDS
         L     R13,SAVEAREA+4
         SR    R15,R15
         RETURN  (14,12),RC=(15)
*
*   CONSTANTS AND WORK AREAS
*
         DS    0D
MOVE     MVC   CHAR(*-*),0(R2)
         DS    0D
SECCHAR  DC    CL8'00000000'
CHAR     EQU   SECCHAR+4,4
         DS    0D
SECPACK  DC    XL4'00000000'
PACKED   DS    XL4
SECONDS  DS    F
         DS    0D
SAVEAREA DS    18F
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
//L.SYSLMOD DD DSN=SYSP.LINKLIB(PAUSE),DISP=SHR
