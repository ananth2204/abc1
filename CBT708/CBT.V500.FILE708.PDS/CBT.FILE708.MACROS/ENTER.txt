         MACRO
&LABEL   ENTER &AMODE=31,                                              *
               &RMODE=ANY,                                             *
               &LEN=,                                                  *
               &EQU=YES,                                               *
               &STATS=YES,                                             *
               &BRANCH=,                                               *
               &HEADER=YES,                                            *
               &BASEREG=R12,                                           *
               &WSREG=R11,                                             *
               &EPREG=R15,                                             *
               &RETREG=R14,                                            *
               &SP=0,                                                  *
               &LOC=RES,                                               *
               &CALLRKY=YES

.**********************************************************************
.*                                                                    *
.*                             E N T E R                              *
.*                                                                    *
.*                                                                    *
.*FU T U R I T Y   S O F T W A R E   I N T E R N A T I O N A L   I N C*
.*                                                                    *
.*                                                                    *
.*                  CHIEF ARCHITECT: GARRY G. GREEN                   *
.*                 ARCHITECT EN CHEF: GARRY G. GREEN                  *
.*                                                                    *
.*                      COPYRIGHT MAY 2005 FSI                        *
.*                                                                    *
.**********************************************************************

.**********************************************************************
.*                                                                    *
.*  CHANGE ACTIVITY                                                   *
.*  $G00=20051399 GGG VERSION 1.0 BASE                                *
.*  $G01=20060730 GGG ADDED SUPPORT FOR STATS AND BRANCH PARAMETERS   *
.*  $G02=20070302 GGG ADDED SUPPORT FOR HEADER, BASEREG, WSREG, EPREG,*
.*                    AND RETREG - R14,R15,R0,AND R1 MAY NOT BE USED  *
.*                                                                    *
.**********************************************************************


.**********************************************************************
.*                                                                    *
.*  ESTABLISH STANADARD ENTRY PROLOG                                  *
.*                                                                    *
.*                                                                    *
.*    - PARAMETERS ARE:                                               *
.*                                                                    *
.*        LABEL  - USED FOR CSECT NAME AND BASE FOR BASEREG - REQ'D   *
.*        AMODE  - CSECT'S AMODE - DEFAULT IS 31                      *
.*        RMODE  - CSECT'S RMODE - DEFAULT IS ANY                     *
.*        LEN    - LEN OF VIRTUAL STORAGE TO OBTAIN - DEFAULT IS NONE *
.*                 TYPICALLY USED TO OBTAIN VIRTUAL STORAGE FOR       *
.*                 WORKING STORAGE FOR REFRESHABLE PROGRAMS; LOCATION *
.*                 OF OBTAINED STORAGE IS RETURNED IN WSREG           *
.*        EQU    - GENERATE GENERAL REGISTER EQUATES - DEFAULT IS YES *
.*        STATS  - MACRO GENERATES A *STATS RECORD USED BY FSIASMX0   *
.*                 ASSEMBLER EXIT (SEE HEADER BELOW) - DEFAULT IS YES *
.*        BRANCH - LABEL TO BRANCH TO AFTER ENTRY PROLOG - USED TO    *
.*                 BRANCH AROUND STATIC VARIABLES AND MODEL DYNAMIC   *
.*                 VARIABLES; IF NOT SPECIFIED, CONTROL IS RETURNED   *
.*                 IMMEDIATLY FOLLOWING THE ENTER MACRO               *
.*        HEADER - GENERATE MODULE HEADER YES/NO - IF NO THEN *STATS  *
.*                 WILL NOT BE GENERATED - DEFAULT IS YES             *
.*        BASEREG- REG TO BE USED AS BASE REGISTER-DEFAULT IS R12     *
.*                 MUST BE R2-R12 AND NOT THE SAME AS WSREG           *
.*        WSREG  - REG TO RETURN OBTAINED STORAGE- DEFAULT IS R11     *
.*                 MUST BE R2-R12 AND NOT THE SAME AS BASEREG         *
.*        EPREG  - REG AT ENTRY CONTAINING EP ADDR-DEFAULT IS R15     *
.*                 MUST NOT BE THE SAME AS RETREG                     *
.*        RETREG - REG AT ENTRY CONTAINING RETURN ADDR-DEFAULT IS R14 *
.*                 MUST NOT BE R0 NOR THE SAME AS EPREG               *
.*                                                                    *
.*        THE FOLLOWING PARAMETERS ARE RELEVENT ONLY IF A VALUE FOR   *
.*        LEN IS SPECIFIED; REFER TO IBM'S DOCUMENTATION FOR THE MACRO*
.*        STORAGE OBTAIN FOR A DESCRIPTION OF THESE PARAMETERS:       *
.*                                                                    *
.*          SP       - SUBPOOL FOR VIRTUAL STORAGE - DEFAULT IS 0     *
.*          LOC      - LOCATION FOR VIRTUAL STORAGE - DEFAULT IS RES  *
.*          CALLRKEY - VALUE FOR CALLRKEY - DEFAULT IS YES            *
.*                                                                    *
.*    - NOTE THAT IF LEN IS SPECIFIED, USER MUST EXPLICITLY CODE A    *
.*      STORAGE RELEASE TO RELEASE STORAGE BEFORE RETURNING TO CALLER *
.*                                                                    *
.*    - RETURN ACCOMPLISHED VIA PR - THIS RESTORES R2-R14 FROM THE    *
.*      STACK THEN BRANCHES TO REGISTER SPECIFIED IN 1ST OPERAND OF   *
.*      BAKR USED TO SAVE STATE                                       *
.*                                                                    *
.**********************************************************************

         PUSH      PRINT                     SAVE PRINTING STATUS
         PRINT     OFF,NOPRINT               SUPRESS PRINTING REG EQ
         AIF       ('&EQU' NE 'YES').NOEQU   SKIP REG EQUATES YES/NO
         YREGS     ,                         REGISTER EQUATES
.NOEQU   ANOP                                COMMON RETURN
         POP       PRINT                     RESTORE PRINTING STATUS
&LABEL   CSECT     ,                         NAME OF PROGRAM
&LABEL   AMODE     &AMODE                    SPECIFY AMODE
&LABEL   RMODE     &RMODE                    SPECIFY RMODE
         BAKR      &RETREG,0                 SAVE STATE
         LAE       &BASEREG,0(&EPREG)        LOAD CODE PAGE REGISTER
         USING     &LABEL,&BASEREG           MAP  CODE PAGE REGISTER
         AIF       ('&LEN' EQ '').NOREFR     NO WORKING STORAGE REQ
         STORAGE   OBTAIN,                   OBTAIN WORKING STORAGE    *
               LENGTH=&LEN,                  FOR THIS LENGTH           *
               SP=&SP,                       IN THIS SUBPOOL           *
               LOC=(&LOC),                   AT THIS LOCATION          *
               CALLRKY=&CALLRKY              USE CALLER'S KEY Y/N
         LR        &WSREG,R1                 COPY @ WORKING STORAGE
         EREG      R14,R1                    RESTORE REGS USED BY
*                                            STORAGE MACRO
.NOREFR  ANOP                                REFR NOT REQUIRED
         LAE       R13,@&SYSNDX.0            @ PSEUDO SAVE AREA
         AIF       ('&BRANCH' EQ '').NOBR    USE DEFAULT BRANCH LABEL
         B         &BRANCH                   GOTO USER SUPPLIED LABEL
         AGO       .BR2                      SKIP MACRO SUPPLIED LABEL
.NOBR    ANOP                                USE MACRO SUPPLIED LABEL
         B         @&SYSNDX.1                GOTO TO MAC SUPPLIED LABEL
.BR2     ANOP                                COMMON GATHERING
         AIF       ('&HEADER' EQ 'NO').NOHDR NO HEADER
         DC        C'*CSECT:'                EYECATCHER
         DC        C'&LABEL'                 MODULE NAME
         DC        C'*COMPILED:'             EYECATCHER
         DC        CL19'&SYSCLOCK'           DATE AND TIME STAMP
         ORG       *-9                       OVERLAY BLANK IN SYSCLOCK
         DC        C'*'                      CHANGE TO AN *
         ORG       ,                         STOP OVERLAYING
.NOHDR   ANOP                                DO NOT GENERATE MOD HEADER
         AIF       ('&STATS' EQ 'NO').NOSTATS DONT GENERATE *STATS LINE
*STATS                                       USED BY FSIASMX0
.*                                           BLANK LINES FOLLOWING DUE
.*                                           TO ASSEMBLER EXIT APAR


.NOSTATS ANOP                                DON'T GENERATE *STATS REC
@&SYSNDX.0 DC      F'0',C'F1SA'              R13 @ HERE INDICATING SA
*                                            USES LINKAGE STACK
         AIF       ('&BRANCH' NE '').BR1     BYPASS CREATE BRANCH LABEL
@&SYSNDX.1 DS      0H                        DEFAULT BRANCH LABEL
.BR1     ANOP                                SKIP CREATE BRANCH LABEL
         MEND
