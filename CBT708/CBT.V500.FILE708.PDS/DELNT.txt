DELNT    CSECT
DELNT    AMODE 31
DELNT    RMODE ANY
         YREGS
         STM   R14,R12,12(R13)         SAVE CALLER'S REGISTERS
         BALR  R10,R0                  ESTABLISH MODULE BASE
BASE     EQU   *
         USING *,R10                   ESTABLISH ADDRESSABILITY
         LA    R11,4095(R10)           R11 = SECOND BASE REGISTER
         LA    R11,1(R11)
         USING BASE+4096,R11
         LA    R12,4095(R11)           R12 = THIRD  BASE REGISTER
         LA    R12,1(R12)
         USING BASE+8192,R12
         ST    R13,SAVE+4              SET BACKWARD PTR
         LA    R15,SAVE                GET ADDRESS OF SAVE AREA
         ST    R15,8(R13)              SET FORWARD PTR
         LR    R13,R15                 SET REG13 TO POINT TO STANDARD
         B     CREATNT                 BRANCH AROUND CONSTANTS
***********************************************************************
         DC    C' MPFLOAD '            PROGRAM NAME IDENTIFIER
         DC    C'&SYSDATE '            ASSEMBLY DATE
         DC    C'&SYSTIME '            ASSEMBLY TIME
         DC    C'Glenn Siegel '
         DC    C'SSC Corp. '
         DC    C'631-444-5339 '
         DC    C'516-607-4005 cell '
         DC    C'GlennSiegel@optonline.net '
SAVE     DS    18F                     MY REGISTER SAVE AREA
***********************************************************************
*********************************************************************
*        NAME TOKEN SECTION                                         *
*        CHECK FOR EXISTING NT IF, FREE STORAGE AND DELETE NAME/TOK *
*        CREATE NEW NAME/TOKEN                                      *
*********************************************************************
CREATNT  EQU   *
         MODESET MODE=SUP,KEY=ZERO     AUTHORIZE FOR GETMAIN IN ECSA
         CALL  IEANTRT,(SYSLVL,NAME,TOKEN2,RETCODE)
         IF   (RETCODE,EQ,=A(IEANT_OK))
         WTO   'MPF002I - NAME/TOKEN EXISTS DELETE N/T AND STORAGE'
         L     R5,ECSA_AD2             STORE ECSA ADDRESS IN R5
         L     R9,ECSA_LE2             STORE LENGTH OF ECSA IN R9
         FREEMAIN RU,LV=(R9),A=(R5),SP=241,KEY=0
         CALL  IEANTDL,(SYSLVL,NAME,RETCODE)
         IF   (RETCODE,EQ,=A(IEANT_OK))
         WTO   'MPF003I - NAME/TOKEN AND STORAGE SUCCESSFULLY DELETED'
         ENDIF
         ENDIF
*********************************************************************
*        END OF JOB                                                 *
*********************************************************************
EOJ      EQU *
         MODESET MODE=PROB,KEY=NZERO
         L     R13,SAVE+4              RESTORE RETURN POINTER
         LM    R14,R12,12(R13)         RESTORE CALLING PGM REGISTERS
         SR    R15,R15                 CLEAR RETURN CODE
         BR    R14                     RETURN TO CALLER
*********************************************************************
*        DATA AREAS FOR NAME TOKEN                                  *
*********************************************************************
SYSLVL   DC    A(IEANT_SYSTEM_LEVEL)
NAME     DC    CL16'MPF_EXIT_TABLE'      THE NAME OF THE TOKEN
         DS    0D
TOKEN    DC    XL16'00'                  THE TOKEN
         ORG   TOKEN
ECSA_ADD DS    F                         ECSA ADDR
ECSA_LEN DS    F                         LENGTH OF ECSA
         ORG
TOKEN2   DC    XL16'00'                  THE TOKEN2
         ORG   TOKEN2
ECSA_AD2 DS    F                         ECSA ADDR
ECSA_LE2 DS    F                         LENGTH OF ECSA
         ORG
RETCODE  DC    F'0'
PERSOPT  DC    A(IEANT_PERSIST)       KEEP THE TOKEN AFTER JOB ENDS
*********************************************************************
         PRINT   GEN
         IEANTASM                NAME TOKEN SERVICE ASSEMBLER DECLARES
*********************************************************************
         END
