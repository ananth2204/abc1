         ICTL      1,71,20

***********************************************************************
*                                                                     *
*                           T S O R O U T E                           *
*                                                                     *
*F U T U R I T Y   S O F T W A R E   I N T E R N A T I O N A L   I N C*
*                                                                     *
*                   CHIEF ARCHITECT: Garry G. Green                   *
*                                                                     *
*                       COPYRIGHT JULY 2006 FSI                       *
*                                                                     *
***********************************************************************

***********************************************************************
*                                                                     *
*   FUNCTION                                                          *
*                                                                     *
*     SUBROUTINE OF MPFXTALL TO HANDLE THE TSOROUTE RULE.             *
*                                                                     *
*     IF THE WTO(R) ISSUER IS NOT A TSO ADDRESS SPACE, THIS ROUTINE   *
*     IMMEDIATLY RETURNS                                              *
*                                                                     *
*     TSOROUTE:                                                       *
*       CAUSES WTO(R) TO BE TPUT TO THE TSO USER'S TERMINAL           *
*                                                                     *
*     TSOROUTE REPLY:                                                 *
*       SAME AS TSOROUTE, BUT IN ADDITION IF MESSAGE IS A WTOR, A TGET*
*       IS ISSUED TO THE TSO USER'S TERMINAL; THE VALUE ENTERED IS    *
*       PREFIXED WITH "R nn,"  WHERE nn IS THE REPLY ID OF THE WTOR,  *
*       AND THEN THIS COMMAND IS ISSUED VIA MGCRE; THE MGCRE MACRO    *
*       SPECIFIES CMDFLAG=NOHCPY, WHICH SUPPRESSES THE ECHO OF THE    *
*       REPLY WTO FROM APPEARING ON SYSLOG/JOBLOG/HARDCOPYLOG         *
*                                                                     *
*     TSOROUTE SUPPRESS:                                              *
*       SAME AS TSOROUTE REPLY, BUT IN ADDITION, ROUTE CODE 9 (THE    *
*       SECURITY ROUTE CODE) IS ADDED TO THE ROUTE CODES FOR THE WTOR *
*       WHICH CAUSES THE REPLY VALUE ENTERED BY THE TSO USER TO APPEAR*
*       IN THE SYSLOG/JOBLOG AS:                                      *
*                                                                     *
*         IEE600I REPLY TO 07 IS;SUPPRESSED, INSTEAD OF               *
*         IEE600I REPLY TO 07 IS;SECRET PASSWORD                      *
*                                                                     *
***********************************************************************

***********************************************************************
*                                                                     *
*   CHANGE ACTIVITY                                                   *
*   $G00=20070304 GGG VERSION 1.0 BASE                                *
*                                                                     *
***********************************************************************

***********************************************************************
*                                                                     *
*   ATTRIBUTES:                                                       *
*     REFRESHABLE, AMODE(31) RMODE(ANY)                               *
*     W/S OBTAINED BELOW THE LINE DUE TO TGET/TPUT RESTRICTIONS       *
*                                                                     *
***********************************************************************

***********************************************************************
*                                                                     *
*   ENTRY/RETURN:                                                     *
*                                                                     *
*     PARM  1 @ CTXT                                                  *
*     PARM  2 @ MPFXTALL COMMAND RULE = TSOROUTE <REPLY/SUPPRESS>     *
*                                                                     *
*     RC    0 ONLY POSSIBLE RETURN CODE                               *
*                                                                     *
***********************************************************************

***********************************************************************
*                                                                     *
*                    T E C H N I C A L   N O T E S                    *
*                                                                     *
*                                                                     *
* R0,R1,R2         USED FOR COMMUNICATIONS                            *
* R13,R14,R15      USED FOR COMMUNICATIONS                            *
*                                                                     *
* R3  @ CTXT       FROM PASSED PARAMETERS                             *
*                  ACTIVE USING                                       *
* R4  @ CMDTEXT    FROM PASSED PARAMETERS                             *
*                  "TSOROUTE", "TSOROUTE REPLY" OR "TSOROUTE SUPPRESS"*
* R5  @ ASCB                                                          *
* R6  @ TSB                                                           *
* R7  = FLAG       0 = WTO                                            *
*                  1 = WTOR WITH TSOROUTE (NO PARAMETERS)             *
*                  2 = WTOR WITH TSOROUTE REPLY                       *
*                  3 = WTOR WITH TSOROUTE SUPPRESS                    *
* R8  @ CTXTATTR   ACTIVE USING                                       *
* R9  @ REPLY CMD  R nn,data-replied-to-by-tso-user                   *
* R10 @ CONSOLE ID INTERNAL CONSOLE ID                                *
* R11 @ W/S                                                           *
* R12 @ CODE PAGE  BASE REGISTER                                      *
*                                                                     *
***********************************************************************

***********************************************************************
*                                                                     *
*                     R E C O R D   L A Y O U T S                     *
*                                                                     *
***********************************************************************

         PRINT     ON,NOGEN                  SUPPRESS EXPANSION

         IHAPSA    ,                         MAP PSA
         IHAASCB   ,                         MAP ASCB
         IEZVX100  ,                         MAP CTXT

         PRINT     ON,GEN                    SHOW EXPANSION

TSOROUTE ENTER     LEN=WSLEN,                WORKING STORAGE LENGTH    *
                   LOC=BELOW,                DUE TO TGET/TPUT LIMITS.  *
                   BRANCH=START,             BRANCH AFTER MODULE HEADER*
                   STATS=OFF                 NO MODULE STATS

***********************************************************************
*                                                                     *
*                    W O R K I N G   S T O R A G E                    *
*                                                                     *
***********************************************************************

@        USING     WS,R11                    MAP WORKING STORAGE (W/S)

WS       DS        0D                        START OF W/S MODEL

MGCREMFL MGCRE     MF=L                      MGCRE LIST FORMAT

CMDTEXT  DS        0H                        COMMAND TEXT STRING
CMDTEXTL DS        H                         COMMAND TEXT'S LENGTH
CMDTEXTT DS        CL126                     COMMAND TEXT

WSLEN    EQU       *-WS                      CALCULATE LEN OF W/S

***********************************************************************
*                                                                     *
*                      S T A T I C   V A L U E S                      *
*                                                                     *
***********************************************************************

TSORMSG1 DC        C'MPF201I ONLY THIS TSO TERMINAL CAN REPLY TO FOLLOW*
                   ING WTOR-CONSOLES CAN''T REPLY'
TSORMSG2 DC        C'MPF202I REPLY VALUE YOU ENTER WILL BE SUPPRESSED F*
                   ROM CONSOLE, JOBLOG && SYSLOG'
TSORMSG3 DC        C'MPF203I FOLLOWING WTOR FOR DISPLAY ONLY - REPLY MU*
                   ST BE ENTERED FROM CONSOLE'

***********************************************************************
*                                                                     *
*                      L O G I C A L   S T A R T                      *
*                                                                     *
***********************************************************************

START    DS        0H                        LOGICAL PROGRAM START

         MVC       @.WS(WSLEN),WS            INIT WORKING STORAGE (W/S)

         L         R5,PSAAOLD-PSA(,0)        GET @ ASCB
         L         R6,ASCBTSB-ASCB(,R5)      GET @ TSB
         LTR       R6,R6                     Q. TSO ADDRESS SPACE
         BZ        RC0                       A. NO - RETURN RC0

         LM        R3,R4,0(R1)               LOAD PARM 1 AND PARM 2
         USING     CTXT,R3                   MAP CTXT
         L         R8,CTXTTXPJ               GET @ CTXTATTR
         USING     CTXTATTR,R8               MAP CTXTATTR
         LA        R7,0                      SET FLAG TO INDICATE WTO
         TM        CTXTTFB1,CTXTTFWR         Q. IS THIS A WTOR
         BNO       WTOWTOR                   A. NO - BRANCH
         LA        R7,2                      SET FLAG TO INDICATE WTOR
*                                            WITH TSOROUTE REPLY
         CLC       =C'REPLY',9(R4)           Q. REPLY TO WTOR REQ'D
         BE        WTOR                      A. YES - BRANCH
         LA        R7,3                      SET FLAG TO INDICATE WTOR
*                                            WITH TSOROUTE SUPPRESS
         CLC       =C'SUPPRESS',9(R4)        Q. SUPPRESS WTOR REPLY
         BE        WTOR                      A. YES - BRANCH
         LA        R7,1                      SET FLAG TO INDICATE WTOR
*                                            WITH TSOROUTE (NO PARMS)
         TPUT      TSORMSG3,L'TSORMSG3       TELL USER FOLLOWING WTOR
*                                            CAN'T BE REPLIED TO FROM
*                                            TSO TERMINAL-CONSOLE ONLY
         B         WTOWTOR                   GO TO WTO/WTOR COMMON
*                                            PROCESSING

WTOR     DS        0H                        REPLY/SUPPRESS SPECIFIED
         TPUT      TSORMSG1,L'TSORMSG1       TELL USER NO CONSOLE REPLY
*                                            ALLOWED;THIS TSO TERM ONLY
         CH        R7,=H'3'                  Q. SUPPRESS REPLY
         BNE       WTOWTOR                   A. NO - BRANCH
         TPUT      TSORMSG2,L'TSORMSG2       TELL USER REPLY WILL BE
*                                            SUPPRESSED FROM LOGS

WTOWTOR  DS        0H                        COMMON WTO/WTOR PROCESSING
*                                            PROCESSING
         LH        R0,CTXTTLEN               GET WTO(R) TEXT LENGTH
         TPUT      CTXTTMSG,(0)              DISPLAY WTO(R)
         CH        R7,=H'2'                  Q. WTO OR WTOR WITH
*                                            TSOROUTE (NO PARAMETERS)
         BL        RC0                       A. TRUE - RETURN RC0
         MVC       @.CMDTEXTT(5),=CL5'R 00,' INIT PREFIX TO COMMAND
         MVC       @.CMDTEXTT+2(L'CTXTRPID),CTXTRPID  MOVE IN REPLY ID
         TGET      @.CMDTEXTT+5,75           GET TSO USER'S REPLY
         LA        R1,5(,R1)                 CALC REPLY COMMAND'S LEN =
*                                            TGET LEN + R nn,  PREFIX
         STH       R1,@.CMDTEXTL             STORE CMD LEN INTO PREFIX
         OC        @.CMDTEXTT+5(75),=CL75' ' UPPER CASE REPLY SAME AS
*                                            WOULD HAVE BEEN DONE IF
*                                            ENTERED FROM THE CONSOLE
         LA        R9,@.CMDTEXT              @ MGCRE COMMAND STRING
         XR        R10,R10                   USE INTERNAL CONSOLE ID
         CH        R7,=H'3'                  Q. SUPPRESS WTOR REPLY
         BNE       NOSUPWTO                  A. NO - BRANCH
         L         R1,CTXTRCP                GET @ ROUTE CODES
         USING     CTXTROUT,R1               MAP ROUTING CODES
         OI        CTXTR002,CTXTR09          ADD SECURITY ROUTE CODE 9
*                                            REPLIES TO SECURITY WTOR'S
*                                            HAVE REPLY TEXT SUPPRESSED
         OI        CTXTRFB1,CTXTRCRC         TELL MPF ROUTE CODES
*                                            MODIFIED

NOSUPWTO DS        0H                        SUPPRESS REPLY Y/N COMMON
         MGCRE     TEXT=(R9),                ISSUE REPLY COMMAND       *
                   CONSID=(R10),             (CONT'D)                  *
                   CMDFLAG=NOHCPY,           (CONT'D)                  *
                   MF=(E,@.MGCREMFL)         (CONT'D)

RC0      DS        0H                        RETURN RC = 0
         XR        R15,R15                   SET RETURN CODE TO 0

***********************************************************************
*                                                                     *
* RELEASE WORKING STORAGE (W/S) AND RETURN                            *
*                                                                     *
***********************************************************************

         LR        R2,R15                    SAVE RETURN CODE
         STORAGE   RELEASE,                  RELEASE W/S               *
                   ADDR=(R11),               CONT'D                    *
                   LENGTH=WSLEN              CONT'D
         LR        R15,R2                    RESTORE RETURN CODE
         PR        ,                         RETURN TO CALLER

***********************************************************************
*                                                                     *
* GENERATE LITERALS AND WIND UP ASSEMBLY                              *
*                                                                     *
***********************************************************************

         LTORG     ,                         ASSEMBLE LITERALS HERE

         END       TSOROUTE                  END OF PROGRAM
