RREOJMOD  CSECT
*  THIS MODULE IS CALLED BY THE GENERATOR PROGRAM
*    TO PRINT OUT
*      TOTALS AT EOJ OF GOOD RUN  NOTE: R5 POINTS TO TOTALS
         SAVE  (2,12),T,*
         BALR  12,0
         USING *,12
         ST    13,SAVEAREA+4
         LA    13,SAVEAREA
         OPEN  (DCBPRT,(OUTPUT))
         MVC   PRTREC(133),PRTREC-1    CLEAR PRT REC
         PUT   DCBPRT,PRTREC+1    SPACE 1 LINE
         MVI   PRTREC,X'09'            CHG TO SKIP 1
         LR    6,5          R6= LHE ADR OF TOTALS
         LA    7,PRTLINES   R7= HEADER LINES
         LA    8,8(0,0)           R8=9 FOR TALLY
ML08     MVC   PRTREC+1(16),0(7)  MOVE HEADER INFO TO PRT RECORD
         UNPK   PRTREC+9(8),0(4,6) UNPACK TOTAL
         OI    PRTREC+16,X'F0'     REMOVE SIGN
         PUT   DCBPRT,PRTREC+1     PRINT TOTAL
         BCT   8,ML10           Q. PRINTED ALL TOTALS
         CLOSE (DCBPRT)
         L     13,SAVEAREA+4      *YES
         LM    14,12,12(13)       * RETURN
         LA    15,0(0,0)          *  CONTROL
         BR    14                 *
ML10     LA    6,4(6)              BUMP R6 BY 4 (POINT TO NEXT TOTAL)
         LA    7,16(7)             BUMP R7 BY 16 (POINT TO NEXT HDR)
         B     ML08
SAVEAREA DC    18F'0'
         DC    CL2' '
PRTREC   DS    CL133
PRTLINES DC    CL8'PRINT---'
         DC    CL8'0'
         DC    CL8'INP1----'
         DC    CL8'0'
         DC    CL8'INP2----'
         DC    CL8'0'
         DC    CL8'INP3----'
         DC    CL8'0'
         DC    CL8'OUT1----'
         DC    CL8'0'
         DC    CL8'OUT2----'
         DC    CL8'0'
         DC    CL8'OUT3----'
         DC    CL8'0'
         DC    CL8'CARDIN--'
         DC    CL8'0'
         DC    CL8'PUNCH---'
         DC    CL8'0'
DCBPRT   DCB   DDNAME=SYSOUT,DSORG=PS,MACRF=PM,RECFM=U,                C
               LRECL=120,BLKSIZE=120
         END
