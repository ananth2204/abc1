RRIN1ISR  CSECT
*  THIS  MODULE  IS CALLED BY THE GENERATOR PROGRAM
*                TO HANDLE READ OF ISAM DATA SETS  (BISAM)
         SAVE  (2,12),T,*
         BALR  11,0
         USING *,11
         ST    13,SAVEAREA+4
         LR    4,13
         LA    13,SAVEAREA
         ST    13,8(4)
ML01     B     ML03             1ST SWITCH
         L     13,SAVEAREA+4      *
         L     14,12(13)          *
         LA    14,6(0,14)         * POINT 14 TO KEY ADR (BASE DISP)
         MVC   ML03A+2(2),0(14)   * MOVE KEY ADR TO NEXT INST
ML03A    LA    14,*+0             * CONV BASE DISP TO ACTUAL
         ST    14,ML02+24         * STORE ACTUAL KEY ADR IN READ MACRO
         LA    13,SAVEAREA        * POIMT 13 BACK TO MY SAVEAREA
ML02     READ  DECBIN,K,DCBIN,'S','S',*+0
         WAIT  ECB=DECBIN
         TM    DECBIN+24,X'80'     Q.  REC NOT FOUND
         BZ    CHECK                  NO - ISSUE CHECK
         LA    7,NOHIT                LOAD 7 WITH ADR OF NO HIT MSG
         B     ML02A                  RETURN TO PROG
CHECK    CHECK DECBIN,DSORG=IS
         L     7,DECBIN+16         POINT REG 7 TO RECORD
ML02A    L     13,SAVEAREA+4  RELOAD HIS REGS
         LM    14,6,12(13)    BYPASS LOADING 7  SINCE IT IS
         LM    8,12,52(13)     ASSIGN TO HIS INP1 REC AREA
         LA    15,0(0,0)      SET RETURN CODE TO 0
         LA    14,12(0,14)    BUMP 14 BY 12 TO RETURN TO ADD TO REC CT
         BR    14
ML03     OPEN  (DCBIN)
         MVI   ML01+1,X'00'   NOP 1ST TIME SWITCH
         B     ML02A
ML04     L     13,SAVEAREA+4
         LM    14,12,12(13)
         LA    15,0(0,0)
         BR    14
SAVEAREA DC    18F'0'
DCBIN    DCB   DDNAME=INP1,DSORG=IS,MACRF=RSC,EODAD=ML04
NOHIT    DC    CL6'NOHIT '
         END
