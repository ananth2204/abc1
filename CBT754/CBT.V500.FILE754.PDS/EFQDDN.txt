/* REXX ----------------------------------------------------------*
 *                                                                *
 * NAME: EFQDDN                                                   *
 * TYPE: REXX function                                            *
 *                                                                *
 * WRITTEN BY:  Henrik Salminen,                                  *
 *              Spadab, Box 593, S-101 26  STOCKHOLM  SWEDEN      *
 *              Date: 1993-02-05                                  *
 *                                                                *
 * DESCRIPTION: This rexx expects one parameters; a DDNAME.       *
 *              All allocated datasets are listed using the       *
 *              LISTALC command with parameter ST (STATUS).       *
 *              The result is sysouttrapped into SYSOUTLINE       *
 *              stem variables which are then scanned for the     *
 *              specified DDNAME.                                 *
 *                                                                *
 *              If hit, return code 0 is returned, followed by    *
 *              the number of datasets allocated, and finally     *
 *              by all the dataset names in the allocated order.  *
 *                                                                *
 *              If no hit, a return code of 8 is returned.        *
 *                                                                *
 *              Note: no ISPEXEC commands are used which makes    *
 *                    this function executable even in TSO        *
 *                    native mode.                                *
 *                                                                *
 * PARAMETERS:  DDNAME                                            *
 *                                                                *
 * EXAMPLE   :                                                    *
 *     set ddname to check
       DDNAME = "sysproc"

       X     = EFQDDN(DDNAME)
       If X ¬= 8 then do
        PARSE VAR X RETCODE NUMBER DATASETS
        say "Return code:" retcode "  Number of datasets:" number
        say datasets
        end
       Else say "No files allocated to DD-name" DDNAME
 *----------------------------------------------------------------*/

  ARG PARMS
  PARSE UPPER VAR PARMS DDNAME DUMMY

  X = OUTTRAP('SYSOUTLINE.')
  "LISTALC ST"
  X = OUTTRAP('OFF')

  BLANKS  = "   "
  FOUND   = "NO"

  i = 1
  Do while FOUND = "NO" & SYSOUTLINE.0 >= i
   XDSN = SYSOUTLINE.i
   IF  LENGTH(XDSN) >= 9 then
    If POS(" "DDNAME" ",XDSN) > 0 then do
     FOUND = "YES"
     i = i-1
     XDSN = SYSOUTLINE.i
     CONCAT = "'"XDSN"'"
     End
    Else i = i+1
   Else i = i+1
   End

  If FOUND = "YES" then do
   Do while i+3 <= SYSOUTLINE.0
    i = i+3
    XDSN = SYSOUTLINE.i
    If SUBSTR(XDSN,1,3) = BLANKS then do
     i = i-1
     XDSN   = SYSOUTLINE.i
     CONCAT = CONCAT" '"XDSN"'"
     End
    Else i = SYSOUTLINE.0
    End
   End

  If FOUND ¬= "YES"  then EXIT 8
  If DUMMY = 'CLIST' then EXIT 0
  NUMBER = WORDS(CONCAT)

  EXIT 0 NUMBER CONCAT

