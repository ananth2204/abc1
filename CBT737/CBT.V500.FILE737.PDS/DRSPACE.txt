         TITLE 'DRSPACE - CALC AVAIL #TRKS IN A DASD UCB'
**********************************************************************
* DRSPACE IS USED AT THE END OF D/R TO CLEAR ALL VOLS TO LO-VALS     *
* ------------------------------------------------------------       *
*   ARGS PASSED : ARG1 = UNPACKED 4 BYTE UCB,                 (INPUT)*
*                 ARG2 = UNPACKED 6 BYTES VOLSER              (OUTPT)*
*                 ARG3 = UNPACKED 8 BYTES NUMBER (AVAIL TRKS) (OUTPT)*
*                 ARG4 = UNPACKED 8 BYTES MAX TRACKS IN UCB   (OUTPT)*
*                 ARG5 = UNPACKED 2 BYTE RETURN CODE          (OUTPT)*
*                 ARG6 = 80 BYTE ERROR MESSAGE                (OUTPT)*
* A UCB ADDR (4 BYTES) IS PASSED. THE PROGRAM CALCULATES # AVAILABLE *
* TRACKS IN THE VOLUME BY :                                          *
*  > FINDING MAX AVAILABLE TRACKS                                    *
*  > MULTIPLYING MAX AVAIL CYL * TRACKS/CYL                          *
*  > ADDING ANY REMAINING AVAIL TRACKS                               *
* THE RESULTS PASSED BACK ARE :                                      *
*     ARG2 = VOLSER                                                  *
*     ARG3 = AVAILABLE TRACKS                                        *
*     ARG4 = MAXIMUM   TRACKS                                        *
*     ARG5 = RETURN CODE                                             *
*     ARG6 = ERROR MESSAGE                                           *
**********************************************************************
DRSPACE  CSECT
         USING *,R15
         B     AROUND
         DC    CL8'DRSPACE'
         DC    CL9'&SYSDATE'
         DC    CL5'&SYSTIME'
AROUND   DS    0H
         STM   R14,R12,12(R13)
         ST    R13,SAVEAREA+4
         LA    R7,SAVEAREA
         ST    R7,8(R13)
         LA    R13,SAVEAREA
         DROP  R15
         USING SAVEAREA,R13
         USING UCB,R8
         B     BEGIN
SAVEAREA DC    18F'0'
BEGIN    DS    0H
*
******** LM    R2,R7,TSTARGS           ********** TEST ONLY **********
         LM    R2,R7,0(R1)             R2=UCB, R3=VOL, R4=AVAIL TRKS
         STM   R2,R7,ARGS              R5=MAXTRKS, R6=RC, R7=ERRMSG
         MVC   HOTUCBD(4),0(R2)        SAVE UCB - DECIMAL
         MVC   HOTUCBB(4),0(R2)        SAVE UCB - BIN
         TRT   HOTUCBD(4),TABLE1       VALIDATE UCB
         BZ    UCBVAL                  ITS OK
         BAL   R11,UCBUNVAL            ITS NOT..ERROR OUT
UCBVAL   EQU   *
         TR    HOTUCBB(4),TABLE2       CUUU (XYXYXYXY=0Y0Y0Y0Y)
         PACK  WORK3(3),HOTUCBB(5)     (YYYY??)
         MVC   HOTUNIT(2),WORK3
         BAL   R11,GETUCB              FIND COPY OF THE U C B
*****
*   GET UCB # TRKS/ CYL
*****
          DEVTYPE ,,UCBLIST=(UCBADDR,1),MF=(E,DEVTYPE2)
          LTR   R15,R15
          BZ    DEVTYPOK
          BAL   R11,DEVTPERR
DEVTYPOK  EQU   *
          LH    R1,DEVCC               R1 = MAX CYLS
          MH    R1,DEVHH               R1 = MAX CYLS * TRKS/CYL=MAX TRK
          CVD   R1,DOUB                MAKE VISIBLE
          UNPK  WORK9(9),DOUB+3(5)
          OI    WORK9+8,240            DITTO
          L     R1,ARGMAX
          MVC   0(8,R1),WORK9+1        MAX TRACKS BACK TO CALLER
*****
*   GET FREE SPACE INFO FOR UCB
*****
         LSPACE F4DSCB=DSCB4,                                          X
               UCB=(R8),                                               X
               EXPMSG=EXPMSG,                                          X
               SMF=NONE,                                               X
               MF=I
         LTR   R15,R15                 LSPACE OK ?
         BZ    LSPACEOK                YES
         BAL   R11,LSPACERR            NO...ERROR OUT
LSPACEOK EQU   *
         PACK  CYLPAK(6),FREECYLS(6)   FREE CYLS
         PACK  TRKPAK(4),FREETRKS(6)   FREE TRKS
         LH    R1,DEVHH                R1=#TRKS/CYL
         CVD   R1,DOUB                 TRKS/CYL IN DECIMAL
         MP    CYLPAK(6),DOUB+6(2)     FREE CYLS * TRKS/CYL = FREE TRKS
         AP    CYLPAK(6),TRKPAK(4)     TACK ON ANY SUB-CYL FREE TRACKS
         UNPK  TOTTRKS(9),CYLPAK+1(5)  MAKE READABLE
         OI    TOTTRKS+8,X'F0'         MAKE PRETTY
         L     R1,ARGTRKS              RETURN AVAIL TRACKS TO CALLER
         MVC   0(8,R1),TOTTRKS+1       *
         B     EXIT0                   GOBACK - NORMALLY
         EJECT
***********************************************************************
*                      S U B R O U T I N E S                          *
***********************************************************************
         SPACE
*****
* FIND D/R UCB IN THE SYSTEM
*****
         SPACE
         DC    CL8'GETUCB',F'0'
GETUCB   ST    R11,*-4
         L     R8,=A(UCBAREA)          R8=A(UCB)
UCBLOOP  EQU   *
         PUSH  PRINT
         PRINT GEN
         UCBSCAN COPY,                                                 X
               WORKAREA=UCBWORK,                                       X
               UCBAREA=UCBAREA,                                        X
               DEVN=0,                                                 X
               DYNAMIC=YES,                                            X
               RANGE=ALL,                                              X
               DEVCLASS=DASD
         POP   PRINT
         LTR   R15,R15                 END OF DASD DEVICES ?
         BNZ   UCBNFND                 YES...
         CLC   UCBCHAN(2),HOTUNIT      IS THIS THE RIGHT UCB ?
         BE    UCBHIT                  YES...
         B     UCBLOOP                 NO..GET NEXT ONE
UCBNFND  EQU   *
         BAL   R11,UCBNF               UCB NOT FOUND .. ERROR OUT
UCBHIT   EQU   *
         L     R1,ARGVOL
         MVC   0(6,R1),UCBVOLI         VOLSER TO USER
         L     R11,GETUCB-4
         BALR  R11,R11
*****
* EXIT0 - NORMAL RC=0 RETURN
*****
EXIT0    EQU   *
         XR    R15,R15
         B     EXIT
*****
* EXIT8 - ABNORMAL RC=8 RETURN
*****
EXIT8    EQU   *
         LA    R15,8
         B     EXIT
*****
* EXIT - COMMON EXIT POINT
*****
EXIT     EQU   *
         CVD   R15,DOUB
         UNPK  MYRC(3),DOUB+6(2)
         OI    MYRC+2,X'F0'
         L     R1,ARGRC
         MVC   0(2,R1),MYRC+1
         L     R13,SAVEAREA+4
         RETURN (14,12),RC=(15)
         SPACE 3
*****
* UCBUNVAL - INVALID UCB PASSED TO DRSPACE
*****
         DC    CL8'UCBUNVAL',F'0'
UCBUNVAL ST    R11,*-4
         MVC   UNVALMSG+4(4),HOTUCBD
         L     R1,ARGMSG
         MVC   0(L'UNVALMSG,R1),UNVALMSG
         B     EXIT8
         SPACE 3
*****
* UCBNF - PASSED UCB NOT FOUND
*****
         DC    CL8'UCBNF',F'0'
UCBNF    ST    R11,*-4
         MVC   UCBNFMSG+4(4),HOTUCBD
         L     R1,ARGMSG
         MVC   0(L'UCBNFMSG,R1),UCBNFMSG
         B     EXIT8
         SPACE 3
*****
* DEVTPERR - ERROR ISSUING DEVTYPE MACRO
*****
         DC    CL8'DEVTPERR',F'0'
DEVTPERR ST    R11,*-4
         MVC   DEVTPMSG+4(4),HOTUCBD
         CVD   R15,DOUB
         UNPK  WORK7(7),DOUB+5(4)
         MVZ   WORK7(7),=7X'00'
         TR    WORK7(7),TABLE0
         MVC   DEVTPMSG+26(4),WORK7+1      RETURN CODE
         CVD   R0,DOUB
         UNPK  WORK7(7),DOUB+5(4)
         MVZ   WORK7(7),=7X'00'
         TR    WORK7(7),TABLE0
         MVC   DEVTPMSG+34(4),WORK7+1      REASON CODE
         MVC   0(L'DEVTPMSG,R5),DEVTPMSG
         L     R1,ARGMSG
         MVC   0(L'DEVTPMSG,R1),DEVTPMSG   ERMSG TO CALLER
         B     EXIT8
*****
* LSPACERR - ERROR ISSUING LSPACE MACRO
*****
         DC    CL8'LSPACERR',F'0'
LSPACERR ST    R11,*-4
         MVC   LSPACMSG+4(4),HOTUCBD
         CVD   R15,DOUB
         UNPK  WORK7(7),DOUB+5(4)
         MVZ   WORK7(7),=7X'00'
         TR    WORK7(7),TABLE0
         MVC   LSPACMSG+25(4),WORK7+1      RETURN CODE
         ST    R0,WORK0
         UNPK  WORK9(9),WORK0(5)
         MVZ   WORK9(8),=8X'00'
         TR    WORK9(8),TABLE0
         MVC   LSPACMSG+33(8),WORK9        REASON CODE
         L     R1,ARGMSG
         MVC   0(L'LSPACMSG,R1),LSPACMSG
         B     EXIT8
         SPACE 5
         LTORG
         EJECT
***********************************************************************
*                      W O R K I N G   S T O R A G E                  *
***********************************************************************
         SPACE 3
HOTUCBD  DC    CL4'????'
HOTUCBB  DC    CL4'????'
HOTUNIT  DC    H'0'
DOUB     DS    D
WORK0    DS    F
         DC    X'0F'         KEEP THIS HERE
WORK3    DS    CL3
WORK7    DS    CL7
WORK9    DS    CL9
CYLPAK   DS    6P
TRKPAK   DS    4P
TOTTRKS  DS    9P            TOTAL #TRACKS THIS DEVICE
MYRC     DS    CL3
ARGS     DS    6F
         ORG   ARGS
ARGUCB   DS    F             A(CALLER UCB)
ARGVOL   DS    F             A(CALLER UCB VOLSER)
ARGTRKS  DS    F             A(CALLER TOT TRKS)
ARGMAX   DS    F             A(CALLER MAX TRKS)
ARGRC    DS    F             A(CALLER RC)
ARGMSG   DS    F             A(CALLER ERRMSG)
UCBADDR  DC    A(UCBAREA)
UCBAREA  DC    12F'0'        UCB COPY
UCBWORK  DC    XL100'00'
UCBNAME4 DS    CL5
DEVTYPE2 DEVTYPE ,(DEVINFO,6*4),UCBLIST=YES,MF=L
DEVINFO  DC    6F'0'
         ORG   DEVINFO+8
DEVCC    DS    H             # PHYSICAL CYLINDERS
DEVHH    DS    H             # TRACKS / CYLINDER
         ORG
DSCB4    DC    XL96'00'
*****
* FREE SPACE INFO
*****
EXPMSG   DC    XL40'00'
         ORG   EXPMSG
         DS    CL6      SPACE=
FREECYLS DS    CL6      CCCCCC
         DS    C        ,
FREETRKS DS    CL6      HHHHHH
         DS    C        ,
FREEXTNT DS    CL6      EEEEEE
         DS    C        /
LARGCYL  DS    CL6      CCCCCC
         DS    C        ,
LARGTRK  DS    CL6      HHHHHH
*
***** ERROR MESSAGES *****
UNVALMSG DC    C'UCB XXXX VALUE IS INVALID'
UCBNFMSG DC    C'UCB XXXX NOT FOUND IN SYSTEM'
DEVTPMSG DC    C'UCB XXXX DEVTYPE ERROR,RC=XXXX,RS=XXXX'
LSPACMSG DC    C'UCB XXXX LSPACE ERROR,RC=XXXX,RS=XXXXXXXX'
***************************
* TEST ONLY ***************
TSTARGS  DC    A(TSTUCB,TSTVOL,TSTATRKS,TSTMTRKS,TSTRC,TSTMSG)
TSTUCB   DC    C'3107'
TSTVOL   DC    C'VOL=??'
TSTATRKS DC    C'AVAIL=??'
TSTMTRKS DC    C'MAXNO=??'
TSTRC    DC    C'??'
TSTMSG   DC    80C' '
***************************
TABLE0   DC    C'0123456789ABCDEF'
*
TABLE1   DS    0CL256
         DC    193X'FF'
         DC    6X'00'        A-F
         DC    41X'FF'
         DC    10X'00'       0-9
         DC    6X'FF'
*
TABLE2   DS    0CL256
         DC    193X'FF'
         DC    XL6'0A0B0C0D0E0F'             A-F
         DC    41X'FF'
         DC    XL10'00010203040506070809'    0-9
         DC    6X'FF'
         SPACE 3
***********************************************************************
*                      D S E C T S                                    *
***********************************************************************
         SPACE 3
UCB      DSECT
         IEFUCBOB
***
*   REGISTER EQUATES
***
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
