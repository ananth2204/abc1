  DATA SUBMIT;
   RPTNODE = SYMGET('SYSPARM');                       /* UserMod */
    TITLE;
    INFILE NDMX0001 RECFM=VB;                         /* 1363862 */
    INPUT RECTYPE $ 3-4 @;
* ********************************************************************
* Licensed Materials - Property of IBM
*
* 5655-X01 IBM(R) Sterling Connect:Direct(R) for z/OS(R)
*
* (C) Copyright IBM Corp. 1982, 2011 All Rights Reserved
*
* US Government Users Restricted Rights - Use, duplication or
* disclosure restricted by GSA ADP Schedule Contract with IBM Corp.
* ********************************************************************
* ONLY WANT SUBMIT W/IN PROC TERMINATION RECORDS;
      IF RECTYPE = 'SW' THEN DO;        /* PSSRTYPE    */
         INPUT  @17 STRDTE PD4.         /* PSSSDATE    */
                @21 CC $4.              /* PSSSCC      */
                @25 MSGID $8.           /* PSSMSGID    */
                @33 PROCNAME $8.        /* PSSPRCNM      1363862*/
                @41 PROCNUM IB4.        /* PSSPRCNO    */
                @45 STEPNAME $8.        /* PSSSTEP     */
                @69 UID $8.             /* PSSUID 1-8  */
                @77 UID56 $56.          /* PSSUID 9-64 */
                @133 MYNODE $16.        /* PSSPNODE    */
                @149 OTHRNODE $16.      /* PSSSNODE    */
                @165 THISNODE $1.       /* PSSNODE     */
                @166 SUBIND $1. @;      /* PSSFROM     */
*        ONLY WANT RECORDS FOR A SPECIFIC NODE NAME;  /* UserMod */
      IF (OTHRNODE ¬= RPTNODE) THEN DELETE;           /* UserMod */
*        ASSIGN PNODE AND SNODE;
         IF THISNODE = 'P' THEN DO;
            PNODE = MYNODE;
            SNODE = OTHRNODE;
            END;
         ELSE DO;
            PNODE = OTHRNODE;
            SNODE = MYNODE;
            END;
*        ASSIGN NODE UPON WHICH PROCESS IS SUBMITTED;
         IF SUBIND = 'P' THEN
            SUBNODE = PNODE;
         ELSE
            SUBNODE = SNODE;
         DATE = DATEJUL(STRDTE);     /* CONVERT JULDATE TO SAS    */
*        GO AFTER DATASET NAME CONTAINING PROCESS;
         INPUT @323 DSP2DSN IB2. @;   /* PSSDSN#  */
         IF DSP2DSN ¬= 0 THEN DO;
           DSP2DSN + 1;
           INPUT @DSP2DSN DSNLEN IB2.
                 DSNAME $VARYING200.DSNLEN;
         END;
*        GET RID OF UNNECESSARY VARIABLES;
         DROP RECTYPE OTHRNODE THISNODE STRDTE SUBIND;
*        ADD OBSERVATION TO SAS DATASET BEING CREATED;
         OUTPUT;
      END;
      FORMAT DATE MMDDYY10.;
      FORMAT CC $HEX8.;
  PROC SORT;
    BY SNODE UID PROCNUM;
  DATA FINALSB;
    SET SUBMIT;
    FILE FT20F001 N=PS LINESLEFT=L;
    IF _N_ = 1 THEN DO;
       PUT @45 'IBM Sterling Connect:Direct for z/OS Submit within a Pro
cess Report';
       PUT @50 'NODE = ' MYNODE;
       PUT /;
       PUT @3 'USERID      PROC #   PROC NAME    STEP NAME    SUBMIT INF
ORMATION';
       PUT @3 '******      ******   *********    *********    **********
************************************************************************
*';
       PUT /;
    END;
    IF L < 8 THEN DO;
       PUT _PAGE_;
       PUT @45 'IBM Sterling Connect:Direct for z/OS Submit within a Pro
cess Report';
       PUT @50 'NODE = ' MYNODE;
       PUT /;
       PUT @3 'USERID      PROC #   PROC NAME    STEP NAME    SUBMIT INF
ORMATION';
       PUT @3 '******      ******   *********    *********    **********
************************************************************************
*';
       PUT /;
    END;
* WRITE TO OUTPUT;
    PUT @3 UID @15 PROCNUM @24 PROCNAME @37 STEPNAME
        @50 DATE @61 'PNODE = ' PNODE @87 'SNODE = ' SNODE;
    PUT @50 'COMPLETION CODE = ' CC @87 'MSG ID = ' MSGID;
    INFILE DMMSGFIL VSAM KEY=MSGID;
    INPUT @23 SHTXTLEN IB2. @;
    SHTXTLEN = SHTXTLEN - 2;
    INPUT SHTEXT $VARYING200.SHTXTLEN;
    UPSHTEXT = UPCASE(SHTEXT);
    PUT @50 UPSHTEXT;
    PUT @50 '===========================================================
========================';
    IF DSNAME = ' ' THEN
      PUT @50 'DSNAME NOT AVAILABLE ';
    ELSE
      PUT @50 'DSNAME ==> ' DSNAME;
    IF CC = '00000000'X THEN
      PUT @50 'PROCESS SUBMITTED ON ' SUBNODE;
    ELSE
      PUT @50 'INTENDED NODE FOR PROCESS SUBMISSION IS ' SUBNODE;
   PUT /@3 '------------------------------------------------------------
----------------------------------------------------------------------';
