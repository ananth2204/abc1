* ********************************************************************
* Licensed Materials - Property of IBM
*
* 5655-X01 Copyright IBM Corp. 2011
*
* All Rights Reserved
*
* US Government Users Restricted Rights - Use, duplication or
* disclosure restricted by GSA ADP Schedule Contract with IBM Corp.
* ********************************************************************
TSTA3390 TITLE '- SAMPLE ALLOCATION EXIT'                       46497
TSTA3390 START ,                                                46497
         DGA$SAMP ,                                            DGA_STD
         COPY  DMGBLS                                           46497
         DMOS                                                   46497
         LCLC  &LASTPTF,&UID                                    1348110
&LASTPTF SETC  'R000000'                                        1348110
&UID     SETC  ''       USE UP TO 8 CHARACTERS FOR LOCAL MOD    1348110
**********************************************************************
*                                                                    *
*    Connect:Direct (tm)                                             *
*    COPYRIGHT (C) 1983, 1998  Sterling Commerce, Inc.               *
*                                                                    *
*    This material is the confidential trade secret and proprietary  *
*    information of Sterling Commerce, Inc. and/or its subsidiaries. *
*    It may not be reproduced, used, sold, or transferred to any     *
*    third party without the prior written consent of Sterling       *
*    Commerce, Inc.  All rights reserved.                            *
*                                                                    *
*    Use, duplication, or disclosure by the Government is subject    *
*    to restrictions as set forth in subparagraph (c)(1)(ii) of      *
*    the Rights in Technical Data and Computer Software clause at    *
*    FAR 52.227*7013.                                                *
*                                                                    *
**********************************************************************
*                                                                    *
*                       Connect:Direct PROLOG                        *
*                                                                    *
* FUNCTION:       Connect:Direct ALLOCATION EXIT                     *
*                                                                    *
* MODULE NAME:    TSTA3390   VER: 01 REL: 04 MOD: 00                 *
*                                                                    *
* DATE WRITTEN:   06/30/89                                           *
*                                                                    *
* PROGRAMMER:     JAY B. MORRIS                                      *
*                                                                    *
* ATTRIBUTES:     LINK WITH RENT, REUS                               *
*                                                                    *
* ENTRY POINT 1:  TSTA3390                                           *
*                                                                    *
*  INPUT:         R1 -> VSAMPL                                       *
*                                                                    *
*  OUTPUT:        MODIFIED DDESCR WITH GENERIC UNIT TYPE FOR 3390    *
*                                                                    *
* DESCRIPTION:    TSTA3390 PERFORMS THE FOLLOWING MAIN FUNCTION:     *
*                                                                    *
*                 1. CHECKS SOURCE DATASET DESCRIPTOR FOR UNIT=3390. *
*                    IF 3390, CHANGES DESTINATION DATASET DESCRIPTOR *
*                    BLOCK UNITTYPE FIELD TO SYSDA.  IF THE SOURCE   *
*                    IS NOT 3390, IT IS NOT CHANGED.  THE DESTINATION*
*                    UNIT IS NOT CHECKED.  IF THE USER SPECIFIES A   *
*                    DESTINATION UNIT OF 3390, IT WILL NOT BE CHANGED*
*                    SEE DESCRIPTION IN CODE.                        *
*                                                                    *
* CALLED BY:      DMVSOPEN                                           *
*                                                                    *
* CONTROL BLOCKS: VSAMPL                                             *
*                 TCQSH                                              *
*                 SDESCR                                             *
*                 DDESCR                                             *
*                                                                    *
* REGISTERS:      R0     RESERVED FOR MACRO USE                      *
*                 R1     RESERVED FOR MACRO USE                      *
*                 R2     MODULE WORK REGISTER                        *
*                 R3     BASE TO VSAMPL                              *
*                 R4     BASE TO TCQSH                               *
*                 R5     BASE TO SDESCR                              *
*                 R6     BASE TO DDESCR                              *
*                 R7     MODULE WORK REGISTER                        *
*                 R8     MODULE WORK REGISTER                        *
*                 R9     MODULE WORK REGISTER                        *
*                 R10    DISPLAY LINK REGISTER               TRAC*API*
*                 R11    MODULE WORK REGISTER                        *
*                 R12    FIRST BASE REGISTER                         *
*                 R13    CURRENT SAVE AREA                           *
*                 R14    RETURN ADDRESS                              *
*                 R15    BRANCH REGISTER AND RETURN CODE             *
*                                                                    *
**********************************************************************
         EJECT
         DMVSAMPL DSECT=YES
         EJECT
         DMTCQSH  DSECT=YES
         EJECT
         DMSDESCR DSECT=YES
         EJECT
         DMDDESCR DSECT=YES
         EJECT
         DMTCA  DSECT=YES              MAP TASK CONTROL AREA            AJC06258
         EJECT                                                          AJC06258
         DMTCQE DSECT=YES              MAP TASK CONTROL QUEUE HEADER    AJC06258
         EJECT                                                          AJC06258
*---------------------------------------------------------------------*
*        LOCAL VARIABLE SECTION                                       *
*---------------------------------------------------------------------*
DSECT    DSECT
SAVEAREA DS    10D         PROVIDED SAVEAREA
WKAREA   DS    D           DISPLAY MACRO WORKAREA
WKAREA2  DS    8D          DISPLAY MACRO WORKAREA
 DEBUG LINKAGE=(R10,DEBUG1,DEBUG2)                             TRAC*API
DEBUG1   DS    D           DISPLAY MACRO WORKAREA
DEBUG2   DS    D           DISPLAY MACRO WORKAREA
*
WTO      WTO   'CDPAL01X UNIT= changed for PROCESS 12345678( ##,###) SU*AJC06258
               BMITTED BY Snode=NNNNNNNNNNNNNNNN',ROUTCDE=(11),MF=L     AJC06258
WTONAME  EQU   WTO+4+35,8                                               AJC06258
WTOPNUM  EQU   WTO+4+45,7                                               AJC06258
WTOCOMA  EQU   WTO+4+47,1                                               AJC06258
WTOPNU3  EQU   WTO+4+48,3                                               AJC06258
WTONODE  EQU   WTO+4+72,16                                              AJC06258
         DS    F                                                        AJC06258
PACDEC   DS    D                                                        AJC06258
ZONDEC   DS    D                                                        AJC06258
DSECT#   EQU   *-DSECT
         EJECT
*---------------------------------------------------------------------*
*     MAINLINE
*---------------------------------------------------------------------*
&DMCSECT SCENTER BASE=(R12),GETMAIN=DSECT#,                     1348110X
               AMODE=ANY,RMODE=24,LASTPTF=&LASTPTF,UID=&UID     1348110
&DMCSECT SCSTART WKAREA=WKAREA,WKAREA2=WKAREA2                  1348110
         USING DSECT,R13
*---------------------------------------------------------------------*
* REG 1 CONTAINS THE ADDRESS OF THE PARAMETER LIST COMING IN. THE     *
* PARAMETER LIST PASSED TO THE ALLOCATION EXIT CONTAINS THE VSAMPL.   *
*---------------------------------------------------------------------*
         LR    R2,R1                   GET PARM LIST COMING IN
         L     R3,0(,R2)               SAVE ADDR AWAY
         USING VSAMPL,R3               USE VSAMPL
*---------------------------------------------------------------------*
* THE FIELD IN THE VSAMPL CALLED VSCCBADR IS THE POINTER TO THE       *
* COPY CONTROL BLOCK STATEMENT HEADER.                                *
*---------------------------------------------------------------------*
         L     R4,VSCCBADR             PT AT TCQSH
         USING TCQSH,R4                USE TCQSH
*---------------------------------------------------------------------*
* THE FIELD IN THE TCQSH CALLED TSHCBHLN CONTAINS THE LENGTH OF THE   *
* TCQSH WHICH IS FOLLOWED BY THE SOURCE DATASET DESCRIPTOR CONTROL    *
* BLOCK (DMSDESCR). ADD THAT LENGTH TO THE START OF THE TCQSH TO GET  *
* THE ADDRESS OF THE SOURCE DESCRIPTOR.                               *
*---------------------------------------------------------------------*
         LH    R5,TSHCBHLN             LOAD LEN OF TCQSH AND
         AR    R5,R4                   ADD START OF TCQSH TO GET SRC
         USING SDESCR,R5               USE SDESCR
*---------------------------------------------------------------------*
* THE FIELD IN THE SDESCR CALLED S1SVSLNG CONTAINS THE LENGTH OF THE  *
* SDESCR WHICH IS FOLLOWED BY THE DESTINATION DATASET DESCRIPTOR      *
* CONTROL BLOCK (DMDDESCR). ADD THAT LENGTH TO THE START OF THE SDESCR*
* TO GET THE ADDRESS OF THE DESTINATION DESCRIPTOR.                   *
*---------------------------------------------------------------------*
         LH    R6,S1SVSLNG             LOAD LEN OF SDESCR AND
         AR    R6,R5                   ADD START OF SDESCR TO GET DEST
         USING DDESCR,R6               USE DDESCR
*---------------------------------------------------------------------*
* HERE IS AN EXAMPLE OF HOW TO MODIFY THE DESTINATION UNIT IF THE     *
* SOURCE UNIT IS A 3390.  THE DESTINATION UNIT IS NOT CHECKED IN THIS *
* EXAMPLE, SO IF A USER WERE TO SPECIFY A DESTINATION UNIT OF 3390,   *
* IT WOULD NOT BE CHANGED.  OF COURSE, IT WOULD BE VERY EASY TO ADD   *
* THIS CHECK IF IT WERE NECESSARY.                                    *
*                                                                     *
* THE FOLLOWING DIAGRAM DESCRIBES THE SITUATION THAT THIS SAMPLE EXIT *
* WILL HANDLE.                                                        *
*                                                                     *
*   SOURCE NODE                       DESTINATION NODE                *
* +--------------+                    +--------------+                *
* |              |                    |              |                *
* |  SYSTEM A    | SUNIT=3390         |   SYSTEM B   | SUNIT=3390     *
* |              |             -----> |              |                *
* |    3390      | DUNIT=____         |    NO 3390   | DUNIT=SYSDA    *
* |              |                    |              |                *
* |   NO EXIT    |                    |  THIS EXIT   |                *
* +--------------+                    +--------------+                *
*                                                                     *
*                                                                     *
* THE FOLLOWING DIAGRAM DESCRIBES THE SITUATION THAT THIS SAMPLE EXIT *
* WILL NOT HANDLE (BUT EASILY COULD IF SO DESIRED).                   *
*                                                                     *
*   SOURCE NODE                       DESTINATION NODE                *
* +--------------+                    +--------------+                *
* |              |                    |              |                *
* |  SYSTEM A    | SUNIT=3380         |   SYSTEM B   | SUNIT=3380     *
* |              |             -----> |              |                *
* |    3390      | DUNIT=3390         |    NO 3390   | DUNIT=3390     *
* |              |                    |              |                *
* |   NO EXIT    |                    |  THIS EXIT   |                *
* +--------------+                    +--------------+                *
*                                                                     *
*---------------------------------------------------------------------*
         TM    D1FLAG8,D1VOLSER        HAS USER SPEC. EXPLICIT VOLSER   AJC06258
         BO    OKUNIT               YES, THEN HONOR THAT REQUEST I5453
         CLC   D1DDISP1,=C' '          Was the DISP1 field specified ?  AJC06258
         BE    CHKUNIT                 NO, the default is DISP=NEW      AJC06258
         CLC   D1DDISP1,=C'N'          Is the first DISP field 'New'?   AJC06258
         BNE   OKUNIT                  NO, UNIT IS OK ASIS              AJC06258
CHKUNIT  DS    0H                                                       AJC06258
         CLC   D1UNIT(4),=C'    '      Was the UNIT field specified ?   AJC06258
         BE    CHKLAST                 NO, the default is ALL DASD      AJC06258
         CLC   D1UNIT,=C'SYSALLDA'     SYSALLDA causes the same issue   AJC06258
         BE    CHKLAST                 NO, the default is ALL DASD      AJC06258
         CLC   D1UNIT(4),=C'3390'      IS UNIT TYPE 3390 ?              AJC06258
         BNE   OKUNIT                  NO, UNIT IS OK ASIS
CHKLAST  DS    0H                                                       AJC06258
         DISPLAY (C'&DMCSECT: Found DISP=NEW+UNIT=3390'),(C),USRINFO    AJC06258
         MVC   D1UNIT,=C'DISK80  '     YES, CHG DEST UNIT TO DISK80     AJC06258
         OI    D1FLAG7,D1GRPTYP             AND SHOW UNIT AS CHANGED
WTOMSG   DS    0H                                                       AJC06258
*---------------------------------------------------------------------* AJC06258
* THE FIELD IN THE VSAMPL CALLED VSTCAADR IS THE POINTER TO THE       * AJC06258
* TASK CONTROL AREA (TCA) FOR THIS TASK.                              * AJC06258
*---------------------------------------------------------------------* AJC06258
         L     R7,VSTCAADR             Point at the TCA                 AJC06258
         USING TCA,R7                  MAP the TCA                      AJC06258
*---------------------------------------------------------------------* AJC06258
* THE FIELD IN THE TCA CALLED TATCQE IS THE POINTER TO THE TASK       * AJC06258
* CONTROL QUEUE ELEMENT (TCQE) FOR THIS TASK.                         * AJC06258
*---------------------------------------------------------------------* AJC06258
         L     R9,TATCQE               Point at the TCQE                AJC06258
         USING TCQE,R9                 MAP the TCQE                     AJC06258
*---------------------------------------------------------------------* AJC06258
*        Issue WTO to provide an audit trail for the changed process  * AJC06258
*---------------------------------------------------------------------* AJC06258
         MVC   WTO(WTO#),WTOMODEL                                       AJC06258
         MVC   WTONAME,TQPROCNM    MOVE IN PROCESS NAME                 AJC06258
         L     R8,TQPROCNO         LOAD THE PROCESS NUMBER              AJC06258
         DISPLAY (C'&DMCSECT: PROC NO     = ',(R8)),(C,X),USRINFO       AJC06258
         CVD   R8,PACDEC           CONVERT TO PACKED DECIMAL            AJC06258
         DISPLAY (C'&DMCSECT: PACDEC      = ',PACDEC),(C,X),USRINFO     AJC06258
         UNPK  ZONDEC,PACDEC+4(4)  Unpk the packed decimal number       AJC06258
         OI    ZONDEC+7,X'F0'      Take care of the sign field          AJC06258
         DISPLAY (C'&DMCSECT: ZONDEC(1)   = ',ZONDEC),(C,X),USRINFO     AJC06258
         MVC   WTOPNU3,ZONDEC+5    MOVE IN THE LOW ORDER DIGITS         AJC06258
         CLI   ZONDEC+3,X'F0'      IS PROCESS NUMBER GT 10000           AJC06258
         BE    NEXTCHK                                                  AJC06258
         MVI   WTOCOMA,C','        MOVE IN THE COMMA                    AJC06258
         MVC   WTOPNUM(2),ZONDEC+3 MOVE IN THE TEN THOUSAND DIGIT       AJC06258
NEXTCHK  DS    0H                                                       AJC06258
         CLI   ZONDEC+4,X'F0'      IS PROCESS NUMBER GT 1000            AJC06258
         BE    PROC999                                                  AJC06258
         MVI   WTOCOMA,C','        MOVE IN THE COMMA                    AJC06258
         MVC   WTOPNUM+1(1),ZONDEC+4 MOVE IN THE THOUSAND DIGIT         AJC06258
PROC999  DS    0H                                                       AJC06258
         LA    R8,6                FILL IN PROCESS NUMBER               AJC06258
         DISPLAY (C'&DMCSECT: ZONDEC(2)   = ',ZONDEC),(C,X),USRINFO     AJC06258
         DISPLAY (C'&DMCSECT: WTOPNUM     = ',WTOPNUM),(C,X),          *AJC06258
               LEN=(,(R8)),USRINFO                                      AJC06258
         MVC   WTONODE,TQSNODE     MOVE IN THE SUBMITTER NODE NAME      AJC06258
         WTO   MF=(E,WTO)                                               AJC06258
OKUNIT   DS    0H
***********************************************************************
*        RETURN LOGIC                                                 *
***********************************************************************
         L     R15,VSRTNCD             PICK UP RETURN CODE
*     SCRETURN WILL FREE SCENTER STORAGE AND RETURN TO CALLER   1348110
         SCRETURN RC=(R15)                                      1348110
         EJECT
*---------------------------------------------------------------------* AJC06258
*   Define Data Constants                                             * AJC06258
*---------------------------------------------------------------------* AJC06258
         DS    0F                                                       AJC06258
WTOMODEL WTO   'CDPAL01X UNIT= changed for PROCESS 12345678(       ) SU*AJC06258
               BMITTED BY Snode=NNNNNNNNNNNNNNNN',ROUTCDE=(11),MF=L     AJC06258
WTO#     EQU   *-WTOMODEL                                               AJC06258
**********************************************************************
*                                                                    *
* MAINTENANCE:                                                       *
*                                                                    *
* P46497  06/29/98 CLEANUP 'USING' FOR ASMA90 AND AMODE/RMODE        *
*         JEL                                                        *
*                                                                    *
* 1348110 06/09/06 Use SCENTER to create standard eye catcher.       *
*         JEL                                                        *
*                                                                    *
* TRAC*API 08/22/2006 Leave R11 (TCA) register alone.                *
*         TOP                                                        *
*                                                                    *
* AJC06258 09/15/2006 Check for destination D/S with DISP=NEW and    *
*          UNIT=3390 without a VOLSER and iff, then change it to     *
*          UNIT=DISK80. If a process is changed, issue a WTO to the  *
*          log. Search on AJC06258 (77) occurrences on COL 73-80.    *
*                                                                    *
* AJC06333 11/29/2006 Testing version of Submit exit. This is the    *
*          same code, but with the DISPLAY macros enabled.           *
*                                                                    *
* AJC08007 01/07/2008 Change the DISPLAYs to use the USRINFO DDNAME  *
*          that was added in C:D V4.6.00.                            *
*                                                                    *
* DGA_STD 01FEB2011  Change to standard SMP/E naming per IBM naming  *
*         SThompson  standards.                                      *
*                                                                    *
**********************************************************************
         SCEND &DMCSECT                                         1348110
