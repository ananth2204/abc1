  DATA RUNJOB;
   RPTNODE = SYMGET('SYSPARM');                       /* UserMod */
    TITLE;
    INFILE NDMX0001 RECFM=VB;                         /* 1363862 */
    INPUT RECTYPE $ 3-4 @;
* ********************************************************************
* Licensed Materials - Property of IBM
*
* 5655-X01 IBM(R) Sterling Connect:Direct(R) for z/OS(R)
*
* (C) Copyright IBM Corp. 1982, 2011 All Rights Reserved
*
* US Government Users Restricted Rights - Use, duplication or
* disclosure restricted by GSA ADP Schedule Contract with IBM Corp.
* ********************************************************************
*     ONLY WANT RUNJOB TERMINATION RECORDS;
      IF RECTYPE = 'RJ' THEN DO;
         INPUT  @17 STRDTE PD4.        /* RJSDATE   */
                @21 CC $4.             /* RJSCC     */
                @25 MSGID $8.          /* RJMSGID   */
                @33 PROCNAME $         /* RJPROCNM  */
                @41 PROCNUM IB4.       /* RJPROCNO  */
                @45 STEPNAME $8.       /* RJSTEP    */
                @69 UID $8.            /* RJUID 1-8 */
                @77 UID56 $56.         /* RJUID 9-64 */
                @133 MYNODE $16.       /* RJPNODE   */
                @149 OTHRNODE $16.     /* RJSNODE   */
                @165 THISNODE $1.      /* RJNODE    */
                @166 SUBIND $1.        /* RJFROM    */
                @177 JOBNUM IB4.       /* RJJOBNO   */
                @189 DSP2DSN IB2. @;   /* RJDDSN    */
*        ONLY WANT RECORDS FOR A SPECIFIC NODE NAME;  /* UserMod */
      IF (OTHRNODE ¬= RPTNODE) THEN DELETE;           /* UserMod */
*        ASSIGN PNODE AND SNODE;
         IF THISNODE = 'P' THEN DO;
            PNODE = MYNODE;
            SNODE = OTHRNODE;
            END;
         ELSE DO;
            PNODE = OTHRNODE;
            SNODE = MYNODE;
            END;
*        ASSIGN NODE JOB IS SUBMITTED ON;
         IF SUBIND  = 'P' THEN
            SUBNODE = PNODE;
         ELSE
            SUBNODE = SNODE;
         DATE = DATEJUL(STRDTE);      /* CONVERT JUL->SAS FOR MATH*/
*        GO AFTER DATASET NAME CONTAINING JOB;
         IF DSP2DSN ¬= 0 THEN DO;
           DSP2DSN + 1;               /* FOR BYTE REL TO 1 */
           INPUT @DSP2DSN DSNLEN IB2.
                 DSNAME $VARYING200.DSNLEN;
         END;
*        INCLUDE THIS OBSERVATION IN THE DATASET BEING CREATED;
         OUTPUT;
      END;
      DROP RECTYPE OTHRNODE THISNODE STRDTE SUBIND;
      FORMAT DATE MMDDYY10.;
      FORMAT CC $HEX8.;
* SORT DATASET FOR PRINT;
  PROC SORT;
    BY SNODE UID PROCNUM;
  DATA FINALRJ;
    SET RUNJOB;
    FILE FT20F001 N=PS LINESLEFT=L;
    IF _N_ = 1 THEN DO;
       PUT @45 'IBM Sterling Connect:Direct for z/OS RUNJOB Report';
       PUT @50 'NODE = ' MYNODE;
       PUT /;
       PUT @3
       'USERID      PROC #   PROC NAME    STEP NAME    RUNJOB INFORMATIO
N';
       PUT @3
       '******      ******   *********    *********    *****************
******************************************************************';
       PUT /;
    END;
    IF L < 9 THEN DO;
       PUT _PAGE_;
       PUT @45 'IBM Sterling Connect:Direct for z/OS RUNJOB Report';
       PUT @50 'NODE = ' MYNODE;
       PUT /;
       PUT @3
       'USERID      PROC #   PROC NAME    STEP NAME    RUNJOB INFORMATIO
N';
       PUT @3
       '******      ******   *********    *********    *****************
******************************************************************';
       PUT /;
    END;
* WRITE TO OUTPUT;
    PUT @3 UID @15 PROCNUM @24 PROCNAME @37 STEPNAME
        @50 DATE @61 'PNODE = ' PNODE @87 'SNODE = ' SNODE;
    PUT @50 'COMPLETION CODE = ' CC @87 'MSG ID = ' MSGID;
    INFILE DMMSGFIL VSAM KEY=MSGID;
    INPUT @23 SHTXTLEN IB2. @;
    SHTXTLEN = SHTXTLEN - 2;
    INPUT SHTEXT $VARYING200.SHTXTLEN;
    UPSHTEXT = UPCASE(SHTEXT);
    PUT @50 UPSHTEXT;
    PUT @50 '===========================================================
========================';
    IF SUBNODE = MYNODE THEN
      PUT @50 'DSNAME ==> ' DSNAME;
    IF CC = '00000000'X THEN DO;
      PUT @50 'JOB #  ==> ' JOBNUM;
      PUT @50 'JOB SUBMITTED ON ' SUBNODE;
      END;
    ELSE
      PUT @50 'INTENDED NODE FOR JOB SUBMISSION IS ' SUBNODE;
   PUT @3 '-------------------------------------------------------------
---------------------------------------------------------------------';
