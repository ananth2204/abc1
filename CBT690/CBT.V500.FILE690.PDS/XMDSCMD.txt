* XMDSCMD  - COMMAND PROCESSOR FOR CROSS-MEMORY BROWSER
* AUTHOR   - MARTIN KLINE
*
XMDSCMD  CSECT
         STM   R14,R12,12(R13)
         LR    R12,R15
         USING XMDSCMD,R12
         GETMAIN RU,LV=72
         ST    R13,4(,R1)
         ST    R1,8(,R13)
         LR    R13,R1
         USING MEMWORK,R10
         CLI   AID,PF5
         BE    FIND
         LA    R9,COMMAND
         LA    R8,L'COMMAND
*
FIND1ST  CLI   0(R9),C' '
         BNE   GOT1ST
*
         LA    R9,1(,R9)
         BCT   R8,FIND1ST
         B     EXIT
*
GOT1ST   SH    R8,=H'2'
         BNH   EXIT
         LA    R7,1(,R9)
         CLI   0(R9),C'+'
         BE    PLUS
*
         CLI   0(R9),C'-'
         BE    MINUS
*
         LA    R7,2(,R9)
         OC    0(2,R9),=8C' '
         CLC   0(2,R9),=C'L '
         BE    LOCATE
*
         CLC   0(2,R9),=C'F '
         BE    FIND
*
         SH    R8,=H'2'
         BNH   EXIT
         LA    R7,4(,R9)
         OC    0(4,R9),=8C' '
         CLC   0(4,R9),=C'LOC '
         BE    LOCATE
*
         SH    R8,=H'1'
         BNH   EXIT
*
         LA    R7,5(,R9)
         OC    0(5,R9),=8C' '
         CLC   0(5,R9),=C'FIND '
         BE    FIND
*
         SH    R8,=H'2'
         BNH   EXIT
*
         LA    R7,7(,R9)
         OC    0(7,R9),=8C' '
         CLC   0(7,R9),=C'LOCATE '
         BE    LOCATE
         BNE   EXIT
*
FIND     CALL  XMDSFIND
         B     EXIT
*
LOCATE   OC    COMMAND,=64C' '
         LR    R9,R7
*
FIND2ND  CLI   0(R9),C' '
         BNE   GOT2ND
         LA    R9,1(,R9)
         BCT   R8,FIND2ND
         B     EXIT
*
GOT2ND   LA    R5,CMDTABLE
         XR    R4,R4
         XR    R3,R3
*
LOCLOOP  ICM   R4,1,0(R5)
         BZ    EXIT
         CR    R4,R8
         BNL   LOCSKIP
         ICM   R3,7,1(R5)
         CLC   0(0,R9),4(R5)
         EX    R4,*-6
         BER   R3
*
LOCSKIP  LA    R5,5(R4,R5)
         B     LOCLOOP
*
LOCPSA   XC    ADDRESS,ADDRESS
         B     EXIT
*
LOCCVT   MVC   ADDRESS,16
         B     EXIT
*
LOCSMCA  L     R1,16
         MVC   ADDRESS,196(R1)
         B     EXIT
*
LOCASVT  L     R1,16
         MVC   ADDRESS,556(R1)
         B     EXIT
*
LOCSMCT  L     R1,16
         MVC   ADDRESS,604(R1)
         B     EXIT
*
LOCASMVT L     R1,16
         MVC   ADDRESS,704(R1)
         B     EXIT
*
LOCTRACE MVC   ADDRESS,84
         B     EXIT
*
LOCPCCA  MVC   ADDRESS,520
         B     EXIT
*
LOCLCCA  MVC   ADDRESS,528
         B     EXIT
*
LOCLOCKS LA    R1,640
         ST    R1,ADDRESS
         B     EXIT
*
LOCJESCT L     R1,16
         MVC   ADDRESS,296(R1)
         B     EXIT
*
LOCPVT   L     R1,16
         MVC   ADDRESS,356(R1)
         B     EXIT
*
LOCCSA   L     R1,16
         MVC   ADDRESS,416(R1)
         B     EXIT
*
LOCAPF   L     R1,16
         MVC   ADDRESS,484(R1)
         B     EXIT
*
LOCASCB  MVC   ADDRESS,ASCBPTR
         B     EXIT
*
LOCASXB  L     R1,ASCBPTR
         MVC   ADDRESS,108(R1)
         B     EXIT
*
LOCTSB   L     R1,ASCBPTR
         MVC   ADDRESS,60(R1)
         B     EXIT
*
LOCCSCB  L     R1,ASCBPTR
         MVC   ADDRESS,56(R1)
         B     EXIT
*
LRCTTCB  L     R1,ASCBPTR
         MVC   ADDRESS,124(R1)
         B     EXIT
*
LOCOUCB  L     R1,ASCBPTR
         MVC   ADDRESS,144(R1)
         B     EXIT
*
LOCOUXB  L     R1,ASCBPTR
         MVC   ADDRESS,148(R1)
         B     EXIT
*
LOCOUSB  B     EXIT
*
LOCTCB1  L     R1,ASCBPTR
         L     R1,108(,R1)
         LA    R1,4(,R1)
         CALL  XMDSMVCP
         ST    R1,ADDRESS
         B     EXIT
*
LTCBLAST L     R1,ASCBPTR
         L     R1,108(,R1)
         LA    R1,8(,R1)
         CALL  XMDSMVCP
         ST    R1,ADDRESS
         B     EXIT
*
LOCACEE  L     R1,ASCBPTR
         L     R1,108(,R1)
         LA    R1,200(,R1)
         CALL  XMDSMVCP
         ST    R1,ADDRESS
         B     EXIT
*
LOCRB    B     EXIT
LOCTIOT  B     EXIT
LOCDEBQ  B     EXIT
LOCTCT   B     EXIT
LOCJSCB  B     EXIT
*
LSVCTABL L     R1,16
         L     R1,200(,R1)
         MVC   ADDRESS,132(R1)
         B     EXIT
*
PLUS     DS    0H
MINUS    DS    0H
         OC    COMMAND,=64C' '
         LR    R2,R9
         LR    R9,R7
*
PLUS2    CLI   0(R9),C' '
         BNE   PLUS3
         LA    R9,1(,R9)
         BCT   R8,PLUS2
         B     EXIT
*
PLUS3    LR    R7,R9
PLUS4    CLI   0(R7),C' '
         BE    PLUS5
         CLI   0(R7),C'%'
         BE    PLUS5
         LA    R7,1(,R7)
         BCT   R8,PLUS4
*
PLUS5    SR    R7,R9
         CH    R7,=H'8'
         BH    EXIT
         MVC   WORK8,=8C'0'
         LA    R1,WORK8+8
         SR    R1,R7
         BCTR  R7,0
         MVC   0(0,R1),0(R9)
         EX    R7,*-6
         TR    WORK8(8),TRCHAR
         PACK  DWORD(5),WORK8(9)
         L     R1,DWORD
         L     R0,ADDRESS
         CLI   0(R2),C'+'
         BE    PLUS6
         SR    R0,R1
         ST    R0,ADDRESS
         B     PLUS7
*
PLUS6    AR    R0,R1
         ST    R0,ADDRESS
*
PLUS7    LA    R1,1(R7,R9)
         CLI   0(R1),C'%'
         BNE   EXIT
*
         L     R1,ADDRESS
         CALL  XMDSMVCP
         ST    R1,ADDRESS
         B     EXIT
*
EXIT     MVI   COMMAND,C' '
         MVC   COMMAND+1(L'COMMAND-1),COMMAND
         LR    R3,R13
         L     R13,4(,R13)
         STM   R15,R1,16(R13)
         FREEMAIN RU,LV=72,A=(3)
         LM    R14,R12,12(R13)
         BR    R14
*
TRCHAR   EQU   *-C'A'
         DC    X'0A0B0C0D0E0F'
         ORG   TRCHAR+C'0'
         DC    X'00010203040506070809'
         ORG
         LTORG
*
         ENTRY CMDTABLE
CMDTABLE DS    0F
         DC    AL1(1),AL3(LOCPSA),C'0 '
         DC    AL1(2),AL3(LOCRB),C'RB '
         DC    AL1(3),AL3(LOCTSB),C'TSB '
         DC    AL1(3),AL3(LOCTCT),C'TCT '
         DC    AL1(3),AL3(LOCPSA),C'PSA '
         DC    AL1(3),AL3(LOCCVT),C'CVT '
         DC    AL1(3),AL3(LOCPVT),C'PVT '
         DC    AL1(3),AL3(LOCCSA),C'CSA '
         DC    AL1(3),AL3(LOCAPF),C'APF '
         DC    AL1(4),AL3(LOCASCB),C'ASCB '
         DC    AL1(4),AL3(LOCASXB),C'ASXB '
         DC    AL1(4),AL3(LOCCSCB),C'CSCB '
         DC    AL1(4),AL3(LOCOUCB),C'OUCB '
         DC    AL1(4),AL3(LOCOUXB),C'OUXB '
         DC    AL1(4),AL3(LOCOUSB),C'OUSB '
         DC    AL1(4),AL3(LOCTCB1),C'TCB1 '
         DC    AL1(4),AL3(LOCACEE),C'ACEE '
         DC    AL1(4),AL3(LOCTIOT),C'TIOT '
         DC    AL1(4),AL3(LOCDEBQ),C'DEBQ '
         DC    AL1(4),AL3(LOCJSCB),C'JSCB '
         DC    AL1(4),AL3(LOCSMCA),C'SMCA '
         DC    AL1(4),AL3(LOCASVT),C'ASVT '
         DC    AL1(4),AL3(LOCSMCT),C'SMCT '
         DC    AL1(4),AL3(LOCPCCA),C'PCCA '
         DC    AL1(4),AL3(LOCLCCA),C'LCCA '
         DC    AL1(5),AL3(LOCJESCT),C'JESCT '
         DC    AL1(5),AL3(LOCASMVT),C'ASMVT '
         DC    AL1(5),AL3(LOCTRACE),C'TRACE '
         DC    AL1(5),AL3(LOCLOCKS),C'LOCKS '
         DC    AL1(6),AL3(LRCTTCB),C'RCTTCB '
         DC    AL1(7),AL3(LTCBLAST),C'TCBLAST '
         DC    AL1(8),AL3(LSVCTABL),C'SVCTABLE '
         DC    AL1(0)
         MEMWORK
         REG
         END
