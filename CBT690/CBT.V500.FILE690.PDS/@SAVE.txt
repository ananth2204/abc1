         MACRO
&NAME    @SAVE &BASE=R12,&BASE2=0,&LV=0,&SP=0
         GBLC  &LABEL,&CORE,&SPL
&NAME    CSECT
         USING *,R15                   ESTABLISH TEMPORARY BASE
&LABEL   SETC  '@@00'.'&SYSNDX'
         B     &LABEL                  BRANCH PAST MODULE ID
         DC    AL1(&LABEL-*)
         DC    CL9'&NAME '
         DC    CL8'&SYSDATE'
&LABEL   STM   R14,R12,12(R13)         SAVE REGISTERS
         LR    &BASE,R15               LOAD BASE REGISTER
         AIF   ('&BASE2' EQ '0').LABL01
         LA    &BASE2,2048(,&BASE)     ESTABLISH
         LA    &BASE2,2048(,&BASE2)    2ND BASE REGISTER
         DROP  R15                     DROP TEMPORARY BASE
         USING &NAME,&BASE,&BASE2      DEFINE BASE REGISTERS
         AGO   .LABL02
.LABL01  DROP  R15                     DROP TEMPORARY BASE
         USING &NAME,&BASE             DEFINE BASE REGISTER
.LABL02  ANOP
&CORE    SETC  '&LV'.'+72'
         LA    R0,&CORE                SAVEAREA LENGTH
         AIF   ('&SP' EQ '0').LABL03
         LA    R1,&SP                  LOAD SUBPOOL
         SLL   R1,24                   SHIFT 24-BITS LEFT
         OR    R0,R1                   PUT SUBPOOL INTO R0
.LABL03  ANOP
         GETMAIN R,LV=(0)              OBTAIN STORAGE
&SPL     SETC  '&SP'
         ST    R13,4(,R1)              SET UP BACKWARD
         ST    R1,8(,R13)              AND FORWARD POINTERS
         LM    R0,R1,20(R13)           RESTORE R0-R1
         L     R13,8(,R13)             POINT TO NEW SAVEAREA
         MEND
