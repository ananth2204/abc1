* XMDSHELP - HELP PROCESSOR FOR CROSS-MEMORY BROWSER
* AUTHOR   - MARTIN KLINE
*
XMDSHELP @SAVE BASE=R12                NORMAL ENTRY
         USING MEMWORK,R10             WORK AREA PASSED IN REG 10
         GETMAIN RU,LV=16384           GET DISPLAY AREA
*
         LR    R11,R1                  COPY AREA ADDRESS
         LR    R0,R1                   COPY IT AGAIN
         L     R1,=F'6144'             GET LENGTH OF IT
         XR    R14,R14                 CLEAR REG 14
         XR    R15,R15                       AND 15
         ICM   R15,8,=X'40'            INSERT A BLANK
         MVCL  R0,R14                  MAKE ENTIRE AREA BLANKS
*
         LR    R9,R11                  COPY AREA POINTER
         MVC   0(8,R9),=X'27F5401140401DF0'  3270 HEADING
         CLC   PRMSIZE,ALTSIZE         PRIMARY = ALTERNATE ?
         BE    *+8                     YES - GO ON
         MVI   1(R9),X'7E'             NO - USE ALTERNATE
         LA    R9,8(,R9)               POINT PAST PREFIX
         MVC   0(41,R9),=C'THE FOLLOWING MAJOR COMMANDS ARE ALLOWED:'
         AH    R9,COLS                 SKIP A LINE
         AH    R9,COLS                 SKIP ANOTHER LINE
         MVC   5(25,R9),=C'L | LOC | LOCATE    BLOCK'
         AH    R9,COLS                 SKIP A LINE
         AH    R9,COLS                 SKIP ANOTHER LINE
         MVC   10(38,R9),=C'WHERE ''BLOCK'' IS ONE OF THE FOLLOWING:'
         AH    R9,COLS                 SKIP A LINE
         AH    R9,COLS                 SKIP ANOTHER LINE
         L     R7,=V(CMDTABLE)         POINT TO COMMAND TABLE
*
CMDLOOP1 LA    R6,15(,R9)              POINT TO COMMAND
         LA    R5,7                    LOOP LIMIT
         XR    R1,R1                   CLEAR REG 1
*
CMDLOOP2 ICM   R1,1,0(R7)              GET COMMAND LENGTH
         BZ    ENDCMDS                 IF ZERO, DONE
*
         MVC   0(0,R6),4(R7)           EXECUTED MOVE
         EX    R1,*-6                  MOVE COMMAND TO DISPLAY
         LA    R7,5(R1,R7)             POINT TO NEXT COMMAND
         LA    R6,9(,R6)               POINT TO NEXT DISPLAY FIELD
         BCT   R5,CMDLOOP2             LOOP THROUGH COMMANDS
*
         AH    R9,COLS                 POINT TO NEXT LINE
         B     CMDLOOP1                LOOP
*
ENDCMDS  AH    R9,COLS                 SKIP A LINE
         AH    R9,COLS                 SKIP ANOTHER LINE
         MVC   5(31,R9),=C'F | FIND   ''STRING'' | X''STRING'''
         AH    R9,COLS                 SKIP A LINE
         AH    R9,COLS                 SKIP ANOTHER LINE
         MVC   0(36,R9),=C'THE FOLLOWING PF KEYS ARE SUPPORTED:'
         AH    R9,COLS                 SKIP A LINE
         AH    R9,COLS                 SKIP ANOTHER LINE
         MVC   5(29,R9),=C'PF1 - HELP          PF3 - END'
         AH    R9,COLS                 SKIP A LINE
         MVC   5(35,R9),=C'PF5 - REPEAT FIND   PF7 - SCROLL UP'
         AH    R9,COLS                 SKIP A LINE
         MVC   5(55,R9),=C'PF8 - SCROOL DOWN   PF11 - LINK TO ADDRESS U+
               NDER CURSOR'
         AH    R9,COLS                 SKIP A LINE
         SR    R9,R11                  CALCULATE DISPLAY LENGTH
*
         TPUT  (R11),(R9),FULLSCR      DISPLAY IT
         TGET  (R11),(R9),ASIS         GET ANSWER
         FREEMAIN RU,LV=16384,A=(R11)   FREE DISPLAY BUFFER
         XR    R15,R15                 SET RETURN CODE
         @RETURN ,                     GO BACK
*
         LTORG
         MEMWORK
         REG
         END
