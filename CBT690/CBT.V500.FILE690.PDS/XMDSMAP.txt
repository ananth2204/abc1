* XMDSMAP  - MAP PROCESSOR FOR CROSS-MEMORY BROWSER
* AUTHOR   - MARTIN KLINE
*
XMDSMAP  CSECT
         STM   R14,R12,12(R13)         SAVE CALLER'S REGS
         LR    R12,R15                 SET BASE REG
         USING XMDSMAP,R12             ESTABLISH ADDRESSABILITY
         GETMAIN RU,LV=72              GET SPACE FOR MY SAVE AREA
         ST    R13,4(,R1)              CHAIN
         ST    R1,8(,R13)                    SAVE
         LR    R13,R1                             AREAS
         USING MEMWORK,R10             WORK AREA PASSED VIA REG 10
*
         LH    R0,SCRSIZE              GET SCREEN SIZE
         SLL   R0,1                    TIMES 2
         GETMAIN RU,LV=(0)             GET AREA FOR SCREEN
         LR    R11,R1                  SAVE ADDRESS OF AREA
         GETMAIN RU,LV=16384           GET AREA
         LR    R9,R1                   POINT TO AREA
         LR    R0,R11                  COPY SCREEN AREA POINTER
         LH    R1,SCRSIZE              GET SCREEN SIZE
         XR    R2,R2                   CLEAR REG 2
         XR    R3,R3                   CLEAR REG 3
         MVCL  R0,R2                   SET SCREEN AREA TO ZEROES
*
         ST    R11,0(,R9)              SAVE SCREEN POINTER IN AREA
         LA    R7,4(,R9)               POINT PAST POINTER
         LR    R8,R11                  COPY SCREEN POINTER
         MVC   1(12,R8),=C'ADDRESS --->'  INSERT LABEL
         LA    R8,13(,R8)              BUMP PAST LABEL
         MVI   0(R7),X'40'             INSERT BLANK
         MVI   1(R7),8                 INSERT 8
         STCM  R8,15,2(R7)             SAVE FIELD ADDRESS
*
         LA    R7,6(,R7)               BUMP POINTER
         LA    R8,9(,R8)               BUMP ADDRESS
         MVC   0(2,R7),=X'F000'        INSERT CHAR 0 AND BINARY 0
         STCM  R8,15,2(R7)             SAVE ADDRESS
*
         LA    R7,6(,R7)               BUMP POINTER
         LA    R8,1(,R8)               BUMP ADDRESS
         MVC   0(9,R8),=C'MODE --->'   INSERT LABEL
         LA    R8,9(,R8)               POINT PAST LABEL
         MVI   0(R7),X'40'             INSERT BLANK
         MVI   1(R7),1                 INSERT 1
         STCM  R8,15,2(R7)             SAVE ADDRESS
*
         LA    R7,6(,R7)               BUMP POINTER
         LA    R8,2(,R8)               BUMP ADDRESS
         MVC   0(2,R7),=X'F000'        INSERT CHAR 0 AND HEX 0
         STCM  R8,15,2(R7)             SAVE ADDRESS
*
         LA    R7,6(,R7)               BUMP POINTER
         LA    R8,1(,R8)               BUMP ADDRESS
         MVC   0(9,R8),=C'ASID --->'   INSERT LABEL
         LA    R8,9(,R8)               POINT PAST LABEL
         MVI   0(R7),X'40'             INSERT BLANK
         MVI   1(R7),4                 INSERT 4
         STCM  R8,15,2(R7)             SAVE ADDRESS
*
         LA    R7,6(,R7)               BUMP POINTER
         LA    R8,5(,R8)               BUMP ADDRESS
         MVC   0(2,R7),=X'F000'        INSERT CHAR 0 AND HEX 0
         STCM  R8,15,2(R7)             SAVE ADDRESS
*
         LA    R7,6(,R7)               BUMP POINTER
         LA    R8,1(,R8)               BUMP ADDRESS
         MVC   0(12,R8),=C'JOBNAME --->' INSERT LABEL
         LA    R8,12(,R8)              POINT PAST LABEL
         MVI   0(R7),X'40'             INSERT BLANK
         MVI   1(R7),8                 INSERT 8
         STCM  R8,15,2(R7)             SAVE ADDRESS
*
         LA    R7,6(,R7)               BUMP POINTER
         LA    R8,9(,R8)               BUMP ADDRESS
         MVC   0(2,R7),=X'F000'        INSERT CHAR 0 AND HEX 0
         STCM  R8,15,2(R7)             SAVE ADDRESS
*
         LA    R7,6(,R7)               BUMP POINTER
         LA    R8,1(,R8)               BUMP ADDRESS
         LH    R15,COLS                GET NUM COLUMNS
         LA    R8,0(R15,R11)           POINT TO NEXT LINE
         MVC   1(12,R8),=C'COMMAND --->' INSERT LABEL
         LA    R8,13(,R8)              POINT PAST LABEL
         MVI   0(R7),X'40'             INSERT BLANK
         MVI   1(R7),64                INSERT 4
         STCM  R8,15,2(R7)             SAVE ADDRESS
*
         LA    R7,6(,R7)               BUMP POINTER
         LA    R8,65(,R8)              BUMP ADDRESS
         MVC   0(2,R7),=X'F800'        INSERT CHAR 8 AND HEX 0
         STCM  R8,15,2(R7)             SAVE ADDRESS
*
         LA    R7,6(,R7)               BUMP POINTER
         LA    R8,1(,R8)               BUMP ADDRESS
         LA    R15,3
         MH    R15,COLS                SIZE OF 3 ROWS
         LA    R8,0(R15,R11)           POINT PAST THEM
         LH    R6,ROWS                 GET NUMBER OF ROWS
         SH    R6,=H'3'                MINUS 3
*
LINELOOP LR    R5,R8                   COPY ROW POINTER
         MVI   0(R7),X'F0'             INSERT CHAR 0
         MVI   1(R7),8                 INSERT 6
         STCM  R8,15,2(R7)             SAVE ADDRESS
*
         LA    R7,6(,R7)               BUMP POINTER
         LA    R8,12(,R8)              BUMP ADDRESS
         LA    R4,4                    COUNT 4 WORDS PER LINE
         CLC   COLS,=H'80'             80-COLUMN DISPLAY ?
         BE    MIDLOOP                 YES - THAT'S RIGHT
         LA    R4,8                    NO - 8 WORDS PER LINE
*
MIDLOOP  MVI   0(R7),X'40'             INSERT BLANK
         MVI   1(R7),8                 INSERT 8
         STCM  R8,15,2(R7)             SAVE ADDRESS
*
         LA    R7,6(,R7)               BUMP POINTER
         LA    R8,9(,R8)               BUMP ADDRESS
         MVC   0(2,R7),=X'F000'        INSERT CHAR 0 AND HEX 0
         STCM  R8,15,2(R7)             SAVE ADDRESS
*
         LA    R7,6(,R7)               BUMP POINTER
         LA    R8,1(,R8)               BUMP ADDRESS
         BCT   R4,MIDLOOP              LOOP FOR ONE LINE
*
         LH    R15,COLS                GET NUM COLUMNS
         LA    R8,0(R15,R5)            POINT TO NEXT ROW
         BCT   R6,LINELOOP             LOOP FOR ALL ROWS
*
         XC    0(10,R7),0(R7)          CLEAR START OF AREA
         LR    R1,R9                   COPY ADDRESS OF MAP
         LR    R3,R13                  POINT TO MY SAVE AREA
         L     R13,4(,R13)             RESTORE CALLER'S SAVE AREA
         STM   R15,R1,16(R13)          SAVE RETURN REGS
         FREEMAIN RU,LV=72,A=(R3)      FREE MY SAVE AREA
         LM    R14,R12,12(R13)         RESTORE REGS
         BR    R14                     GO BACK
*
         LTORG
         MEMWORK
         REG
         END
