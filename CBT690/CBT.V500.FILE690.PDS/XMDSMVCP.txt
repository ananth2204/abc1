* XMDSMVCP - MOVETO PRIMARY PROCESSOR FOR CROSS-MEMORY BROWSER
* AUTHOR   - MARTIN KLINE
*
XMDSMVCP CSECT
         STM   R14,R12,12(R13)         SAVE REGS
         LR    R12,R15                 SET UP BASE REG
         USING XMDSMVCP,R12
         LR    R8,R1                   SAVE PARM REG
         GETMAIN RU,LV=72              GET MY SAVE AREA
         ST    R13,4(,R1)              CHAIN
         ST    R1,8(,R13)                    SAVE
         LR    R13,R1                             AREAS
         USING MEMWORK,R10             WORK AREA PASSED VIA REG 10
*
***      @AUTH
         MODESET MODE=SUP              SUPERVISOR STATE
***      @NOAUTH
         CLI   MODE,C'X'               EXTENDED ADDRESSING MODE ?
         BNE   SETESTAE                NO - GO ON
*
         LA    R3,SETESTAE             WHERE TO GO
         ICM   R3,8,=X'80'             SET 31-BIT
         BSM   R0,R3                   SWITCH TO 31-BIT MODE
*
SETESTAE ESTAE ESTAEXIT,CT             SET UP RECOVERY
*
         STCM  R1,15,MSPIESV           SAVE REGS FOR RECOVERY
         EPAR  1                       GET PRIMARY ASID
         SSAR  1                       RESET SECONDARY ASID
*
         LA    R1,1                    INDEX = 1
         AXSET AX=(R1)                 SET INDEX
         L     R1,ASCBPTR              POINT TO TARGET ASCB
         USING ASCB,R1
         LH    R0,ASCBASID             GET TARGET ASID
         SSAR  0                       SWITCH TO SECONDARY MODE
*
         LA    R0,0                    SET KEY
         LA    R1,4                    SET LENGTH
         MVCP  WORK8(R1),0(R8),0       MOVE 4 BYTES
         B     DONE                    GO ON
*
NOTAVAIL STCM  R8,15,WORK8
DONE     EPAR  0                       GET PRIMARY ASID
         SSAR  0                       SET SECONDARY ASID
*
***      @AUTH
         MODESET MODE=PROB             SWITCH STATE
***      @NOAUTH
*
         ICM   R2,15,MSPIESV           RESTORE REGS
         ESTAE 0                       REMOVE RECOVERY
         LA    R3,EXIT                 WHERE TO GO
         BSM   R0,R3                   SET AMODE = 24
*
EXIT     ICM   R1,15,WORK8             GET RETURN POINTER
         LR    R3,R13                  COPY SAVE AREA ADDRESS
         L     R13,4(,R13)             RESTORE CALLER'S SAVE AREA
         STM   R15,R1,16(R13)          SAVE RETURN REGS
         FREEMAIN RU,LV=72,A=(R3)      RELEASE MY SAVE AREA
         LM    R14,R12,12(R13)         RESTORE REGS
         BR    R14                     GO BACK
*
ESTAEXIT DS    0H
         USING SDWA,R1
         LM    R2,R12,SDWAGR02
         MVC   DATALEN,SDWAABCC
         CLC   SDWACMPC,=X'0D5000'     0D5 ABEND ?
         BE    ESTAEOK                 YES - OK
         CLI   SDWAICD1,X'04'
         BE    ESTAEOK
         CLI   SDWAICD1,X'05'
         BE    ESTAEOK
         CLI   SDWAICD1,X'10'
         BE    ESTAEOK
         CLI   SDWAICD1,X'11'
         BE    ESTAEOK
         BR    R14
*
ESTAEOK  LA    R2,NOTAVAIL
         SETRP RC=4,RETADDR=(R2),FRESDWA=YES,DUMP=NO
         BR    R14
*
         LTORG
         MEMWORK
         PRINT NOGEN
         IHAASCB
         IHASDWA
         REG
         END
