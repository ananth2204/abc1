/* rexx */
/*********************************************************************/
/* This exec will handle block deletes from option 3.4, bypassing    */
/* errors for VSAM DATA and INDEX components.                        */
/*                                                                   */
/* Change Log                                                        */
/*                                                                   */
/*    Date    Name           Description                             */
/* ---------- -------------- --------------------------------------- */
/* 2018/12/23 Conley         Initial coding.                         */
/*********************************************************************/
/*********************************************************************/
/* Parse the arguments for the dataset name to be deleted.           */
/*********************************************************************/
parse arg dsn
/*********************************************************************/
/* VGET the delete confirmation variable ZDSLCONF.  It the value is  */
/* not equal to 'OFF', then display the delete confirmation panel,   */
/* otherwise set the delrc variable to 0 and proceed with DELETE.    */
/*********************************************************************/
address ispexec "VGET (ZDSLCONF)"
if zdslconf <> 'OFF' then
   do
      address ispexec "DISPLAY PANEL(ISRUADC4)"
/*********************************************************************/
/* Save the return code from the delete confirmation panel display.  */
/* If the user hit 'ENTER' on the panel, rc will be 0, any other key */
/* will be a non-zero rc.                                            */
/*********************************************************************/
      delrc = rc
   end
else
   delrc = 0
/*********************************************************************/
/* If the user hit 'ENTER' to confirm the delete, or if delete       */
/* confirmations were off, allocate files and invoke IDCAMS to       */
/* process the DELETE.                                               */
/*********************************************************************/
if delrc = 0 then
   do
/*********************************************************************/
/* Dummy out SYSPRINT to suppress any IDCAMS messages coming back to */
/* the terminal.  Allocate SYSIN, write the DELETE command to it,    */
/* set the lastrc to 0, call IDCAMS to complete the DELETE, and FREE */
/* SYSPRINT AND SYSIN.                                               */
/*********************************************************************/
      address tso "ALLOC FI(SYSPRINT) DUMMY REUSE"
      address tso "ALLOC FI(SYSIN) UNIT(SYSALLDA) SPACE(1) TRACK",
                        "RECFM(F B) LRECL(80)"
      sysin.1 = " DELETE "dsn
      "EXECIO * DISKW SYSIN (OPEN STEM SYSIN. FINIS)"
      lastrc = 0
      address tso "CALL *(IDCAMS)"
/*********************************************************************/
/* Set all non-zero return codes to 4, to echo back a small error to */
/* the user.  The return code 8 that IDCAMS receives when trying to  */
/* delete any DATA or INDEX components causes a misleading "Command  */
/* failed" message to appear on the re-display of ISRUDSL0.  Setting */
/* the return code to 4 prevents this, while still showing the user  */
/* "USRDEL34 RC=4" in the message area.                              */
/*********************************************************************/
      if rc <> 0 then
         lastrc = 4
      address tso "FREE FI(SYSPRINT)"
      address tso "FREE FI(SYSIN)"
   end
/*********************************************************************/
/* If the user hit 'CANCEL' or 'END' on the delete confirmation      */
/* panel to cancel the delete, set lastrc = 4.                       */
/*********************************************************************/
else
   lastrc = 4
/*********************************************************************/
/* Send back the return code to prevent the misleading "Command      */
/* failed" error message, and keep the re-display of ISRUDSL0 clean. */
/*********************************************************************/
exit(lastrc)
