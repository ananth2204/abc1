/* Rexx ***** HSM Recovery Facility **************************** */
/* UREC:      Recover backed up data set from panel URECP        */
/*            Input = data set (DSNAME to be recovered).         */
/*            Freeware from XEPHON JOURNAL JULY 2002 Edition.    */
/*            Located, downloaded and tested by                  */
/*            M. Wickman 09/??/2008                              */
/*            Program and Panel rewritten to Rexx and            */
/*            beautified to Rexx from CLIST by                   */
/*            T. Alleman 10/08/2008                              */
/* ************************************************************* */
/* Envoked normally from ISPF 3.4 panel but may be entered     * */
/* in the form of:                                             * */
/* urec dsn    - or -                                          * */
/* tso urec dsn                                                * */
/* ************************************************************* */
/* ************************************************************* */
/* Get the data set passed and strip off the tic's               */
/* ************************************************************* */
parse upper arg dataset .
dataset = strip(dataset,"B","'")
/* ************************************************************* */
/* We need a work data set for HSM report.                       */
/* ************************************************************* */
user = Sysvar(SYSUID)
temprec = user'.HSM.TEMPREC'
/* ************************************************************* */
/* Setting TrapON to ON will supress any message from the DELETE */
/* ************************************************************* */
TrapON=OutTrap('ON')
DELETE temprec
TrapON=OutTrap('OFF')
/* ************************************************************* */
/* Get HSM report for data set given */
/* ************************************************************* */
"HLIST DATASET("dataset") BCDS ODS("temprec")"
"ALLOC F(AL) DA("temprec") SHR REU INPUT"
"EXECIO" 0 "DISKR AL (OPEN"
eof = 'no'
count1 = 0
/* ************************************************************* */
/* Clear possible backup generations return variable area        */
/* ************************************************************* */
x = 0
do while x < 15
   trov.x = ''
   x = x + 1
end
/* ************************************************************* */
/* Read the HSM report looking for backup generation lines       */
/* ************************************************************* */
do while eof = 'no'
   "EXECIO 1 DISKR AL"
   if rc Â¬= 0 then
      eof = 'yes'
   else
   do
      pull data
      barr1 = substr(data,70,1)
      barr2 = substr(data,73,1)
      if barr1 = '/' & barr2 = '/' then
      do
/* ************************************************************* */
/* If we think we found a report line pick out the stuff we      */
/* want.                                                         */
/* ************************************************************* */
         datacop = substr(data,67,9)        /* date              */
         datatim = substr(data,77,6)        /* time              */
         gennum  = substr(data,94,2)        /* gen#              */
         gennum  = gennum + 1               /* fake gen for display */
         backvl  = substr(data,51,7)        /* volser            */
/* ************************************************************* */
/* Build a stub of the data we want                              */
/* ************************************************************* */
         trov.gennum = gennum backvl datacop datatim
/* ************************************************************* */
/* Build a default data set name for recovery (once)             */
/* ************************************************************* */
         if gennum = 1 then
            dtc = substr(data,68,2)||substr(data,71,2)||substr(data,74,2)
         count1 = count1 + 1
      end
   end
end
"EXECIO" 0 "DISKR AL (FINIS"
"FREE FI(AL)"
TrapON=OutTrap('ON')
DELETE temprec
TrapON=OutTrap('OFF')
/* ************************************************************* */
/* After reading input file  TEMPREC", if no back-up found,      */
/* EXIT with code(12).  Else display the URECP panel             */
/* filled with info taken from input.                            */
/* ************************************************************* */
if count1 = 0 then
   do
      say '*** No HSM backups for 'dataset 'found.'
      exit 12
   end
rimpiazz = 'NO'
ndataset = dataset||'.AT'dtc
/* ************************************************************* */
/* Establish all possible GEN results for display panel.         */
/* ************************************************************* */
trov1 = trov.1
trov2 = trov.2
trov3 = trov.3
trov4 = trov.4
trov5 = trov.5
trov6 = trov.6
trov7 = trov.7
trov8 = trov.8
trov9 = trov.9
trov10 = trov.10
trov11 = trov.11
trov12 = trov.12
trov13 = trov.13
trov14 = trov.14
/* ************************************************************* */
/* Display the URECP panel to the user                           */
/* ************************************************************* */
"ISPEXEC DISPLAY PANEL(URECP)"
if rc = 8 then exit 0
/* ************************************************************* */
/* If the user requested to replace the existing data set        */
/* then clear the derived new data set name                      */
/*      activate the REPLACE keyword                             */
/* ************************************************************* */
if rimpiazz = 'YES' then
   do
      rep = "REPLACE"
      newds = ''
   end
else
   do
      rep = ''
      newds = "NEWNAME('"ndataset"')"
   end
/* ************************************************************* */
/* If the user specified a receiveing volume serial number       */
/* then set the UNIT and TVOL else clear them                    */
/* ************************************************************* */
if volumer = '' then
   do
      unit = ''
      vol  = ''
   end
else
   do
      unit = "U(3390)"
      vol  = "TOVOL("volumer")"
   end
"ISPEXEC SETMSG MSG(UREC003)"
/* ************************************************************* */
/* Set actual GEN requested relative to zero.                    */
/* ************************************************************* */
backgen = backgen - 1
"HRECOV "dataset" GEN("backgen")" rep newds unit vol
exit 0
