          TITLE 'AOF ROUTINE TO WAIT FOR MESSAGE'
*PROCESS  USING(NOWARN)
AOFWTOR   CSECT ,
AOFWTOR   AMODE 31
AOFWTOR   RMODE ANY
***********************************************************************
* This is the AOFWTOR REXX function.                                  *
***********************************************************************
* command syntax:                                                     *
*                                                                     *
* aofwtorrc = AOFWTOR(text,reply_length)                              *
*                                                                     *
* Where text is the text to put out with the wtor and reply_length    *
*       is the maximum length of the reply.                           *
*                                                                     *
* Deafult reply_length is 119                                         *
*                                                                     *
* This command is designed to be used with the AOF application. It is *
* intended to provide an AOF automation exec  with the capability of  *
* issuing  a WTOR and getting the reply back in rexx variable         *
* aofwtor_reply.                                                      *
*                                                                     *
* If the message does appear the function will exit with a return     *
* code of 0.                                                          *
***********************************************************************
*---------------------------------------------------------------------*
*- register usage                                                    -*
*---------------------------------------------------------------------*
R0      EQU    0                  work reg
R1      EQU    1                  work and parm reg
R2      EQU    2                  work reg
R3      EQU    3                  work reg
R4      EQU    4                  work reg
R5      EQU    5
R6      EQU    6
R7      EQU    7
R8      EQU    8
R9      EQU    9
R10     EQU    10
R11     EQU    11                 work area base
R12     EQU    12                 module base
R13     EQU    13                 linkage
R14     EQU    14                 linkage
R15     EQU    15                 linkage
***********************************************************************
* begin linkage convention
       STM     R14,R12,12(R13)
       LR      R2,R13             Save savearea
       LR      R10,R1             Save parm pointer
       LA      R12,0(R15)         base and address regs
       USING   AOFWTOR,R12
       MODID   ,                  eye catcher and date
       STORAGE OBTAIN,LENGTH=DYNL get dynamic storage
       LA      R11,0(0,R1)        use R11 as dyn base
       USING   DYNAREA,R11        map model over dynamic area
       LA      R13,SAVEAREA
       ST      R13,8(R2)          Link saveareas
       ST      R2,SAVEAREA+4
******************************************************************
       L       R1,16(R10)          let's get our argument
       CLC     0(L'PARMEND,R1),PARMEND    is there an argument
       BE      BAD_WTOR            no, then send error
       ICM     R3,15,4(R1)         get the length of argument
       BZ      BAD_WTOR            if none send error
       CH      R3,=AL2(L'WTOR_TEXT) verify it's not too long
       BNH     SAVE_LENGTH         no, set length
       LH      R3,=AL2(L'WTOR_TEXT) set length to max
SAVE_LENGTH DS 0H
       STH     R3,WTOR_TEXT_LENGTH save the length of wtor
       BCTR    R3,0                decrement for executed mvc
       L       R7,0(R1)            and address the argument
       EX      R3,MOVEWTOR
       B       GET_REPLY_LENGTH
MOVEWTOR DS    0H
       MVC     WTOR_TEXT(*-*),0(R7) executed
******************************************************************
GET_REPLY_LENGTH EQU   *
       LA      R1,8(,R1)
       CLC     0(L'PARMEND,R1),PARMEND    is there an argument?
       BE      SET_DEFAULT         no, then use default reply length
       ICM     R3,15,4(R1)         get the length of descriptor
       BZ      SET_DEFAULT         zero, then use default reply length
       CH      R3,=H'3'            verify it's not too long
       BH      BAD_WTOR            too long
       L       R7,0(R1)            point to the argument
       LR      R0,R3
       LR      R1,R7
TEST_REPLY_LENGTH_LOOP EQU *
       CLC     0(1,R1),=C'0'
       BL      BAD_WTOR
       CLC     0(1,R1),=C'9'
       BH      BAD_WTOR
       LA      R1,1(,R1)
       BCT     R0,TEST_REPLY_LENGTH_LOOP
       BCTR    R3,0
       EX      R3,PACK
       B       CONVERT_REPLY_LENGTH
PACK   DS      0H
       PACK    DWORD,0(*-*,R7)
CONVERT_REPLY_LENGTH EQU *
       CVB     R3,DWORD              convert to binary
       C       R3,=F'1'
       BL      SET_DEFAULT
       C       R3,=F'119'
       BH      SET_DEFAULT
       STH     R3,REPLY_LENGTH
       B       REPLY_LENGTH_OK
SET_DEFAULT EQU *
       MVC     REPLY_LENGTH,=H'119' default to 119
***********************************************************************
*                                                                     *
* Issue WTOR
*
***********************************************************************
REPLY_LENGTH_OK EQU *
       MVI     REPLY,C' '
       MVC     REPLY+1(L'REPLY-1),REPLY
       LA      R2,REPLY
       XC      WTORECB,WTORECB
       LA      R3,WTORECB
       LA      R4,WTOR_TEXT_LENGTH
       LH      R8,REPLY_LENGTH
       WTOR    TEXT=((4),(2),(8),(3))
       WAIT    ECB=(3)
***********************************************************************
*                                                                     *
* SET aofwtor_reply
*
***********************************************************************
       LA      R1,IRXSHVBSTG
       USING   SHVBLOCK,R1
       LA      R5,KAOFWTOR_REPLY
       ST      R5,SHVNAMA
       LA      R5,REPLY
       ST      R5,SHVVALA
       LA      R5,L'KAOFWTOR_REPLY
       ST      R5,SHVNAML
       ST      R8,SHVVALL
       SR      R0,R0        NO ENVIRONMENT BLOCK
       ST      R0,SHVNEXT
       MVI     SHVCODE,SHVSTORE
       DROP    R1
       LINK       EP=IRXEXCOM,DCB=0,MF=(E,CALLIST),SF=(E,LINKLIST),    *
               PARAM=(EYEC,DUMMY,DUMMY,IRXSHVBSTG),VL=1
       B       OK
***********************************************************************
*                                                                     *
* SET REPLY
*
***********************************************************************
OK     EQU     *
       MVC     ERROR(L'OK_FUNCTION),OK_FUNCTION
       L       R8,=A(L'OK_FUNCTION)
       MVC     RETCODE(2),=CL2'00'    set return code
       B       SETERRORMSG
BAD_WTOR       EQU   *
       MVC     ERROR(L'ERROR_FUNCTION),ERROR_FUNCTION
       L       R8,=A(L'ERROR_FUNCTION)
       MVC     RETCODE(2),=CL2'08'    set return code
       B       SETERRORMSG
SETERRORMSG DS    0H
       LA      R1,IRXSHVBSTG
       USING   SHVBLOCK,R1
       LA      R5,KAOFWTOR_ERROR
       ST      R5,SHVNAMA
       LA      R5,ERROR
       ST      R5,SHVVALA
       LA      R5,L'KAOFWTOR_ERROR
       ST      R5,SHVNAML
       ST      R8,SHVVALL
       SR      R0,R0        NO ENVIRONMENT BLOCK
       ST      R0,SHVNEXT
       MVI     SHVCODE,SHVSTORE
       DROP    R1
       LINK       EP=IRXEXCOM,DCB=0,MF=(E,CALLIST),SF=(E,LINKLIST),    *
               PARAM=(EYEC,DUMMY,DUMMY,IRXSHVBSTG),VL=1
       B       SETRETCODE
SETRETCODE DS  0H
       L       R7,20(R10)
       L       R7,0(R7)
       USING   EVALBLOCK,R7
       MVC     EVALBLOCK_EVLEN,=F'2'
       MVC     EVALBLOCK_EVDATA(2),RETCODE
ENDPROG EQU    *
       L       R3,SAVEAREA+4
       STORAGE RELEASE,           free dynamic storage                 X
               LENGTH=DYNL,                                            X
               ADDR=(R11)
       SLR     R15,R15
       L       R14,12(R3)
       LM      R0,R12,20(R3)
       BR      R14
*
PARMEND           DC    X'FFFFFFFFFFFFFFFF'
ERROR_FUNCTION    DC    C'ERROR IN SPECIFYING AOFWTOR FUNCTION'
OK_FUNCTION       DC    C'OK'
KAOFWTOR_ERROR    DC    C'AOFWTOR_ERROR'
KAOFWTOR_REPLY    DC    C'AOFWTOR_REPLY'
EYEC              DC    CL8'IRXEXCOM'
DUMMY             DC    A(0)
              LTORG
*================================================================
DYNAREA       DSECT
SAVEAREA      DS    18F
REPLY         DS    CL119
ERROR         DS    CL120
DWORD         DS    D
WTORECB       DS    F
REPLY_LENGTH  DS    H
WTOR_TEXT_LENGTH  DS  H
WTOR_TEXT     DS  CL122
**
RETCODE       DS    H
**
LINKLIST      LINK    EP=IRXEXCOM,DCB=0,SF=L
CALLIST       CALL    ,(EYEC,DUMMY,DUMMY,IRXSHVBSTG),VL,MF=L
IRXSHVBSTG    DS      CL(SHVBLEN)
**
**
*
DYNL          EQU  *-SAVEAREA
         IRXSHVB
         IRXEVALB
         IEANTASM
         END
