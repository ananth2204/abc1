         TITLE 'AOFSVC - ISSUE SYSTEM LEVEL TOKEN CREATE/DELETE'
***********************************************************************
*                                                                     *
* MODULE NAME: AOFSVC (SVC 254)                                       *
*                                                                     *
* DESCRIPTION: ROUTINE TO ISSUE SYSTEM LEVEL TOKEN CREATE/DELETE      *
*                                                                     *
* Function:                                                           *
*                                                                     *
*  1) CHECK TO SEE IF USER IS AUTHORIZED TO ISSE SVC                  *
*         If not return with code 8                                   *
*                                                                     *
*  2) Check to see if DELETE or CREATE was specified                  *
*                                                                     *
*  3a) If DELETE then CALL IEANTDL to delete a system level           *
*      name/token pair                                                *
*                                                                     *
*  3b) If CREATE then CALL IEANTCR to create a system level           *
*      name/token pair                                                *
*                                                                     *
*  4) Return with IEANTDL or IEANTCR return code                      *
*                                                                     *
*  Entry point:                                                       *
*      AOFSVC                                                         *
*                                                                     *
*  Purpose:                                                           *
*      Delete or create a system level name/token pair                *
*                                                                     *
*  Input:                                                             *
*      R0  = Pointer to character string "CREATE" or "DELETE"         *
*      R1  = Address of IEANTDL or IEANTCR parameter list             *
*      R6  = Entry point                                              *
*      R7  = ADDRESS OF ASCB                                          *
*      R13 =                                                          *
*      R14 = Return address                                           *
*                                                                     *
*  Output:                                                            *
*      Return code of 8 if user not authorized for $TRU NT.AOFSVC     *
*      otherwise return code from IEANTDL or IEANTCR                  *
*                                                                     *
*  Register useage:                                                   *
*       R0  - Work register                                           *
*       R1  - Work register                                           *
*       R2  - Work                                                    *
*       R3  - Work                                                    *
*       R4  - Work                                                    *
*       R5  - Work                                                    *
*       R6  - Base register for program                               *
*       R7  - Unused                                                  *
*       R8  - Unused                                                  *
*       R9  - Unused                                                  *
*       R10 - Unused                                                  *
*       R11 - Unused                                                  *
*       R12 - Unused                                                  *
*       R13 - Unused                                                  *
*       R14 - Return address                                          *
*       R15 - Linkage and return code                                 *
*                                                                     *
***********************************************************************
*---------------------------------------------------------------------*
*- Register usage                                                    -*
*---------------------------------------------------------------------*
R0      EQU    0                  Work reg
R1      EQU    1                  Work and parm reg
R2      EQU    2                  Work reg
R3      EQU    3                  Work reg
R4      EQU    4                  Work reg
R5      EQU    5
R6      EQU    6                  Module base
R7      EQU    7
R8      EQU    8
R9      EQU    9
R10     EQU    10
R11     EQU    11
R12     EQU    12
R13     EQU    13                 Dynamic storage base
R14     EQU    14                 Linkage
R15     EQU    15                 Linkage
        EJECT ,
        PRINT ON,GEN
AOFSVC  CSECT
AOFSVC  AMODE 31
AOFSVC  RMODE ANY
         USING AOFSVC,R6       Set addressability
         MODID
         SPACE 2
***********************************************************************
*                                                                     *
*        Save parameters and get storage                              *
*                                                                     *
***********************************************************************
         LR    R5,R1            Save parm address
         LR    R4,R0            Save parm address
         GETMAIN RU,LV=DATALEN,SP=252 Get storage
         LR    R13,R1
         USING DATA,R13
         ST    R14,RETADDR
         MVC   FUNCTION,0(R4)
         SPACE 2
***********************************************************************
*                                                                     *
*        Check authority                                              *
*                                                                     *
***********************************************************************
         MVC   RESOURCE,RESOURCEC      Init resource to 'NT.AOFSVC'
         LA    R0,ENTITYL
         STH   R0,ENTITY
         XC    ENTITY2,ENTITY2
         MVC   AUTHCHK(AUTHCHKL),AUTHCHKC Init RACROUTE macro
         LA    R3,ENTITY
         RACROUTE REQUEST=AUTH,                                        X
               RELEASE=1.9,                                            X
               ENTITYX=((R3)),                                         X
               WORKA=SAFWORK,                                          X
               MF=(E,AUTHCHK)
         C     R15,=F'8'               Is user authorized?
         BNL   RET8
         SPACE 2
         CLC   FUNCTION,=CL6'DELETE'
         BE    CALL_DELETE
         CLC   FUNCTION,=CL6'CREATE'
         BE    CALL_CREATE
         B     RET8
***********************************************************************
*                                                                     *
*        Call IEANTDL                                                 *
*                                                                     *
***********************************************************************
CALL_DELETE DS 0H
         USING PSA,0
         L     R15,CVTPTR                 Get the CVT pointer
         USING CVTMAP,R15
         L     R15,CVTCSRT
         DROP  R15
         L     R15,X'14'(R15,0)
         L     R15,X'0C'(R15,0)            Get address of IEANTDL
         LR    R1,R5
         BALR  R14,R15
         LR    R2,R15
         B     RETURN
         EJECT ,
***********************************************************************
*                                                                     *
*        Call IEANTCR                                                 *
*                                                                     *
***********************************************************************
CALL_CREATE DS 0H
         USING PSA,0
         L     R15,CVTPTR                 Get the CVT Pointer
         USING CVTMAP,R15
         L     R15,CVTCSRT
         DROP  R15
         L     R15,X'14'(R15,0)
         L     R15,X'04'(R15,0)
         LR    R1,R5
         BALR  R14,R15
         LR    R2,R15
         B     RETURN
         EJECT ,
***********************************************************************
*                                                                     *
*        Return                                                       *
*                                                                     *
***********************************************************************
RET8     DS    0H
         LA    R2,8                     Load return code of 8
         B     RETURN                   BR to return
RETURN   DS    0H
         L     R3,RETADDR
         LR    R1,R13
         FREEMAIN RU,LV=DATALEN,A=(1),SP=252 Free data dsect storage
         LR    R14,R3
         LR    R15,R2
         BR    R14                      Return
         EJECT ,
***********************************************************************
*                                                                     *
*        Literals                                                     *
*                                                                     *
***********************************************************************
         LTORG ,
AUTHCHKC RACROUTE REQUEST=AUTH,                                        X
               CLASS='$TRU',                                           X
               RELEASE=1.9,                                            X
               MF=L
AUTHCHKL EQU   *-AUTHCHKC
RESOURCEC DC   C'NT.AOFSVC'
         EJECT ,
***********************************************************************
*                                                                     *
*        DATA DSECT                                                   *
*                                                                     *
***********************************************************************
DATA     DSECT  ,
SAVEAREA DS     18F
RETADDR  DS     F
FUNCTION DS     CL6
AUTHCHK  RACROUTE REQUEST=AUTH,                                        X
               CLASS='$TRU',                                           X
               RELEASE=1.9,                                            X
               MF=L
ENTITY   DS    AL2(ENTITYL)
ENTITY2  DS    AL2(0)
RESOURCE DS    C'NT.AOFSVC'
ENTITYL  EQU   *-RESOURCE
SAFWORK  DS    XL512
DATALEN  EQU    *-DATA
         PRINT ON, NOGEN
         CVT   DSECT=YES
         IHAPSA
         END
