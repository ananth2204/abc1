* ------------------------------------------------------------------- *
*                                                                     *
*  Module name: VZAPNBR3                                              *
*                                                                     *
*   Display/update 3 byte numeric fields                              *
*                                                                     *
* ------------------------------------------------------------------- *
VZAPNBR3 CSECT
VZAPNBR3 AMODE 31
VZAPNBR3 RMODE ANY
         USING VZAPCOMM,R12
         USING VZAPNBR3,R15
         B     INIT0000
MODID    DC    CL8'VZAPNBR3'
         DC    CL8'&SYSDATE'
         DC    CL8'&SYSTIME'
         DC    A(DSCBEND-VZAPNBR3)
INIT0000 DS    0H
         STM   R14,R12,12(R13)                SAVE REGS
         LR    R10,R15                        COPY ENTRY POINT
         LR    R2,R0                          COPY R0
         LR    R3,R1                          COPY R1
         DROP  R15
         USING VZAPNBR3,R10                   DEFINE BASE
         L     R15,COMM_DXD
         A     R15,DXD_START
         ST    R13,4(,R15)                    SAVE R13
         ST    R15,8(,R13)                    CHAIN SAVE AREA
         LR    R13,R15                        COPY WORK AREA ADDRESS
         USING DXDNBR3,R13                    DEFINE WORK AREA BASE
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY
         L     R9,COMM_VZAPSPFD
         USING VZAPSPFD,R9
         L     R8,COMM_SELECTED_DSCB          SELECTED DSCB
         USING VTOCDATA,R8
* ------------------------------------------------------------------- *
*        Compute data address within DSCB                             *
* ------------------------------------------------------------------- *
         L     R5,COMM_SELECTED_FIELD         SELECTED FIELD
         USING FIELD_DSECT,R5                 DEFINE BASE
         ITRACE ID=FIELD,                                              +
               DATA1=FIELD_LABEL,                                      +
               DATA2=(FIELD_DISP,2)
         SR    R6,R6
         ICM   R6,3,FIELD_DISP                DISP TO DATA
         AR    R6,R8                          PLUS BASE
* ------------------------------------------------------------------- *
*        Copy data to allow user to abort                             *
* ------------------------------------------------------------------- *
         ITRACE ID=DATA,                                               +
               RDATA1=R6,                                              +
               DATA2=(0(R6),3)
         MVC   DXD_DATA,0(R6)                 SAVE DATA
* ------------------------------------------------------------------- *
*        Build detail                                                 *
* ------------------------------------------------------------------- *
MAIN0000 DS    0H
         ITRACE ID=BUILD
         SR    R0,R0
         ICM   R0,7,0(R6)                     DATA FROM DSCB
         CVD   R0,COMM_DWORD                  CONVERT TO DECIMAL
         MVC   DXD_EDITWORD,EDITWORD          INITIALIZE EDIT WORD
         ED    DXD_EDITWORD,COMM_DWORD+3      EDIT
         MVC   SPF_8,DXD_EDITWORD+2           COPY EDITED NUMBER
* ------------------------------------------------------------------- *
*        Display                                                      *
* ------------------------------------------------------------------- *
MAIN0010 DS    0H
         ITRACE ID=DISPLAY
         MVC   COMM_PANEL,NBR3_PANEL          COPY PANEL NAME
         XC    COMM_VDATA_ADDR,COMM_VDATA_ADDR
         XC    COMM_VDATA_LENGTH,COMM_VDATA_LENGTH
         XC    COMM_VDATA_CURSOR,COMM_VDATA_CURSOR
         L     R15,COMM_V_VZAPDISP            VZAPDISP ENTRY POINT
         BALR  R14,R15                        CALL VZAPDISP
         ITRACE ID=DISP_RC,                                            +
               DATA1=(COMM_DISP_RC,2)
* ------------------------------------------------------------------- *
*        Does user want to cancel changes?                            *
* ------------------------------------------------------------------- *
         CLC   SPF_ZCMD,COMM_BLANKS           COMMAND PRESENT?
         BE    MAIN0020                       NO
         CLC   CAN,SPF_ZCMD                   CAN?
         BE    MAIN0300                       YES
         CLC   CANCEL,SPF_ZCMD                CANCEL?
         BE    MAIN0300                       YES
         B     ERR0020                        OTHERWISE BAD COMMAND
* ------------------------------------------------------------------- *
*        Verify numeric                                               *
* ------------------------------------------------------------------- *
MAIN0020 DS    0H
         ITRACE ID=VERIFY,                                             +
               DATA1=(SPF_8,8)
         LA    R1,SPF_8                       1ST CHARACTER
         LA    R2,L'SPF_8                     NUMBER OF DIGITS
MAIN0030 DS    0H
         CLI   0(R1),C' '                     BLANK?
         BNE   MAIN0040                       NO
         LA    R1,1(,R1)                      NEXT
         BCT   R2,MAIN0030                    LOOP
         B     MAIN0060                       ALL BLANK
MAIN0040 DS    0H
         LR    R3,R1                          SAVE ADDR OF NON-BLANK
         LR    R4,R2                          SAVE DIGITS LEFT
MAIN0050 DS    0H
         CLI   0(R1),C' '                     BLANK?
         BE    MAIN0060                       YES
         CLI   0(R1),C'0'                     VALID DIGIT?
         BL    ERR0010                        NO
         CLI   0(R1),C'9'                     VALID DIGIT?
         BH    ERR0010                        NO
         LA    R1,1(,R1)                      NEXT DIGIT
         BCT   R2,MAIN0050                    LOOP
* ------------------------------------------------------------------- *
*        Convert new value to binary                                  *
* ------------------------------------------------------------------- *
MAIN0060 DS    0H
         ITRACE ID=CONVERT
         MVC   DXD_NUMBER,=C'000000000'       INITIALIZE WORK AREA
MAIN0100 DS    0H
         CLI   0(R3),C' '                     BLANK?
         BE    MAIN0110                       YES
         MVC   DXD_NUMBER(8),DXD_NUMBER+1     SHIFT LEFT 1 DIGIT
         MVC   DXD_NUMBER+8(1),0(R3)          COPY TO DIGITS POSITION
         LA    R3,1(,R3)                      NEXT DIGIT
         BCT   R4,MAIN0100                    LOOP
MAIN0110 DS    0H
         ITRACE ID=PACK,                                               +
               DATA1=DXD_NUMBER+1
         PACK  COMM_DWORD,DXD_NUMBER          PACK THE NUMBER
         CVB   R0,COMM_DWORD                  CONVERT TO BINARY
         C     R0,=A(X'007FFFFF')             TOO LARGE?
         BH    ERR0030                        YES
         STCM  R0,7,0(R6)                     CHANGE DATA
* ------------------------------------------------------------------- *
*        Does user want to exit?                                      *
* ------------------------------------------------------------------- *
         OC    COMM_DISP_RC,COMM_DISP_RC      RC = 0?
         BZ    MAIN0000                       YES
* ------------------------------------------------------------------- *
*        Exiting.  Has data changed?                                  *
* ------------------------------------------------------------------- *
MAIN0200 DS    0H
         CLC   DXD_DATA,0(R6)                 DATA UPDATED?
         BE    EXIT0000                       NO
         ITRACE ID=UPDATE
         OI    COMM_FLAGS,$COMM_UPDATE        UPDATE PENDING
         OI    VTOC_FLAGS,$VTOC_UPDATED       THIS DSCB UPDATED
         B     EXIT0000
* ------------------------------------------------------------------- *
*        User wants to cancel changes                                 *
* ------------------------------------------------------------------- *
MAIN0300 DS    0H
         ITRACE ID=CANCEL
         MVC   SPF_ZCMD,COMM_BLANKS
         CLC   DXD_DATA,0(R6)                 DATA CHANGED?
         BE    EXIT0000                       NO
         MVC   SPF_MSG_1(EMSG02_L),EMSG02     WARN THE USER
         MVC   0(3,R6),DXD_DATA               RESTORE DATA
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0010  DS    0H
         ITRACE ID=BADDIGIT
         MVC   SPF_MSG_1(EMSG03_L),EMSG03     COPY MESSAGE
         B     MAIN0010
ERR0020  DS    0H
         MVC   SPF_MSG_1(EMSG04_L),EMSG04     COPY MESSAGE
         B     MAIN0010
ERR0030  DS    0H
         MVC   SPF_MSG_1(EMSG05_L),EMSG05     COPY MESSAGE
         B     MAIN0010
* ------------------------------------------------------------------- *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=EXIT
         ICM   R1,15,COMM_VDATA_ADDR          VDATA GETMAINED?
         L     R0,COMM_VDATA_LENGTH           LENGTH
         BZ    EXIT0010                       NO
         FREEMAIN RU,                         FREEMAIN VDATA           +
               A=(1),                                                  +
               LV=(0)
EXIT0010 DS    0H
         L     R13,4(,R13)                    RESTORE SAVE AREA ADDRESS
         LM    R14,R12,12(R13)                RESTORE REGISTERS
         SR    R15,R15                        SET RC
         BR    R14                            RETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXD_START     DC    Q(DXDNBR3)
              LTORG

CAN           DC    C'CAN '
CANCEL        DC    C'CANCEL '
NBR3_PANEL    DC    CL8'VZAPNBR3'
EDITWORD      DC    X'40202020202020202120'

EMSG02        DS    0C
              DC    C'VZAPNBR302W Changes cancelled'
EMSG02_L      EQU   *-EMSG02

EMSG03        DS    0C
              DC    C'VZAPNBR303E Non numeric digit'
EMSG03_L      EQU   *-EMSG03

EMSG04        DS    0C
              DC    C'VZAPNBR304E Invalid command'
EMSG04_L      EQU   *-EMSG04

EMSG05        DS    0C
              DC    C'VZAPNBR305E Must be less than 32768'
EMSG05_L      EQU   *-EMSG05

DSCBEND       EQU   *
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDNBR3       DSECT
              COPY  DXDPREF

DXD_EDITWORD  DS    CL10
DXD_NUMBER    DS    CL9

DXD_DATA      DS    XL3
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY     VZAPFLD
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY     VTOCDATA
         ORG      VTOC_DSCB
         IECSDSL1 1
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         VZAPSPFD TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY     ATTRS
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY     REGEQU
         END      VZAPNBR3
