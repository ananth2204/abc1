* ------------------------------------------------------------------- *
*                                                                     *
*  Module name: VZAPDATE                                              *
*                                                                     *
*   DISPLAY/UPDATE Date  (discontiguous binary)                       *
*                                                                     *
* ------------------------------------------------------------------- *
VZAPDATE CSECT
VZAPDATE AMODE 31
VZAPDATE RMODE ANY
         USING VZAPCOMM,R12
         USING VZAPDATE,R15
         B     INIT0000
MODID    DC    CL8'VZAPDATE'
         DC    CL8'&SYSDATE'
         DC    CL8'&SYSTIME'
         DC    A(DSCBEND-VZAPDATE)
INIT0000 DS    0H
         STM   R14,R12,12(R13)                SAVE REGS
         LR    R10,R15                        COPY ENTRY POINT
         LR    R2,R0                          COPY R0
         LR    R3,R1                          COPY R1
         DROP  R15
         USING VZAPDATE,R10                   DEFINE BASE
         L     R15,COMM_DXD
         A     R15,DXD_START
         ST    R13,4(,R15)                    SAVE R13
         ST    R15,8(,R13)                    CHAIN SAVE AREA
         LR    R13,R15                        COPY WORK AREA ADDRESS
         USING DXDDATE,R13                    DEFINE WORK AREA BASE
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY
         L     R9,COMM_VZAPSPFD
         USING VZAPSPFD,R9
         L     R8,COMM_SELECTED_DSCB          SELECTED DSCB
         USING VTOCDATA,R8
         L     R5,COMM_SELECTED_FIELD         SELECTED FIELD
         USING FIELD_DSECT,R5
         ITRACE ID=FIELD,                                              +
               DATA1=FIELD_LABEL,                                      +
               DATA2=(FIELD_DISP,2)
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         SR    R6,R6
         ICM   R6,3,FIELD_DISP                DISP TO FIELD
         AR    R6,R8                          COMPUTE DATA ADDRESS
         ITRACE ID=DATA,                                               +
               RDATA1=R6,                                              +
               DATA2=(0(R6),2)
         MVC   DXD_DATE,0(R6)                 SAVE DATE
* ------------------------------------------------------------------- *
*        Build detail                                                 *
* ------------------------------------------------------------------- *
MAIN0000 DS    0H
         ITRACE ID=BUILD
         SR    R0,R0
         IC    R0,0(R6)                       YEAR
         CVD   R0,COMM_DWORD                  CONVERT TO DECIMAL
         MVC   DXD_EDITWORD,EDITWORD          INITIALIZE EDIT WORD
         ED    DXD_EDITWORD,COMM_DWORD+5      EDIT
         MVC   SPF_2,DXD_EDITWORD+4           COPY EDITED YEAR
         ICM   R0,3,1(R6)                     DAY OF YEAR
         CVD   R0,COMM_DWORD                  CONVERT TO DECIMAL
         MVC   DXD_EDITWORD,EDITWORD          INITIALIZE EDIT WORD
         ED    DXD_EDITWORD,COMM_DWORD+5      EDIT
         MVC   SPF_3,DXD_EDITWORD+3           COPY EDITED DAY OF YEAR
* ------------------------------------------------------------------- *
*        Display                                                      *
* ------------------------------------------------------------------- *
MAIN0010 DS    0H
         ITRACE ID=DISPLAY
         MVC   COMM_PANEL,DATE_PANEL          COPY PANEL NAME
         XC    COMM_VDATA_ADDR,COMM_VDATA_ADDR
         XC    COMM_VDATA_LENGTH,COMM_VDATA_LENGTH
         XC    COMM_VDATA_CURSOR,COMM_VDATA_CURSOR
         L     R15,COMM_V_VZAPDISP            VZAPDISP ENTRY POINT
         BALR  R14,R15                        CALL VZAPDISP
         ITRACE ID=DISP_RC,                                            +
               DATA1=(COMM_DISP_RC,2)
         MVC   DXD_DISP_RC,COMM_DISP_RC       SAVE RC
* ------------------------------------------------------------------- *
*        Does user want to cancel changes?                            *
* ------------------------------------------------------------------- *
         ITRACE ID=ZCMD,                                               +
               DATA1=SPF_ZCMD+0,                                       +
               DATA2=SPF_ZCMD+8
         CLC   SPF_ZCMD,COMM_BLANKS           COMMAND PRESENT?
         BE    MAIN0020                       NO
         CLC   CAN,SPF_ZCMD                   CAN?
         BE    MAIN0300                       YES
         CLC   CANCEL,SPF_ZCMD                CANCEL?
         BE    MAIN0300                       YES
         B     ERR0010                        OTHERWISE BAD COMMAND
* ------------------------------------------------------------------- *
*        Convert year                                                 *
* ------------------------------------------------------------------- *
MAIN0020 DS    0H
         ITRACE ID=YEAR,                                               +
               DATA1=(SPF_2,2)
         LA    R1,SPF_2                       1ST CHARACTER OF YEAR
         LA    R2,L'SPF_2                     NUMBER OF DIGITS
         BAL   R7,MAIN0400                    VERIFY/CONVERT YEAR
         ITRACE ID=NEW_DAY,                                            +
               RDATA1=R0
         STC   R0,0(R6)                       CHANGE YEAR
* ------------------------------------------------------------------- *
*        Convert day of year                                          *
* ------------------------------------------------------------------- *
         ITRACE ID=DAY,                                                +
               DATA1=(SPF_3,3)
         LA    R1,SPF_3                       1ST CHARACTER OF DAY
         LA    R2,L'SPF_3                     NUMBER OF DIGITS
         BAL   R7,MAIN0400                    VERIFY/CONVERT DAY
* ------------------------------------------------------------------- *
*        Verify day of year                                           *
* ------------------------------------------------------------------- *
         LTR   R0,R0                          YEAR ZERO?
         BNZ   MAIN0030                       NO
         ITRACE ID=ZERO_DAY                   DAY IS ZERO
         MVC   SPF_MSG_1(EMSG03_L),EMSG03
         L     R15,COMM_V_VZAPCNFM            VZAPCNFM ENTRY POINT
         ITRACE ID=CONFIRM,                                            +
               RDATA1=R15
         BALR  R14,R15                        CONFIRM
         CLI   SPF_YES_OR_NO,C'N'             DID USER SAY NO?
         BE    MAIN0010                       YES.. DISPLAY AGAIN
MAIN0030 DS    0H
         CH    R0,H366                        MORE THAN 366?
         BNH   MAIN0040                       NO
         MVC   SPF_MSG_1(EMSG04_L),EMSG04
         L     R15,COMM_V_VZAPCNFM            VZAPCNFM ENTRY POINT
         ITRACE ID=CONFIRM,                                            +
               RDATA1=R15
         BALR  R14,R15                        CONFIRM
         CLI   SPF_YES_OR_NO,C'N'             DID USER SAY NO?
         BE    MAIN0010                       YES.. DISPLAY AGAIN
MAIN0040 DS    0H
         ITRACE ID=NEW_YEAR,                                           +
               RDATA1=R0
         STCM  R0,3,1(R6)                     CHANGE DAY OF YEAR
* ------------------------------------------------------------------- *
*        Does user want to exit?                                      *
* ------------------------------------------------------------------- *
         OC    DXD_DISP_RC,DXD_DISP_RC        RC = 0?
         BZ    MAIN0000                       YES
* ------------------------------------------------------------------- *
*        EXITING.  HAS CREATION DATE CHANGED?                         *
* ------------------------------------------------------------------- *
MAIN0200 DS    0H
         CLC   DXD_DATE,0(R6)                 ANYTHING UPDATED?
         BE    EXIT0000                       NO
         ITRACE ID=UPDATE
         OI    COMM_FLAGS,$COMM_UPDATE        UPDATE PENDING
         OI    VTOC_FLAGS,$VTOC_UPDATED       THIS DSCB UPDATED
         B     EXIT0000
* ------------------------------------------------------------------- *
*        User wants to cancel changes                                 *
* ------------------------------------------------------------------- *
MAIN0300 DS    0H
         ITRACE ID=CANCEL
         MVC   SPF_ZCMD,COMM_BLANKS
         CLC   DXD_DATE,0(R6)                 DATE CHANGED?
         BE    EXIT0000                       NO
         MVC   SPF_MSG_1(EMSG05_L),EMSG05     WARN THE USER
         MVC   0(R6),DXD_DATE                 RESTORE DATE
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
*        Verify/convert numeric                                       *
*                                                                     *
*         R1   Data address                                           *
*         R2   Data length                                            *
*         R7   Return address                                         *
*                                                                     *
*        At exit R0 number in binary                                  *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0400 DS    0H
         ITRACE ID=VERIFY,                                             +
               RDATA1=R1,                                              +
               RDATA2=R2
MAIN0410 DS    0H
         CLI   0(R1),C' '                     BLANK?
         BNE   MAIN0420                       NO
         LA    R1,1(,R1)                      NEXT
         BCT   R2,MAIN0410                    LOOP
         MVC   SPF_MSG_1(EMSG01_L),EMSG01     COPY MESSAGE
         B     MAIN0010                       DISPLAY WITH MESSAGE
MAIN0420 DS    0H
         LR    R3,R1                          SAVE ADDR OF NON-BLANK
         LR    R4,R2                          SAVE DIGITS LEFT
MAIN0430 DS    0H
         CLI   0(R1),C' '                     BLANK?
         BE    MAIN0440                       YES
         CLI   0(R1),C'0'                     VALID DIGIT?
         BL    ERR0020                        NO
         CLI   0(R1),C'9'                     VALID DIGIT?
         BH    ERR0020                        NO
         LA    R1,1(,R1)                      NEXT DIGIT
         BCT   R2,MAIN0430                    LOOP
* ------------------------------------------------------------------- *
*        Convert new value to binary                                  *
* ------------------------------------------------------------------- *
MAIN0440 DS    0H
         ITRACE ID=CONVERT
         MVC   DXD_NUMBER,=C'00000'           INITIALIZE WORK AREA
MAIN0450 DS    0H
         CLI   0(R3),C' '                     BLANK?
         BE    MAIN0460                       YES
         MVC   DXD_NUMBER(4),DXD_NUMBER+1     SHIFT LEFT 1 DIGIT
         MVC   DXD_NUMBER+4(1),0(R3)          COPY TO DIGITS POSITION
         LA    R3,1(,R3)                      NEXT DIGIT
         BCT   R4,MAIN0450                    LOOP
MAIN0460 DS    0H
         PACK  COMM_DWORD,DXD_NUMBER          PACK THE NUMBER
         CVB   R0,COMM_DWORD                  CONVERT TO BINARY
         BR    R7
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0010  DS    0H
         ITRACE ID=BAD_CMD
         MVC   SPF_MSG_1(EMSG01_L),EMSG01     COPY MESSAGE
         B     MAIN0010
ERR0020  DS    0H
         MVC   SPF_MSG_1(EMSG02_L),EMSG02     COPY MESSAGE
         B     MAIN0010
* ------------------------------------------------------------------- *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=EXIT
         ICM   R1,15,COMM_VDATA_ADDR          VDATA GETMAINED?
         L     R0,COMM_VDATA_LENGTH           LENGTH
         BZ    EXIT0010                       NO
         FREEMAIN RU,                         FREEMAIN VDATA           +
               A=(1),                                                  +
               LV=(0)
EXIT0010 DS    0H
         L     R13,4(,R13)                    RESTORE SAVE AREA ADDRESS
         LM    R14,R12,12(R13)                RESTORE REGISTERS
         SR    R15,R15                        SET RC
         BR    R14                            RETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXD_START     DC    Q(DXDDATE)
              LTORG

H99           DC    H'99'
H366          DC    H'366'

CAN           DC    C'CAN '
CANCEL        DC    C'CANCEL '
DATE_PANEL    DC    CL8'VZAPDATE'
EDITWORD      DC    X'F02020202120'

EMSG01        DS    0C
              DC    C'VZAPDATE01E Invalid command'
EMSG01_L      EQU   *-EMSG01

EMSG02        DS    0C
              DC    C'VZAPDATE02E Non-numeric digit'
EMSG02_L      EQU   *-EMSG02

EMSG03        DS    0C
              DC    C'VZAPDATE03W Day of year is zero.  Please confirm'
EMSG03_L      EQU   *-EMSG03

EMSG04        DS    0C
              DC    C'VZAPDATE04W Day of year > 366.  Please confirm'
EMSG04_L      EQU   *-EMSG04

EMSG05        DS    0C
              DC    C'VZAPDATE05W Changes to date cancelled'
EMSG05_L      EQU   *-EMSG05

DSCBEND       EQU   *
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDDATE       DSECT
              COPY  DXDPREF

DXD_DISP_RC   DS    XL2

DXD_EDITWORD  DS    CL6
DXD_NUMBER    DS    CL5

DXD_DATE      DS    XL3               SAVED DATE
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY     VTOCDATA
         ORG      VTOC_DSCB
         IECSDSL1 1
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY     VZAPFLD
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         VZAPSPFD TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY     ATTRS
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY     REGEQU
         END      VZAPDATE
