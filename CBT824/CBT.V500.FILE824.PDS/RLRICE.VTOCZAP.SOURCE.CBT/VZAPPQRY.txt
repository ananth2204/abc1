* ------------------------------------------------------------------- *
*                                                                     *
*  Module name: VZAPPQRY                                              *
*                                                                     *
*   Issue a PQUERY to determine the dimensions of the dynamic         *
*   area on an SPF panel.  Panel name is in COMM_PANEL.               *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
VZAPPQRY CSECT
VZAPPQRY AMODE 31
VZAPPQRY RMODE ANY
         USING VZAPCOMM,R12
         USING VZAPPQRY,R15
         B     INIT0000
MODID    DC    CL8'VZAPPQRY'
         DC    CL8'&SYSDATE'
         DC    CL8'&SYSTIME'
         DC    A(PQRYEND-VZAPPQRY)
INIT0000 DS    0H
         STM   R14,R12,12(R13)                SAVE REGS
         LR    R10,R15                        COPY ENTRY POINT
         DROP  R15
         USING VZAPPQRY,R10                   DEFINE BASE
         L     R15,COMM_DXD
         A     R15,DXD_START
         ST    R13,4(,R15)                    SAVE R13
         ST    R15,8(,R13)                    CHAIN SAVE AREA
         LR    R13,R15                        COPY WORK AREA ADDRESS
         USING DXDPQRY,R13                    DEFINE WORK AREA BASE
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY,                                              +
               DATA1=COMM_PANEL
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         L     R9,COMM_VZAPSPFD               SPF DATA
         USING VZAPSPFD,R9                    DEFINE BASE
         LA    R0,PQUERY                      COMMAND'S ADDRESS
         ST    R0,WORK_SPF_1                  SET IN PARM LIST
         LA    R0,COMM_PANEL                  PANEL NAME ADDRESS
         ST    R0,WORK_SPF_2                  SET ADDR OF PANEL NAME
         LA    R0,CVDATA                      AREA NAME
         ST    R0,WORK_SPF_3                  SET IN PARM LIST
         LA    R0,CVTYPE                      AREA TYPE
         ST    R0,WORK_SPF_4                  SET IN PARM LIST
         LA    R0,CVWIDTH                     SCREEN DEPTH
         ST    R0,WORK_SPF_5                  SET IN PARM LIST
         LA    R0,CVDEPTH                     SCREEN WIDTH
         ST    R0,WORK_SPF_6                  SET IN PARM LIST
         LA    R0,CVROW                       SCREEN ROW
         ST    R0,WORK_SPF_7                  SET IN PARM LIST
         LA    R0,CVCOL                       SCREEN COLUMN
         ST    R0,WORK_SPF_8                  SET IN PARM LIST
         OI    WORK_SPF_8,X'80'               FLAG 'END-OF-LIST'
         LA    R1,WORK_SPF_PARMS              PQUERY PARM LIST ADDRESS
         L     R15,COMM_ISPLINK               SPF INTERFACE'S ADDRESS
         BALR  R14,R15                        ISSUE QUERY
         ITRACE ID=PQUERYRC,                                           +
               RDATA1=R15
         LTR   R15,R15                        PQUERY SUCCESSFUL?
         Bz    EXIT0000                       YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0010  DS    0H
         ITRACE ID=PQRYFAIL
         DC    H'1'
         DC    C'PQUERY FAILED'
* ------------------------------------------------------------------- *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=EXIT
         L     R13,4(,R13)                    RESTORE SAVE AREA ADDRESS
         LM    R14,R12,12(R13)                RESTORE REGISTERS
         SR    R15,R15                        SET RC
         BR    R14                            RETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXD_START     DC    Q(DXDPQRY)
PQUERY        DC    CL8'PQUERY'
CVDATA        DC    CL8'VDATA'
CVDEPTH       DC    CL8'VDEPTH'
CVTYPE        DC    CL8'VTYPE'
CVWIDTH       DC    CL8'VWIDTH'
CVROW         DC    CL8'VROW'
CVCOL         DC    CL8'VCOL'
              LTORG
PQRYEND       EQU   *
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDPQRY           DSECT
                  COPY    DXDPREF

WORK_SPF_PARMS    DS      0A     SPF PARM LIST
WORK_SPF_1        DS      A
WORK_SPF_2        DS      A
WORK_SPF_3        DS      A
WORK_SPF_4        DS      A
WORK_SPF_5        DS      A
WORK_SPF_6        DS      A
WORK_SPF_7        DS      A
WORK_SPF_8        DS      A
WORK_SPF_9        DS      A
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         VZAPSPFD TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY     REGEQU
         END      VZAPPQRY
