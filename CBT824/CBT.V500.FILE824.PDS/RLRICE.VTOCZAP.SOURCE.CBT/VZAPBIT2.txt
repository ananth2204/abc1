* ------------------------------------------------------------------- *
*                                                                     *
*  Module name: VZAPBIT2                                              *
*                                                                     *
*   Display/update 2 byte fields that are bit flags                   *
*                                                                     *
*   In this case the bit table is assumed to be 8 bits for two bytes  *
*   There is no "FF" to flag end of table.                            *
*                                                                     *
* ------------------------------------------------------------------- *
VZAPBIT2 CSECT
VZAPBIT2 AMODE 31
VZAPBIT2 RMODE ANY
         USING VZAPCOMM,R12
         USING VZAPBIT2,R15
         B     INIT0000
MODID    DC    CL8'VZAPBIT2'
         DC    CL8'&SYSDATE'
         DC    CL8'&SYSTIME'
         DC    A(DSCBEND-VZAPBIT2)
INIT0000 DS    0H
         STM   R14,R12,12(R13)                SAVE REGS
         LR    R10,R15                        COPY ENTRY POINT
         LR    R2,R0                          COPY R0
         LR    R3,R1                          COPY R1
         DROP  R15
         USING VZAPBIT2,R10                   DEFINE BASE
         L     R15,COMM_DXD
         A     R15,DXD_START
         ST    R13,4(,R15)                    SAVE R13
         ST    R15,8(,R13)                    CHAIN SAVE AREA
         LR    R13,R15                        COPY WORK AREA ADDRESS
         USING DXDBIT2,R13                    DEFINE WORK AREA BASE
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY
         L     R9,COMM_VZAPSPFD
         USING VZAPSPFD,R9
         L     R8,COMM_SELECTED_DSCB          SELECTED DSCB
         USING VTOCDATA,R8
* ------------------------------------------------------------------- *
*        Compute data address                                         *
* ------------------------------------------------------------------- *
         L     R5,COMM_SELECTED_FIELD         SELECTED FIELD
         USING FIELD_DSECT,R5
         ITRACE ID=FIELD,                                              +
               DATA1=FIELD_LABEL,                                      +
               DATA2=(FIELD_DISP,2)
         SR    R4,R4
         ICM   R4,3,FIELD_DISP                DISP TO DATA
         AR    R4,R8                          PLUS BASE
* ------------------------------------------------------------------- *
*        Copy data to allow user to abort                             *
* ------------------------------------------------------------------- *
         ITRACE ID=DATA,                                               +
               RDATA1=R4,                                              +
               DATA2=(0(R4),1)
         MVC   DXD_DATA,0(R1)                 COPY DATA
* ------------------------------------------------------------------- *
*        Obtain VDATA for dynamic area                                *
* ------------------------------------------------------------------- *
         MVC   COMM_PANEL,FIELD_PANEL         COPY PANEL NAME
         ITRACE ID=PQRY,                                               +
               DATA1=COMM_PANEL
         L     R15,COMM_V_VZAPPQRY            VZAPPQRY ENTRY POINT
         BALR  R14,R15                        CALL VTOPQRY
         L     R0,SPF_VDEPTH                  NUMBER OF ROWS
         MH    R0,SPF_VWIDTH+2                COMPUTE VDATA SIZE
         ST    R0,COMM_VDATA_LENGTH           SAVE LENGTH
         GETMAIN RU,                          GETMAIN STORAGE FOR VDATA+
               LV=(0),                                                 +
               LOC=ANY
         ST    R1,COMM_VDATA_ADDR             SAVE ADDR
         ITRACE ID=VDATA,                                              +
               RDATA1=R1,                                              +
               DATA2=(COMM_VDATA_LENGTH,4)
* ------------------------------------------------------------------- *
*        Initialize dyamic area                                       *
* ------------------------------------------------------------------- *
MAIN0010 DS    0H
         ITRACE ID=INITVDATA
         L     R0,COMM_VDATA_ADDR             VDATA ADDR
         L     R1,COMM_VDATA_LENGTH           VDATA LENGTH
         SR    R14,R14
         SR    R15,R15
         MVCL  R0,R14                         INITIALIZE VDATA BUFFER
* ------------------------------------------------------------------- *
*        Build detail                                                 *
* ------------------------------------------------------------------- *
         ITRACE ID=BUILD
         L     R7,COMM_VDATA_ADDR             DYNAMIC AREA BUFFER
         MVC   0(MSG01_L,R7),MSG01
         USING MSG01,R7
         UNPK  COMM_DWORD(5),0(3,R4)          UNPACK FLAG
         TR    COMM_DWORD(4),COMM_HEXCHAR     TRANSLATE
         MVC   MSG01_HEX,COMM_DWORD           COPY TO VDATA BUFFER

         LA    R1,MSG01_BINARY1               BINARY BIT FLAGS
         LA    R2,X'80'                       BIT 1000 0000
         LA    R3,8                           BITS IN A BYTE
MAIN0020 DS    0H
         EX    R2,BIT_TM1                     THIS BIT ON?
         BNO   MAIN0030                       NO
         MVI   0(R1),C'1'                     INDICATE BIT IS ON
MAIN0030 DS    0H
         LA    R1,1(,R1)                      NEXT
         SRL   R2,1                           SHIFT BIT
         BCT   R3,MAIN0020                    LOOP

         LA    R1,MSG01_BINARY2               BINARY BIT FLAGS
         LA    R2,X'80'                       BIT 1000 0000
         LA    R3,8                           BITS IN A BYTE
MAIN0040 DS    0H
         EX    R2,BIT_TM2                     THIS BIT ON?
         BNO   MAIN0050                       NO
         MVI   0(R1),C'1'                     INDICATE BIT IS ON
MAIN0050 DS    0H
         LA    R1,1(,R1)                      NEXT
         SRL   R2,1                           SHIFT BIT
         BCT   R3,MAIN0040                    LOOP
         DROP  R7

         A     R7,SPF_VWIDTH                  NEXT LINE IN VDATA

         L     R6,COMM_BIT_TABLE              BIT TABLE
         USING BIT_DSECT,R6                   DEFINE BASE
         LA    R5,8                           BITS PER BYTE
MAIN0060 DS    0H
         ITRACE ID=BIT,                                                +
               DATA1=BIT_NAME
         MVC   0(MSG02_L,R7),MSG02
         USING MSG02,R7                       DEFINE BASE
         MVC   MSG02_NAME,BIT_NAME            COPY NAME
         MVC   MSG02_DESC,BIT_DESC            COPY DESCRIPTION
         SR    R1,R1
         IC    R1,BIT_FLAG                    BITS TO TEST
         EX    R1,BIT_TM1                     BIT ON?
         BNO   MAIN0070                       NO
         MVI   MSG02_X,C'X'                   INDICATE BIT IS ON
         MVI   MSG02_ATTR_1,$SCREEN_ATTR_HIGH CHANGE ATTRIBUTE
         MVI   MSG02_ATTR_2,$SCREEN_ATTR_HIGH_TURQUOISE
         DROP  R7
MAIN0070 DS    0H
         A     R7,SPF_VWIDTH                  NEXT LINE
         LA    R6,BIT_DSECT_L(,R6)            NEXT BIT
         BCT   R5,MAIN0060

         A     R7,SPF_VWIDTH                  NEXT LINE IN VDATA

         LA    R5,8                           BITS PER BYTE
MAIN0080 DS    0H
         ITRACE ID=BIT,                                                +
               DATA1=BIT_NAME
         MVC   0(MSG02_L,R7),MSG02
         USING MSG02,R7                       DEFINE BASE
         MVC   MSG02_NAME,BIT_NAME            COPY NAME
         MVC   MSG02_DESC,BIT_DESC            COPY DESCRIPTION
         SR    R1,R1
         IC    R1,BIT_FLAG                    BITS TO TEST
         EX    R1,BIT_TM2                     BIT ON?
         BNO   MAIN0090                       NO
         MVI   MSG02_X,C'X'                   INDICATE BIT IS ON
         MVI   MSG02_ATTR_1,$SCREEN_ATTR_HIGH CHANGE ATTRIBUTE
         MVI   MSG02_ATTR_2,$SCREEN_ATTR_HIGH_TURQUOISE
         DROP  R6,R7
MAIN0090 DS    0H
         A     R7,SPF_VWIDTH                  NEXT LINE
         LA    R6,BIT_DSECT_L(,R6)            NEXT BIT
         BCT   R5,MAIN0080                    LOOP
* ------------------------------------------------------------------- *
*        Display                                                      *
* ------------------------------------------------------------------- *
MAIN0100 DS    0H
         ITRACE ID=DISPLAY
         MVC   COMM_PANEL,FIELD_PANEL         COPY PANEL NAME
         MVC   COMM_VDATA_ADDR,COMM_VDATA_ADDR
         MVC   COMM_VDATA_LENGTH,COMM_VDATA_LENGTH
         MVC   COMM_VDATA_CURSOR,COMM_VDATA_CURSOR
         L     R15,COMM_V_VZAPDISP            VZAPDISP ENTRY POINT
         BALR  R14,R15                        CALL VZAPDISP
         ITRACE ID=DISP_RC,                                            +
               DATA1=(COMM_DISP_RC,2)
         OC    COMM_DISP_RC,COMM_DISP_RC      RC = 0?
         BNZ   MAIN0200                       NO
* ------------------------------------------------------------------- *
*        Does user want to cancel changes?                            *
* ------------------------------------------------------------------- *
         CLC   SPF_ZCMD,COMM_BLANKS           COMMAND BLANK?
         BE    MAIN0110                       YES
         CLC   CAN,SPF_ZCMD                   CAN?
         BE    MAIN0300                       YES
         CLC   CANCEL,SPF_ZCMD                CANCEL?
         BE    MAIN0300                       YES
         B     ERR0010                        OTHERWISE BAD COMMAND
* ------------------------------------------------------------------- *
*        Change bit(s)                                                *
* ------------------------------------------------------------------- *
MAIN0110 DS    0H
         L     R6,COMM_BIT_TABLE              FIRST BIT FLAG
         USING BIT_DSECT,R6
         L     R7,COMM_VDATA_ADDR             FIRST IN DYNAMIC AREA
         A     R7,SPF_VWIDTH                  BITS START ON 2ND LINE
         USING MSG02,R7
         XC    0(2,R4),0(R4)                  RESET ALL FLAGS
         LA    R5,8                           8 BITS PER BYTE
MAIN0120 DS    0H
         ITRACE ID=UPDATE1,                                            +
               DATA1=BIT_NAME,                                         +
               DATA2=(MSG02_X,1)
         CLI   MSG02_X,C' '                   BLANK?
         BE    MAIN0130                       YES
         SR    R1,R1
         IC    R1,BIT_FLAG                    BIT(S) TO SET
         EX    R1,BIT_OI1                     SET BIT(S)
MAIN0130 DS    0H
         A     R7,SPF_VWIDTH                  NEXT IN DYNAMIC AREA
         LA    R6,BIT_DSECT_L(,R6)            NEXT BIT
         BCT   R5,MAIN0120                    LOOP

         A     R7,SPF_VWIDTH

         LA    R5,8                           8 BITS PER BYTE
MAIN0140 DS    0H
         ITRACE ID=UPDATE2,                                            +
               DATA1=BIT_NAME,                                         +
               DATA2=(MSG02_X,1)
         CLI   MSG02_X,C' '                   BLANK?
         BE    MAIN0150                       YES
         SR    R1,R1
         IC    R1,BIT_FLAG                    BIT(S) TO SET
         EX    R1,BIT_OI2                     SET BIT(S)
MAIN0150 DS    0H
         A     R7,SPF_VWIDTH                  NEXT IN DYNAMIC AREA
         LA    R6,BIT_DSECT_L(,R6)            NEXT BIT
         BCT   R5,MAIN0140                    LOOP

         B     MAIN0010                       REBUILD DISPLAY
         DROP  R7
* ------------------------------------------------------------------- *
*        Exiting.  Have flags been changed?                           *
* ------------------------------------------------------------------- *
MAIN0200 DS    0H
         CLC   DXD_DATA,0(R4)                 ANYTHING UPDATED?
         BE    EXIT0000                       NO
         ITRACE ID=UPDATE
         OI    COMM_FLAGS,$COMM_UPDATE        UPDATE PENDING
         OI    VTOC_FLAGS,$VTOC_UPDATED       THIS DSCB UPDATED
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0300 DS    0H
         ITRACE ID=CANCEL
         MVC   SPF_ZCMD,COMM_BLANKS
         CLC   DXD_DATA,0(R4)                 FLAGS CHANGED?
         BE    EXIT0000                       NO
         MVC   SPF_MSG_1(EMSG01_L),EMSG01     WARN THE USER
         MVC   0(2,R4),DXD_DATA               RESET FLAGS
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0010  DS    0H
         MVC   SPF_MSG_1(EMSG02_L),EMSG02     COPY MESSAGE
         B     MAIN0100
* ------------------------------------------------------------------- *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=EXIT
         ICM   R1,15,COMM_VDATA_ADDR          VDATA GETMAINED?
         L     R0,COMM_VDATA_LENGTH           LENGTH
         BZ    EXIT0010                       NO
         FREEMAIN RU,                         FREEMAIN VDATA           +
               A=(1),                                                  +
               LV=(0)
EXIT0010 DS    0H
         L     R13,4(,R13)                    RESTORE SAVE AREA ADDRESS
         LM    R14,R12,12(R13)                RESTORE REGISTERS
         SR    R15,R15                        SET RC
         BR    R14                            RETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
BIT_TM1  TM    0(R4),0                        TEST BIT(S) IN BYTE 1
BIT_TM2  TM    1(R4),0                        TEST BIT(S) IN BYTE 2
BIT_OI1  OI    0(R4),0                        SET BIT(S)  IN BYTE 1
BIT_OI2  OI    1(R4),0                        SET BIT(S)  IN BYTE 2
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXD_START     DC    Q(DXDBIT2)
              LTORG

CAN           DC    C'CAN '
CANCEL        DC    C'CANCEL '
FIELD_PANEL   DC    CL8'VZAPFLD'

MSG01         DS    0C
              DC    AL1($SCREEN_ATTR_HIGH_GREEN)
MSG01_HEX     DC    CL4' '
              DC    AL1($SCREEN_ATTR_HIGH_YELLOW)
              DC    C'(HEX)    '
MSG01_BINARY1 DC    CL8'00000000'
              DC    C' '
MSG01_BINARY2 DC    CL8'00000000'
              DC    C' (BINARY)'
MSG01_L       EQU   *-MSG01

MSG02         DS    0C
              DC    AL1($SCREEN_ATTR_INPUT)
MSG02_X       DC    C' '
MSG02_ATTR_1  DC    AL1($SCREEN_ATTR_HIGH_GREEN)
MSG02_NAME    DC    CL15' '
MSG02_ATTR_2  DC    AL1($SCREEN_ATTR_NORMAL)
MSG02_DESC    DC    CL50' '
MSG02_L       EQU   *-MSG02

EMSG01        DS    0C
              DC    C'VZAPBIT201W Updates cancelled'
EMSG01_L      EQU   *-EMSG01

EMSG02        DS    0C
              DC    C'VZAPBIT202E Invalid command'
EMSG02_L      EQU   *-EMSG02

DSCBEND       EQU   *
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDBIT2       DSECT
              COPY  DXDPREF


DXD_DATA      DS    XL2               SAVED DATA
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
BIT_DSECT     DSECT
BIT_FLAG      DS    X
BIT_NAME      DS    CL15
BIT_DESC      DS    CL50
BIT_DSECT_L   EQU   *-BIT_DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY     VZAPFLD
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY     VTOCDATA
         ORG      VTOC_DSCB
         IECSDSL1 1
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         VZAPSPFD TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY     ATTRS
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY     REGEQU
         END      VZAPBIT2
