* ------------------------------------------------------------------- *
*                                                                     *
*  Module name: VZAPWRIT                                              *
*                                                                     *
*   Write the updated records back to the VTOC using CVAF.            *
*                                                                     *
* ------------------------------------------------------------------- *
VZAPWRIT CSECT
VZAPWRIT AMODE 31
VZAPWRIT RMODE 24
         USING VZAPCOMM,R12
         USING VZAPWRIT,R15
         B     INIT0000
MODID    DC    CL8'VZAPWRIT'
         DC    CL8'&SYSDATE'
         DC    CL8'&SYSTIME'
         DC    A(DISPEND-VZAPWRIT)
INIT0000 DS    0H
         STM   R14,R12,12(R13)                  SAVE REGS
         LR    R10,R15                          COPY ENTRY POINT
         LR    R2,R0                            COPY R0
         LR    R3,R1                            COPY R1
         DROP  R15
         USING VZAPWRIT,R10                     DEFINE BASE
         GETMAIN RU,                                                   +
               LV=WORK_L,                                              +
               LOC=BELOW
         LR    R2,R1
         LA    R3,WORK_L
         SR    R14,R14
         SR    R15,R15
         MVCL  R2,R14                           INITIALIZE WORK AREA
         ST    R13,4(,R1)                       SAVE R13
         ST    R1,8(,R13)                       CHAIN SAVE AREA
         LR    R13,R1                           COPY WORK AREA ADDRESS
         USING WORK,R13                         DEFINE WORK AREA BASE
         USING JFCB,WORK_JFCB
         USING CVPL,WORK_CVAFDIR
         MVC   DXD_CSECT,MODID
INIT0010 DS    0H
         ITRACE ID=ENTRY
         MVI   WORK_RC,0                      SET DEFAULT RC
         MVI   WORK_FLAGS,0                   SET FLAGS
* ------------------------------------------------------------------- *
*        Verify user is allowed to update the VTOC                    *
* ------------------------------------------------------------------- *
         ITRACE ID=AUTH
         MVI   COMM_VTOCZAP_FUNCTION,$VTOCZAP_UPDATE_VTOC
         L     R15,COMM_V_VZAPAUTH            VZAPAUTH ENTRY POINT
         BALR  R14,R15                        CALL VZAPAUTH
         ITRACE ID=AUTH_RC,                   AUTH RC                  +
               RDATA1=R15
         LTR   R15,R15                        USER AUTHORIZED?
         BNZ   ERR0020                        NO
* ------------------------------------------------------------------- *
*        Initialization                                               *
* ------------------------------------------------------------------- *
         MVC   WORK_DCB(DCB_L),DCB_I          INITIALIZE DCB
         LA    R9,WORK_DCB                    DCB ADDRESS
         USING IHADCB,R9
         ITRACE ID=DCB,                                                +
               RDATA1=R9
         MVC   DCBDDNAM+2(6),COMM_VOLSER      ADD VOLSER TO DD NAME
         LA    R0,WORK_EXLST                  EXLST ADDR
         STCM  R0,7,DCBEXLSA                  RELOCATE EXIT LIST
         LA    R0,WORK_JFCB                   JFCB ADDR
         ST    R0,WORK_EXLST                  SET ADDR FOR EXLST
         MVI   WORK_EXLST,X'87'               'RDJFCB'
         MVC   WORK_OPEN(OPEN_L),OPEN_I       INITIALIZE OPEN
* ------------------------------------------------------------------- *
*        Read the JFCB                                                *
* ------------------------------------------------------------------- *
         ITRACE ID=RDJFCB
         RDJFCB ((R9)),                       READ THE JFCB            +
               MF=(E,WORK_OPEN)
         ITRACE ID=RC,                                                 +
               RDATA1=R15
         LTR   R15,R15                        RC=0?
         BZ    INIT0020                       YES
         DC    H'0'                           CRASH AND BURN
* ------------------------------------------------------------------- *
*        Change DSN to x'04...                                        *
* ------------------------------------------------------------------- *
INIT0020 DS    0H
         MVI   JFCBDSNM,X'04'                 CHANGE DSN
         MVC   JFCBDSNM+1(L'JFCBDSNM-1),JFCBDSNM
         MVI   JFCBTSDM,JFCNWRIT              NO JFCB WRITE-BACK
* ------------------------------------------------------------------- *
*        APF authorize ourselves                                      *
* ------------------------------------------------------------------- *
         LA    R0,1                           REQUEST APF AUTHORIZATION
         L     R15,COMM_V_VZAPAPF
         BALR  R14,R15                        TURN ON APF AUTHORIZATION
* ------------------------------------------------------------------- *
*        Open the VTOC                                                *
* ------------------------------------------------------------------- *
         ITRACE ID=OPEN
         OPEN  ((R9),OUTPUT),                                          +
               TYPE=J,                                                 +
               MF=(E,WORK_OPEN)
         TM    DCBOFLGS,DCBOFOPN              DCB OPEN?
         BO    MAIN0000                       YES
         ITRACE ID=NOT_OPEN
         DC    H'0'
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0000 DS    0H
         SR    R8,R8
         ICM   R8,7,DCBDEBA                   DEB ADDRESS
         ICM   R7,15,COMM_VTOC_DATA           FIRST VTOCDATA BLOCK
         BZ    EXIT0000                       NO VTOC DATA
MAIN0010 DS    0H
         USING VTOCDATA,R7
         ITRACE ID=DSCB,                                               +
               RDATA1=R7,                                              +
               DATA2=(VTOC_FLAGS,1)
         TM    VTOC_FLAGS,$VTOC_UPDATED       THIS RECORD UPDATED?
         BNO   MAIN0020                       NO
         ITRACE ID=UPDATE,                                             +
               DATA1=VTOC_COUNT,                                       +
               DATA2=VTOC_DSCB
         XC    WORK_BUFLIST(BFLHLN+BFLELN),WORK_BUFLIST
         OI    BFLHFL,BFLHDSCB                DSCB IS BEING UPDATED
         MVC   BFLEARG,VTOC_COUNT             COPY DISK ADDR (CCHHR)
         MVI   BFLHNOE,1                      ONE BUFFER LIST ENTRY
         LA    R0,VTOC_DSCB+44                DSCB (MINUS DSN)
         ST    R0,BFLEBUF                     SET ADDRESS
         MVI   BFLELTH,96                     LENGTH OF DSCB BUFFER
         OI    BFLEFL,BFLECHR                 ARG IS CCHHR
         OI    BFLEFL,BFLENOVR                DO NOT VERIFY DSN
         MVC   WORK_CVAFDIR(CVAFDIRL),CVAFDIRI
         ITRACE ID=CVAFDIR
         OI    WORK_FLAGS,$RLSE               RLSE NEEDED
         CVAFDIR ACCESS=WRITE,                UPDATE VTOC              +
               BUFLIST=WORK_BUFLIST,          .. BUFFER PARMS          +
               DEB=(R8),                      .. DEB ADDRESS           +
               DSN=VTOC_DSCB,                 .. DSN                   +
               VERIFY=NO,                                              +
               MF=(E,WORK_CVAFDIR)
         ITRACE ID=RC,                                                 +
               RDATA1=R15
         LTR   R15,R15                        SUCCESSFUL?
         BNZ   ERR0030                        NO
         LA    R0,1                           CONSTANT
         A     R0,COMM_VTOC_WRITES            PLUS TOTAL SO FAR
         ST    R0,COMM_VTOC_WRITES            UPDATE TOTAL
MAIN0020 DS    0H
         ICM   R7,15,VTOC_NEXT                NEXT DSCB
         BNZ   MAIN0010                       LOOP
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0010  DS    0H
         ITRACE ID=SYNAD
         DC    H'0'
ERR0020  DS    0H
         ITRACE ID=NOT_AUTH
         MVI   WORK_RC,12
         B     EXIT0000
ERR0030  DS    0H
         LA    R2,WORK_CVAFDIR
         ITRACE ID=CVAF_ERR,                                           +
               RDATA1=R2,                                              +
               DATA2=CVPL
         DC    H'0'
* ------------------------------------------------------------------- *
*                                                                     *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
* ------------------------------------------------------------------- *
*        Release CVAF resources                                       *
* ------------------------------------------------------------------- *
         TM    WORK_FLAGS,$RLSE                 RLSE NEEDED?
         BNO   EXIT0010                         NO
         ITRACE ID=RELEASE
         CVAFDIR  ACCESS=RLSE,                                         +
               IOAREA=NOKEEP,                                          +
               IXRCDS=NOKEEP,                                          +
               BUFLIST=0,                                              +
               MF=(E,WORK_CVAFDIR)
         ITRACE ID=RC,                                                 +
               RDATA1=R15
         LTR   R15,R15
         BNZ   ERR0030
* ------------------------------------------------------------------- *
*        Close VTOC DCB                                               *
* ------------------------------------------------------------------- *
EXIT0010 DS    0H
         ITRACE ID=CLOSE
         MVC   WORK_CLOSE(CLOSE_L),CLOSE_I      INITIALIZE CLOSE
         CLOSE ((R9)),                          CLOSE VTOC DCB         +
               MF=(E,WORK_CLOSE)
* ------------------------------------------------------------------- *
*        Turn off APF authorization                                   *
* ------------------------------------------------------------------- *
         SR    R0,R0
         L     R15,COMM_V_VZAPAPF
         BALR  R14,R15                          TURN OFF APF AUTH
         SR    R2,R2
         IC    R2,WORK_RC
         ITRACE ID=EXIT,                                               +
               RDATA1=R2,                                              +
               DATA2=(COMM_VTOC_WRITES,1)
         LR    R1,R13
         L     R13,4(,R13)                      RESTORE R13
         FREEMAIN RU,                           FREE WORK AREA         +
               A=(1),                                                  +
               LV=WORK_L
         L     R14,12(,R13)
         LR    R15,R2
         LM    R0,R12,20(R13)                   RESTORE REGISTERS
         BR    R14                              RETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
SYNAD    SYNADAF ACSMETH=BDAM
         OI    WORK_FLAGS,$SYNAD
         SYNADRLS
         BR    R14
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DCB_I    DCB   DSORG=DA,                                               +
               DDNAME=VZ,                                              +
               EXLST=*,                                                +
               RECFM=F,                                                +
               BLKSIZE=96,                                             +
               KEYLEN=44,                                              +
               OPTCD=A,                                                +
               SYNAD=SYNAD,                                            +
               MACRF=WKC
DCB_L    EQU   *-DCB_I

OPEN_I   OPEN  (*,OUTPUT),                                             +
               MF=L
OPEN_L   EQU   *-OPEN_I

CLOSE_I  CLOSE *,                                                      +
               MF=L
CLOSE_L  EQU   *-CLOSE_I

CVAFDIRI CVAFDIR ACCESS=WRITE,                UPDATE VTOC              +
               BUFLIST=*,                     .. BUFFER PARMS          +
               MF=L
CVAFDIRL EQU   *-CVAFDIRI

WAIT_TIME  DC  F'50'                HALF SECOND

              LTORG
DISPEND       EQU   *
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
WORK          DSECT
              COPY  DXDPREF

WORK_DCB      DS    0A,(DCB_L)X
WORK_OPEN     DS    0A,(OPEN_L)X
WORK_CLOSE    DS    0A,(CLOSE_L)X
WORK_EXLST    DS    A
WORK_JFCB     DS    0A,(JFCB_L)X
WORK_CVAFDIR  DS    0A,(CVAFDIRL)X

WORK_BUFLIST  ICVAFBFL DSECT=NO

WORK_RC       DS    X

WORK_FLAGS    DS    X
$SYNAD        EQU   X'80'
$RLSE         EQU   X'40'           CVAFDIR RLSE NEEDED

WORK_L        EQU   *-WORK
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
CVPL          ICVAFPL
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY     VTOCDATA
                  ORG      VTOC_DSCB
                  IECSDSL1 1
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         VZAPSPFD TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         DCBD     DSORG=PS
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
JFCB     DSECT
         IEFJFCBN
JFCB_L   EQU      *-JFCB
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY     REGEQU
         END      VZAPWRIT
