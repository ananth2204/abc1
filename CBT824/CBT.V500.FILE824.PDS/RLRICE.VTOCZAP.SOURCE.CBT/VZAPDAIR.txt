* ------------------------------------------------------------------- *
*                                                                     *
*  Module name: VZAPDAIR                                              *
*                                                                     *
*   Allocate/free a volume.                                           *
*   Volser is in COMM_VOLSER.                                         *
*   Volume is allocated with DISP=SHR.                                *
*                                                                     *
* ------------------------------------------------------------------- *
VZAPDAIR CSECT
VZAPDAIR AMODE 31
VZAPDAIR RMODE ANY
         USING VZAPCOMM,R12
         USING VZAPDAIR,R15
         B     INIT0000
MODID    DC    CL8'VZAPDAIR'
         DC    CL8'&SYSDATE'
         DC    CL8'&SYSTIME'
         DC    A(DISPEND-VZAPDAIR)
INIT0000 DS    0H
         STM   R14,R12,12(R13)                  SAVE REGS
         LR    R10,R15                          COPY ENTRY POINT
         LR    R2,R0                            COPY R0
         LR    R3,R1                            COPY R1
         DROP  R15
         USING VZAPDAIR,R10                     DEFINE BASE
         L     R15,COMM_DXD
         A     R15,DXD_START
         ST    R13,4(,R15)                      SAVE R13
         ST    R15,8(,R13)                      CHAIN SAVE AREA
         LR    R13,R15                          COPY WORK AREA ADDRESS
         USING DXDDAIR,R13                      DEFINE WORK AREA BASE
         MVC   DXD_CSECT,MODID
INIT0010 DS    0H
         ITRACE ID=ENTRY,                                              +
               DATA1=(COMM_DAIR_FUNC,1),                               +
               DATA2=(COMM_VOLSER,6)
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         MVC   DXD_TEXT_UNITS(TEXT_UNITS_L),TEXT_UNITS_I
         MVC   DXD_DD+2(6),COMM_VOLSER          ADD VOLSER TO DD NAME
         LA    R0,DXD_RB                        RB ADDRESS
         ST    R0,DXD_RB_ADDRESS                SET ADDRESS
         OI    DXD_RB_ADDRESS,X'80'             SET HIGH BIT
         XC    DXD_RB(S99RBEND-S99RB),DXD_RB    INITIALIZE RB
         USING S99RB,DXD_RB
         MVI   S99RBLN,S99RBEND-S99RB           SET LENGTH
         CLI   COMM_DAIR_FUNC,$DAIR_ALLOC       ALLOCATE?
         BE    DAIR0100                         YES
         CLI   COMM_DAIR_FUNC,$DAIR_FREE        FREE?
         BE    DAIR0200                         YES
         ITRACE ID=BAD_FUNC
         DC    H'0'
* ------------------------------------------------------------------- *
*                                                                     *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
DAIR0100 DS    0H
         ITRACE ID=ALLOC
         MVI   S99VERB,S99VRBAL                 SET FUNCTION
         MVC   DXD_VOLSER,COMM_VOLSER           COPY VOLSER
         LA    R0,DXD_DD_TEXT_UNITS             DD TEXT UNIT ADDR
         ST    R0,DXD_TEXT1_ADDR                SET ADDR IN PARM LIST
         LA    R0,DXD_DISP_TEXT_UNITS           DISP TEXT UNIT ADDR
         ST    R0,DXD_TEXT2_ADDR                SET ADDR IN PARM LIST
         LA    R0,DXD_UNIT_TEXT_UNITS           UNIT TEXT UNIT ADDR
         ST    R0,DXD_TEXT3_ADDR                SET ADDR IN PARM LIST
         LA    R0,DXD_VOLSER_TEXT_UNITS         VOLSER TEXT UNIT ADDR
         ST    R0,DXD_TEXT4_ADDR                SET ADDR IN PARM LIST
         OI    DXD_TEXT4_ADDR,X'80'             FLAG END OF LIST
         B     DAIR0300
* ------------------------------------------------------------------- *
*                                                                     *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
DAIR0200 DS    0H
         ITRACE ID=FREE
         MVI   S99VERB,S99VRBUN                 CHANGE VERB
         LA    R0,DXD_DD_TEXT_UNITS             DD TEXT UNIT ADDR
         ST    R0,DXD_TEXT1_ADDR                SET ADDR IN PARM LIST
         OI    DXD_TEXT1_ADDR,X'80'             FLAG END OF LIST
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DAIR0300 DS    0H
         LA    R0,DXD_TEXT1_ADDR                FIRST TEXT UNIT ADDR
         ST    R0,S99TXTPP                      SET ADDRESS IN RB
         LA    R1,DXD_RB_ADDRESS                SVC 99 PARMS
         ITRACE ID=SVC99,                                              +
               RDATA1=R1
         SVC   99                               ISSUE SVC
         ITRACE ID=S99_RC,                                             +
               RDATA1=R15,                                             +
               DATA2=(S99ERROR,4)
         STCM  R15,3,COMM_DAIR_R15              SAVE R15
         MVC   COMM_DAIR_ERROR,S99ERROR         SAVE ERROR CODE
         MVC   COMM_DAIR_INFO,S99INFO           SAVE INFO CODE
* ------------------------------------------------------------------- *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=EXIT
         L     R13,4(,R13)                      RESTORE R13
         LM    R14,R12,12(R13)                  RESTORE REGISTERS
         SR    R15,R15                          SET RC
         BR    R14                              RETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXD_START     DC    Q(DXDDAIR)

TEXT_UNITS_I  DS    0X
              DC    AL2(DALDDNAM)         DDNAME PARMS
              DC    AL2(1)
              DC    AL2(8)
              DC    CL8'VZ'
              DC    AL2(DALSTATS)         INITIAL DISPOSITION PARMS
              DC    AL2(1)
              DC    AL2(1)
              DC    X'08'                 DISP=SHR
              DC    AL2(DALUNIT)          UNIT PARAMETERS
              DC    AL2(1)
              DC    AL2(8)
              DC    CL8'SYSALLDA'         UNIT NAME
              DC    AL2(DALVLSER)         VOLSER PARAMETERS
              DC    AL2(1)
              DC    AL2(6)
              DC    CL6' '                VOLSER
TEXT_UNITS_L  EQU   *-TEXT_UNITS_I

              LTORG
DISPEND       EQU   *
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDDAIR           DSECT
                  COPY    DXDPREF

DXD_RB_ADDRESS    DS      A                     RB'S ADDRESS
DXD_RB            DS      0A,(S99RBEND-S99RB)X  SVC 99 RB

DXD_TEXT1_ADDR    DS      A
DXD_TEXT2_ADDR    DS      A
DXD_TEXT3_ADDR    DS      A
DXD_TEXT4_ADDR    DS      A
DXD_TEXT5_ADDR    DS      A
DXD_TEXT6_ADDR    DS      A

DXD_TEXT_UNITS            DS   0C
DXD_DD_TEXT_UNITS         DS   AL2,AL2,AL2
DXD_DD                    DS   CL8
DXD_DISP_TEXT_UNITS       DS   AL2,AL2,AL2
                          DS   X
DXD_UNIT_TEXT_UNITS       DS   AL2,AL2,AL2
                          DS   CL8
DXD_VOLSER_TEXT_UNITS     DS   AL2,AL2,AL2
DXD_VOLSER                DS   CL6
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         VZAPSPFD TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         IEFZB4D0
         IEFZB4D2
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY     REGEQU
         END      VZAPDAIR
