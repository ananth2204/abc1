* ------------------------------------------------------------------- *
*                                                                     *
*  MODULE NAME: VZAPVMGR                                              *
*                                                                     *
*  Define or delete SPF variables.                                    *
*                                                                     *
* ------------------------------------------------------------------- *
VZAPVMGR CSECT
VZAPVMGR AMODE 31
VZAPVMGR RMODE ANY
         USING VZAPVMGR,R15
         USING VZAPCOMM,R12
         USING VAR_DSECT,R6                   DEFINE BASE
         B     INIT0000
MODID    DC    CL8'VZAPVMGR'
         DC    CL8'&SYSDATE'
         DC    CL8'&SYSTIME'
         DC    A(VARMGREND-VZAPVMGR)
INIT0000 DS    0H
         STM   R14,R12,12(R13)                SAVE REGISTERS
         LR    R10,R15                        COPY BASE ADDRESS
         DROP  R15
         USING VZAPVMGR,R10
         L     R15,COMM_DXD                   DXD AREA BASE ADDRESS
         A     R15,DXD_START                  PLUS OFFSET
         ST    R13,4(,R15)
         ST    R15,8(,R13)
         LR    R13,R15                        COPY DXD AREA ADDRESS
         USING DXDVMGR,R13                    DEFINE WORK AREA BASE
         MVC   DXD_CSECT,MODID
* ------------------------------------------------------------------- *
         ITRACE ID=ENTRY,                                              +
               DATA1=(COMM_VAR_COMMAND,1),                             +
               DATA2=COMM_VARIABLE
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
VAR0010  DS    0H
         CLI   COMM_VAR_COMMAND,$VAR_DEFINE   DEFINE VARIABLES?
         BE    VAR0100                        YES
         CLI   COMM_VAR_COMMAND,$VAR_DELETE   DELETE VARIABLE?
         BE    VAR0200                        YES
         CLI   COMM_VAR_COMMAND,$VAR_GET      VGET VARIABLE?
         BE    VAR0300                        YES
         CLI   COMM_VAR_COMMAND,$VAR_PUT      VPUT VARIABLE?
         BE    VAR0400                        YES
         ITRACE ID=BADCMD
         ABEND 1,DUMP,,USER                   BAD COMMAND
* ------------------------------------------------------------------- *
*        PROCESS DEFINE COMMANDS                                      *
* ------------------------------------------------------------------- *
VAR0100  DS    0H
         ITRACE ID=DEFINE
         CLC   VDATA,COMM_VARIABLE            VDATA?
         BE    VAR0180                        YES.. DONE SPECIFICALLY
         CLC   COMM_VARIABLE,ALL              ALL VARIABLES?
         BNE   VAR0130                        NO
* ------------------------------------------------------------------- *
*        VDEFINE ALL VARIABLES                                        *
* ------------------------------------------------------------------- *
         ITRACE ID=ALL
         L     R6,COMM_VZAPSPFV
VAR0110  DS    0H
         TM    VAR_FLAG,$VDEFINE              VARIABLE ALREADY DEFINED?
         BO    VAR0120                        YES
         TM    VAR_FLAG,$VNEVER               NEVER DEFINE?
         BO    VAR0120                        YES
         MVI   DXD_FLAGS,0                    RESET FLAGS
         BAL   R5,VAR0170                     VDEFINE VARIABLE
VAR0120  DS    0H
         LA    R6,VAR_DSECT_L(,R6)            NEXT VARIABLE
         CLI   0(R6),X'FF'                    END OF TABLE?
         BNE   VAR0110                        NO
         B     EXIT0000                       ALL PROCESSED
* ------------------------------------------------------------------- *
*        VDEFINE specific variable                                    *
* ------------------------------------------------------------------- *
VAR0130  DS    0H
         L     R6,COMM_VZAPSPFV               FIRST VARIABLE
VAR0140  DS    0H
         CLC   VAR_NAME,COMM_VARIABLE         LOCATED THE VARIABLE?
         BE    VAR0150
         LA    R6,VAR_DSECT_L(,R6)            NEXT VARIABLE
         CLI   0(R6),X'FF'                    END OF TABLE?
         BE    ERR0010                        YES
         B     VAR0140
VAR0150  DS    0H
         TM    VAR_FLAG,$VDEFINE              VDEFINED?
         BNO   VAR0160                        NO
         BAL   R5,VAR0250                     VDELETE IT
VAR0160  DS    0H
         BAL   R5,VAR0170                     VDEFINE IT
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
VAR0170  DS    0H
         LA    R1,VDEFINE                     COMMAND'S ADDRESS
         ST    R1,DXD_SPF1                    SET ADDRESS IN PARM LIST
         LA    R1,VAR_NAME                    VARIABLE'S NAME
         ST    R1,DXD_SPF2                    SET NAME'S ADDRESS
         SR    R3,R3                          CLEAR REGISTER
         ICM   R3,3,VAR_DISP                  DISPLACEMENT TO DATA
         A     R3,COMM_VZAPSPFD               PLUS BASE ADDRESS
         ST    R3,DXD_SPF3                    SET ADDRESS IN PARM LIST
         LA    R1,VAR_FORMAT                  DATA FORMAT
         ST    R1,DXD_SPF4                    SET ADDRESS IN PARM LIST
         LA    R1,VAR_LENGTH                  VARIABLE LENGTH
         ST    R1,DXD_SPF5                    SET ADDRESS IN PARM LIST
         OI    DXD_SPF5,X'80'                 SET END-OF-LIST
         LA    R1,DXD_SPFP                    SPF PARM LIST ADDRESS
         ITRACE ID=VDEFINE,                   DEFINING A VARIABLE      +
               DATA1=VAR_NAME,                .. VARIABLE'S NAME       +
               RDATA2=R1                      .. ADDRESS OF PARMS
         L     R15,COMM_ISPLINK               SPFLINK ENTRY POINT
         BALR  R14,R15                        LINK TO SPF
         ITRACE ID=VDEF_RC,                                            +
               RDATA1=R15
         LTR   R15,R15                        VDEFINE SUCCESSFUL?
         BNZ   ERR0020                        NO
         OI    VAR_FLAG,$VDEFINE              VARIABLE HAS BEEN DEFINED
         TM    VAR_FLAG,$VGET                 VGET NOW?
         BNOR  R5                             NO
         MVC   DXD_NAMELIST,COMM_BLANKS
         MVC   DXD_NAMELIST+1(8),VAR_NAME
         BAL   R7,VAR0310                     VGET THE VARIABLE
         BR    R5
* ------------------------------------------------------------------- *
*        VDEFINE VDATA                                                *
* ------------------------------------------------------------------- *
VAR0180  DS    0H
         ITRACE ID=VDATA,                                              +
               DATA1=(COMM_VDATA_ADDR,4),                              +
               DATA2=(COMM_VDATA_LENGTH,4)
         LA    R1,VDEFINE                     COMMAND'S ADDRESS
         ST    R1,DXD_SPF1                    SET ADDRESS IN PARM LIST
         LA    R1,COMM_VARIABLE               VARIABLE'S NAME
         ST    R1,DXD_SPF2                    SET NAME'S ADDRESS
         MVC   DXD_SPF3,COMM_VDATA_ADDR       DATA ADDRESS
         LA    R1,CHAR                        CHARACTER
         ST    R1,DXD_SPF4                    SET ADDRESS IN PARM LIST
         LA    R1,COMM_VDATA_LENGTH           VARIABLE LENGTH
         ST    R1,DXD_SPF5                    SET ADDRESS IN PARM LIST
         OI    DXD_SPF5,X'80'                 SET END-OF-LIST
         LA    R1,DXD_SPFP                    SPF PARM LIST ADDRESS
         ITRACE ID=VDEFINE,                   DEFINING A VARIABLE      +
               DATA1=COMM_VARIABLE,           .. VARIABLE'S NAME       +
               RDATA2=R1                      .. ADDRESS OF PARMS
         L     R15,COMM_ISPLINK               SPFLINK ENTRY POINT
         BALR  R14,R15                        LINK TO SPF
         ITRACE ID=VDEF_RC,                                            +
               RDATA1=R15
         LTR   R15,R15                        VDEFINE SUCCESSFUL?
         BNZ   ERR0020                        NO
         B     EXIT0000
* ------------------------------------------------------------------- *
*        PROCESS VARIABLE DELETION                                    *
* ------------------------------------------------------------------- *
VAR0200  DS    0H
         ITRACE ID=DELETE                     DELETING ALL VARIABLES
         CLC   VDATA,COMM_VARIABLE            VDATA?
         BE    VAR0270                        YES
         MVC   DXD_POOL,SHARED
         CLC   ALL,COMM_VARIABLE              ALL?
         BNE   VAR0220                        NO
* ------------------------------------------------------------------- *
*        DELETE ALL VARIABLES                                         *
* ------------------------------------------------------------------- *
         L     R6,COMM_VZAPSPFV               START WITH FIRST VARIABLE
VAR0210  DS    0H
         BAL   R5,VAR0250
         LA    R6,VAR_DSECT_L(,R6)            NEXT VARIABLE
         CLI   0(R6),X'FF'                    END OF TABLE?
         BE    EXIT0000                       YES
         B     VAR0210                        PROCESS ALL VARIABLES
* ------------------------------------------------------------------- *
*        DELETE SPECIFIC VARIABLE                                     *
* ------------------------------------------------------------------- *
VAR0220  DS    0H
         L     R6,COMM_VZAPSPFV               FIRST VARIABLE
VAR0230  DS    0H
         CLC   VAR_NAME,COMM_VARIABLE         VARIABLE LOCATED?
         BE    VAR0240                        YES
         LA    R6,VAR_DSECT_L(,R6)            NEXT VARIABLE
         CLI   0(R6),X'FF'                    END OF TABLE?
         BNE   VAR0230                        NO
         B     ERR0010                        VARIABLE NOT FOUND
VAR0240  DS    0H
         BAL   R5,VAR0250
         B     EXIT0000
* ------------------------------------------------------------------- *
*        DELETE VARIABLE                                              *
* ------------------------------------------------------------------- *
VAR0250  DS    0H
         ITRACE ID=A,RDATA1=R6,DATA2=VAR_NAME
         TM    VAR_FLAG,$VDEFINE              VARIABLE DEFINED?
         BNOR  R5                             NO
         TM    VAR_FLAG,$VPUT                 VPUT BEFORE DELETING?
         BNO   VAR0260                        NO
         MVC   DXD_NAMELIST+1(8),VAR_NAME
         BAL   R7,VAR0410
VAR0260  DS    0H
         LA    R0,VDELETE                     COMMAND'S ADDRESS
         ST    R0,DXD_SPF1                    SET ADDRESS IN PARM LIST
         ST    R6,DXD_SPF2                    SET VARIABLE NAME ADDRESS
         OI    DXD_SPF2,X'80'                 FLAG END-OF-LIST
         ITRACE ID=VDELETE,                   DELETING A VARIABLE      +
               DATA1=VAR_NAME                 .. VARIABLE NAME
         LA    R1,DXD_SPFP                    PARM LIST'S ADDRESS
         L     R15,COMM_ISPLINK               SPFLINK ENTRY POINT
         BALR  R14,R15                        LINK TO SPF
         LTR   R15,R15                        VDELETE SUCCESSFUL?
         BNZ   ERR0030                        NO
         BR    R5
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
VAR0270  DS    0H
         LA    R0,VDELETE                     COMMAND'S ADDRESS
         ST    R0,DXD_SPF1                    SET ADDRESS IN PARM LIST
         LA    R0,COMM_VARIABLE               VARIABLE TO DELETE
         ST    R0,DXD_SPF2                    SET VARIABLE NAME ADDRESS
         OI    DXD_SPF2,X'80'                 FLAG END-OF-LIST
         ITRACE ID=VDELETE,                   DELETING A VARIABLE      +
               DATA1=COMM_VARIABLE            .. VARIABLE NAME
         LA    R1,DXD_SPFP                    PARM LIST'S ADDRESS
         L     R15,COMM_ISPLINK               SPFLINK ENTRY POINT
         BALR  R14,R15                        LINK TO SPF
         ITRACE ID=RC,                                                 +
               RDATA1=R15
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
VAR0300  DS    0H
         MVC   DXD_NAMELIST,COMM_BLANKS
         MVC   DXD_NAMELIST+1(8),COMM_VARIABLE
         BAL   R7,VAR0310                     DO VGET
         B     EXIT0000
* ------------------------------------------------------------------- *
*        VGET a variable                                              *
* ------------------------------------------------------------------- *
VAR0310  DS    0H
         MVI   DXD_NAMELIST+0,C'('
         LA    R1,DXD_NAMELIST+10
         LA    R2,8
VAR0320  DS    0H
         CLI   0(R1),C' '
         BNE   VAR0330
         BCTR  R1,0
         BCT   R2,VAR0320
VAR0330  DS    0H
         MVI   1(R1),C')'
         LA    R0,VGET                        VGET COMMAND
         ST    R0,DXD_SPF1                    SET ADDRESS
         LA    R0,DXD_NAMELIST          < --  DON'T LIKE THE CLOSE NAME
         ST    R0,DXD_SPF2                    SET NAME
         LA    R0,DXD_POOL                    SHARED OF PROFILE POOL
         ST    R0,DXD_SPF3
         OI    DXD_SPF3,X'80'                 FLAG END-OF-LIST
         TM    VAR_FLAG,$VPUT                 THIS VARIABLE SAVED?
         BO    VAR0340                        YES
         ITRACE ID=SHARED
         MVC   DXD_POOL,SHARED                FROM SHARED POOL
         B     VAR0350
VAR0340  DS    0H
         ITRACE ID=PROFILE
         MVC   DXD_POOL,PROFILE
VAR0350  DS    0H
         ST    R0,DXD_SPF3                    SET ADDRESS
         ITRACE ID=VGET,                                               +
               DATA1=DXD_NAMELIST+1,                                   +
               DATA2=DXD_POOL
         LA    R1,DXD_SPF1
         L     R15,COMM_ISPLINK               SPF ENTRY POINT
         BALR  R14,R15
         ITRACE ID=VGET_RC,                                            +
               RDATA1=R15,                                             +
               DATA2=0(R3)
         LTR   R15,R15                        SUCCESSFUL?
         BZR   R7                             YES
         TM    DXD_FLAGS,$DXD_VPUT            VPUT ISSUED?
         BO    ERR0040
         LA    R0,VPUT
         ST    R0,DXD_SPF1
         LA    R1,DXD_SPF1                    CHANGE VGET TO VPUT
         ITRACE ID=VPUT,                                               +
               DATA1=DXD_NAMELIST+1,                                   +
               DATA2=DXD_POOL
         L     R15,COMM_ISPLINK               SPF ENTRY POINT
         BALR  R14,R15
         ITRACE ID=VPUT_RC,                                            +
               RDATA1=R15,                                             +
               DATA2=0(R3)
         OI    DXD_FLAGS,$DXD_VPUT
         B     VAR0330                        TRY AGAIN
* ------------------------------------------------------------------- *
*        VPUT A VARIABLE                                              *
* ------------------------------------------------------------------- *
VAR0400  DS    0H
         MVC   DXD_NAMELIST+1(8),COMM_VARIABLE
         BAL   R7,VAR0410                     VPUT THE VARIABLE
         B     EXIT0000
VAR0410  DS    0H
         MVI   DXD_NAMELIST+0,C'('
         LA    R1,DXD_NAMELIST+10
         LA    R2,8
VAR0420  DS    0H
         CLI   0(R1),C' '                     BLANK?
         BNE   VAR0430                        NO
         BCTR  R1,0                           BACK UP 1
         BCT   R2,VAR0420                     LOOP
VAR0430  DS    0H
         MVI   1(R1),C')'                     INSERT CLOSING PAREN
         LA    R0,VPUT                        SPF COMMAND
         ST    R0,DXD_SPF1                    SET ADDRESS
         LA    R0,DXD_NAMELIST                NAME LIST
         ST    R0,DXD_SPF2                    SET ADDRESS
         LA    R0,DXD_POOL
         ST    R0,DXD_SPF3
         OI    DXD_SPF3,X'80'
         ITRACE ID=VPUT,                                               +
               DATA1=DXD_NAMELIST+1,                                   +
               DATA2=DXD_POOL
         LA    R1,DXD_SPF1                    SPF PARM LIST
         L     R15,COMM_ISPLINK               SPF ENTRY POINT
         BALR  R14,R15                        VPUT THE VARIABLE
         ITRACE ID=VPUT_RC,                   TRACE VPUT RC            +
               RDATA1=R15
         LTR   R15,R15                        VPUT OK?
         BNZ   ERR0050                        NO
         BR    R7
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0010  DS    0H
         ITRACE ID=BAD_VAR
         DC    H'1'
ERR0020  DS    0H
         ITRACE ID=VDEFFAIL,                                           +
               RDATA1=R15
         DC    H'2'
ERR0030  DS    0H
         ITRACE ID=VDELFAIL,                                           +
               RDATA1=R15
         DC    H'3'
ERR0040  DS    0H
         ITRACE ID=VGETFAIL,                                           +
               RDATA1=R15
         DC    H'4'
ERR0050  DS    0H
         ITRACE ID=VPUTFAIL,                                           +
               RDATA1=R15
         DC    H'5'
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=EXIT
         L     R13,4(,R13)                    RESTORE SAVE AREA ADDRESS
         LM    R14,R12,12(R13)                RESTORE REGISTERS
         BR    R14                            RETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXD_START DC    Q(DXDVMGR)
VARNAME_I DC    F'1'
          DC    F'0'
          DC    CL8' '
VARNAME_L EQU   *-VARNAME_I
ALL       DC    CL8'ALL'
CHAR      DC    CL8'CHAR'
VDATA     DC    CL8'VDATA'
VDEFINE   DC    CL8'VDEFINE'
VDELETE   DC    CL8'VDELETE'
VGET      DC    CL8'VGET'
VPUT      DC    CL8'VPUT'
PROFILE   DC    CL8'PROFILE'
SHARED    DC    CL8'SHARED'
          LTORG
VARMGREND EQU   *
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDVMGR      DSECT
             COPY  DXDPREF
DXD_SPFP     DS    0A                    ISPF PARM LIST
DXD_SPF1     DS    A                     PARM 1 ADDRESS
DXD_SPF2     DS    A                     PARM 2 ADDRESS
DXD_SPF3     DS    A                     PARM 3 ADDRESS
DXD_SPF4     DS    A                     PARM 4 ADDRESS
DXD_SPF5     DS    A                     PARM 5 ADDRESS

DXD_FLAGS    DS    X
$DXD_VPUT    EQU   X'80'

DXD_NAMELIST DS    CL12

DXD_POOL     DS    CL8
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY     VARDSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         VZAPSPFD TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY     REGEQU
         END      VZAPVMGR
