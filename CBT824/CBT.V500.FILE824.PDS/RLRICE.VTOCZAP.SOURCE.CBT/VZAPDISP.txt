* ------------------------------------------------------------------- *
*                                                                     *
*  Module name: VZAPDISP                                              *
*                                                                     *
*   Display SPF panels.                                               *
*   Panel name is in COMM_PANEL.                                      *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
VZAPDISP CSECT
VZAPDISP AMODE 31
VZAPDISP RMODE ANY
         USING VZAPCOMM,R12
         USING VZAPDISP,R15
         B     INIT0000
MODID    DC    CL8'VZAPDISP'
         DC    CL8'&SYSDATE'
         DC    CL8'&SYSTIME'
         DC    A(DISPEND-VZAPDISP)
INIT0000 DS    0H
         STM   R14,R12,12(R13)                  SAVE REGS
         LR    R10,R15                          COPY ENTRY POINT
         LR    R2,R0                            COPY R0
         LR    R3,R1                            COPY R1
         DROP  R15
         USING VZAPDISP,R10                     DEFINE BASE
         L     R15,COMM_DXD
         A     R15,DXD_START
         ST    R13,4(,R15)                      SAVE R13
         ST    R15,8(,R13)                      CHAIN SAVE AREA
         LR    R13,R15                          COPY WORK AREA ADDRESS
         USING DXDDISP,R13                      DEFINE WORK AREA BASE
         CLC   DXD_CSECT,MODID                  INTIALIZATION DONE?
         BE    INIT0010                         YES
         MVC   DXD_CSECT,MODID
INIT0010 DS    0H
         ITRACE ID=ENTRY,                                              +
               DATA1=COMM_PANEL
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         L     R9,COMM_VZAPSPFD                 SPF DATA
         USING VZAPSPFD,R9
         MVC   COMM_VARIABLE,VDATA              VARIABLE NAME
         OC    COMM_VDATA_ADDR,COMM_VDATA_ADDR  VDATA IN USE?
         BZ    DISP0010                         NO
         ITRACE ID=VDELETE                      VDELETING VDATA
         MVI   COMM_VAR_COMMAND,$VAR_DELETE     SET FUNCTION
         L     R15,COMM_V_VZAPVMGR              VARIABLE MANAGER
         ITRACE ID=VDEFINE                      VDELETING VDATA
         BALR  R14,R15                          VDELETE VDATA
         MVI   COMM_VAR_COMMAND,$VAR_DEFINE     CHANGE COMMAND
         L     R15,COMM_V_VZAPVMGR              VARIABLE MANAGER
         BALR  R14,R15                          VDEFINE VDATA
DISP0010 DS    0H
         ITRACE ID=DISPLAY,                                            +
               DATA1=COMM_PANEL,                                       +
               DATA2=(COMM_VDATA_ADDR,4)
         LA    R1,DISPLAY                       DISPLAY FUNCTION
         ST    R1,DXD_SPF1                      SET ADDRESS
         LA    R1,COMM_PANEL                    PANEL TO DISPLAY
         ST    R1,DXD_SPF2                      SET PANEL ADDRESS
         OI    DXD_SPF2,X'80'                   FLAG END-OF-LIST
         LA    R1,DXD_SPF_PARMS                 SPF PARM LIST
         L     R15,COMM_ISPLINK                 SPF ENTRY POINT
         BALR  R14,R15                          DISPLAY THE PANEL
         ITRACE ID=DISP_RC,                                            +
               RDATA1=R15
         STH   R15,COMM_DISP_RC                 SAVE RC
* ------------------------------------------------------------------- *
*        Reset messages                                               *
* ------------------------------------------------------------------- *
         TM    COMM_FLAGS,$COMM_DONT_CLEAR_MSG   CLEAR MESSAGES?
         BO    DISP0020                          NO
         MVC   SPF_MSG_1,COMM_BLANKS
         MVC   SPF_MSG_2,COMM_BLANKS
         MVC   SPF_MSG_3,COMM_BLANKS
         MVC   SPF_MSG_4,COMM_BLANKS
         MVC   SPF_MSG_5,COMM_BLANKS
DISP0020 DS    0H
         LTR   R15,R15                           RC = 0?
         BNZ   EXIT0000                          NO
* ------------------------------------------------------------------- *
*        VGET spf variables                                           *
* ------------------------------------------------------------------- *
DISP0030 DS    0H
         LA    R1,VGET                           COMMAND
         ST    R1,DXD_SPF1                       SET ADDRESS
         LA    R1,VGET_LIST                      SYSTEM VARIABLES
         ST    R1,DXD_SPF2                       SET ADDRESS
         OI    DXD_SPF2,X'80'                    FLAG END-OF-LIST
         LA    R1,DXD_SPF_PARMS                  SPF PARM LIST
         ITRACE ID=VGET_2,                                             +
               RDATA1=R1
         L     R15,COMM_ISPLINK                  SPF INTERFACE
         BALR  R14,R15                           VGET SPF VARIABLE(S)
         ITRACE ID=VGET2_RC,                                           +
               RDATA1=R15
         LTR   R15,R15                           SUCCESSFUL?
         BNZ   DISP0040                          NO
         TM    DXD_FLAGS,$DXD_VPUT               VPUT ALREADY ISSUED?
         BO    DISP0050                          YES
* ------------------------------------------------------------------- *
*        VPUT SPF variables                                           *
* ------------------------------------------------------------------- *
DISP0040 DS    0H
         OI    DXD_FLAGS,$DXD_VPUT               SET FLAG
         LA    R1,VPUT                           COMMAND
         ST    R1,DXD_SPF1                       SET ADDRESS
         LA    R1,VPUT_LIST
         ST    R1,DXD_SPF2
         OI    DXD_SPF2,X'80'                    SET END-OF-LIST
         LA    R1,DXD_SPF_PARMS                  PARM LIST ADDRESS
         L     R15,COMM_ISPLINK                  SPF INTERFACE
         BALR  R14,R15                           UPDATE SPF VARIABLE(S)
         ITRACE ID=VPUT2_RC,                                           +
               RDATA1=R15
         B     DISP0030                          DO VGET
* ------------------------------------------------------------------- *
*        Check for global command                                     *
* ------------------------------------------------------------------- *
DISP0050 DS    0H
         ITRACE ID=ZCMD,                                               +
               DATA1=SPF_ZCMD+0,                                       +
               DATA2=SPF_ZCMD+8
         OC    SPF_ZCMD,COMM_BLANKS              'TRANSLATE' TO UPPER
         CLC   ABEND,SPF_ZCMD                    ABEND?
         BE    ABEND000                          YES
         CLC   TPUT,SPF_ZCMD                     TPUT?
         BE    TPUT0000                          YES
* ------------------------------------------------------------------- *
*        Compute cursor position (row and column)                     *
* ------------------------------------------------------------------- *
         XC    SPF_ROW,SPF_ROW
         XC    SPF_COLUMN,SPF_COLUMN
         PACK  COMM_DWORD,SPF_ZSCREENC           PACK CURSOR POSITION
         CVB   R1,COMM_DWORD                     CONVERT TO BINARY
         SR    R0,R0                             CLEAR R0
         ICM   R2,15,SPF_VWIDTH                  WIDTH VALID?
         BZ    DISP0060                          NO
         D     R0,SPF_VWIDTH                     COMPUTE ROW/COLUMN
         ST    R1,SPF_ROW                        SAVE ROW
         ST    R0,SPF_COLUMN                     SAVE COLUMN
DISP0060 DS    0H
         ITRACE ID=ROW_COL,                                            +
               RDATA1=R1,                                              +
               RDATA2=R0
* ------------------------------------------------------------------- *
*        Parse the command                                            *
* ------------------------------------------------------------------- *
         ITRACE ID=PARSE
         LA    R0,SPF_ZCMD                       COMMAND ADDRESS
         ST    R0,COMM_COMMAND_ADDRESS
         LA    R0,L'SPF_ZCMD                     COMMAND LENGTH
         ST    R0,COMM_COMMAND_LENGTH
         L     R15,COMM_V_VZAPPARS               VZAPPARS ENTRY POINT
         BALR  R14,R15                           CALL VZAPPARS
         ITRACE ID=RC,                                                 +
               RDATA1=R15
         LTR   R15,R15                           RC=0?
         BNZ   DISP0010                          NO
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
TPUT0000 DS    0H
         MVC   SPF_ZCMD,COMM_BLANKS              RESET THE COMMAND
         ICM   R1,15,COMM_TRACE_TPUT             TPUT STORAGE PRESENT?
         BNZ   TPUT0010                          YES
         ITRACE ID=TPUT_ON
         GETMAIN RU,                             GETMAIN TPUT STORAGE  +
               LV=TR_ENTRY_L,                                          +
               LOC=BELOW
         ST    R1,COMM_TRACE_TPUT                SAVE ADDRESS
         B     EXIT0000
TPUT0010 DS    0H
         ITRACE ID=TPUT_OFF
         FREEMAIN RU,                                                  +
               A=(1),                                                  +
               LV=TR_ENTRY_L
* ------------------------------------------------------------------- *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=EXIT
         L     R13,4(,R13)                      RESTORE R13
         LM    R14,R12,12(R13)                  RESTORE REGISTERS
         SR    R15,R15                          SET RC
         BR    R14                              RETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ABEND000 DS    0H
         ITRACE ID=ABEND
         DC    H'0'
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXD_START     DC    Q(DXDDISP)
ABEND         DC    C'ABEND '
DISPLAY       DC    CL8'DISPLAY'
TPUT          DC    CL8'TPUT'
VDATA         DC    CL8'VDATA'
VGET          DC    CL8'VGET'
VPUT          DC    CL8'VPUT'

VGET_LIST     DC    C'(ZSCROLLA ZSCROLLN ZVERB ZSCREENC)'
VPUT_LIST     DC    C'(ZSCROLLA ZSCROLLN ZVERB)'

              LTORG
DISPEND       EQU   *
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDDISP           DSECT
                  COPY    DXDPREF

DXD_SPF_PARMS     DS      0A                    SPF PARM LIST
DXD_SPF1          DS      A
DXD_SPF2          DS      A
DXD_SPF3          DS      A

DXD_FLAGS         DS      X
$DXD_VGET         EQU     X'80'
$DXD_VPUT         EQU     X'40'
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         VZAPSPFD TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY     REGEQU
         END      VZAPDISP
