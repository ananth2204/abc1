* ------------------------------------------------------------------- *
*                                                                     *
*  Module name: VZAPCNFM                                              *
*                                                                     *
*   Confirm user's intention                                          *
*                                                                     *
* ------------------------------------------------------------------- *
VZAPCNFM CSECT
VZAPCNFM AMODE 31
VZAPCNFM RMODE ANY
         USING VZAPCOMM,R12
         USING VZAPCNFM,R15
         B     INIT0000
MODID    DC    CL8'VZAPCNFM'
         DC    CL8'&SYSDATE'
         DC    CL8'&SYSTIME'
         DC    A(DSCBEND-VZAPCNFM)
INIT0000 DS    0H
         STM   R14,R12,12(R13)                SAVE REGS
         LR    R10,R15                        COPY ENTRY POINT
         LR    R2,R0                          COPY R0
         LR    R3,R1                          COPY R1
         DROP  R15
         USING VZAPCNFM,R10                   DEFINE BASE
         L     R15,COMM_DXD
         A     R15,DXD_START
         ST    R13,4(,R15)                    SAVE R13
         ST    R15,8(,R13)                    CHAIN SAVE AREA
         LR    R13,R15                        COPY WORK AREA ADDRESS
         USING DXDCNFM,R13                    DEFINE WORK AREA BASE
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY
         L     R9,COMM_VZAPSPFD
         USING VZAPSPFD,R9
         MVC   COMM_PANEL,CONFIRM_PANEL       COPY PANEL NAME
         XC    COMM_VDATA_ADDR,COMM_VDATA_ADDR
         XC    COMM_VDATA_LENGTH,COMM_VDATA_LENGTH
         XC    COMM_VDATA_CURSOR,COMM_VDATA_CURSOR
         OI    COMM_FLAGS,$COMM_DONT_CLEAR_MSG
* ------------------------------------------------------------------- *
*        Display                                                      *
* ------------------------------------------------------------------- *
MAIN0010 DS    0H
         ITRACE ID=DISPLAY
         L     R15,COMM_V_VZAPDISP            VZAPDISP ENTRY POINT
         BALR  R14,R15                        CALL VZAPDISP
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         ITRACE ID=REPLY,                                              +
               DATA1=(SPF_YES_OR_NO,3)
         CLC   SPF_YES_OR_NO,YES              YES?
         BE    EXIT0000                       YES
         CLC   SPF_YES_OR_NO,Y                SHORT YES?
         BE    EXIT0000                       YES
         CLC   SPF_YES_OR_NO,NO               NO?
         BE    EXIT0000                       YES
         CLC   SPF_YES_OR_NO,N                SHORT NO?
         BE    EXIT0000                       YES
         MVC   SPF_MSG_3(EMSG01_L),EMSG01     COPY ERROR MESSAGE
         B     MAIN0010                       YES
* ------------------------------------------------------------------- *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=EXIT
         MVC   SPF_MSG_1,COMM_BLANKS          CLEAR MESSAGE
         MVC   SPF_MSG_2,COMM_BLANKS          CLEAR MESSAGE
         MVC   SPF_MSG_3,COMM_BLANKS          CLEAR MESSAGE
         MVC   SPF_MSG_4,COMM_BLANKS          CLEAR MESSAGE
         MVC   SPF_MSG_5,COMM_BLANKS          CLEAR MESSAGE
         NI    COMM_FLAGS,255-$COMM_DONT_CLEAR_MSG
         L     R13,4(,R13)                    RESTORE SAVE AREA ADDRESS
         LM    R14,R12,12(R13)                RESTORE REGISTERS
         SR    R15,R15                        SET RC
         BR    R14                            RETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXD_START     DC    Q(DXDCNFM)
              LTORG

Y             DC    CL3'Y'
YES           DC    CL3'YES'
N             DC    CL3'N'
NO            DC    CL3'NO '
CONFIRM_PANEL DC    CL8'VZAPCNFM'

EMSG01        DS    0C
              DC    C'VZAPCNFM01E Reply Y(es) or N(o)'
EMSG01_L      EQU   *-EMSG01
DSCBEND       EQU   *
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDCNFM       DSECT
              COPY     DXDPREF
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              VZAPSPFD TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              COPY     REGEQU
              END      VZAPCNFM
