* ------------------------------------------------------------------- *
*                                                                     *
*  MODULE NAME: VZAPAUTH                                              *
*                                                                     *
*  Verify user is authorized to perform function(s)                   *
*                                                                     *
* ------------------------------------------------------------------- *
VZAPAUTH CSECT
VZAPAUTH AMODE 31
VZAPAUTH RMODE ANY
         USING VZAPCOMM,R12
         USING VZAPAUTH,R15
         B     INIT0000
MODID    DC    CL8'VZAPAUTH'
         DC    CL8'&SYSDATE'
         DC    CL8'&SYSTIME'
         DC    A(AUTHEND-VZAPAUTH)
INIT0000 DS    0H
         STM   R14,R12,12(R13)                SAVE REGISTERS
         LR    R10,R15                        COPY BASE ADDRESS
         DROP  R15
         USING VZAPAUTH,R10
         L     R15,COMM_DXD                   DXD ADDRESS
         A     R15,DXD_START                  PLUS OFFSET
         ST    R13,4(,R15)
         ST    R15,8(,R13)
         LR    R13,R15                        COPY DXD AREA ADDRESS
         USING DXDAUTH,R13                    DEFINE WORK AREA BASE
         MVC   DXD_CSECT,MODID
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         ITRACE ID=ENTRY,                                              +
               DATA1=(COMM_VTOCZAP_FUNCTION,1)
         L     R8,COMM_VZAPSPFD
         USING VZAPSPFD,R8
         MVI   DXD_RC,0                       SET DEFAULT RC
         MVC   DXD_RACROUTE(RACROUTE_L),RACROUTE_I
         MVI   DXD_CLASS_LENGTH,8             SET CLASS LENGTH
         MVC   DXD_CLASS_NAME,CLASS           INITIALIZE CLASS NAME
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         CLI   COMM_VTOCZAP_FUNCTION,$VTOCZAP_READ_VTOC
         BE    MAIN0100
         CLI   COMM_VTOCZAP_FUNCTION,$VTOCZAP_DISPLAY_FMT1
         BE    MAIN0200
         CLI   COMM_VTOCZAP_FUNCTION,$VTOCZAP_UPDATE_FMT1
         BE    MAIN0300
         CLI   COMM_VTOCZAP_FUNCTION,$VTOCZAP_UPDATE_VTOC
         BE    MAIN0400
         DC    H'0'
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0100 DS    0H
         ITRACE ID=READVTOC
         MVC   DXD_ENTITY,READVTOC
         MVC   DXD_MSG_1(MSG01_L),MSG01
         B     MAIN1000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0200 DS    0H
         ITRACE ID=DISPFMT1
         MVC   DXD_ENTITY,DISPFMT1
         MVC   DXD_MSG_1(MSG02_L),MSG02
         B     MAIN1000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0300 DS    0H
         ITRACE ID=UPDTFMT1
         MVC   DXD_ENTITY,UPDTFMT1
         MVC   DXD_MSG_1(MSG03_L),MSG03
         B     MAIN1000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0400 DS    0H
         ITRACE ID=UPDTVTOC
         MVC   DXD_ENTITY,UPDTVTOC
         MVC   DXD_MSG_1(MSG04_L),MSG04
* ------------------------------------------------------------------- *
*                                                                     *
*        The LOG=NO option requires APF authorization                 *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN1000 DS    0H
         ITRACE ID=APF_ON
         LA    R0,1
         L     R15,COMM_V_VZAPAPF             TURN ON APF AUTHORIZATION
         BALR  R14,R15
         ITRACE ID=RACROUTE,                                           +
               DATA1=DXD_CLASS_NAME,                                   +
               DATA2=DXD_ENTITY
         RACROUTE REQUEST=AUTH,               CHECK RACF AUTHORIZATION +
               ATTR=READ,                                              +
               CLASS=DXD_CLASS_LENGTH,                                 +
               ENTITY=DXD_ENTITY,                                      +
               WORKA=DXD_WORKA,                                        +
               MF=(E,DXD_RACROUTE)
         ITRACE ID=RC,                        TRACE RACF RC            +
               RDATA1=R15
         STC   R15,DXD_RC                     SAVE RACF RC
         ITRACE ID=APF_OFF
         SR    R0,R0
         L     R15,COMM_V_VZAPAPF
         BALR  R14,R15                        TURN OFF APF
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         CLI   DXD_RC,0                       USER AUTHORIZED?
         BE    EXIT0000                       YES
         MVC   SPF_MSG_1,DXD_MSG_1
         MVC   SPF_MSG_2,COMM_BLANKS
         MVC   SPF_MSG_2,COMM_BLANKS
         MVC   SPF_MSG_3,COMM_BLANKS
         MVC   SPF_MSG_4,COMM_BLANKS
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         SR    R15,R15
         IC    R15,DXD_RC                     GET RC
         ITRACE ID=EXIT,                                               +
               RDATA1=R15
         L     R13,4(,R13)                    RESTORE R13
         L     R14,12(,R13)                   RESTORE R14
         LM    R0,R12,20(R13)                 RESTORE REGISTERS
         BR    R14                            RETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXD_START      DC    Q(DXDAUTH)
RACROUTE_I     RACROUTE REQUEST=AUTH,LOG=NONE,MF=L
RACROUTE_L     EQU   *-RACROUTE_I

CLASS          DC    CL8'class'             <---- change this
READVTOC       DC    CL8'READVTOC'
DISPFMT1       DC    CL8'DISPFMT1'
UPDTFMT1       DC    CL8'UPDTFMT1'
UPDTVTOC       DC    CL8'UPDTVTOC'

MSG01          DS    0C
               DC    C'VZAPAUTH01E Not authorized to read VTOCs'
MSG01_L        EQU   *-MSG01

MSG02          DS    0C
               DC    C'VZAPAUTH02E Not authorized to display format-1 i+
               nfo'
MSG02_L        EQU   *-MSG02

MSG03          DS    0C
               DC    C'VZAPAUTH03E Not authorized to update format-1 in+
               fo'
MSG03_L        EQU   *-MSG03

MSG04          DS    0C
               DC    C'VZAPAUTH04E Not authorized to update VTOCs'
MSG04_L        EQU   *-MSG04

AUTHEND        EQU   *
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDAUTH        DSECT
               COPY  DXDPREF

DXD_RACROUTE   DS    0F,(RACROUTE_L)X

DXD_CLASS_LENGTH DS  X
DXD_CLASS_NAME   DS  CL8
DXD_ENTITY       DS  CL8

DXD_MSG_1      DS    CL80

DXD_WORKA      DS    CL512

DXD_RC         DS    X
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               VZAPSPFD  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY REGEQU
               END  VZAPAUTH
