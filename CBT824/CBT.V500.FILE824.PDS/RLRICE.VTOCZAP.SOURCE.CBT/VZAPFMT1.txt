         MACRO
&NAME    FIELD &LABEL,&DISPLAYER,&EDITOR,&DESC
         GBLA  &FLDNBR
         AIF   (T'&LABEL   EQ 'O').NOLABEL
         AIF   (T'&DISPLAYER    EQ 'O').NODISP
         AIF   (T'&EDITOR  EQ 'O').NOEXIT
         AIF   (T'&DESC    EQ 'O').NODESC
&FLDNBR  SETA  &FLDNBR+1
&NAME    DC    CL8'&LABEL'                    FIELD LABEL
         DC    AL2(&FLDNBR)                   FIELD NUMBER
         DC    AL2(&LABEL-VTOCDATA)           DISP TO FIELD IN SCB
         DC    AL4(&DISPLAYER)                DISPLAYER
         DC    VL4(&EDITOR)                   EDITOR
         DC    CL50&DESC                      DESCRIPTION
         MEXIT
.NOLABEL ANOP
         MNOTE 12,'FIELD LABEL OMITTED'
         MEXIT
.NODISP  ANOP
         MNOTE 12,'FIELD DISPLAY ROUTINE OMITTED'
         MEXIT
.NOEXIT  ANOP
         MNOTE 12,'FIELD UPDATE/EDITOR NAME OMITTED'
         MEXIT
.NODESC  ANOP
         MNOTE 12,'DESCRIPTION OMITTED'
         MEND
* ------------------------------------------------------------------- *
*                                                                     *
*  Module name: VZAPFMT1                                              *
*                                                                     *
*   List FIELDS in the selected format-1 DSCB                         *
*                                                                     *
* ------------------------------------------------------------------- *
VZAPFMT1 CSECT
VZAPFMT1 AMODE 31
VZAPFMT1 RMODE ANY
         USING VZAPCOMM,R12
         USING VZAPFMT1,R15
         B     INIT0000
MODID    DC    CL8'VZAPFMT1'
         DC    CL8'&SYSDATE'
         DC    CL8'&SYSTIME'
         DC    A(FLDSEND-VZAPFMT1)
INIT0000 DS    0H
         STM   R14,R12,12(R13)                SAVE REGS
         LR    R10,R15                        COPY ENTRY POINT
         LR    R2,R0                          COPY R0
         LR    R3,R1                          COPY R1
         DROP  R15
         USING VZAPFMT1,R10                   DEFINE BASE
         L     R15,COMM_DXD
         A     R15,DXD_START
         ST    R13,4(,R15)                    SAVE R13
         ST    R15,8(,R13)                    CHAIN SAVE AREA
         LR    R13,R15                        COPY WORK AREA ADDRESS
         USING DXDFLDS,R13                    DEFINE WORK AREA BASE
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY
         L     R9,COMM_VZAPSPFD
         USING VZAPSPFD,R9
         L     R8,COMM_SELECTED_DSCB          SELECTED DSCB
         USING VTOCDATA,R8
         MVC   DXD_FIRST_DISPLAYED,TABLE_TOP
         MVI   DXD_RC,0                       DEFAULT RC
         MVI   DXD_FLAGS,0                    RESET ALL FLAGS
* ------------------------------------------------------------------- *
*        Verify user is allowed to display format-1 info              *
* ------------------------------------------------------------------- *
         ITRACE ID=AUTH
         MVI   COMM_VTOCZAP_FUNCTION,$VTOCZAP_DISPLAY_FMT1
         L     R15,COMM_V_VZAPAUTH
         BALR  R14,R15                        CALL VZAPAUTH
         ITRACE ID=AUTH_RC,                                            +
               RDATA1=R15
         LTR   R15,R15                        ALLOWED?
         BNZ   ERR0030                        NO
* ------------------------------------------------------------------- *
*        Obtain VDATA for dynamic area                                *
* ------------------------------------------------------------------- *
         MVC   COMM_PANEL,FIELDS_PANEL        COPY PANEL NAME
         ITRACE ID=PQRY,                                               +
               DATA1=COMM_PANEL
         L     R15,COMM_V_VZAPPQRY            VZAPPQRY ENTRY POINT
         BALR  R14,R15                        CALL VTOPQRY
         L     R0,SPF_VDEPTH                  NUMBER OF ROWS
         MH    R0,SPF_VWIDTH+2                COMPUTE VDATA SIZE
         ST    R0,DXD_VDATA_LENGTH            SAVE LENGTH
         GETMAIN RU,                          GETMAIN STORAGE FOR VDATA+
               LV=(0),                                                 +
               LOC=ANY
         ST    R1,DXD_VDATA_ADDR              SAVE ADDR
         XC    DXD_VDATA_CURSOR,DXD_VDATA_CURSOR
* ------------------------------------------------------------------- *
*        Initialize dyamic area                                       *
* ------------------------------------------------------------------- *
MAIN0010 DS    0H
         ITRACE ID=INITVDATA
         L     R0,DXD_VDATA_ADDR              VDATA ADDR
         L     R1,DXD_VDATA_LENGTH            VDATA LENGTH
         SR    R14,R14
         SR    R15,R15
         MVCL  R0,R14                         INITIALIZE VDATA BUFFER
* ------------------------------------------------------------------- *
*        Build detail                                                 *
* ------------------------------------------------------------------- *
         MVC   SPF_1,COMM_FMTID               FORMAT ID
         MVC   SPF_DSN,COMM_DSN               DSN
         L     R6,SPF_VDEPTH                  NBR OF LINES AVAILABLE
         L     R7,DXD_VDATA_ADDR              VDATA BUFFER ADDRESS
         L     R5,DXD_FIRST_DISPLAYED         FIRST FIELD TO DISPLAY
         USING FIELD_DSECT,R5
MAIN0020 DS    0H
         MVC   0(MSG01_L,R7),MSG01            INITIALIZE
         USING MSG01,R7
MAIN0030 DS    0H
         ITRACE ID=FIELD,                                              +
               DATA1=FIELD_LABEL,                                      +
               RDATA2=R6
         SR    R0,R0
         ICM   R0,3,FIELD_NBR                 FIELD NUMBER
         CVD   R0,COMM_DWORD                  CONVERT TO DECIMAL
         MVC   MSG01_NBR,=X'40202120'         COPY EDIT WORD
         ED    MSG01_NBR,COMM_DWORD+6         EDIT FIELD NUMBER
         MVC   MSG01_LABEL,FIELD_LABEL        COPY FIELD LABEL
         TM    DXD_FLAGS,$SHOW_DESC           SHOW DESCRIPTION?
         BO    MAIN0040                       YES
         ICM   R15,15,FIELD_DISPLAYER         DISPLAYER
         BZ    MAIN0050                       NO DISPLAYER
         BALR  R14,R15                        LINK TO DISPLAYER
         B     MAIN0050
MAIN0040 DS    0H
         MVC   MSG01_DESC,FIELD_DESC          COPY DESCRIPTION
MAIN0050 DS    0H
         DROP  R7
         A     R7,SPF_VWIDTH                  NEXT LINE
         BCT   R6,MAIN0060
         B     MAIN0070                       SCREEN IS FULL
MAIN0060 DS    0H
         LA    R5,FIELD_DSECT_L(,R5)          NEXT DSCB
         CLI   0(R5),X'FF'                    END OF TABLE?
         BNE   MAIN0020                       NO
* ------------------------------------------------------------------- *
*        Display                                                      *
* ------------------------------------------------------------------- *
MAIN0070 DS    0H
         MVC   COMM_PANEL,FIELDS_PANEL        COPY PANEL NAME
         ITRACE ID=DISPLAY
         MVC   COMM_VDATA_ADDR,DXD_VDATA_ADDR
         MVC   COMM_VDATA_LENGTH,DXD_VDATA_LENGTH
         MVC   COMM_VDATA_CURSOR,DXD_VDATA_CURSOR
         L     R15,COMM_V_VZAPDISP            VZAPDISP ENTRY POINT
         BALR  R14,R15                        CALL VZAPDISP
         ITRACE ID=DISP_RC,                                            +
               DATA1=(COMM_DISP_RC,2)
         OC    COMM_DISP_RC,COMM_DISP_RC      RC = 0?
         BNZ   EXIT0000                       NO
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         ITRACE ID=ZCMD,                                               +
               DATA1=SPF_ZCMD+0,                                       +
               DATA2=SPF_ZCMD+8
         CLC   SPF_ZCMD,COMM_BLANKS           COMMAND PRESENT?
         BE    MAIN0200                       NO.. CHECK FOR SCROLL
         CLC   =C'+ ',SPF_ZCMD                SWITCH SCREENS?
         BE    MAIN0800                       YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         NI    DXD_FLAGS,255-$NOT_NUMERIC     RESET FLAG
         MVC   DXD_COMMAND,COMM_BLANKS        INITIALIZE WORK AREA
         MVC   DXD_NUMBER,=C'00000000'        INITIALIZE
         LA    R1,SPF_ZCMD                    COMMAND
         LA    R2,DXD_COMMAND                 WORK AREA ADDRESS
         LA    R3,L'SPF_ZCMD                  MAX LENGTH
MAIN0110 DS    0H
         CLI   0(R1),C' '                     BLANK?
         BNE   MAIN0120                       NO
         LA    R1,1(,R1)                      NEXT
         BCT   R3,MAIN0110                    KEEP LOOKING
         B     MAIN0200                       SHOULD NOT HAPPEN
MAIN0120 DS    0H
         CH    R3,H8                          MORE THAN 8 LEFT?
         BNH   MAIN0130                       NO
         LH    R3,H8                          LIMIT TO 8 CHARACTERS
MAIN0130 DS    0H
         CLI   0(R1),C' '                     BLANK?
         BE    MAIN0150                       YES
         MVC   0(1,R2),0(R1)                  COPY TO WORK AREA
         MVC   DXD_NUMBER(7),DXD_NUMBER+1     SHIFT LEFT 1 DIGIT
         MVC   DXD_NUMBER+7(1),0(R1)          COPY TO WORK AREA
         CLI   0(R1),C'0'                     DIGIT?
         BNL   MAIN0140
         OI    DXD_FLAGS,$NOT_NUMERIC         NOT NUMERIC
MAIN0140 DS    0H
         LA    R1,1(,R1)                      NEXT IN COMMAND
         LA    R2,1(,R2)                      NEXT IN WORK AREA
         BCT   R3,MAIN0120                    KEEP LOOKING
MAIN0150 DS    0H
         ITRACE ID=COMMAND,                                            +
               DATA1=DXD_COMMAND,                                      +
               DATA2=DXD_NUMBER
         TM    DXD_FLAGS,$NOT_NUMERIC         NON-NUMERIC?
         BO    MAIN0160                       YES
         PACK  COMM_DWORD,DXD_NUMBER          PACK NUMERIC
         CVB   R5,COMM_DWORD                  CONVERT TO DECIMAL
         ITRACE ID=NUMERIC,                                            +
               RDATA1=R5
         CH    R5,MAX_FIELD                   TOO HIGH?
         BH    ERR0010                        YES
         BCTR  R5,0
         MH    R5,=Y(FIELD_DSECT_L)           DISP TO FIELD
         A     R5,TABLE_TOP                   PLUS BASE
         B     MAIN0300
MAIN0160 DS    0H
         L     R5,TABLE_TOP                   FIRST FIELD
MAIN0170 DS    0H
         CLC   FIELD_LABEL,DXD_COMMAND        FIELD LOCATED?
         BE    MAIN0300                       YES
         C     R5,TABLE_BOTTOM                AT LAST FIELD?
         BE    ERR0020                        YES
         LA    R5,FIELD_DSECT_L(,R5)          NEXT FIELD
         B     MAIN0170                       LOOP
* ------------------------------------------------------------------- *
*        Check for line selection                                     *
* ------------------------------------------------------------------- *
MAIN0200 DS    0H
         L     R6,SPF_VDEPTH                  NBR OF LINES AVAILABLE
         L     R7,DXD_VDATA_ADDR              VDATA BUFFER ADDRESS
         USING MSG01,R7
         L     R5,DXD_FIRST_DISPLAYED         FIRST FIELD DISPLAYED
MAIN0210 DS    0H
         CLI   MSG01_SELECT,C' '              BLANK?
         BNE   MAIN0300                       NO
         A     R7,SPF_VWIDTH                  NEXT LINE
         LA    R5,FIELD_DSECT_L(,R5)          NEXT FIELD
         CLI   0(R5),X'FF'                    END OF TABLE?
         BE    MAIN0400                       YES
         BCT   R6,MAIN0210
         B     MAIN0400                       NOTHING SELECTED
         DROP  R7
* ------------------------------------------------------------------- *
*                                                                     *
*        A FIELD has been selected.                                   *
*        FIELD adderss is in R5.                                      *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0300 DS    0H
         ITRACE ID=SELECTED,                                           +
               DATA1=FIELD_LABEL,                                      +
               RDATA2=R5
         TM    DXD_FLAGS,$NOT_AUTH            ALREADY KNOW?
         BO    ERR0040                        YES
         ST    R5,COMM_SELECTED_FIELD         FIELD SELECTED
         ITRACE ID=FIELD,                                              +
               DATA1=FIELD_LABEL,                                      +
               RDATA2=R5
         TM    DXD_FLAGS,$AUTH_VERIFIED       AUTHORIZATION VERIFIED?
         BO    MAIN0310                       YES
         ITRACE ID=UPDTAUTH
         OI    DXD_FLAGS,$AUTH_VERIFIED       SET FLAG
         MVI   COMM_VTOCZAP_FUNCTION,$VTOCZAP_UPDATE_FMT1
         L     R15,COMM_V_VZAPAUTH            VZAPAUTH ENTRY POINT
         BALR  R14,R15                        CALL VZAPAUTH
         ITRACE ID=AUTH_RC,                                            +
               RDATA1=R15
         LTR   R15,R15                        USER AUTHORIZED?
         BZ    MAIN0310                       YES
         OI    DXD_FLAGS,$NOT_AUTH            USER NOT AUTHORIZED
         B     MAIN0070
MAIN0310 DS    0H
         MVC   SPF_ZCMD,COMM_BLANKS           RESET COMMAND
         MVC   SPF_LABEL,FIELD_LABEL          COPY LABEL
         MVC   SPF_DESC,FIELD_DESC            COPY DESCRIPTION
         ICM   R15,15,FIELD_EDITOR            EXIT ENTRY POINT
         ITRACE ID=CALLEDIT,                                           +
               DATA1=FIELD_LABEL,                                      +
               DATA2=(FIELD_EDITOR,4)
         BALR  R14,R15                        CALL EDITOR
         XC    COMM_SELECTED_FIELD,COMM_SELECTED_FIELD
         XC    COMM_BIT_TABLE,COMM_BIT_TABLE  RESET BIT TABLE ADDR
         B     MAIN0010                       REBUILD AND DISPLAY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0400 DS    0H
         ITRACE ID=SCROLL,                                             +
               DATA1=(SPF_ZVERB,1),                                    +
               DATA2=(SPF_ZSCROLLA,1)
         CLI   SPF_ZVERB,C'B'                 BOTTOM?
         BE    MAIN0410                       YES
         CLI   SPF_ZVERB,C'D'                 DOWN?
         BE    MAIN0420                       YES
         CLI   SPF_ZVERB,C'T'                 TOP?
         BE    MAIN0470                       YES
         CLI   SPF_ZVERB,C'U'                 UP?
         BE    MAIN0440                       YES
         B     MAIN0070                       RE-DISPLAY
MAIN0410 DS    0H
         ITRACE ID=BOTTOM
         MVC   DXD_FIRST_DISPLAYED,TABLE_BOTTOM
         B     MAIN0010
MAIN0420 DS    0H
         ITRACE ID=DOWN,                                               +
               DATA1=(SPF_ZSCROLLA,1),                                 +
               DATA2=(SPF_ZSCROLLN,4)
         CLI   SPF_ZSCROLLA,C'M'              MAX?
         BE    MAIN0410                       YES
         L     R5,DXD_FIRST_DISPLAYED         FIRST ON DISPLAY
         L     R1,SPF_ZSCROLLN                NBR TO SCROLL
MAIN0430 DS    0H
         ST    R5,DXD_FIRST_DISPLAYED
         C     R5,TABLE_BOTTOM                AT BOTTOM?
         LA    R5,FIELD_DSECT_L(,R5)          NEXT FIELD
         BE    MAIN0010                       YES
         BCT   R1,MAIN0430                    LOOP
         B     MAIN0010                       BUILD DISPLAY
MAIN0440 DS    0H
         ITRACE ID=UP,                                                 +
               DATA1=(SPF_ZSCROLLA,1),                                 +
               DATA2=(SPF_ZSCROLLN,4)
         CLI   SPF_ZSCROLLA,C'M'              MAX?
         BE    MAIN0470
MAIN0450 DS    0H
         L     R1,SPF_ZSCROLLN                NBR TO SCROLL
MAIN0460 DS    0H
         ST    R5,DXD_FIRST_DISPLAYED
         C     R5,TABLE_TOP                   AT TOP OF TABLE?
         BE    MAIN0010                       YES
         SH    R5,=Y(FIELD_DSECT_L)           UP 1 FIELD
         BCT   R1,MAIN0460                    LOOP
         B     MAIN0010                       BUILD DISPLAY
MAIN0470 DS    0H
         ITRACE ID=TOP
         MVC   DXD_FIRST_DISPLAYED,TABLE_TOP
         B     MAIN0010                       BUILD DISPLAY
* ------------------------------------------------------------------- *
*                                                                     *
*        R14 is used as a linkage register.                           *
*        Do not use ITRACE                                            *
*                                                                     *
* ------------------------------------------------------------------- *
         USING MSG01,R7
MAIN0500 DS    0H
         MVC   MSG01_DESC(L'DS1DSNAM),DS1DSNAM
         BR    R14
MAIN0510 DS    0H
         MVC   MSG01_DESC(L'DS1DSSN),DS1DSSN
         BR    R14
MAIN0520 DS    0H
         SR    R0,R0
         ICM   R0,3,DS1VOLSQ
         CVD   R0,COMM_DWORD
         MVC   DXD_EDITWORD,EDITWORD
         ED    DXD_EDITWORD,COMM_DWORD+3
         MVC   MSG01_DESC(5),DXD_EDITWORD+5
         BR    R14
MAIN0530 DS    0H
         SR    R0,R0
         IC    R0,DS1CREDT
         CVD   R0,COMM_DWORD
         MVC   DXD_EDITWORD,EDITWORD
         ED    DXD_EDITWORD,COMM_DWORD+3
         MVC   MSG01_DESC+0(2),DXD_EDITWORD+8
         MVI   MSG01_DESC+2,C'.'
         ICM   R0,3,DS1CREDT+1
         CVD   R0,COMM_DWORD
         MVC   DXD_EDITWORD,EDITWORD
         ED    DXD_EDITWORD,COMM_DWORD+3
         MVC   MSG01_DESC+3(3),DXD_EDITWORD+7
         BR    R14
MAIN0540 DS    0H
         SR    R0,R0
         IC    R0,DS1EXPDT+0
         CVD   R0,COMM_DWORD
         MVC   DXD_EDITWORD,EDITWORD
         ED    DXD_EDITWORD,COMM_DWORD+3
         MVC   MSG01_DESC+0(2),DXD_EDITWORD+8
         MVI   MSG01_DESC+2,C'.'
         ICM   R0,3,DS1EXPDT+1
         CVD   R0,COMM_DWORD
         MVC   DXD_EDITWORD,EDITWORD
         ED    DXD_EDITWORD,COMM_DWORD+3
         MVC   MSG01_DESC+3(3),DXD_EDITWORD+7
         BR    R14
MAIN0550 DS    0H
         SR    R0,R0
         IC    R0,DS1NOEPV
         CVD   R0,COMM_DWORD
         MVC   DXD_EDITWORD,EDITWORD
         ED    DXD_EDITWORD,COMM_DWORD+3
         MVC   MSG01_DESC(3),DXD_EDITWORD+7
         BR    R14
MAIN0560 DS    0H
         SR    R0,R0
         IC    R0,DS1NOBDB
         CVD   R0,COMM_DWORD
         MVC   DXD_EDITWORD,EDITWORD
         ED    DXD_EDITWORD,COMM_DWORD+3
         MVC   MSG01_DESC(3),DXD_EDITWORD+7
         BR    R14
MAIN0570 DS    0H
         UNPK  COMM_DWORD(3),DS1FLAG1(2)
         TR    COMM_DWORD(2),COMM_HEXCHAR
         MVC   MSG01_DESC(2),COMM_DWORD
         BR    R14
MAIN0580 DS    0H
         MVC   MSG01_DESC(L'DS1SYSCD),DS1SYSCD
         BR    R14
MAIN0590 DS    0H
         SR    R0,R0
         IC    R0,DS1REFD+0
         CVD   R0,COMM_DWORD
         MVC   DXD_EDITWORD,EDITWORD
         ED    DXD_EDITWORD,COMM_DWORD+3
         MVC   MSG01_DESC+0(2),DXD_EDITWORD+8
         MVI   MSG01_DESC+2,C'.'
         ICM   R0,3,DS1REFD+1
         CVD   R0,COMM_DWORD
         MVC   DXD_EDITWORD,EDITWORD
         ED    DXD_EDITWORD,COMM_DWORD+3
         MVC   MSG01_DESC+3(3),DXD_EDITWORD+7
         BR    R14
MAIN0600 DS    0H
         UNPK  COMM_DWORD(3),DS1SMSFG(2)
         TR    COMM_DWORD(2),COMM_HEXCHAR
         MVC   MSG01_DESC(2),COMM_DWORD
         BR    R14
MAIN0610 DS    0H
         UNPK  COMM_DWORD(3),DS1SCXTF(2)
         TR    COMM_DWORD(2),COMM_HEXCHAR
         MVC   MSG01_DESC(2),COMM_DWORD
         BR    R14
MAIN0620 DS    0H
         SR    R0,R0
         ICM   R0,3,DS1SCXTV
         CVD   R0,COMM_DWORD
         MVC   DXD_EDITWORD,EDITWORD
         ED    DXD_EDITWORD,COMM_DWORD+3
         MVC   MSG01_DESC(5),DXD_EDITWORD+5
         BR    R14
MAIN0630 DS    0H
         UNPK  COMM_DWORD(5),DS1DSORG(3)
         TR    COMM_DWORD(4),COMM_HEXCHAR
         MVC   MSG01_DESC(4),COMM_DWORD
         BR    R14
MAIN0640 DS    0H
         UNPK  COMM_DWORD(3),DS1RECFM(2)
         TR    COMM_DWORD(2),COMM_HEXCHAR
         MVC   MSG01_DESC(2),COMM_DWORD
         BR    R14
MAIN0650 DS    0H
         UNPK  COMM_DWORD(3),DS1OPTCD(2)
         TR    COMM_DWORD(2),COMM_HEXCHAR
         MVC   MSG01_DESC(2),COMM_DWORD
         BR    R14
MAIN0660 DS    0H
         SR    R0,R0
         ICM   R0,3,DS1BLKL
         CVD   R0,COMM_DWORD
         MVC   DXD_EDITWORD,EDITWORD
         ED    DXD_EDITWORD,COMM_DWORD+3
         MVC   MSG01_DESC(5),DXD_EDITWORD+5
         BR    R14
MAIN0670 DS    0H
         SR    R0,R0
         ICM   R0,3,DS1LRECL
         CVD   R0,COMM_DWORD
         MVC   DXD_EDITWORD,EDITWORD
         ED    DXD_EDITWORD,COMM_DWORD+3
         MVC   MSG01_DESC(5),DXD_EDITWORD+5
         BR    R14
MAIN0680 DS    0H
         SR    R0,R0
         IC    R0,DS1KEYL
         CVD   R0,COMM_DWORD
         MVC   DXD_EDITWORD,EDITWORD
         ED    DXD_EDITWORD,COMM_DWORD+3
         MVC   MSG01_DESC(3),DXD_EDITWORD+7
         BR    R14
MAIN0690 DS    0H
         SR    R0,R0
         ICM   R0,3,DS1RKP
         CVD   R0,COMM_DWORD
         MVC   DXD_EDITWORD,EDITWORD
         ED    DXD_EDITWORD,COMM_DWORD+3
         MVC   MSG01_DESC(5),DXD_EDITWORD+5
         BR    R14
MAIN0700 DS    0H
         UNPK  COMM_DWORD(3),DS1DSIND(2)
         TR    COMM_DWORD(2),COMM_HEXCHAR
         MVC   MSG01_DESC(2),COMM_DWORD
         BR    R14
MAIN0710 DS    0H
         UNPK  COMM_DWORD(3),DS1SCAL1(2)
         TR    COMM_DWORD(2),COMM_HEXCHAR
         MVC   MSG01_DESC(2),COMM_DWORD
         BR    R14
MAIN0720 DS    0H
         SR    R0,R0
         ICM   R0,7,DS1SCAL3
         CVD   R0,COMM_DWORD
         MVC   DXD_EDITWORD,EDITWORD
         ED    DXD_EDITWORD,COMM_DWORD+3
         MVC   MSG01_DESC(8),DXD_EDITWORD+2
         BR    R14
MAIN0730 DS    0H
         LA    R1,DS1EXT1                     EXTENT 1
         BAL   R15,MAIN0760                   FORMAT EXTENT
         BR    R14
MAIN0740 DS    0H
         LA    R1,DS1EXT2                     EXTENT 2
         BAL   R15,MAIN0760                   FORMAT EXTENT
         BR    R14
MAIN0750 DS    0H
         LA    R1,DS1EXT3                     EXTENT 3
         BAL   R15,MAIN0760                   FORMAT EXTENT
         BR    R14
MAIN0760 DS    0H
         SR    R0,R0
         ICM   R0,3,2(R1)                     'FROM' CYLINDER
         CVD   R0,COMM_DWORD                  CONVERT TO DECIMAL
         MVC   DXD_EDITWORD,EDITWORD          COPY EDITWORD
         ED    DXD_EDITWORD,COMM_DWORD+3      EDIT CYLINDER
         MVC   DXD_CYLINDER,DXD_EDITWORD+5    COPY CYLINDER
         MVI   DXD_HYPHEN1,C'-'               INSERT HYPHEN
         ICM   R0,3,4(R1)                     'FROM' HEAD
         CVD   R0,COMM_DWORD                  CONVERT TO DECIMAL
         MVC   DXD_EDITWORD,EDITWORD          COPY EDITWORD
         ED    DXD_EDITWORD,COMM_DWORD+3      EDIT HEAD NUMBER
         MVC   DXD_HEAD,DXD_EDITWORD+5        COPY HEAD NUMBER
         MVC   MSG01_DESC+00(11),DXD_CYLINDER COPY TO DYNAMIC AREA
         ICM   R0,3,6(R1)                     'TO' CYLINDER
         CVD   R0,COMM_DWORD                  CONVERT TO DECIMAL
         MVC   DXD_EDITWORD,EDITWORD          COPY EDITWORD
         ED    DXD_EDITWORD,COMM_DWORD+3      EDIT CYLINDER
         MVC   DXD_CYLINDER,DXD_EDITWORD+5    COPY CYLINDER
         MVI   DXD_HYPHEN1,C'-'               INSERT HYPHEN
         ICM   R0,3,8(R1)                     'TO' HEAD
         CVD   R0,COMM_DWORD                  CONVERT TO DECIMAL
         MVC   DXD_EDITWORD,EDITWORD          COPY EDITWORD
         ED    DXD_EDITWORD,COMM_DWORD+3      EDIT HEAD NUMBER
         MVC   DXD_HEAD,DXD_EDITWORD+5        COPY HEAD NUMBER
         MVC   MSG01_DESC+15(11),DXD_CYLINDER COPY TO DYNAMIC AREA
         BR    R14
MAIN0780 DS    0H
         SR    R0,R0
         ICM   R0,3,DS1PTRDS+0                CYLINDER
         CVD   R0,COMM_DWORD                  CONVERT TO DECIMAL
         MVC   DXD_EDITWORD,EDITWORD          COPY EDITWORD
         ED    DXD_EDITWORD,COMM_DWORD+3      EDIT CYLINDER
         MVC   DXD_CYLINDER,DXD_EDITWORD+5    COPY CYLINDER
         MVI   DXD_HYPHEN1,C'-'               INSERT HYPHEN
         ICM   R0,3,DS1PTRDS+2                HEAD
         CVD   R0,COMM_DWORD                  CONVERT TO DECIMAL
         MVC   DXD_EDITWORD,EDITWORD          COPY EDITWORD
         ED    DXD_EDITWORD,COMM_DWORD+3      EDIT HEAD NUMBER
         MVC   DXD_HEAD,DXD_EDITWORD+5        COPY HEAD NUMBER
         MVI   DXD_HYPHEN2,C'-'               INSERT HYPHEN
         SR    R0,R0
         IC    R0,4(R1)
         IC    R0,DS1PTRDS+4                  RECORD
         CVD   R0,COMM_DWORD                  CONVERT TO DECIMAL
         MVC   DXD_EDITWORD,EDITWORD          COPY EDITWORD
         ED    DXD_EDITWORD,COMM_DWORD+3      EDIT RECORD NUMBER
         MVC   DXD_RECORD,DXD_EDITWORD+7      COPY RECORD
         MVC   MSG01_DESC(15),DXD_CYLINDER    COPY TO DYNAMIC AREA
         BR    R14
         DROP  R7
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0800 DS    0H
         ITRACE ID=SWITCH,                                             +
               DATA1=(DXD_FLAGS,1)
         MVC   SPF_ZCMD,COMM_BLANKS           RESET COMMAND
         XI    DXD_FLAGS,$SHOW_DESC           SWITCH SCREENS
         B     MAIN0010
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0010  DS    0H
         MVC   SPF_MSG_1(EMSG01_L),EMSG01
         B     MAIN0070
ERR0020  DS    0H
         MVC   SPF_MSG_1(EMSG02_L),EMSG02
         B     MAIN0070
ERR0030  DS    0H
         ITRACE ID=NOT_AUTH
         MVI   DXD_RC,12
         B     EXIT0000
ERR0040  DS    0H
         MVC   SPF_MSG_1(EMSG03_L),EMSG03
         B     MAIN0070
* ------------------------------------------------------------------- *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ICM   R1,15,DXD_VDATA_ADDR           VDATA GETMAINED?
         BZ    EXIT0010                       NO
         L     R0,DXD_VDATA_LENGTH            VDATA LENGTH
         FREEMAIN RU,                         FREEMAIN VDATA           +
               A=(1),                                                  +
               LV=(0)
EXIT0010 DS    0H
         SR    R15,R15
         IC    R15,DXD_RC                     GET RETURN CODE
         ITRACE ID=EXIT,                                               +
               RDATA1=R15
         L     R13,4(,R13)                    RESTORE R13
         L     R14,12(,R13)                   RESTORE R14
         LM    R0,R12,20(R13)                 RESTORE REGISTERS
         BR    R14                            RETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXD_START     DC    Q(DXDFLDS)
              LTORG

TABLE_TOP     DC    A(FIRST_FIELD)
TABLE_BOTTOM  DC    A(LAST_FIELD)
MAX_FIELD     DC    Y((LAST_FIELD-FIRST_FIELD)/(SECOND_FIELD-FIRST_FIEL+
               D)+1)
H8            DC    H'8'

FIELDS_PANEL  DC    C'VZAPFMT1'

EDITWORD      DC    X'F0202020202020202120'

EMSG01        DS    0C
              DC    C'VZAPFMT101E Invalid field number'
EMSG01_L      EQU   *-EMSG01

EMSG02        DS    0C
              DC    C'VZAPFMT102E Invalid field name'
EMSG02_L      EQU   *-EMSG02

EMSG03        DS    0C
              DC    C'VZAPFMT103E You are not authorized'
EMSG03_L      EQU   *-EMSG03

MSG01         DS    0C
              DC    AL1($SCREEN_ATTR_INPUT)
MSG01_SELECT  DC    C' '
              DC    AL1($SCREEN_ATTR_HIGH_TURQUOISE)
MSG01_NBR     DC    CL4' '
              DC    C' '
MSG01_LABEL   DC    CL8' '
              DC    AL1($SCREEN_ATTR_HIGH_GREEN)
MSG01_DESC    DC    CL50' '
MSG01_L       EQU   *-MSG01

FIRST_FIELD   FIELD DS1DSNAM,                                          +
               MAIN0500,                                               +
               VZAPDSNM,                                               +
               'Data set name'

SECOND_FIELD  FIELD DS1DSSN,                                           +
               MAIN0510,                                               +
               VZAPDSSN,                                               +
               'Data set VOLSER'

              FIELD DS1VOLSQ,                                          +
               MAIN0520,                                               +
               VZAPNBR2,                                               +
               'Volume sequence number'

              FIELD DS1CREDT,                                          +
               MAIN0530,                                               +
               VZAPDATE,                                               +
               'Creation date'

              FIELD DS1EXPDT,                                          +
               MAIN0540,                                               +
               VZAPDATE,                                               +
               'Expiration date'

              FIELD DS1NOEPV,                                          +
               MAIN0550,                                               +
               VZAPNBR1,                                               +
               'Number of extents on volume'

              FIELD DS1NOBDB,                                          +
               MAIN0560,                                               +
               VZAPNBR1,                                               +
               'Bytes used in last directory block'

              FIELD DS1FLAG1,                                          +
               MAIN0570,                                               +
               VZAPFLG1,                                               +
               'Flag 1'

              FIELD DS1SYSCD,                                          +
               MAIN0580,                                               +
               VZAPSCDE,                                               +
               'System code'

              FIELD DS1REFD,                                           +
               MAIN0590,                                               +
               VZAPDATE,                                               +
               'Reference date'

              FIELD DS1SMSFG,                                          +
               MAIN0600,                                               +
               VZAPSMS,                                                +
               'SMS flags'

              FIELD DS1SCXTF,                                          +
               MAIN0610,                                               +
               VZAPCXTF,                                               +
               'Secondary space extension flags'

              FIELD DS1SCXTV,                                          +
               MAIN0620,                                               +
               VZAPNBR2,                                               +
               'Secondary space extension value'

              FIELD DS1DSORG,                                          +
               MAIN0630,                                               +
               VZAPORG,                                                +
               'DSORG'

              FIELD DS1RECFM,                                          +
               MAIN0640,                                               +
               VZAPRECF,                                               +
               'Record format (RECFM)'

              FIELD DS1OPTCD,                                          +
               MAIN0650,                                               +
               VZAPOPT,                                                +
               'Option codes'

              FIELD DS1BLKL,                                           +
               MAIN0660,                                               +
               VZAPNBR2,                                               +
               'Block size'

              FIELD DS1LRECL,                                          +
               MAIN0670,                                               +
               VZAPNBR2,                                               +
               'Record length'

              FIELD DS1KEYL,                                           +
               MAIN0680,                                               +
               VZAPNBR1,                                               +
               'Key length'

              FIELD DS1RKP,                                            +
               MAIN0690,                                               +
               VZAPNBR2,                                               +
               'Key position (RKP)'

              FIELD DS1DSIND,                                          +
               MAIN0700,                                               +
               VZAPIND,                                                +
               'Data set indicators'

              FIELD DS1SCAL1,                                          +
               MAIN0710,                                               +
               VZAPCAL1,                                               +
               'Secondary allocation parms'

              FIELD DS1SCAL3,                                          +
               MAIN0720,                                               +
               VZAPNBR3,                                               +
               'Secondary space quantity'

              FIELD DS1EXT1,                                           +
               MAIN0730,                                               +
               VZAPXTNT,                                               +
               'EXTENT 1'

              FIELD DS1EXT2,                                           +
               MAIN0740,                                               +
               VZAPXTNT,                                               +
               'EXTENT 2'

              FIELD DS1EXT3,                                           +
               MAIN0750,                                               +
               VZAPXTNT,                                               +
               'EXTENT 3'

LAST_FIELD    FIELD DS1PTRDS,                                          +
               MAIN0780,                                               +
               VZAPVPTR,                                               +
               'Format 3, 8, or 9 address'

              DC    X'FF'
FLDSEND       EQU   *
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDFLDS              DSECT
                     COPY DXDPREF
DXD_VDATA_ADDR       DS   A
DXD_VDATA_LENGTH     DS   F
DXD_VDATA_CURSOR     DS   A
DXD_FIRST_DISPLAYED  DS   A

DXD_RC               DS   X

DXD_FLAGS            DS   X
$NOT_NUMERIC         EQU  X'80'
$SHOW_DESC           EQU  X'40'
$AUTH_VERIFIED       EQU  X'20'
$NOT_AUTH            EQU  X'10'

DXD_COMMAND          DS   CL8
DXD_NUMBER           DS   CL8
DXD_EDITWORD         DS   CL10

DXD_CYLINDER         DS   CL5
DXD_HYPHEN1          DS   C
DXD_HEAD             DS   CL5
DXD_HYPHEN2          DS   C
DXD_RECORD           DS   CL3

* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY     VZAPFLD
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         VZAPSPFD TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY     VTOCDATA
         ORG      VTOC_DSCB
         IECSDSL1 1
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY     ATTRS
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY     REGEQU
         END      VZAPFMT1
