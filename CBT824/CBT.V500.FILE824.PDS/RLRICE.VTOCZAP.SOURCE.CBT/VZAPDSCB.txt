* ------------------------------------------------------------------- *
*                                                                     *
*  Module name: VZAPDSCB                                              *
*                                                                     *
*   List DSCBs in the VZAP as a scrollable list with a line selection.*
*                                                                     *
* ------------------------------------------------------------------- *
VZAPDSCB CSECT
VZAPDSCB AMODE 31
VZAPDSCB RMODE ANY
         USING VZAPCOMM,R12
         USING VZAPDSCB,R15
         B     INIT0000
MODID    DC    CL8'VZAPDSCB'
         DC    CL8'&SYSDATE'
         DC    CL8'&SYSTIME'
         DC    A(DSCBEND-VZAPDSCB)
INIT0000 DS    0H
         STM   R14,R12,12(R13)                SAVE REGS
         LR    R10,R15                        COPY ENTRY POINT
         LR    R2,R0                          COPY R0
         LR    R3,R1                          COPY R1
         DROP  R15
         USING VZAPDSCB,R10                   DEFINE BASE
         L     R15,COMM_DXD
         A     R15,DXD_START
         ST    R13,4(,R15)                    SAVE R13
         ST    R15,8(,R13)                    CHAIN SAVE AREA
         LR    R13,R15                        COPY WORK AREA ADDRESS
         USING DXDDSCB,R13                    DEFINE WORK AREA BASE
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY
         L     R9,COMM_VZAPSPFD
         USING VZAPSPFD,R9
         MVC   DXD_FIRST_DISPLAYED,COMM_VTOC_DATA
* ------------------------------------------------------------------- *
*        Obtain VDATA for dynamic area                                *
* ------------------------------------------------------------------- *
         MVC   COMM_PANEL,DSCB_PANEL          COPY PANEL NAME
         ITRACE ID=PQRY,                                               +
               DATA1=COMM_PANEL
         L     R15,COMM_V_VZAPPQRY            VZAPPQRY ENTRY POINT
         BALR  R14,R15                        CALL VTOPQRY
         L     R0,SPF_VDEPTH                  NUMBER OF ROWS
         MH    R0,SPF_VWIDTH+2                COMPUTE VDATA SIZE
         ST    R0,DXD_VDATA_LENGTH            SAVE LENGTH
         GETMAIN RU,                          GETMAIN STORAGE FOR VDATA+
               LV=(0),                                                 +
               LOC=ANY
         ST    R1,DXD_VDATA_ADDR              SAVE ADDR
         XC    DXD_VDATA_CURSOR,DXD_VDATA_CURSOR
* ------------------------------------------------------------------- *
*        Initialize dyamic area                                       *
* ------------------------------------------------------------------- *
MAIN0010 DS    0H
         ITRACE ID=INITVDATA
         L     R0,DXD_VDATA_ADDR              VDATA ADDR
         L     R1,DXD_VDATA_LENGTH            VDATA LENGTH
         SR    R14,R14
         SR    R15,R15
         MVCL  R0,R14                         INITIALIZE VDATA BUFFER
* ------------------------------------------------------------------- *
*        Any updates pending?                                         *
* ------------------------------------------------------------------- *
         TM    COMM_FLAGS,$COMM_UPDATE        UPDATE(S) PENDING?
         BNO   MAIN0020                       NO
         MVC   SPF_MSG_4(EMSG01_L),EMSG01
* -- ---------------------------------------------------------------- *
*        Build detail                                                 *
* ------------------------------------------------------------------- *
MAIN0020 DS    0H
         L     R6,SPF_VDEPTH                  NBR OF LINES AVAILABLE
         L     R7,DXD_VDATA_ADDR              VDATA BUFFER ADDRESS
         L     R8,DXD_FIRST_DISPLAYED         FIRST VTOCDATA TO DISPLAY
         USING VTOCDATA,R8
MAIN0030 DS    0H
         ITRACE ID=DATA,                                               +
               RDATA1=R8,                                              +
               RDATA2=R7
         MVC   0(MSG01_L,R7),MSG01            INITIALIZE
         USING MSG01,R7
         MVC   MSG01_FMTID,DS1FMTID           COPY FORMAT ID
         UNPK  COMM_DWORD(5),VTOC_COUNT+0(3)  UNPACK CYLINDER
         TR    COMM_DWORD(4),HEXCHAR          TRANSLATE
         MVC   MSG01_CCX,COMM_DWORD           COPY CYLINDER
         UNPK  COMM_DWORD(5),VTOC_COUNT+2(3)  UNPACK HEAD
         TR    COMM_DWORD(4),HEXCHAR          TRANSLATE
         MVC   MSG01_HHX,COMM_DWORD           COPY HEAD
         UNPK  COMM_DWORD(3),VTOC_COUNT+4(2)  UNPACK RECORD
         TR    COMM_DWORD(2),HEXCHAR          TRANSLATE
         MVC   MSG01_RX,COMM_DWORD            COPY RECORD
         CLI   DS1FMTID,C'1'                  FORMAT-1?
         BNE   MAIN0040                       NO
         MVC   MSG01_DSN,DS1DSNAM             COPY DATA SET NAME
         DROP  R7
MAIN0040 DS    0H
         A     R7,SPF_VWIDTH                  NEXT LINE
         BCT   R6,MAIN0050
         B     MAIN0060                       SCREEN IS FULL
MAIN0050 DS    0H
         ICM   R8,15,VTOC_NEXT                NEXT DSCB
         BNZ   MAIN0030                       LOOP
* ------------------------------------------------------------------- *
*        Display                                                      *
* ------------------------------------------------------------------- *
MAIN0060 DS    0H
         ITRACE ID=DISPLAY
         MVC   COMM_PANEL,DSCB_PANEL          COPY PANEL NAME
         MVC   COMM_VDATA_ADDR,DXD_VDATA_ADDR
         MVC   COMM_VDATA_LENGTH,DXD_VDATA_LENGTH
         MVC   COMM_VDATA_CURSOR,DXD_VDATA_CURSOR
         L     R15,COMM_V_VZAPDISP            VZAPDISP ENTRY POINT
         BALR  R14,R15                        CALL VZAPDISP
         ITRACE ID=DISP_RC,                                            +
               DATA1=(COMM_DISP_RC,2)
         OC    COMM_DISP_RC,COMM_DISP_RC      RC = 0?
         BNZ   EXIT0000                       NO
* ------------------------------------------------------------------- *
*        Determine if user wants to scroll                            *
* ------------------------------------------------------------------- *
         CLI   SPF_ZVERB,C'B'                 BOTTOM
         BE    MAIN0100                       YES
         CLI   SPF_ZVERB,C'D'                 DOWN?
         BE    MAIN0120                       YES
         CLI   SPF_ZVERB,C'U'                 UP?
         BE    MAIN0140                       YES.. FOR NOW
         CLI   SPF_ZVERB,C'T'                 TOP?
         BE    MAIN0190                       YES
         B     MAIN0200                       NO SCROLL, TRY LINE CMD
MAIN0100 DS    0H
         ITRACE ID=BOTTOM
         ICM   R8,15,DXD_FIRST_DISPLAYED      FIRST ON DISPLAY
MAIN0110 DS    0H
         ST    R8,DXD_FIRST_DISPLAYED
         ICM   R8,15,VTOC_NEXT                NEXT ON CHAIN
         BNZ   MAIN0110                       FOLLOW CHAIN
         B     MAIN0010                       BUILD DISPLAY
MAIN0120 DS    0H
         ITRACE ID=DOWN,                                               +
               DATA1=(SPF_ZSCROLLA,1),                                 +
               DATA2=(SPF_ZSCROLLN,4)
         CLI   SPF_ZSCROLLA,C'M'              MAX?
         BE    MAIN0100                       YES.. GO TO BOTTOM
         L     R1,SPF_ZSCROLLN                NBR OF LINES TO SCROLL
         ICM   R8,15,DXD_FIRST_DISPLAYED      FIRST ON SCREEN
MAIN0130 DS    0H
         STCM  R8,15,DXD_FIRST_DISPLAYED      SET FIRST ON DISPLAY
         ICM   R8,15,VTOC_NEXT                NEXT ON CHAIN
         BZ    MAIN0010                       END OF CHAIN STOP HERE
         BCT   R1,MAIN0130                    LOOP
         B     MAIN0010                       BUILD DISPLAY
MAIN0140 DS    0H
         ITRACE ID=UP
         CLI   SPF_ZSCROLLA,C'M'              MAX?
         BE    MAIN0190                       YES
         ICM   R1,15,SPF_ZSCROLLN             NBR OF LINES TO SCROLL
         BZ    MAIN0010                       NONE..
         ICM   R8,15,DXD_FIRST_DISPLAYED      FIRST ON DISPLAY
MAIN0150 DS    0H
         ST    R8,DXD_FIRST_DISPLAYED         SAVE FOR NEXT START POINT
         ICM   R8,15,VTOC_PREVIOUS            PREVIOUS VTOC RECORD
         BZ    MAIN0010                       END OF CHAIN.. STOP
         OC    VTOC_PREVIOUS,VTOC_PREVIOUS    END OF CHAIN?
         BZ    MAIN0010                       YES.. STOP
         BCT   R1,MAIN0150                    LOOP
         B     MAIN0010                       BUILD DISPLAY
MAIN0190 DS    0H
         ITRACE ID=TOP
         MVC   DXD_FIRST_DISPLAYED,COMM_VTOC_DATA
         B     MAIN0010                       BUILD DISPLAY
* ------------------------------------------------------------------- *
*        Determine if there were any line commands                    *
* ------------------------------------------------------------------- *
MAIN0200 DS    0H
         L     R6,SPF_VDEPTH                  NBR OF LINES AVAILABLE
         L     R7,DXD_VDATA_ADDR              VDATA BUFFER ADDRESS
         L     R8,DXD_FIRST_DISPLAYED         FIRST VTOCDATA TO DISPLAY
         NI    DXD_FLAGS,255-$SELECT          RESET FLAG
MAIN0210 DS    0H
         ITRACE ID=LINE_CMD,                                           +
               RDATA1=R7,                                              +
               DATA2=(MSG01_SELECT,1)
         USING MSG01,R7
         CLI   MSG01_SELECT,C' '              BLANK?
         BE    MAIN0310                       YES
         CLI   DS1FMTID,C'1'                  FORMAT-1?
         BE    MAIN0220                       YES
         CLI   DS1FMTID,C'3'                  FORMAT-3?
         BE    MAIN0230                       YES
         CLI   DS1FMTID,C'4'                  FORMAT-4?
         BE    MAIN0240                       YES
         ITRACE ID=BAD_FMT
         MVC   SPF_MSG_1(EMSG01_L),EMSG01
         B     MAIN0060
MAIN0220 DS    0H
         ITRACE ID=FMT1,                                               +
               DATA1=DS1DSNAM+00,                                      +
               DATA2=DS1DSNAM+08
         MVC   COMM_DSN,DS1DSNAM              COPY DSN
         B     MAIN0300
MAIN0230 DS    0H
         ITRACE ID=FMT3
         MVC   COMM_DSN,FORMAT_3              INDICATE IT'S A FORMAT-3
         B     MAIN0300
MAIN0240 DS    0H
         ITRACE ID=FMT4
         MVC   COMM_DSN,FORMAT_4              INDICATE IT'S A FORMAT-4
MAIN0300 DS    0H
         MVI   MSG01_SELECT,C' '              RESET SELECTION
         MVC   COMM_FMTID,MSG01_FMTID         COPY FORMAT ID
         MVC   SPF_CCHHR,MSG01_CCX            COPY DISK ADDR
         OI    DXD_FLAGS,$SELECT              SOMETHING SELECTED
         ST    R8,COMM_SELECTED_DSCB          SET DSCB ADDRESS
         ITRACE ID=SELECTED,                                           +
               RDATA1=R8,                                              +
               DATA2=DS1DSNAM
         L     R15,COMM_V_VZAPFMT1            FMT1 FIELD DISPLAY
         ITRACE ID=FIELDS,                                             +
               RDATA1=R15
         BALR  R14,R15                        CALL VZAPFMT1
         XC    COMM_SELECTED_DSCB,COMM_SELECTED_DSCB
MAIN0310 DS    0H
         DROP  R7
         A     R7,SPF_VWIDTH                  NEXT LINE
         BCT   R6,MAIN0320
         B     MAIN0400                       ALL LINE CHECKED
MAIN0320 DS    0H
         ICM   R8,15,VTOC_NEXT                ANOTHER VTOC RECORD?
         BNZ   MAIN0210                       YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0400 DS    0H
         TM    DXD_FLAGS,$SELECT              ANYTHING SELECTED?
         BO    MAIN0010                       YES
* ------------------------------------------------------------------- *
*        Check for primary command                                    *
* ------------------------------------------------------------------- *
         SR    R0,R0
         ICM   R0,3,COMM_OPERANDS_NBR         ANY OPERANDS?
         BZ    MAIN0010                       NO
         ICM   R1,15,COMM_OPERAND01_ADDRESS   1ST OPERAND
         CLC   CAN,0(R1)                      CAN?
         BE    MAIN0500                       YES
         CLC   CANCEL,0(R1)                   CANCEL?
         BE    MAIN0500                       YES
         CLC   SORT,0(R1)                     SORT?
         BE    MAIN0600                       YES
         MVC   SPF_MSG_2(EMSG04_L),EMSG04     COPY ERROR MESSAGE
         B     MAIN0060
MAIN0500 DS    0H
         TM    COMM_FLAGS,$COMM_UPDATE        ANY UPDATES PENDING?
         BNO   EXIT0000                       NO
         ITRACE ID=WARN
         B     EXIT0000                       EXIT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0600 DS    0H
         ICM   R1,15,COMM_OPERAND02_ADDRESS   OPERAND 2 ADDRESS
         ITRACE ID=SORT,                                               +
               DATA1=(COMM_OPERANDS_NBR,2),                            +
               RDATA2=R1
         CH    R0,H2                          CORRECT NBR OF OPERANDS?
         BNE   ERR0010                        NO
         CLC   DSN,0(R1)                      SORT BY DSN?
         BE    MAIN0610                       YES
         CLC   NAME,0(R1)                     SORT BY NAME (DSN)?
         BE    MAIN0610                       YES
         CLC   ADDR,0(R1)                     SORT BY ADDR (DISK ADDR)?
         BE    MAIN0620                       YES
         CLC   CCHHR,0(R1)                    SORT BY CCHHR (DISK ADDR)
         BE    MAIN0620                       YES
         B     ERR0020
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0610 DS     0H
         ITRACE ID=BY_DSN
         LA    R2,DSN_CLC                     CLC FOR SORT
         B     MAIN0700                       START SORT
MAIN0620 DS     0H
         ITRACE ID=BY_ADDR
         LA    R2,CCHHR_CLC                   CLC FOR SORT
* ------------------------------------------------------------------- *
*                                                                     *
*        Sort DSCB blocks into ascending sequence.                    *
*                                                                     *
*        R2 is address of CLC to use for comparing "keys"             *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0700 DS    0H
         MVC   SPF_ZCMD,COMM_BLANKS           RESET COMMAND
         ICM   R3,15,COMM_VTOC_DATA           FIRST DSCB
         BZ    MAIN0010                       NONE TO SORT
DSCB1    USING VTOCDATA,R3
MAIN0710 DS    0H
         LR    R5,R3                          ASSUME FIRST IS LOWEST
LOW      USING VTOCDATA,R5
         ICM   R4,15,DSCB1.VTOC_NEXT          NEXT ON CHAIN
         BZ    MAIN0010                       ONLY 1 LEFT
DSCB2    USING VTOCDATA,R4
MAIN0720 DS    0H
         EX    R0,0(R2)                       COMPARE DATA
         BL    MAIN0730                       FIRST IS LOWER
         LR    R5,R4                          SAVE ADDR OF LOWEST
MAIN0730 DS    0H
         ICM   R4,15,DSCB2.VTOC_NEXT          NEXT DSCB
         BNZ   MAIN0720
         CR    R3,R5                          LOWEST ALREADY IN PLACE?
         BE    MAIN0740                       YES
         XC    16(VTOCDATA_L-16,R3),16(R5)
         XC    16(VTOCDATA_L-16,R5),16(R3)    SWAP ALL BUT HEADER INFO
         XC    16(VTOCDATA_L-16,R3),16(R5)
MAIN0740 DS    0H
         ICM   R3,15,DSCB1.VTOC_NEXT          DROP DOWN 1
         BNZ   MAIN0710                       SCAN AGAIN
         B     MAIN0010

DSN_CLC   CLC   LOW.VTOC_DSCB(44),DSCB2.VTOC_DSCB    COMPARE DSNs
CCHHR_CLC CLC   LOW.VTOC_COUNT(5),DSCB2.VTOC_COUNT   COMPARE DISK ADDRs
         DROP  DSCB1,DSCB2,LOW
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0010  DS    0H
         ITRACE ID=BAD_NBR
         MVC   SPF_MSG_1(EMSG05_L),EMSG05
         B     MAIN0010
ERR0020  DS    0H
         ITRACE ID=BAD_SORT
         MVC   SPF_MSG_1(EMSG06_L),EMSG06
         B     MAIN0010
* ------------------------------------------------------------------- *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=EXIT
         ICM   R1,15,DXD_VDATA_ADDR           VDATA GETMAINED?
         L     R0,DXD_VDATA_LENGTH            LENGTH
         BZ    EXIT0010                       NO
         FREEMAIN RU,                         FREEMAIN VDATA           +
               A=(1),                                                  +
               LV=(0)
EXIT0010 DS    0H
         L     R13,4(,R13)                    RESTORE SAVE AREA ADDRESS
         LM    R14,R12,12(R13)                RESTORE REGISTERS
         SR    R15,R15                        SET RC
         BR    R14                            RETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXD_START     DC    Q(DXDDSCB)
              LTORG

H2            DC    H'2'

HEXCHAR       EQU   *-C'0'
              DC    C'0123456789ABCDEF'

ADDR          DC    C'ADDR '
CAN           DC    C'CAN '
CANCEL        DC    C'CANCEL '
CCHHR         DC    C'CCHHR '
DSN           DC    C'DSN '
NAME          DC    C'NAME '
SORT          DC    C'SORT'

DSCB_PANEL    DC    C'VZAPDSCB'

MSG01         DS    0C
              DC    AL1($SCREEN_ATTR_INPUT)
MSG01_SELECT  DC    C' '
              DC    AL1($SCREEN_ATTR_HIGH_TURQUOISE)
MSG01_CCX     DC    CL4' '
              DC    C'-'
MSG01_HHX     DC    CL4' '
              DC    C'-'
MSG01_RX      DC    CL2' '
              DC    CL4' '
              DC    AL1($SCREEN_ATTR_HIGH_YELLOW)
MSG01_FMTID   DC    C' '
              DC    AL1($SCREEN_ATTR_HIGH_GREEN)
MSG01_DSN     DC    CL44' '
MSG01_L       EQU   *-MSG01

FORMAT_3      DC    C'Format-3'
FORMAT_4      DC    C'Format-4'

EMSG01        DS    0C
              DC    C'VZAPDSCB01I Updates pending'
EMSG01_L      EQU   *-EMSG01

EMSG02        DS    0C
              DC    C'VZAPDSCB02I VTOCZAP does not support this DSCB'
EMSG02_L      EQU   *-EMSG02

EMSG03        DS    0C
              DC    C'VZAPDSCB03I Select a DSCB'
EMSG03_L      EQU   *-EMSG03

EMSG04        DS    0C
              DC    C'VZAPDSCB04I Invalid primary command'
EMSG04_L      EQU   *-EMSG04

EMSG05        DS    0C
              DC    C'VZAPDSCB05E Incorrect number of SORT operands'
EMSG05_L      EQU   *-EMSG05

EMSG06        DS    0C
              DC    C'VZAPDSCB06E Invalid SORT field'
EMSG06_L      EQU   *-EMSG06
DSCBEND       EQU   *
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDDSCB              DSECT
                     COPY DXDPREF

DXD_VDATA_ADDR       DS   A
DXD_VDATA_LENGTH     DS   F
DXD_VDATA_CURSOR     DS   F
DXD_FIRST_DISPLAYED  DS   A

DXD_FLAGS            DS   X
$SELECT              EQU  X'80'          AT LEAST ONE DSCB SELECTED
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY     VTOCDATA
         ORG      VTOC_DSCB
         IECSDSL1 1
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         VZAPSPFD TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY     ATTRS
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY     REGEQU
         END      VZAPDSCB
