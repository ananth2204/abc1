//MIKEX    JOB 1,'M WOJTUKIEWICZ',
//             NOTIFY=&SYSUID,TIME=30,REGION=6M,
//             CLASS=A,MSGCLASS=X
//* ------------------------------------------------------------- *
//*                                                               *
//*        Assemble, Link, and Go  JCL  to run FIXCATLG           *
//*                                                               *
//* ------------------------------------------------------------- *
//*****************************************************************
//* Copyright by Mike Wojtukiewicz                                *
//*                                                               *
//* Function of this code                                         *
//* You supply 2 items                                            *
//* 1 - MASTERCatalog you wish to catalog datasets in (it is your)*
//*     responsibility to IMPORT it if it doesnt exist on you     *
//*     present system                                            *
//* 2 - a list volsers and symbols to use                         *
//* What this program then does is list the datasets on all the   *
//* volumes and then deletes without scratching and defining the  *
//* datasets on those volumes with their corresponding symbolic   *
//* value using IDCAMS                                            *
//*     AND                                                       *
//* create DDDEF statements for UCLIN processing to connect a     *
//* Target/DLIB ddname to a dataset on a specific VOLSER          *
//* 2 datasets will be defined to run in their respective IDCAMS  *
//* and SMPE batch jobs. See <<<=== at the bottom of what to      *
//* modify for your job to run                                    *
//*****************************************************************
//ASMACLG  PROC
//ASM      EXEC PGM=ASMA90
//SYSLIB   DD  DSN=SYS1.MACLIB,DISP=SHR
//SYSUT1   DD  DSN=&&SYSUT1,SPACE=(4096,(120,120)),UNIT=3390
//SYSPRINT DD  SYSOUT=*
//SYSLIN   DD  DSN=&&OBJ,SPACE=(3040,(40,40)),UNIT=3390,
//             DISP=(MOD,PASS),
//             DCB=(BLKSIZE=3040,LRECL=80,RECFM=FBS,BUFNO=1)
//LKED     EXEC PGM=HEWLF064,PARM='MAP,LET,LIST',COND=(8,LT,ASM)
//SYSLIN   DD  DSN=&&OBJ,DISP=(OLD,DELETE)
//         DD  DDNAME=SYSIN
//SYSLMOD  DD  DISP=(,PASS),UNIT=3390,SPACE=(TRK,(4,2,1)),
//             DSN=&&LL(FIXCATLG)
//SYSUT1   DD  DSN=&&SYSUT1,SPACE=(1024,(120,120)),UNIT=3390
//SYSPRINT DD  SYSOUT=*
// PEND
//*
//ALLOC  EXEC PGM=IEFBR14
//SYSPRINT DD DISP=(,PASS),DSN=&&SYSPR,UNIT=3390,SPACE=(TRK,(90,90))
//*
//ASSEM2 EXEC ASMACLG
//ASM.SYSIN  DD  *
FIXCATLG CSECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
BASE     EQU   3
         PRINT NOGEN
         STM   14,12,12(13)
         BALR  BASE,R0
         USING *,BASE
         LR    12,13
         LA    13,S
         ST    13,8(12)
         ST    12,S+4
         B     BYP
MOVEMCAT MVC   DEFMCATN(1),2(R4)
DW       DS    D
OPT      DC    F'0'
BYP      DS    0F
         EJECT
         L     R4,0(R1)                Extract MASTERCat name from PARM
         LH    R5,0(R4)
         BCTR  R5,0
         EX    R5,MOVEMCAT
*
         OPEN  (SYSIN,(OUTPUT))        Open SYSIN for IEHLIST
*
         OPEN  (SYSRES,(INPUT))        Open Disk input file
*
         LA    R11,1                   Start ctr at one
         LA    R8,RESES                Load start of VS:Symbol tbl
SRLOOP   EQU   *
*
         GET   SYSRES,INAREA           Get PARM
*
         CVD   R11,DW                  Convert
         MVC   DW(4),=X'40212020'        to a digit
         ED    DW(4),DW+6
         MVC   0(6,R8),INAREA          Extract volser
         MVC   6(9,R8),INAREA+7        and symbol from card
         MVC   VOLSTU3V,INAREA
         MVC   VOLSTU1D+1(1),DW+3
         MVC   IEHCARDV,INAREA
*
         PUT   SYSIN,IEHCARD           Put IEHLIST card
*
         LA    R1,ALLCVOLS             DYNALLOC that VOLSER
*
         DYNALLOC
*                                      Dealing w SYSRES 1?
         CLC   6(9,R8),=C'&&SYSR1      '
         BNE   NSR1                    Nope - jump
*                                      Yes - set VOL to ******
         MVC   6(9,R8),=C'******      '
NSR1     EQU   *
         LA    R11,1(R11)              Increment volser count
         LA    R8,15(R8)               Bump up in VS table
         B     SRLOOP
ENDRES   EQU   *
         CLOSE SYSRES                  All done, call IEHLIST
         CLOSE SYSIN
*
         LINK  EP=IEHLIST,PARAM=(OPT),VL=1
         EJECT
***********************************************************************
* Read through IEHLIST SYSPRINT and create IDCAMS and DDDEF cards     *
***********************************************************************
*
         OPEN  (SYSPRINT,(INPUT))      Open SYSPRINT
         OPEN  (IDCAMS,(OUTPUT))       Open IDCAMS control card file
         OPEN  (DDDEF,(OUTPUT))        Open DDDEF UCLIN file
*
         LA    R8,RESES                Start at top of Volser table
LOOP     EQU   *
         MVC   INAREA,=CL255' '
*
         GET   SYSPRINT,INAREA         Get SYSPRINT record
*                                      Dump title/listing records
         CLC   INAREA+31(L'SSU),SSU
         BE    LOOP
         CLC   INAREA+1(L'DATE),DATE
         BE    LOOP
         CLC   INAREA+1(L'DASHES),DASHES
         BE    LOOP
         CLC   INAREA+17(L'CONTENTS),CONTENTS
         BNE   NOTVOL
*                                      Found record with VOLSER on it
         MVC   DDDEF2V,INAREA+17+L'CONTENTS
         MVC   DEFVOL,6(R8)            Move symbol to IDCAMS card
         LA    R8,15(R8)               Bump up in table
         B     LOOP
NOTVOL   EQU   *
         CLI   INAREA+1,C' '
         BE    LOOP
         MVC   DELDSN,INAREA+1         Extract dsn for IDCAMS
         MVC   DEFDSN,INAREA+1         DEL DEF control cards
         PUT   IDCAMS,DELCARD          Write
         PUT   IDCAMS,DELCARD2           them
         PUT   IDCAMS,DEFCARD            out
         PUT   IDCAMS,DEFCARD2
         PUT   IDCAMS,DEFCARD3
         LA    R10,DEFDSN              See if dsn is one of those
*                                      datasets with many levels
DSNLOOP1 EQU   *
         CLI   0(R10),C'.'
         BE    GOTDOT1
         LA    R10,1(R10)
         B     DSNLOOP1
GOTDOT1  EQU   *
         EJECT
         LA    R15,DSCVTS              Load start of irregular dsns
CVTLOOP  EQU   *
         CLI   0(R15),X'FF'            Hit end of list?
         BE    NOCVT                   Yes - jump
         CLC   1(15,R10),0(R15)
         BE    DOCVT
         LA    R15,23(R15)
         B     CVTLOOP
DOCVT    EQU   *
         MVC   DDDEF1DD,=CL10' '
         MVC   DDDEF1DD,15(R15)
         B     GOTDOT2
NOCVT    EQU   *
         MVC   DDDEF1DD,=CL10' '
         LA    R10,1(R10)
         LA    R11,DDDEF1DD
DSNLOOP2 EQU   *
         CLI   0(R10),C'.'
         BE    GOTDOT2
         CLI   0(R10),C' '
         BE    GOTDOT2
         MVC   0(1,R11),0(R10)
         LA    R10,1(R10)
         LA    R11,1(R11)
         B     DSNLOOP2
GOTDOT2  EQU   *
         MVC   DDDEF1DS,DEFDSN         Move dsn to DDDEF card
         PUT   DDDEF,DDDEF1
         PUT   DDDEF,DDDEF2
         B     LOOP
         EJECT
EODAD    EQU   *
*
         CLOSE (SYSPRINT,,IDCAMS,,DDDEF)
         L     13,S+4
         LM    14,12,12(13)
         SR    15,15
         BR    14
         PRINT OFF
         LTORG *
         PRINT ON
S        DC    9D'0'
INAREA   DC    CL255' '
IEHCARD  DC    C' LISTVTOC VOL=3390='
IEHCARDV DC    CL6' '
         DC    CL(80-(*-IEHCARD))' '
*
DDDEF1   DS    0C
         DC    C'  REP DDDEF('
DDDEF1DD DC    CL8' '
         DC    C') DATASET('
DDDEF1DS DC    CL44' '
         DC    CL(80-(*-DDDEF1))')'
*
DDDEF2   DC    C'      UNIT(3390)      VOLUME('
DDDEF2V  DC    CL6' '
         DC    CL(80-(*-DDDEF2))') SHR .'
*
DELCARD  DS    0CL80
         DC    C'  DELETE '
DELDSN   DC    CL44' '
         DC    CL(80-(*-DELCARD))'  NVSAM NSCR -'
*
DEFCARD  DS    0CL80
         DC    C'  DEF    NVSAM(NAME('
DEFDSN   DC    CL44' '
         DC    CL(80-(*-DEFCARD))') -'
*
DEFCARD2 DC    C'         DEVT(0000) VOLUMES('
DEFVOL   DC    CL9' '
         DC    CL(80-(*-DEFCARD2))')) -'
*
DELCARD2 DS    0CL80
DEFCARD3 DC    C'         CATALOG('
DEFMCATN DC    CL44' '
         DC    CL(80-(*-DEFCARD3))')'
*
SSU      DC    C'SYSTEMS SUPPORT UTILITIES'
DATE     DC    C'DATE:'
CONTENTS DC    C'CONTENTS OF VTOC ON VOL '
DASHES   DC    C'-------------'
*
SYSPRINT DCB   DDNAME=SYSPRINT,DSORG=PS,EODAD=EODAD,MACRF=GM
*
IDCAMS   DCB   DDNAME=IDCAMS,DSORG=PS,MACRF=PM,LRECL=80,RECFM=FB,      X
               BLKSIZE=27920,BUFNO=2
*
DDDEF    DCB   DDNAME=DDDEF,DSORG=PS,MACRF=PM,LRECL=80,RECFM=FB,       X
               BLKSIZE=27920,BUFNO=2
*
SYSRES   DCB   DDNAME=SYSRES,EODAD=ENDRES,DSORG=PS,MACRF=GM
*
SYSIN    DCB   DDNAME=SYSIN,DSORG=PS,MACRF=PM,LRECL=80,                X
               RECFM=FB,BLKSIZE=800
*
DSCVTS   DC    CL15'SGOSH.H',CL8'SGOSH'
         DC    CL15'SGOSHH.HH',CL8'SGOSHH'
         DC    CL15'SGOSIDL.IDL',CL8'SGOSIDL'
         DC    CL15'SGOSSH.STARS.H',CL8'SGOSSH'
         DC    CL15'SGOSSMPC.C',CL8'SGOSSMPC'
         DC    CL15'SGOSSMPD.DAT',CL8'SGOSSMPD'
         DC    CL15'SGOSSMPH.H',CL8'SGOSSMPH'
         DC    CL15'SGOSSMPI.IDL',CL8'SGOSSMPI'
         DC    CL15'SGOSSMPI.IDL',CL8'SGOSSMPI'
         DC    CL15'SGOSSMPX.CXX',CL8'SGOSSMPX'
         DC    CL15'SGOSXH.XH',CL8'SGOSXH'
         DC    CL15'SCEEH.ARPA.H',CL8'SCEEHARP'
         DC    CL15'SCEEH.H     ',CL8'SCEEHH'
         DC    CL15'SCEEH.NETINET.H',CL8'SCEEHNEI'
         DC    CL15'SCEEH.NET.H',CL8'SCEEHNET'
         DC    CL15'SCEEH.SYS.H',CL8'SCEEHSYS'
         DC    CL15'SCLBH.H',CL8'SBLBHH'
         DC    CL15'SCLBH.INL',CL8'SCLBHI'
         DC    CL15'SCLBH.HPP',CL8'SCLBHPP'
         DC    CL15'SCLBSAM.C',CL8'SCLBSAC'
         DC    CL15'SCLBSAM.H',CL8'SCLBSAH'
         DC    CL15'SCLBTUT.H',CL8'SCLBTUH'
         DC    CL15'SCLBXL.HH',CL8'SCLBXHH'
         DC    CL15'SCLBXL.H',CL8'SCLBXLH'
         DC    CL15'SCLBXL.IDL',CL8'SCLBXLI'
         DC    CL15'SCLBXSM.IDL',CL8'SCLBXSI'
         DC    CL15'SCLDH.C',CL8'SCLDHC'
         DC    CL15'SCLDH.CPP',CL8'SCLDHCPP'
         DC    CL15'SCLDH.CXX',CL8'SCLDHCX'
         DC    CL15'SCLDH.H',CL8'SCLDHH'
         DC    CL15'SCLDH.INL',CL8'SCLDHI'
         DC    CL15'SCLDH.HPP',CL8'SCLDHPP'
         DC    CL15'SIEAHDR.H',CL8'SIEAHDR'
         DC    CL15'SCRIPT.MACLIB',CL8'DCFGML'
         DC    X'FF'
RESES    DC    8CL15' '
         EJECT
***********************************************************************
* Allocate //XX       DD DISP=SHR,DSN=???????                         *
***********************************************************************
*
ALLCVOLS DS    0D
         DC    X'80',AL3(VOLSRB)
*
VOLSRB   DC    X'14010000'
S99ERR2  DC    AL2(0)
S99INF2  DC    AL2(0)
         DC    A(VOLSTUS),A(0),A(0)
*
VOLSTUS  DC    A(VOLSTU1),A(VOLSTU2)
         DC    X'80',AL3(VOLSTU3)
*
VOLSTU1  DC    AL2(DALDDNAM),AL2(1)
VOLSTU1L DC    AL2(2)
VOLSTU1D DC    CL2'X '
*
VOLSTU2  DC    AL2(DALSTATS),AL2(1),AL2(1),AL1(8)  disp=shr
*
VOLSTU3  DC    AL2(DALVLSER),AL2(1)
VOLSTU3L DC    AL2(6)
VOLSTU3V DC    CL6' '
         PRINT OFF
         IEFZB4D0
         IEFZB4D2
         PRINT ON
FIXCATLG CSECT
         END   FIXCATLG
//RUN    EXEC PGM=FIXCATLG,COND=(0,LT),
//  PARM='TEST.CATALOG'           <<<== new MASTERCAT
//STEPLIB  DD DISP=SHR,DSN=&&LL
//SYSRES   DD *,DCB=LRECL=80
RDZR01=&SYSR1                     <<<=== volumes to scan
RDZR02=&SYSR2                            VOLSER=SYMBOL
RDZD01=&DLBV1
//IDCAMS   DD DSN=xxx,   <===== dsname for IDCAMS cards
//            DISP=(,CATLG),SPACE=(TRK,(15,15),RLSE),UNIT=3390
//DDDEF    DD DSN=yyy,   <===== dsname for UCLIN cards
//            DISP=(,CATLG),SPACE=(TRK,(15,15),RLSE),UNIT=3390
//SYSIN    DD DISP=NEW,DSN=&&SYSIN,UNIT=3390,SPACE=(TRK,1)
//SYSPRINT DD DISP=(SHR,PASS),DSN=&&SYSPR
//SYSUDUMP DD SYSOUT=*
