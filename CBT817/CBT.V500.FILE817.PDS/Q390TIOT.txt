         TITLE 'Q390TIOT - CHECK IF DD NAME IS IN THE JCL'
* ------------------------------------------------------------------ *
*   PROGRAM:    Q390TIOT                                             *
*                                                                    *
*   AUTHOR :    MICHAEL WOJTUKIEWICZ                                 *
*                                                                    *
*   DATE WRITTEN:  JULY 20, 2002                                     *
*                                                                    *
*   JCL    :    // JOB CARD                                          *
*               //*                                                  *
*               //CHECKDD EXEC PGM=Q390TIOT,PARM='MYDDNM  '          *
*               //STEPLIB  DD  DISP=SHR,DSN=MY.LOAD.LIBRARY          *
*               //  (SOME DD NAMES)                                  *
*               //MYDDNM   DD  DSN=XXXXX  (THERE OR NOT?)            *
*               //  (MORE DD NAMES)                                  *
*                                                                    *
*        --->   (8 CHARACTERS ARE REQUIRED IN THE PARM)              *
*                                                                    *
*   RETURN :    RETURN CODE = 0 IF DDNAME IS PRESENT IN JOB STEP     *
*               RETURN CODE = 4 IF DDNAME IS NOT IN JOB STEP         *
*                                                                    *
*                                                                    *
*   CHANGES:    CLC FOR DD NAME DONE ON TWO 8-CHARACTER FIELDS.      *
*                                                                    *
*                                                                    *
* ------------------------------------------------------------------ *
Q390TIOT CSECT
Q390TIOT AMODE 31
Q390TIOT RMODE 24
         YREGS
BASE     EQU   3
         PRINT GEN
         STM   R14,R12,12(R13)   Save caller's GPRs
*
         BALR BASE,R0            Prime base
         USING *,BASE              register 3
*
         LR    R12,R13           Save caller's savearea address
         LA    R13,S             Put my savearea address
         ST    R13,8(,R12)       two fullwords into his
         ST    R12,S+4           and save his in my savearea
         B     BYP
         DC    C'Q390TIOT'       Module name
         DC    C'&SYSDATE'       Compile date
         DC    C'&SYSTIME'       Compile time
MD       DC    C'12/03/2020'     Modification date
MT       DC    C'10:03'          Modification time
S        DC    9D'0'             My savearea
BYP      DS    0H                Begin program execution.
         MVI   PARM8-1,X'40'
         MVC   PARM8,PARM8-1     Blank 8-character staging area
         L     R5,0(,R1)         Load parm address
         SR    R2,R2             Clear work register
         LH    R2,0(,R5)         Get parm length
         C     R2,=F'8'          More than 8?
         BNH   NOCHANGE          No. Just save it.
         LA    R2,8              Cut it to 8
NOCHANGE DS    0H
         ST    R2,SAVELNGH       Save length, up to 8
*
         LA    R10,X'10'         GET
         L     R10,0(,R10)       CVT ADDRESS
         L     R10,0(,R10)       GET ADDRESS OF TCBS
         LA    R10,4(,R10)       BUMP TO CURRENT TCB ADDRESS
         L     R10,0(,R10)       GET TCBS ADDRESS
         LA    R10,X'C'(,R10)    GET ADDRESS OF TIOT
         L     R10,0(,R10)       TIOT ADDRESS
*
         USING TIOTDSCT,R10      ADDRESS TIOT DSECT
*
         LA    R15,4             DEFAULT RETURN CODE
TIOTLOOP DS    0H
         XR    R8,R8             ZERO REGISTER 8
         IC    R8,TIOELNGH       GET LENGTH OF DDCARD
         LTR   R8,R8             IF ZERO, END OF DDCARDS
         BZ    NODISK            DID NOT FIND DDCARD
         L     R2,SAVELNGH       RELOAD LENGTH OF PARM
         BCTR  R2,0              ONE LESS FOR EXECUTE
         EX    R2,MOVEPARM       ONLY MOVE PROPER NUMBER OF CHARS
         CLC   TIOEDDNM,PARM8    DOES DDCARD COMPARE (8 CHARS)
         BE    GOTDISKX          YES - GOT IT
         AR    R10,R8            ELSE BUMP TO NEXT ENTRY
         B     TIOTLOOP          TRY AGAIN
GOTDISKX DS    0H
*
         LA    R15,0             RETURN CODE IF DD MATCHED
*
NODISK   DS    0H
         L     R13,S+4           Load caller's save area address
         L     R14,12(,R13)
         LM    R0,R12,20(R13)
         BR    R14               Go back
         EJECT
         LTORG *
MOVEPARM MVC   PARM8(*-*),2(R5)        EXECUTED
         DC    CL1' '
PARM8    DC    CL8' '
SAVELNGH DC    F'0'              Length of PARM
*
TIOTDSCT DSECT
         IEFTIOT1
Q390TIOT CSECT
         END   Q390TIOT
