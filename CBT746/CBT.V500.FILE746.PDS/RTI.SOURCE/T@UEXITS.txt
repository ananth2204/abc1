         COPY  #REGS
         SPACE 2
BASEREG  EQU   R11                BASE REGISTER
LINKREG  EQU   R14                SUBROUTINE LINK REG
         SPACE 2
#EXITMAP @INTRPRT MF=EXITPARM
         SPACE 2
GLOBAL   DSECT
#NAMEV   DS   A                   ADDR OF NAMEV
#SCOREV1 DS   A                   ADDR OF SCOREV1
#SCOREV2 DS   A                   ADDR OF SCOREV2
         EJECT
***********************************************************************
*                                                                     *
*   PERFORM INITIAL HOUSEKEEPING                                      *
*                                                                     *
***********************************************************************
T@UEXITS #ENTRY BASEREG=BASEREG,SAVEAREA=SAVEAREA,PARM=@PARM
         SPACE 1
         ENTRY EXT1
         ENTRY EXT2
         ENTRY EXT3
         EJECT
***********************************************************************
*                                                                     *
*   START OF MAIN PROGRAM                                             *
*                                                                     *
***********************************************************************
BEGIN    DS    0H
         L     R2,@PARM           GET ADDR OF PARM LIST
         USING GLOBAL,R2          SET ADDR
         SPACE 1
         MVC   @NAMEV,#NAMEV      STORE ADDR OF NAMEV
         MVC   @SCOREV1,#SCOREV1  STORE ADDR OF SCOREV1
         MVC   @SCOREV2,#SCOREV2  STORE ADDR OF SCOREV2
         SPACE 1
EXIT     DS    0H
         L     R13,SAVEAREA+4     PT TO CALLER'S REG SAVEAREA
         SPACE 1
         RETURN (14,12),RC=0      THAT'S ALL FOLKS!
         SPACE 1
         DROP  R2
         EJECT
***********************************************************************
*                                                                     *
*   SUBROUTINE - ROUTINE TO EXTRACT AND DISPLAY VALUE FOR "NAME="     *
*                                                                     *
***********************************************************************
EXT1     DS    0H
         PUSH  USING
         SAVE  (14,12)            SAVE CALLER'S REGS
         SPACE 1
         LR    BASEREG,R15        PRIME BASE REG
         USING EXT1,BASEREG       SET ADDR
         SPACE 1
         ST    R13,SUBSAVE+4      SAVE PTR TO CALLER'S REG. SAVE AREA
         LA    R13,SUBSAVE        PRIME SAVE AREA PTR
         LR    R7,R1              PARM ADDR
         USING #EXITMAP,R7        SET ADDR
         SPACE 1
         L     R8,@NAMEV          PT TO NAMEV
         MVC   5(8,R8),=C'        '   RESET OUTPUT FIELD
         SPACE 1
         L     R2,@IXTOKAD        ADDR OF TOKEN
         L     R4,@IXTOKLN        TOKEN LENGTH
         BCTR  R4,R0              MACHINE LENGTH
         EX    R4,COPYNAMV        COPY COMMAND KEYWORD TO BUFFER
         SPACE 1
EXT1X    DS    0H
         L     R13,SUBSAVE+4      PT TO CALLER'S REG SAVEAREA
         SPACE 1
         RETURN (14,12),RC=0      GO BACK TO @INTRPRT
         SPACE 1
COPYNAMV MVC   5(*-*,R8),0(R2)  ** SUBJECT OF EX INSTR **
         SPACE 1
         DROP  R7
         POP   USING
         EJECT
***********************************************************************
*                                                                     *
*   SUBROUTINE - EXTRACT AND DISPLAY 1ST VALUE FOR "SCORES="          *
*                                                                     *
***********************************************************************
EXT2     DS    0H
         PUSH  USING
         SAVE  (14,12)            SAVE CALLER'S REGS
         SPACE 1
         LR    BASEREG,R15        PRIME BASE REG
         USING EXT2,BASEREG       SET ADDR
         SPACE 1
         XR    R15,R15            CLEAR RC
         ST    R13,SUBSAVE+4      SAVE PTR TO CALLER'S REG. SAVE AREA
         LA    R13,SUBSAVE        PRIME SAVE AREA PTR
         LR    R7,R1              PARM ADDR
         USING #EXITMAP,R7        SET ADDR
         SPACE 1
         L     R8,@SCOREV1        PT TO SCOREV1
         MVC   8(8,R8),=C'        '   RESET OUTPUT FIELD
         SPACE 1
         L     R2,@IXTOKAD        ADDR OF TOKEN
         L     R4,@IXTOKLN        TOKEN LENGTH
         BCTR  R4,R0              MACHINE LENGTH
         EX    R4,COPYSCR1        COPY COMMAND KEYWORD TO BUFFER
         EX    R4,PACKSCR1        CONVERT TO DECIMAL
         CP    EXT2WD,=P'0'       VALID NUMERIC?
         BNE   EXT2X              YES, CONTINUE
         SPACE 1
         L     R1,@IXERRMG        POINT TO MESSAGE BUFFER
         MVC   0(16,R1),=C'>>> 0 IS INVALID'
         LA    R15,4              INDICATE AN ERROR
         SPACE 1
EXT2X    DS    0H
         L     R13,SUBSAVE+4      PT TO CALLER'S REG SAVEAREA
         SPACE 1
         RETURN (14,12),RC=(15)   GO BACK TO @INTRPRT
         SPACE 1
EXT2WD   DS    D                  DOUBLE WORD
PACKSCR1 PACK  EXT2WD,8(*-*,R8) ** SUBJECT OF EX INSTR **
COPYSCR1 MVC   8(*-*,R8),0(R2)  ** SUBJECT OF EX INSTR **
         SPACE 1
         DROP  R7
         POP   USING
         EJECT
***********************************************************************
*                                                                     *
*   SUBROUTINE - EXTRACT AND DISPLAY 2ND VALUE FOR "SCORES="          *
*                                                                     *
***********************************************************************
EXT3     DS    0H
         PUSH  USING
         SAVE  (14,12)            SAVE CALLER'S REGS
         SPACE 1
         LR    BASEREG,R15        PRIME BASE REG
         USING EXT3,BASEREG       SET ADDR
         SPACE 1
         ST    R13,SUBSAVE+4      SAVE PTR TO CALLER'S REG. SAVE AREA
         LA    R13,SUBSAVE        PRIME SAVE AREA PTR
         LR    R7,R1              PARM ADDR
         USING #EXITMAP,R7        SET ADDR
         SPACE 1
         L     R8,@SCOREV2        PT TO SCOREV2
         MVC   8(8,R8),=C'        '   RESET OUTPUT FIELD
         SPACE 1
         L     R2,@IXTOKAD        ADDR OF TOKEN
         L     R4,@IXTOKLN        TOKEN LENGTH
         BCTR  R4,R0              MACHINE LENGTH
         EX    R4,COPYSCR2        COPY COMMAND KEYWORD TO BUFFER
         SPACE 1
EXT3X    DS    0H
         L     R13,SUBSAVE+4      PT TO CALLER'S REG SAVEAREA
         SPACE 1
         RETURN (14,12),RC=0      GO BACK TO @INTRPRT
         SPACE 1
COPYSCR2 MVC   8(*-*,R8),0(R2)  ** SUBJECT OF EX INSTR **
         SPACE 1
         DROP  R7
         POP   USING
         EJECT
***********************************************************************
*                                                                     *
*   CONSTANTS, WORK AREAS, ETC.                                       *
*                                                                     *
***********************************************************************
SAVEAREA DS    18F                REGISTER SAVE AREA
SUBSAVE  DS    18F                REGISTER SAVE AREA FOR SUBROUTINES
@PARM    DS    A                  OS PARM ADDR
DUBLWORK DS    D                  DOUBLE WORD
RETCODE  DS    F                  RETURN CODE
*
STATFLAG DS    X                  STATUS FLAG
ERROR    EQU   X'80'              ERROR
*
NUMWORK  DS    CL2                NUMERIC WORK AREA
*
@NAMEV   DC    A(0)               ADDR OF NAMEV
@SCOREV1 DC    A(0)               ADDR OF SCOREV1
@SCOREV2 DC    A(0)               ADDR OF SCOREV2
*
MSGOBUFF DS    CL120              OUTPUT BUFFER
         SPACE 1
         LTORG *
         SPACE 1
         END
