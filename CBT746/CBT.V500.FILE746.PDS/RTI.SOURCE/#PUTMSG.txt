         TITLE '#PUTMSG - PRINT A MESSAGE'
***********************************************************************
*                                                                     *
* MODULE NAME : #PUTMSG                                               *
*                                                                     *
*                                                                     *
* DESCRIPTIVE NAME : PRINT A MESSAGE                                  *
*                                                                     *
*                                                                     *
* FUNCTION :  SCAN MESSAGE TABLE, E.G. #GENMSG, USING THE USER        *
*             PROVIDED MESSAGE ID, OUTPUT BUFFER AND FILE, AND        *
*             WRITE TO THE USER-SPECIFIED OUTPUT FILE.                *
*                                                                     *
* ENTRY: FROM OS                                                      *
*                                                                     *
* EXIT:  RETURN TO OS                                                 *
*                                                                     *
*                                                                     *
* INPUT:                                                              *
*                                                                     *
*    PARMS:     WORD-1 = MESSAGE ID                                   *
*               WORD-2 = ADDRESS OF MESSAGE TABLE                     *
*               WORD-3 = ADDRESS OF OUTPUT DCB                        *
*               WORD-4 = ADDRESS OF OUTPUT BUFFER                     *
*               WORD-5 = LENGTH OF OUTPUT BUFFER                      *
*                                                                     *
*                                                                     *
* OUTPUT: NONE                                                        *
*                                                                     *
*                                                                     *
* ABENDS:                                                             *
*                                                                     *
*   SEE MESSAGES (IF ANY)                                             *
*                                                                     *
*--PROPRIETARY_STATEMENT----------------------------------------------*
*                                                                     *
*  THIS PROGRAM IS THE SOLE PROPERTY OF RICHARD TSUJIMOTO, INC.       *
*                                                                     *
***********************************************************************
         EJECT
         COPY  #REGS
         SPACE 2
BASEREG  EQU   R11                BASE REGISTER
LINKREG  EQU   R14                SUBROUTINE LINK REG
         EJECT
$PRMLST1 #PUTMSG MF=DSECT
         EJECT
***********************************************************************
*                                                                     *
*   PERFORM INITIAL HOUSEKEEPING                                      *
*                                                                     *
***********************************************************************
#PUTMSG  #ENTRY BASEREG=BASEREG,SAVEAREA=SAVEAREA,PARM=@PARM
         #OWNER VER=V1R0M0
         EJECT
***********************************************************************
*                                                                     *
*   START OF MAIN PROGRAM                                             *
*                                                                     *
***********************************************************************
BEGIN    DS    0H
         L     R8,@PARM           POINT TO PARM LIST
         USING $PRMLST1,R8        SET ADDR
         SPACE 1
         L     R1,#PMSGNO         MESSAGE ID
         L     R4,#PMSGTAA        POINT TO MESSAGE TABLE
         L     R5,#POTDCBA        POINT TO OUTPUT DCB
         L     R6,#POTBUFA        POINT TO OUTPUT BUFFER
         L     R7,#POTBUFL        LENGTH OF OUTPUT BUFFER
         SPACE 1
         BCTR  R7,0               MACHINE LENGTH
         BCTR  R7,0               MACHINE LENGTH
         MVI   0(R6),X'40'        SET OUTPUT AREA
         EX    R7,SETOBLNK        TO BLANKS
         SPACE 1
         SLL   R1,2               MULTIPLY MSG. NO BY 4
         S     R4,=F'4'           PT TO MESSAGE TABLE - 4
         LA    R4,0(R1,R4)        PT TO PROPER ENTRY
         XR    R1,R1              CLEAR WORK REG
         IC    R1,0(,R4)          LENGTH OF MESSAGE TEXT
         BCTR  R1,0               MACHINE LENGTH
         ICM   R2,B'0111',1(R4)   PT TO MESSAGE
         EX    R1,MOVEMESG        MOVE MSG TO OUTPUT BUFFER
         SPACE 1
         PUT   (R5),(R6)
         SPACE 1
RETURN   DS    0H
         L     R13,SAVEAREA+4     POINT TO CALLER'S SAVE AREA
         RETURN (14,12),RC=0      FINIS
         SPACE 1
         DROP  R8
         EJECT
***********************************************************************
*                                                                     *
*   CONSTANTS, WORK AREAS, ETC.                                       *
*                                                                     *
***********************************************************************
MOVEMESG MVC   0(*-*,R6),0(R2)    SUBJECT OF EX INSTR
SETOBLNK MVC   1(*-*,R6),0(R6)    SUBJECT OF EX INSTR
         SPACE 1
SAVEAREA DS    18F                REGISTER SAVE AREA
@PARM    DS    A                  ADDR OF PARM LIST
         SPACE 1
         LTORG *
         SPACE 1
         END
