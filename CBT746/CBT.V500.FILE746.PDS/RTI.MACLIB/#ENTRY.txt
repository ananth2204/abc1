         MACRO
&LABEL   #ENTRY &BASEREG=,        LIST OF BASE REGS                    +
               &SAVEAREA=,        LOCAL REG SAVE AREA                  +
               &PARM=,            PARM POINTER SAVE AREA               +
               &MODE=24,          ADDRESSING MODE                      +
               &DOC=NO            DOCUMENTATION ONLY
.*
         LCLC  &S,&NULL
         LCLC  &BR1,&BR2,&BR3,&BR4,&BR5
         LCLA  &I,&J,&K
.*
         AIF   ('&DOC' NE 'YES').TSTPRMS
*--PROPRIETARY_STATEMENT----------------------------------------------*
*                                                                     *
*  THIS MACRO IS THE SOLE PROPERTY OF RICHARD TSUJIMOTO, INC.         *
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
*                      # E N T R Y                                    *
*                                                                     *
*        FUNCTION :                                                   *
*                                                                     *
*        PERFORM INITIAL HOUSEKEEPING UPON PROGRAM ENTRY, SUCH AS     *
*        INITIALIZING BASE REGISTER(S), AND DECLARING CONSTANTS       *
*        THAT IDENTIFY THE CSECT NAME, CURRENT DATE AND TIME.         *
*                                                                     *
*        PARAMETERS:                                                  *
*                                                                     *
*              #ENTRY BASEREG = (REG),                                *
*                    SAVEAREA = VAR,                                  *
*                    MODE=    = VAL,                                  *
*                    PARM=    = VAR,                                  *
*                    DOC = YES | NO                                   *
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
*        FUNCTION:                                                    *
*                                                                     *
*        1.  GENERATE THE CODE TO INITIALIZE A PROGRAM.               *
*                                                                     *
*        2.  GENERATE THE DOC FOR THIS MACRO.                         *
*                                                                     *
*---------------------------------------------------------------------*
         EJECT
*---------------------------------------------------------------------*
*        PARAMETER         DESCRIPTION                                *
*---------------------------------------------------------------------*
*                                                                     *
*        BASEREG           (REG) - (2-12)                             *
*                                  SPECIFIES THE REGISTERS THAT WILL  *
*                                  USED AS PROGRAM BASE REGS.         *
*                                                                     *
*        SAVEAREA          VAR     RX-TYPE ADDRESS                    *
*                                  SPECIFIES THE BEGINNING OF THE     *
*                                  LOCAL REGISTER SAVE AREA.          *
*                                                                     *
*        MODE              VAL     '24' - 24-BIT PROGRAM              *
*                                  '31' - 31-BIT PROGRAM              *
*                                  DEFAULT: MODE=24                   *
*                                                                     *
*        PARM              VAR     RX-TYPE ADDRESS                    *
*                                  SPECIFIES THE LOCATION TO SAVE     *
*                                  THE OS PARM POINTER, OR R1.        *
*                                                                     *
*        DOC               'YES'   PRINT MACRO DOC ONLY               *
*                          'NO'    OMIT PRINTING MACRO DOC (DEFAULT)  *
*                                                                     *
*---------------------------------------------------------------------*
*        RETURN CODE VALUES                                           *
*---------------------------------------------------------------------*
*                                                                     *
*        N/A                                                          *
*                                                                     *
*---------------------------------------------------------------------*
*        PARAMETER LIST DESCRIPTION                                   *
*---------------------------------------------------------------------*
*                                                                     *
*        N/A                                                          *
*                                                                     *
*---------------------------------------------------------------------*
         MEXIT
.TSTPRMS ANOP
         AIF   ('&DOC' EQ 'NO').TSTPRM2
         MNOTE 8,'DOC PARM IS INVALID; MUST BE EITHER "YES" OR "NO"'
         MEXIT
.*
.TSTPRM2 ANOP
         AIF   ('&SAVEAREA' NE '').TSTPRMA
         MNOTE 8,'*** PARM SAVEAREA IS MISSING ***'
         MEXIT
.*
.TSTPRMA ANOP
         AIF   ('&PARM' NE '').TSTLBL
         MNOTE 8,'*** PARM=PARM IS MISSING ***'
         MEXIT
.*
.TSTLBL  ANOP
         AIF   ('&LABEL' NE '').TSTNBR
         MNOTE 8,'*** CSECT NAME IS MISSING, CODE AS A LABEL ***'
         MEXIT
.*
.TSTNBR  ANOP
         AIF   (N'&BASEREG LT 7).TSTMODE
         MNOTE 8,'*** TOO MANY BASE REGISTERS ***'
         MEXIT
.*
.TSTMODE ANOP
         AIF   ((&MODE EQ 24) OR (&MODE EQ 31)).GENCODE
         MNOTE 8,'*** MODE IS INVALID, IT MUST BE EITHER 24 OR 31 ***'
         MEXIT
.*
.*    INITIALIZE BASE REGISTERS
.*
.GENCODE ANOP
&S       SETC  '&SYSNDX'
&LABEL   CSECT
         AIF   (N'&BASEREG LT 7).GENSVE1
         MNOTE 8,'*** TOO MANY BASE REGISTERS ***'
         MEXIT
.GENSVE1 ANOP
&BR1     SETC  '&BASEREG(1)'
         AIF   (&MODE EQ 24).GEN24SV
&LABEL   AMODE 31
&LABEL   RMODE ANY
         BAKR  14,0               SAVE CALLER'S STATUS ON LINK. STACK
         SAC   0                  MAKE SURE WE'RE IN PRIMARY MODE
         SPACE 1
         SYSSTATE ASCENV=P        LET ASSEMBLER KNOW ALSO
         SPACE 1
         LR    &BASEREG(1),15     INITIALIZE BASE REG
         AGO   .GENUSNG
.GEN24SV ANOP
         SAVE  (14,12)            SAVE CALLER'S REGS
         SPACE 1
         LR    &BR1,15            INITIALIZE 1ST/ONLY BASE REG
.GENUSNG AIF   (N'&BASEREG NE 1).GENUSG2
         USING &LABEL,&BR1        SET ADDR
         AGO   .GENURG1
.GENUSG2 AIF   (N'&BASEREG NE 2).GENUSG3
&BR2     SETC  '&BASEREG(2)'
         USING &LABEL,&BR1,&BR2   SET ADDR
         AGO   .GENURG1
.GENUSG3 AIF   (N'&BASEREG NE 3).GENUSG4
&BR2     SETC  '&BASEREG(2)'
&BR3     SETC  '&BASEREG(3)'
         USING &LABEL,&BR1,&BR2,&BR3   SET ADDR
         AGO   .GENURG1
.GENUSG4 AIF   (N'&BASEREG NE 4).GENUSG5
&BR2     SETC  '&BASEREG(2)'
&BR3     SETC  '&BASEREG(3)'
&BR4     SETC  '&BASEREG(4)'
         USING &LABEL,&BR1,&BR2,&BR3,&BR4   SET ADDR
         AGO   .GENURG1
.GENUSG5 AIF   (N'&BASEREG NE 5).GENUSG6
&BR2     SETC  '&BASEREG(2)'
&BR3     SETC  '&BASEREG(3)'
&BR4     SETC  '&BASEREG(4)'
&BR5     SETC  '&BASEREG(5)'
         USING &LABEL,&BR1,&BR2,&BR3,&BR4,&BR5   SET ADDR
         AGO   .GENURG1
.GENUSG6 ANOP
&BR2     SETC  '&BASEREG(2)'
&BR3     SETC  '&BASEREG(3)'
&BR4     SETC  '&BASEREG(4)'
&BR5     SETC  '&BASEREG(5)'
&BR6     SETC  '&BASEREG(6)'
         USING &LABEL,&BR1,&BR2,&BR3,&BR4,&BR5,&BR6   SET ADDR
.GENURG1 ANOP
         SPACE 1
&I       SETA  N'&BASEREG
&J       SETA  0
&K       SETA  2
.LOADBR  ANOP
         AIF   (&K GT &I).GENADDR
         L     &BASEREG(&K),BREG&S+(&J*4)  INIT BASE REG
&J       SETA  &J+1
&K       SETA  &K+1
         AGO   .LOADBR
.GENADDR ANOP
         B     BEGN&S             CONTINUE
         SPACE 1
&I       SETA  N'&BASEREG-1
         AIF   (&I EQ 0).GENADND
&K       SETA  1
BREG&S   DS    0A
.GENDC   ANOP
         AIF   (&K GT &I).GENADND
         DC    A(&LABEL+(&K*4096))
&K       SETA  &K+1
         AGO   .GENDC
.GENADND ANOP
         SPACE 1
         DC    C'*',C'&LABEL'     MODULE NAME
         DC    C'*',C'&SYSDATE'   ASSEMBLY DATE
         DC    C'*',C'&SYSTIME'   ASSEMBLY TIME
         SPACE 1
         LTORG *
         SPACE 1
BEGN&S   DS    0H
         ST    1,&PARM            SAVE OS PARM POINTER
         ST    13,&SAVEAREA+4     SAVE PTR TO CALLER'S SAVE AREA
         LA    13,&SAVEAREA       INIT CURRENT SAVE AREA PTR
         AIF   (&MODE EQ 24).EXIT
         MVC   4(4,13),=C'F1SA'   INDICATE CALLER'S STATUS ON LK. STACK
.EXIT    ANOP
         MEND
