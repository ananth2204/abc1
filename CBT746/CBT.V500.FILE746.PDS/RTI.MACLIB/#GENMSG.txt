         MACRO
&LABEL   #GENMSG &TYPE=,          MACRO TYPE                           +
               &MSGID=,           MESSAGE ID                           +
               &MSGTXT=,          MESSAGE TEXT                         +
               &DOC=NO            DOCUMENTATION ONLY
         GBLC  &GMSGID(1000),&GMSGTXT(1000),&GLABELS(1000),&MSGLBL
         GBLB  &NOGEN,&INITF,&ENTRYF,&LASTF
         GBLA  &MSGNO
         LCLA  &J,&K
         LCLC  &A,&B,&LBLS
.*
         AIF   ('&DOC' NE 'YES').INIT
*--PROPRIETARY_STATEMENT----------------------------------------------*
*                                                                     *
*  THIS MACRO IS THE SOLE PROPERTY OF RICHARD TSUJIMOTO, INC.         *
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
*                        # G E N M S G                                *
*                                                                     *
*        FUNCTION :                                                   *
*                                                                     *
*        GENERATE A TABLE OF MESSAGE ID'S AND TEXT.                   *
*                                                                     *
*        PARAMETERS:                                                  *
*                                                                     *
*              #GENMSG DOC = YES | NO                                 *
*                                                                     *
*              #GENMSG TYPE = INITIAL                                 *
*                                                                     *
*              #GENMSG TYPE = ENTRY,                                  *
*                     MSGID= MSGID,                                   *
*                     MSGTXT = 'MSGTXT'                               *
*                                                                     *
*              #GENMSG TYPE = LAST                                    *
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
*        FUNCTION:                                                    *
*                                                                     *
*        1.  GENERATE A MESSAGE TABLE                                 *
*                                                                     *
*        2.  GENERATE THE DOC FOR THIS MACRO.                         *
*                                                                     *
*---------------------------------------------------------------------*
         EJECT                                                        *
*---------------------------------------------------------------------*
*        PARAMETER         DESCRIPTION                                *
*---------------------------------------------------------------------*
*                                                                     *
*        LABEL             LABEL     - CAN BE SPECIFIED (OPTIONAL);   *
*                                      IT COULD BE USEFUL WHEN THE    *
*                                      MESSAGE TEXT NEEDS TO BE       *
*                                      MODIFIED DYNAMICALLY.          *
*                                                                     *
*        TYPE              'INITIAL' - MUST BE SPECIFIED ON THE FIRST *
*                                      MACRO SPECIFICATION.           *
*                          'ENTRY'   - MUST BE SPECIFIED ON EACH MACRO*
*                                      WHICH DESCRIBES THE MESSAGE-ID *
*                                      AND MESSAGE TEXT.              *
*                          'LAST'    - MUST BE SPECIFIED ON THE LAST  *
*                                      MACRO SPECIFICATION.           *
*                                                                     *
*        MSGID             LABEL     - 1-TO-8 CHARACTER NAME THAT IS  *
*                                      USED TO IDENTIFY THE MESSAGE   *
*                                      TEXT ASSOCIATED WITH THE ID.   *
*                                      THE NAME CONFORMS TO NORMAL    *
*                                      ASSEMBLER VARIABLE NAMING      *
*                                      STANDARDS.                     *
*                                                                     *
*        'MSGTXT'          TEXT      - THE TEXT OF THE MESSAGE, EN-   *
*                                      CLOSED IN APOSTROPHES.         *
*                                                                     *
*        DOC               'YES'     - PRINT MACRO DOC ONLY           *
*                          'NO'      - OMIT PRINTING MACRO DOC        *
*                                      (DEFAULT)                      *
*                                                                     *
*---------------------------------------------------------------------*
         MEXIT
.*
.*  CHECK VALIDITY OF TYPE=INITIAL PARAMETERS
.*
.INIT    AIF   ('&TYPE' NE 'INITIAL').TSTTYP2
         AIF   (NOT &INITF).INIT1
         MNOTE 8,'MULTIPLE TYPE=INITIAL PARAMETERS IS NOT VALID'
&NOGEN   SETB  1
.INIT1   AIF   (NOT &LASTF).INIT2
         MNOTE 8,'TYPE=INITIAL MUST PRECEDE TYPE=LAST'
&NOGEN   SETB  1
.INIT2   AIF   ('&MSGID' EQ '').INIT3
         MNOTE 8,'MSGID PARAMETER IS INVALID FOR TYPE=INITIAL'
&NOGEN   SETB  1
.INIT3   AIF   ('&MSGTXT' EQ '').INIT4
         MNOTE 8,'MSGTXT PARAMETER IS INVALID FOR TYPE=INITIAL'
&NOGEN   SETB  1
.INIT4   AIF   (&NOGEN).INITEND
&LBLS    SETC  '&LABEL'
         AIF   ('&LBLS' NE '').SVELABL
&MSGLBL  SETC  '#MSGPTR'
         AGO   .INIT5
.SVELABL ANOP
&MSGLBL  SETC  '&LBLS'
.INIT5   ANOP
&INITF   SETB  1
&MSGNO   SETA  0
.INITEND ANOP
         MEXIT
.*
.*  CHECK VALIDITY OF TYPE=ENTRY PARAMETERS
.*
.TSTTYP2 AIF   ('&TYPE' NE 'ENTRY').TSTTYP3
         AIF   (&INITF).ENT1
         MNOTE 8,'TYPE=INITIAL MUST BE SPECIFIED FIRST'
&NOGEN   SETB  1
.ENT1    AIF   (NOT &LASTF).ENT2
         MNOTE 8,'TYPE=ENTRY MUST PRECEDE TYPE=LAST'
&NOGEN   SETB  1
.ENT2    AIF   ('&MSGID' NE '').ENT3
         MNOTE 8,'MSGID PARAMETER MUST BE SPECIFIED'
&NOGEN   SETB  1
.ENT3    AIF   ('&MSGTXT' NE '').ENT4
         MNOTE 8,'MSGTXT PARAMETER MUST BE SPECIFIED'
&NOGEN   SETB  1
.ENT4    AIF   (K'&MSGID LT 9).ENT5
         MNOTE 8,'MSGID PARAMETER IS TOO LONG'
&NOGEN   SETB  1
.ENT5    AIF   (&NOGEN).ENTEXIT
&ENTRYF  SETB  1
&MSGNO   SETA  &MSGNO+1
         AIF   ('&LABEL' NE '').SAVELBL
&LBLS    SETC  '#MSG&MSGNO'
         AGO   .SAVETXT
.SAVELBL ANOP
&LBLS    SETC  '&LABEL'
.SAVETXT ANOP
&GLABELS(&MSGNO) SETC '&LBLS'
&GMSGID(&MSGNO) SETC '&MSGID'
&GMSGTXT(&MSGNO) SETC '&MSGTXT'
.ENTEXIT ANOP
         MEXIT
.*
.*  CHECK VALIDITY OF TYPE=LAST  PARAMETERS
.*
.TSTTYP3 AIF   ('&TYPE' EQ 'LAST').LAST1
         MNOTE 8,'TYPE PARAMETER IS INVALID'
&NOGEN   SETB  1
.LAST1   AIF   (&INITF).LAST2
         MNOTE 8,'TYPE=INITIAL MUST PRECEDE TYPE=LAST'
&NOGEN   SETB  1
.LAST2   AIF   (&ENTRYF).LAST3
         MNOTE 8,'TYPE=ENTRY MUST PRECEDE TYPE=LAST'
&NOGEN   SETB  1
.LAST3   AIF   ('&MSGID' EQ '').LAST4
         MNOTE 8,'MSGID PARAMETER IS NOT VALID FOR TYPE=LAST'
&NOGEN   SETB  1
.LAST4   AIF   ('&MSGTXT' EQ '').LAST5
         MNOTE 8,'MSGTXT PARAMETER IS NOT VALID FOR TYPE=LAST'
&NOGEN   SETB  1
.LAST5   AIF   (&NOGEN).EXIT
.*
&J       SETA  1
&M       SETA  0
&N       SETA  0
         SPACE 1
.LLUP1   AIF   (&J GT &MSGNO).GENTXTP
.*
&GMSGID(&J) EQU &J
.*
&J       SETA  &J+1
         AGO   .LLUP1
.*
.GENTXTP ANOP
&J       SETA  1
&MSGLBL  DS    0A
.*
.LLUP2   AIF   (&J GT &MSGNO).GENTEXT
&K       SETA  K'&GMSGTXT(&J)-2
&A       SETC  '&GLABELS(&J)'
         DC    AL1(&K),AL3(&A)
&J       SETA  &J+1
         AGO   .LLUP2
.*
.GENTEXT ANOP
&J       SETA  1
.*
.LLUP3   AIF   (&J GT &MSGNO).EXIT
&A       SETC  '&GLABELS(&J)'
&B       SETC  '&GMSGTXT(&J)'
&A       DC    C&B
&J       SETA  &J+1
         AGO   .LLUP3
.EXIT    ANOP
         MEND
