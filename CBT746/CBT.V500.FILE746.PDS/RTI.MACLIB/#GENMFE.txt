         MACRO
&LABEL   #GENMFE &RTN=,           SUBROUTINE NAME                      +
               &PRM=,             REG POINTING TO PARM LIST            +
               &P1=,              PARM-1                               +
               &P2=,              PARM-2                               +
               &P3=,              PARM-3                               +
               &P4=,              PARM-4                               +
               &P5=,              PARM-5                               +
               &P6=,              PARM-6                               +
               &P7=,              PARM-7                               +
               &P8=,              PARM-8                               +
               &P9=,              PARM-9                               +
               &P10=,             PARM-10                              +
               &P11=,             PARM-11                              +
               &P12=,             PARM-12                              +
               &P13=,             PARM-13                              +
               &P14=,             PARM-14                              +
               &P15=,             PARM-15                              +
               &DOC=NO            DOCUMENTATION ONLY
.*
         LCLC  &S
.*
         AIF   ('&DOC' NE 'YES').TSTPRMS
*--PROPRIETARY_STATEMENT----------------------------------------------*
*                                                                     *
*  THIS MACRO IS THE SOLE PROPERTY OF RICHARD TSUJIMOTO, INC.         *
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
*                      # G E N M F E                                  *
*                                                                     *
*        FUNCTION :                                                   *
*                                                                     *
*        GENERATE INSTRUCTIONS TO (OPTIONALLY) OVERRIDE PARMS IN      *
*        A PARM LIST AND INVOKE THE TARGET SUBROUTINE.                *
*                                                                     *
*        PARAMETERS:                                                  *
*                                                                     *
*              #GENMFE  RTN = SUBROUTINE NAME                         *
*                       PRM = (REG)                                   *
*                       PN = VAR | (REG)                              *
*                       DOC = YES | NO                                *
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
*        FUNCTION:                                                    *
*                                                                     *
*        1.  GENERATE THE INSTRUCTIONS TO OVERRIDE PARMS IN THE       *
*            PARM LIST AND INVOKE THE TARGET SUBROUTINE.              *
*                                                                     *
*        2.  GENERATE THE DOC FOR THIS MACRO.                         *
*                                                                     *
*---------------------------------------------------------------------*
         EJECT
*---------------------------------------------------------------------*
*        PARAMETER         DESCRIPTION                                *
*---------------------------------------------------------------------*
*                                                                     *
*        RTN               SUBROUTINE NAME - NAME OF ROUTINE TO CALL  *
*                                                                     *
*        PRM               REG  -  REGISTER                           *
*                                  A REGISTER THAT POINTS TO THE      *
*                                  PARM LIST                          *
*                                                                     *
*        PN                VAR  -  RX-TYPE ADDRESS                    *
*                                  SPECIFIES A FULLWORD THAT CONTAINS *
*                                  THE VALUE THAT WILL BE USED TO     *
*                                  OVERRIDE ITS POSITION IN THE PARM  *
*                                  LIST. NOTE, THE VARIABLE COULD     *
*                                  CONTAIN AN ADDRESS OR THE VALUE    *
*                                  ITSELF, E.G. KEY LENGTH.           *
*                          REG  -  REGISTER                           *
*                                  SPECIFIES A REGISTER THAT CONTAINS *
*                                  THE VALUE THAT WILL BE USED TO     *
*                                  OVERRIDE ITS POSITION IN THE PARM  *
*                                  LIST. NOTE, THE REGISTER COULD     *
*                                  CONTAIN AN ADDRESS OR THE VALUE    *
*                                  ITSELF, E.G. KEY LENGTH.           *
*                                                                     *
*        DOC               'YES'   PRINT MACRO DOC ONLY               *
*                          'NO'    OMIT PRINTING MACRO DOC (DEFAULT)  *
*                                                                     *
*---------------------------------------------------------------------*
         MEXIT
.TSTPRMS ANOP
         AIF   ('&DOC' EQ 'NO').GNPRM01
         MNOTE 8,'DOC PARM IS INVALID; MUST BE EITHER "YES" OR "NO"'
         MEXIT
.*
.GNPRM01 ANOP
&S       SETC  '&SYSNDX'
&LABEL   DS    0H
         AIF   ('&P1' EQ '').GNPRM02
         AIF   ('&P1'(1,1) EQ '(').GNPRMA1
         L     0,&P1              POINT TO NEW PARM
         ST    0,0(,&PRM)         OVERRIDE PARM-1
         AGO   .GNPRM02
.GNPRMA1 ANOP
         ST    &P1,0(,&PRM)       OVERRIDE PARM-1
.GNPRM02 ANOP
         AIF   ('&P2' EQ '').GNPRM03
         AIF   ('&P2'(1,1) EQ '(').GNPRMA2
         L     0,&P2              POINT TO NEW PARM
         ST    0,4(,&PRM)         OVERRIDE PARM-2
         AGO   .GNPRM03
.GNPRMA2 ANOP
         ST    &P2,4(,&PRM)       OVERRIDE PARM-2
.GNPRM03 ANOP
         AIF   ('&P3' EQ '').GNPRM04
         AIF   ('&P3'(1,1) EQ '(').GNPRMA3
         L     0,&P3              POINT TO NEW PARM
         ST    0,8(,&PRM)         OVERRIDE PARM-3
         AGO   .GNPRM04
.GNPRMA3 ANOP
         ST    &P3,8(,&PRM)       OVERRIDE PARM-3
.GNPRM04 ANOP
         AIF   ('&P4' EQ '').GNPRM05
         AIF   ('&P4'(1,1) EQ '(').GNPRMA4
         L     0,&P4              POINT TO NEW PARM
         ST    0,12(,&PRM)        OVERRIDE PARM-4
         AGO   .GNPRM05
.GNPRMA4 ANOP
         ST    &P4,12(,&PRM)      OVERRIDE PARM-4
.GNPRM05 ANOP
         AIF   ('&P5' EQ '').GNPRM06
         AIF   ('&P5'(1,1) EQ '(').GNPRMA5
         L     0,&P5              POINT TO NEW PARM
         ST    0,16(,&PRM)        OVERRIDE PARM-5
         AGO   .GNPRM06
.GNPRMA5 ANOP
         ST    &P5,16(,&PRM)      OVERRIDE PARM-5
.GNPRM06 ANOP
         AIF   ('&P6' EQ '').GNPRM07
         AIF   ('&P6'(1,1) EQ '(').GNPRMA6
         L     0,&P6              POINT TO NEW PARM
         ST    0,20(,&PRM)        OVERRIDE PARM-6
         AGO   .GNPRM07
.GNPRMA6 ANOP
         ST    &P6,20(,&PRM)      OVERRIDE PARM-6
.GNPRM07 ANOP
         AIF   ('&P7' EQ '').GNPRM08
         AIF   ('&P7'(1,1) EQ '(').GNPRMA7
         L     0,&P7              POINT TO NEW PARM
         ST    0,24(,&PRM)        OVERRIDE PARM-7
         AGO   .GNPRM08
.GNPRMA7 ANOP
         ST    &P7,24(,&PRM)      OVERRIDE PARM-7
.GNPRM08 ANOP
         AIF   ('&P8' EQ '').GNPRM09
         AIF   ('&P8'(1,1) EQ '(').GNPRMA8
         L     0,&P8              POINT TO NEW PARM
         ST    0,28(,&PRM)        OVERRIDE PARM-8
         AGO   .GNPRM09
.GNPRMA8 ANOP
         ST    &P8,28(,&PRM)      OVERRIDE PARM-8
.GNPRM09 ANOP
         AIF   ('&P9' EQ '').GNPRM10
         AIF   ('&P9'(1,1) EQ '(').GNPRMA9
         L     0,&P9              POINT TO NEW PARM
         ST    0,32(,&PRM)        OVERRIDE PARM-9
         AGO   .GNPRM10
.GNPRMA9 ANOP
         ST    &P9,32(,&PRM)      OVERRIDE PARM-9
.GNPRM10 ANOP
         AIF   ('&P10' EQ '').GNPRM11
         AIF   ('&P10'(1,1) EQ '(').GNPR101
         L     0,&P10             POINT TO NEW PARM
         ST    0,36(,&PRM)        OVERRIDE PARM-10
         AGO   .GNPRM11
.GNPR101 ANOP
         ST    &P10,36(,&PRM)     OVERRIDE PARM-10
.GNPRM11 ANOP
         AIF   ('&P11' EQ '').GNPRM12
         AIF   ('&P11'(1,1) EQ '(').GNPR111
         L     0,&P11             POINT TO NEW PARM
         ST    0,40(,&PRM)        OVERRIDE PARM-11
         AGO   .GNPRM12
.GNPR111 ANOP
         ST    &P11,40(,&PRM)     OVERRIDE PARM-11
.GNPRM12 ANOP
         AIF   ('&P12' EQ '').GNPRM13
         AIF   ('&P12'(1,1) EQ '(').GNPR121
         L     0,&P12             POINT TO NEW PARM
         ST    0,44(,&PRM)        OVERRIDE PARM-12
         AGO   .GNPRM13
.GNPR121 ANOP
         ST    &P12,44(,&PRM)     OVERRIDE PARM-12
.GNPRM13 ANOP
         AIF   ('&P13' EQ '').GNPRM14
         AIF   ('&P13'(1,1) EQ '(').GNPR131
         L     0,&P9              POINT TO NEW PARM
         ST    0,48(,&PRM)        OVERRIDE PARM-13
         AGO   .GNPRM14
.GNPR131 ANOP
         ST    &P13,48(,&PRM)     OVERRIDE PARM-13
.GNPRM14 ANOP
         AIF   ('&P14' EQ '').GNPRM15
         AIF   ('&P14'(1,1) EQ '(').GNPR141
         L     0,&P14             POINT TO NEW PARM
         ST    0,52(,&PRM)        OVERRIDE PARM-14
         AGO   .GNPRM15
.GNPR141 ANOP
         ST    &P14,52(,&PRM)     OVERRIDE PARM-14
.GNPRM15 ANOP
         AIF   ('&P15' EQ '').GENCALL
         AIF   ('&P15'(1,1) EQ '(').GNPR151
         L     0,&P15             POINT TO NEW PARM
         ST    0,56(,&PRM)        OVERRIDE PARM-15
         AGO   .GENCALL
.GNPR151 ANOP
         ST    &P15,56(,&PRM)     OVERRIDE PARM-15
.GENCALL ANOP
         LR    1,&PRM             POINT TO PARM LIST
         L     15,#RTN&S          POINT TO SUBROUTINE
         BALR  14,15              GO TO SUBROUTINE
         B     #XTN&S             CONTINUE
#RTN&S   DC    V(&RTN)            ADDR OF SUBROUTINE
#XTN&S   DS    0H
         MEND
