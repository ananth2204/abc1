         MACRO
         @DELIMST &KEYWD,         EXTERNAL TOKEN NAME                  +
               &STATF,            ON | OFF                             +
               &GENTBL,           Y = GEN ARRAY LIST, N = RUNTIME CHG  +
               &DELIMTB           ADDR OF DELIMITER ARRAY
.*
         GBLC  &DNAME(33),&TOKNAM(33),&DELIM(33)
         GBLA  &STAT(33),&LSIZE
.*
         LCLC  &S
         LCLA  &I
.*
         AGO   .START
*--PROPRIETARY_STATEMENT----------------------------------------------*
*                                                                     *
*  THIS MACRO IS THE SOLE PROPERTY OF RICHARD TSUJIMOTO, INC.         *
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
*                      @ D E L I M S T                                *
*                                                                     *
*        FUNCTION :                                                   *
*                                                                     *
*        INITIALIZE ARRAY OF DELIMITERS, OR GENERATE THE CODE TO      *
*        MODIFY THE STATUS OF GIVEN ENTRIES IN THE ARRAY.             *
*                                                                     *
*        PARAMETERS: SEE GLOBAL VARS AND MACRO HEADER                 *
*                                                                     *
*---------------------------------------------------------------------*
.START   ANOP
.*
.*   LOCATE LOCATION OF EXTERNAL TOKEN NAME
.*
&I       SETA  1
.LOOP1   AIF   (&I GT &LSIZE).INTERR1
         AIF   ('&KEYWD' EQ '&DNAME(&I)').GOTIT
&I       SETA  &I+1
         AGO   .LOOP1
.*
.INTERR1 ANOP
         MNOTE 8,'*** INTERNAL ERROR - EXTERNAL TOKEN NAME LOOKUP ***'
         MEXIT
.*
.GOTIT   AIF   ('&GENTBL' EQ 'Y').UPDTIT
.*
.*   GENERATE CODE TO MODIFY STATUS OF DELIMITER
.*
         L     R1,&DELIMTB        POINT TO DELIMITER ARRAY
         S     R1,=F'2'           BACKUP 2 BYTES
         LA    R15,&I             INDEX LOCATION OF DELIMITER IN ARRAY
         SLL   R15,1              MULIPLY BY 2
         AR    R1,R15             POINT TO ENTRY IN DELIMITER ARRAY
         AIF   ('&STATF' EQ 'OFF').SETI001
         MVI   1(R1),X'01'        SET STATUS TO "YES"
         AGO   .GETOUT
.*
.SETI001 ANOP
         MVI   1(R1),X'00'        SET STATUS TO "NO'
.*
.GETOUT  ANOP
         MEXIT
.*
.UPDTIT  AIF   ('&STATF' NE 'YES').SETS001
&STAT(&I) SETA 1
         AGO   .DONE
.*
.SETS001 AIF   ('&STATF' NE 'NO').SETS002
&STAT(&I) SETA 0
         AGO   .DONE
.*
.SETS002 ANOP
&STAT(&I) SETA 129
         AGO   .DONE
.*
.DONE    ANOP
         MEND
