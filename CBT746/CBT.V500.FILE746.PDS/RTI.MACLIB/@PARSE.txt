         MACRO
&LABEL   @PARSE &CMDLINE=,        ADDRESS OF COMMAND LINE STRING       +
               &CMDLEN=,          LENGTH OF COMMAND LINE STRING        +
               &DELIMTB=,         ADDRESS OF DELIMITER ARRAY           +
               &MF=,              MACRO FORMAT                         +
               &DOC=NO            DOCUMENTATION ONLY
.*
         LCLC  &S,&NULL,&EOL
         LCLA  &MAXSTRL,&TOKENL,&STAKPFX,&WRKLEN
.*
&S       SETC  '&SYSNDX'
&EOL     SETC  '$EOL$'
&MAXSTRL SETA  100                MAX. LENGTH FOR COMMAND LINE STRING
&TOKENL  SETA  8                  LENGTH OF TOKEN ENTRY
&STAKPFX SETA  12                 LENGTH OF TOKEN STACK PREFIX
.*
         AIF   ('&DOC' NE 'YES').TSTPRMS
*--PROPRIETARY_STATEMENT----------------------------------------------*
*                                                                     *
*  THIS MACRO IS THE SOLE PROPERTY OF RICHARD TSUJIMOTO, INC.         *
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
*                      @ P A R S E                                    *
*                                                                     *
*        FUNCTION :                                                   *
*                                                                     *
*        SCAN A CHARACTER STRING AND CONVERT THE INPUT INTO TOKENS.   *
*                                                                     *
*        PARAMETERS:                                                  *
*                                                                     *
*              @PARSER CMDLINE  = VAR  | (REG),                       *
*                      CMDLEN   = VAR  | (REG),                       *
*                      DELIM    = VAR  | (REG),                       *
*                      MF = L | (E,REG) | DSECT | GENLIST,            *
*                      DOC = YES | NO                                 *
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
*        FUNCTION:                                                    *
*                                                                     *
*        1.  GENERATE THE CODE TO CALL THE SUBROUTINE @PARSE.         *
*                                                                     *
*        2.  GENERATE THE PARM LIST REQUIRED BY THE SUBROUTINE.       *
*                                                                     *
*        3.  GENERATE A DSECT OF THE PARM LIST.                       *
*                                                                     *
*        4.  GENERATE THE SPACE REQUIRED FOR THE TOKEN STACK.         *
*                                                                     *
*        5.  GENERATE THE DOC FOR THIS MACRO.                         *
*                                                                     *
*---------------------------------------------------------------------*
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*        DESCRIPTION :                                                *
*                                                                     *
*        @PARSE WILL PERFORM A LEXCIAL SCAN OF THE CHARACTER STRING   *
*        AND CREATE TOKENS.                                           *
*                                                                     *
*        SEE "PARSING, SYNTAX CHECKING AND INTERPRETING, USER'S       *
*            GUIDE" FOR DETAILS.                                      *
*                                                                     *
*---------------------------------------------------------------------*
         EJECT
*---------------------------------------------------------------------*
*        PARAMETER         DESCRIPTION                                *
*---------------------------------------------------------------------*
*                                                                     *
*        CMDLINE           VAR  -  RX-TYPE ADDRESS                    *
*                                  SPECIFIES THE VARIABLE THAT IS     *
*                                  ADDRESS OF THE COMMAND STRING      *
*                          (REG) - (2-12)                             *
*                                  SPECIFIES THE REGISTER THAT HAS    *
*                                  THE ADDRESS OF THE COMMAND STRING  *
*                                                                     *
*        CMDLEN            VAR  -  RX-TYPE ADDRESS                    *
*                                  SPECIFIES THE VARIABLE THAT IS     *
*                                  THE LENGTH OF THE COMMAND STRING   *
*                          (REG) - (2-12)                             *
*                                  SPECIFIES THE REGISTER THAT HAS    *
*                                  THE LENGTH OF THE COMMAND STRING   *
*                                                                     *
*                                  MAXIMUM LENGTH ALLOWED IS SET      *
*                                  BY THE MACRO VAR MAXSTRLEN         *
*                                                                     *
*        DELIM             VAR  -  RX-TYPE ADDRESS                    *
*                                  SPECIFIES THE VARIABLE THAT IS     *
*                                  THE ADDR. OF THE DLIMITER ARRAY    *
*                          (REG) - (2-12)                             *
*                                  SPECIFIES THE REGISTER THAT HAS    *
*                                  THE ADDR. OF THE DELIMITER ARRAY   *
*                                                                     *
*        MF                'L'   - GENERATE THE PARM LIST             *
*                          '(E,RX)' - GENERATE CODE TO INVOKE THE     *
*                                     SUBROUTINE AND (OPTIONALLY)     *
*                                     MODIFY THE PARM LIST            *
*                          'DSECT' -  GENERATE PARAMETER LIST DSECT   *
*                          'GENLIST' - GENERATE SPACE FOR WORK AREA   *
*                                                                     *
*                          NOTE: GENLIST IS FOR INTERNAL USE ONLY     *
*                                                                     *
*        DOC               'YES'   PRINT MACRO DOC ONLY               *
*                          'NO'    OMIT PRINTING MACRO DOC (DEFAULT)  *
*                                                                     *
*---------------------------------------------------------------------*
*        RETURN CODE VALUES                                           *
*---------------------------------------------------------------------*
*                                                                     *
*        R15 =  0 - SUCCESS                                           *
*               4 - ADDR FOR COMMAND STRING IS ZERO                   *
*               8 - STRING HAS INVALID LENGTH                         *
*              12 - ADDR FOR DELIMITER ARRAY IS ZERO                  *
*                                                                     *
*---------------------------------------------------------------------*
*        PARAMETER LIST DESCRIPTION                                   *
*---------------------------------------------------------------------*
*                                                                     *
*        R1 => +0 +-------------------------------------+             *
*                 | ADDRESS OF COMMAND STRING           |             *
*              +4 +-------------------------------------+             *
*                 | LENGTH OF COMMAND STRING            |             *
*              +8 +-------------------------------------+             *
*                 | ADDRESS OF ARRAY OF DELIMITERS      |             *
*                 +-------------------------------------+             *
*                                                                     *
*---------------------------------------------------------------------*
         MEXIT
.TSTPRMS ANOP
         AIF   ('&DOC' EQ 'NO').TSTPRM2
         MNOTE 8,'DOC PARM IS INVALID; MUST BE EITHER "YES" OR "NO"'
         MEXIT
.*
.TSTPRM2 ANOP
         AIF   ('&MF' NE 'DSECT').TSTMFL
.*
***********************************************************************
*                                                                     *
*   @PARSE - PARM LIST                                                *
*                                                                     *
***********************************************************************
&LABEL   DSECT
@PCMDADR DS    A                       ADDR OF COMMAND LINE STRING
@PCMDLEN DS    F                       LENGTH OF COMMAND STRING
@PCMDMXL EQU   &MAXSTRL                   MAX. STRING LENGTH ALLOWED
@PDELIMA DS    A                       ADDR OF ARRAY OF DELIMITERS
         MEXIT
.*
.*    MF=L - GENERATE PARM LIST
.*
.TSTMFL  ANOP
&CMDLNAD SETC  '&CMDLINE'
&CMDLLND SETC  '&CMDLEN'
&DELIMAD SETC  '&DELIMTB'
.*
         AIF   ('&CMDLINE' NE '').TSTMFL1
&CMDLNAD SETC  '0'
.*
.TSTMFL1 AIF   ('&CMDLEN' NE '').TSTMFL2
&CMDLLND SETC  '0'
.*
.TSTMFL2 AIF   ('&DELIMTB' NE '').TSTMFL3
&DELIMAD SETC  '0'
.*
.TSTMFL3 AIF   ('&MF' NE 'L').TSTMFG
.*
.GENLIST ANOP
&LABEL   #GENMFL P1=(4,&CMDLNAD),P2=(4,&CMDLLND),P3=(4,&DELIMAD)
         MEXIT
.*
.TSTMFG  AIF   ('&MF' NE 'GENWORK').TSTMFF
.*
.*    GENERATE @PARSE WORK AREA
.*
&WRKLEN  SETA  ((&MAXSTRL*&TOKENL)+&TOKENL+&STAKPFX)
         DS    0F
&LABEL   DC    (&WRKLEN)X'00'     @PARSE WORK AREA
         MEXIT
.*
.TSTMFF  AIF   ('&MF' EQ '').GENMFI
         AIF   (N'&MF NE 2).TSTMFER
         AIF   ('&MF(1)' EQ 'E').GENMFE
.*
.TSTMFER ANOP
         MNOTE 8,'*** INVALID FORMAT FOR MF PARM ***'
         MEXIT
.*
.*    MF=E - GENERATE CODE TO INVOKE SUBROUTINE
.*
.GENMFE  ANOP
&LABEL   #GENMFE RTN=@PARSE,PRM=&MF(2),P1=&CMDLINE,P2=&CMDLEN,         +
               P3=&DELIMTB
         MEXIT
.*
.*    GENERATE IN-LINE CODE TO INVOKE SUBROUTINE
.*
.GENMFI  AMOP
&LABEL   #GENMFI RTN=@PARSE,P1=&CMDLINE,P2=&CMDLEN,P3=&DELIMTB,P4=&EOL
.*
         MEND
