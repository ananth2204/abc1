         MACRO
&LABEL   @SYNTXCK &SYNTXTB=,      ADDRESS OF SYNTAX TABLE              +
               &MF=,              MACRO FORMAT                         +
               &DOC=NO            DOCUMENTATION ONLY
.*
         GBLC  &DNAME(33),&TOKNAM(33),&DELIM(33)
         GBLA  &STAT(33),&LSIZE
.*
         LCLC  &S,&NULL,&EOL
         LCLA  &I
.*
&EOL     SETC  '$EOL$'
.*
         AIF   ('&DOC' NE 'YES').TSTPRMS
*--PROPRIETARY_STATEMENT----------------------------------------------*
*                                                                     *
*  THIS MACRO IS THE SOLE PROPERTY OF RICHARD TSUJIMOTO, INC.         *
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
*                      @ S Y N T X C K                                *
*                                                                     *
*        FUNCTION :                                                   *
*                                                                     *
*        SCAN A STACK THAT WAS CREATED BY THE @PARSE MACRO AND        *
*        CHECK TO SEE THAT THE INPUT IS SYNTACTICALLY CORRECT.        *
*                                                                     *
*        PARAMETERS:                                                  *
*                                                                     *
*             #SYNTXCK SYNTXTB = VAR  | (REG),                        *
*                      MF = L | (E,REG) | DSECT,                      *
*                      DOC = YES | NO                                 *
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
*        FUNCTION:                                                    *
*                                                                     *
*        1.  GENERATE THE CODE TO CALL THE SUBROUTINE @SYNTXCK.       *
*                                                                     *
*        2.  GENERATE THE PARM LIST REQUIRED BY THE SUBROUTINE.       *
*                                                                     *
*        3.  GENERATE A DSECT OF THE CALLER'S PARM LIST               *
*                                                                     *
*        4.  GENERATE THE DOC FOR THIS MACRO.                         *
*                                                                     *
*---------------------------------------------------------------------*
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*        DESCRIPTION :                                                *
*                                                                     *
*        #SYNTXCK WILL SCAN THE STACK OF TOKENS CREATED BY @PARSE     *
*        FIFO ORDER AND PERFORM A SYNTAX CHECK OF  THE  CHARACTER     *
*        STRING.  THE SYNTAX RULES ARE SPECIFIED BY THE USER AND      *
*        GENERATED BY USING THE @RULE MACRO.                          *
*                                                                     *
*        SEE "PARSING, SYNTAX CHECKING AND INTERPRETING, USER'S       *
*            GUIDE" FOR DETAILS.                                      *
*                                                                     *
*---------------------------------------------------------------------*
         EJECT
*---------------------------------------------------------------------*
*        PARAMETER         DESCRIPTION                                *
*---------------------------------------------------------------------*
*                                                                     *
*        SYNTXTB           VAR  -  RX-TYPE ADDRESS                    *
*                                  SPECIFIES THE VARIABLE THAT IS     *
*                                  THE BEGINNING OF THE SYNTAX TABLE  *
*                          (REG) - (2-12)                             *
*                                  SPECIFIES THE REGISTER THAT HAS    *
*                                  THE ADDRESS OF THE BEGINNING OF    *
*                                  THE SYNTAX TABLE                   *
*                                                                     *
*        MF                'L'   - GENERATE THE PARM LIST             *
*                          '(E,RX)' - GENERATE CODE TO INVOKE THE     *
*                                     SUBROUTINE AND (OPTIONALLY)     *
*                                     MODIFY THE PARM LIST            *
*                          'DSECT' -  GEN A DSECT OF THE PARM LIST    *
*                                     AND THE TOKEN NAMES             *
*                                                                     *
*        DOC               'YES'   PRINT MACRO DOC ONLY               *
*                          'NO'    OMIT PRINTING MACRO DOC (DEFAULT)  *
*                                                                     *
*---------------------------------------------------------------------*
*        RETURN CODE VALUES                                           *
*---------------------------------------------------------------------*
*                                                                     *
*        R15 =  0 - SUCCESS                                           *
*            =  4 - ADDRESS OF USER'S SYNTAX TABLE IS ZERO            *
*            =  8 - SYNTAX ERROR DETECTED                             *
*            = 12 - NEXT RULE INDEX IS ZERO                           *
*                                                                     *
*---------------------------------------------------------------------*
*        PARAMETER LIST DESCRIPTION                                   *
*---------------------------------------------------------------------*
*                                                                     *
*        R1 => +0 +-------------------------------------+             *
*                 | ADDRESS OF USER'S SYNTAX TABLE      |             *
*              +4 +-------------------------------------+             *
*                 | LENGTH OF TOKEN IN ERROR            |             *
*              +8 +-------------------------------------+             *
*                 | ADDRESS OF TOKEN IN ERROR           |             *
*              +C +-------------------------------------+             *
*                 | ADDRESS OF "BAD RULE"               |             *
*                 +-------------------------------------+             *
*                                                                     *
*---------------------------------------------------------------------*
         MEXIT
.TSTPRMS ANOP
         AIF   ('&DOC' EQ 'NO').TSTPRM2
         MNOTE 8,'DOC PARM IS INVALID; MUST BE EITHER "YES" OR "NO"'
         MEXIT
.*
.TSTPRM2 ANOP
         AIF   ('&MF' NE 'DSECT').TSTMFL
.*
         SPACE 2
***********************************************************************
*                                                                     *
*   @SYNTXCK - PARM LIST                                              *
*                                                                     *
***********************************************************************
&LABEL   DSECT
@SYSYNTB DS    A                       ADDR OF USER'S SYNTAX TABLE
@SYTOKLN DS    F                       LENGTH OF TOKEN IN ERROR
@SYTOKAD DS    A                       ADDR OF TOKEN IN ERROR
@SYTBRAD DS    A                       ADDR OF INVALID RULE
         MEXIT
.*
.*    MF=L - GENERATE PARM LIST
.*
.TSTMFL  ANOP
         AIF   ('&MF' NE 'L').TSTMFE
.*
.GENLIST ANOP
&LABEL   #GENMFL P1=(4,&SYNTXTB),P2=(4,0),P3=(4,0),P4=(4,0)
         MEXIT
.*
.*    MF=E - GENERATE CODE TO INVOKE SUBROUTINE
.*
.TSTMFE  ANOP
         AIF   ('&MF(1)' NE 'E').TSTMFI
         AIF   (N'&MF EQ 2).GENCODE
         MNOTE 8,'*** INVALID FORMAT FOR MF PARM ***'
         MEXIT
.*
.GENCODE ANOP
&LABEL   #GENMFE RTN=@SYNTXCK,PRM=&MF(2),P1=&SYNTXTB
         MEXIT
.*
.*    GENERATE IN-LINE CODE TO INVOKE SUBROUTINE
.*
.TSTMFI  ANOP
&LABEL   #GENMFI RTN=@SYNTXCK,P1=&SYNTXTB,P2=(4,0),P3=(4,0),P4=(4,0),  +
               P5=&EOL
.*
         MEND
