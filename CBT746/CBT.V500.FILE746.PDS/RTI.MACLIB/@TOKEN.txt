         MACRO
         @TOKEN &CMDLINE=,        ADDRESS OF COMMAND LINE STRING       +
               &MF=,              MACRO FORMAT                         +
               &DOC=NO            DOCUMENTATION ONLY
.*
         GBLC  &DNAME(33),&TOKNAM(33),&DELIM(33)
         GBLA  &STAT(33),&LSIZE
.*
         LCLC  &S
         LCLA  &I
.*
&S       SETC  '&SYSNDX'
.*
         AIF   ('&DOC' NE 'YES').TSTPRMS
*--PROPRIETARY_STATEMENT----------------------------------------------*
*                                                                     *
*  THIS MACRO IS THE SOLE PROPERTY OF RICHARD TSUJIMOTO, INC.         *
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
*                      @ T O K E N                                    *
*                                                                     *
*        FUNCTION :                                                   *
*                                                                     *
*        GENERATE A LIST OF TOKEN SYMBOLS AND VALUES, AND/OR A DSECT  *
*        THAT MAPS THE TOKEN STACK AND ITS ENTRIES.                   *
*                                                                     *
*        PARAMETERS:                                                  *
*                                                                     *
*              @TOKEN  MF = DSECT | LISTVALS | ALL,                   *
*                      DOC = YES | NO                                 *
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
*        FUNCTION:                                                    *
*                                                                     *
*        1.  GENERATE THE LIST OF TOKEN TYPE SYMBOLS AND VALUES.      *
*                                                                     *
*        2.  GENERATE A DSECT OF THE PARM LIST.                       *
*                                                                     *
*        3.  GENERATE THE DOC FOR THIS MACRO.                         *
*                                                                     *
*---------------------------------------------------------------------*
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*        DESCRIPTION :                                                *
*                                                                     *
*        @TOKEN IS USED TO LIST THE TOKEN TYPE SYMBOLS AND/OR THE     *
*        DSECT THAT MAPS THE TOKEN STACK AND ITS ENTRIES.             *
*                                                                     *
*        SEE "PARSING, SYNTAX CHECKING AND INTERPRETING, USER'S       *
*            GUIDE" FOR DETAILS.                                      *
*                                                                     *
*---------------------------------------------------------------------*
         EJECT
*---------------------------------------------------------------------*
*        PARAMETER         DESCRIPTION                                *
*---------------------------------------------------------------------*
*                                                                     *
*                                                                     *
*        MF                'LISTVAL' - GENERATE LIST OF TOKEN TYPE    *
*                                      SYMBOLS AND VALUES             *
*                          'DSECT' -  GENERATE PARAMETER LIST DSECT   *
*                          'ALL' - SAME AS 'LISTVAL' AND 'DSECT'      *
*                                                                     *
*                          NOTE: MF=DSECT IS FOR INTERNAL USE ONLY    *
*                                                                     *
*        DOC               'YES'   PRINT MACRO DOC ONLY               *
*                          'NO'    OMIT PRINTING MACRO DOC (DEFAULT)  *
*                                                                     *
*---------------------------------------------------------------------*
*        RETURN CODE VALUES                                           *
*---------------------------------------------------------------------*
*                                                                     *
*        N/A                                                          *
*                                                                     *
*---------------------------------------------------------------------*
*        PARAMETER LIST DESCRIPTION                                   *
*---------------------------------------------------------------------*
*                                                                     *
*        N/A                                                          *
*                                                                     *
*---------------------------------------------------------------------*
         MEXIT
.TSTPRMS ANOP
         AIF   ('&DOC' EQ 'NO').TSTPRM1
         MNOTE 8,'DOC PARM IS INVALID; MUST BE EITHER "YES" OR "NO"'
         MEXIT
.*
.*   GEN THE EXTERNAL AND INTERNAL TOKEN NAMES AND DEFAULT DELIMITERS
.*   STORE THE DATA IN GLOBAL MACRO ARRAYS, WHICH ARE REFERENCED BY
.*   @DELIM, @DELIMST, @SYNTXCK AND @INTRPRT
.*
.TSTPRM1 AIF   (('&MF' NE 'LISTVAL') AND ('&MF' NE 'ALL')).TSTDSCT
.*
         @DEFTOKN
.*
***********************************************************************
*                                                                     *
*   LIST OF TOKEN TYPES                                               *
*                                                                     *
***********************************************************************
TOK_IS_DATA EQU 100
TOK_IS_NUM  EQU 101
TOK_IS_EOS  EQU 255
*
GOTORULE    EQU X'BBBB'
CONTRULE    EQU X'CCCC'
SYNTAXERR   EQU X'DDDD'
LASTRULE    EQU X'EEEE'
*
&I       SETA 1
.LOOP1   AIF  (&I GT &LSIZE).TSTALL
&TOKNAM(&I) EQU   &I
&I       SETA &I+1
         AGO  .LOOP1
.*
.TSTALL  AIF   ('&MF' EQ 'ALL').GENDSCT
         MEXIT
.*
.TSTDSCT AIF   ('&MF' EQ 'DSECT').GENDSCT
         MNOTE 8,'*** INVALID FORMAT FOR MF PARM ***'
         MEXIT
.*
.GENDSCT ANOP
.*
         SPACE 2
***********************************************************************
*                                                                     *
*   @PARSE TOKEN STACK PREFIX                                         *
*                                                                     *
***********************************************************************
@TOKENPX DSECT
         DS    C'*PARSE STAK*'    EYE-CATCHER
@TOKENXL EQU   *-@TOKENPX         LENGTH OF TOKEN STACK PREFIX
         SPACE 2
***********************************************************************
*                                                                     *
*   @PARSE TOKEN STACK ENTRY                                          *
*                                                                     *
***********************************************************************
@TOKEN   DSECT
@TOKNTYP DS    XL2                TOKEN TYPE
         DS    X                  FILLER
@TOKENLN DS    X                  TOKEN LENGTH
@TOKENAD DS    A                  ADDR OF TOKEN
@TOKENL  EQU   *-@TOKEN           LENGTH OF TOKEN STACK ENTRY
.*
         MEND
