         MACRO
&LABEL   @INTRPRT &SYNTXTB=,      ADDRESS OF USER'S SYNTAX TABLE       +
               &ERRMSG=,          ADDRESS OF 100-BYTE MESSAGE BUFFER   +
               &MF=,              MACRO FORMAT                         +
               &DOC=NO            DOCUMENTATION ONLY
.*
         LCLC  &S,&NULL,&EOL
.*
&EOL     SETC  '$EOL$'
.*
         AIF   ('&DOC' NE 'YES').TSTPRMS
*--PROPRIETARY_STATEMENT----------------------------------------------*
*                                                                     *
*  THIS MACRO IS THE SOLE PROPERTY OF RICHARD TSUJIMOTO, INC.         *
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
*                      # I N T R P T R                                *
*                                                                     *
*        FUNCTION :                                                   *
*                                                                     *
*        SCAN THE STACK THAT WAS CREATED BY THE @PARSE MACRO,         *
*        USING THE USER'S SYNTAX TABLE AND BRANCH TO THE USER'S       *
*        EXIT ROUTINE(S).                                             *
*                                                                     *
*        PARAMETERS:                                                  *
*                                                                     *
*              @INTRPRT SYNTXTB = VAR  | (REG),                       *
*                      ERRMSG   = VAR  | (REG),                       *
*                      MF = L | (E,REG) | DSECT | EXITPARM,           *
*                      DOC = YES | NO                                 *
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
*        FUNCTION:                                                    *
*                                                                     *
*        1.  GENERATE THE CODE TO CALL THE SUBROUTINE @INTRPRT.       *
*                                                                     *
*        2.  GENERATE THE PARM LIST REQUIRED BY THE SUBROUTINE.       *
*                                                                     *
*        3.  GENERATE A DSECT OF THE PARM LIST.                       *
*                                                                     *
*        4.  GENERATE A DSECT OF THE PARM LIST PASSED TO THE USER'S   *
*            EXIT ROUTINE(S).                                         *
*                                                                     *
*        5.  GENERATE THE DOC FOR THIS MACRO.                         *
*                                                                     *
*---------------------------------------------------------------------*
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*        DESCRIPTION :                                                *
*                                                                     *
*        @INTRPRT PROCESSES THE STACK THAT WAS CREATED BY @PARSE      *
*        IN FIRST-IN FIRST-OUT (FIFO) ORDER.  THE USER-PROVIDED       *
*        SYNTAX TABLE IS USED TO NAVIGATE THRU THE STRING UNTIL       *
*        A USER EXIT ROUTINE IS SPECIFIED FOR A KEYWORD/DATA MATCH.   *
*        @INTRPRT WILL THEN FORMAT A PARMLIST TO PASS TO THE USER'S   *
*        EXIT ROUTINE AND THEN PASS CONTROL TO IT.  CONTROL WILL BE   *
*        PASSED USING THE STANDARD OS PROTOCOL, E.G. R13 POINTS TO    *
*        @INTRPRT'S REGISTER SAVE AREA.  THE USER'S EXIT PROCESSES    *
*        THE TOKEN, RETURNING WITH R15=0 IF SUCCESSFUL, ELSE SETTING  *
*        R15=4 AND STORING AN ERROR MESSAGE IN THE BUFFER PROVIDED.   *
*                                                                     *
*---------------------------------------------------------------------*
         EJECT
*---------------------------------------------------------------------*
*        PARAMETER         DESCRIPTION                                *
*---------------------------------------------------------------------*
*                                                                     *
*        SYNTXTB           VAR  -  RX-TYPE ADDRESS                    *
*                                  SPECIFIES THE VARIABLE THAT IS     *
*                                  THE BEGINNING OF THE USER'S        *
*                                  SYNTAX TABLE                       *
*                          (REG) - (2-12)                             *
*                                  SPECIFIES THE REGISTER THAT HAS    *
*                                  THE ADDRESS OF THE BEGINNING OF    *
*                                  THE USER'S SYNTAX TABLE            *
*                                                                     *
*        ERRMSG            VAR  -  RX-TYPE ADDRESS                    *
*                                  SPECIFIES THE VARIABLE THAT IS     *
*                                  THE BEGINNING OF A 100-BYTE        *
*                                  MESSAGE BUFFER                     *
*                          (REG) - (2-12)                             *
*                                  SPECIFIES THE REGISTER THAT HAS    *
*                                  THE ADDRESS OF THE BEGINNING OF    *
*                                  A 100-BYTE MESSAGE BUFFER          *
*                                                                     *
*        MF                'L'   - GENERATE THE PARM LIST             *
*                          '(E,RX)' - GENERATE CODE TO INVOKE THE     *
*                                     SUBROUTINE AND (OPTIONALLY)     *
*                                     MODIFY THE PARM LIST            *
*                          'DSECT' -  GEN A DSECT OF THE PARM LIST    *
*                          'EXITPARM' - GEN A DSECT OF THE USER EXIT  *
*                                     PARM LIST                       *
*                                                                     *
*        DOC               'YES'   PRINT MACRO DOC ONLY               *
*                          'NO'    OMIT PRINTING MACRO DOC (DEFAULT)  *
*                                                                     *
*---------------------------------------------------------------------*
*        RETURN CODE VALUES                                           *
*---------------------------------------------------------------------*
*                                                                     *
*        R15 =  0 - SUCCESS                                           *
*               4 - ADDR OF USER'S SYNTAX TABLE IS ZERO, OR           *
*                   ADDR OF USER'S MESSAGE BUFFER IS ZERO             *
*               8 - ERROR DETECTED BY USER'S EXIT                     *
*                                                                     *
*---------------------------------------------------------------------*
*        PARAMETER LIST DESCRIPTION                                   *
*---------------------------------------------------------------------*
*                                                                     *
*        R1 => +0 +-------------------------------------+             *
*                 | ADDRESS OF USER'S SYNTAX TABLE      |             *
*              +4 +-------------------------------------+             *
*                 | LENGTH OF TOKEN IN ERROR            |             *
*              +8 +-------------------------------------+             *
*                 | ADDRESS OF TOKEN IN ERROR           |             *
*              +C +-------------------------------------+             *
*                 | ADDRESS OF 100-BYTE MESSAGE         |             *
*                 +-------------------------------------+             *
*                                                                     *
*---------------------------------------------------------------------*
*        PARAMETER LIST DESCRIPTION FOR USER EXIT                     *
*---------------------------------------------------------------------*
*                                                                     *
*        R1 => +0 +-------------------------------------+             *
*                 | ADDRESS OF TOKEN                    |             *
*              +4 +-------------------------------------+             *
*                 | LENGTH OF TOKEN                     |             *
*              +8 +-------------------------------------+             *
*                 | ADDRESS OF 100-BYTE MESSAGE BUFFER  |             *
*                 +-------------------------------------+             *
*                                                                     *
*---------------------------------------------------------------------*
         MEXIT
.*
.TSTPRMS ANOP
         AIF   ('&DOC' EQ 'NO').TSTPRM2
         MNOTE 8,'DOC PARM IS INVALID; MUST BE EITHER "YES" OR "NO"'
         MEXIT
.*
.TSTPRM2 ANOP
         AIF   ('&MF' NE 'DSECT').TSTPRM3
***********************************************************************
*                                                                     *
*   @INTRPRT - PARM LIST                                              *
*                                                                     *
***********************************************************************
&LABEL   DSECT
@ISYNTXB DS    A                       ADDR OF USER'S SYNTAX TABLE
@ITOKLEN DS    F                       LENGTH OF TOKEN IN ERROR
@ITOKNAD DS    A                       ADDR OF TOKEN IN ERROR
@IERRMSG DS    A                       ADDR OF 100-BYTE ERROR MESSAGE
         MEXIT
.*
.TSTPRM3 ANOP
         AIF   ('&MF' NE 'EXITPARM').TSTMFL
***********************************************************************
*                                                                     *
*   @INTRPRT - EXIT ROUTINE PARM LIST                                 *
*                                                                     *
***********************************************************************
&LABEL   DSECT
@IXTOKAD DS    A                       ADDR OF TOKEN
@IXTOKLN DS    F                       LENGTH OF TOKEN
@IXERRMG DS    A                       ADDR OF 100-BYTE MESSAGE BUFFER
@IXERRML EQU   100                     LENGTH OF MESSAGE BUFFER
@IXPRMLN EQU   *-@IXTOKAD              LENGTH OF PARM LIST
         MEXIT
.*
.*    MF=L - GENERATE PARM LIST
.*
.TSTMFL  ANOP
         AIF   ('&MF' NE 'L').TSTMFE
.*
.GENLIST ANOP
&LABEL   #GENMFL P1=(4,&SYNTXTB),P2=(4,0),P3=(4,0),P4=(4,&ERRMSG)
         MEXIT
.*
.*    MF=E - GENERATE CODE TO INVOKE SUBROUTINE
.*
.TSTMFE  ANOP
         AIF   ('&MF(1)' NE 'E').TSTMFI
         AIF   (N'&MF EQ 2).GENCODE
         MNOTE 8,'*** INVALID FORMAT FOR MF PARM ***'
         MEXIT
.*
.GENCODE ANOP
&LABEL   #GENMFE RTN=@INTRPRT,PRM=&MF(2),P1=&SYNTXTB,P4=&ERRMSG
         MEXIT
.*
.*    GENERATE IN-LINE CODE TO INVOKE SUBROUTINE
.*
.TSTMFI  ANOP
&LABEL   #GENMFI RTN=@INTRPRT,P1=&SYNTXTB,P4=&ERRMSG,P5=&EOL
.*
         MEND
