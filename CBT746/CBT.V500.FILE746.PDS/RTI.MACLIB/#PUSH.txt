         MACRO
&LABEL   #PUSH &STACK=,           STACK LOCATION                       +
               &DOC=NO            DOCUMENTATION ONLY
.*
         LCLB  &GENFLAG,&RFLAG
.*
&GENFLAG SETB  1
         AIF   ('&DOC' NE 'YES').TSTSK
*--PROPRIETARY_STATEMENT----------------------------------------------*
*                                                                     *
*  THIS MACRO IS THE SOLE PROPERTY OF RICHARD TSUJIMOTO, INC.         *
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
*                        # P U S H                                    *
*                                                                     *
*        FUNCTION : SAVE CALLER'S REGS ON A PUSHDOWN STACK            *
*                                                                     *
*        PARAMETERS:                                                  *
*                                                                     *
*              #PUSH STACK = VAL1 | (RX),                             *
*                    DOC = YES | NO                                   *
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
*        RESTRICTIONS : THE STACK MUST BE GENERATED AND INITIALIZED   *
*                       BY THE #STACK MACRO BEFORE THE #PUSH MACRO    *
*                       CAN BE USED.  FOR EXAMPLE:                    *
*                                                                     *
*                       RSAVE   #STACK ACTION=GEN,LEVELS=2            *
*                                 .                                   *
*                                 .                                   *
*                                 .                                   *
*                               #STACK STACK=RSAVE,ACTION=INIT,       *
*                                      LEVELS=2                       *
*                               #PUSH STACK=RSAVE                     *
*                                                                     *
*                       NOTE: R15 IS USED AS A WORK REGISTER.         *
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
*        DESCRIPTION :                                                *
*                                                                     *
*        GENERATE A PUSHDOWN STACK THAT WILL BE USED TO SAVE A        *
*        CALLER'S GENERAL REGISTERS (R0-R14).  ALSO, STORE A          *
*        CALLER'S GENERAL REGISTERS (R0-R14) ON THE PUSHDOWN STACK.   *
*        THE #PUSH MACRO IS NORMALLY USED WITH THE #POP MACRO.        *
*                                                                     *
*---------------------------------------------------------------------*
         EJECT
*---------------------------------------------------------------------*
*        PARAMETER         DESCRIPTION                                *
*---------------------------------------------------------------------*
*                                                                     *
*        STACK             VAL1 - A LABEL THAT POINTS TO THE STACK.   *
*                          (RX) - A REGISTER THAT POINTS TO THE STACK.*
*                                                                     *
*        DOC               'YES' - GENERATE THE MACRO DOCUMENTATION.  *
*                          'NO'  - DO NOT GENERATE THE DOCUMENTATION. *
*                                  DEFAULT IS 'NO'.                   *
*                                                                     *
*---------------------------------------------------------------------*
         MEXIT
.*
.* EXAMINE PUSH PARAMETERS
.*
.TSTSK   AIF   ('&STACK' NE '').TSTSK1
         MNOTE 8,'STACK PARM MUST BE SPECIFIED'
&GENFLAG SETB  0
         AGO   .TSTGEN
.TSTSK1  AIF   ('&STACK'(1,1) NE '(').TSTGEN
&RFLAG   SETB  1
.TSTGEN  AIF   (&GENFLAG).GENMAC
         MEXIT
.*
.* GENERATE PUSH INSTRUCTIONS
.*
.GENMAC  AIF   (&RFLAG).USEREG1
         L     15,&STACK+4        POINT TO CURRENT STACK ENTRY
         AGO   .USEREGX
.USEREG1 ANOP
         L     15,4&STACK         POINT TO CURRENT STACK ENTRY
.USEREGX ANOP
         STM   0,14,0(15)         SAVE CALLER'S REGS
         LA    15,60(,15)         POINT TO NEXT STACK ENTRY
         AIF   (&RFLAG).USEREG3
         ST    15,&STACK+4        SAVE CURRENT STACK POINTER
         AGO   .USEREGW
.USEREG3 ANOP
         ST    15,4&STACK         SAVE CURRENT STACK POINTER
.USEREGW ANOP
         MEND
