         MACRO
&LABEL   #STACK &STACK=,          STACK LOCATION                       +
               &LEVELS=3,         STACK DEPTH (NO. OF REG SAVEAREAS)   +
               &ACTION=INIT,      STACK OPERATION                      +
               &DOC=NO            DOCUMENTATION ONLY
.*
         LCLB  &GENFLAG,&RFLAG,&SGEN
.*
&GENFLAG SETB  1
         AIF   ('&DOC' NE 'YES').TSTSK
*--PROPRIETARY_STATEMENT----------------------------------------------*
*                                                                     *
*  THIS MACRO IS THE SOLE PROPERTY OF RICHARD TSUJIMOTO, INC.         *
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
*                        # S T A C K                                  *
*                                                                     *
*        FUNCTION : INITIALIZE OR GENERATE A PUSHDOWN STACK.          *
*                                                                     *
*        PARAMETERS:                                                  *
*                                                                     *
*              #STACK STACK = VAL1 | (RX),                            *
*                    LEVELS = VAL2,                                   *
*                    ACTION = INIT | GEN,                             *
*                    DOC = YES | NO                                   *
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
*        RESTRICTIONS : R0-R3 ARE USED AS WORK REGISTERS.             *
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
*        DESCRIPTION :                                                *
*                                                                     *
*        GENERATE OR INITIALIZE A PUSHDOWN STACK.                     *
*        THE #PUSH AND #POP MACROS USE THE PUSHDOWN STACK WHICH       *
*        IS INITIALIZED AND GENERATED BY THIS MACRO.                  *
*                                                                     *
*---------------------------------------------------------------------*
         EJECT
*---------------------------------------------------------------------*
*        PARAMETER         DESCRIPTION                                *
*---------------------------------------------------------------------*
*                                                                     *
*        STACK             VAL1 - A LABEL THAT POINTS TO THE STACK.   *
*                          (RX) - A REGISTER THAT POINTS TO THE STACK.*
*                                                                     *
*        LEVELS            VAL2 - A NUMERIC VALUE GREATER THAN 0      *
*                                  WHICH REFLECTS THE DEPTH OF THE    *
*                                  STACK.  NOTE: 15 FULLWORDS ARE     *
*                                  ALLOCATED FOR EACH LEVEL.          *
*                                  A NONRELOCATABLE VALUE IS ALSO     *
*                                  VALID, E.G. 'DEPTH EQU 4'          *
*                                  DEFAULT IS 3.                      *
*                                                                     *
*        ACTION            'INIT' - INITIALIZE THE PUSHDOWN STACK.    *
*                          'GEN'  - GENERATE THE DATA AREA FOR THE    *
*                                   THE PUSHDOWN STACK.               *
*                                  DEFAULT IS 'INIT'.                 *
*                                                                     *
*        DOC               'YES' - GENERATE THE MACRO DOCUMENTATION.  *
*                          'NO'  - DO NOT GENERATE THE DOCUMENTATION. *
*                                  DEFAULT IS 'NO'.                   *
*                                                                     *
*---------------------------------------------------------------------*
         MEXIT
.*
.* EXAMINE STACK PARAMETERS
.*
.TSTSK   AIF   ('&STACK' NE '').TSTSK1
         AIF   ('&ACTION' EQ 'GEN').TSTLVLS
         MNOTE 8,'STACK PARM MUST BE SPECIFIED'
&GENFLAG SETB  0
.TSTSK1  AIF   ('&ACTION' EQ 'GEN').TSTLVLS
         AIF   ('&STACK'(1,1) NE '(').TSTLVLS
&RFLAG   SETB  1
.TSTLVLS AIF   (&LEVELS NE 0).TSTACTN
         MNOTE 8,'LEVELS PARM MUST BE NONZERO'
&GENFLAG SETB  0
.TSTACTN AIF   ('&ACTION' EQ 'INIT').TSTGEN
         AIF   ('&ACTION' EQ 'GEN').TSTGEN
         MNOTE 8,'ACTION PARM IS INVALID'
&GENFLAG SETB  0
.TSTGEN  AIF   (&GENFLAG).GENMAC
         MEXIT
.*
.* GENERATE STACK INSTRUCTIONS/DATA
.*
.GENMAC  AIF   ('&ACTION' NE 'GEN').DOINIT
&LABEL   DS    A                  ADDR OF BEGINNING OF STACK
         DS    A                  ADDR OF CURRENT ENTRY
         DS    (15*&LEVELS)F      STACK
         MEXIT
.DOINIT  AIF   (&RFLAG).USEREG1
         LA    0,&STACK+8         POINT TO 1ST STACK ENTRY
         ST    0,&STACK           SAVE START OF STACK POINTER
         ST    0,&STACK+4         SAVE AS CURRENT STACK ENTRY PTRR
         AGO   .USEREGY
.USEREG1 ANOP
         LA    0,8&STACK          POINT TO 1ST STACK ENTRY
         ST    0,0&STACK          SAVE START OF STACK POINTER
         ST    0,4&STACK          SAVE AS CURRENT STACK ENTRY PTRR
.USEREGY ANOP
         L     1,=A(60*&LEVELS)   LENGTH OF STACK
         LR    2,0                SAME ORIGIN
         XR    3,3                PAD VALUE
         MVCL  0,2                SET STACK TO NULLS
         MEND
